#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Сотрудник") И ЗначениеЗаполнено(ДанныеЗаполнения.Сотрудник) Тогда
			ДанныеЗаполнения = ДанныеЗаполнения.Сотрудник;	
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;

	ЗарплатаКадрыРасширенный.ОбработкаЗаполненияМногофункциональногоДокумента(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Подготовка к регистрации перерасчетов
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, ДанныеДляПроведения.СотрудникиДаты, Ссылка);
	
	РазрядыКатегорииДолжностей.СформироватьДвиженияРазрядовКатегорийСотрудников(Движения, ДанныеДляПроведения.РазрядыКатегорииСотрудников);
	
	Если НачисленияУтверждены Тогда
		
		СтруктураПлановыхНачислений = Новый Структура;
		СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
		СтруктураПлановыхНачислений.Вставить("ЗначенияПоказателей", ДанныеДляПроведения.ЗначенияПоказателейНачислений);
		СтруктураПлановыхНачислений.Вставить("КоэффициентИндексацииЗаработка", ДанныеДляПроведения.КоэффициентыИндексации);
		Если ДанныеДляПроведения.КоэффициентыИндексации.Количество() > 0 Тогда
			УчетСреднегоЗаработка.ЗарегистрироватьДанныеКоэффициентовИндексации(Движения, ДанныеДляПроведения.КоэффициентыИндексации);
		КонецЕсли;
		
		Если УстановитьБазовыйПериод И ДанныеДляПроведения.БазовыеПериодыИндексации.Количество() > 0 Тогда
			РасчетЗарплаты.СформироватьДвиженияБазовыеПериодыИндексации(ЭтотОбъект, Движения ,ДанныеДляПроведения.БазовыеПериодыИндексации);
		КонецЕсли;
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений, Истина);
		РасчетЗарплатыРасширенный.СформироватьДвиженияЗначенийСовокупныхТарифныхСтавок(Движения, ДанныеДляПроведения.ДанныеСовокупныхТарифныхСтавок);
		
	КонецЕсли;
	
	// Регистрация перерасчетов
	Если ЕстьПерерасчеты Тогда
		ПерерасчетЗарплаты.РегистрацияПерерасчетов(Движения, ДанныеДляРегистрацииПерерасчетов, Организация);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадрыРасширенный.ПроверитьУтверждениеДокумента(ЭтотОбъект, Отказ);
	
	Если НачисленияУтверждены Тогда 
		ЗарплатаКадрыРасширенный.ПроверитьЗаполнениеВидаТарифнойСтавки(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Подготовка к регистрации перерасчетов
	ДанныеДляРегистрацииПерерасчетов = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТДанныеДокументов(ДанныеДляРегистрацииПерерасчетов);
	ЕстьПерерасчеты = ПерерасчетЗарплаты.СборДанныхДляРегистрацииПерерасчетов(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	
	// Регистрация перерасчетов
	Если ЕстьПерерасчеты Тогда
		ПерерасчетЗарплаты.РегистрацияПерерасчетовПриОтменеПроведения(Ссылка, ДанныеДляРегистрацииПерерасчетов, Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.ПередЗаписьюМногофункциональногоДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЗарплатаКадрыРасширенный.ПриКопированииМногофункциональногоДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина,ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник),"Должность,Подразделение", ДатаИзменения);
	
	Запрос.УстановитьПараметр("Подразделение", ДанныеСотрудников[0].Подразделение);
	Запрос.УстановитьПараметр("Должность", ДанныеСотрудников[0].Должность);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.Ссылка.ДатаИзменения КАК ДатаСобытия,
		|	Начисления.Ссылка.Сотрудник КАК Сотрудник,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Ссылка.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	ИСТИНА КАК Используется,
		|	Начисления.Размер КАК Размер,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзменениеКвалификационногоРазрядаНачисления.ДокументОснование,
		|	НачисленияПоказатели.Показатель
		|ПОМЕСТИТЬ ВТПоказателиНачисленийДокумента
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда.Начисления КАК ИзменениеКвалификационногоРазрядаНачисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|		ПО ИзменениеКвалификационногоРазрядаНачисления.Начисление = НачисленияПоказатели.Ссылка
		|			И (ИзменениеКвалификационногоРазрядаНачисления.Ссылка = &Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеКвалификационногоРазряда.ДатаИзменения КАК ДатаСобытия,
		|	ИзменениеКвалификационногоРазряда.Организация КАК Организация,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник,
		|	ИзменениеКвалификационногоРазряда.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ИзменениеКвалификационногоРазряда.Показатель КАК Показатель,
		|	ПоказателиНачисленийДокумента.ДокументОснование КАК ДокументОснование,
		|	ИзменениеКвалификационногоРазряда.ЗначениеПоказателя КАК Значение,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиНачисленийДокумента КАК ПоказателиНачисленийДокумента
		|		ПО ИзменениеКвалификационногоРазряда.Показатель = ПоказателиНачисленийДокумента.Показатель
		|			И (ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка)
		|			И (ИзменениеКвалификационногоРазряда.Показатель <> ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.ПустаяСсылка))
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка
		|	И ИзменениеКвалификационногоРазряда.Показатель <> ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеКвалификационногоРазряда.ДатаИзменения КАК ДатаСобытия,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник,
		|	ИзменениеКвалификационногоРазряда.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ИзменениеКвалификационногоРазряда.СовокупнаяТарифнаяСтавка КАК Значение,
		|	ВЫБОР
		|		КОГДА ИзменениеКвалификационногоРазряда.СовокупнаяТарифнаяСтавка = 0
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыТарифныхСтавок.ПустаяСсылка)
		|		ИНАЧЕ ИзменениеКвалификационногоРазряда.ВидТарифнойСтавки
		|	КОНЕЦ КАК ВидТарифнойСтавки,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеКвалификационногоРазряда.ДатаИзменения КАК ДатаСобытия,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник,
		|	ИзменениеКвалификационногоРазряда.РазрядКатегория КАК РазрядКатегория,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеКвалификационногоРазряда.ДатаИзменения КАК ДатаСобытия,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка";
				   
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения = Новый Структура; 
	
	ПлановыеНачисления = РезультатыЗапроса[0].Выгрузить();
	ДанныеДляПроведения.Вставить("ПлановыеНачисления", ПлановыеНачисления);
	
	ЗначенияПоказателей = РезультатыЗапроса[2].Выгрузить();
	ДанныеДляПроведения.Вставить("ЗначенияПоказателейНачислений", ЗначенияПоказателей);
	
	ДанныеСовокупныхТарифныхСтавок = РезультатыЗапроса[3].Выгрузить();
	ДанныеДляПроведения.Вставить("ДанныеСовокупныхТарифныхСтавок", ДанныеСовокупныхТарифныхСтавок);
	
	РазрядыКатегорииСотрудников = РезультатыЗапроса[4].Выгрузить();
	ДанныеДляПроведения.Вставить("РазрядыКатегорииСотрудников", РазрядыКатегорииСотрудников);
	
	СотрудникиДаты = РезультатыЗапроса[5].Выгрузить();
	ДанныеДляПроведения.Вставить("СотрудникиДаты", СотрудникиДаты);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИзменениеКвалификационногоРазряда.ДатаИзменения КАК Период,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник,
		|	ИзменениеКвалификационногоРазряда.КоэффициентИндексации КАК Коэффициент
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка
		|	И ИзменениеКвалификационногоРазряда.УчитыватьКакИндексациюЗаработка";
	
	КоэффициентыИндексации = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("КоэффициентыИндексации", КоэффициентыИндексации);
	Запрос.УстановитьПараметр("ДатаИзмененияПорядкаИндексации", Дата(2021,04,01));

	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА ИзменениеКвалификационногоРазряда.ДатаИзменения < &ДатаИзмененияПорядкаИндексации 
		|		ТОГДА НАЧАЛОПЕРИОДА(ИзменениеКвалификационногоРазряда.ДатаИзменения, МЕСЯЦ)
		|		ИНАЧЕ ИзменениеКвалификационногоРазряда.ДатаИзменения 
		|	КОНЕЦ КАК Период,
		|	ИзменениеКвалификационногоРазряда.Организация КАК Организация,
		|	&Подразделение КАК Подразделение,
		|	&Должность КАК Должность,
		|	ИзменениеКвалификационногоРазряда.Сотрудник КАК Сотрудник
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ИзменениеКвалификационногоРазряда
		|ГДЕ
		|	ИзменениеКвалификационногоРазряда.Ссылка = &Ссылка
		|	И ИзменениеКвалификационногоРазряда.УстановитьБазовыйПериод";
	
	БазовыеПериодыИндексации = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("БазовыеПериодыИндексации", БазовыеПериодыИндексации);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура СоздатьВТДанныеДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Организация,
		|	ТаблицаДокумента.Сотрудник,
		|	НАЧАЛОПЕРИОДА(ТаблицаДокумента.ДатаИзменения, МЕСЯЦ) КАК ПериодДействия,
		|	ТаблицаДокумента.Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ИзменениеКвалификационногоРазряда КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Регистратор";
		
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли	