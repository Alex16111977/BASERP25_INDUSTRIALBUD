#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании, Параметры) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРасчетЗарплатыРасширенная,УправлениеПредприятием,КомплекснаяАвтоматизация";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Формирует документ отражения зарплаты за период по указанной организации. Вызывается
// при автоматическом выполнении операций закрытия месяца.
// 
// Параметры:
//   Организации - СправочникСсылка.Организации - Организация, по которой формируется документ
//   Период - Дата - Период, за который формируется документ
//
Процедура ОтражениеЗарплатыПриЗакрытииМесяца(Организации, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ") Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Отражение зарплаты при закрытии месяца';uk='Відображення зарплати при закритті місяця'"));
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтражениеЗарплатыВФинансовомУчете.Ссылка КАК Ссылка,
	|	ОтражениеЗарплатыВФинансовомУчете.Организация КАК Организация,
	|	ОтражениеЗарплатыВФинансовомУчете.Проведен КАК Проведен
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете КАК ОтражениеЗарплатыВФинансовомУчете
	|ГДЕ
	|	(ОтражениеЗарплатыВФинансовомУчете.Организация В(&Организации) ИЛИ &ПоВсемОрганизациям)
	|	И НАЧАЛОПЕРИОДА(ОтражениеЗарплатыВФинансовомУчете.ПериодРегистрации, МЕСЯЦ) = &НачалоМесяца
	|	И НЕ ОтражениеЗарплатыВФинансовомУчете.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Проведен УБЫВ");
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организации));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	
	СуществующиеДокументы = Запрос.Выполнить().Выбрать();
	
	Для Каждого Организация Из Организации Цикл
		
		ДокументыПоОрганизацииОбработаны = Ложь;
		
		ДокументОбъект = Неопределено;
		
		СуществующиеДокументы.Сбросить();
		
		Пока СуществующиеДокументы.НайтиСледующий(Новый Структура("Организация, Проведен", Организация, Истина)) Цикл
			
			ДокументОбъект = СуществующиеДокументы.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект.ПроверитьЗаполнение() Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
			ДокументыПоОрганизацииОбработаны = Истина;
			
		КонецЦикла;
		
		Если ДокументыПоОрганизацииОбработаны Тогда
			Продолжить;
		КонецЕсли;
		
		СуществующиеДокументы.Сбросить();
		
		Если СуществующиеДокументы.НайтиСледующий(Новый Структура("Организация, Проведен", Организация, Ложь)) Тогда
			ДокументОбъект = СуществующиеДокументы.Ссылка.ПолучитьОбъект();
			ДокументОбъект.НачисленнаяЗарплатаИВзносы.Очистить();
			ДокументОбъект.НачисленнаяЗарплатаИВзносыПоФизлицам.Очистить();
			ДокументОбъект.НачисленныйНДФЛ.Очистить();
			ДокументОбъект.УдержаннаяЗарплата.Очистить();
			ДокументОбъект.ФизическиеЛица.Очистить();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;;
		
		Если ДокументОбъект = Неопределено Тогда
			ДокументОбъект = Документы.ОтражениеЗарплатыВФинансовомУчете.СоздатьДокумент();
		КонецЕсли;
		
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Организация = Организация;
		ДокументОбъект.ПериодРегистрации = НачалоМесяца(Период);
		ДокументОбъект.Дата = КонецМесяца(Период);
		ДокументОбъект.Сводно = Ложь;
		
		// Заполнение табличных частей
		
		ТабличныеЧасти = Новый Массив;
		ТабличныеЧасти.Добавить("НачисленнаяЗарплатаИВзносы");
		ТабличныеЧасти.Добавить("НачисленныйНДФЛ");
		ТабличныеЧасти.Добавить("УдержаннаяЗарплата");
		
		СтруктураПараметров = Новый Структура("Организация, ПериодРегистрации", ДокументОбъект.Организация, ДокументОбъект.ПериодРегистрации);
		
		Для Каждого Таблица Из ТабличныеЧасти Цикл
			
			СтруктураПараметров.Вставить("СтрокаСписокТаблиц", Таблица);
			
			АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
			ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища);
			
			Результат = Новый Структура;
			Результат.Вставить("АдресХранилища", АдресХранилища);
			Результат.Вставить("СтрокаСписокТаблиц", Таблица);
			Результат.Вставить("ТекстОповещения", );
			Результат.Вставить("ПояснениеОповещения", );
			Результат.Вставить("ЕстьДанныеДляЗаполнения", Ложь);
			Результат.Вставить("Сводно", ДокументОбъект.Сводно);
			Результат.Вставить("ЗаполнятьАналитики", Истина);
			
			ЗаполнениеПослеВыполненияДлительнойОперации(ДокументОбъект, Результат);
			
		КонецЦикла;
		
		Если ДокументОбъект.НачисленнаяЗарплатаИВзносы.Количество() = 0 И
			ДокументОбъект.НачисленныйНДФЛ.Количество() = 0 И
			ДокументОбъект.УдержаннаяЗарплата.Количество() = 0 И
			ДокументОбъект.НачисленныеПроцентыПоЗаймам.Количество() = 0 Тогда
			
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает строку с наименованием колонок взносов, через запятую.
// 
// Возвращаемое значение:
//   Строка
//
Функция КолонкиДляСуммирования() Экспорт
	
	Возврат "ВзносыВсего";	
		
КонецФункции

// Процедура заполняет структуру данных для заполнения документа отражения зарплаты.
// 
// Параметры:
//   СтруктураПараметров - Структура - параметры заполнения, содержит имена таблиц, организацию и период регистрации.
//   АдресХранилища - Строка - Адрес хранилища для помещения результата заполнения.
//
Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗаполнения = Новый Структура;
	
	// получим отражение в учете
	УстановитьПривилегированныйРежим(Истина);
	ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.ДанныеДляОтраженияЗарплатыВБухучете( 
		СтруктураПараметров.ПериодРегистрации,
		СтруктураПараметров.Организация,
		СтруктураПараметров.СтрокаСписокТаблиц);
	
	ВыделитьОтпускаЗаСчетОценочныхОбязательств(ДанныеДляОтражения, СтруктураПараметров);
	
	ДобавитьПолеПодразделениеПредприятия(ДанныеДляОтражения, СтруктураПараметров);
	
	// Сведения о начисленных процентах по займам, погашенным через кассу или безналичным платежом.
	Если ДанныеДляОтражения.Свойство("УдержаннаяЗарплата") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Организация КАК Организация,
		|	ВзаиморасчетыПоЗаймамСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ВзаиморасчетыПоЗаймамСотрудникам.Проценты) КАК Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму) КАК ВидОперации,
		|	ВзаиморасчетыПоЗаймамСотрудникам.Регистратор.Подразделение КАК ПодразделениеПредприятия
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыПоЗаймамСотрудникам КАК ВзаиморасчетыПоЗаймамСотрудникам
		|ГДЕ
		|	ВзаиморасчетыПоЗаймамСотрудникам.Организация = &Организация
		|	И (ВзаиморасчетыПоЗаймамСотрудникам.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ИЛИ ВзаиморасчетыПоЗаймамСотрудникам.Регистратор ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств)
		|	И ВзаиморасчетыПоЗаймамСотрудникам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВзаиморасчетыПоЗаймамСотрудникам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВзаиморасчетыПоЗаймамСотрудникам.ФизическоеЛицо,
		|	ВзаиморасчетыПоЗаймамСотрудникам.Регистратор.Подразделение";
		
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(СтруктураПараметров.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(СтруктураПараметров.ПериодРегистрации));
		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ДанныеДляОтражения.УдержаннаяЗарплата.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеДляЗаполнения.Вставить("РезультатОтраженияЗарплаты", ДанныеДляОтражения);
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

// Процедура дополнительной обработки данных перед отражением в учете.
// 
// Параметры:
//   Объект - ДанныеФормыСтруктура, Структура - объект, в который будет помещен результат
//   Параметры - Структура - дополнительные параметры обработки
//   ЗаполнитьСводнуюТаблицу - Булево - Истина, если требуется заполнить сводную таблицу начислений
//
Процедура ЗаполнениеПослеВыполненияДлительнойОперации(Объект, Параметры, ЗаполнитьСводнуюТаблицу = Истина) Экспорт
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(Параметры.АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	
	РезультатОтраженияЗарплаты = Неопределено;
	Если СтруктураДанных.Свойство("РезультатОтраженияЗарплаты", РезультатОтраженияЗарплаты) Тогда
		
		Параметры.ТекстОповещения = НСтр("ru='Заполнено строк';uk='Заповнено рядків'");
		
		Если РезультатОтраженияЗарплаты.Свойство("НачисленнаяЗарплатаИВзносы") И РезультатОтраженияЗарплаты.НачисленнаяЗарплатаИВзносы.Количество() > 0 Тогда
			
			Для Каждого Строка Из РезультатОтраженияЗарплаты.НачисленнаяЗарплатаИВзносы Цикл
				
				ВзносыВсего = Строка.ВзносыВсего;
				Если ВзносыВсего = 0 И Строка.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Объект.НачисленнаяЗарплатаИВзносыПоФизлицам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.ВзносыВсего = ВзносыВсего;
				
				Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы Тогда
					
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если ЗаполнитьСводнуюТаблицу Тогда
				
					КолонкаВидНачисления = "";
				
				ТаблицаИтоги = Объект.НачисленнаяЗарплатаИВзносыПоФизлицам.Выгрузить(,КолонкаВидНачисления + "ПодразделениеПредприятия, ВидОперации, СпособОтраженияЗарплатыВБухучете,ГруппаУчетаНачислений,Налог,
					|Сумма, ВзносыВсего");
					
				ТаблицаИтоги.Свернуть(КолонкаВидНачисления + "ПодразделениеПредприятия, ВидОперации, СпособОтраженияЗарплатыВБухучете,ГруппаУчетаНачислений, Налог",
					"Сумма, ВзносыВсего");
					
				ТаблицаИтоги.Сортировать("ПодразделениеПредприятия");
				
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаИтоги, Объект.НачисленнаяЗарплатаИВзносы);
				
				Если Объект.НачисленнаяЗарплатаИВзносы.Количество() > 0 Тогда
					
					Параметры.ЕстьДанныеДляЗаполнения = Истина;
					
					Параметры.ПояснениеОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Начисленния: %1';uk='Нарахування: %1'"),
						Объект.НачисленнаяЗарплатаИВзносы.Количество());
					
				КонецЕсли;
				
				Если Параметры.Сводно Тогда
					Объект.НачисленнаяЗарплатаИВзносыПоФизлицам.Очистить();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РезультатОтраженияЗарплаты.Свойство("НачисленныйНДФЛ") И РезультатОтраженияЗарплаты.НачисленныйНДФЛ.Количество() > 0 Тогда
			
			Если Параметры.Сводно Тогда
				ТаблицаИтогиНДФЛ = РезультатОтраженияЗарплаты.НачисленныйНДФЛ.Скопировать(, "ПодразделениеПредприятия, Сумма, ВидОперации, КодДохода, ГруппаУчетаНачислений");
				ТаблицаИтогиНДФЛ.Свернуть("ПодразделениеПредприятия, ВидОперации, КодДохода, ГруппаУчетаНачислений", "Сумма");
				ТаблицаИтогиНДФЛ.Сортировать("ВидОперации, КодДохода");
			Иначе
				ТаблицаИтогиНДФЛ = РезультатОтраженияЗарплаты.НачисленныйНДФЛ.Скопировать(, "ФизическоеЛицо, ПодразделениеПредприятия, Сумма, ВидОперации, КодДохода, ГруппаУчетаНачислений");
				ТаблицаИтогиНДФЛ.Свернуть("ФизическоеЛицо, ПодразделениеПредприятия, ВидОперации, КодДохода, ГруппаУчетаНачислений", "Сумма");
				ТаблицаИтогиНДФЛ.Сортировать("ФизическоеЛицо, ВидОперации, КодДохода");
			КонецЕсли;
			
			Для Каждого Строка Из ТаблицаИтогиНДФЛ Цикл
				
				Если Строка.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(Объект.НачисленныйНДФЛ.Добавить(), Строка);
			КонецЦикла;
			
			Если Объект.НачисленныйНДФЛ.Количество() > 0 Тогда
				
				Если ЗначениеЗаполнено(Параметры.ПояснениеОповещения) Тогда
					Параметры.ПояснениеОповещения = Параметры.ПояснениеОповещения + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru=', НДФЛ: %1';uk=', ПДФО: %1'"),
						Объект.НачисленныйНДФЛ.Количество());
				Иначе
					Параметры.ПояснениеОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='НДФЛ: %1';uk='ПДФО: %1'"),
						Объект.НачисленныйНДФЛ.Количество());
				КонецЕсли;
				
				Параметры.ЕстьДанныеДляЗаполнения = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТипЗнч(Объект) = Тип("Структура") Тогда
			УдержаннаяЗарплата = Объект.УдержаннаяЗарплата.Скопировать();
			НачисленныеПроцентыПоЗаймам = Объект.НачисленныеПроцентыПоЗаймам.Скопировать();
		Иначе
			УдержаннаяЗарплата = Объект.УдержаннаяЗарплата.Выгрузить();
			НачисленныеПроцентыПоЗаймам = Объект.НачисленныеПроцентыПоЗаймам.Выгрузить();
		КонецЕсли;
		
		Если РезультатОтраженияЗарплаты.Свойство("УдержаннаяЗарплата")
			И РезультатОтраженияЗарплаты.УдержаннаяЗарплата.Количество() > 0 Тогда
			
			Для Каждого Строка Из РезультатОтраженияЗарплаты.УдержаннаяЗарплата Цикл
				
				Если Строка.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму Тогда
					ЗаполнитьЗначенияСвойств(НачисленныеПроцентыПоЗаймам.Добавить(), Строка);
				Иначе
					НоваяСтрока = УдержаннаяЗарплата.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ТаблицыДляЗаполненияСтатейПоУмолчанию = "";
		
		Если СтрНайти(Параметры.СтрокаСписокТаблиц, "УдержаннаяЗарплата") > 0 Тогда
			
			Если УдержаннаяЗарплата.Количество() > 0 И Параметры.ЗаполнятьАналитики Тогда
				
				Если ТипЗнч(Объект) = Тип("Структура") Тогда
					Объект.УдержаннаяЗарплата = УдержаннаяЗарплата;
				Иначе
					Объект.УдержаннаяЗарплата.Загрузить(УдержаннаяЗарплата);
				КонецЕсли;
				
				ТаблицыДляЗаполненияСтатейПоУмолчанию = "УдержаннаяЗарплата";
				
				Если ЗначениеЗаполнено(Параметры.ПояснениеОповещения) Тогда
					Параметры.ПояснениеОповещения = Параметры.ПояснениеОповещения + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru=', удержанния: %1';uk=', утримання: %1'"),
						Объект.УдержаннаяЗарплата.Количество());
				Иначе
					Параметры.ПояснениеОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Удержанния: %1';uk='Утримання: %1'"),
						Объект.УдержаннаяЗарплата.Количество());
				КонецЕсли;
				
				Параметры.ЕстьДанныеДляЗаполнения = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрНайти(Параметры.СтрокаСписокТаблиц, "НачисленныеПроцентыПоЗаймам") > 0 Тогда
			
			Если НачисленныеПроцентыПоЗаймам.Количество() > 0 И Параметры.ЗаполнятьАналитики Тогда
				
				Если ТипЗнч(Объект) = Тип("Структура") Тогда
					Объект.НачисленныеПроцентыПоЗаймам = НачисленныеПроцентыПоЗаймам;
				Иначе
					Объект.НачисленныеПроцентыПоЗаймам.Загрузить(НачисленныеПроцентыПоЗаймам);
				КонецЕсли;
				
				ТаблицыДляЗаполненияСтатейПоУмолчанию = ТаблицыДляЗаполненияСтатейПоУмолчанию + ", " + "НачисленныеПроцентыПоЗаймам";
				
				Если ЗначениеЗаполнено(Параметры.ПояснениеОповещения) Тогда
					Параметры.ПояснениеОповещения = Параметры.ПояснениеОповещения + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru=', проценты по займам: %1';uk=' відсотки по позиках: %1'"),
						Объект.НачисленныеПроцентыПоЗаймам.Количество());
				Иначе
					Параметры.ПояснениеОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Проценты по займам: %1';uk='Відсотки за позиками: %1'"),
						Объект.НачисленныеПроцентыПоЗаймам.Количество());
				КонецЕсли;
				
				Параметры.ЕстьДанныеДляЗаполнения = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТаблицыДляЗаполненияСтатейПоУмолчанию) Тогда
			ЗаполнитьСтатьиАналитикуРасходовПоУмолчанию(Объект, ТаблицыДляЗаполненияСтатейПоУмолчанию);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Параметры.ЕстьДанныеДляЗаполнения Тогда
		Параметры.ТекстОповещения = НСтр("ru='Начисления не заполнены';uk='Нарахування не заповнені'");
		Параметры.ПояснениеОповещения = НСтр("ru='В информационной базе отсутствуют данные для заполнения.';uk='В інформаційній базі відсутні дані для заповнення.'");
	КонецЕсли;
	
КонецПроцедуры

// Создает в менеджере таблицу с аналитикой других документов
// Параметры
//	- МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер,
//	- ТекущийДокумент - ДокументСсылка.ОтражениеЗарплатыВФинансовомУчете - текущий документ,
//	- ДатаАктуальности - Дата - дата, до которой обрабатываются документы.
//
Процедура СоздатьВТАналитикаДругихДокументов(МенеджерВременныхТаблиц, ТекущийДокумент, ДатаАктуальности, ИмяТаблицы = "") Экспорт
	
	// Получаем последние используемые значения по совпадающим сочетаниям заполненных полей
	
	ТекстЗапросаУдержаннаяЗарплата = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты) КАК ВидОперации,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументам) КАК СчетУчета,
	|	ЛОЖЬ КАК АналитикаПоРаботникам,
	|	ИСТИНА КАК АналитикаПоКонтрагентам
	|ПОМЕСТИТЬ ВТСчетаПоОперациям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВозмещениеУщерба),
    |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВозмещениюПричиненногоУщерба),
	|	ИСТИНА,
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументам),
	|	ЛОЖЬ,
	|	ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ    
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.Профвзносы),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте),
	|	ЛОЖЬ,
	|	ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПрочиеУдержания),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте),
	|	ЛОЖЬ,
	|	ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям),
	|	ИСТИНА,
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами),
	|	ИСТИНА,
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеПоПрочимОперациямСРаботниками),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям),
	|	ИСТИНА,
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПогашениеЗаймов),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям),
	|	ИСТИНА,
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПроцентыПоЗайму),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям),
	|	ИСТИНА,
	|	ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НезаполненнаяАналитика.ВидОперации КАК ВидОперации,
	|	Удержания.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	Удержания.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	НезаполненнаяАналитика.ФизическоеЛицо,
	|	НезаполненнаяАналитика.ПодразделениеПредприятия,
	|	Документы.Дата КАК Дата,
	|	Удержания.СчетУчета,
	|	Удержания.Субконто1,
	|	Удержания.Субконто2,
	|	Удержания.Субконто3,
	|	Удержания.Контрагент
	|ПОМЕСТИТЬ ВТАналитикаДругихДокументовУдержания
	|ИЗ
	|	ВТНезаполненнаяАналитика КАК НезаполненнаяАналитика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК Удержания
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете КАК Документы
	|			ПО Удержания.Ссылка = Документы.Ссылка
	|		ПО НезаполненнаяАналитика.ВидОперации = Удержания.ВидОперации
	|			И Удержания.ФизическоеЛицо = НезаполненнаяАналитика.ФизическоеЛицо
	|			И Удержания.ПодразделениеПредприятия = НезаполненнаяАналитика.ПодразделениеПредприятия
	|			И НезаполненнаяАналитика.Контрагент = Удержания.Контрагент
	|ГДЕ
	|	Удержания.Ссылка <> &ТекущийДокумент
	|	И Документы.ПериодРегистрации <= &ДатаАктуальности
	|	И Документы.Проведен
	|	И Удержания.СтатьяАктивовПассивов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТАналитикаДругихДокументовУдержания.ВидОперации,
	|	ВТАналитикаДругихДокументовУдержания.ФизическоеЛицо,
	|	ВТАналитикаДругихДокументовУдержания.ПодразделениеПредприятия,
	|	МАКСИМУМ(ВТАналитикаДругихДокументовУдержания.Дата) КАК Дата,
	|	ВТАналитикаДругихДокументовУдержания.Контрагент
	|ПОМЕСТИТЬ ВТЕдинственныеЗначенияСтатьи
	|ИЗ
	|	ВТАналитикаДругихДокументовУдержания КАК ВТАналитикаДругихДокументовУдержания
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТАналитикаДругихДокументовУдержания.ФизическоеЛицо,
	|	ВТАналитикаДругихДокументовУдержания.ПодразделениеПредприятия,
	|	ВТАналитикаДругихДокументовУдержания.ВидОперации,
	|	ВТАналитикаДругихДокументовУдержания.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАналитикаДругихДокументовУдержания.ВидОперации,
	|	ВТАналитикаДругихДокументовУдержания.ФизическоеЛицо,
	|	ВТАналитикаДругихДокументовУдержания.ПодразделениеПредприятия,
	|	ВТАналитикаДругихДокументовУдержания.СтатьяАктивовПассивов,
	|	ВТАналитикаДругихДокументовУдержания.АналитикаАктивовПассивов,
	|	ВТАналитикаДругихДокументовУдержания.СчетУчета КАК СчетУчета,
	|	ВТАналитикаДругихДокументовУдержания.Субконто1 КАК Субконто1,
	|	ВТАналитикаДругихДокументовУдержания.Субконто2 КАК Субконто2,
	|	ВТАналитикаДругихДокументовУдержания.Субконто3 КАК Субконто3,
	|	ВТАналитикаДругихДокументовУдержания.Контрагент
	|ПОМЕСТИТЬ ВТЗаполненнаяСтатьяПассивов
	|ИЗ
	|	ВТАналитикаДругихДокументовУдержания КАК ВТАналитикаДругихДокументовУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЕдинственныеЗначенияСтатьи КАК ВТЕдинственныеЗначенияСтатьи
	|		ПО ВТЕдинственныеЗначенияСтатьи.ВидОперации = ВТАналитикаДругихДокументовУдержания.ВидОперации
	|			И ВТЕдинственныеЗначенияСтатьи.ФизическоеЛицо = ВТАналитикаДругихДокументовУдержания.ФизическоеЛицо
	|			И ВТЕдинственныеЗначенияСтатьи.ПодразделениеПредприятия = ВТАналитикаДругихДокументовУдержания.ПодразделениеПредприятия
	|			И ВТЕдинственныеЗначенияСтатьи.Дата = ВТАналитикаДругихДокументовУдержания.Дата
	|			И ВТАналитикаДругихДокументовУдержания.Контрагент = ВТЕдинственныеЗначенияСтатьи.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НезаполненнаяАналитика.ВидОперации КАК ВидОперации,
	|	НезаполненнаяАналитика.ФизическоеЛицо,
	|	НезаполненнаяАналитика.ПодразделениеПредприятия,
	|	ВТЗаполненнаяСтатьяПассивов.СтатьяАктивовПассивов,
	|	ВТЗаполненнаяСтатьяПассивов.АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ВТЗаполненнаяСтатьяПассивов.СчетУчета ЕСТЬ NULL 
	|			ТОГДА ВТСчетаПоОперациям.СчетУчета
	|		ИНАЧЕ ВТЗаполненнаяСтатьяПассивов.СчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ВТЗаполненнаяСтатьяПассивов.СчетУчета ЕСТЬ NULL 
	|				И ВТСчетаПоОперациям.АналитикаПоРаботникам
	|			ТОГДА НезаполненнаяАналитика.ФизическоеЛицо
	|		КОГДА ВТЗаполненнаяСтатьяПассивов.СчетУчета ЕСТЬ NULL 
	|				И ВТСчетаПоОперациям.АналитикаПоКонтрагентам
	|			ТОГДА НезаполненнаяАналитика.Контрагент
	|		ИНАЧЕ ВТЗаполненнаяСтатьяПассивов.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВТЗаполненнаяСтатьяПассивов.Субконто2,
	|	ВТЗаполненнаяСтатьяПассивов.Субконто3
	|ПОМЕСТИТЬ ЗаполненнаяСтатьяПассивов
	|ИЗ
	|	ВТНезаполненнаяАналитика КАК НезаполненнаяАналитика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаполненнаяСтатьяПассивов КАК ВТЗаполненнаяСтатьяПассивов
	|		ПО НезаполненнаяАналитика.ВидОперации = ВТЗаполненнаяСтатьяПассивов.ВидОперации
	|			И НезаполненнаяАналитика.ФизическоеЛицо = ВТЗаполненнаяСтатьяПассивов.ФизическоеЛицо
	|			И НезаполненнаяАналитика.ПодразделениеПредприятия = ВТЗаполненнаяСтатьяПассивов.ПодразделениеПредприятия
	|			И НезаполненнаяАналитика.Контрагент = ВТЗаполненнаяСтатьяПассивов.Контрагент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСчетаПоОперациям КАК ВТСчетаПоОперациям
	|		ПО НезаполненнаяАналитика.ВидОперации = ВТСчетаПоОперациям.ВидОперации
	|;";
	
	ТекстЗапросаНачисленныеПроцентыПоЗаймам = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.Дата КАК Дата,
	|	Проценты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Проценты.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
	|	Проценты.СтатьяДоходов КАК СтатьяДоходов,
	|	Проценты.АналитикаДоходов КАК АналитикаДоходов
	|ПОМЕСТИТЬ ВТАналитикаДругихДокументовПроценты
	|ИЗ
	|	ВТНезаполненнаяАналитикаДоходов КАК ВТНезаполненнаяАналитикаДоходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК Проценты
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете КАК Документы
	|			ПО Проценты.Ссылка = Документы.Ссылка
	|		ПО ВТНезаполненнаяАналитикаДоходов.ФизическоеЛицо = Проценты.ФизическоеЛицо
	|			И ВТНезаполненнаяАналитикаДоходов.ПодразделениеПредприятия = Проценты.ПодразделениеПредприятия
	|ГДЕ
	|	Проценты.Ссылка <> &ТекущийДокумент
	|	И Документы.ПериодРегистрации <= &ДатаАктуальности
	|	И Документы.Проведен
	|	И Проценты.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАналитикаДругихДокументовПроценты.ФизическоеЛицо,
	|	ВТАналитикаДругихДокументовПроценты.ПодразделениеПредприятия,
	|	МАКСИМУМ(ВТАналитикаДругихДокументовПроценты.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТЕдинственноеЗначениеСтатьиДоходов
	|ИЗ
	|	ВТАналитикаДругихДокументовПроценты КАК ВТАналитикаДругихДокументовПроценты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТАналитикаДругихДокументовПроценты.ФизическоеЛицо,
	|	ВТАналитикаДругихДокументовПроценты.ПодразделениеПредприятия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЕдинственноеЗначениеСтатьиДоходов.ФизическоеЛицо,
	|	ВТЕдинственноеЗначениеСтатьиДоходов.ПодразделениеПредприятия,
	|	ВТАналитикаДругихДокументовПроценты.СтатьяДоходов,
	|	ВТАналитикаДругихДокументовПроценты.АналитикаДоходов
	|ПОМЕСТИТЬ ВТЗаполненнаяСтатьяДоходов
	|ИЗ
	|	ВТЕдинственноеЗначениеСтатьиДоходов КАК ВТЕдинственноеЗначениеСтатьиДоходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТАналитикаДругихДокументовПроценты КАК ВТАналитикаДругихДокументовПроценты
	|		ПО ВТЕдинственноеЗначениеСтатьиДоходов.ФизическоеЛицо = ВТАналитикаДругихДокументовПроценты.ФизическоеЛицо
	|			И ВТЕдинственноеЗначениеСтатьиДоходов.Дата = ВТАналитикаДругихДокументовПроценты.Дата
	|			И ВТЕдинственноеЗначениеСтатьиДоходов.ПодразделениеПредприятия = ВТАналитикаДругихДокументовПроценты.ПодразделениеПредприятия";
	
	Если ИмяТаблицы = "" Тогда
		ТекстЗапроса = ТекстЗапросаУдержаннаяЗарплата + ТекстЗапросаНачисленныеПроцентыПоЗаймам;
	ИначеЕсли ИмяТаблицы = "УдержаннаяЗарплата" Тогда
		ТекстЗапроса = ТекстЗапросаУдержаннаяЗарплата;
	Иначе
		ТекстЗапроса = ТекстЗапросаНачисленныеПроцентыПоЗаймам;
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Выполнить();
	
КонецПроцедуры


// Возвращает текст запроса для отражения в регл. учете.
// 
// Возвращаемое значение:
//   Строка
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт


	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

#Область ТекстЗапросаНачисленоПостатьям
	ТекстЗапросаНачисленоПостатьям = "
	// зарплата и взносы на расходы по статьям
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.Сумма) КАК Сумма,
	|	Сумма(Строки.Сумма / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,	
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2,
	|	Строки.АналитикаРасходов КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,		
	|
	|	Строки.СчетКт КАК СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ КАК СубконтоКт1,
	|	Строки.СубконтоКт2 КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	СУММА(Строки.Сумма) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		НачисленнаяЗарплатаИВзносы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|ГДЕ
	|	Строки.ВидОперации НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы))
	|	И Строки.ВидСчетаДт = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	И Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ),
	|	Операция.Организация,
	|	Строки.АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее),
	|	Строки.СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ,
	|	Строки.СубконтоКт2,
	|	Строки.СтатьяРасходов,
	|	Строки.Подразделение,
	|	Строки.НаправлениеДеятельности,
	|	Строки.СтатьяРасходов,
	|	Строки.Подразделение,
	|	Строки.ТипНалога
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// зарплата и взносы на прочие операции (устаревшее)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.Сумма) КАК Сумма,
	|	Сумма(Строки.Сумма / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	Строки.СчетКт КАК СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ КАК СубконтоКт1,	
	|	Строки.СубконтоКт2 КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	СУММА(Строки.Сумма) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		НачисленнаяЗарплатаИВзносы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|ГДЕ
	|	Строки.ВидОперации НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы))
	|	И Строки.ВидСчетаДт = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|	И Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ),
	|	Операция.Организация,
	|	Строки.СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ,
	|	Строки.СубконтоКт2,
	|	Строки.Подразделение,
	|	Строки.Подразделение,
	|	Строки.ТипНалога	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// зарплата и взносы на пассивы
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	СУММА(Строки.Сумма) КАК Сумма,
	|	Сумма(Строки.Сумма / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|	
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	Строки.СчетКт КАК СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ КАК СубконтоКт1,
	|	Строки.СубконтоКт2 КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	СУММА(Строки.Сумма) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		НачисленнаяЗарплатаИВзносы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|ГДЕ
	|	Строки.ВидОперации НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы))
	|	И Строки.ВидСчетаДт = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	И Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ),
	|	Операция.Организация,
	|	Строки.АналитикаРасходов,
	|	Строки.СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ,
	|	Строки.СубконтоКт2,
	|	Строки.СтатьяРасходов,
	|	Строки.Подразделение,
	|	Строки.СтатьяРасходов,
	|	Строки.НаправлениеДеятельности,
	|	Строки.СчетУчета,
	|	Строки.Субконто1,
	|	Строки.Субконто2,
	|	Строки.Субконто3,
	|	Строки.ТипНалога
	|";

	ТекстЗапросаНачисленоПостатьям = СтрШаблон(ТекстЗапросаНачисленоПостатьям, 
		НСтр("ru='Начисление заработной платы';uk='Нарахування заробітної плати'",ЯзыкСодержания),
		НСтр("ru='Начисление взносов с заработной платы';uk='Нарахування внесків із заробітної плати'",ЯзыкСодержания));
	
#КонецОбласти


// Отпуск за счет обязательств: отражение резервов только в БУ, в НУ на счета затрат
#Область ТекстЗапросаЕжегодныйОтпускОценочныеОбязательства

	ТекстЗапросаЕжегодныйОтпускОценочныеОбязательства = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.Сумма / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,	
	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбеспечениеВыплатОтпусков) КАК СчетДт,
	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Строки.Сумма КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,	
	|
	|	Строки.СчетКт КАК СчетКт,
	|   ВЫБОР КОГДА Строки.Налог = НЕОПРЕДЕЛЕНО
	|		ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|   КОНЕЦ КАК СубконтоКт1,
	|	Строки.СубконтоКт2 КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Строки.Сумма КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		НачисленнаяЗарплатаИВзносы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|ГДЕ
	|	Строки.ВидОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства))
	|";

	ТекстЗапросаЕжегодныйОтпускОценочныеОбязательства = СтрШаблон(ТекстЗапросаЕжегодныйОтпускОценочныеОбязательства, 
		НСтр("ru='Отпуск за счет обязательств';uk='Відпустка за рахунок зобов''язань'",ЯзыкСодержания));

#КонецОбласти


#Область ТекстЗапросаНачисленоСдельно
	ТекстЗапросаНачисленоСдельно = "
	// прямые производственные
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ) КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.СуммаРегл) КАК Сумма,
	|	Сумма(Строки.СуммаРегл / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ВидРабот КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.СдельнаяОплатаТруда) КАК СубконтоДт2,
	|	Строки.ГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,	
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ВтСчета.СчетКт КАК СчетКт,
	|	ВЫБОР КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) ТОГДА
	|		Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Строки.Налог
	|	КОНЕЦ КАК СубконтоКт1,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Сумма(Строки.СуммаРегл) КАК ВалютнаяСуммаКт,	
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		(
	|			ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) КАК ТипНалога,				ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) КАК СчетКт	// (Кт 661)
	|			ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НачисленныйЕСВ),			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоПенсионномуОбеспечению)		// (Кт 651)
	|		) КАК ВтСчета
	|	ПО
	|		Строки.ТипНалога = ВтСчета.ТипНалога
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ), День)
	|	
	|ГДЕ
	|	Строки.ВидРабот <> ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Регистратор,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ),
	|	Строки.Организация,
	|	Строки.Подразделение,
	|	Строки.НаправлениеДеятельности,
	|	Строки.ВидРабот,
	|	Строки.ГруппаПродукции,
	|	ВтСчета.СчетКт,
	|	ВЫБОР
	|		КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)
	|			ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|	КОНЕЦ,
	|	Строки.ТипНалога
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на расходы
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ) КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.СуммаРегл) КАК Сумма,
	|	Сумма(Строки.СуммаРегл / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ВЫБОР 
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|	КОНЕЦ КАК ВидСчетаДт,
	|
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL ТОГДА
	|			Строки.СтатьяРасходов
	|	ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL ТОГДА
	|			Строки.Подразделение
	|	ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL ТОГДА
	|			Строки.СтатьяРасходов
	|	ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт1,
	|
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL ТОГДА
	|			Строки.АналитикаРасходов
	|	ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт2,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,	
	|
	|	Сумма(Строки.СуммаРегл) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ВтСчета.СчетКт КАК СчетКт,
	|	ВЫБОР КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) ТОГДА
	|		Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Строки.Налог
	|	КОНЕЦ КАК СубконтоКт1,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		(
	|			ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) КАК ТипНалога,				ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) КАК СчетКт	// (Кт 661)
	|			ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НачисленныйЕСВ),			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоПенсионномуОбеспечению)		// (Кт 651)
	|		) КАК ВтСчета
	|	ПО
	|		Строки.ТипНалога = ВтСчета.ТипНалога
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиНаПрочиеАктивы
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиНаПрочиеАктивы.Ссылка
	|		И СтатьиНаПрочиеАктивы.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ), День)
	|
	|ГДЕ
	|	Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)

	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Регистратор,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ),
	|	Строки.Организация,
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
	|			ТОГДА Строки.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
	|			ТОГДА Строки.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
	|			ТОГДА Строки.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
	|			ТОГДА Строки.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтСчета.СчетКт,
	|	ВЫБОР
	|		КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)
	|			ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Подразделение,
	|	Строки.НаправлениеДеятельности,
	|	Строки.ТипНалога
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие пассивы
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ) КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.СуммаРегл) КАК Сумма,
	|	Сумма(Строки.СуммаРегл / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ВтСчета.СчетКт КАК СчетКт,
	|	ВЫБОР КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) ТОГДА
	|		Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Строки.Налог
	|	КОНЕЦ КАК СубконтоКт1,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Сумма(Строки.СуммаРегл) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|		ТОГДА ""%1""
	|		ИНАЧЕ ""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		(
	|			ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) КАК ТипНалога,				ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) КАК СчетКт	// (Кт 661)
	|			ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НачисленныйЕСВ),			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоПенсионномуОбеспечению)		// (Кт 651)
	|		) КАК ВтСчета
	|	ПО
	|		Строки.ТипНалога = ВтСчета.ТипНалога
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ), День)
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Регистратор,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ),
	|	Строки.Организация,
	|	Строки.Подразделение,
	|	Строки.СчетУчета,
	|	ВтСчета.СчетКт,
	|	Строки.НаправлениеДеятельности,
	|	Строки.Субконто1,
	|	Строки.Субконто2,
	|	Строки.Субконто3,
	|	ВЫБОР
	|		КОГДА ВтСчета.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)
	|			ТОГДА Строки.ФизическоеЛицо
	|		ИНАЧЕ Строки.Налог
	|	КОНЕЦ,
	|	Строки.ТипНалога
	|";        

	ТекстЗапросаНачисленоСдельно = СтрШаблон(ТекстЗапросаНачисленоСдельно, 
		НСтр("ru='Начисление сдельной заработной платы';uk='Нарахування відрядної заробітної плати'",ЯзыкСодержания),
		НСтр("ru='Начисление взносов со сдельной заработной платы';uk='Нарахування внесків із відрядної заробітної плати'",ЯзыкСодержания));

#КонецОбласти

#Область ТекстЗапросаНДФЛ
	ТекстЗапросаНДФЛ = "
	// НДФЛ, НФДЛДивидендыСотрудникам
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.Сумма) КАК Сумма,
	|	Сумма(Строки.Сумма / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЕСТЬNULL(ГруппыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)) КАК СчетДт,
	|	ВЫБОР КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА ЕСТЬNULL(ТаблицаСоотвествияФизлицКонтрагентов.Контрагент, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Значение(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Сумма(Строки.Сумма) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЕСТЬNULL(ГруппыУчетаНДФЛ.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДФЛ)) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Сумма(Строки.Сумма) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчетаНДФЛ
	|	ПО ГруппыУчетаНДФЛ.ГруппаУчета = ЗНАЧЕНИЕ(Справочник.ГруппыУчетаНачисленийИУдержаний.НДФЛ)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|	ПО Строки.ГруппаУчетаНачислений = ГруппыУчета.ГруппаУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСоотвествияФизлицКонтрагентов КАК ТаблицаСоотвествияФизлицКонтрагентов
	|	ПО Строки.ФизическоеЛицо = ТаблицаСоотвествияФизлицКонтрагентов.ФизическоеЛицо
	|
	|ГДЕ
	|	Строки.ВидОперации В (Значение(Перечисление.ВидыОперацийПоЗарплате.НДФЛ),
	|		Значение(Перечисление.ВидыОперацийПоЗарплате.НФДЛДивидендыСотрудникам),
	|		Значение(Перечисление.ВидыОперацийПоЗарплате.НДФЛДоходыКонтрагентов),
	|		Значение(Перечисление.ВидыОперацийПоЗарплате.НДФЛРасчетыСБывшимиСотрудниками))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА ЕСТЬNULL(ТаблицаСоотвествияФизлицКонтрагентов.Контрагент, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Значение(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ),
	|	Строки.ПодразделениеПредприятия,
	|	Строки.ПодразделениеПредприятия,
	|	ГруппыУчета.СчетУчета,
	|	ГруппыУчетаНДФЛ.СчетУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВоенныйСбор
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.Сумма) КАК Сумма,
	|	Сумма(Строки.Сумма / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЕСТЬNULL(ГруппыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)) КАК СчетДт,

	|	ВЫБОР КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА ЕСТЬNULL(ТаблицаСоотвествияФизлицКонтрагентов.Контрагент, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Значение(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Сумма(Строки.Сумма) КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЕСТЬNULL(ГруппыУчетаНДФЛ.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоОбязательнымПлатежам)) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Налоги.ВоенныйСбор) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Сумма(Строки.Сумма) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%2"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|	ПО Строки.ГруппаУчетаНачислений = ГруппыУчета.ГруппаУчета
	|
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчетаНДФЛ
	|	ПО ГруппыУчетаНДФЛ.ГруппаУчета = ЗНАЧЕНИЕ(Справочник.ГруппыУчетаНачисленийИУдержаний.ВоенныйСбор)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСоотвествияФизлицКонтрагентов КАК ТаблицаСоотвествияФизлицКонтрагентов
	|	ПО Строки.ФизическоеЛицо = ТаблицаСоотвествияФизлицКонтрагентов.ФизическоеЛицо
	|
	|ГДЕ
	|	Строки.ВидОперации В (Значение(Перечисление.ВидыОперацийПоЗарплате.ВоенныйСбор),
	|						Значение(Перечисление.ВидыОперацийПоЗарплате.ВСДоходыКонтрагентов),
	|						Значение(Перечисление.ВидыОперацийПоЗарплате.ВСРасчетыСБывшимиСотрудниками))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА ЕСТЬNULL(ТаблицаСоотвествияФизлицКонтрагентов.Контрагент, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	КОГДА Операция.ПроводкиПоРаботникам И ГруппыУчета.СубконтоУчета <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) 
	|		ТОГДА Строки.ФизическоеЛицо
	|	ИНАЧЕ
	|		Значение(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ),
	|	Строки.ПодразделениеПредприятия,
	|	Строки.ПодразделениеПредприятия,
	|	ГруппыУчета.СчетУчета,
	|	ГруппыУчетаНДФЛ.СчетУчета
	|
	|";  

	ТекстЗапросаНДФЛ = СтрШаблон(ТекстЗапросаНДФЛ, 
		НСтр("ru='НДФЛ';uk='ПДФО'",ЯзыкСодержания),
		НСтр("ru='Военный сбор';uk='Військовий збір'",ЯзыкСодержания));

#КонецОбласти

#Область ТекстЗапросаУдержания
	ТекстЗапросаУдержания = "
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.Сумма / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,		
	|
	|	ЕСТЬNULL(ГруппыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)) КАК СчетДт,
	|	ВЫБОР КОГДА (Операция.Сводно Или Не Операция.ПроводкиПоРаботникам) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Строки.ФизическоеЛицо
	|	КОНЕЦ КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,		
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Субконто1 КАК СубконтоКт1,
	|	Строки.Субконто2 КАК СубконтоКт2,
	|	Строки.Субконто3 КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	Строки.ВидОперации КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|	ПО Строки.ГруппаУчетаНачислений = ГруппыУчета.ГруппаУчета
	|
	|ГДЕ
	|	Строки.ВидОперации НЕ В (Значение(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Возврат излишне выплаченных суммы вследствие счетных ошибок
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.Сумма / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,		
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате) КАК СчетДт,
	|	ВЫБОР КОГДА (Операция.Сводно Или Не Операция.ПроводкиПоРаботникам) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Строки.ФизическоеЛицо
	|	КОНЕЦ КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеПредприятия КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,		
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|	ПО Строки.ГруппаУчетаНачислений = ГруппыУчета.ГруппаУчета
	|
	|ГДЕ
	|	Строки.ВидОперации В (Значение(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок))
	|";

	ТекстЗапросаУдержания = СтрШаблон(ТекстЗапросаУдержания, 
		НСтр("ru='Возврат излишне выплаченных суммы вследствие счетных ошибок';uk='Повернення зайво виплачених суми внаслідок лічильних помилок'",ЯзыкСодержания));
	
#КонецОбласти
	
#Область ТекстЗапросаНачисленныеПроцентыПоЗаймам
	
	ТекстЗапросаНачисленныеПроцентыПоЗаймам = "
	// Начисленные проценты по займам
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.Сумма / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,		
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям) КАК СчетДт,
	|	Строки.ФизическоеЛицо КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеПредприятия КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеПредприятия КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,		
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ), День)	
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";

	ТекстЗапросаНачисленныеПроцентыПоЗаймам = СтрШаблон(ТекстЗапросаНачисленныеПроцентыПоЗаймам, 
		НСтр("ru='Начисленные проценты по займам';uk='Нараховані відсотки за позиками'",ЯзыкСодержания));
	
#КонецОбласти
	
	Возврат                   ТекстЗапросаНачисленоПостатьям
		+ " ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаЕжегодныйОтпускОценочныеОбязательства
		+ " ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаНачисленоСдельно
		+ " ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаНДФЛ
		+ " ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаУдержания
		+ " ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаНачисленныеПроцентыПоЗаймам
		+ "";
	
КонецФункции
	
// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапроса = 
		ТекстЗапросаВтСписокСпособовОтражения(Неопределено, ТекстыЗапроса) +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаТаблицаНачисленнаяЗарплатаИВзносы(Неопределено, ТекстыЗапроса) +
		"ВЫБРАТЬ
		|	ВТНачисленнаяЗарплатаИВзносы.Ссылка,
		|	МАКСИМУМ(ВТНачисленнаяЗарплатаИВзносы.НомерСтроки) КАК НомерСтроки,
		|	ВТНачисленнаяЗарплатаИВзносы.Подразделение,
		|	ВТНачисленнаяЗарплатаИВзносы.НаправлениеДеятельности,
		|	ВТНачисленнаяЗарплатаИВзносы.ВидОперации,
		|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов,
		|	ВТНачисленнаяЗарплатаИВзносы.АналитикаРасходов,
		|	ВТНачисленнаяЗарплатаИВзносы.АналитикаАктивовПассивов,
		|	ВТНачисленнаяЗарплатаИВзносы.СчетУчета,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто1,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто2,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто3,
		|	СУММА(ВТНачисленнаяЗарплатаИВзносы.Сумма) КАК Сумма,
		|	ВТНачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТНачисленнаяЗарплатаИВзносы.СчетКт КАК СчетКт,
		|	ВЫБОР
		|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
		|	КОНЕЦ КАК ВидСчетаДт,
		|	СтатьиНаПрочиеАктивы.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	ВЫБОР
		|		КОГДА ВТНачисленнаяЗарплатаИВзносы.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
        |			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоКт2,
		|	ВЫБОР
		|		КОГДА ВТНачисленнаяЗарплатаИВзносы.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
        |			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТНачисленнаяЗарплатаИВзносы.Налог
		|	КОНЕЦ КАК Налог,
		|	ВТНачисленнаяЗарплатаИВзносы.ТипНалога КАК ТипНалога
		|ПОМЕСТИТЬ НачисленнаяЗарплатаИВзносы
		|ИЗ
		|	ВТНачисленнаяЗарплатаИВзносы КАК ВТНачисленнаяЗарплатаИВзносы
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиНаПрочиеАктивы
		|	ПО 
		|		ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов = СтатьиНаПрочиеАктивы.Ссылка
		|		И (СтатьиНаПрочиеАктивы.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
		|
		|ГДЕ
		|	ВТНачисленнаяЗарплатаИВзносы.Сумма <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНачисленнаяЗарплатаИВзносы.Ссылка,
		|	ВТНачисленнаяЗарплатаИВзносы.Подразделение,
		|	ВТНачисленнаяЗарплатаИВзносы.НаправлениеДеятельности,		
		|	ВТНачисленнаяЗарплатаИВзносы.ВидОперации,
		|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов,
		|	ВТНачисленнаяЗарплатаИВзносы.АналитикаРасходов,
		|	ВТНачисленнаяЗарплатаИВзносы.АналитикаАктивовПассивов,
		|	ВТНачисленнаяЗарплатаИВзносы.СчетУчета,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто1,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто2,
		|	ВТНачисленнаяЗарплатаИВзносы.Субконто3,
		|	ВТНачисленнаяЗарплатаИВзносы.СчетКт,
		|	ВЫБОР
		|		КОГДА СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
		|	КОНЕЦ,
		|	СтатьиНаПрочиеАктивы.ВариантРаспределенияРасходовРегл,
		|	ВТНачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА ВТНачисленнаяЗарплатаИВзносы.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВТНачисленнаяЗарплатаИВзносы.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
        |			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТНачисленнаяЗарплатаИВзносы.Налог
		|	КОНЕЦ,
		|	ВТНачисленнаяЗарплатаИВзносы.ТипНалога
		|;";
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат	ТекстОтраженияВРеглУчетеУКР();	
	
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции
	
// Функция возвращает статью активов пассивов по умолчанию для заполнения в табличной части Удержания.
// Вызывается из формы документа, а также используется правилами загрузки данных из ЗУП25.
// Параметры:
//	- ФизическоеЛицо - СправочникСсылка.ФизическиеЛица,
//	- ПодразделениеПредприятия - СправочникСсылка.СтруктураПредприятия,
//	- ВидОперации - ПеречислениеСсылка.ВидыОперацийПоЗарплате,
//	- Контрагент - СправочникСсылка.Контрагенты,
//	- ТекущийДокумент - ДокументСсылка.ОтражениеЗарплатыВФинансовомУчете - текущий документ,
//	- ДатаАктуальности - Дата - дата, до которой обрабатываются документы.
//
Функция СтатьяАктивовПассивовПоУмолчанию(ФизическоеЛицо, ПодразделениеПредприятия, ВидОперации, Контрагент, ТекущийДокумент, ДатаАктуальности) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ФизическоеЛицо КАК ФизическоеЛицо,
	|	&ПодразделениеПредприятия КАК ПодразделениеПредприятия,
	|	&ВидОперации КАК ВидОперации,
	|	&Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ВТНезаполненнаяАналитика";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ФизическоеЛицо",           ФизическоеЛицо);
	Запрос.УстановитьПараметр("ПодразделениеПредприятия", ПодразделениеПредприятия);
	Запрос.УстановитьПараметр("ВидОперации",              ВидОперации);
	Запрос.УстановитьПараметр("Контрагент",               Контрагент);
	Запрос.Выполнить();
	
	Документы.ОтражениеЗарплатыВФинансовомУчете.СоздатьВТАналитикаДругихДокументов(МенеджерВременныхТаблиц, ТекущийДокумент, ДатаАктуальности, "УдержаннаяЗарплата");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АналитикаДругихДокументов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	АналитикаДругихДокументов.ВидОперации КАК ВидОперации,
	|	АналитикаДругихДокументов.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	АналитикаДругихДокументов.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	АналитикаДругихДокументов.СчетУчета КАК СчетУчета,
	|	АналитикаДругихДокументов.Субконто1 КАК Субконто1,
	|	АналитикаДругихДокументов.Субконто2 КАК Субконто2,
	|	АналитикаДругихДокументов.Субконто3 КАК Субконто3
	|ИЗ
	|	ЗаполненнаяСтатьяПассивов КАК АналитикаДругихДокументов";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	СтатьяАналитикаРасходов = Новый Структура("СтатьяАктивовПассивов, АналитикаАктивовПассивов, СчетУчета, Субконто1, Субконто2, Субконто3, ПредставлениеОтраженияВРеглУчете");
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтатьяАналитикаРасходов, Выборка);
	КонецЕсли;
	
	
	Возврат СтатьяАналитикаРасходов;
	
КонецФункции


// Возвращает параметры выбора статей в документе.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	МассивПаметровВыбора = Новый Массив;

	
	#Область УдержаннаяЗарплатаСтатьяАктивовПассивов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.УдержаннаяЗарплата";
	ПараметрыВыбора.Статья = "СтатьяАктивовПассивов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("УдержаннаяЗарплатаСтатьяАктивовПассивов");

	
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("УдержаннаяЗарплатаАналитикаАктивовПассивов");


	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область УдержаннаяЗарплатаСтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.УдержаннаяЗарплата";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("УдержаннаяЗарплатаСтатьяДоходов");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("УдержаннаяЗарплатаАналитикаДоходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область НачисленныеПроцентыПоЗаймамСтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.НачисленныеПроцентыПоЗаймам";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("НачисленныеПроцентыПоЗаймамСтатьяДоходов");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("НачисленныеПроцентыПоЗаймамАналитикаДоходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

// Возвращает параметры настройки счетов учета в документе.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. НастройкаСчетовУчетаСервер.ПараметрыНастройкиСчетаУчетаОперации).
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	МассивПараметров = Новый Массив();
	
	#Область НастройкаСчетовОтраженияУдержаннойЗарплаты
	ПараметрыНастройки = НастройкаСчетовУчета.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект.УдержаннаяЗарплата";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("УдержаннаяЗарплатаСчетРеглУчетаАктивовПассивов");
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение


Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                                 КАК Ссылка,
	|	Реквизиты.Дата                                   КАК Дата,
	|	КОНЕЦПЕРИОДА(Реквизиты.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	Реквизиты.Организация                            КАК Организация,
	|	Не Реквизиты.Сводно                              КАК ДетализацияПоФизлицам,
	|	Реквизиты.Сводно                                 КАК Сводно,
	|	Реквизиты.ПроводкиПоРаботникам                   КАК ПроводкиПоРаботникам
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		Константы.ВалютаРегламентированногоУчета.Получить(),
		,
		Реквизиты.ПериодРегистрации);
	
	Запрос.УстановитьПараметр("Ссылка",                                 Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Период",                                 Реквизиты.ПериодРегистрации);
	Запрос.УстановитьПараметр("Дата",                                   Реквизиты.Дата);
	Запрос.УстановитьПараметр("Организация",                            Реквизиты.Организация);
	Запрос.УстановитьПараметр("ДетализацияПоФизлицам",                  Реквизиты.ДетализацияПоФизлицам);
	Запрос.УстановитьПараметр("ДатаНачала",                             НачалоМесяца(Реквизиты.ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания",                          КонецМесяца(Реквизиты.ПериодРегистрации));
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",         Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("Сводно",                                 Реквизиты.Сводно);
	Запрос.УстановитьПараметр("ПроводкиПоРаботникам",                   Реквизиты.ПроводкиПоРаботникам);
	Запрос.УстановитьПараметр("ВалютаРегл",                             Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихАктивовПассивов",  ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации",         Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Дата));
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

#Область УстановкаВременныхТаблицИПараметров

Функция ТекстЗапросаВтНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса)
	
	ТекстЗапросаВтТаблицаДокументовКОтражению(Запрос, ТекстыЗапроса);
	
	ТекстЗапросаВтСписокСпособовОтражения(Запрос, ТекстыЗапроса);
	
	ТекстЗапросаТаблицаНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса);
	
	Возврат "";
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДокументовКОтражению(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаДокументовКОтражению";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтСписокСпособовОтражения(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтСписокСпособовОтражения";
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленнаяЗарплата.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения
	|ПОМЕСТИТЬ ВтСписокСпособовОтражения
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплата
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО НачисленнаяЗарплата.Ссылка = ДокументыКОтражению.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса)
	
	
#Область ВтТаблицаДанныхСпособовОтражения
	
	ИмяРегистра = "ВтТаблицаДанныхСпособовОтражения";
	
	ТекстЗапросаВтТаблицаДанныхСпособовОтражения = Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов("ВтТаблицаДанныхСпособовОтражения");
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВтТаблицаДанныхСпособовОтражения, ИмяРегистра);
	
#КонецОбласти

#Область ВтТаблицаДанныхГруппУчета
	
	ИмяРегистра = "ВтТаблицаДанныхГруппУчета";
	
	ТекстЗапросаВтТаблицаДанныхГруппУчета = Справочники.ГруппыУчетаНачисленийИУдержаний.ТекстЗапросаДанныхГруппУчета("ВтТаблицаДанныхГруппУчета");
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВтТаблицаДанныхГруппУчета, ИмяРегистра);
	
#КонецОбласти

#Область ВТТаблицаДокумента
	
	ИмяРегистра = "ВТТаблицаДокумента";
	ТекстЗапросаВТТаблицаДокумента =
	"ВЫБРАТЬ
	|	ЗарплатаСводно.Ссылка КАК Ссылка,
	|	ЗарплатаСводно.Ссылка.Организация КАК Организация,
	|	ЗарплатаСводно.НомерСтроки КАК НомерСтроки,
	|	ЗарплатаСводно.ПодразделениеПредприятия КАК Подразделение,
	|	ЗарплатаСводно.ВидОперации КАК ВидОперации,
	|	ЗарплатаСводно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА ЗарплатаСводно.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ ЗарплатаСводно.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗарплатаСводно.ВзносыВсего,
	|	ЗарплатаСводно.Налог,
	|	ЗарплатаСводно.ГруппаУчетаНачислений
	|ПОМЕСТИТЬ ВТТаблицаДокумента
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК ЗарплатаСводно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ЗарплатаСводно.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	ЗарплатаСводно.Ссылка.Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗарплатаДетально.Ссылка,
	|	ЗарплатаДетально.Ссылка.Организация КАК Организация,
	|	ЗарплатаДетально.НомерСтроки,
	|	ЗарплатаДетально.ПодразделениеПредприятия,
	|	ЗарплатаДетально.ВидОперации,
	|	ЗарплатаДетально.СпособОтраженияЗарплатыВБухучете,
	|	СУММА(ВЫБОР
	|		КОГДА ЗарплатаДетально.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ ЗарплатаДетально.Сумма
	|	КОНЕЦ),
	|	ВЫБОР КОГДА ЗарплатаДетально.Ссылка.ПроводкиПоРаботникам ТОГДА
	|		ЗарплатаДетально.ФизическоеЛицо
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК ФизическоеЛицо,
	|	СУММА(ЗарплатаДетально.ВзносыВсего),
	|	ЗарплатаДетально.Налог,
	|	ЗарплатаДетально.ГруппаУчетаНачислений
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК ЗарплатаДетально
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ЗарплатаДетально.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ ЗарплатаДетально.Ссылка.Сводно
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	ЗарплатаДетально.Ссылка,
	|	ЗарплатаДетально.Ссылка.Организация,
	|	ЗарплатаДетально.НомерСтроки,
	|	ЗарплатаДетально.ПодразделениеПредприятия,
	|	ЗарплатаДетально.ВидОперации,
	|	ЗарплатаДетально.СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР КОГДА ЗарплатаДетально.Ссылка.ПроводкиПоРаботникам ТОГДА
	|		ЗарплатаДетально.ФизическоеЛицо
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗарплатаДетально.Налог,
	|	ЗарплатаДетально.ГруппаУчетаНачислений";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТТаблицаДокумента, ИмяРегистра);
	
#КонецОбласти

#Область ВтТаблицаСоотвествияФизлицКонтрагентов
	
	ИмяРегистра = "ВтТаблицаСоотвествияФизлицКонтрагентов";
	
	ТекстЗапросаВтТаблицаСоотвествияФизлицКонтрагентов = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|   МАКСИМУМ(Контрагенты.Ссылка) КАК Контрагент
	|
	|ПОМЕСТИТЬ ВтТаблицаСоотвествияФизлицКонтрагентов
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Зарплата.ФизическоеЛицо
	|	ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК Зарплата
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО Зарплата.Ссылка = ДокументыКОтражению.Ссылка
	|
	|   ОБЪЕДИНИТЬ ВСЕ
	|
	|   ВЫБРАТЬ
	|	НачисленныйНДФЛ.ФизическоеЛицо
	|	ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК НачисленныйНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО НачисленныйНДФЛ.Ссылка = ДокументыКОтражению.Ссылка)КАК НачисленнаяЗарплатаИВзносы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|	ПО Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|	 И НачисленнаяЗарплатаИВзносы.ФизическоеЛицо.КодПоДРФО = Контрагенты.КодПоЕДРПОУ
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВтТаблицаСоотвествияФизлицКонтрагентов, ИмяРегистра);
	
#КонецОбласти


#Область ТаблицаДокумента
	
	ИмяРегистра = "ТаблицаДокумента";
	ТекстЗапросаТаблицаДокумента =
	// сумма оплаты труда
	"ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.Ссылка КАК Ссылка,
	|	НачисленнаяЗарплатаИВзносы.НомерСтроки КАК НомерСтроки,
	|	НачисленнаяЗарплатаИВзносы.Подразделение КАК Подразделение,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Подразделение
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Подразделение
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивов
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|	СтатьиРасходов.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Субконто1 = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	СтатьиРасходов.Субконто2 КАК Субконто2,
	|	СтатьиРасходов.Субконто3 КАК Субконто3,
	|	НачисленнаяЗарплатаИВзносы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ГруппыУчета.СубконтоУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|			ТОГДА ЕСТЬNULL(ТаблицаСоотвествияФизлицКонтрагентов.Контрагент, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|		ИНАЧЕ НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|	КОНЕЦ КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ГруппыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате)) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) КАК ТипНалога,
	|   НЕОПРЕДЕЛЕНО КАК Налог
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ВТТаблицаДокумента КАК НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхСпособовОтражения КАК СтатьиРасходов
	|		ПО НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|		ПО НачисленнаяЗарплатаИВзносы.ГруппаУчетаНачислений = ГруппыУчета.ГруппаУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСоотвествияФизлицКонтрагентов КАК ТаблицаСоотвествияФизлицКонтрагентов
	|		ПО НачисленнаяЗарплатаИВзносы.ФизическоеЛицо = ТаблицаСоотвествияФизлицКонтрагентов.ФизическоеЛицо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ЕСВ
	|ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.Ссылка,
	|	НачисленнаяЗарплатаИВзносы.НомерСтроки,
	|	НачисленнаяЗарплатаИВзносы.Подразделение,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации,
	|	СтатьиРасходов.СтатьяРасходовВзносов,
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Подразделение
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачислениеЕСВ)
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовВзносов
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Подразделение
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачислениеЕСВ)
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ,
	|	СтатьиРасходов.СчетУчетаВзносов,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Субконто1Взносов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.ФизическоеЛицо
	
	|		ИНАЧЕ СтатьиРасходов.Субконто1Взносов
	|	КОНЕЦ,
	|	СтатьиРасходов.Субконто2Взносов,
	|	СтатьиРасходов.Субконто3Взносов,
	|	НачисленнаяЗарплатаИВзносы.ВзносыВсего,
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	ЕСТЬNULL(ГруппыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоПенсионномуОбеспечению)),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НачисленныйЕСВ),
	|	НачисленнаяЗарплатаИВзносы.Налог
	|ИЗ
	|	ВТТаблицаДокумента КАК НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхСпособовОтражения КАК СтатьиРасходов
	|		ПО НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхГруппУчета КАК ГруппыУчета
	|		ПО ГруппыУчета.ГруппаУчета = ЗНАЧЕНИЕ(Справочник.ГруппыУчетаНачисленийИУдержаний.ЕСВ)";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаДокумента, ИмяРегистра);
	
#КонецОбласти

#Область ВТНачисленнаяЗарплатаИВзносы
	
	ИмяРегистра = "ВТНачисленнаяЗарплатаИВзносы";
	ТекстЗапросаВТНачисленнаяЗарплатаИВзносы =
	"ВЫБРАТЬ
	|	ВТНачисленнаяЗарплатаИВзносы.Ссылка,
	|	МИНИМУМ(ВТНачисленнаяЗарплатаИВзносы.НомерСтроки),
	|	ВТНачисленнаяЗарплатаИВзносы.Подразделение,
	|	ВТНачисленнаяЗарплатаИВзносы.ВидОперации,
	|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов,
	|	ВТНачисленнаяЗарплатаИВзносы.НаправлениеДеятельности,
	|	ВТНачисленнаяЗарплатаИВзносы.АналитикаРасходов,
	|	ВТНачисленнаяЗарплатаИВзносы.АналитикаАктивовПассивов,
	|	ВТНачисленнаяЗарплатаИВзносы.СчетУчета,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто1,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто2,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто3,
	|	СУММА(ВТНачисленнаяЗарплатаИВзносы.Сумма) КАК Сумма,
	|	ВТНачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	ВТНачисленнаяЗарплатаИВзносы.СчетКт,
	|	ВТНачисленнаяЗарплатаИВзносы.ТипНалога,
	|	ВТНачисленнаяЗарплатаИВзносы.Налог,
	|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл
	|ПОМЕСТИТЬ ВТНачисленнаяЗарплатаИВзносы
	|ИЗ
	|	ТаблицаДокумента КАК ВТНачисленнаяЗарплатаИВзносы
	|
	|ГДЕ
	|	ВТНачисленнаяЗарплатаИВзносы.ВидОперации Не В(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачисленнаяЗарплатаИВзносы.ТипНалога,
	|	ВТНачисленнаяЗарплатаИВзносы.Налог,
	|	ВТНачисленнаяЗарплатаИВзносы.СчетКт,
	|	ВТНачисленнаяЗарплатаИВзносы.ВидОперации,
	|	ВТНачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	ВТНачисленнаяЗарплатаИВзносы.АналитикаРасходов,
	|	ВТНачисленнаяЗарплатаИВзносы.АналитикаАктивовПассивов,
	|	ВТНачисленнаяЗарплатаИВзносы.СчетУчета,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто1,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто2,
	|	ВТНачисленнаяЗарплатаИВзносы.Субконто3,
	|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов,
	|	ВТНачисленнаяЗарплатаИВзносы.НаправлениеДеятельности,
	|	ВТНачисленнаяЗарплатаИВзносы.Ссылка,
	|	ВТНачисленнаяЗарплатаИВзносы.Подразделение,
	|	ВТНачисленнаяЗарплатаИВзносы.СтатьяРасходов.ВариантРаспределенияРасходовРегл
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТНачисленнаяЗарплатаИВзносы.Сумма) <> 0";

	ТекстыЗапроса.Добавить(ТекстЗапросаВТНачисленнаяЗарплатаИВзносы, ИмяРегистра);
	
#КонецОбласти

	ТекстЗапроса =
		ТекстЗапросаВтТаблицаДанныхСпособовОтражения +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаВтТаблицаДанныхГруппУчета +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаВТТаблицаДокумента +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаВтТаблицаСоотвествияФизлицКонтрагентов +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаТаблицаДокумента +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|" +
		ТекстЗапросаВТНачисленнаяЗарплатаИВзносы +
		";
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВыработкаСотрудников";
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВыработкаСотрудников.Период,
	|	ВыработкаСотрудников.Организация,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	
	|	ВыработкаСотрудников.ПартияПроизводства,	
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.Этап,
	
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.Налог,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате,
	
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.СчетУчета,
	|	ВыработкаСотрудников.Субконто1,
	|	ВыработкаСотрудников.Субконто2,
	|	ВыработкаСотрудников.Субконто3,
	|	ВыработкаСотрудников.Сотрудник,
	
	|	ВыработкаСотрудников.Сумма,
	|	ВыработкаСотрудников.СуммаРегл
	
	|ПОМЕСТИТЬ ВтТаблицаВыработкаСотрудников
	|ИЗ
	|	&ТаблицаВыработкаСотрудников КАК ВыработкаСотрудников";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос)
	
	Если Запрос.Параметры.Свойство("ТаблицаВыработкаСотрудников") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросВыработка = Новый Запрос;
	ЗапросВыработка.Текст = 
		БазаРаспределенияТрудозатрат() +
		Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов();
		

	ЗапросВыработка.УстановитьПараметр("Ссылка",                          Запрос.Параметры.Ссылка);
	ЗапросВыработка.УстановитьПараметр("Период",                          Запрос.Параметры.Период);
	ЗапросВыработка.УстановитьПараметр("Организация",                     Запрос.Параметры.Организация);
	ЗапросВыработка.УстановитьПараметр("ДатаНачала",                      Запрос.Параметры.ДатаНачала);
	ЗапросВыработка.УстановитьПараметр("ДатаОкончания",                   Запрос.Параметры.ДатаОкончания);
	ЗапросВыработка.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Запрос.Параметры.КоэффициентПересчетаВВалютуУПР);
	ЗапросВыработка.УстановитьПараметр("Сводно",                          Запрос.Параметры.Сводно);
	ЗапросВыработка.УстановитьПараметр("ПроводкиПоРаботникам",            Запрос.Параметры.ПроводкиПоРаботникам);
	ЗапросВыработка.УстановитьПараметр("ВалютаРегл",                      Запрос.Параметры.ВалютаРегл);
	
	КоэффициентПересчетаВВалютуУПР = Запрос.Параметры.КоэффициентПересчетаВВалютуУПР;
	
	МассивРезультатов = ЗапросВыработка.ВыполнитьПакет();
	
	ТаблицаВыработкаСотрудников   = НоваяТаблицаВыработкаСотрудников();
	ВидыВзносов                   = ВидыСуммВзносов();
	БазаРаспределения             = МассивРезультатов[3].Выгрузить();
	СтрокиКРаспределению          = МассивРезультатов[4].Выгрузить();
	ПараметрыСпособовОтражения    = МассивРезультатов[5].Выбрать();
	
	БазаРаспределения.Индексы.Добавить("Сотрудник, Организация, Подразделение, СпособОтражения");
	
	СтруктураОтбора = Новый Структура("Сотрудник, Организация, Подразделение, СпособОтражения");
	
	Для Каждого Строка Из СтрокиКРаспределению Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Строка);
		
		СтрокиБазы = БазаРаспределения.НайтиСтроки(СтруктураОтбора);
		
		МассивКоэффициентов = Новый Массив;
		
		Для Каждого СтрокаБазы Из СтрокиБазы Цикл
			МассивКоэффициентов.Добавить(СтрокаБазы.Коэффициент);
		КонецЦикла;
		
		Для Каждого ВидВзноса Из ВидыВзносов Цикл
			
			МассивРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка[ВидВзноса.Представление], МассивКоэффициентов);
			
			Если МассивРаспределения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Индекс = 0 По СтрокиБазы.Количество()-1 Цикл
				
				СтрокаБазы = СтрокиБазы[Индекс];
				
				ПараметрыСтроки = ПараметрыРаспределенияСтроки(ВидВзноса, СтрокаБазы, ПараметрыСпособовОтражения);
				
				НоваяСтрока = ТаблицаВыработкаСотрудников.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБазы); // производственная аналитика
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыСтроки); // аналитика расходов в соответствии со способами отражения
				
				НоваяСтрока.ВидОперацииПоЗарплате = Строка.ВидОперации;
				Если ЗначениеЗаполнено(НоваяСтрока.ТипНалога) Тогда
					НоваяСтрока.Налог				  = Строка.Налог;
				КонецЕсли;
				
				НоваяСтрока.Сумма = 0;
				НоваяСтрока.СуммаРегл = 0;
				
				Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства
					ИЛИ Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства Тогда
					// Отражаем суммы только в НУ
				ИначеЕсли МассивРаспределения <> Неопределено Тогда
					НоваяСтрока.Сумма = Окр(МассивРаспределения[Индекс]*КоэффициентПересчетаВВалютуУПР, 2);
					НоваяСтрока.СуммаРегл = МассивРаспределения[Индекс];
				КонецЕсли;
				
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КолонкиГруппировок = "";
	КолонкиСуммирования = "Сумма, СуммаРегл";
	
	Для Каждого Колонка Из ТаблицаВыработкаСотрудников.Колонки Цикл
		Если СтрНайти(КолонкиСуммирования, Колонка.Имя) = 0 Тогда
			КолонкиГруппировок = КолонкиГруппировок + Колонка.Имя + ",";
		КонецЕсли;
	КонецЦикла;
	КолонкиГруппировок = Лев(КолонкиГруппировок, СтрДлина(КолонкиГруппировок) - 1);
	ТаблицаВыработкаСотрудников.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	Запрос.УстановитьПараметр("ТаблицаВыработкаСотрудников", ТаблицаВыработкаСотрудников);
	
КонецПроцедуры

#КонецОбласти

Функция ТекстЗапросаТаблицаТрудозатратыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТрудозатратыНезавершенногоПроизводства";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства  КАК ПартияПроизводства,	
	|	ВыработкаСотрудников.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап КАК Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот КАК ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции КАК ГруппаПродукции,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК Стоимость,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СтоимостьРегл
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов = НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства,	
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеЗарплатыВФинансовомУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеЗарплатыВФинансовомУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВЫБОР
	|		КОГДА Не &Сводно И &ПроводкиПоРаботникам ТОГДА ВыработкаСотрудников.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК ФизическоеЛицо,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.Налог,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.СчетУчета,
	|	ВыработкаСотрудников.Субконто1,
	|	ВыработкаСотрудников.Субконто2,
	|	ВыработкаСотрудников.Субконто3,
	|	ВыработкаСотрудников.СуммаРегл
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисленнаяЗарплатаИВзносы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТРасходыПоСтатьям.Подразделение КАК Подразделение,
	|	ВТРасходыПоСтатьям.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВТРасходыПоСтатьям.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ КАК НалоговоеНазначение,
	|	ВТРасходыПоСтатьям.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТРасходыПоСтатьям.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыПоСтатьям.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаСНДС,
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыПоСтатьям.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаБезНДС,
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыПоСтатьям.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаБезНДСУпр,
	|	СУММА(ВТРасходыПоСтатьям.Сумма) КАК СуммаСНДСРегл,
	|	СУММА(ВТРасходыПоСтатьям.Сумма) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК ВТРасходыПоСтатьям
	|ГДЕ
	|	ВТРасходыПоСтатьям.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы))
	|	И ВТРасходыПоСтатьям.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И ВТРасходыПоСтатьям.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРасходыПоСтатьям.Подразделение,
	|	ВТРасходыПоСтатьям.НаправлениеДеятельности,
	|	ВТРасходыПоСтатьям.СтатьяРасходов,
	|	ВТРасходыПоСтатьям.АналитикаРасходов,
	|	ВЫБОР КОГДА ВТРасходыПоСтатьям.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка по прочим работам
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ КАК НалоговоеНазначение,
	|	ВыработкаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаСНДС,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаБезНДС,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаБезНДСУпр,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаСНДСРегл,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И ВыработкаСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисленнаяЗарплатаИВзносы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТРасходыПоСтатьям.Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ВТРасходыПоСтатьям.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТРасходыПоСтатьям.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)	КАК АналитикаУчетаПартий,
	|	ВТРасходыПоСтатьям.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВТРасходыПоСтатьям.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ КАК НалоговоеНазначение,	
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыПоСтатьям.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Стоимость,
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыПоСтатьям.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	СУММА(ВТРасходыПоСтатьям.Сумма) КАК СтоимостьРегл,
	|	0 КАК ВременнаяРазница,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК ВТРасходыПоСтатьям
	|ГДЕ
	|	ВТРасходыПоСтатьям.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы))
	|	И ВТРасходыПоСтатьям.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРасходыПоСтатьям.Подразделение,
	|	ВТРасходыПоСтатьям.НаправлениеДеятельности,
	|	ВТРасходыПоСтатьям.СтатьяРасходов,
	|	ВТРасходыПоСтатьям.АналитикаРасходов,
	|	ВЫБОР КОГДА ВТРасходыПоСтатьям.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка по прочим работам
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ВыработкаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)	КАК АналитикаУчетаПартий,
	|	ВыработкаСотрудников.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ КАК НалоговоеНазначение,	
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	СУММА(ВыработкаСотрудников.Сумма) КАК Стоимость,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СтоимостьРегл,
	|	0 КАК ВременнаяРазница,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И ВыработкаСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	ИНАЧЕ
	|		&НалоговоеНазначениеОрганизации
	|	КОНЕЦ";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисленнаяЗарплатаИВзносы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	// начисленная зарплата на расходы
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеЗаработнойПлаты)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ                                             КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаРасходов,
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		НачисленнаяЗарплата.ТипНалога
	|	КОНЕЦ                                             КАК АналитикаАктивовПассивов,
	|	НачисленнаяЗарплата.Подразделение                 КАК КорПодразделение,
	|	НачисленнаяЗарплата.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НачисленнаяЗарплата.СтатьяРасходов                КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаДоходов,
	|	НачисленнаяЗарплата.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаАктивовПассивов,

	|	СУММА(ВЫРАЗИТЬ(НачисленнаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(НачисленнаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(НачисленнаяЗарплата.Сумма) КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|	
	|	НачисленнаяЗарплата.ВидОперации КАК ВидОперацииПоЗарплате
	|	
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплата
	|ГДЕ
	|	НачисленнаяЗарплата.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства))
	|	И НачисленнаяЗарплата.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|	И НачисленнаяЗарплата.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплата.Подразделение,
	|	НачисленнаяЗарплата.ТипНалога,
	|	НачисленнаяЗарплата.СтатьяРасходов,
	|	НачисленнаяЗарплата.АналитикаРасходов,
	|	НачисленнаяЗарплата.НаправлениеДеятельности,
	|	НачисленнаяЗарплата.ВидОперации
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка на расходы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеЗаработнойПлаты)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ                                               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                        КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ВыработкаСотрудников.ТипНалога
	|	КОНЕЦ                                               КАК АналитикаАктивовПассивов,

	|	ВыработкаСотрудников.Подразделение                  КАК КорПодразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности        КАК КорНаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяРасходов                 КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаДоходов,
	|	ВыработкаСотрудников.АналитикаРасходов              КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаАктивовПассивов,

	|	СУММА(ВыработкаСотрудников.Сумма) КАК Сумма,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаУпр,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|	
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате КАК ВидОперацииПоЗарплате
	|	
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|	И ВыработкаСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате
	|              
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка на Производство
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеСдельнойЗаработнойПлаты)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСоСдельнойЗаработнойПлаты)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ                                               КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	ВыработкаСотрудников.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ВыработкаСотрудников.ТипНалога
	|	КОНЕЦ                                               КАК АналитикаАктивовПассивов,
	|	                                      
	|	ВыработкаСотрудников.Подразделение                  КАК КорПодразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности        КАК КорНаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаДоходов,
	|	ВыработкаСотрудников.АналитикаРасходов              КАК КорАналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|       &Организация
	|	ИНАЧЕ
	|		ВыработкаСотрудников.ТипНалога
	|	КОНЕЦ КАК КорАналитикаАктивовПассивов,
	|	
	|	СУММА(ВыработкаСотрудников.Сумма) КАК Сумма,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаУпр,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|	
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате КАК ВидОперацииПоЗарплате
	|	
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов = НЕОПРЕДЕЛЕНО
	|   ИЛИ ВыработкаСотрудников.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|   ИЛИ ВыработкаСотрудников.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// начисленная зарплата на прочие активы и пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеЗаработнойПлаты)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ                                                  КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                           КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                           КАК АналитикаРасходов,
	|	ВЫБОР КОГДА НачисленнаяЗарплата.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		НачисленнаяЗарплата.ТипНалога
	|	КОНЕЦ                                                  КАК АналитикаАктивовПассивов,

	|	НачисленнаяЗарплата.Подразделение                      КАК КорПодразделение,
	|	НачисленнаяЗарплата.НаправлениеДеятельности            КАК КорНаправлениеДеятельности,
	|	НачисленнаяЗарплата.СтатьяРасходов                     КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорАналитикаРасходов,
	|	НачисленнаяЗарплата.АналитикаАктивовПассивов           КАК КорАналитикаАктивовПассивов,

	|	СУММА(ВЫРАЗИТЬ(НачисленнаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(НачисленнаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(НачисленнаяЗарплата.Сумма) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	НачисленнаяЗарплата.ВидОперации КАК ВидОперацииПоЗарплате
	|
	|	
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплата
	|ГДЕ
	|	НачисленнаяЗарплата.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|											ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства))
	|	И НачисленнаяЗарплата.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И НачисленнаяЗарплата.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплата.ТипНалога,
	|	НачисленнаяЗарплата.Подразделение,
	|	НачисленнаяЗарплата.НаправлениеДеятельности,
	|	НачисленнаяЗарплата.СтатьяРасходов,
	|	НачисленнаяЗарплата.АналитикаАктивовПассивов,
	|	НачисленнаяЗарплата.ВидОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка на прочие активы и пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеЗаработнойПлаты)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,
	|
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|	КОНЕЦ                                                       КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                                КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                                КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВыработкаСотрудников.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ВыработкаСотрудников.ТипНалога
	|	КОНЕЦ                                                       КАК АналитикаАктивовПассивов,

	|	ВыработкаСотрудников.Подразделение                          КАК КорПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)   КАК КорНаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяРасходов                         КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                                КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                                КАК КорАналитикаРасходов,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов               КАК КорАналитикаАктивовПассивов,

	|	СУММА(ВыработкаСотрудников.Сумма) КАК Сумма,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК СуммаУпр,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате КАК ВидОперацииПоЗарплате
	|
	|	
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И ВыработкаСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// удержанная зарплата на прочие активы и пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеИзЗарплатыСотрудника) КАК ХозяйственнаяОперация,
	|	
	|	&Организация КАК Организация,
	
	|	УдержаннаяЗарплата.ПодразделениеПредприятия    КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	УдержаннаяЗарплата.СтатьяАктивовПассивов       КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                   КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                   КАК АналитикаРасходов,
	|	УдержаннаяЗарплата.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК КорПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                   КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                   КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                   КАК КорАналитикаАктивовПассивов,
	
	|	СУММА(ВЫРАЗИТЬ(УдержаннаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(УдержаннаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(УдержаннаяЗарплата.Сумма) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	УдержаннаяЗарплата.ВидОперации КАК ВидОперацииПоЗарплате
	|	
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК УдержаннаяЗарплата
	|ГДЕ
	|	УдержаннаяЗарплата.Ссылка = &Ссылка
	|	И УдержаннаяЗарплата.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм),
	|										   ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок))
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	УдержаннаяЗарплата.ПодразделениеПредприятия,
	|	УдержаннаяЗарплата.СтатьяАктивовПассивов,
	|	УдержаннаяЗарплата.АналитикаАктивовПассивов,
	|	УдержаннаяЗарплата.ВидОперации
	|
	// проценты по займам сотрудникам.
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным) КАК ХозяйственнаяОперация,
	|
	|	&Организация КАК Организация,
	
	|	ПроцентыПоЗаймам.ПодразделениеПредприятия           КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                                        КАК НаправлениеДеятельности,
	|	ПроцентыПоЗаймам.СтатьяДоходов                      КАК Статья,
	|	ПроцентыПоЗаймам.АналитикаДоходов                   КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК АналитикаАктивовПассивов,
	
	|	ПроцентыПоЗаймам.ПодразделениеПредприятия           КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                        КАК КорАналитикаАктивовПассивов,
	
	|	СУММА(ВЫРАЗИТЬ(ПроцентыПоЗаймам.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(ПроцентыПоЗаймам.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(ПроцентыПоЗаймам.Сумма) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоПроцентовПоЗайму) КАК ВидОперацииПоЗарплате
	|
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК ПроцентыПоЗаймам
	|ГДЕ
	|	ПроцентыПоЗаймам.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроцентыПоЗаймам.ПодразделениеПредприятия,
	|	ПроцентыПоЗаймам.СтатьяДоходов,
	|	ПроцентыПоЗаймам.АналитикаДоходов
	|
	// возврат излишне выплаченных суммы вследствие счетных ошибок на доходы
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеИзЗарплатыСотрудника) КАК ХозяйственнаяОперация,
	|
	|	&Организация КАК Организация,
	
	|	УдержаннаяЗарплата.ПодразделениеПредприятия   КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	УдержаннаяЗарплата.СтатьяДоходов              КАК Статья,
	|	УдержаннаяЗарплата.АналитикаДоходов           КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                  КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                  КАК АналитикаАктивовПассивов,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК КорПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                  КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                  КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                  КАК КорАналитикаАктивовПассивов,
	
	|	СУММА(ВЫРАЗИТЬ(УдержаннаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(УдержаннаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(УдержаннаяЗарплата.Сумма) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок) КАК ВидОперацииПоЗарплате
	|
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК УдержаннаяЗарплата
	|ГДЕ
	|	УдержаннаяЗарплата.Ссылка = &Ссылка
	|	И УдержаннаяЗарплата.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок)
	|
	|СГРУППИРОВАТЬ ПО
	|	УдержаннаяЗарплата.ПодразделениеПредприятия,
	|	УдержаннаяЗарплата.СтатьяДоходов,
	|	УдержаннаяЗарплата.АналитикаДоходов
	|
	// НДФЛ корреспондирует со статьей активов\пассивов оплата труда
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаРасходов,
	|	НачисленныйНДФЛ.ТипНалога                         КАК АналитикаАктивовПассивов,

	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК КорПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаАктивовПассивов,

	|	СУММА(ВЫРАЗИТЬ(НачисленныйНДФЛ.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(НачисленныйНДФЛ.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(НачисленныйНДФЛ.Сумма) КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК СуммаВВалюте,
	|
	|	НачисленныйНДФЛ.ВидОперации КАК ВидОперацииПоЗарплате
	|
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК НачисленныйНДФЛ
	|ГДЕ
	|	НачисленныйНДФЛ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленныйНДФЛ.ВидОперации,
	|	НачисленныйНДФЛ.ТипНалога";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДенежныеСредстваДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Значение(Перечисление.ХозяйственныеОперации.УдержаниеНеизрасходованныхПодотчетныхСумм) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	УдержаннаяЗарплата.ПодразделениеПредприятия КАК Подразделение,
	|
	|	УдержаннаяЗарплата.ФизическоеЛицо КАК ДенежныеСредства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяДвиженияДенежныхСредств,
	|	&ВалютаРегл КАК Валюта,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|
	|	УдержаннаяЗарплата.Сумма КАК Сумма,
	|	УдержаннаяЗарплата.Сумма КАК СуммаРегл,
	|	УдержаннаяЗарплата.Сумма КАК СуммаВВалюте,
	|
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДоходовРасходов
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК УдержаннаяЗарплата
	|	
	|ГДЕ
	|	УдержаннаяЗарплата.Ссылка = &Ссылка
	|	И УдержаннаяЗарплата.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисленнаяЗарплатаИВзносы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтНачисленнаяЗарплатаИВзносы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	// постатейная зарплата к выплате на пассивы
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК Строки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	И Строки.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства))
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// взносы с постатейной зарплаты к перечислению на пассивы
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	Строки.ТипНалога,
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК Строки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И Строки.ТипНалога <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	И Строки.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства))
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.ТипНалога
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// сдельная зарплата к выплате на пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|
	|ИМЕЮЩИЕ
	|	СУММА(Строки.Сумма) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// взносы со сдельной зарплаты к перечислению на пассивы
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	Строки.ТипНалога,
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.ТипНалога
	|
	|ИМЕЮЩИЕ
	|	СУММА(Строки.Сумма) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// зарплата и взносы на активы / пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	Строки.Подразделение КАК Подразделение,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Строки.СтатьяРасходов КАК Статья,
	|	Строки.АналитикаАктивовПассивов КАК Аналитика,
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК Строки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И Строки.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|	И Строки.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства),
	|								ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства))
	|СГРУППИРОВАТЬ ПО
	|	Строки.СтатьяРасходов,
	|	Строки.АналитикаАктивовПассивов,
	|	Строки.Подразделение,
	|	Строки.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// выработка на активы / пассивы
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяРасходов КАК Статья,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов КАК Аналитика,
	|	СУММА(ВыработкаСотрудников.Сумма) КАК Сумма
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И ВыработкаСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(ВыработкаСотрудников.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// уменьшаем сумму по оплате труда на величину удержаний
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда),
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ВЫРАЗИТЬ(Удержанния.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК Удержанния
	|ГДЕ
	|	Удержанния.Ссылка = &Ссылка
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(Удержанния.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// уменьшаем сумму по оплате труда на величину НДФЛ
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда),
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ВЫРАЗИТЬ(НачисленныйНДФЛ.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК НачисленныйНДФЛ
	|ГДЕ
	|	НачисленныйНДФЛ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫРАЗИТЬ(НачисленныйНДФЛ.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// удержания
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	Удержанния.ПодразделениеПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	Удержанния.СтатьяАктивовПассивов,
	|	Удержанния.АналитикаАктивовПассивов,
	|	СУММА(ВЫРАЗИТЬ(Удержанния.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК Удержанния
	|ГДЕ
	|	Удержанния.Ссылка = &Ссылка
	|	И Удержанния.ВидОперации НЕ В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм),
	|								   ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок))
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	Удержанния.СтатьяАктивовПассивов,
	|	Удержанния.АналитикаАктивовПассивов,
	|	Удержанния.ПодразделениеПредприятия,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// НДФЛ
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	НДФЛ.ТипНалога,
	|	СУММА(ВЫРАЗИТЬ(НДФЛ.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК НДФЛ
	|ГДЕ
	|	НДФЛ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛ.ТипНалога
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Начисленные проценты по займам
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Организация,
	|	Проценты.ПодразделениеПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные),
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ВЫРАЗИТЬ(Проценты.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК Проценты
	|ГДЕ
	|	Проценты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Проценты.ПодразделениеПредприятия
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстПрочаяВыручка =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	ТаблицаДоходы.ПодразделениеПредприятия КАК Подразделение,
	|	ТаблицаДоходы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным) КАК ХозяйственнаяОперация,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	0 КАК Количество,
	|	ТаблицаДоходы.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ВыручкаБезНДСУпр,
	|	ТаблицаДоходы.Сумма КАК ВыручкаБезНДСРегл,
	|	0 КАК НДСУпр,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КАК НалогообложениеНДС,
	|	&ВалютаРегл КАК ВалютаВзаиморасчетов,
	|	ТаблицаДоходы.Сумма КАК СуммаВзаиморасчетов
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК ТаблицаДоходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	ТаблицаДоходы.ПодразделениеПредприятия КАК Подразделение,
	|	ТаблицаДоходы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	0 КАК Количество,
	|	ТаблицаДоходы.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ВыручкаБезНДСУпр,
	|	ТаблицаДоходы.Сумма КАК ВыручкаБезНДСРегл,
	|	0 КАК НДСУпр,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КАК НалогообложениеНДС,
	|	&ВалютаРегл КАК ВалютаВзаиморасчетов,
	|	ТаблицаДоходы.Сумма КАК СуммаВзаиморасчетов
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.ВидОперации = Значение(Перечисление.ВидыОперацийПоЗарплате.ВозвратИзлишнеВыплаченныхСуммВследствиеСчетныхОшибок)
	|
	|";
	
	ДоходыИРасходыСервер.ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры, ТекстПрочаяВыручка);
	
КонецПроцедуры


Функция ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваУПодотчетныхЛиц";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	УдержаннаяЗарплата.ФизическоеЛицо КАК ПодотчетноеЛицо,
	|	&ВалютаРегл КАК Валюта,
	|
	|	УдержаннаяЗарплата.ЦельВыдачи КАК ЦельВыдачи,
	|	
	|	УдержаннаяЗарплата.Сумма КАК Сумма,
	|	УдержаннаяЗарплата.Сумма КАК СуммаРегл,
	|	ВЫРАЗИТЬ(УдержаннаяЗарплата.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК СуммаУпр,
	|	УдержаннаяЗарплата.Сумма КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеНеизрасходованныхПодотчетныхСумм) КАК ХозяйственнаяОперация
	|	
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК УдержаннаяЗарплата
	|	
	|ГДЕ
	|	УдержаннаяЗарплата.Ссылка = &Ссылка
	|	И УдержаннаяЗарплата.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.УдержаниеНеизрасходованныхПодотчетныхСумм)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата						 КАК Период,
	|	&Организация 				 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&Дата	КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|		ПО СпособыОтражения.Ссылка = Строки.СпособОтраженияЗарплатыВБухучете
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И СпособыОтражения.ЕстьПрочиеОперации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#Область СлужебныеПроцедурыИФункцииПроведения


// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаТрудозатратыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеЗарплатыВФинансовомУчете(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры);
		
		
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры);
		ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ИнтеграцияБЗК");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОтражениеЗарплатыВФинансовомУчете");
	МеханизмыДокумента.Добавить("РасчетыСПодотчетниками");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНЗП");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

Функция БазаРаспределенияТрудозатрат()
	
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.ПодразделениеПредприятия         КАК Подразделение,
	|	ДД.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	ДД.ФизическоеЛицо                   КАК Сотрудник,
	|	&Организация                        КАК Организация
	|ПОМЕСТИТЬ ВТСотрудникиСдельная
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И НЕ &Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.ПодразделениеПредприятия,
	|	ДД.СпособОтраженияЗарплатыВБухучете,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	&Организация
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И &Сводно
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СпособОтражения,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковСотрудники.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	0 КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО КАК Этап,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка) КАК ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения КАК СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты КАК СпособОтраженияПрочихРабот,
	|	СУММА(ВыработкаСотрудниковСотрудники.Сумма * ВыработкаСотрудниковВидыРабот.Сумма / ВыработкаСотрудников.СуммаДокумента
	|		* &КоэффициентПересчетаВВалютуУПР) КАК НормативнаяСтоимость,
	|	&Организация КАК Организация,
	|	""ПрочиеРасходыАктивыПассивы"" КАК ИдентификаторНабора,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	0 КАК Количество,
	|	ВЫБОР ИСТИНА
	//++ НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗаказНаРемонт.Ссылка.ОбъектЭксплуатации
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	ЛОЖЬ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ПОМЕСТИТЬ ВТТрудозатраты
	|ИЗ
	|	Документ.ВыработкаСотрудников КАК ВыработкаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.ВидыРабот КАК ВыработкаСотрудниковВидыРабот
	|			ПО ВыработкаСотрудников.Ссылка = ВыработкаСотрудниковВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.Работники КАК ВыработкаСотрудниковСотрудники
	|			ПО ВыработкаСотрудниковВидыРабот.Ссылка = ВыработкаСотрудниковСотрудники.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|			ПО (&Сводно ИЛИ ВыработкаСотрудниковСотрудники.Работник = ВТСотрудникиСдельная.Сотрудник)
	|			И ВыработкаСотрудников.Подразделение = ВТСотрудникиСдельная.Подразделение
	|			И ВыработкаСотрудников.Организация = ВТСотрудникиСдельная.Организация
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Трудозатраты КАК ЗаказНаРемонт
	|		ПО (ВыработкаСотрудниковВидыРабот.Распоряжение = ЗаказНаРемонт.Ссылка)
	|			И (ВыработкаСотрудниковВидыРабот.КодСтрокиРаспоряжения = ЗаказНаРемонт.КодСтроки)
	//-- НЕ УТКА
	|ГДЕ
	|	ВыработкаСотрудников.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт))
	|	И ВыработкаСотрудников.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВыработкаСотрудников.Проведен
	|	И ВыработкаСотрудников.Организация = &Организация
	|	И ВыработкаСотрудников.Автораспределение
	|	И НЕ ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты.ОплатаСдельныхРабот
	|
	|СГРУППИРОВАТЬ ПО
	//++ НЕ УТКА
	|	ЗаказНаРемонт.Ссылка.ОбъектЭксплуатации,
	//-- НЕ УТКА
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковСотрудники.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВыработкаСотрудников.Ссылка,
	|	ВыработкаСотрудников.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковВидыРабот.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	0 КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО КАК Этап,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка) КАК ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения КАК СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты КАК СпособОтраженияПрочихРабот,
	|	СУММА(ВыработкаСотрудниковВидыРабот.Сумма
	|		* &КоэффициентПересчетаВВалютуУПР) КАК НормативнаяСтоимость,
	|	&Организация КАК Организация,
	|	""ПрочиеРасходыАктивыПассивы"" КАК ИдентификаторНабора,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	0 КАК Количество,
	|	ВЫБОР ИСТИНА
	//++ НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗаказНаРемонт.Ссылка.ОбъектЭксплуатации
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	ЛОЖЬ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ИЗ
	|	Документ.ВыработкаСотрудников КАК ВыработкаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.ВидыРабот КАК ВыработкаСотрудниковВидыРабот
	|			ПО ВыработкаСотрудников.Ссылка = ВыработкаСотрудниковВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|		ПО (&Сводно ИЛИ ВыработкаСотрудниковВидыРабот.Работник = ВТСотрудникиСдельная.Сотрудник)
	|			И ВыработкаСотрудников.Подразделение = ВТСотрудникиСдельная.Подразделение
	|			И ВыработкаСотрудников.Организация = ВТСотрудникиСдельная.Организация
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Трудозатраты КАК ЗаказНаРемонт
	|		ПО (ВыработкаСотрудниковВидыРабот.Распоряжение = ЗаказНаРемонт.Ссылка)
	|			И (ВыработкаСотрудниковВидыРабот.КодСтрокиРаспоряжения = ЗаказНаРемонт.КодСтроки)
	//-- НЕ УТКА
	|ГДЕ
	|	ВыработкаСотрудников.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт))
	|	И ВыработкаСотрудников.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВыработкаСотрудников.Проведен
	|	И ВыработкаСотрудников.Организация = &Организация
	|	И НЕ ВыработкаСотрудников.Автораспределение
	|	И НЕ ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты.ОплатаСдельныхРабот
	|
	|СГРУППИРОВАТЬ ПО
	//++ НЕ УТКА
	|	ЗаказНаРемонт.Ссылка.ОбъектЭксплуатации,
	//-- НЕ УТКА
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковВидыРабот.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВыработкаСотрудников.Ссылка,
	|	ВыработкаСотрудников.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТрудозатратыНЗП.Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ТрудозатратыНЗП.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.ПартияПроизводства,
	|	ТрудозатратыНЗП.ЗаказНаПроизводство,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказа.Ссылка ЕСТЬ NULL ТОГДА
	|			ПродукцияЗаказа.Назначение.НаправлениеДеятельности
	//-- НЕ УТКА
	|		КОГДА ИСТИНА ТОГДА
	|			ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|				ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТрудозатратыНЗП.КодСтрокиПродукция,
	|	ТрудозатратыНЗП.Этап,
	|	ТрудозатратыНЗП.СтатьяКалькуляции,
	|	ТрудозатратыНЗП.ВидРабот,
	|	ТрудозатратыНЗП.ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	СУММА(ТрудозатратыНЗП.НормативнаяСтоимость),
	|	ТрудозатратыНЗП.Организация,
	|	""ТрудозатратыНезавершенногоПроизводства"",
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
	|	ТрудозатратыНЗП.Количество,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА НЕ ТрудозатратыНЗП.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказа.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	//-- НЕ УТКА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|		ПО (&Сводно ИЛИ ТрудозатратыНЗП.Сотрудник = ВТСотрудникиСдельная.Сотрудник)
	|			И ТрудозатратыНЗП.Организация = ВТСотрудникиСдельная.Организация
	|			И ТрудозатратыНЗП.Подразделение = ВТСотрудникиСдельная.Подразделение
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ПродукцияЗаказа
	|		ПО ТрудозатратыНЗП.ЗаказНаПроизводство = ПродукцияЗаказа.Ссылка
	|			И ТрудозатратыНЗП.КодСтрокиПродукция = ПродукцияЗаказа.КодСтроки
	//-- НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ТрудозатратыНЗП.ПартияПроизводства
	|
	|ГДЕ
	|	ТрудозатратыНЗП.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТрудозатратыНЗП.Организация В
	|			(ВЫБРАТЬ
	|				Т.Организация
	|			ИЗ
	|				ВТСотрудникиСдельная КАК Т)
	|	И ТрудозатратыНЗП.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТрудозатратыНЗП.НормативнаяСтоимость > 0
	|	И ТрудозатратыНЗП.Регистратор Ссылка Документ.ВыработкаСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНЗП.Организация,
	|	ТрудозатратыНЗП.Подразделение,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ТрудозатратыНЗП.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.ПартияПроизводства,
	|	ТрудозатратыНЗП.ЗаказНаПроизводство,
	|	ТрудозатратыНЗП.Этап,
	|	ТрудозатратыНЗП.СтатьяКалькуляции,
	|	ТрудозатратыНЗП.ВидРабот,
	|	ТрудозатратыНЗП.ГруппаПродукции,
	|	ТрудозатратыНЗП.КодСтрокиПродукция,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказа.Ссылка ЕСТЬ NULL ТОГДА
	|			ПродукцияЗаказа.Назначение.НаправлениеДеятельности
	//-- НЕ УТКА
	|		КОГДА ИСТИНА ТОГДА
	|			ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|				ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.Количество,
	|	ВЫБОР
	|		КОГДА НЕ ТрудозатратыНЗП.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказа.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	//-- НЕ УТКА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.СпособОтражения КАК СпособОтражения
	|ПОМЕСТИТЬ ВтСписокСпособовОтражения
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.СпособОтраженияПрочихРабот КАК СпособОтражения
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Реквизиты.БазаРаспределенияПоСдельнымРаботам = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|			ТОГДА ВТТрудозатраты.НормативнаяСтоимость
	|		КОГДА ВидыРабот.КратностьТрудоемкости > 0
	|			ТОГДА ВЫРАЗИТЬ(ВТТрудозатраты.Количество * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК ЧИСЛО(15,3))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Коэффициент,
	|	ВТТрудозатраты.Подразделение КАК Подразделение,
	|	ВТТрудозатраты.Сотрудник КАК Сотрудник,
	|	ВТТрудозатраты.ПартияПроизводства КАК ПартияПроизводства,
	|	ВТТрудозатраты.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВТТрудозатраты.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТТрудозатраты.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	ВТТрудозатраты.Этап КАК Этап,
	|	ВТТрудозатраты.ВидРабот КАК ВидРабот,
	|	ВТТрудозатраты.ГруппаПродукции,
	|	ВТТрудозатраты.Организация КАК Организация,
	|	ВТТрудозатраты.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
	|	ВТТрудозатраты.ИдентификаторНабора КАК ИдентификаторНабора,
	|	ВТТрудозатраты.Количество,
	|	ВТТрудозатраты.СпособОтражения,
	|	ВТТрудозатраты.СпособОтраженияПрочихРабот,
	|	ВТТрудозатраты.ОбъектЭксплуатации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОтдельнаяСтатьяКалькуляции И ВТТрудозатраты.ДоступноПереопределениеСтатьиКалькуляции
	|			ТОГДА Реквизиты.СтатьяКалькуляции
	|		ИНАЧЕ ВТТрудозатраты.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОтдельнаяСтатьяКалькуляцииВзносов И ВТТрудозатраты.ДоступноПереопределениеСтатьиКалькуляции
	|			ТОГДА Реквизиты.СтатьяКалькуляцииВзносов
	|		ИНАЧЕ ВТТрудозатраты.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляцииВзносов
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО ВТТрудозатраты.СпособОтражения = Реквизиты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРабот
	|			ПО ВТТрудозатраты.ВидРабот = ВидыРабот.Ссылка
	|
	|ГДЕ
	|	ВЫБОР
	|		КОГДА Реквизиты.БазаРаспределенияПоСдельнымРаботам = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|			ТОГДА ВТТрудозатраты.НормативнаяСтоимость
	|		КОГДА ВидыРабот.КратностьТрудоемкости > 0
	|			ТОГДА ВЫРАЗИТЬ(ВТТрудозатраты.Количество * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК ЧИСЛО(15,3))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начислено.НомерСтроки КАК НомерСтроки,
	|	Начислено.ФизическоеЛицо КАК Сотрудник,
	|	Начислено.ПодразделениеПредприятия КАК Подразделение,
	|	Начислено.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Начислено.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ Начислено.Сумма
	|	КОНЕЦ КАК Сумма,
	|	Начислено.ВзносыВсего КАК ВзносыВсего,
	|	Начислено.Налог КАК Налог,
	|	0 КАК Взносы,
	|	0 КАК СуммаНУ,
	|	0 КАК ВзносыНУ,
	|	0 КАК ФССНесчастныеСлучаиНУ,
	|	ЛОЖЬ КАК РаспределятьНУ,
	|	Начислено.ВидОперации
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК Начислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО Начислено.СпособОтраженияЗарплатыВБухучете = Реквизиты.Ссылка
	|ГДЕ
	|	Начислено.Ссылка = &Ссылка
	|	И Начислено.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И НЕ &Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начислено.НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	Начислено.ПодразделениеПредприятия,
	|	Начислено.СпособОтраженияЗарплатыВБухучете,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА Начислено.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ Начислено.Сумма
	|	КОНЕЦ КАК Сумма,
	|	Начислено.ВзносыВсего КАК ВзносыВсего,
	|	Начислено.Налог КАК Налог,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЛОЖЬ,
	|	Начислено.ВидОперации
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Начислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО Начислено.СпособОтраженияЗарплатыВБухучете = Реквизиты.Ссылка
	|ГДЕ
	|	Начислено.Ссылка = &Ссылка
	|	И Начислено.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И &Сводно
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстЗапроса = ТекстЗапроса +
	";
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция НоваяТаблицаВыработкаСотрудников()
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ОтражениеЗарплатыВФинансовомУчете.Период,
		|	ОтражениеЗарплатыВФинансовомУчете.Регистратор,
		|	ОтражениеЗарплатыВФинансовомУчете.НомерСтроки,
		|	ОтражениеЗарплатыВФинансовомУчете.Активность,
		|	ОтражениеЗарплатыВФинансовомУчете.Организация,
		|	ОтражениеЗарплатыВФинансовомУчете.Подразделение,
		|	ОтражениеЗарплатыВФинансовомУчете.ВидОперацииПоЗарплате,
		|	ОтражениеЗарплатыВФинансовомУчете.СтатьяРасходов,
		|	ОтражениеЗарплатыВФинансовомУчете.НаправлениеДеятельности,
		|	ОтражениеЗарплатыВФинансовомУчете.АналитикаРасходов,
		|	ОтражениеЗарплатыВФинансовомУчете.ВидРабот,
		|	ОтражениеЗарплатыВФинансовомУчете.ФизическоеЛицо КАК Сотрудник,
		|	ОтражениеЗарплатыВФинансовомУчете.ГруппаПродукции,
		|	ОтражениеЗарплатыВФинансовомУчете.ТипНалога,
		|	ОтражениеЗарплатыВФинансовомУчете.Налог,
		|	ОтражениеЗарплатыВФинансовомУчете.АналитикаАктивовПассивов,
		|	ОтражениеЗарплатыВФинансовомУчете.СчетУчета,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто1,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто2,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто3,
		|	ОтражениеЗарплатыВФинансовомУчете.СуммаРегл,
		|	0 КАК Сумма,
		|	НЗП.ЗаказНаПроизводство                               КАК ЗаказНаПроизводство,
		|	0                                                     КАК КодСтрокиПродукция,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)   КАК Этап,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)   КАК СтатьяКалькуляции,
		|	НЗП.ПартияПроизводства                                КАК ПартияПроизводства
		|ИЗ
		|	РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК ОтражениеЗарплатыВФинансовомУчете
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК НЗП
		|		ПО ИСТИНА";
	
	Результат = Запрос.Выполнить();
	
	Таблица = Результат.Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

Функция ПараметрыРаспределенияСтроки(ВидСуммы, СтрокаБазы, ДанныеСпособа)
	

    Параметры = Новый Структура("ИдентификаторНабора, ТипНалога");
	Параметры.ТипНалога       = ВидСуммы.ТипНалога;
	Если ВидСуммы.ТипНалога = Перечисления.ТипыНалогов.НачисленныйЕСВ Тогда
		Параметры.Вставить("СтатьяКалькуляции", СтрокаБазы.СтатьяКалькуляцииВзносов);
	КонецЕсли;	
	
	Если СтрокаБазы.ИдентификаторНабора = "ПрочиеРасходыАктивыПассивы" Тогда
		
		СтруктураОтбора = Новый Структура("СпособОтражения", СтрокаБазы.СпособОтражения);
		ДанныеСпособа.НайтиСледующий(СтруктураОтбора);
		
		Параметры.Вставить("СтатьяРасходов", ДанныеСпособа["СтатьяРасходов" + ВидСуммы.Постфикс]);
		
		Если ТипЗнч(Параметры.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			Параметры.ИдентификаторНабора = "ПрочиеРасходы";
			Параметры.Вставить("АналитикаРасходов",         ДанныеСпособа["АналитикаРасходов" + ВидСуммы.Постфикс]);
			
			ИмяАналитики = "АналитикаРасходов";
			
		Иначе
			Параметры.ИдентификаторНабора = "ПрочиеАктивыПассивы";
			Параметры.Вставить("АналитикаАктивовПассивов",  ДанныеСпособа["АналитикаАктивовПассивов" + ВидСуммы.Постфикс]);
			Параметры.Вставить("СчетУчета",                 ДанныеСпособа["СчетУчета" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто1",                 ДанныеСпособа["Субконто1" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто2",                 ДанныеСпособа["Субконто2" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто3",                 ДанныеСпособа["Субконто3" + ВидСуммы.Постфикс]);
			
			ИмяАналитики = "АналитикаАктивовПассивов";
			
			// автоматический подбор Субконто
			Для НомерСубконто = 1 По 3 Цикл
				
				ИмяСубконто = "Субконто" + Строка(НомерСубконто);
				
				Если Не ЗначениеЗаполнено(Параметры[ИмяСубконто]) Тогда
					Если ТипЗнч(Параметры[ИмяСубконто]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
						Параметры[ИмяСубконто] = СтрокаБазы.Сотрудник;
					ИначеЕсли ТипЗнч(Параметры[ИмяСубконто]) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
						Параметры[ИмяСубконто] = СтрокаБазы.ОбъектЭксплуатации;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// автоматический подбор аналитики
		Если Не ЗначениеЗаполнено(Параметры[ИмяАналитики]) Тогда
			
			Если ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.Организации") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Организация;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Сотрудник;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Подразделение;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.ОбъектЭксплуатации;
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСпособа.Сбросить();
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных; 
	
КонецФункции

#КонецОбласти

#КонецОбласти

//++ НЕ УТКА
#Область ПроведениеМеждународныйУчет

Процедура ЗаполнитьТаблицуДокументаКОтражениюВМеждународномУчете(ТаблицаДокумента, Документ) Экспорт
	
	НоваяСтрока = ТаблицаДокумента.Добавить();
	НоваяСтрока.Регистратор = Документ.Ссылка;
	НоваяСтрока.Организация = Документ.Организация;
	НоваяСтрока.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтражениеНДФЛ;
	
КонецПроцедуры

Функция ТекстОтраженияВМеждународномУчете() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика,
	|	МАКСИМУМ(КурсыВалютыРегл.Период) КАК ДатаКурсаВалютыРегл,
	|	МАКСИМУМ(КурсыВалютыФункциональной.Период) КАК ДатаКурсаВалютыФункциональной,
	|	МАКСИМУМ(КурсыВалютыПредставления.Период) КАК ДатаКурсаВалютыПредставления
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют КАК КурсыВалютыРегл
	|	ПО 
	|		&ВалютаРегл = КурсыВалютыРегл.Валюта
	|		И ДокументыКОтражению.Период >= КурсыВалютыРегл.Период
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют КАК КурсыВалютыФункциональной
	|	ПО 
	|		(&ВалютаФункциональная = КурсыВалютыФункциональной.Валюта)
	|		И ДокументыКОтражению.Период >= КурсыВалютыФункциональной.Период
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют КАК КурсыВалютыПредставления
	|	ПО 
	|		(&ВалютаПредставления = КурсыВалютыПредставления.Валюта)
	|		И ДокументыКОтражению.Период >= КурсыВалютыПредставления.Период
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Регистратор) = ТИП(Документ.ОтражениеЗарплатыВФинансовомУчете)
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтражению.Период,
	|	ДокументыКОтражению.Регистратор,
	|	ДокументыКОтражению.Организация,
	|	ДокументыКОтражению.УчетнаяПолитика
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблоныПроводок.Ссылка КАК ШаблонПроводки,
	|	ШаблоныПроводок.СчетДебетаПоУмолчанию КАК СчетДебетаПоУмолчанию,
	|	ШаблоныПроводок.СчетКредитаПоУмолчанию КАК СчетКредитаПоУмолчанию,
	|	ШаблоныПроводок.ИзменятьЗнакОперацииПриОтраженииВУчете КАК ИзменятьЗнакОперации,
	|	ПравилаОтражения.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НастройкиОтражения
	|ИЗ 
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|	ПО
	|		ШаблоныПроводок.Ссылка = ПравилаОтражения.ШаблонПроводки
	|ГДЕ
	|	ШаблоныПроводок.Операция = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОтражениеНДФЛ)
	|	И НЕ ШаблоныПроводок.ЭтоГруппаШаблонов
	|	И НЕ ПравилаОтражения.УчетнаяПолитика ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Период КАК Период,
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ДанныеДокумента.Сумма * КурсыВалютыРегл.Курс * КурсыВалютыФункциональной.Кратность 
	|		/ (КурсыВалютыРегл.Кратность * КурсыВалютыФункциональной.Курс) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ДанныеДокумента.Сумма * КурсыВалютыРегл.Курс * КурсыВалютыПредставления.Кратность 
	|		/ (КурсыВалютыРегл.Кратность * КурсыВалютыПредставления.Курс) КАК ЧИСЛО(15, 2)) КАК СуммаПредставления,
	|
	|	ДанныеДокумента.ПодразделениеПредприятия КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	НастройкиОтражения.ШаблонПроводки КАК ШаблонПроводки,
	|	НастройкиОтражения.СчетДебетаПоУмолчанию КАК СчетДт,
	|	НастройкиОтражения.СчетКредитаПоУмолчанию КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютнаяСуммаДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютнаяСуммаКт
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.КурсыВалют КАК КурсыВалютыРегл
	|		ПО
	|			&ВалютаРегл = КурсыВалютыРегл.Валюта
	|			И ТаблицаДокументов.ДатаКурсаВалютыРегл = КурсыВалютыРегл.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.КурсыВалют КАК КурсыВалютыФункциональной
	|		ПО 
	|			(&ВалютаФункциональная = КурсыВалютыФункциональной.Валюта)
	|			И ТаблицаДокументов.ДатаКурсаВалютыФункциональной = КурсыВалютыФункциональной.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.КурсыВалют КАК КурсыВалютыПредставления
	|		ПО 
	|			(&ВалютаПредставления = КурсыВалютыПредставления.Валюта)
	|			И ТаблицаДокументов.ДатаКурсаВалютыПредставления = КурсыВалютыПредставления.Период
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Регистратор
	|		И ДанныеДокумента.Ссылка.Организация = ТаблицаДокументов.Организация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		НастройкиОтражения КАК НастройкиОтражения
	|	ПО
	|		ТаблицаДокументов.УчетнаяПолитика = НастройкиОтражения.УчетнаяПолитика
	|ГДЕ
	|	ДанныеДокумента.Сумма <> 0
	|	И НЕ НастройкиОтражения.ШаблонПроводки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ
	|	ТаблицаДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ
	|	НастройкиОтражения
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПроверитьЗаполнениеПроводкиМеждународногоУчета(Проводка, Выборка) Экспорт
	
	// Проверок не предусмотрено
	Возврат;
	
КонецПроцедуры

#КонецОбласти
//-- НЕ УТКА

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

Процедура ДобавитьПолеПодразделениеПредприятия(ДанныеДляОтражения, СтруктураПараметров)
	
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("НачисленнаяЗарплатаИВзносы");
	ИменаТаблиц.Добавить("НачисленныйНДФЛ");
	ИменаТаблиц.Добавить("УдержаннаяЗарплата");
	
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл
		Если ДанныеДляОтражения[ИмяТаблицы].Колонки.Найти("ПодразделениеПредприятия") = Неопределено Тогда
			ДанныеДляОтражения[ИмяТаблицы].Колонки.Добавить(
			"ПодразделениеПредприятия", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
		КонецЕсли;
		
		КолонкаПодразделение = ДанныеДляОтражения[ИмяТаблицы].Колонки.Найти("Подразделение");
		Если КолонкаПодразделение <> Неопределено Тогда
			ДанныеДляОтражения[ИмяТаблицы].Колонки.Удалить(КолонкаПодразделение);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы.Свернуть("ВидОперации,  ПериодПринятияРасходов, ПодразделениеПредприятия,
		|Сотрудник, СпособОтраженияЗарплатыВБухучете, СтатьяРасходов, СтатьяФинансирования, ФизическоеЛицо,ГруппаУчетаНачислений,Налог", "Сумма, ВзносыВсего");
		
	ДанныеДляОтражения.НачисленныйНДФЛ.Свернуть("ВидОперации, КодДохода, СтатьяРасходов, СтатьяФинансирования, ФизическоеЛицо, ГруппаУчетаНачислений, ПодразделениеПредприятия, Сотрудник", "Сумма");
		
	ДанныеДляОтражения.УдержаннаяЗарплата.Свернуть("ВидОперации, Контрагент, ПодразделениеПредприятия, Сотрудник, СтатьяРасходов,
		|СтатьяФинансирования, ФизическоеЛицо, ГруппаУчетаНачислений", "Сумма");
		
	МассивСотрудников = ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы.ВыгрузитьКолонку("Сотрудник");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ДанныеДляОтражения.НачисленныйНДФЛ.ВыгрузитьКолонку("Сотрудник"));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ДанныеДляОтражения.УдержаннаяЗарплата.ВыгрузитьКолонку("Сотрудник"));
	
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСотрудников);
	
	// заполнение подразделений по кадровым данным сотрудников
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
		Ложь,
		МассивСотрудников,
		"МестоВСтруктуреПредприятия",
		КонецМесяца(СтруктураПараметров.ПериодРегистрации));
	
	ПодразделенияСотрудников = Новый Соответствие;
	Для Каждого Строка Из КадровыеДанные Цикл
		ПодразделенияСотрудников.Вставить(Строка.Сотрудник, Строка.МестоВСтруктуреПредприятия);
	КонецЦикла;
	
	Для Каждого Строка Из ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы Цикл
		
		Строка.ПодразделениеПредприятия = ПодразделенияСотрудников[Строка.Сотрудник];
	КонецЦикла;
	
	Для Каждого Строка Из ДанныеДляОтражения.НачисленныйНДФЛ Цикл
		Строка.ПодразделениеПредприятия = ПодразделенияСотрудников[Строка.Сотрудник];
	КонецЦикла;
	
	Для Каждого Строка Из ДанныеДляОтражения.УдержаннаяЗарплата Цикл
		Строка.ПодразделениеПредприятия = ПодразделенияСотрудников[Строка.Сотрудник];
	КонецЦикла;
	
	ЗаполнитьПодразделениеПредприятияНачисленоСдельно(ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы, СтруктураПараметров);
	
КонецПроцедуры

Процедура ЗаполнитьПодразделениеПредприятияНачисленоСдельно(НачисленнаяЗарплатаИВзносы, СтруктураПараметров)
	

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТрудозатратыНЗП.Подразделение КАК ПодразделениеПредприятия,
	|	ТрудозатратыНЗП.Сотрудник КАК ФизическоеЛицо,
	|	СУММА(ТрудозатратыНЗП.НормативнаяСтоимость) КАК НормативнаяСтоимость
	|ПОМЕСТИТЬ ВТБазаРаспределения
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
	|ГДЕ
	|	ТрудозатратыНЗП.Регистратор ССЫЛКА Документ.ВыработкаСотрудников
	|	И ТрудозатратыНЗП.Сотрудник В(&СписокФизическихЛиц)
	|	И ТрудозатратыНЗП.Организация = &Организация
	|	И ТрудозатратыНЗП.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНЗП.Подразделение,
	|	ТрудозатратыНЗП.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Выработка.Подразделение,
	|	ТЧСотрудники.Работник,
	|	СУММА(ТЧСотрудники.Сумма)
	|ИЗ
	|	Документ.ВыработкаСотрудников.Работники КАК ТЧСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников КАК Выработка
	|		ПО ТЧСотрудники.Ссылка = Выработка.Ссылка
	|ГДЕ
	|	ТЧСотрудники.Работник В(&СписокФизическихЛиц)
	|	И Выработка.Проведен
	|	И Выработка.Организация = &Организация
	|	И Выработка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Выработка.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы)) 
	|	И Выработка.Автораспределение
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧСотрудники.Работник,
	|	Выработка.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Выработка.Подразделение,
	|	ТЧСотрудники.Работник,
	|	СУММА(ТЧСотрудники.Сумма)
	|ИЗ
	|	Документ.ВыработкаСотрудников.ВидыРабот КАК ТЧСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников КАК Выработка
	|		ПО ТЧСотрудники.Ссылка = Выработка.Ссылка
	|ГДЕ
	|	ТЧСотрудники.Работник В(&СписокФизическихЛиц)
	|	И Выработка.Проведен
	|	И Выработка.Организация = &Организация
	|	И Выработка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Выработка.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы))
	|	И НЕ Выработка.Автораспределение
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧСотрудники.Работник,
	|	Выработка.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБазаРаспределения.ПодразделениеПредприятия,
	|	СУММА(ВТБазаРаспределения.НормативнаяСтоимость) КАК Коэффициент,
	|	ВТБазаРаспределения.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТБазаРаспределения КАК ВТБазаРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТБазаРаспределения.ПодразделениеПредприятия,
	|	ВТБазаРаспределения.ФизическоеЛицо
	|ИТОГИ ПО
	|	ФизическоеЛицо
	|;");
	
	СписокФизическихЛиц = НачисленнаяЗарплатаИВзносы.ВыгрузитьКолонку("ФизическоеЛицо");
	СписокСпособов = НачисленнаяЗарплатаИВзносы.ВыгрузитьКолонку("СпособОтраженияЗарплатыВБухучете");
	ОплатаСдельныхРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСпособов, "ОплатаСдельныхРабот");
	
	Запрос.УстановитьПараметр("СписокФизическихЛиц",    СписокФизическихЛиц);
	Запрос.УстановитьПараметр("НачалоПериода",          НачалоМесяца(СтруктураПараметров.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",           КонецМесяца(СтруктураПараметров.ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация",            СтруктураПараметров.Организация);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ПодразделенияСдельщиков = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Колонки = "Сумма, " + КолонкиДляСуммирования();
	МассивКолонок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Колонки,,,Истина);
	
	ТаблицаДополнения = НачисленнаяЗарплатаИВзносы.СкопироватьКолонки();
	
	МассивУдаляемыхСтрок = Новый Массив;
	
	Для Каждого Строка Из НачисленнаяЗарплатаИВзносы Цикл
		
		Если ЗначениеЗаполнено(Строка.СпособОтраженияЗарплатыВБухучете) Тогда
			НачисленоСдельно = ОплатаСдельныхРабот[Строка.СпособОтраженияЗарплатыВБухучете];
		Иначе
			НачисленоСдельно = Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход;
		КонецЕсли;
		
		Если Не НачисленоСдельно Тогда
			Продолжить;
		КонецЕсли;
		
		ПодразделенияСдельщиков.Сбросить();
		ПодразделенияСдельщиков.НайтиСледующий(Строка.ФизическоеЛицо, "ФизическоеЛицо");
		Выборка = ПодразделенияСдельщиков.Выбрать();
		
		Если Выборка.Количество() = 0 Тогда
			Продолжить;
		ИначеЕсли Выборка.Количество() = 1 Тогда
			
			Выборка.Следующий();
			Строка.ПодразделениеПредприятия = Выборка.ПодразделениеПредприятия;
			
			Продолжить;
		КонецЕсли;
		
		МассивКоэффициентов = Новый Массив;
		МассивПодразделений = Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			МассивКоэффициентов.Добавить(Выборка.Коэффициент);
			МассивПодразделений.Добавить(Выборка.ПодразделениеПредприятия);
		КонецЦикла;
		
		МассивыРаспределений = Новый Структура();
		
		Для Каждого Колонка Из МассивКолонок Цикл
			
			Если Строка[Колонка] = 0 Тогда
				РезультатРаспределения = Новый Массив;
				Для Каждого Коэффициент Из МассивКоэффициентов Цикл
					РезультатРаспределения.Добавить(0);
				КонецЦикла;
			Иначе
				РезультатРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка[Колонка], МассивКоэффициентов);
			КонецЕсли;
			
			МассивыРаспределений.Вставить(Колонка, РезультатРаспределения);
			
		КонецЦикла;
		
		Индекс = 0;
		
		Для Каждого Коэффициент Из МассивКоэффициентов Цикл
			
			НоваяСтрока = ТаблицаДополнения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , Колонки);
			
			Для Каждого Колонка Из МассивКолонок Цикл
				НоваяСтрока[Колонка] = МассивыРаспределений[Колонка][Индекс];
			КонецЦикла;
			
			НоваяСтрока.ПодразделениеПредприятия = МассивПодразделений[Индекс];
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
		МассивУдаляемыхСтрок.Добавить(Строка);
		
	КонецЦикла;
	
	Для Каждого Строка Из МассивУдаляемыхСтрок Цикл
		НачисленнаяЗарплатаИВзносы.Удалить(Строка);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДополнения, НачисленнаяЗарплатаИВзносы);

КонецПроцедуры

Функция ВидыСуммВзносов()
	
	СписокВидовСумм = Новый Массив;
	
	// начисление
	СтруктураСуммы = Новый Структура("Представление, Постфикс, ТипНалога",
		"Сумма", "", Перечисления.ТипыНалогов.ПустаяСсылка());
	СписокВидовСумм.Добавить(СтруктураСуммы);
	
	// Взносы
	СтруктураСуммы = Новый Структура("Представление, Постфикс, ТипНалога",
		"ВзносыВсего", "Взносов", Перечисления.ТипыНалогов.НачисленныйЕСВ);
	СписокВидовСумм.Добавить(СтруктураСуммы);
	
	Возврат СписокВидовСумм;
	
КонецФункции

Процедура ЗаполнитьСтатьиАналитикуРасходовПоУмолчанию(Объект, СтрокаТаблицы)
	
	// Получаем последние используемые значения по совпадающим сочетаниям заполненных полей
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Удержания.ВидОперации КАК ВидОперации,
	|	Удержания.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
	|	Удержания.Контрагент КАК Контрагент,
	|	Удержания.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов
	|ПОМЕСТИТЬ ВТУдержаннаяЗарплата
	|ИЗ
	|	&УдержаннаяЗарплата КАК Удержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленныеПроценты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленныеПроценты.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
	|	НачисленныеПроценты.СтатьяДоходов КАК СтатьяДоходов
	|ПОМЕСТИТЬ ВТНачисленныеПроценты
	|ИЗ
	|	&НачисленныеПроцентыПоЗаймам КАК НачисленныеПроценты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Удержания.ВидОперации КАК ВидОперации,
	|	Удержания.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
	|	Удержания.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ВТНезаполненнаяАналитика
	|ИЗ
	|	ВТУдержаннаяЗарплата КАК Удержания
	|ГДЕ
	|	Удержания.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Удержания.ВидОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленныеПроценты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленныеПроценты.ПодразделениеПредприятия КАК ПодразделениеПредприятия
	|ПОМЕСТИТЬ ВТНезаполненнаяАналитикаДоходов
	|ИЗ
	|	ВТНачисленныеПроценты КАК НачисленныеПроценты
	|ГДЕ
	|	НачисленныеПроценты.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачисленныеПроценты.ФизическоеЛицо,
	|	НачисленныеПроценты.ПодразделениеПредприятия";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("УдержаннаяЗарплата",          Объект.УдержаннаяЗарплата.Выгрузить());
	Запрос.УстановитьПараметр("НачисленныеПроцентыПоЗаймам", Объект.НачисленныеПроцентыПоЗаймам.Выгрузить());
	Запрос.Выполнить();
	
	Документы.ОтражениеЗарплатыВФинансовомУчете.СоздатьВТАналитикаДругихДокументов(МенеджерВременныхТаблиц, Объект.Ссылка, Объект.Дата);
	
	Если Найти(СтрокаТаблицы, "УдержаннаяЗарплата") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	АналитикаДругихДокументов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	АналитикаДругихДокументов.ВидОперации КАК ВидОперации,
		|	АналитикаДругихДокументов.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
		|	АналитикаДругихДокументов.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
		|	АналитикаДругихДокументов.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
		|	АналитикаДругихДокументов.СчетУчета КАК СчетУчета,
		|	АналитикаДругихДокументов.Субконто1 КАК Субконто1,
		|	АналитикаДругихДокументов.Субконто2 КАК Субконто2,
		|	АналитикаДругихДокументов.Субконто3 КАК Субконто3,
		|	3 КАК ТипСтатьи
		|ИЗ
		|	ЗаполненнаяСтатьяПассивов КАК АналитикаДругихДокументов";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтборСтрок = Новый Структура("ФизическоеЛицо, ВидОперации, ПодразделениеПредприятия");
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, Выборка);
			
			НайденныеСтроки = Объект.УдержаннаяЗарплата.НайтиСтроки(ОтборСтрок);
            
            Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
                
				ИсключаемыеПоля = "";
				Если ТипЗнч(Выборка.Субконто1) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(НайденнаяСтрока.Контрагент) Тогда
					НайденнаяСтрока.Субконто1 = НайденнаяСтрока.Контрагент;
					ИсключаемыеПоля = "Субконто1";
				КонецЕсли;
				
				Если ТипЗнч(Выборка.Субконто1) = Тип("СправочникСсылка.СтруктураПредприятия") И ЗначениеЗаполнено(НайденнаяСтрока.ПодразделениеПредприятия) Тогда
					НайденнаяСтрока.Субконто1 = НайденнаяСтрока.ПодразделениеПредприятия;
					Если ЗначениеЗаполнено(ИсключаемыеПоля) Тогда
						ИсключаемыеПоля = ИсключаемыеПоля + ",ПодразделениеПредприятия";
					Иначе
						ИсключаемыеПоля = "ПодразделениеПредприятия";
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗнч(Выборка.Субконто1) = Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") И ЗначениеЗаполнено(НайденнаяСтрока.ЦельВыдачи) Тогда
					НайденнаяСтрока.Субконто1 = НайденнаяСтрока.ЦельВыдачи;
					Если ЗначениеЗаполнено(ИсключаемыеПоля) Тогда
						ИсключаемыеПоля = ИсключаемыеПоля + ",ЦельВыдачи";
					Иначе
						ИсключаемыеПоля = "ЦельВыдачи";
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗнч(Выборка.Субконто1) = Тип("СправочникСсылка.ФизическиеЛица") И ЗначениеЗаполнено(НайденнаяСтрока.ФизическоеЛицо) Тогда
					НайденнаяСтрока.Субконто1 = НайденнаяСтрока.ФизическоеЛицо;
					Если ЗначениеЗаполнено(ИсключаемыеПоля) Тогда
						ИсключаемыеПоля = ИсключаемыеПоля + ",ФизическоеЛицо";
					Иначе
						ИсключаемыеПоля = "ФизическоеЛицо";
					КонецЕсли;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НайденнаяСтрока, Выборка,,ИсключаемыеПоля);
                
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Если Найти(СтрокаТаблицы, "НачисленныеПроцентыПоЗаймам") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	АналитикаДругихДокументов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	АналитикаДругихДокументов.ПодразделениеПредприятия КАК ПодразделениеПредприятия,
		|	АналитикаДругихДокументов.СтатьяДоходов КАК СтатьяДоходов,
		|	АналитикаДругихДокументов.АналитикаДоходов КАК АналитикаДоходов
		|ИЗ
		|	ВТЗаполненнаяСтатьяДоходов КАК АналитикаДругихДокументов";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтборСтрок = Новый Структура("ФизическоеЛицо, ПодразделениеПредприятия");
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, Выборка);
			
			НайденныеСтроки = Объект.НачисленныеПроцентыПоЗаймам.НайтиСтроки(ОтборСтрок);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НайденнаяСтрока.СтатьяДоходов    = Выборка.СтатьяДоходов;
				НайденнаяСтрока.АналитикаДоходов = Выборка.АналитикаДоходов;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыделитьОтпускаЗаСчетОценочныхОбязательств(ДанныеДляОтражения, СтруктураПараметров)
	
	
	Если Не ДанныеДляОтражения.Свойство("НачисленнаяЗарплатаИВзносы") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиРезервов = РезервОтпусков.НастройкиРезервовОтпусков(СтруктураПараметров.Организация, СтруктураПараметров.ПериодРегистрации);
	
	Если Не НастройкиРезервов.ФормироватьРезервОтпусковБУ Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиРезервов.ФормироватьРезервОтпусковНУ Тогда
		ВидОперацииРезервы   = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы;
	Иначе
		ВидОперацииРезервы = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства;
	КонецЕсли;
	
	НачисленнаяЗарплатаИВзносы = ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы;
	
	КолонкиДляСуммированияСтрокой = КолонкиДляСуммирования();
	КолонкиДляСуммирования = Новый Структура(КолонкиДляСуммированияСтрокой);

	ТаблицаВыплатЗаСчетРезерва = ТаблицаВыплатЗаСчетРезерва(СтруктураПараметров);
	
	РаспределяемыеСтроки = НачисленнаяЗарплатаИВзносы.НайтиСтроки(Новый Структура("ВидОперации", Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск));
	
	ДопРаспределяемыеСтроки = НачисленнаяЗарплатаИВзносы.НайтиСтроки(Новый Структура("ВидОперации", Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РаспределяемыеСтроки, ДопРаспределяемыеСтроки);
	
	ОтборСотрудник = Новый Структура("Сотрудник");
	
	Для Каждого Строка Из РаспределяемыеСтроки Цикл
		
		ОтборСотрудник.Сотрудник = Строка.Сотрудник;
		
		ВыплатыЗаСчетРезерва = ТаблицаВыплатЗаСчетРезерва.НайтиСтроки(ОтборСотрудник);
		
		Если ВыплатыЗаСчетРезерва.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВыплаты = ВыплатыЗаСчетРезерва[0];
		
		ВзносыПоСтроке = СтруктураВзносовПоСтроке(Строка, КолонкиДляСуммирования);
		
		// Распределение суммы БУ
		
		Если Строка.Сумма <= СтрокаВыплаты.СуммаВыбыло И ВзносыПоСтроке.ВзносыВсего <= СтрокаВыплаты.ВзносыВыбыло Тогда
			
			Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
				Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства;
			Иначе
				Строка.ВидОперации = ВидОперацииРезервы;
			КонецЕсли;
			
			СтрокаВыплаты.СуммаВыбыло = СтрокаВыплаты.СуммаВыбыло - Строка.Сумма;
			СтрокаВыплаты.ВзносыВыбыло = СтрокаВыплаты.ВзносыВыбыло -  ВзносыПоСтроке.ВзносыВсего;
			
		ИначеЕсли Строка.Сумма > СтрокаВыплаты.СуммаВыбыло Или ВзносыПоСтроке.ВзносыВсего > СтрокаВыплаты.ВзносыВыбыло Тогда
			
			НоваяСтрока = НачисленнаяЗарплатаИВзносы.Вставить(НачисленнаяЗарплатаИВзносы.Индекс(Строка) + 1);
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , "Сумма, " + КолонкиДляСуммированияСтрокой);
			
			Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
				НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства;
			Иначе
				НоваяСтрока.ВидОперации = ВидОперацииРезервы;
			КонецЕсли;
			
			СуммаНачисленоРезерв = Мин(Строка.Сумма, СтрокаВыплаты.СуммаВыбыло);
			НоваяСтрока.Сумма = СуммаНачисленоРезерв;
			Строка.Сумма = Строка.Сумма - НоваяСтрока.Сумма;
			
			СуммаВзносыРезерв    = Мин(ВзносыПоСтроке.ВзносыВсего, СтрокаВыплаты.ВзносыВыбыло);
			Распределение = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаВзносыРезерв, ВзносыПоСтроке.МассивВзносов);
			
			Если Распределение <> Неопределено Тогда
				Индекс = 0;
				Для Каждого Взнос Из КолонкиДляСуммирования Цикл
					НоваяСтрока[Взнос.Ключ] = Распределение[Индекс];
					Строка[Взнос.Ключ] = Строка[Взнос.Ключ] - НоваяСтрока[Взнос.Ключ];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
			
			СтрокаВыплаты.СуммаВыбыло = СтрокаВыплаты.СуммаВыбыло - СуммаНачисленоРезерв;
			СтрокаВыплаты.ВзносыВыбыло = СтрокаВыплаты.ВзносыВыбыло - СуммаВзносыРезерв;
			
			Если НастройкиРезервов.ФормироватьРезервОтпусковНУ И Строка.ВидОперации <> Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
				Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаВыплатЗаСчетРезерва(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Выплаты.Сотрудник КАК Сотрудник,
		|	СУММА(Выплаты.СуммаРезерваВыбылоОборот) КАК СуммаВыбыло,
		|	СУММА(Выплаты.СуммаРезерваСтраховыхВзносовВыбылоОборот) КАК ВзносыВыбыло
		|ИЗ
		|	РегистрНакопления.ВыплатаОтпусковЗаСчетРезерва.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК Выплаты
		|
		|СГРУППИРОВАТЬ ПО
		|	Выплаты.Сотрудник";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(СтруктураПараметров.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(СтруктураПараметров.ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СтруктураВзносовПоСтроке(Строка, КолонкиДляСуммирования)
	
	ВзносыВсего = 0;
	МассивВзносов = Новый Массив;
	
	Для Каждого Колонка Из КолонкиДляСуммирования Цикл
		ВзносыВсего = ВзносыВсего + Строка[Колонка.Ключ];
		МассивВзносов.Добавить(Строка[Колонка.Ключ]);
	КонецЦикла;
	
	Возврат Новый Структура("ВзносыВсего, МассивВзносов", ВзносыВсего, МассивВзносов);
	
КонецФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";      
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.ОтражениеЗарплатыВФинансовомУчете.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("aa408f18-2f8c-4787-9564-bd0077a245dc");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ОтражениеЗарплатыВФинансовомУчете.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = ""
		+ "Документ.ОтражениеЗарплатыВФинансовомУчете"
	;
	Обработчик.ИзменяемыеОбъекты = ""
		+ "Документ.ОтражениеЗарплатыВФинансовомУчете"
	;
	Обработчик.БлокируемыеОбъекты = ""
		+ "Документ.ОтражениеЗарплатыВФинансовомУчете"
	;
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ""Тип налога"" в ТЧ ""НачисленныйНДФЛ"".
                                   |Заполняет реквизит ""Способ отражения"" для строк с видом операции ""Начислено (сдельно)"".'
                                   |;uk='Заповнює реквізит ""Тип податку"" у ТЧ ""Нарахований ПДФО"".
                                   |Заповнює реквізит ""Спосіб відображення"" для рядків з видом операції ""Нараховано (відрядно)"".'");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.СебестоимостьТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СпособыОтраженияЗарплатыВБухУчете.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТрудозатратыНезавершенногоПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПрочиеАктивыПассивы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	

#КонецОбласти
	
КонецПроцедуры 

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеЗарплатыВФинансовомУчетеНачисленныйНДФЛ.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК ОтражениеЗарплатыВФинансовомУчетеНачисленныйНДФЛ
	|ГДЕ
	|	ОтражениеЗарплатыВФинансовомУчетеНачисленныйНДФЛ.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК ДД
	|ГДЕ
	|	ДД.Ссылка.Проведен
	|	И ДД.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход)
	|	И ДД.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|	И ДД.Ссылка.Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК ДД
	|ГДЕ
	|	ДД.Ссылка.Проведен
	|	И ДД.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоСдельноДоход)
	|	И ДД.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|");
	
	Данные = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Данные
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ОтражениеЗарплатыВФинансовомУчете";
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК ДД
	|ГДЕ
	|	НЕ ДД.ПометкаУдаления
	|	И ДД.ОплатаСдельныхРабот
	|	И НЕ ДД.ОтдельнаяСтатьяКалькуляции
	|	И НЕ ДД.ОтдельнаяСтатьяКалькуляцииВзносов
	|	И ДД.БазаРаспределенияПоСдельнымРаботам = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		СпособОтраженияСдельныхРабот = Выборка.Ссылка;
	Иначе
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь,ПолноеИмяОбъекта);
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектИзменен = Ложь;
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для Каждого НДФЛ Из ДокументОбъект.НачисленныйНДФЛ Цикл
				
				Если Не ЗначениеЗаполнено(НДФЛ.ТипНалога) Тогда
					ИнтеграцияБЗК.ЗаполнитьТипНалогаОперацииПоЗарплаты(НДФЛ.ТипНалога, НДФЛ.ВидОперации);
					ОбъектИзменен = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого Строка Из ДокументОбъект.НачисленнаяЗарплатаИВзносы Цикл
				Если Не ЗначениеЗаполнено(Строка.СпособОтраженияЗарплатыВБухучете)
					И Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход Тогда
					ОбъектИзменен = Истина;
					Строка.СпособОтраженияЗарплатыВБухучете = СпособОтраженияСдельныхРабот;
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Строка Из ДокументОбъект.НачисленнаяЗарплатаИВзносыПоФизлицам Цикл
				Если Не ЗначениеЗаполнено(Строка.СпособОтраженияЗарплатыВБухучете)
					И Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоСдельноДоход Тогда
					ОбъектИзменен = Истина;
					Строка.СпособОтраженияЗарплатыВБухучете = СпособОтраженияСдельныхРабот;
				КонецЕсли;
			КонецЦикла;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,, РежимЗаписиДокумента.Запись);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	// из БазаРаспределенияТрудозатрат()
	ВходящиеДанные.Вставить(Метаданные.Документы.ВыработкаСотрудников);
	//++ НЕ УТКА
	ВходящиеДанные.Вставить(Метаданные.Документы.ЗаказНаПроизводство);
	ВходящиеДанные.Вставить(Метаданные.Документы.ЗаказНаРемонт);
	//-- НЕ УТКА
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТрудозатратыНезавершенногоПроизводства);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

