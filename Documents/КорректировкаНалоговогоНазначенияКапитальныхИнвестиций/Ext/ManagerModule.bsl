#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетНДС");
	
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат СформироватьДвижения(ПолучитьДанныеДокумента(Документ), Регистры, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Распределение НДС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений, Неопределено - 
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРаздельныйУчетПоНалогообложению,УправлениеПредприятием,КомплекснаяАвтоматизация";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	Возврат ТекстОтраженияВРеглУчетеУКР();
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
КонецФункции

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
	
#Область ТекстВключениеИсключениеНДС // (Дт 15 :: Кт 6435)
	
	ТекстВключениеИсключениеНДС = "
	|ВЫБРАТЬ //// Включение/исключение НДС в стоимость капитальных инвестиций (Дт 15 :: Кт 6435)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК АналитикаУчетаДт,
	|	ПараметрыОбъектовУчета.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПараметрыОбъектовУчета.Подразделение КАК ПодразделениеДт,
	|	ПараметрыОбъектовУчета.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
    |	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
    |	ЕСТЬNULL(СтатьиРасходов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК СубконтоДт2,
	|	Партии.АналитикаРасходов КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт, 
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ПараметрыОбъектовУчета.Подразделение КАК ПодразделениеКт,
	|	ПараметрыОбъектовУчета.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		""%1""
	|	ИНАЧЕ
	|		""%2""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		Партии.Ссылка = Операция.Ссылка
	|		И (Партии.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|         ИЛИ Партии.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|         )
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		ВТПараметрыОбъектовУчета КАК ПараметрыОбъектовУчета
	|	ПО 
	|		Операция.Ссылка = ПараметрыОбъектовУчета.Ссылка
	|		И Партии.АналитикаРасходов = ПараметрыОбъектовУчета.ОбъектУчета
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
    |		ПО Партии.АналитикаУчетаНоменклатуры = СтатьиРасходов.Ссылка
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)
	|";
 
	ТекстВключениеИсключениеНДС = СтрШаблон(ТекстВключениеИсключениеНДС, 
		НСтр("ru='Условная продажа';uk='Умовний продаж'",ЯзыкСодержания),
		НСтр("ru='Сторно условной продажи';uk='Сторно умовного продажу'",ЯзыкСодержания));
	
#КонецОбласти 

#Область УчетВнеоборотныхАктивов2_1

#Область ТекстСуммаКорректировкиОСНМАНаСчетКапитальныхИнвестиций // (Дт 10, 12 :: Кт 15)

	ТекстСуммаКорректировкиОСНМАНаСчетКапитальныхИнвестиций_2_1 = "
	|ВЫБРАТЬ //// Сумма корректировки ОС, НМА на счет капитальных инвестиций (Дт 10, 12 :: Кт 15)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт, 
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПараметрыОбъектовУчета.Подразделение КАК ПодразделениеДт,
	|	ПараметрыОбъектовУчета.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	| 	Партии.НалоговоеНазначениеПартии КАК НалоговоеНазначениеДт,
	|
	|	ПараметрыОбъектовУчета.СчетУчета КАК СчетДт,
	|	Партии.АналитикаРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА Партии.НалоговоеНазначениеПартии = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ 
	|			ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|				Партии.НДСРегл
	|			ИНАЧЕ
	|				-Партии.НДСРегл
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт, 
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК АналитикаУчетаКт,
	|	ПараметрыОбъектовУчета.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ПараметрыОбъектовУчета.Подразделение КАК ПодразделениеКт,
	|	ПараметрыОбъектовУчета.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.АналитикаРасходов КАК СубконтоКт1,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Партии.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации ТОГДА
	|			ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|				""%1""
	|			ИНАЧЕ
	|				""%2""
	|			КОНЕЦ
	|		ИНАЧЕ 
	|			ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|				""%3""
	|			ИНАЧЕ
	|				""%4""
	|			КОНЕЦ
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		Партии.Ссылка = Операция.Ссылка
	|		И (Партии.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|         ИЛИ Партии.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|         )
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		ВТПараметрыОбъектовУчета КАК ПараметрыОбъектовУчета
	|	ПО 
	|		(Операция.Ссылка = ПараметрыОбъектовУчета.Ссылка)
	|		И (Партии.АналитикаРасходов = ПараметрыОбъектовУчета.ОбъектУчета)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
    |		ПО Партии.АналитикаУчетаНоменклатуры = СтатьиРасходов.Ссылка
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)
	|	И &ИспользоватьВнеоборотныеАктивы2_2 
	|	И (&Дата < &ДатаНачалаУчетаВнеоборотныхАктивов2_4 ИЛИ НЕ &ИспользоватьВнеоборотныеАктивы2_4)
	|";

	ТекстСуммаКорректировкиОСНМАНаСчетКапитальныхИнвестиций_2_1 = СтрШаблон(ТекстСуммаКорректировкиОСНМАНаСчетКапитальныхИнвестиций_2_1, 
		НСтр("ru='Оценка стоимости ОС при условной продаже';uk='Оцінка вартості ОЗ при умовному продажу'",ЯзыкСодержания),
		НСтр("ru='Оценка стоимости ОС при сторно условной продажи';uk='Оцінка вартості ОЗ при сторно умовного продажу'",ЯзыкСодержания),
		НСтр("ru='Оценка стоимости НМА при условной продаже';uk='Оцінка вартості НМА при умовному продажу'",ЯзыкСодержания),
		НСтр("ru='Оценка стоимости НМА при сторно условной продажи';uk='Оцінка вартості НМА при сторно умовного продажу'",ЯзыкСодержания));

#КонецОбласти 

#КонецОбласти 

	ТекстыОтражения = Новый Массив;
	// Общие фрагменты
	ТекстыОтражения.Добавить(ТекстВключениеИсключениеНДС);
	
	// Учет внеоборотных активов 2_1
	ТекстыОтражения.Добавить(ТекстСуммаКорректировкиОСНМАНаСчетКапитальныхИнвестиций_2_1);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	СписокЗапросов = Новый Массив;	
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Организация КАК Организация,
	|	Партии.АналитикаРасходов КАК ОбъектУчета,
	|	ЕСТЬNULL(СчетаОтражения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета,
	|	ЕСТЬNULL(Местонахождения.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение
	|ПОМЕСТИТЬ ВТПараметрыОбъектовУчета
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Партии КАК Партии
	|		ПО ДокументыКОтражению.Ссылка = Партии.Ссылка
	|		И (Партии.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыУчета
	|		ПО (Партии.АналитикаРасходов = ОбъектыУчета.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|				&Дата,
	|				(ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						Т.АналитикаРасходов
	|					ИЗ
	|						Партии КАК Т)) КАК СчетаОтражения
	|		ПО Партии.АналитикаРасходов = СчетаОтражения.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(
	|				&Дата,
	|				(ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						Т.АналитикаРасходов
	|					ИЗ
	|						Партии КАК Т)) КАК Местонахождения
	|		ПО Партии.АналитикаРасходов = Местонахождения.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Ссылка,
	|	Партии.АналитикаРасходов,
	|	Партии.Организация,
	|	ЕСТЬNULL(СчетаОтражения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)),
	|	ЕСТЬNULL(Местонахождения.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Партии.Ссылка,
	|	Партии.Организация,
	|	Партии.АналитикаРасходов,
	|	ЕСТЬNULL(СчетаОтражения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета,
	|	ЕСТЬNULL(Местонахождения.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Партии КАК Партии
	|		ПО ДокументыКОтражению.Ссылка = Партии.Ссылка
	|		И (Партии.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК ОбъектыУчета
	|		ПО (Партии.АналитикаРасходов = ОбъектыУчета.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|				&Дата,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.АналитикаРасходов
	|					ИЗ
	|						Партии КАК Т)) КАК СчетаОтражения
	|		ПО (Партии.АналитикаРасходов = СчетаОтражения.НематериальныйАктив)
	|			И (Партии.Организация = СчетаОтражения.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестоУчетаНМА.СрезПоследних(
	|				&Дата,
	|				(НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.АналитикаРасходов
	|					ИЗ
	|						Партии КАК Т)) КАК Местонахождения
	|		ПО (Партии.АналитикаРасходов = Местонахождения.НематериальныйАктив)
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Ссылка,
	|	Партии.АналитикаРасходов,
	|	Партии.Организация,
	|	ЕСТЬNULL(СчетаОтражения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)),
	|	ЕСТЬNULL(Местонахождения.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОбъектУчета
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	
	СписокЗапросов.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
КонецФункции

//-- НЕ УТ



// Выполняет корректировку налоговых назначений капитальных инвестиций за указанный период в указанных организациях. Предназначена для вызова в рамках закрытия месяца
//
// Параметры:
//	Период - Дата - месяц, за который необходимо создать документы КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.
//	Организация - Массив, СправочникСсылка.Организации, Неопределено - отбор организаций, по которым создаются документы.
//		Если передан пустой параметр, то документы будут созданы по всем организациям.
//	ИмяСобытияЖР - Строка - имя события об ошибке для записи журнала регистрации
//	МенеджерВТКэшейРегистров - МенеджерВременныхТаблиц - содержит кэш оборотов/остатков регистров.
//		Используется только при вызове этого метода в ходе выполнения расчета механизмом партионного учета версии 2.5.
//	ИдетРасчетСебестоимости - Булево - признак вызова корректировки налоговых назначений капитальных инвестиций из механизма расчета партий и себестоимости.
//
// Возвращаемое значение:
//		Структура - результат корректировки налоговых назначений капитальных инвестиций; ключи структуры:
//		  * МассивДокументов - Массив из ДокументСсылка - ссылки на все обработанные документы корректировки налоговых назначений капитальных инвестиций (независимо от наличия в них ошибок)
//		  * РассчитанныеОрганизации - Массив из СправочникСсылка.Организации - ссылки на организации, по которым корректировка налоговых назначений капитальных инвестиций была выполнена без ошибок
//		  * ТекстОшибки - Строка - текст всех ошибок, зафиксированных при корректировке налоговых назначений капитальных инвестиций.
//
Функция КорректироватьНалоговыеНазначенияКапитальныхИнвестиций(Период, Организация, ИмяСобытияЖР = "", МенеджерВТКэшейРегистров = Неопределено, ИдетРасчетСебестоимости = Ложь) Экспорт
	
	РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций = Новый Структура;
	РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.Вставить("МассивДокументов", 		Новый Массив);
	РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.Вставить("РассчитанныеОрганизации", Новый Массив);
	РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.Вставить("ТекстОшибки", 	   		"");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка В (&МассивОрганизаций)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОрганизации.Организация	 	 КАК Организация,
	|	МАКСИМУМ(ЕСТЬNULL(КорректировкаНалоговогоНазначенияКапитальныхИнвестицийПроведен.Ссылка, КорректировкаНалоговогоНазначенияКапитальныхИнвестицийСоздан.Ссылка)) КАК Документ
	|ИЗ
	|	ВТОрганизации КАК ВТОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК КорректировкаНалоговогоНазначенияКапитальныхИнвестицийПроведен
	|		ПО ВТОрганизации.Организация = КорректировкаНалоговогоНазначенияКапитальныхИнвестицийПроведен.Организация
	|			И КорректировкаНалоговогоНазначенияКапитальныхИнвестицийПроведен.Проведен
	|			И КорректировкаНалоговогоНазначенияКапитальныхИнвестицийПроведен.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК КорректировкаНалоговогоНазначенияКапитальныхИнвестицийСоздан
	|		ПО ВТОрганизации.Организация = КорректировкаНалоговогоНазначенияКапитальныхИнвестицийСоздан.Организация
	|			И НЕ КорректировкаНалоговогоНазначенияКапитальныхИнвестицийСоздан.ПометкаУдаления
	|			И КорректировкаНалоговогоНазначенияКапитальныхИнвестицийСоздан.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВТОрганизации.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("ДатаНачала",    			 НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 			 КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("МассивОрганизаций",  	 ОбщегоНазначенияУТКлиентСервер.Массив(Организация));
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 	 НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОшибкиДатЗапретаИзменения = "";
	ОшибкиПроверкиЗаполнения  = "";
	ОшибкиПроведения		  = "";
	
	Пока Выборка.Следующий() Цикл 
		
		Состояние = ЗакрытиеМесяцаСервер.ОпределитьСостояниеЭтаповРасчета(
			Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций,
			Период,
			Выборка.Организация,
			НЕ ИдетРасчетСебестоимости,
			,
			Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций);
		
		Если Состояние <> Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено
		 И Состояние <> Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемыРасчета(
			?(ИдетРасчетСебестоимости,
				Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости,
				Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций),
			Выборка.Организация,
			НачалоМесяца(Период));
			
		ТекстРегистрацииОшибки = СтрЗаменить(
			НСтр("ru='При %операция% по организации ""%1"" за период %2 были диагностированы ошибки';uk='При %операция% по організації ""%1"" за період %2 були діагностовані помилки'",ОбщегоНазначения.КодОсновногоЯзыка()),
			"%операция%",
			?(ИдетРасчетСебестоимости, НСтр("ru='корректировке налоговых назначений капитальных инвестиций';uk='коригування податкових призначень капітальних інвестицій'",ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru='выполнении расчета';uk='виконанні розрахунку'",ОбщегоНазначения.КодОсновногоЯзыка())));
			
		НачатьТранзакцию();
		
		Попытка
		
			Если НЕ ЗначениеЗаполнено(Выборка.Документ) Тогда
				
				ДокументОбъект = Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.СоздатьДокумент();
				ДокументОбъект.Дата 		 = КонецМесяца(Период);
				ДокументОбъект.Организация 	 = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
				
				
			Иначе
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Документ);
				Блокировка.Заблокировать();
				
				ДокументОбъект = Выборка.Документ.ПолучитьОбъект();
				
			КонецЕсли;
		
			// Проверим, что документ находится в доступном для изменения периоде.
			ОписаниеОшибкиДаты = "";
			
			Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ДокументОбъект, , ОписаниеОшибкиДаты) Тогда
				
				ОшибкиДатЗапретаИзменения = ОшибкиДатЗапретаИзменения + ?(ОшибкиДатЗапретаИзменения = "", "", "
					|") + РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
				
				ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
					ПараметрыРегистрации,
					ТекстРегистрацииОшибки,
					,
					ОписаниеОшибкиДаты);
				
				ОтменитьТранзакцию();
				
				Продолжить;
				
			КонецЕсли;
			
			// Заполним дополнительные свойства документа.
			ДокументОбъект.ДополнительныеСвойства.Вставить("РегламентныеОперации");
			
			РасчетСебестоимостиПрикладныеАлгоритмы.УстановитьПризнакВыполненияРасчетаПартийИСебестоимости(ДокументОбъект);
			
			Если МенеджерВТКэшейРегистров <> Неопределено Тогда
				// Вызвано из партионного учета версии 2.5.
				ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВТКэшейРегистров", МенеджерВТКэшейРегистров);
				ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодРасчетаПартий", 	   Период);
			КонецЕсли;
			
			Если ДокументОбъект.ЭтоНовый() Тогда
				// Регламентный документ должен остаться в ИБ независимо от того, удастся его провести или нет.
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
			
			// Проверим корректность заполнения документа.
			СообщенияПроверки = РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьЗаполнениеОбъектаСПерехватомСообщений(ДокументОбъект);
			
			Если ЗначениеЗаполнено(СообщенияПроверки) Тогда
				
				// Проверка заполнения документа обнаружила ошибки.
				ОшибкиПроверкиЗаполнения = ОшибкиПроверкиЗаполнения	+ ?(ОшибкиПроверкиЗаполнения = "", "", "
					|") + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='При проверке документа ""%1"" были обнаружены ошибки:
|%2'
|;uk='При перевірці документа ""%1"" були виявлені помилки:
|%2'",ОбщегоНазначения.КодОсновногоЯзыка()),
							СокрЛП(ДокументОбъект),
							РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(СообщенияПроверки, " - "));
				
				ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
					ПараметрыРегистрации,
					ТекстРегистрацииОшибки,
					,
					ОшибкиПроверкиЗаполнения,
					ДокументОбъект.Ссылка);
				
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.РассчитанныеОрганизации.Добавить(Выборка.Организация); // корректировка выполнена успешно
			КонецЕсли;
			
			РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.МассивДокументов.Добавить(ДокументОбъект.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОшибкиПроведения = ОшибкиПроведения + ?(ОшибкиПроведения = "", "", "
				|") + ТекстОшибки;
			
			ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРегистрации,
				ТекстРегистрацииОшибки,
				,
				ТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла;
	
	// Обработаем информацию об ошибках.
	Если ЗначениеЗаполнено(ОшибкиДатЗапретаИзменения)
	 ИЛИ ЗначениеЗаполнено(ОшибкиПроверкиЗаполнения)
	 ИЛИ ЗначениеЗаполнено(ОшибкиПроведения) Тогда
		
		РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки = ОшибкиДатЗапретаИзменения;
		
		РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки =
			?(РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки = "", "", РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки + "
				|") + ОшибкиПроверкиЗаполнения;
		
		РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки =
			?(РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки = "", "", РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки + "
				|") + ОшибкиПроведения;
		
		Если ЗначениеЗаполнено(ИмяСобытияЖР) Тогда
			
			ТекстДляЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось выполнить корректировку налоговых назначений капитальных инвестиций по причине:
|%1'
|;uk='Не вдалося виконати коригування податкових призначень капітальних інвестицій по причині: 
|%1'"),
				РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций.ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытияЖР,
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций,
				,
				ТекстДляЖурнала);
			
		КонецЕсли;
		
 	КонецЕсли;
	
	Возврат РезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций;
	
КонецФункции

// Ищет и перепроводит существующий документ только по регистрам сведений ОС и НМА 
// Параметры:
//	Период      - Дата - Начало месяца, за который необходимо перепровести документы.
//	Организация - Справочники.Организации - организация, по которой необходимо перепровести документы.
//
Процедура ПерепровестиПоРегистрамСведенийОСНМА(Период, Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка = &Организация
	|		ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Ссылка,
	|	КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Организация
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизации КАК ВТОрганизации
	|		ПО КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Организация = ВТОрганизации.Организация
	|ГДЕ
	|	КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОрганизации.Организация КАК Организация,
	|	ВТДокументы.Ссылка КАК Документ
	|ИЗ
	|	ВТОрганизации КАК ВТОрганизации
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВТДокументы КАК ВТДокументы
	|	ПО 
	|		ВТОрганизации.Организация = ВТДокументы.Организация
	|ГДЕ
	|	НЕ (ВТДокументы.Ссылка ЕСТЬ NULL)
	|";
	
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(Выборка.Документ, "ПервоначальныеСведенияОС, ПервоначальныеСведенияНМА, ПараметрыАмортизацииОСБУ");
		
		// запишем только рс по ОС и НМА
		
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияОС.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Документ);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияНМА.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Документ);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыАмортизацииОСБУ.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Документ);
		
		Блокировка.Заблокировать();
		
		// ПервоначальныеСведенияОС
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияОС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Документ);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияОС);
		НаборЗаписей.Записать();
		
		// ПервоначальныеСведенияНМА
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМА.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Документ);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияНМА);
		НаборЗаписей.Записать();
		
		// ПараметрыАмортизацииОСБУ
		НаборЗаписей = РегистрыСведений.ПараметрыАмортизацииОСБУ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Документ);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПараметрыАмортизацииОСБУ);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
КонецПроцедуры // ПерепровестиПоРегистрамСведенийОСНМА

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитыДокумента() Экспорт
	
	РеквизитыДокумента = Новый Массив();
	
	Возврат РеквизитыДокумента
КонецФункции

Функция ДополнитьТекстЗапросаРеквизитамиДокумента(ТекстЗапроса, ИмяТаблицы) 
	СтрокаПодстановка = "";
	Шаблон = ", " + ИмяТаблицы + ".%1";
	Для Каждого РеквизитДокумента Из РеквизитыДокумента() Цикл
		СтрокаРеквизит = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, РеквизитДокумента);
		СтрокаПодстановка = СтрокаПодстановка + СтрокаРеквизит; 
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ДополнительныеРеквизиты", СтрокаПодстановка);
	
	Возврат ТекстЗапроса
	
КонецФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция ПолучитьДанныеДокумента(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка							КАК Ссылка,
	|	ДанныеДокумента.Дата							КАК Период,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)		КАК НачалоПериода,
	|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)		КАК ОкончаниеПериода,
	|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)		КАК ПериодБазы,
	|	ДанныеДокумента.Организация						КАК Организация
	|	,&ДополнительныеРеквизиты
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	ДополнитьТекстЗапросаРеквизитамиДокумента(Запрос.Текст, "ДанныеДокумента");
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Реквизиты = Новый Структура;
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Реквизиты.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
	КонецЦикла;

	ПериодОкончание = КонецМесяца(Выборка.Период);
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик("НастройкиСистемыНалогообложения",
		Выборка.Организация, 
		ПериодОкончание);
	
	
	Реквизиты.Вставить("ПериодНачало",    Выборка.НачалоПериода);
	Реквизиты.Вставить("ПериодОкончание", Выборка.ОкончаниеПериода);
	
	Реквизиты.Вставить("РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС",
			?(НЕ ПараметрыУчетнойПолитики = Неопределено, 
				ПараметрыУчетнойПолитики.РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС,
				Ложь));
	
	Возврат Реквизиты;
	
КонецФункции

// Данные для распределения
// 
// Параметры:
// 	Реквизиты - Структура - Описание:
// * ПериодОкончание - Дата -
// * РаспределениеЗаПолныйПериод - Булево -
// 	ДопПараметры - Структура, Неопределено -
// Возвращаемое значение:
// 	Структура - Описание:
// * ВнеоборотныеАктивы - ТаблицаЗначений, ДеревоЗначений -
// * ПартииПрочихРасходов - ТаблицаЗначений, ДеревоЗначений -
Функция ДанныеДляРаспределения(Реквизиты, ДопПараметры)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период",             Реквизиты.Период);
	Запрос.УстановитьПараметр("ПериодБазы",         Реквизиты.ПериодБазы);
	Запрос.УстановитьПараметр("ПериодНачало",       Реквизиты.ПериодНачало);
	Запрос.УстановитьПараметр("ПериодОкончание",    Реквизиты.ПериодОкончание);
	Запрос.УстановитьПараметр("Организация",        Реквизиты.Организация);
	Запрос.УстановитьПараметр("Граница",            Новый Граница(Реквизиты.ПериодОкончание, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДокументСсылка",     Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС", 
		Реквизиты.РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("ИспользоватьРаздельныйУчетВНАПоНалогообложению",  ПолучитьФункциональнуюОпцию("ИспользоватьРаздельныйУчетВНАПоНалогообложению"));
	//-- НЕ УТ

	Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("МенеджерВТКэшейРегистров") Тогда
		
		// Вызвано из партионного учета версии 2.5.
		// Необходимо скорректировать остатки на конец периода:
		// вычесть из них "расчетные" движения, хранящиеся в ИБ (результаты прошлого расчета партий)
		// и добавить "расчетные" движения, хранящиеся во временной таблице (результаты выполняющего сейчас расчета партий).
		
		Запрос.МенеджерВременныхТаблиц = ДопПараметры.МенеджерВТКэшейРегистров; // содержит ВТКэшПартииПрочихРасходов
		
		// Период расчета партий (месяц) может отличаться от периода выборки данных для распределения (месяц или квартал).
		Запрос.УстановитьПараметр("НачалоПериодаРасчетаПартий", НачалоМесяца(ДопПараметры.ПериодРасчетаПартий));
		Запрос.УстановитьПараметр("КонецПериодаРасчетаПартий",  КонецМесяца(ДопПараметры.ПериодРасчетаПартий));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ 						  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.НалоговоеНазначение         КАК НалоговоеНазначение,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл,
		|	Т.НДСУпр			  	  	  КАК НДСУпр
		|ПОМЕСТИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериодаРасчетаПартий И &КонецПериодаРасчетаПартий
		|	И Т.Организация = &Организация
		|	И Т.Активность
		|	И Т.РасчетПартий
		|	И Т.Регистратор <> &ДокументСсылка
		|	И (Т.СтоимостьРегл <> 0 
		|		ИЛИ Т.НДСРегл <> 0 ИЛИ Т.НДСУпр <> 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.СлужебноеВидДвиженияПриход
		|		ТОГДА 1
		|		ИНАЧЕ -1
		|	КОНЕЦ 						  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.НалоговоеНазначение         КАК НалоговоеНазначение,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл,
		|	Т.НДСУпр			  	  	  КАК НДСУпр
		|ИЗ
		|	ВТКэшПартииПрочихРасходов КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериодаРасчетаПартий И &КонецПериодаРасчетаПартий
		|	И Т.Организация = &Организация
		|	И Т.РасчетПартий
		|	И Т.Регистратор <> &ДокументСсылка
		|	И (Т.СтоимостьРегл <> 0 
		|		ИЛИ Т.НДСРегл <> 0 ИЛИ Т.НДСУпр <> 0)
		|";
		
	Иначе
		
		// Вызвано не из партионного учета версии 2.5.
		// Корректировать остатки на конец периода не надо - "расчетные" движения хранятся только в ИБ.
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0							  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.НалоговоеНазначение         КАК НалоговоеНазначение,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл,
		|	Т.НДСУпр			  	  	  КАК НДСУпр
		|ПОМЕСТИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК Т";
		
	КонецЕсли;
	
	Запрос.Выполнить(); // формируется ВТКорректировкаОстатковПартииПрочихРасходов
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Т.Организация                   КАК Организация,
	|	Т.Подразделение                 КАК Подразделение,
	|	Т.СтатьяРасходов                КАК СтатьяРасходов,
	|	Т.АналитикаРасходов             КАК АналитикаРасходов,
	|	Т.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|	Т.ДокументПоступленияРасходов   КАК ДокументПоступленияРасходов,
	|	Т.АналитикаУчетаПартий          КАК АналитикаУчетаПартий,
	|	Т.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	Т.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	Т.НалоговоеНазначение           КАК НалоговоеНазначение,
	|	СУММА(Т.Знак * Т.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(Т.Знак * Т.НДСРегл)       КАК НДСРегл,
	|	СУММА(Т.Знак * Т.НДСУпр)        КАК НДСУпр
	|ПОМЕСТИТЬ ПартииПрочихРасходовОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 							  КАК Знак,
	|		Т.Организация                 КАК Организация,
	|		Т.Подразделение               КАК Подразделение,
	|		Т.СтатьяРасходов              КАК СтатьяРасходов,
	|		Т.АналитикаРасходов           КАК АналитикаРасходов,
	|		Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
	|		Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|		Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Т.НалоговоеНазначение         КАК НалоговоеНазначение,
	|		Т.СтоимостьРеглОстаток        КАК СтоимостьРегл,
	|		Т.НДСРеглОстаток              КАК НДСРегл,
	|		Т.НДСУпрОстаток               КАК НДСУпр
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Остатки(&Граница, Организация = &Организация) КАК Т
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-1 							  КАК Знак,
	|		Т.Организация                 КАК Организация,
	|		Т.Подразделение               КАК Подразделение,
	|		Т.СтатьяРасходов              КАК СтатьяРасходов,
	|		Т.АналитикаРасходов           КАК АналитикаРасходов,
	|		Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
	|		Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|		Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Т.НалоговоеНазначение         КАК НалоговоеНазначение,
	|		Т.СтоимостьРеглОборот		  КАК СтоимостьРегл,
	|		Т.НДСРеглОборот			  	  КАК НДСРегл,
	|		Т.НДСУпрОборот                КАК НДСУпр
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Обороты(&ПериодНачало, &ПериодОкончание,
	|			Регистратор, Организация = &Организация) КАК Т
	|	ГДЕ
	|		Т.Регистратор = &ДокументСсылка
	|
	|	
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.СтатьяРасходов,
	|	Т.АналитикаРасходов,
	|	Т.АналитикаАктивовПассивов,
	|	Т.ДокументПоступленияРасходов,
	|	Т.АналитикаУчетаПартий,
	|	Т.НаправлениеДеятельности,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.НалоговоеНазначение
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.Знак * Т.СтоимостьРегл) <> 0 
	|	ИЛИ СУММА(Т.Знак * Т.НДСРегл) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.НДСУпр) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДокументПоступленияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                                   КАК Организация,
	|	ТаблицаОстатков.Подразделение                                 КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности                       КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                                КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                             КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов                      КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов                   КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий                          КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры                    КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.НалоговоеНазначение                           КАК НалоговоеНазначение,
	|	АналитикаПартий.Контрагент                                    КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаОстатков.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|			ТОГДА ТаблицаОстатков.НалоговоеНазначение // Версия ПартионногоУчета 2.5
	|		ИНАЧЕ АналитикаПартий.НалоговоеНазначение     // Версия ПартионногоУчета 2.1             
	|	КОНЕЦ                                                         КАК НалоговоеНазначениеПартии,
	|	АналитикаПартий.СтавкаНДС                                     КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, &Период) КАК ДатаПоступленияРасходов,
	|	
	|	СтатьиРасходов.ВариантРаспределенияРасходовРегл               КАК ВариантРаспределенияРасходовСтатьи,
	|	СтатьиРасходов.ВидЦенности                                    КАК ВидЦенностиСтатьиРасходов,
	|	
	|	ВЫБОР
	|		КОГДА АналитикаПартий.ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|			ТОГДА АналитикаПартий.ВидЦенности        // Версия ПартионногоУчета 2.5
	|		КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГруппыФинансовогоУчетаНоменклатуры.ВидЦенности, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары))
	|		КОГДА НЕ СтатьиРасходов.ВидЦенности ЕСТЬ NULL
	|			ТОГДА СтатьиРасходов.ВидЦенности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	КОНЕЦ                                                      КАК ВидЦенности,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                              КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                                    КАК НДСРегл,
	|	ТаблицаОстатков.НДСУпр                                     КАК НДСУпр
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	ПартииПрочихРасходовОстатки КАК ТаблицаОстатков
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО
	|		ТаблицаОстатков.Организация =  ДанныеПервичныхДокументов.Организация
	|		И ТаблицаОстатков.ДокументПоступленияРасходов = ДанныеПервичныхДокументов.Документ
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
	|	ПО 
	|		ТаблицаОстатков.АналитикаУчетаПартий = АналитикаПартий.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО 
	|		ТаблицаОстатков.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчетаНоменклатуры
	|	ПО
	|		АналитикаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета = ГруппыФинансовогоУчетаНоменклатуры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ТаблицаОстатков.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступленияРасходов
	|;
	|
	|///////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПартииПрочихРасходовОстатки
	|;
	|";
	
	Запрос.Выполнить(); // формируются таблицы ПартииПрочихРасходовКорректировка, ТаблицаОстатков
	
	//++ НЕ УТ
	Если ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Реквизиты.Период) Тогда
		ВнеоборотныеАктивыЛокализация.СформироватьТаблицуПринятыхКУчетуВнеоборотныхАктивовДляКорректировкиНалоговогоНазначенияКапитальныхИнвестиций_2_4(
			?(НачалоМесяца(Реквизиты.ПериодНачало) = Реквизиты.ПериодНачало, НачалоМесяца(Реквизиты.ПериодНачало), НачалоМесяца(Реквизиты.ПериодОкончание)), 
			Реквизиты.ПериодОкончание, Запрос.МенеджерВременныхТаблиц);
	ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_2") Тогда
		ВнеоборотныеАктивыЛокализация.СформироватьТаблицуПринятыхКУчетуВнеоборотныхАктивовДляКорректировкиНалоговогоНазначенияКапитальныхИнвестиций_2_2(
			Реквизиты.ПериодНачало, Реквизиты.ПериодОкончание, Запрос.МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ВтВнеоборотныеАктивы") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1,1,1) КАК ДатаПринятияКУчету,
		|	""ОС"" КАК ВнеоборотныйАктив,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка) КАК Объект,
		|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение
		|ПОМЕСТИТЬ ВтВнеоборотныеАктивы
		|ГДЕ
		|	ЛОЖЬ
		|";
		Запрос.Выполнить();
	КонецЕсли;
	//-- НЕ УТ
	
	Запрос.Текст = "
	//++ НЕ УТ
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                      КАК Организация,
	|	ТаблицаОстатков.Подразделение                    КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                   КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов         КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов      КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.НалоговоеНазначение              КАК НалоговоеНазначение,
	|	
	|	ТаблицаОстатков.Контрагент                       КАК Контрагент,
	|	ТаблицаОстатков.НалоговоеНазначениеПартии        КАК НалоговоеНазначениеПартии,
	|	ТаблицаОстатков.СтавкаНДС                        КАК СтавкаНДС,
	|	
	|	ТаблицаОстатков.ДатаПоступленияРасходов          КАК ДатаПоступленияРасходов,
	|	
	|	
	|	&ПериодБазы КАК ПериодБазы,
	|	
	|	ВнеобАктивы.НалоговоеНазначение                  КАК НовоеНалоговоеНазначение,
	|	ВнеобАктивы.ВнеоборотныйАктив                    КАК ВнеоборотныйАктив,
	|	
	|	
	|	ТаблицаОстатков.ВидЦенности                      КАК ВидЦенности,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                          КАК НДСРегл,
	|	ТаблицаОстатков.НДСУпр                           КАК НДСУпр
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		ВтВнеоборотныеАктивы КАК ВнеобАктивы
	|	ПО 
	|		ТаблицаОстатков.АналитикаРасходов = ВнеобАктивы.Объект
	|		ИЛИ ТаблицаОстатков.АналитикаАктивовПассивов = ВнеобАктивы.Объект 
	|	
	|	
	|ГДЕ
	|	ТаблицаОстатков.ВариантРаспределенияРасходовСтатьи = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|;
	|
	|///////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнеоборотныеАктивы.Организация КАК Организация,
	|	ВнеоборотныеАктивы.Объект КАК Объект
	|ИЗ
	|	ВтВнеоборотныеАктивы КАК ВнеоборотныеАктивы
	//-- НЕ УТ
	|";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПартииПрочихРасходов", РезультатыЗапроса[0].Выгрузить());
	//++ НЕ УТ
	Результат.Вставить("ВнеоборотныеАктивы",   РезультатыЗапроса[1].Выгрузить());
	//-- НЕ УТ

	// Уничтожим все сформированные временные таблицы.
	// Если этого не сделать, то при вызове этого метода из партионного учета версии 2.2
	// возможно возникновение ошибки вида "Временная таблица уже существует".
	
	Запрос.Текст = "
	//++ НЕ УТ
	|УНИЧТОЖИТЬ ВтВнеоборотныеАктивы
	|;
	//-- НЕ УТ
	|УНИЧТОЖИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
	|;
	|УНИЧТОЖИТЬ ТаблицаОстатков
	|";
	
	Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьДвижения(Реквизиты, Регистры, ДопПараметры)
	
	ДанныеДляРаспределения = ДанныеДляРаспределения(Реквизиты, ДопПараметры);
	РезультатРаспределения = РаспределитьПартииПрочихРасходовПоБазе(ДанныеДляРаспределения.ПартииПрочихРасходов, Реквизиты);
	
	ТаблицыДляДвижений = Новый Структура;
	ТаблицыДляДвижений.Вставить("ТаблицаПартииПрочихРасходов", ТаблицаПартииПрочихРасходов(РезультатРаспределения, Реквизиты));
	
	ТаблицаНДСУсловнаяПродажа     = ТаблицаНДСУсловнаяПродажа(РезультатРаспределения, Реквизиты);
	ТаблицаНДСВосстановлениеНДС   = ТаблицаНДСВосстановлениеНДС(РезультатРаспределения, Реквизиты);
	ТаблицаКорректировкиНДСПартий = ТаблицаКорректировкиНДСПартий(ТаблицаНДСУсловнаяПродажа, ТаблицаНДСВосстановлениеНДС);
	ТаблицыДляДвижений.Вставить("ТаблицаКорректировкиНДСПартий",  ТаблицаКорректировкиНДСПартий);
	ТаблицаДвиженияНДСУсловныеПродажи = ТаблицаДвиженияНДСУсловныеПродажи(ТаблицаНДСУсловнаяПродажа, ТаблицаНДСВосстановлениеНДС);
	ТаблицыДляДвижений.Вставить("ТаблицаНДСУсловныеПродажи",  ТаблицаДвиженияНДСУсловныеПродажи);
	
	ТаблицыДляДвижений.Вставить("ТаблицаПрочиеРасходы", ТаблицаПрочиеРасходы(РезультатРаспределения, Реквизиты));
	
	//++ НЕ УТ
	ВнеоборотныеАктивыЛокализация.ВключитьВСтоимостьВнеоборотныхАктивовРезультатКорректировкиНалоговогоНазначенияКапитальныхИнвестиций(
		ДанныеДляРаспределения.ВнеоборотныеАктивы,
		ТаблицыДляДвижений.ТаблицаПрочиеРасходы,
		РезультатРаспределения,
		Реквизиты,
		ТаблицыДляДвижений);
	
	ТаблицаОтражениеДокументовВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	НоваяСтрока = ТаблицаОтражениеДокументовВРеглУчете.Добавить();
	НоваяСтрока.Период = Реквизиты.Период;
	НоваяСтрока.Организация = Реквизиты.Организация;
	НоваяСтрока.ДатаОтражения = НачалоДня(Реквизиты.Период);
	ТаблицыДляДвижений.Вставить("ТаблицаОтражениеДокументовВРеглУчете", ТаблицаОтражениеДокументовВРеглУчете);
	//-- НЕ УТ
	
	Для Каждого Таблица Из ТаблицыДляДвижений Цикл
		ИмяРегистра = Таблица.Ключ;
		Если ВРег(Лев(ИмяРегистра,7))= "ТАБЛИЦА" Тогда
			ИмяРегистра = Прав(ИмяРегистра, СтрДлина(ИмяРегистра)-7);
		КонецЕсли;
		
		Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			ТаблицыДляДвижений.Удалить(Таблица.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

Функция РаспределитьПартииПрочихРасходовПоБазе(ПартииПрочихРасходов, Реквизиты)
	
	ПартииПрочихРасходовУсловнаяПродажа   = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовВосстановлениеНДС = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовБезКорректировок  = ТаблицаРезультатРаспределения();
	
	ТаблицыРаспределения = Новый Массив;
	ТаблицыРаспределения.Добавить(ПартииПрочихРасходовУсловнаяПродажа);
	ТаблицыРаспределения.Добавить(ПартииПрочихРасходовВосстановлениеНДС);
	ТаблицыРаспределения.Добавить(ПартииПрочихРасходовБезКорректировок);
	
	Для каждого СтрокаПрочихРасходов Из ПартииПрочихРасходов Цикл
		
		ВидДеятельностиНДСПартии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПрочихРасходов.НалоговоеНазначениеПартии, "ВидДеятельностиНДС");
		ВидДеятельностиНДСНовый  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПрочихРасходов.НовоеНалоговоеНазначение,  "ВидДеятельностиНДС");
		
		Если (ВидДеятельностиНДСПартии = Перечисления.ВидыДеятельностиНДС.Необлагаемая И ВидДеятельностиНДСНовый = Перечисления.ВидыДеятельностиНДС.Облагаемая) Тогда
			НоваяСтрока = ПартииПрочихРасходовВосстановлениеНДС.Добавить();
		ИначеЕсли (ВидДеятельностиНДСПартии = Перечисления.ВидыДеятельностиНДС.Облагаемая И ВидДеятельностиНДСНовый = Перечисления.ВидыДеятельностиНДС.Необлагаемая) 
			ИЛИ (ВидДеятельностиНДСПартии = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая И СтрокаПрочихРасходов.НовоеНалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность) 
			Тогда
			НоваяСтрока = ПартииПрочихРасходовУсловнаяПродажа.Добавить();
		Иначе	
			НоваяСтрока = ПартииПрочихРасходовБезКорректировок.Добавить();
		КонецЕсли; 
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПрочихРасходов);
		НоваяСтрока.НалоговоеНазначение = СтрокаПрочихРасходов.НовоеНалоговоеНазначение; 
		
	КонецЦикла;
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПартииПрочихРасходовВосстановлениеНДС",    ПартииПрочихРасходовВосстановлениеНДС);
	Результат.Вставить("ПартииПрочихРасходовУсловнаяПродажа",      ПартииПрочихРасходовУсловнаяПродажа);
	Результат.Вставить("ПартииПрочихРасходовБезКорректировок",     ПартииПрочихРасходовБезКорректировок);
	
	Возврат Результат;
	
КонецФункции


Функция ТаблицаРезультатРаспределения()
	
	НаборЗаписей = РегистрыНакопления.ПартииПрочихРасходов.СоздатьНаборЗаписей();
	Результат = НаборЗаписей.ВыгрузитьКолонки();
	
	Результат.Колонки.Удалить("Активность");
	Результат.Колонки.Удалить("МоментВремени");
	
	Результат.Колонки.Добавить("ОснованиеКорректировки",  Метаданные.Документы.КорректировкаПриобретения.Реквизиты.ДокументОснование.Тип);
	Результат.Колонки.Добавить("ДатаПоступленияРасходов", Новый ОписаниеТипов("Дата"));
	
	Результат.Колонки.Добавить("НалоговоеНазначениеПартии",        Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
	Результат.Колонки.Добавить("Контрагент",                       Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Результат.Колонки.Добавить("СтавкаНДС",                        Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	Результат.Колонки.Добавить("ВидЦенности",                      Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	
	Результат.Колонки.Добавить("ВнеоборотныйАктив",                Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
	
КонецФункции


Функция ТаблицаНДСУсловнаяПродажа(РезультатРаспределения, Реквизиты)
	
	НДСУсловнаяПродажа = РегистрыНакопления.КорректировкиНДСПартий.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	НДСУсловнаяПродажа.Колонки.Удалить("Активность");
	
	ТаблицыИсточники = Новый Структура;
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовУсловнаяПродажа");
	
	Для Каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		
		Если НЕ ТаблицыИсточники.Свойство(ТаблицаРаспределения.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаУсловнаяПродажа Из ТаблицаРаспределения.Значение Цикл
			
			НоваяСтрока = НДСУсловнаяПродажа.Добавить();
			
			НоваяСтрока.Период             				= Реквизиты.Период;
			
			НоваяСтрока.Организация        				= Реквизиты.Организация;
			НоваяСтрока.АналитикаУчетаНоменклатуры 		= СтрокаУсловнаяПродажа.СтатьяРасходов;
			НоваяСтрока.СтавкаНДС        				= СтрокаУсловнаяПродажа.СтавкаНДС;
			НоваяСтрока.ДокументПоступления 			= СтрокаУсловнаяПродажа.ДокументПоступленияРасходов;
			НоваяСтрока.НалоговоеНазначение         	= СтрокаУсловнаяПродажа.НалоговоеНазначениеПартии;
			НоваяСтрока.НалоговоеНазначениеПоФакту  	= СтрокаУсловнаяПродажа.НалоговоеНазначение;
			НоваяСтрока.АналитикаУчетаПартий        	= СтрокаУсловнаяПродажа.АналитикаУчетаПартий;
			
			НоваяСтрока.Количество     					= 0;
			НоваяСтрока.СтоимостьРегл  					= СтрокаУсловнаяПродажа.СтоимостьРегл;
			НоваяСтрока.НДСРегл        					= СтрокаУсловнаяПродажа.НДСРегл;
			НоваяСтрока.НДСРеглПоФакту 					= 0;
			НоваяСтрока.НДСУпр        					= СтрокаУсловнаяПродажа.НДСУпр;
			НоваяСтрока.НДСУпрПоФакту 					= 0;
			
			НоваяСтрока.ХозяйственнаяОперация       	= Перечисления.ХозяйственныеОперации.ПредполагаемаяУсловнаяПродажа;
			НоваяСтрока.НДСРеглПодтвержденный			= 0;
			НоваяСтрока.НДСРеглПродукции				= 0;
			НоваяСтрока.РазделКорректировки         	= Перечисления.РазделыКорректировокНДСПартий.КапитальныеИнвестиции;
			НоваяСтрока.СтатьяРасходовСписания      	= Неопределено;
			НоваяСтрока.АналитикаРасходов           	= СтрокаУсловнаяПродажа.АналитикаРасходов;
			НоваяСтрока.ГруппаПродукции             	= Неопределено;
			НоваяСтрока.ВидЗапасов                  	= Неопределено;
			НоваяСтрока.КорВидЗапасов               	= Неопределено;
			НоваяСтрока.АналитикаУчетаПродукции     	= Неопределено;
			НоваяСтрока.ПодразделениеПолучатель     	= Неопределено;
			НоваяСтрока.СтатьяРасходовПолучатель    	= Неопределено;
			НоваяСтрока.АналитикаРасходовПолучатель		= Неопределено;
			НоваяСтрока.КорАналитикаУчетаНоменклатуры 	= СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры;
			НоваяСтрока.АналитикаАктивовПассивов	  	= СтрокаУсловнаяПродажа.АналитикаРасходов;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НДСУсловнаяПродажа;
	
КонецФункции // ТаблицаНДСУсловнаяПродажа

Функция ТаблицаНДСВосстановлениеНДС(РезультатРаспределения, Реквизиты)
	
	НДСВосстановлениеНДС = РегистрыНакопления.КорректировкиНДСПартий.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	НДСВосстановлениеНДС.Колонки.Удалить("Активность");
	
	ТаблицыИсточники = Новый Структура;
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовВосстановлениеНДС");
	
	Для Каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		
		Если НЕ ТаблицыИсточники.Свойство(ТаблицаРаспределения.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаВосстановлениеНДС Из ТаблицаРаспределения.Значение Цикл
			
			НоваяСтрока = НДСВосстановлениеНДС.Добавить();
			
			НоваяСтрока.Период             				= Реквизиты.Период;
			
			НоваяСтрока.Организация        				= Реквизиты.Организация;
			НоваяСтрока.АналитикаУчетаНоменклатуры 		= СтрокаВосстановлениеНДС.СтатьяРасходов;
			НоваяСтрока.СтавкаНДС        				= СтрокаВосстановлениеНДС.СтавкаНДС;
			НоваяСтрока.ДокументПоступления 			= СтрокаВосстановлениеНДС.ДокументПоступленияРасходов;
			НоваяСтрока.НалоговоеНазначение         	= СтрокаВосстановлениеНДС.НалоговоеНазначениеПартии;
			НоваяСтрока.НалоговоеНазначениеПоФакту  	= СтрокаВосстановлениеНДС.НалоговоеНазначение;
			НоваяСтрока.АналитикаУчетаПартий        	= СтрокаВосстановлениеНДС.АналитикаУчетаПартий;
			
			НоваяСтрока.Количество     					= 0;
			НоваяСтрока.СтоимостьРегл  					= СтрокаВосстановлениеНДС.СтоимостьРегл;
			НоваяСтрока.НДСРегл        					= 0;
			НоваяСтрока.НДСРеглПоФакту 					= СтрокаВосстановлениеНДС.НДСРегл;
			НоваяСтрока.НДСУпр        					= 0;
			НоваяСтрока.НДСУпрПоФакту 					= СтрокаВосстановлениеНДС.НДСУпр;
			
			НоваяСтрока.ХозяйственнаяОперация       	= Перечисления.ХозяйственныеОперации.ПредполагаемоеВосстановлениеНДС;
			НоваяСтрока.НДСРеглПодтвержденный			= 0;
			НоваяСтрока.НДСРеглПродукции				= 0;
			НоваяСтрока.РазделКорректировки         	= Перечисления.РазделыКорректировокНДСПартий.КапитальныеИнвестиции;
			НоваяСтрока.СтатьяРасходовСписания      	= Неопределено;
			НоваяСтрока.АналитикаРасходов           	= СтрокаВосстановлениеНДС.АналитикаРасходов;
			НоваяСтрока.ГруппаПродукции             	= Неопределено;
			НоваяСтрока.ВидЗапасов                  	= Неопределено;
			НоваяСтрока.КорВидЗапасов               	= Неопределено;
			НоваяСтрока.АналитикаУчетаПродукции     	= Неопределено;
			НоваяСтрока.ПодразделениеПолучатель     	= Неопределено;
			НоваяСтрока.СтатьяРасходовПолучатель    	= Неопределено;
			НоваяСтрока.АналитикаРасходовПолучатель		= Неопределено;
			НоваяСтрока.КорАналитикаУчетаНоменклатуры 	= СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры;
			НоваяСтрока.АналитикаАктивовПассивов	  	= СтрокаВосстановлениеНДС.АналитикаРасходов;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НДСВосстановлениеНДС;
	
КонецФункции // ТаблицаНДСВосстановлениеНДС

Функция ТаблицаКорректировкиНДСПартий(ТаблицаНДСУсловнаяПродажа, ТаблицаНДСВосстановлениеНДС)
	
	КорректировкиНДСПартий = РегистрыНакопления.КорректировкиНДСПартий.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	КорректировкиНДСПартий.Колонки.Удалить("Активность");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНДСУсловнаяПродажа,   КорректировкиНДСПартий);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНДСВосстановлениеНДС, КорректировкиНДСПартий);
	
	Возврат КорректировкиНДСПартий;
	
КонецФункции // ТаблицаКорректировкиНДСПартий

Функция ТаблицаДвиженияНДСУсловныеПродажи(ТаблицаНДСУсловнаяПродажа, ТаблицаНДСВосстановлениеНДС)
	
	ТаблицаДвиженияНДСУсловныеПродажи = РегистрыНакопления.НДСУсловныеПродажи.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаДвиженияНДСУсловныеПродажи.Колонки.Удалить("Активность");
	
	Для Каждого СтрокаУсловнаяПродажа Из ТаблицаНДСУсловнаяПродажа Цикл
		
		НоваяСтрока = ТаблицаДвиженияНДСУсловныеПродажи.Добавить();
		
		НоваяСтрока.Период             			= СтрокаУсловнаяПродажа.Период;
		
		НоваяСтрока.Организация        			= СтрокаУсловнаяПродажа.Организация;
		НоваяСтрока.ВидУсловнойПродажи        	= Перечисления.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажа;
		
		Если ТипЗнч(СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры) = Тип("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры") Тогда
			НоваяСтрока.Номенклатура 				= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры, "Номенклатура"); 
			НоваяСтрока.Характеристика 	            = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры, "Характеристика"); 
		ИначеЕсли ТипЗнч(СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда	
			НоваяСтрока.Номенклатура 				= СтрокаУсловнаяПродажа.АналитикаУчетаНоменклатуры; 
			НоваяСтрока.Характеристика 	            = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Иначе
			НоваяСтрока.Номенклатура 				= Неопределено; 
			НоваяСтрока.Характеристика 	            = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли; 
		НоваяСтрока.СтавкаНДС        			= СтрокаУсловнаяПродажа.СтавкаНДС;
		НоваяСтрока.НалоговоеНазначение         = СтрокаУсловнаяПродажа.НалоговоеНазначение;
		НоваяСтрока.НалоговоеНазначениеПоФакту  = СтрокаУсловнаяПродажа.НалоговоеНазначениеПоФакту;
		
		НоваяСтрока.Количество     				= СтрокаУсловнаяПродажа.Количество;
		ПроцентНДС 								= УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтрокаУсловнаяПродажа.СтавкаНДС);
		Если ПроцентНДС = 0 Тогда
			НоваяСтрока.СтоимостьРегл  			= СтрокаУсловнаяПродажа.СтоимостьРегл;
		Иначе
			НоваяСтрока.СтоимостьРегл  			= ОКР(СтрокаУсловнаяПродажа.НДСРегл/(ПроцентНДС/100), 2);
		КонецЕсли;
		НоваяСтрока.НДСРегл        				= СтрокаУсловнаяПродажа.НДСРегл;
		НоваяСтрока.НДСРеглРучныеКорректировки 	= 0;
		НоваяСтрока.НДСУпр        				= СтрокаУсловнаяПродажа.НДСУпр;
		НоваяСтрока.НДСУпрРучныеКорректировки 	= 0;
		
	КонецЦикла;
	
	Для Каждого СтрокаВосстановлениеНДС Из ТаблицаНДСВосстановлениеНДС Цикл
		
		НоваяСтрока = ТаблицаДвиженияНДСУсловныеПродажи.Добавить();
		
		НоваяСтрока.Период             			= СтрокаВосстановлениеНДС.Период;
		
		НоваяСтрока.Организация        			= СтрокаВосстановлениеНДС.Организация;
		НоваяСтрока.ВидУсловнойПродажи        	= Перечисления.ВидУсловнойПродажи.ПредполагаемоеВосстановлениеНДС;
		
		Если ТипЗнч(СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры) = Тип("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры") Тогда
			НоваяСтрока.Номенклатура 				= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры, "Номенклатура"); 
			НоваяСтрока.Характеристика 	            = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры, "Характеристика"); 
		ИначеЕсли ТипЗнч(СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			НоваяСтрока.Номенклатура 				= СтрокаВосстановлениеНДС.АналитикаУчетаНоменклатуры; 
			НоваяСтрока.Характеристика 	            = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Иначе
			НоваяСтрока.Номенклатура 				= Неопределено; 
			НоваяСтрока.Характеристика 	            = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли; 
		
		НоваяСтрока.СтавкаНДС        			= СтрокаВосстановлениеНДС.СтавкаНДС;
		НоваяСтрока.НалоговоеНазначение         = СтрокаВосстановлениеНДС.НалоговоеНазначение;
		НоваяСтрока.НалоговоеНазначениеПоФакту  = СтрокаВосстановлениеНДС.НалоговоеНазначениеПоФакту;
		
		НоваяСтрока.Количество     				= СтрокаВосстановлениеНДС.Количество;
		ПроцентНДС 								= УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтрокаВосстановлениеНДС.СтавкаНДС);
		Если ПроцентНДС = 0 Тогда
			НоваяСтрока.СтоимостьРегл  			= СтрокаВосстановлениеНДС.СтоимостьРегл;
		Иначе
			НоваяСтрока.СтоимостьРегл  			= ОКР(СтрокаВосстановлениеНДС.НДСРеглПоФакту/(ПроцентНДС/100), 2);
		КонецЕсли;
		НоваяСтрока.НДСРегл        				= СтрокаВосстановлениеНДС.НДСРеглПоФакту;
		НоваяСтрока.НДСРеглРучныеКорректировки 	= 0;
		НоваяСтрока.НДСУпр        				= СтрокаВосстановлениеНДС.НДСУпрПоФакту;
		НоваяСтрока.НДСУпрРучныеКорректировки 	= 0;
		
	КонецЦикла;
		
	Возврат ТаблицаДвиженияНДСУсловныеПродажи;
	
КонецФункции // ТаблицаДвиженияНДСУсловныеПродажи


Функция ТаблицаПартииПрочихРасходов(РезультатРаспределения, Реквизиты)
	
	ПартииПрочихРасходов = РегистрыНакопления.ПартииПрочихРасходов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПартииПрочихРасходов.Колонки.Удалить("Активность");
	
	Для каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		Для Каждого СтрокаТаблицыИсточник Из ТаблицаРаспределения.Значение Цикл
			
			СтрокаПартииПрочихРасходов = ПартииПрочихРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПартииПрочихРасходов, СтрокаТаблицыИсточник);
			
			СтрокаПартииПрочихРасходов.НалоговоеНазначение 			= СтрокаТаблицыИсточник.НалоговоеНазначениеПартии;
			СтрокаПартииПрочихРасходов.НалоговоеНазначениеСписания 	= СтрокаТаблицыИсточник.НалоговоеНазначение;
			
		КонецЦикла;
	КонецЦикла;
	
	ПартииПрочихРасходов.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	ПартииПрочихРасходов.ЗаполнитьЗначения(Реквизиты.Период,             "Период");
	
	Возврат ПартииПрочихРасходов;
	
КонецФункции

Функция ТаблицаПрочиеРасходы(РезультатРаспределения, Реквизиты)
	
	ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПрочиеРасходы.Колонки.Удалить("Активность");
	
	ПрочиеРасходыДобавитьСтрокиВключениеВСтоимость(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовУсловнаяПродажа,  Реквизиты);
	ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовВосстановлениеНДС,  Реквизиты);
	
	МассивТекстовЗапросов = Новый Массив;
	ТекстЗапросаВтИсходныеПрочиеРасходы = 
	"ВЫБРАТЬ
	|	ПрочиеРасходы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	ПрочиеРасходы.НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	ПрочиеРасходы.СуммаУпр КАК СуммаБезНДСУпр,
	|	ПрочиеРасходы.СуммаРегл КАК СуммаСНДСРегл,
	|	ПрочиеРасходы.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	ПрочиеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ПрочиеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	|	ПрочиеРасходы.ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	&ПрочиеРасходы КАК ПрочиеРасходы
	|";
	МассивТекстовЗапросов.Добавить(ТекстЗапросаВтИсходныеПрочиеРасходы);
	МассивТекстовЗапросов.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы());
	МассивТекстовЗапросов.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы());
	
	Запрос = Новый Запрос(СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов()));
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	Запрос.УстановитьПараметр("ПрочиеРасходы", ПрочиеРасходы);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ПрочиеРасходыДобавитьСтрокиВключениеВСтоимость(ПрочиеРасходы, ТаблицаИсточник, Реквизиты)
	
	ИспользоватьВНА2_4 = Ложь;
	//++ НЕ УТ
	ИспользоватьВНА2_4 = ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Реквизиты.Период);
	//-- НЕ УТ
	
	Для каждого Строка Из ТаблицаИсточник Цикл
		
		Если НЕ ПустаяСтрока(Строка.ВнеоборотныйАктив) 
			 И НЕ ИспользоватьВНА2_4 Тогда 
			Продолжить;
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(Строка.СтатьяОтраженияРасходов) Тогда
			Подразделение		= Строка.Подразделение;
			СтатьяРасходов		= Строка.СтатьяОтраженияРасходов;
			АналитикаРасходов	= Строка.АналитикаОтраженияРасходов;
		Иначе
			Подразделение		= Строка.Подразделение;
			СтатьяРасходов		= Строка.СтатьяРасходов;
			АналитикаРасходов	= Строка.АналитикаРасходов;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтатьяРасходов) 
				ИЛИ ТипЗнч(СтатьяРасходов) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			// Это не расходы
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ПрочиеРасходы.Добавить();
		НоваяСтрока.Период					= Реквизиты.Период;
		НоваяСтрока.Организация				= Реквизиты.Организация;
		НоваяСтрока.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций;
		НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
		НоваяСтрока.Подразделение			= Подразделение;
		НоваяСтрока.СтатьяРасходов			= СтатьяРасходов;
		НоваяСтрока.АналитикаРасходов		= АналитикаРасходов;
		
		НоваяСтрока.СуммаРегл = Строка.НДСРегл;
		НоваяСтрока.СуммаУпр = Строка.НДСУпр;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, ТаблицаИсточник, Реквизиты)
	
	ИспользоватьВНА2_4 = Ложь;
	//++ НЕ УТ
	ИспользоватьВНА2_4 = ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Реквизиты.Период);
	//-- НЕ УТ
	
	
	Для каждого Строка Из ТаблицаИсточник Цикл
		
		Если Не ПустаяСтрока(Строка.ВнеоборотныйАктив) И НЕ ИспользоватьВНА2_4 Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
			// Это не расходы
			Продолжить;
		КонецЕсли;
		
		
		НоваяСтрока = ПрочиеРасходы.Добавить();
		НоваяСтрока.Период					= Реквизиты.Период;
		НоваяСтрока.Организация				= Реквизиты.Организация;
		НоваяСтрока.Подразделение			= Строка.Подразделение;
		НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
		НоваяСтрока.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций;
		НоваяСтрока.СтатьяРасходов			= Строка.СтатьяРасходов;
		НоваяСтрока.АналитикаРасходов		= Строка.АналитикаРасходов;
		
		НоваяСтрока.СуммаРегл = -Строка.НДСРегл;
		НоваяСтрока.СуммаУпр  = -Строка.НДСУпр;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
