#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	РеализацияУслугПрочихАктивовЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		//++ НЕ УТ
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
		//-- НЕ УТ
	    ТекстЗапросаТаблицаНДСНоменклатурныйСоставДляНалоговыхНакладных(Запрос, ТекстыЗапроса, Регистры);

		РеализацияУслугПрочихАктивовЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры);
	ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ДоверенностьВыданная.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

	Документы.ПоступлениеБезналичныхДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.ПриходныйКассовыйОрдер.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Справочники.ПретензииКлиентов.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.ПоручениеЭкспедитору.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	РеализацияУслугПрочихАктивовЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Реализация услуг и прочих активов".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияУслугПрочихАктивов) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РеализацияУслугПрочихАктивов);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	ВзаиморасчетыСервер.КарточкаРасчетовСКлиентом_ДобавитьКомандуОтчетаПоДокументам(КомандыОтчетов);
	
	РеализацияУслугПрочихАктивовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры


// Поставляет данные для зачета оплат
// Параметры:
//	ДокументСсылка - ДокументСсылка.РеализацияУслугПрочихАктивов - по данной ссылке формируется результат
// Возвращаемое значение:
//	Структура - Структура с данными указанного документа, данные могут быть подменены, в зависимости от состояния документа.
Функция РеквизитыДокумента(ДокументСсылка) Экспорт
	Реквизиты = Новый Структура("Дата, ХозяйственнаяОперация, Организация",
		'00010101', Перечисления.ХозяйственныеОперации.РеализацияКлиенту, Справочники.Организации.ПустаяСсылка());
	Реквизиты.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	Реквизиты.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Реквизиты.Вставить("Договор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	Реквизиты.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	Реквизиты.Вставить("ПорядокРасчетов", Перечисления.ПорядокРасчетов.ПустаяСсылка());
	Реквизиты.Вставить("ПоЗаказу", Ложь);
	Реквизиты.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	Реквизиты.Вставить("ВалютаВзаиморасчетов", Справочники.Валюты.ПустаяСсылка());
	Реквизиты.Вставить("СуммаДокумента", 0);
	Реквизиты.Вставить("СуммаВзаиморасчетов", 0);
	Реквизиты.Вставить("Курс", 1);
	Реквизиты.Вставить("Кратность", 1);
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Данные.Дата КАК Дата,
		|	(ВЫБОР КОГДА Данные.ХозяйственнаяОперация=ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
		|		ИНАЧЕ Данные.ХозяйственнаяОперация КОНЕЦ) КАК ХозяйственнаяОперация,
		|	Данные.Организация КАК Организация,
		|	Данные.Партнер КАК Партнер,
		|	Данные.Контрагент КАК Контрагент,
		|	Данные.Договор КАК Договор,
		|	Данные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Данные.ПорядокРасчетов КАК ПорядокРасчетов,
		|	ЛОЖЬ КАК ПоЗаказу,
		|	Данные.Валюта КАК Валюта,
		|	Данные.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	Данные.СуммаДокумента КАК СуммаДокумента,
		|	(ВЫБОР КОГДА Данные.Проведен ТОГДА Данные.СуммаВзаиморасчетов ИНАЧЕ 0 КОНЕЦ) КАК СуммаВзаиморасчетов,
		|	Данные.Курс КАК Курс,
		|	Данные.Кратность КАК Кратность
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Данные
		|ГДЕ
		|	Данные.Ссылка = &ДокументСсылка
		|");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	КонецЕсли;
	Возврат Реквизиты;
КонецФункции

// Расчитывает СостояниеРасчетов (ДолгПартнера, ПорядокРасчетов, СуммаДолга, ПроцентДолга, СуммаОплаты, ПроцентОплаты)
// Параметры:
//	ДокументСсылка - ДокументСсылка.РеализацияУслугПрочихАктивов - для этого документа вычисляется состояние расчетов.
//	ДоговорСсылка - СправочникСсылка.ДоговорыКонтрагентов - если задан, то расчитываем относительно этого договора.
//	СостояниеРасчетов - Структура, ДанныеФормыСтруктура - содержит результат расчета в полях.
//		ДолгПартнера - Булево - показывает, что рассчитанная сумма является задолженностью клиента.
//		СуммаДолга - Число > 0. - накопленная сумма задолженности.
//		ПроцентДолга - Число > 0.
//		СуммаОплаты - Число > 0. - расчет по договору дает 0.0, расчет по накладным дает неотрицательную сумму оплаты по накладной.
//		ПроцентОплаты - Число > 0.
Процедура РассчитатьСостояние(Знач ДокументСсылка, Знач ДоговорСсылка, СостояниеРасчетов) Экспорт
	СостояниеРасчетов.ДолгПартнера = Ложь;
	СостояниеРасчетов.СуммаДолга = 0.;
	СостояниеРасчетов.ПроцентДолга = 0.;
	СостояниеРасчетов.СуммаОплаты = 0.;
	СостояниеРасчетов.ПроцентОплаты = 0.;
	СостояниеРасчетов.СуммаКОплате = 0.;
	
	Если Не (ЗначениеЗаполнено(ДокументСсылка) И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСКлиентами)) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос(" // СуммаОстаток: (+) нам должны, (-) мы должны
		|ВЫБРАТЬ 
		|	ЕСТЬNULL(Расчеты.СуммаПриход,0) КАК СуммаВзаиморасчетов,
		|	ЕСТЬNULL(Расчеты.СуммаРасход,0) КАК СуммаОплаты,
		|	ЕСТЬNULL(Расчеты.СуммаКонечныйОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(Расчеты.КОплатеКонечныйОстаток, 0) КАК КОплатеОстаток
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Накладная
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(,,,, ОбъектРасчетов.Объект = &Заказ) КАК Расчеты
		|		ПО ИСТИНА
		|ГДЕ
		|	Накладная.Ссылка = &Ссылка И Накладная.Проведен И Накладная.СуммаВзаиморасчетов > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ 
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.Сумма КАК СуммаОплаты,
		|	0 КАК СуммаОстаток,
		|	0 КАК КОплатеОстаток
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка И РасшифровкаПлатежа.Ссылка.Проведен
		|	 И РасшифровкаПлатежа.ОбъектРасчетов.Объект <> НЕОПРЕДЕЛЕНО
		|");
	
	СтруктураРевизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ПорядокРасчетов, Договор");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Если СтруктураРевизитов.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
		Запрос.УстановитьПараметр("Заказ", СтруктураРевизитов.Договор);
	Иначе
		Запрос.УстановитьПараметр("Заказ", ДокументСсылка);
	КонецЕсли;
	
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	ТаблицаОстатков.Свернуть(,"СуммаВзаиморасчетов,СуммаОплаты,СуммаОстаток,КОплатеОстаток");
	Если ТаблицаОстатков.Количество() > 0 Тогда
		СтрокаОстатков = ТаблицаОстатков[0];
		СостояниеРасчетов.ДолгПартнера = (СтрокаОстатков.СуммаОстаток >= 0.);
		СостояниеРасчетов.СуммаДолга = ?(СтрокаОстатков.СуммаОстаток < 0., -СтрокаОстатков.СуммаОстаток, СтрокаОстатков.СуммаОстаток);
		Если СтрокаОстатков.СуммаВзаиморасчетов <> 0 И СтруктураРевизитов.ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
			СостояниеРасчетов.СуммаОплаты = СтрокаОстатков.СуммаОплаты;
			СостояниеРасчетов.ПроцентДолга = Окр(100 * СостояниеРасчетов.СуммаДолга / СтрокаОстатков.СуммаВзаиморасчетов, 0);
			СостояниеРасчетов.ПроцентОплаты = Окр(100 * СостояниеРасчетов.СуммаОплаты / СтрокаОстатков.СуммаВзаиморасчетов, 0);
		КонецЕсли;
		СостояниеРасчетов.СуммаКОплате = СтрокаОстатков.КОплатеОстаток;
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РеализацияУслугПрочихАктивовЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламетированном учете.
//
// Возвращаемое значение:
//	Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РеализацияУслугПрочихАктивовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

// Возвращает параметры настройки счетов учета в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 	
// Возвращаемое значение:
// 	Структура - См. НастройкаСчетовУчета.ПараметрыНастройки
//
Функция ПараметрыНастройкиСчетовУчетаОперации(ХозяйственнаяОперация) Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчета.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект.Расходы";
	ПараметрыНастройки.СчетУчета     = "СчетУчета";
	ПараметрыНастройки.Субконто1     = "Субконто1";
	ПараметрыНастройки.Субконто2     = "Субконто2";
	ПараметрыНастройки.Субконто3     = "Субконто3";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	
	ПараметрыНастройки.ДоступностьПоОперации = НЕ
		(ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав);
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "Объект.Расходы.АналитикаАктивовПассивов";

	ПараметрыНастройки.ЭлементыФормы.Добавить("РасходыПредставлениеОтраженияВРеглУчете");
	
	Возврат ПараметрыНастройки;
	
КонецФункции

//-- НЕ УТ

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация) Экспорт
	
	МассивПаметровВыбора = Новый Массив;
	
	#Область РасходыСтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Расходы";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РасходыСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("РасходыАналитикаРасходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область РасходыСтатьяАктивовПассивов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Расходы";
	ПараметрыВыбора.Статья = "СтатьяАктивовПассивов";
	ПараметрыВыбора.ДоступностьПоОперации = НЕ
		(ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав);
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РасходыСтатьяАктивовПассивов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("РасходыАналитикаАктивовПассивов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти

	#Область ДоходыСтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Доходы";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("ДоходыСтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("ДоходыАналитикаДоходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

// Возвращает параметры механизма взаиморасчетов.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров функций механизма взаиморасчетов (См. ВзаиморасчетыСервер.ПараметрыМеханизма)
//
Функция ПараметрыВзаиморасчеты() Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	#Область ОбязательныеПараметры
	
	СтруктураПараметров.ЭтоПродажаЗакупка                = Истина;
	СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	
	#КонецОбласти
	
	#Область НеобязательныеПараметры
	
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов              = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.ПутьКДаннымТЧ                    = "Объект.Доходы";
	СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа  = "Объект.РасшифровкаПлатежа";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.Менеджер                         = "Объект.Менеджер";
	СтруктураПараметров.ОбъектРасчетов                   = "Объект.ОбъектРасчетов";
	
	СтруктураПараметров.ЭлементыФормы.ЗачетОплаты        = "ЗачетОплаты";
	СтруктураПараметров.ЭлементыФормы.СуммаВзаиморасчетовТЧ = "ДоходыСуммаВзаиморасчетов";
	
	#КонецОбласти
	
	#Область РедактированиеВалютИВалютныхСуммДокумента
	
	//Курс и кратность документа - одна пара для документа.
	//Используются для расчета суммы взаиморасчетов и в форме редактирования валют и курсов документа.
	СтруктураПараметров.Курс                             = "Объект.Курс";
	СтруктураПараметров.Кратность                        = "Объект.Кратность";
	
	//Имя гиперссылки, отображающей текущий курс взаиморасчетов документа и открывающей соответствующую форму.
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты                    = "ДекорацияВалюты";
	
	#КонецОбласти
	
	#Область ГрафикПлановойОплатыИДатаПлатежа
	
	//Функция Этапы оплаты и дата платежа
	СтруктураПараметров.ДатаПлатежа = "Объект.ДатаПлатежа";
	СтруктураПараметров.ЭлементыФормы.НадписьЭтапы = "ДекорацияЭтапыОплаты";

	#КонецОбласти
	
	#Область СостояниеВзаиморасчетов
	
	//Имя гиперссылки, отображающей состояние расчетов и открывающей соответствующий отчет,
	СтруктураПараметров.ЭлементыФормы.НадписьРасчеты = "ДекорацияСостояниеРасчетов";
	
	#КонецОбласти
	
	#Область ОграниченияЗадолженностиПоДоговору
	
	//Гиперссылка отображающая состояние ограничения задолженности
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиТекст    = "ДекорацияОграничениеЗадолженности";
	//Картинка отображающая запрет отгрузки
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиКартинка = "КартинкаОтгрузкаЗапрещена"; 
	
	#КонецОбласти
	
	Возврат СтруктураПараметров;
КонецФункции

#Область ПроверкиПриСменеСтатуса

// Возвращает таблицу используемых статусов документа учитывая зависимости от функциональных опций и иных параметров.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. ОбщегоНазначенияУТ.ТаблицаСтатусовИзменяемыхИзСписка().
//
Функция СтатусыДокументаИзменяемыеИзСписка() Экспорт
	
	Таблица = ОбщегоНазначенияУТ.ТаблицаСтатусовИзменяемыхИзСписка();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности") Тогда
		
		ОбщегоНазначенияУТ.ДобавитьСтрокуВТаблицуСтатусовИзменяемыхИзСписка(Таблица, Перечисления.СтатусыРеализацийТоваровУслуг.ВПути, НСтр("ru='Передано';uk='Передано'"));
		ОбщегоНазначенияУТ.ДобавитьСтрокуВТаблицуСтатусовИзменяемыхИзСписка(Таблица, Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено);
				
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//	НовыйСтатус - Строка - Имя нового статуса
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса.
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыРеализацийТоваровУслуг[НовыйСтатус];
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка                КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус)                  КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                    КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен               КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления        КАК ПометкаУдаления,
	|	ИСТИНА                                   КАК ЗаписьПроведением,
	|	ЛОЖЬ                                     КАК РеализацияПоЗаказам,
	|
	|	ТаблицаДокументов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		ИЛИ ТаблицаДокументов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов) КАК ЭтоРеализацияКлиенту,
	|
	|	ТаблицаДокументов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		ИЛИ ТаблицаДокументов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав) КАК ЭтоРеализацияБезПереходаПраваСобственности,
	|
	|	ТаблицаДокументов.Дата                            КАК Дата,
	|	ТаблицаДокументов.ДатаПереходаПраваСобственности  КАК ДатаПереходаПраваСобственности
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
//	НовыйСтатус - ПеречислениеСсылка.СтатусыРеализацийТоваровУслуг - Новый статус
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки.
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Перем ДатаПереходаПраваСобственности;
	
	Отказ = Ложь;
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыРеализацийТоваровУслуг[НовыйСтатус];
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		ДополнительныеПараметры.Свойство("НоваяДата", ДатаПереходаПраваСобственности);
	КонецЕсли;
	
	Если ВыборкаПроверки.ЭтоРеализацияКлиенту Тогда
		
			ТекстОшибки = НСтр("ru='У документа %Документ% статус ""%Статус%"" не установлен, т.к. для реализаций с операцией ""Реализация клиенту"" статусы не применимы';uk='У документа %Документ% статус ""%Статус%"" не встановлено, тому що для реалізацій з операцією ""Реалізація клієнту"" статуси не використовуються'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаПроверки.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", ВыборкаПроверки.ПредставлениеНовогоСтатуса);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ВыборкаПроверки.Ссылка, , , Отказ);
		
	ИначеЕсли ЗначениеНовогоСтатуса = Перечисления.СтатусыРеализацийТоваровУслуг.ВПути
		И Не ВыборкаПроверки.ЭтоРеализацияБезПереходаПраваСобственности Тогда
		
			ТекстОшибки = НСтр("ru='У документа %Документ% статус ""%Статус%"" не установлен, т.к. этот статус применим
|только для реализаций с операцией ""Реализация (товары в пути)""'
|;uk='У документа %Документ% статус ""%Статус%"" не встановлено, оскільки цей статус застовується
|тільки для реалізацій з операцією ""Реалізація (товари у дорозі)""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаПроверки.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", ВыборкаПроверки.ПредставлениеНовогоСтатуса);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ВыборкаПроверки.Ссылка, , , Отказ);	
			
	ИначеЕсли ЗначениеНовогоСтатуса = Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено
		И ВыборкаПроверки.ЭтоРеализацияБезПереходаПраваСобственности
		И ДатаПереходаПраваСобственности < НачалоДня(ВыборкаПроверки.Дата) Тогда
		
			ТекстОшибки =  НСтр("ru='У документа %Документ% не установлена дата перехода права собственности ""%ДатаПерехода%"", 
|так как она не может быть меньше даты документа ""%Дата%""'
|;uk='У документа %Документ% не встановлена дата переходу права власності ""% ДатаПерехода%"", 
|тому що вона не може бути менше дати документа ""% Дата%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаПроверки.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПерехода%", Формат(ДатаПереходаПраваСобственности, "ДЛФ=D"));
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Дата%", Формат(ВыборкаПроверки.Дата, "ДЛФ=D"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ВыборкаПроверки.Ссылка, , , Отказ);
			
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти

#Область УчетНДС

// Инициализирует параметры заполнения налогообложения НДС продажи
//
// Параметры:
//  Объект - ДокументОбъект.РеализацияУслугПрочихАктивов, ДокументСсылка.РеализацияУслугПрочихАктивов, ДанныеФормыСтруктура - документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  Структура - структура параметров, см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи().
//
Функция ПараметрыЗаполненияНалогообложенияНДСПродажи(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияУслугПрочихАктивов")
		Или ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ДанныеОбъекта = Объект;
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, "Организация,Дата,Договор,НаправлениеДеятельности");
	КонецЕсли;
	
	ПараметрыЗаполнения.Организация = ДанныеОбъекта.Организация;
	ПараметрыЗаполнения.Дата = ДанныеОбъекта.Дата;
	ПараметрыЗаполнения.Договор = ДанныеОбъекта.Договор;
	ПараметрыЗаполнения.НаправлениеДеятельности = ДанныеОбъекта.НаправлениеДеятельности;
	ПараметрыЗаполнения.РеализацияПрочихАктивов = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//               (см. функцию ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов)
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Менеджер";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Соглашение";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Менеджер";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Организация", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Менеджер";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Соглашение";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Менеджер";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Подразделение", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Организация";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Договор, Валюта";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "БанковскийСчетОрганизации", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Контрагент";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Договор, Валюта";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Контрагент";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "БанковскийСчетКонтрагента", Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

//++ НЕ УТ

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.РеализацияУслугПрочихАктивов - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//	Структура - таблицы для формирования движений
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ВременнаяТаблицаОстаточнойСтоимости(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Неопределено);
	ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Неопределено);
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос,
																							ТекстыЗапроса,
																							Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата                                 КАК Период,
	|	ДанныеДокумента.ДатаПереходаПраваСобственности       КАК ДатаПереходаПраваСобственности,
	|	ДанныеДокумента.ДатаПлатежа                          КАК ДатаПлатежа,
	|	ДанныеДокумента.Ссылка                               КАК Ссылка,
	|	ДанныеДокумента.Организация                          КАК Организация,
	|	ДанныеДокумента.Партнер                              КАК Партнер,
	|	ДанныеДокумента.Контрагент                           КАК Контрагент,
	|	ДанныеДокумента.Валюта                               КАК Валюта,
	|	ВЫБОР КОГДА ДанныеДокумента.Курс = 0
	|			ТОГДА 1
	|			ИНАЧЕ ДанныеДокумента.Курс
	|	КОНЕЦ                                                КАК Курс,
	|	ВЫБОР КОГДА ДанныеДокумента.Кратность = 0
	|			ТОГДА 1
	|			ИНАЧЕ ДанныеДокумента.Кратность
	|	КОНЕЦ                                                Кратность,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                 КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.ЦенаВключаетНДС                      КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.Подразделение                        КАК Подразделение,
	|	ДанныеДокумента.ХозяйственнаяОперация                КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Статус                               КАК Статус,
	|	ДанныеДокумента.ФормаОплаты                          КАК ФормаОплаты,
	|	ДанныеДокумента.Договор                              КАК Договор,
	|	ДанныеДокумента.Договор.ДопустимаяСуммаЗадолженности КАК ДопустимаяСуммаЗадолженности,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка) КАК ВидПоставки,
	|	ДанныеДокумента.ГруппаФинансовогоУчета               КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Номер                  КАК Номер,
	|	ДанныеДокумента.Менеджер               КАК Менеджер,
	|	ДанныеДокумента.СуммаДокумента         КАК СуммаДокумента,
	|	ДанныеДокумента.Комментарий            КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления        КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен               КАК Проведен,
	|	ДанныеДокумента.ОбъектРасчетов               КАК ОбъектРасчетов
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	ПереходПраваСобственности = (Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
									ИЛИ Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
								И Реквизиты.Статус = Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено;
	
	СписокОперацийПрочаяВыручка = Новый Массив;
	СписокОперацийПрочаяВыручка.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	//++ НЕ УТ
	СписокОперацийПрочаяВыручка.Добавить(Перечисления.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов);
	//-- НЕ УТ
	
	Запрос.УстановитьПараметр("Валюта",                          Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Курс",                            Реквизиты.Курс);
	Запрос.УстановитьПараметр("Кратность",                       Реквизиты.Кратность);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов",            Реквизиты.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",  Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",      Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("Период",                          Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаПлатежа",                     Реквизиты.ДатаПлатежа);
	Запрос.УстановитьПараметр("ДатаПереходаПраваСобственности",  Реквизиты.ДатаПереходаПраваСобственности);
	Запрос.УстановитьПараметр("ПереходПраваСобственности",       ПереходПраваСобственности);
	Запрос.УстановитьПараметр("ФормаОплаты",                     Реквизиты.ФормаОплаты);
	Запрос.УстановитьПараметр("Организация",                     Реквизиты.Организация);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",                 ?(Реквизиты.ЦенаВключаетНДС, 0, 1));
	Запрос.УстановитьПараметр("Подразделение",                   Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",           Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Статус",                          Реквизиты.Статус);
	Запрос.УстановитьПараметр("Договор",                         Реквизиты.Договор);
	Запрос.УстановитьПараметр("Контрагент",                      Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДопустимаяСуммаЗадолженности",    Реквизиты.ДопустимаяСуммаЗадолженности);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", 
	                                                             ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов")); 
	Запрос.УстановитьПараметр("Партнер",                         Реквизиты.Партнер);
	Запрос.УстановитьПараметр("ГруппаФинансовогоУчета",          Реквизиты.ГруппаФинансовогоУчета);
	Запрос.УстановитьПараметр("НаправлениеДеятельности",         Реквизиты.НаправлениеДеятельности);
	
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ИнформацияПоДоговору = НСтр("ru='По договору ""%Договор%""';uk='За договором ""%Договор%""'",ОбщегоНазначения.КодОсновногоЯзыка());
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору",          ИнформацияПоДоговору);
	Запрос.УстановитьПараметр("СуммаДокумента",                Реквизиты.СуммаДокумента);
	Запрос.УстановитьПараметр("Номер",                         Реквизиты.Номер);
	Запрос.УстановитьПараметр("Комментарий",                   Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("ПометкаУдаления",               Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                      Реквизиты.Проведен);
	Запрос.УстановитьПараметр("Менеджер",                      Реквизиты.Менеджер);
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	Запрос.УстановитьПараметр("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы);
	//-- НЕ УТ
	Запрос.УстановитьПараметр("СтатьяАП_ЦФ", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование);
	Запрос.УстановитьПараметр("ОбъектРасчетов", Реквизиты.ОбъектРасчетов);
	Запрос.УстановитьПараметр("СписокОперацийПрочаяВыручка", СписокОперацийПрочаяВыручка);
	Запрос.УстановитьПараметр("ОрганизацияПлательщикНДС",       НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ВидПоставки",                    Реквизиты.ВидПоставки);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта, 
	                                                                         Запрос.Параметры.ВалютаВзаиморасчетов, 
	                                                                         Запрос.Параметры.Период);

	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",           Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",          Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуВзаиморасчетов",Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
																				Запрос.Параметры.ВалютаВзаиморасчетов,
																				Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУПРПереноса",  Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРеглПереноса", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаПрочиеДоходы";
	
	СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДоходы.СтатьяДоходов       КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов    КАК АналитикаДоходов,
	|	ТаблицаДоходы.Количество          КАК Количество,
	|	ТаблицаДоходы.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаДоходы.СтавкаНДС           КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0)    КАК СуммаСНДСУпр,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0)  КАК СуммаБезНДСУпр,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 0)    КАК НДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСУпр, 0)     КАК НДСУпр
	|
	|ПОМЕСТИТЬ ВтТаблицаПрочиеДоходы
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		ТаблицаДоходы.Ссылка = Суммы.Ссылка
	|		И ТаблицаДоходы.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочаяВыручка";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаПрочиеДоходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстПрочаяВыручка =
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА &ПереходПраваСобственности
	|		ТОГДА &ДатаПереходаПраваСобственности
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	&Организация КАК Организация,
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	&Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ФизическоеЛицо,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаДоходы.Количество КАК Количество,
	|	ТаблицаДоходы.СуммаБезНДСУпр КАК ВыручкаБезНДСУпр,
	|	ТаблицаДоходы.СуммаБезНДСРегл КАК ВыручкаБезНДСРегл,
	|	ТаблицаДоходы.СуммаСНДСУпр - ТаблицаДоходы.СуммаБезНДСУпр КАК НДСУпр,
	|	ТаблицаДоходы.НДСРегл КАК НДСРегл,
	|	ТаблицаДоходы.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаДоходы.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
	|ИЗ
	|	ВтТаблицаПрочиеДоходы КАК ТаблицаДоходы
	|ГДЕ
	|	(&ХозяйственнаяОперация В (&СписокОперацийПрочаяВыручка)
	|		ИЛИ &ПереходПраваСобственности)
	|";
	
	ДоходыИРасходыСервер.ОтразитьПрочуюВыручку(Запрос, ТекстыЗапроса, Регистры, ТекстПрочаяВыручка);
	
КонецПроцедуры

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ТекстЗапросаДанных =
	"ВЫБРАТЬ
	|	""Доходы"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка.Валюта КАК ВалютаДокумента,
	|	ТаблицаДокумента.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаДокумента.Ссылка.Дата КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	ТаблицаДокумента.Ссылка.ОбъектРасчетов КАК ОбъектРасчетов
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);

КонецПроцедуры

Функция ТекстЗапросаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияКонтрагентДоходыРасходы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА &ПереходПраваСобственности
	|		ТОГДА &ДатаПереходаПраваСобственности
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходы.СтатьяДоходов.ДоходыПоОбъектамЭксплуатации
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОС)
	|		КОГДА ТаблицаДоходы.СтатьяДоходов.ДоходыПоНМАиНИОКР
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияНМА)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПрочихУслуг)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	&Договор КАК Договор,
	|	&ОбъектРасчетов КАК ОбъектРасчетов,
	|	
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходовРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	
	|	ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) КАК Сумма,
	|	ЕСТЬNULL(Суммы.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ЕСТЬNULL(Суммы.СуммаСНДСРегл, 0) КАК СуммаРегл,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КАК СуммаРеглБезНДС,
	|	
	|	&Валюта КАК Валюта,
	|	ТаблицаДоходы.СуммаСНДС КАК СуммаВВалюте,
	|	ТаблицаДоходы.СуммаСНДС - ТаблицаДоходы.СуммаНДС КАК СуммаБезНДСВВалюте,
	|	
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаДоходы.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаДоходы.СуммаВзаиморасчетов - ВЫБОР КОГДА ТаблицаДоходы.СуммаСНДС <> 0 ТОГДА
	|											ВЫРАЗИТЬ(ТаблицаДоходы.СуммаВзаиморасчетов * ТаблицаДоходы.СуммаНДС / ТаблицаДоходы.СуммаСНДС КАК ЧИСЛО(31,2))
	|										ИНАЧЕ
	|											0
	|										КОНЕЦ КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|	&ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|		ПО ТаблицаДоходы.Ссылка = Суммы.Ссылка
	|		И ТаблицаДоходы.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		ИЛИ &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
	|		ИЛИ &ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
	|			И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено))"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	//++ НЕ УТ
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("втОстаточнаяСтоимость", ТекстыЗапроса) Тогда
		ТекстЗапросаПустаяТаблицаОстаточнойСтоимости(ТекстыЗапроса, Запрос);
	КонецЕсли;
	//-- НЕ УТ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА &ПереходПраваСобственности
	|		ТОГДА &ДатаПереходаПраваСобственности
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРасходы.СтатьяАктивовПассивов КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ТаблицаРасходы.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение КАК КорПодразделение,
	|	&НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ТаблицаРасходы.СтатьяРасходов КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаДоходов,
	|	ТаблицаРасходы.АналитикаРасходов КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО  КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаРасходы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаРасходы.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		 ИЛИ НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТаблицаРасходы.Сумма
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаРегл,
	|	
	|	&Валюта КАК Валюта,
	|	ВЫБОР &Валюта
	|		КОГДА &ВалютаУправленческогоУчета ТОГДА ТаблицаРасходы.Сумма
	|		КОГДА &ВалютаРегламентированногоУчета ТОГДА ТаблицаРасходы.СуммаРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасходы.СуммаРегл /&Курс * &Кратность КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаВВалюте
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		ИЛИ &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|			И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено))
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав) КАК ХозяйственнаяОперация,
	|	&Организация                       КАК Организация,
	|
	|	&Подразделение                     КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                       КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаВНА.ВнеоборотныйАктив       КАК АналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                     КАК КорПодразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                       КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаВНА.ВнеоборотныйАктив       КАК КорАналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаВНА.Стоимость               КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТаблицаВНА.Стоимость
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаВНА.СтоимостьРегл + ТаблицаВНА.СтоимостьЦФ КАК СуммаРегл,
	|	
	|	&Валюта КАК Валюта,
	|	ВЫБОР &Валюта
	|		КОГДА &ВалютаУправленческогоУчета ТОГДА ТаблицаВНА.Стоимость
	|		КОГДА &ВалютаРегламентированногоУчета ТОГДА ТаблицаВНА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВНА.СтоимостьРегл /&Курс * &Кратность КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаВВалюте
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаВНА
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|			ТОГДА &ДатаПереходаПраваСобственности
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ВЫБОР 
	|		КОГДА ТаблицаВНА.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|				И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РасходыОтСписанияАктиваСОтложеннымПереходомПрав)
	|		КОГДА ТаблицаВНА.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииОС)
	|		КОГДА ТаблицаВНА.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииНМА)
	|	КОНЕЦ                              КАК ХозяйственнаяОперация,
	|	&Организация                       КАК Организация,
	|
	|	&Подразделение                     КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР ТаблицаВНА.ВидАктива
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство) ТОГДА &СтатьяАП_ОС
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА) ТОГДА &СтатьяАП_НМА
	|	КОНЕЦ                              КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаВНА.ВнеоборотныйАктив       КАК АналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                     КАК КорПодразделение,
	|	&НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ТаблицаВНА.СтатьяРасходов          КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	ТаблицаВНА.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаВНА.Стоимость               КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаВНА.СтатьяРасходов ЕСТЬ NULL И ТаблицаВНА.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		 ИЛИ НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТаблицаВНА.Стоимость
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаВНА.СтоимостьРегл + ТаблицаВНА.СтоимостьЦФ КАК СуммаРегл,
	|	
	|	&Валюта КАК Валюта,
	|	ВЫБОР &Валюта
	|		КОГДА &ВалютаУправленческогоУчета ТОГДА ТаблицаВНА.Стоимость
	|		КОГДА &ВалютаРегламентированногоУчета ТОГДА ТаблицаВНА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВНА.СтоимостьРегл /&Курс * &Кратность КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаВВалюте
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаВНА
	|ГДЕ
	|	(&ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|		ИЛИ &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Признание доходов от целевого финансирования
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДвижениеДоходовЗаСчетАктивовПассивов) КАК ХозяйственнаяОперация,
	|	&Организация                       КАК Организация,
	|
	|	&Подразделение                     КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаВНА.СтатьяДоходов           КАК Статья,
	|	ТаблицаВНА.АналитикаДоходов        КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                     КАК КорПодразделение,
	|	&НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ЦФ                       КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаВНА.ВнеоборотныйАктив       КАК КорАналитикаАктивовПассивов,
	|	ТаблицаВНА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	0 КАК Сумма,
	|	0 КАК СуммаУпр,
	|	ТаблицаВНА.СтоимостьЦФ             КАК СуммаРегл,
	|	
	|	&Валюта КАК Валюта,
	|	0 КАК СуммаВВалюте
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.СтоимостьЦФ <> 0
	//-- НЕ УТ
	|"; 
	
	//++ НЕ УТ
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&НаправлениеДеятельности КАК НаправлениеДеятельности",
		"ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ТаблицаРасходы.АналитикаАктивовПассивов) = ТИП(Справочник.ОбъектыЭксплуатации)
		|		ТОГДА ТаблицаРасходы.АналитикаАктивовПассивов.НаправлениеДеятельности
		|		ИНАЧЕ &НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности");
	//-- НЕ УТ
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА &ПереходПраваСобственности
	|		ТОГДА &ДатаПереходаПраваСобственности
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяАктивовПассивов КАК Статья,
	|	ТаблицаДокумента.АналитикаАктивовПассивов КАК Аналитика,
	|	
	|	ТаблицаДокумента.Сумма КАК Сумма
	|	
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Сумма <> 0
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		ИЛИ &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
	|		ИЛИ &ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
	|			И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено))
	|"; 
	
	//++ НЕ УТ
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&НаправлениеДеятельности КАК НаправлениеДеятельности",
		"ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.АналитикаАктивовПассивов) = ТИП(Справочник.ОбъектыЭксплуатации)
		|		ТОГДА ТаблицаДокумента.АналитикаАктивовПассивов.НаправлениеДеятельности
		|		ИНАЧЕ &НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности");
	//-- НЕ УТ
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы(Ложь);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.РеализацияУслугПрочихАктивов";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать",        """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
		
	//++ НЕ УТ
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаНМА";
	//-- НЕ УТ	
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РеализацияУслугПрочихАктивов"));
	//++ НЕ УТ
	ЗначенияПараметровПроведения.Вставить("ДатаНачалаУчетаВнеоборотныхАктивов2_4", ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4());
	ЗначенияПараметровПроведения.Вставить("ИспользоватьВнеоборотныеАктивы2_2", ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_2"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьВнеоборотныеАктивы2_4", ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
	//-- НЕ УТ
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	//++ НЕ УТ
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("втОстаточнаяСтоимость", ТекстыЗапроса) Тогда
		ТекстЗапросаПустаяТаблицаОстаточнойСтоимости(ТекстыЗапроса, Запрос);
	КонецЕсли;
	//-- НЕ УТ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	ТаблицаДокумента.Сумма КАК СуммаСНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаСНДСРегл,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаПереходаПраваСобственности КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	ТаблицаДокумента.Сумма КАК СуммаСНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаСНДСРегл,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
	//++ НЕ УТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	ТаблицаДокумента.Стоимость КАК СуммаСНДС,
	|	ТаблицаДокумента.Стоимость КАК СуммаБезНДС,
	|	ТаблицаДокумента.Стоимость КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СтоимостьРегл КАК СуммаСНДСРегл,
	|	ТаблицаДокумента.СтоимостьРегл КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииОС)
	|		КОГДА ТаблицаДокумента.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РасходыОтСписанияАктиваСОтложеннымПереходомПрав) КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаПереходаПраваСобственности КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	ТаблицаДокумента.Стоимость КАК СуммаСНДС,
	|	ТаблицаДокумента.Стоимость КАК СуммаБезНДС,
	|	ТаблицаДокумента.Стоимость КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СтоимостьРегл КАК СуммаСНДСРегл,
	|	ТаблицаДокумента.СтоимостьРегл КАК СуммаБезНДСРегл,
    |	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РасходыОтСписанияАктиваСОтложеннымПереходомПрав) КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
	//-- НЕ УТ
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК МестоХранения,
	|	&Подразделение                          КАК Подразделение,
	|	&Менеджер                               КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Валюта                                 КАК Валюта,
	|	&СуммаДокумента                         КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору                   КАК Дополнительно,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура УстановитьПараметрЗапросаАналитикаУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаУчетаПоПартнерам") Тогда
		Возврат;
	КонецЕсли;
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Запрос.Параметры));
	
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаНДСНоменклатурныйСоставДляНалоговыхНакладных(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСНоменклатурныйСоставДляНалоговыхНакладных";
	ИмяРегистраДляПроверки = "НДСНоменклатурныйСоставДляНалоговыхНакладных";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистраДляПроверки, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаАналитикаУчетаПоПартнерам(Запрос);
	
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|
	|	Ссылка.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	&ВидПоставки КАК ВидПоставки,
	|	&Валюта КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияНоменклатурыНалоговыхДокументов.ТоварыПрочее) КАК СпособЗаполнения, 
	|
	|	ТаблицаДоходы.СтавкаНДС		КАК СтавкаНДС,
	|	Неопределено       			КАК НомерГТД,
	|	Неопределено   				КАК Номенклатура,
	|	Неопределено 				КАК Характеристика,
	|	Неопределено       			КАК Упаковка,
	|	0 							КАК ЦенаНН,
	|   &Ссылка                     КАК ДокументПоставки,
	|   НЕОПРЕДЕЛЕНО				КАК ДокументПоставкиДляВозвратов,
	|	СУММА(ТаблицаДоходы.СуммаСНДС) КАК СуммаВзаиморасчетов,
	|	0 							КАК КоличествоУпаковок
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|
	|ГДЕ
	|	&ОрганизацияПлательщикНДС
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПрочихАктивов)
	|		)
	|	И ТаблицаДоходы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО 
	|	Ссылка, 
	|	ТаблицаДоходы.СтавкаНДС
	|";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры)
	
	#Область Продажа
	
	ТекстПродажа = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                           КАК Ссылка,
		|	
		|	Таблица.Партнер                                                   КАК Партнер,
		|	Таблица.Организация                                               КАК Организация,
		|	Таблица.Контрагент                                                КАК Контрагент,
		|	Таблица.Договор                                                   КАК Договор,
		|	Таблица.НаправлениеДеятельности                                   КАК НаправлениеДеятельности,
		|	
		|	Таблица.ОбъектРасчетов                                            КАК ОбъектРасчетов,
		|	Таблица.ДатаПлатежа                                               КАК ДатаПлатежа,
		|	Неопределено                                                      КАК ЗаказПродажи,
		|	ЛОЖЬ                                                              КАК НакладнаяПоЗаказам,
		|	Таблица.СуммаВзаиморасчетов                                       КАК СуммаВзаиморасчетов,
		|	Таблица.СуммаДокумента                                            КАК Сумма,
		|	0                                                                 КАК СуммаВзаиморасчетовПоТаре,
		|	0                                                                 КАК Отгружается,
		|	0                                                                 КАК УменьшениеКОтгрузке,
		|	
		|	Таблица.ПорядокРасчетов                                           КАК ПорядокРасчетов,
		|	ВЫБОР
		|		КОГДА (Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|							ИЛИ Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
		|						И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
		|			ТОГДА Таблица.ДатаПереходаПраваСобственности
		|		ИНАЧЕ Таблица.Дата
		|	КОНЕЦ                                                             КАК ДатаРегистратора,
		|	Таблица.Номер                                                     КАК НомерРегистратора,
		|	Таблица.ВалютаВзаиморасчетов                                      КАК ВалютаВзаиморасчетов,
		|	Таблица.ХозяйственнаяОперация                                     КАК ХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА (Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|							ИЛИ Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
		|						И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
		|			ТОГДА Таблица.ДатаПереходаПраваСобственности
		|		ИНАЧЕ Таблица.Дата
		|	КОНЕЦ                                                             КАК ДатаКурса,
		|	Таблица.Валюта                                                    КАК ВалютаДокумента,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки) КАК ВариантОплаты,
		|	ЛОЖЬ                                                              КАК СверхЗаказа,
		|	Неопределено                                                      КАК СвязанныйДокумент
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И (Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
		|		ИЛИ Таблица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
		|		ИЛИ (Таблица.ХозяйственнаяОперация В (
		|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
		|			И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)))";
		
	#КонецОбласти
	
	#Область УвеличениеПланаОплат
	
	ТекстПланированиеОплат = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                    КАК Ссылка,
		|	
		|	Таблица.Партнер                                                   КАК Партнер,
		|	Таблица.Организация                                               КАК Организация,
		|	Таблица.Контрагент                                                КАК Контрагент,
		|	Таблица.Договор                                                   КАК Договор,
		|	Таблица.НаправлениеДеятельности                                   КАК НаправлениеДеятельности,
		|	
		|	Таблица.ОбъектРасчетов                                            КАК ОбъектРасчетов,
		|	Таблица.Дата                                                      КАК ДатаРегистратора,
		|	Таблица.Номер                                                     КАК НомерРегистратора,
		|	Таблица.ПорядокРасчетов                                           КАК ПорядокРасчетов,
		|	Таблица.ВалютаВзаиморасчетов                                      КАК ВалютаВзаиморасчетов,
		|	Таблица.Валюта                                                    КАК ВалютаДокумента,
		|	Таблица.ХозяйственнаяОперация                                     КАК ХозяйственнаяОперация,
		|	Таблица.ФормаОплаты                                               КАК ФормаОплаты,
		|	
		|	Таблица.ДатаПлатежа                                               КАК ДатаПлатежа,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки) КАК ВариантОплаты,
		|	Таблица.СуммаВзаиморасчетов                                       КАК КОплате,
		|		
		|	ИСТИНА                                                            КАК ИсключатьПриКонтроле,
		|	ЛОЖЬ                                                              КАК НакладнаяПоЗаказам,
		|	Неопределено                                                      КАК ЗаказПродажи,
		|	ЛОЖЬ                                                              КАК СверхЗаказа,
		|	Неопределено                                                      КАК СвязанныйДокумент
		|	
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)";
	
	#КонецОбласти
	
	#Область ЗачетАванса
	
	ТекстЗачетАванса = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                           КАК Ссылка,
		|	
		|	Таблица.ОбъектРасчетов                                                   КАК ОбъектРасчетовИсточник,
		|	Таблица.Ссылка.ОбъектРасчетов                                            КАК ОбъектРасчетовПриемник,
		|	
		|	Таблица.Ссылка.Партнер                                                   КАК Партнер,
		|	Таблица.Ссылка.Организация                                               КАК Организация,
		|	Таблица.Ссылка.Контрагент                                                КАК Контрагент,
		|	Таблица.Ссылка.Договор                                                   КАК Договор,
		|	Таблица.Ссылка.НаправлениеДеятельности                                   КАК НаправлениеДеятельности,
		|
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                      КАК ВалютаВзаиморасчетов,
		|	Таблица.СуммаВзаиморасчетов                                              КАК СуммаВзаиморасчетов,
		|	Таблица.Ссылка.Валюта                                                    КАК ВалютаДокумента,
		|	Таблица.Сумма                                                            КАК Сумма,
		|
		|	Таблица.Ссылка.Дата                                                      КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                     КАК НомерРегистратора,
		|	ВЫБОР КОГДА Таблица.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
		|		ИЛИ Таблица.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
		|	КОНЕЦ                                                                    КАК ХозяйственнаяОперация
		|	
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.РасшифровкаПлатежа КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|";
	
	#КонецОбласти
	
	ВзаиморасчетыСервер.ПроведениеПродажи(Запрос, ТекстыЗапроса, Регистры, ТекстПродажа, ТекстПланированиеОплат, ТекстЗачетАванса);
	
КонецПроцедуры

//++ НЕ УТ

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ВнеоборотныйАктив      КАК ОсновноеСредство
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
	|			И (ТаблицаОС.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|				ИЛИ ТаблицаОС.ВидАктива ЕСТЬ NULL)
	|		ИЛИ &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.АналитикаАктивовПассивов КАК ОсновноеСредство
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ИспользоватьВнеоборотныеАктивы2_2 
	|	И ТаблицаОС.АналитикаАктивовПассивов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|	И ТаблицаОС.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|	И НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.АналитикаДоходов       КАК ОсновноеСредство
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ПО ТаблицаОС.Ссылка = ТаблицаРасходы.Ссылка
	|			И ТаблицаОС.АналитикаДоходов = ТаблицаРасходы.АналитикаАктивовПассивов
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ИспользоватьВнеоборотныеАктивы2_2 
	|	И ТаблицаРасходы.Ссылка ЕСТЬ NULL
	|	И ТаблицаОС.АналитикаДоходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|	И ТаблицаОС.АналитикаДоходов <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|	И НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаНМА.ВнеоборотныйАктив     КАК НематериальныйАктив
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (ТаблицаНМА.ВидАктива В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР))
	|		ИЛИ ТаблицаНМА.ВидАктива ЕСТЬ NULL)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаНМА.АналитикаАктивовПассивов КАК НематериальныйАктив
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ИспользоватьВнеоборотныеАктивы2_2 
	|	И ТаблицаНМА.АналитикаАктивовПассивов ССЫЛКА Справочник.НематериальныеАктивы
	|	И ТаблицаНМА.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка)
	|	И НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ОтражатьВРеглУчете,
	|	ИСТИНА                           КАК ОтражатьВУпрУчете,
	|	ТаблицаНМА.АналитикаДоходов      КАК НематериальныйАктив
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ПО ТаблицаНМА.Ссылка = ТаблицаРасходы.Ссылка
	|			И ТаблицаНМА.АналитикаДоходов = ТаблицаРасходы.АналитикаАктивовПассивов
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ИспользоватьВнеоборотныеАктивы2_2 
	|	И ТаблицаРасходы.Ссылка ЕСТЬ NULL
	|	И ТаблицаНМА.АналитикаДоходов ССЫЛКА Справочник.НематериальныеАктивы
	|	И ТаблицаНМА.АналитикаДоходов <> ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка)
	|	И НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)
	
	ИмяРегистра = "СтоимостьОС";
	
	ВременнаяТаблицаОстаточнойСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииОС)
	|	КОНЕЦ                                    КАК ХозяйственнаяОперация,
	|
	|	ТаблицаСтоимости.Организация             КАК Организация,
	|	ТаблицаСтоимости.Подразделение           КАК Подразделение,
	|	ТаблицаСтоимости.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаСтоимости.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаСтоимости.ВнеоборотныйАктив       КАК ОсновноеСредство,
	|	
	|	ТаблицаСтоимости.Стоимость               КАК Стоимость,
	|	ТаблицаСтоимости.СтоимостьРегл           КАК СтоимостьРегл,
	|	ТаблицаСтоимости.СтоимостьНУ             КАК СтоимостьНУ,
	|	0          								 КАК СтоимостьПР,
	|	0							             КАК СтоимостьВР,
	|	ТаблицаСтоимости.СтоимостьЦФ             КАК СтоимостьЦФ,
	|	ТаблицаСтоимости.СтоимостьНУЦФ           КАК СтоимостьНУЦФ,
	|	0           							 КАК СтоимостьПРЦФ,
	|	0								         КАК СтоимостьВРЦФ,
	|
	|	ТаблицаСтоимости.СтатьяРасходов          КАК КорСтатьяРасходов,
	|	ТаблицаСтоимости.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	
	|	&Контрагент                              КАК КорКонтрагент,
	|	&Подразделение                           КАК КорПодразделение,
	|	&НаправлениеДеятельности                 КАК КорНаправлениеДеятельности
	|	
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаСтоимости
	|ГДЕ
	|	ТаблицаСтоимости.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Приходуем ОС обратно, но уже КАК находящиеся "в пути".
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав) КАК ХозяйственнаяОперация,
	|
	|	ТаблицаСтоимости.Организация             КАК Организация,
	|	ТаблицаСтоимости.Подразделение           КАК Подразделение,
	|	ТаблицаСтоимости.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаСтоимости.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаСтоимости.ВнеоборотныйАктив       КАК ОсновноеСредство,
	|	
	|	ТаблицаСтоимости.Стоимость               КАК Стоимость,
	|	ТаблицаСтоимости.СтоимостьРегл           КАК СтоимостьРегл,
	|	ТаблицаСтоимости.СтоимостьНУ             КАК СтоимостьНУ,
	|	0							             КАК СтоимостьПР,
	|	0							             КАК СтоимостьВР,
	|	ТаблицаСтоимости.СтоимостьЦФ             КАК СтоимостьЦФ,
	|	ТаблицаСтоимости.СтоимостьНУЦФ           КАК СтоимостьНУЦФ,
	|	0							             КАК СтоимостьПРЦФ,
	|	0							             КАК СтоимостьВРЦФ,
	|
	|	ТаблицаСтоимости.СтатьяРасходов          КАК КорСтатьяРасходов,
	|	ТаблицаСтоимости.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	
	|	&Контрагент                              КАК КорКонтрагент,
	|	&Подразделение                           КАК КорПодразделение,
	|	&НаправлениеДеятельности                 КАК КорНаправлениеДеятельности
	|	
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаСтоимости
	|ГДЕ
	|	ТаблицаСтоимости.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	// В УУ и БУ ОС списываются после перехода прав.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&ДатаПереходаПраваСобственности          КАК Период,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РасходыОтСписанияАктиваСОтложеннымПереходомПрав) КАК ХозяйственнаяОперация,
	|
	|	ТаблицаСтоимости.Организация             КАК Организация,
	|	ТаблицаСтоимости.Подразделение           КАК Подразделение,
	|	ТаблицаСтоимости.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаСтоимости.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаСтоимости.ВнеоборотныйАктив       КАК ОсновноеСредство,
	|	
	|	ТаблицаСтоимости.Стоимость               КАК Стоимость,
	|	ТаблицаСтоимости.СтоимостьРегл           КАК СтоимостьРегл,
	|	ТаблицаСтоимости.СтоимостьНУ             КАК СтоимостьНУ,	
	|	0                                        КАК СтоимостьПР,
	|	0          								 КАК СтоимостьВР,
	|	ТаблицаСтоимости.СтоимостьЦФ             КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0 							            КАК СтоимостьВРЦФ,
	|
	|	ТаблицаСтоимости.СтатьяРасходов          КАК КорСтатьяРасходов,
	|	ТаблицаСтоимости.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	
	|	&Контрагент                              КАК КорКонтрагент,
	|	&Подразделение                           КАК КорПодразделение,
	|	&НаправлениеДеятельности                 КАК КорНаправлениеДеятельности
	|	
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаСтоимости
	|ГДЕ
	|	ТаблицаСтоимости.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав)
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)
	
	ИмяРегистра = "СтоимостьНМА";
	
	ВременнаяТаблицаОстаточнойСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииНМА) КАК ХозяйственнаяОперация,
	|
	|	ТаблицаСтоимости.Организация             КАК Организация,
	|	ТаблицаСтоимости.Подразделение           КАК Подразделение,
	|	ТаблицаСтоимости.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаСтоимости.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаСтоимости.ВнеоборотныйАктив       КАК НематериальныйАктив,
	|	
	|	ТаблицаСтоимости.Стоимость               КАК Стоимость,
	|	ТаблицаСтоимости.СтоимостьРегл           КАК СтоимостьРегл,
	|	ТаблицаСтоимости.СтоимостьНУ             КАК СтоимостьНУ,
	|	ТаблицаСтоимости.СтоимостьПР             КАК СтоимостьПР,
	|	ТаблицаСтоимости.СтоимостьВР             КАК СтоимостьВР,
	|	ТаблицаСтоимости.СтоимостьЦФ             КАК СтоимостьЦФ,
	|	ТаблицаСтоимости.СтоимостьНУЦФ           КАК СтоимостьНУЦФ,
	|	ТаблицаСтоимости.СтоимостьПРЦФ           КАК СтоимостьПРЦФ,
	|	ТаблицаСтоимости.СтоимостьВРЦФ           КАК СтоимостьВРЦФ,
	|
	|	ТаблицаСтоимости.СтатьяРасходов          КАК КорСтатьяРасходов,
	|	ТаблицаСтоимости.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	
	|	&Подразделение                           КАК КорПодразделение,
	|	&НаправлениеДеятельности                 КАК КорНаправлениеДеятельности
	|ИЗ
	|	втОстаточнаяСтоимость КАК ТаблицаСтоимости
	|ГДЕ
	|	ТаблицаСтоимости.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПустаяТаблицаОстаточнойСтоимости(ТекстыЗапроса, Запрос)

	ИмяТаблицы = "втОстаточнаяСтоимость";
	
	Если Запрос.МенеджерВременныхТаблиц.Таблицы.Найти(ИмяТаблицы) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	NULL КАК Ссылка,
	|	NULL КАК Организация,
	|	NULL КАК Подразделение,
	|	NULL КАК ВидАктива,
	|	NULL КАК ВнеоборотныйАктив,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьНУ,
	|	0 КАК СтоимостьПР,
	|	0 КАК СтоимостьВР,
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	
	|	NULL КАК ГруппаФинансовогоУчета,
	|	NULL КАК НаправлениеДеятельности,
	|	NULL КАК СтатьяДоходов,
	|	NULL КАК АналитикаДоходов,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение, 
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	NULL КАК АналитикаРасходов
	|ПОМЕСТИТЬ втОстаточнаяСтоимость
	|ГДЕ
	|	ЛОЖЬ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаВНА(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаВНА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = РеализацияУслугПрочихАктивовЛокализация.ВременнаяТаблицаВНА();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаВНА.НомерСтроки                  КАК НомерСтроки,
		|	МестонахождениеОС.Организация           КАК Организация,
		|	МестонахождениеОС.Местонахождение       КАК Подразделение,
		|	ТаблицаВНА.ВидАктива                    КАК ВидАктива,
		|	ТаблицаВНА.ВнеоборотныйАктив            КАК ВнеоборотныйАктив,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|
		|	ТаблицаВНА.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаВНА.АналитикаРасходов КАК АналитикаРасходов,
		|	ТаблицаВНА.НалоговоеНазначение КАК НалоговоеНазначение,
		|
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов
		|	
		|ПОМЕСТИТЬ ТаблицаВНА
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаВНА
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(&Период) КАК ПорядокУчета
		|		ПО ТаблицаВНА.ВнеоборотныйАктив = ПорядокУчета.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(
		|				&Период,
		|				Регистратор <> &Ссылка И Организация = &Организация
		|		) КАК МестонахождениеОС
		|		ПО ТаблицаВНА.ВнеоборотныйАктив = МестонахождениеОС.ОсновноеСредство
		|ГДЕ
		|	ТаблицаВНА.Ссылка = &Ссылка
		|	И ТаблицаВНА.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаВНА.НомерСтроки                  КАК НомерСтроки,
		|	МестоУчетаНМА.Организация               КАК Организация,
		|	МестоУчетаНМА.Подразделение             КАК Подразделение,
		|	ТаблицаВНА.ВидАктива                    КАК ВидАктива,
		|	ТаблицаВНА.ВнеоборотныйАктив            КАК ВнеоборотныйАктив,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.ГруппаФинансовогоУчета КАК НаправлениеДеятельности,
		|
		|	ТаблицаВНА.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаВНА.АналитикаРасходов КАК АналитикаРасходов,
		|	ТаблицаВНА.НалоговоеНазначение КАК НалоговоеНазначение,
		|
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов
		|	
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаВНА
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(&Период) КАК ПорядокУчета
		|		ПО ТаблицаВНА.ВнеоборотныйАктив = ПорядокУчета.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестоУчетаНМА.СрезПоследних(
		|				&Период,
		|				Регистратор <> &Ссылка И Организация = &Организация
		|		) КАК МестоУчетаНМА
		|		ПО ТаблицаВНА.ВнеоборотныйАктив = МестоУчетаНМА.НематериальныйАктив
		|ГДЕ
		|	ТаблицаВНА.Ссылка = &Ссылка
		|	И ТаблицаВНА.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)";
		
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаточнойСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "втОстаточнаяСтоимость";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаВНА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                    КАК Ссылка,
	|	ТаблицаВНА.Организация                                     КАК Организация,
	|	ТаблицаВНА.Подразделение                                   КАК Подразделение,
	|	ТаблицаВНА.ВидАктива                                       КАК ВидАктива,
	|	ТаблицаВНА.ВнеоборотныйАктив                               КАК ВнеоборотныйАктив, 
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)          КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)  КАК СтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)      КАК СтоимостьНУ,
	|	0																				     КАК СтоимостьПР,
	|	0																				     КАК СтоимостьВР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)      КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0)  КАК СтоимостьНУЦФ,
	|	0  														   КАК СтоимостьПРЦФ,
	|	0  														   КАК СтоимостьВРЦФ,
	|	
	|	ТаблицаВНА.ГруппаФинансовогоУчета                          КАК ГруппаФинансовогоУчета,
	|	ТаблицаВНА.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|	ТаблицаВНА.СтатьяДоходов                                   КАК СтатьяДоходов,
	|	ТаблицаВНА.АналитикаДоходов                                КАК АналитикаДоходов,
	|	ТаблицаВНА.НалоговоеНазначение 							   КАК НалоговоеНазначение,
	|	ТаблицаВНА.СтатьяРасходов                                  КАК СтатьяРасходов,
	|	ТаблицаВНА.АналитикаРасходов                               КАК АналитикаРасходов
	|
	|ПОМЕСТИТЬ втОстаточнаяСтоимость
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаВНА.ВнеоборотныйАктив = СтоимостьОС.ОбъектУчета
	|		И СтоимостьОС.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО ТаблицаВНА.ВнеоборотныйАктив = АмортизацияОС.ОбъектУчета
	|		И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	(ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0) > 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) > 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) > 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) > 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) > 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		// Акт об оказании услуг
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьАктОбОказанииУслуг";
		КомандаПечати.Идентификатор = "Акт";
		КомандаПечати.Представление = НСтр("ru='Акт об оказании услуг';uk='Акт про надання послуг'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	Если Пользователи.РолиДоступны("ДобавлениеИзменениеРеализацийУслугПрочихАктивов, ЧтениеРеализацийУслугПрочихАктивов") Тогда
		// Реализация товаров
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор  = "РасходнаяНакладная";
		КомандаПечати.Представление = НСтр("ru='Расходная накладная';uk='Видаткова накладна'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ОтображатьСкидки", Истина);
		КомандаПечати.Порядок = 20;
	КонецЕсли;

	РеализацияУслугПрочихАктивовЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	РеализацияУслугПрочихАктивовЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
КонецПроцедуры

// Возвращает данные, необходимые для печатной формы "Акт об оказании услуг".
// 
// Параметры:
// 	ПараметрыПечати - Структура - дополнительные настройки печати
// 	МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать
// 	
// Возвращаемое значение:
// 	Структура - Содержит в себе данные по шапке документа, табличной части:
// 	* РезультатПоШапке          - РезультатЗапроса - 
// 	* РезультатПоТабличнойЧасти - РезультатЗапроса - 
//
Функция ПолучитьДанныеДляПечатнойФормыАктОбОказанииУслуг(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|   ИСТИНА КАК УчитыватьНДС,
		|   Товары.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаУчитыватьНДС
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Товары
		|ГДЕ
		|	Товары.Ссылка В(&МассивДокументов) И Товары.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НеНДС)
		|СГРУППИРОВАТЬ ПО Товары.Ссылка 
		|;
		|// ЗАПРОС ПО ШАПКЕ
		|ВЫБРАТЬ // документы без посредника
		|	Реализация.Ссылка КАК Ссылка,
		|	Реализация.Номер КАК Номер,
		|	Реализация.Дата КАК Дата,
		|	Реализация.Партнер КАК Партнер,
		|	Реализация.Контрагент КАК Контрагент,
		|	Реализация.Организация КАК Организация,
		|	Реализация.Организация.Префикс КАК Префикс,
		|	Реализация.Валюта КАК Валюта,
		|	Реализация.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|   ЕСТЬNULL(ТаблицаУчитыватьНДС.УчитыватьНДС, Ложь) КАК УчитыватьНДС,
		|	"""" КАК ДополнительнаяИнформация,
		|	"""" КАК ДополнительнаяИнформацияШапки,
		|	Реализация.Договор КАК Договор,
		|	Реализация.Договор.НаименованиеДляПечати                КАК ДоговорНаименованиеДляПечати,
		|	ЛОЖЬ                                                    КАК ПечататьЗаказ,
		|	НЕОПРЕДЕЛЕНО                                            КАК Заказ,
		|	Реализация.Соглашение                                   КАК Соглашение,
		|	ТаблицаОтветственныеЛица.РуководительНаименование       КАК РуководительОрганизации,
		|	ТаблицаОтветственныеЛица.РуководительДолжность          КАК ДолжностьРуководителяОрганизации,
		|	""""                                                    КАК РуководительКонтрагента,
		|	""""                                                    КАК ДолжностьРуководителяКонтрагента,
		|	Реализация.ПредставительОрганизации                     КАК ПредставительОрганизации,
		|	Реализация.ПредставительОрганизацииДолжность            КАК ПредставительОрганизацииДолжность,
		|	Реализация.ПредставительКонтрагента                     КАК ПредставительКонтрагента,
		|	""""                                                    КАК ПредставительОрганизацииПолучателя,
		|	""""                                                    КАК ПредставительОрганизацииПолучателяДолжность,
		|	Реализация.МестоСоставленияДокумента                    КАК МестоСоставленияДокумента,
		|	Реализация.БанковскийСчетОрганизации                    КАК БанковскийСчетОрганизации,
		|	Реализация.БанковскийСчетКонтрагента                    КАК БанковскийСчетКонтрагента	
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Реализация
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
		|		ПО Реализация.Ссылка = ТаблицаОтветственныеЛица.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаУчитыватьНДС КАК ТаблицаУчитыватьНДС
		|		ПО Реализация.Ссылка = ТаблицаУчитыватьНДС.Ссылка
		|ГДЕ
		|	Реализация.Ссылка В (&МассивДокументов) И Реализация.Проведен
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка;
		|
		|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
		|ВЫБРАТЬ
		|	Реализация.Ссылка КАК Ссылка,
		|	Реализация.НомерСтроки КАК НомерСтроки,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	Реализация.Содержание КАК УслугаНаименованиеПолное,
		|	"""" КАК Код,
		|	"""" КАК Артикул,
		|	"""" КАК ЕдиницаЦены,
		|	Реализация.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	"""" КАК ХарактеристикаНаименованиеПолное,
		|	"""" КАК УпаковкаНаименование,
		|	Реализация.СтавкаНДС КАК СтавкаНДС,
		|	Реализация.Цена КАК Цена,
		|	Реализация.Количество КАК Количество,
		|	Реализация.Сумма КАК Сумма,
		|	Реализация.СуммаНДС КАК СуммаНДС,
		|	0 КАК ПроцентСкидки,
		|	0 КАК СуммаСкидки,
		|	Реализация.Сумма КАК СуммаБезСкидки,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Реализация
		|ГДЕ
		|	Реализация.Ссылка В (&МассивДокументов)
		|	И НЕ ТИПЗНАЧЕНИЯ(Реализация.АналитикаДоходов) В (ТИП(Справочник.ОбъектыЭксплуатации), ТИП(Справочник.НематериальныеАктивы))
		|УПОРЯДОЧИТЬ ПО
		|	Реализация.Ссылка, Реализация.НомерСтроки
		|ИТОГИ ПО
		|	Ссылка
		|";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Возврат Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти", РезультатыЗапроса[1], РезультатыЗапроса[2]);

КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыРасходнаяНакладная(ПараметрыПечати, МассивОбъектов) Экспорт	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|   ИСТИНА КАК УчитыватьНДС,
	|   Доходы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаУчитыватьНДС
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Доходы
	|ГДЕ
	|	Доходы.Ссылка В(&МассивДокументов) И Доходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НеНДС)
	|СГРУППИРОВАТЬ ПО Доходы.Ссылка 
	|;
	|ВЫБРАТЬ
	|	РеализацияУслугПрочихАктивов.Ссылка КАК Ссылка,
	|	РеализацияУслугПрочихАктивов.Номер КАК Номер,
	|	РеализацияУслугПрочихАктивов.Дата КАК Дата,
	|	РеализацияУслугПрочихАктивов.Партнер КАК Партнер,
	|	РеализацияУслугПрочихАктивов.Контрагент КАК Получатель,
	|	РеализацияУслугПрочихАктивов.Организация КАК Организация,
	|	РеализацияУслугПрочихАктивов.Организация.Префикс КАК Префикс,
	|	РеализацияУслугПрочихАктивов.Валюта КАК Валюта,
	|	РеализацияУслугПрочихАктивов.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|   ЕСТЬNULL(ТаблицаУчитыватьНДС.УчитыватьНДС, Ложь) КАК УчитыватьНДС,
	|	РеализацияУслугПрочихАктивов.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	РеализацияУслугПрочихАктивов.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	РеализацияУслугПрочихАктивов.Договор КАК Договор,
	|	РеализацияУслугПрочихАктивов.Договор.НаименованиеДляПечати КАК ДоговорНаименованиеДляПечати,
	|	ЛОЖЬ КАК ПечататьЗаказ,
	|	НЕОПРЕДЕЛЕНО КАК Заказ,
	|	РеализацияУслугПрочихАктивов.АдресДоставки КАК АдресДоставки,
	|	РеализацияУслугПрочихАктивов.МестоСоставленияДокумента КАК МестоСоставленияДокумента,
	|	РеализацияУслугПрочихАктивов.ПредставительОрганизации.Наименование КАК ПредставительОрганизации,
	|	РеализацияУслугПрочихАктивов.ПредставительОрганизацииДолжность КАК ПредставительОрганизацииДолжность,
	|	РеализацияУслугПрочихАктивов.ПолучилПоДругомуДокументу КАК ПолучилПоДругомуДокументу,
	|	РеализацияУслугПрочихАктивов.ДоверенностьАльтернативныйВидДокумента КАК ДоверенностьАльтернативныйВидДокумента,
	|	РеализацияУслугПрочихАктивов.ДоверенностьСерия КАК ДоверенностьСерия,
	|	РеализацияУслугПрочихАктивов.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияУслугПрочихАктивов.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияУслугПрочихАктивов.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияУслугПрочихАктивов.ПредставительКонтрагента КАК ПредставительКонтрагента,
	|	РеализацияУслугПрочихАктивов.ДоверенностьПримечание КАК ДоверенностьПримечание
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК РеализацияУслугПрочихАктивов
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаУчитыватьНДС КАК ТаблицаУчитыватьНДС
	|		ПО РеализацияУслугПрочихАктивов.Ссылка = ТаблицаУчитыватьНДС.Ссылка
	|ГДЕ
	|	РеализацияУслугПрочихАктивов.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Содержание КАК Содержание,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование КАК ЕдиницаЦены,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	|	ВложенныйЗапрос.Цена КАК Цена,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	(ВЫБРАТЬ
	|		РеализацияУслугПрочихАктивов.Ссылка КАК Ссылка,
	|		РеализацияУслугПрочихАктивов.Содержание КАК Содержание,
	|		РеализацияУслугПрочихАктивов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		РеализацияУслугПрочихАктивов.СтавкаНДС КАК СтавкаНДС,
	|		РеализацияУслугПрочихАктивов.Цена КАК Цена,
	|		РеализацияУслугПрочихАктивов.Количество КАК Количество,
	|		РеализацияУслугПрочихАктивов.Сумма КАК Сумма,
	|		РеализацияУслугПрочихАктивов.СуммаНДС КАК СуммаНДС,
	|		РеализацияУслугПрочихАктивов.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.РеализацияУслугПрочихАктивов.Доходы КАК РеализацияУслугПрочихАктивов
	|	ГДЕ
	|		РеализацияУслугПрочихАктивов.Ссылка В(&МассивДокументов)
//++ НЕ УТ
	|		И ТИПЗНАЧЕНИЯ(РеализацияУслугПрочихАктивов.АналитикаДоходов) В (ТИП(Справочник.ОбъектыЭксплуатации), ТИП(Справочник.НематериальныеАктивы)) 
//-- НЕ УТ
	|		) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		МассивРезультатов[1],
		МассивРезультатов[2]
	);
	
КонецФункции

// Заполняет структуру получателей печатных форм.
//
// Параметры:
//	СтруктураДанныхОбъектаПечати - Структура - содержит:
//		* МассивРеквизитовПолучателей - Массив - реквизиты получателей
//
Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Партнер";
	МассивРеквизитовПолучателей = СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей;
	МассивРеквизитовПолучателей.Добавить("Партнер");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда 
		МассивРеквизитовПолучателей.Добавить("Контрагент");
	КонецЕсли;
	МассивРеквизитовПолучателей.Добавить("КонтактноеЛицо");
	МассивРеквизитовПолучателей.Добавить("Грузоотправитель");
	МассивРеквизитовПолучателей.Добавить("Грузополучатель");
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов:
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS.
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьПоУмолчанию(Объект) Экспорт
	
	Объект.Менеджер = Пользователи.ТекущийПользователь();
	Объект.Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Объект.Менеджер, Объект.Подразделение);
	
	Объект.Валюта = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Объект.Валюта);
	
	Объект.Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Объект.Организация);
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    		= Объект.Организация;
	СтруктураПараметров.БанковскийСчет		= Объект.БанковскийСчетОрганизации;
	Объект.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
КонецПроцедуры

Процедура ЗаполнитьПоПартнеру(Объект, Партнер) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.Партнер) Или (Объект.Партнер <> Партнер) Тогда
		Объект.Партнер = Партнер;
	КонецЕсли;
	
	УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(
		Объект.Партнер,
		Новый Структура("ХозяйственныеОперации, ВыбранноеСоглашение", Объект.ХозяйственнаяОперация, Объект.Соглашение));
	
	Если ЗначениеЗаполнено(УсловияПродаж) Тогда
		Объект.Соглашение = УсловияПродаж.Соглашение;
		ЗаполнитьПоУсловиямПродаж(Объект, УсловияПродаж);
	Иначе
		Объект.Соглашение = Неопределено;
		ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
		Объект.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Объект.Контрагент, , Объект.БанковскийСчетКонтрагента);
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Объект.Партнер, Объект.КонтактноеЛицо);
	
КонецПроцедуры

Процедура ЗаполнитьПоУсловиямПродаж(Объект, УсловияПродаж) Экспорт
	
	ЗаполнитьЗначенияСвойств(Объект, УсловияПродаж, "Валюта, ЦенаВключаетНДС, ФормаОплаты, ГруппаФинансовогоУчета, НаправлениеДеятельности, ОплатаВВалюте");
	Объект.ВалютаВзаиморасчетов = УсловияПродаж.ВалютаВзаиморасчетов;
	
	Если Не ЗначениеЗаполнено(Объект.ХозяйственнаяОперация) Тогда
		Объект.ХозяйственнаяОперация = УсловияПродаж.ХозяйственнаяОперация;
	ИначеЕсли Не УсловияПродаж.ВозможнаРеализацияБезПереходаПраваСобственности Тогда
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности Тогда
			Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
		ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав Тогда
			Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(УсловияПродаж.Организация) И УсловияПродаж.Организация<>Объект.Организация Тогда
		Объект.Организация = УсловияПродаж.Организация;
	КонецЕсли;
	Если (Не УсловияПродаж.Типовое) И ЗначениеЗаполнено(УсловияПродаж.Контрагент) И УсловияПродаж.Контрагент<>Объект.Контрагент Тогда
		Объект.Контрагент = УсловияПродаж.Контрагент;
	КонецЕсли;
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
	Если (НЕ УсловияПродаж.Типовое) И НЕ ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		Объект.КонтактноеЛицо = УсловияПродаж.КонтактноеЛицо;
	КонецЕсли;
	
	Если УсловияПродаж.ИспользуютсяДоговорыКонтрагентов <> Неопределено И УсловияПродаж.ИспользуютсяДоговорыКонтрагентов Тогда
	
		Объект.Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
			Объект, Перечисления.ХозяйственныеОперации.РеализацияКлиенту, Объект.ВалютаВзаиморасчетов);
		
		ПродажиСервер.ЗаполнитьБанковскиеСчетаПоДоговору(Объект.Договор, Объект.БанковскийСчетОрганизации, Объект.БанковскийСчетКонтрагента);
	
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности") Тогда
			НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, Объект.Соглашение, Объект.Договор);
		КонецЕсли;

		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			Объект.ОплатаВВалюте = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор,"ОплатаВВалюте");
		КонецЕсли;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(УсловияПродаж.ГрафикОплаты) Или ЗначениеЗаполнено(УсловияПродаж.Соглашение) Тогда
		Объект.ДатаПлатежа = ПродажиСервер.ПолучитьПоследнююДатуПоГрафику(Объект.Дата, УсловияПродаж.ГрафикОплаты, УсловияПродаж.Соглашение);
	КонецЕсли;
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    			= Объект.Организация;
	СтруктураПараметров.БанковскийСчет			= Объект.БанковскийСчетОрганизации;
	СтруктураПараметров.НаправлениеДеятельности	= Объект.НаправлениеДеятельности;
	Объект.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	Объект.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Объект.Контрагент, , Объект.БанковскийСчетКонтрагента);
	Объект.АдресДоставки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Объект.Партнер);
	
КонецПроцедуры


#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РеализацияУслугПрочихАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";    
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ba606bc2-b462-46b7-9dc2-19753dd78f1d");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РеализацияУслугПрочихАктивов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизиты Документ-основание, Курс и Кратность.
                                   |В табличной части Доходы документов заполняет незаполненный реквизит СтавкаНДС.
                                   |Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС. 
                                   |Заполняет признак Оплата в иностранной валюте.
                                   |Заполняет реквизит ОбъектРасчетов.'
                                   |;uk='Заповнює реквізити Документ-підстава, Курс та Кратність.
                                   |У табличній частині Доходи документів заповнює незаповнений реквізит СтавкаНДС.
                                   |Заповнює реквізит СтавкаНДС з типом СправочникСсылка.СтавкиНДС.
                                   |Заповнює ознаку Оплата в іноземній валюті.
                                   |Заповнює реквізит ОбъектРасчетов.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя());
	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеНМА.ПолноеИмя());
	//-- НЕ УТ
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.БанковскиеСчетаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СоглашенияСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.КурсыВалют.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ГруппыФинансовогоУчетаРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";                     
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РасходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОперацияПоПлатежнойКарте.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаявкаНаРасходованиеДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";          

	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПодготовкаКПередачеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	//-- НЕ УТ
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ДокументыПоНМА.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ДокументыПоОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.СуммыДокументовВВалютахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);                       

#КонецОбласти

#Область СгенерироватьОбъектыРасчетов

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РеализацияУслугПрочихАктивов.СгенерироватьОбъектыРасчетов";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e54d3454-eaa3-49eb-9109-c34a0566a8b3");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РеализацияУслугПрочихАктивов.ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Генерирует элементы справочника Объекты расчетов на основании данных документа.';uk='Генерує елементи довідника Об''єкти розрахунків на підставі даних документа.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияУслугПрочихАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Документы.РеализацияУслугПрочихАктивов";
	НоваяСтрока.Порядок    = "Удалить"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения
	);	

#КонецОбласти

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.РеализацияУслугПрочихАктивов";
	ПоляУпорядочиванияПриРаботеПользователей = ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей; // Массив
	ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПоляУпорядочиванияПриОбработкеДанных = ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных; // Массив
	ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|
	//++ НЕ УТ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втДокументы
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА КАК ОснованиеПодготовкаКПередачеНМА
	|		ПО Операция.ДокументОснование = ОснованиеПодготовкаКПередачеНМА.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС КАК ОснованиеПодготовкаКПередачеОС
	|		ПО Операция.ДокументОснование = ОснованиеПодготовкаКПередачеОС.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления
	|	И НЕ Операция.РеализацияНаОсновании
	|	И (ТабличнаяЧасть.СтатьяДоходов.ДоходыПоОбъектамЭксплуатации
	|			ИЛИ ТабличнаяЧасть.СтатьяДоходов.ДоходыПоНМАиНИОКР)
	|	И ОснованиеПодготовкаКПередачеНМА.Ссылка ЕСТЬ NULL 
	|	И ОснованиеПодготовкаКПередачеОС.Ссылка ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокументы.Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА СтатьиДоходов.ДоходыПоОбъектамЭксплуатации
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОС,
	|	СУММА(ВЫБОР
	|			КОГДА СтатьиДоходов.ДоходыПоНМАиНИОКР
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоНМА,
	|	СУММА(ВЫБОР
	|			КОГДА СтатьиДоходов.ДоходыПоОбъектамЭксплуатации
	|					ИЛИ СтатьиДоходов.ДоходыПоНМАиНИОКР
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК КоличествоПрочих
	|ПОМЕСТИТЬ втСсылкиИКоличествоТипов
	|ИЗ
	|	втДокументы КАК втДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТабличнаяЧасть
	|		ПО втДокументы.Ссылка = ТабличнаяЧасть.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
	|		ПО (ТабличнаяЧасть.СтатьяДоходов = СтатьиДоходов.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втДокументы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(ПодготовкаКПередачеНМАОперация.Ссылка, ПодготовкаКПередачеОСТабличнаяЧасть.Ссылка)) КАК КоличествоРазличныхОснований
	|ПОМЕСТИТЬ втСоответствиеДокументов
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСсылкиИКоличествоТипов КАК втСсылкиИКоличествоТипов
	|		ПО Операция.Ссылка = втСсылкиИКоличествоТипов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА КАК ПодготовкаКПередачеНМАОперация
	|		ПО (ПодготовкаКПередачеНМАОперация.Проведен)
	|			И Операция.Дата >= ПодготовкаКПередачеНМАОперация.Дата
	|			И Операция.Организация = ПодготовкаКПередачеНМАОперация.Организация
	|			И Операция.Подразделение = ПодготовкаКПередачеНМАОперация.Подразделение
	|			И Операция.Партнер = ПодготовкаКПередачеНМАОперация.Партнер
	|			И Операция.Контрагент = ПодготовкаКПередачеНМАОперация.Контрагент
	|			И (ТабличнаяЧасть.АналитикаДоходов = ПодготовкаКПередачеНМАОперация.НематериальныйАктив)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС КАК ПодготовкаКПередачеОСОперация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС.ОС КАК ПодготовкаКПередачеОСТабличнаяЧасть
	|			ПО ПодготовкаКПередачеОСОперация.Ссылка = ПодготовкаКПередачеОСТабличнаяЧасть.Ссылка
	|		ПО (ПодготовкаКПередачеОСОперация.ПередачаВыполнена)
	|			И (ПодготовкаКПередачеОСОперация.Проведен)
	|			И (Операция.Дата >= НАЧАЛОПЕРИОДА(ПодготовкаКПередачеОСОперация.ДатаПередачи, ДЕНЬ))
	|			И Операция.Организация = ПодготовкаКПередачеОСОперация.Организация
	|			И Операция.Подразделение = ПодготовкаКПередачеОСОперация.Подразделение
	|			И Операция.Партнер = ПодготовкаКПередачеОСОперация.Партнер
	|			И Операция.Контрагент = ПодготовкаКПередачеОСОперация.Контрагент
	|			И (ТабличнаяЧасть.АналитикаДоходов = ПодготовкаКПередачеОСТабличнаяЧасть.ОсновноеСредство)
	|ГДЕ
	|	(втСсылкиИКоличествоТипов.КоличествоОС > 0
	|			ИЛИ втСсылкиИКоличествоТипов.КоличествоНМА > 0)
	|	И втСсылкиИКоличествоТипов.КоличествоПрочих = 0
	|	И НЕ ЕСТЬNULL(ПодготовкаКПередачеНМАОперация.Ссылка, ПодготовкаКПередачеОСТабличнаяЧасть.Ссылка) ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСоответствиеДокументов.Ссылка
	|ПОМЕСТИТЬ втДокументыКЗаполнениюОснования
	|ИЗ
	|	втСоответствиеДокументов КАК втСоответствиеДокументов
	|ГДЕ
	|	втСоответствиеДокументов.КоличествоРазличныхОснований = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//-- НЕ УТ
	|ВЫБРАТЬ
	|	РеализацияУслугПрочихАктивов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК РеализацияУслугПрочихАктивов
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументыКЗаполнениюОснования КАК втДокументыКЗаполнениюОснования
	|		ПО РеализацияУслугПрочихАктивов.Ссылка = втДокументыКЗаполнениюОснования.Ссылка
	//-- НЕ УТ
	|ГДЕ
	|	(РеализацияУслугПрочихАктивов.УдалитьПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)
	|			ИЛИ (РеализацияУслугПрочихАктивов.Курс = 0 И НЕ РеализацияУслугПрочихАктивов.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) 
	|			И НЕ РеализацияУслугПрочихАктивов.ВалютаВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	//++ НЕ УТ
	|			ИЛИ НЕ втДокументыКЗаполнениюОснования.Ссылка ЕСТЬ NULL 
	//-- НЕ УТ
	|	)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОбработке.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Операция.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.РеализацияУслугПрочихАктивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК Строки
	|			ПО Строки.Ссылка = Операция.Ссылка
	|	ГДЕ
	|		Операция.Проведен
	|		И Строки.УдалитьСтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|	) КАК КОбработке
	|";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияУслугПрочихАктивов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК РеализацияУслугПрочихАктивов
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.РеализацияУслугПрочихАктивов.Доходы КАК РеализацияУслугПрочихАктивовДоходы
	|			ГДЕ
	|				РеализацияУслугПрочихАктивов.Ссылка = РеализацияУслугПрочихАктивовДоходы.Ссылка
	|				И РеализацияУслугПрочихАктивовДоходы.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|				И РеализацияУслугПрочихАктивовДоходы.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))
	|	ИЛИ РеализацияУслугПрочихАктивов.УдалитьПорядокОплаты <> ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)
	|	ИЛИ (ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.РеализацияУслугПрочихАктивов.РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
	|			ГДЕ
	|				ТЧРасшифровкаПлатежа.Ссылка = РеализацияУслугПрочихАктивов.Ссылка
	|				И ТЧРасшифровкаПлатежа.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|				И ТЧРасшифровкаПлатежа.УдалитьЗаказ НЕ В (&ПустыеЗначенияОбъектРасчетов)))
	|	ИЛИ РеализацияУслугПрочихАктивов.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка
	|ПОМЕСТИТЬ ВТОбъектыДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.Договор.УдалитьПорядокОплаты
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСКлиентами.ПустаяСсылка)
	|				И НЕ ОбъектыДляОбработки.Ссылка.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.Соглашение.УдалитьПорядокОплаты
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОбъектыДляОбработки.Ссылка.ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах)
	|			КОНЕЦ
	|	КОНЕЦ КАК УдалитьПорядокОплаты,
	|	ВЫБОР
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.БанковскийСчетОрганизации = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.БанковскийСчетОрганизации.ВалютаДенежныхСредств
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ КАК ВалютаОплаты,
	|	ОбъектыДляОбработки.Ссылка.Дата КАК Дата,
	|	ОбъектыДляОбработки.Ссылка.Валюта КАК Валюта,
	|	ОбъектыДляОбработки.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ОбъектыДляОбработки.Ссылка.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ОбъектыДляОбработки.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	ОбъектыДляОбработки.Ссылка.РеализацияНаОсновании КАК РеализацияНаОсновании
	|ПОМЕСТИТЬ ТаблицаСсылок
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСсылок.Дата КАК Дата,
	|	ТаблицаСсылок.Валюта КАК Валюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК ДатаКурса
	|ПОМЕСТИТЬ ДатыКурсовВалютыДокумента
	|ИЗ
	|	ТаблицаСсылок КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаСсылок.Валюта = КурсыВалют.Валюта
	|			И ТаблицаСсылок.Дата >= КурсыВалют.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСсылок.Дата,
	|	ТаблицаСсылок.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСсылок.Дата КАК Дата,
	|	ТаблицаСсылок.ВалютаВзаиморасчетов КАК Валюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК ДатаКурса
	|ПОМЕСТИТЬ ДатыКурсовВалютыВзаиморасчетов
	|ИЗ
	|	ТаблицаСсылок КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаСсылок.ВалютаВзаиморасчетов = КурсыВалют.Валюта
	|			И ТаблицаСсылок.Дата >= КурсыВалют.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСсылок.Дата,
	|	ТаблицаСсылок.ВалютаВзаиморасчетов
	|;
	|
	//++ НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ПодготовкаКПередачеНМАОперация.Ссылка, ЕСТЬNULL(ПодготовкаКПередачеОСОперация.Ссылка, НЕОПРЕДЕЛЕНО))) КАК ДокументОснование
	|ПОМЕСТИТЬ втДокументыКЗаполнениюОснования
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|		ПО Операция.Ссылка = ОбъектыДляОбработки.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА КАК ПодготовкаКПередачеНМАОперация
	|		ПО (ПодготовкаКПередачеНМАОперация.Проведен)
	|			И Операция.Дата >= ПодготовкаКПередачеНМАОперация.Дата
	|			И Операция.Организация = ПодготовкаКПередачеНМАОперация.Организация
	|			И Операция.Подразделение = ПодготовкаКПередачеНМАОперация.Подразделение
	|			И Операция.Партнер = ПодготовкаКПередачеНМАОперация.Партнер
	|			И Операция.Контрагент = ПодготовкаКПередачеНМАОперация.Контрагент
	|			И (ТабличнаяЧасть.АналитикаДоходов = ПодготовкаКПередачеНМАОперация.НематериальныйАктив)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС КАК ПодготовкаКПередачеОСОперация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС.ОС КАК ПодготовкаКПередачеОСТабличнаяЧасть
	|			ПО ПодготовкаКПередачеОСОперация.Ссылка = ПодготовкаКПередачеОСТабличнаяЧасть.Ссылка
	|		ПО (ПодготовкаКПередачеОСОперация.ПередачаВыполнена)
	|			И (ПодготовкаКПередачеОСОперация.Проведен)
	|			И (Операция.Дата >= НАЧАЛОПЕРИОДА(ПодготовкаКПередачеОСОперация.ДатаПередачи, ДЕНЬ))
	|			И Операция.Организация = ПодготовкаКПередачеОСОперация.Организация
	|			И Операция.Подразделение = ПодготовкаКПередачеОСОперация.Подразделение
	|			И Операция.Партнер = ПодготовкаКПередачеОСОперация.Партнер
	|			И Операция.Контрагент = ПодготовкаКПередачеОСОперация.Контрагент
	|			И (ТабличнаяЧасть.АналитикаДоходов = ПодготовкаКПередачеОСТабличнаяЧасть.ОсновноеСредство)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыДляОбработки.Ссылка
	|;
	|
	//-- НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка КАК Ссылка,
	//++ НЕ УТ
	|	НЕ ЕСТЬNULL(ПодготовкаКПередачеНМАОперация.Ссылка, ПодготовкаКПередачеОСОперация.Ссылка) ЕСТЬ NULL КАК ТребуетсяЗаполнениеОснования,
	|	ЕСТЬNULL(ПодготовкаКПередачеНМАОперация.Ссылка, ПодготовкаКПередачеОСОперация.Ссылка) КАК ДокументОснование,
	//-- НЕ УТ
	|	ДанныеДляОбработки.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДляОбработки.ВалютаОплаты = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)  
	|   	ТОГДА
	|	    	ВЫБОР КОГДА ДанныеДляОбработки.УдалитьПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)  
	|           	ТОГДА   
	|	    			ВЫБОР КОГДА ДанныеДляОбработки.ВалютаВзаиморасчетов = &ВалютаРеглУчета 
	|           			ТОГДА   
	|               			ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|						ИНАЧЕ
	|							ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах) 
	|           		КОНЕЦ
	|				ИНАЧЕ
	|               	ДанныеДляОбработки.УдалитьПорядокОплаты
	|           КОНЕЦ
	|   	ИНАЧЕ
	|	    	ВЫБОР КОГДА ДанныеДляОбработки.ВалютаВзаиморасчетов = &ВалютаРеглУчета 
	|           	ТОГДА   
	|               	ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|				ИНАЧЕ
	|               	ВЫБОР КОГДА ДанныеДляОбработки.ВалютаОплаты = &ВалютаРеглУчета 
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах) 
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте) 
	|					КОНЕЦ 
	|           КОНЕЦ
	|   КОНЕЦ КАК УдалитьПорядокОплаты,
	|	ДанныеДляОбработки.Валюта КАК ВалютаДокумента,
	|	ДанныеДляОбработки.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДляОбработки.ВалютаОплаты КАК ВалютаОплаты,
	|	ДанныеДляОбработки.СуммаДокумента КАК СуммаДокумента,
	|	ДанныеДляОбработки.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	КурсыВалютыДокумента.Кратность КАК КратностьВалютыДокумента,
	|	КурсыВалютыВзаиморасчетов.Кратность КАК КратностьВалютыВзаиморасчетов
	|ИЗ
	|	ТаблицаСсылок КАК ДанныеДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыКурсовВалютыДокумента КАК ДатыКурсовВалютыДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютыДокумента
	|			ПО ДатыКурсовВалютыДокумента.ДатаКурса = КурсыВалютыДокумента.Период
	|				И ДатыКурсовВалютыДокумента.Валюта = КурсыВалютыДокумента.Валюта
	|		ПО ДанныеДляОбработки.Валюта = ДатыКурсовВалютыДокумента.Валюта
	|			И ДанныеДляОбработки.Дата = ДатыКурсовВалютыДокумента.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыКурсовВалютыВзаиморасчетов КАК ДатыКурсовВалютыВзаиморасчетов
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютыВзаиморасчетов
	|			ПО ДатыКурсовВалютыВзаиморасчетов.ДатаКурса = КурсыВалютыВзаиморасчетов.Период
	|				И ДатыКурсовВалютыВзаиморасчетов.Валюта = КурсыВалютыВзаиморасчетов.Валюта
	|		ПО ДанныеДляОбработки.ВалютаВзаиморасчетов = ДатыКурсовВалютыВзаиморасчетов.Валюта
	|			И ДанныеДляОбработки.Дата = ДатыКурсовВалютыВзаиморасчетов.Дата
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументыКЗаполнениюОснования КАК ДокументыКЗаполнениюОснования
	|		ПО ДанныеДляОбработки.Ссылка = ДокументыКЗаполнениюОснования.Ссылка
	|			И (НЕ ДанныеДляОбработки.РеализацияНаОсновании)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА КАК ПодготовкаКПередачеНМАОперация
	|		ПО (ДокументыКЗаполнениюОснования.ДокументОснование = ПодготовкаКПередачеНМАОперация.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС КАК ПодготовкаКПередачеОСОперация
	|		ПО (ДокументыКЗаполнениюОснования.ДокументОснование = ПодготовкаКПередачеОСОперация.Ссылка)
	//-- НЕ УТ
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);

	Документ = Запрос.Выполнить().Выбрать();
	
	Пока Документ.Следующий() Цикл
		
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				Если ДокументОбъект.Курс = 0 И ЗначениеЗаполнено(ДокументОбъект.Валюта) И ЗначениеЗаполнено(ДокументОбъект.ВалютаВзаиморасчетов) Тогда
					Если Документ.СуммаВзаиморасчетов = 0 ИЛИ Документ.СуммаДокумента = 0 ИЛИ Документ.ВалютаДокумента = Документ.ВалютаВзаиморасчетов Тогда
						ДокументОбъект.Курс = 1;
						ДокументОбъект.Кратность = 1;
					ИначеЕсли Документ.ВалютаДокумента = ВалютаРеглУчета И НЕ Документ.ВалютаВзаиморасчетов = ВалютаРеглУчета Тогда
						ДокументОбъект.Курс = Окр(Документ.СуммаДокумента / Документ.СуммаВзаиморасчетов * Документ.КратностьВалютыВзаиморасчетов, 4);
						ДокументОбъект.Кратность = Документ.КратностьВалютыВзаиморасчетов;
					Иначе
						ДокументОбъект.Курс = Окр(Документ.СуммаВзаиморасчетов / Документ.СуммаДокумента * Документ.КратностьВалютыДокумента, 4);
						ДокументОбъект.Кратность = Документ.КратностьВалютыДокумента;
					КонецЕсли;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				//++ НЕ УТ
				Если Документ.ТребуетсяЗаполнениеОснования Тогда
					ДокументОбъект.ДокументОснование = Документ.ДокументОснование;
					ДокументОбъект.РеализацияНаОсновании = Истина;
					ОбъектИзменен = Истина;
				КонецЕсли;
				//-- НЕ УТ
				
				Для Каждого СтрокаТЧ Из ДокументОбъект.Доходы Цикл
					
					Если НЕ ЗначениеЗаполнено(СтрокаТЧ.УдалитьСтавкаНДС) И НЕ ЗначениеЗаполнено(СтрокаТЧ.СтавкаНДС) Тогда
						СтрокаТЧ.УдалитьСтавкаНДС 	= Перечисления.СтавкиНДС.НеНДС;
						СтрокаТЧ.СтавкаНДС 			= Справочники.СтавкиНДС.НеНДС;
						ОбъектИзменен = Истина;
					КонецЕсли;
					
				КонецЦикла;
				
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("Доходы");
				
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				Если НЕ ЗначениеЗаполнено(ДокументОбъект.УдалитьПорядокОплаты) Тогда
					ДокументОбъект.ОплатаВВалюте = (Документ.УдалитьПорядокОплаты = Перечисления.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте);
					ДокументОбъект.УдалитьПорядокОплаты = Документ.УдалитьПорядокОплаты;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				ОбъектыРасчетовСервер.ЗаполнитьОбъектыРасчетов(ДокументОбъект, ОбъектИзменен);
				
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов(Параметры) Экспорт

	ОбъектыРасчетовСервер.ЗарегистрироватьДанныеДляГенерацииОбъектовРасчета(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

Процедура СгенерироватьОбъектыРасчетов(Параметры) Экспорт
	
	ОбъектыРасчетовСервер.СгенерироватьОбъектыРасчетов(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект
//	см. ОбщегоНазначения.ЗначениеРеквизитаОбъекта
// Если полученное значение не имеет тип булево, возвращается значение Ложь.
//
// Возвращаемое значение:
//	Произвольный - зависит от типа значения прочитанного реквизита.
//               - если в параметр Ссылка передана пустая ссылка, то возвращается Неопределено;
//               - если в параметр Ссылка передана ссылка несуществующего объекта (битая ссылка), 
//                 то возвращается Неопределено.
//
Функция ЗначениеРеквизитаОбъектаТипаБулево(Ссылка, ИмяРеквизита) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	Если ТипЗнч(Результат) <> Тип("Булево") Тогда
		Результат = Ложь;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);	
	Возврат Результат
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
