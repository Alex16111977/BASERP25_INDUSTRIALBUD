#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("ТМЦВЭксплуатации");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТМЦВЭксплуатации(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Списание из эксплуатации".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеИзЭксплуатации) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеИзЭксплуатации.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеИзЭксплуатации);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнутреннееПотребление";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
	Отчеты.МатериалыВЭксплуатации.ДобавитьКомандуОтчета(КомандыОтчетов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеИзЭксплуатации";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата КАК Период,
	|	ДанныеШапки.Номер КАК Номер,
	|	ДанныеШапки.Организация КАК Организация,
	|	ДанныеШапки.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеИзЭксплуатации"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Списание из эксплуатации';uk='Списання з експлуатації'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СписаниеИзЭксплуатации);
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ТМЦВЭксплуатации(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТМЦВЭксплуатации";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации КАК Партия,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначение,
	|	-ТабличнаяЧасть.Количество КАК Количество
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.Дата КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР()
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции

#КонецОбласти

#Область ПроводкиРеглУчетаУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
	
#Область СписаниеИзЭксплуатации // (Дт 1322 :: Кт 111, 1122)
	СписаниеИзЭксплуатации =  "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Списание из эксплуатации товаров (Дт 1322 :: Кт 111, 1122)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	втСтоимостиТМЦВЭксплуатации.Стоимость КАК Сумма, 
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьУУ КАК СуммаУУ, 
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииАмортизация) КАК ВидСчетаДт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоДт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	втСтоимостиТМЦВЭксплуатации.Количество КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоКт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	втСтоимостиТМЦВЭксплуатации.Количество КАК КоличествоКт,
	|	втСтоимостиТМЦВЭксплуатации.Стоимостьну КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеИзЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		втСтоимостиТМЦВЭксплуатации КАК втСтоимостиТМЦВЭксплуатации
	|	ПО
	|		Операция.Ссылка = втСтоимостиТМЦВЭксплуатации.Ссылка
	|"; 

	СписаниеИзЭксплуатации = СтрШаблон(СписаниеИзЭксплуатации, 
		НСтр("ru='Списание ТМЦ из эксплуатации';uk='Списання ТМЦ з експлуатації'",ЯзыкСодержания));

#КонецОбласти 	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(СписаниеИзЭксплуатации);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаТЦМВЭксплуатации
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)
	|	И ПланСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|	И НЕ ПланСчетов.Забалансовый
	|	И ПланСчетов.Количественный
	|	И НЕ ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеИзЭксплуатацииТовары.Ссылка КАК Ссылка,
	|	СписаниеИзЭксплуатацииТовары.Ссылка.Организация КАК Организация,
	|	СписаниеИзЭксплуатацииТовары.Ссылка.Подразделение КАК Подразделение,
	|	СписаниеИзЭксплуатацииТовары.Номенклатура КАК Номенклатура,
	|	СписаниеИзЭксплуатацииТовары.Характеристика,
	|	ВЫРАЗИТЬ(СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК Справочник.ФизическиеЛица) КАК ФизическоеЛицо,
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СУММА(СписаниеИзЭксплуатацииТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ втСписаниеТЦМ
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|ГДЕ
	|	СписаниеИзЭксплуатацииТовары.Ссылка = &Ссылка
	|	И СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В (ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив), ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.БиблиотечныеФонды))
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеИзЭксплуатацииТовары.Номенклатура,
	|	СписаниеИзЭксплуатацииТовары.Характеристика,
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации,
	|	СписаниеИзЭксплуатацииТовары.Ссылка.Организация,
	|	СписаниеИзЭксплуатацииТовары.Ссылка.Подразделение,
	|	СписаниеИзЭксплуатацииТовары.Ссылка,
	|	ВЫРАЗИТЬ(СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК Справочник.ФизическиеЛица),
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСписаниеТЦМ.Ссылка,
	|	втСписаниеТЦМ.Номенклатура КАК Номенклатура,
	|	втСписаниеТЦМ.Характеристика КАК Характеристика,
	|	втСписаниеТЦМ.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	втСписаниеТЦМ.ФизическоеЛицо,
	|	втСписаниеТЦМ.Количество,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	втСписаниеТЦМ.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ХозрасчетныйОстатки.НалоговоеНазначение,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = втСписаниеТЦМ.Количество
	|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)
	|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) * втСписаниеТЦМ.Количество / ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0)
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = втСписаниеТЦМ.Количество
	|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаУУОстатокДт, 0)
	|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаУУОстатокДт, 0) * втСписаниеТЦМ.Количество / ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0)
	|	КОНЕЦ КАК СтоимостьУУ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = втСписаниеТЦМ.Количество
	|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0)
	|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0) * втСписаниеТЦМ.Количество / ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0)
	|	КОНЕЦ КАК СтоимостьНУ
	|ПОМЕСТИТЬ втСтоимостиТМЦВЭксплуатации
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Ссылка
	|				ИЗ
	|					втСчетаТЦМВЭксплуатации КАК Т),
	|			,
	|			(Организация, Подразделение, Субконто1, Субконто2) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.Номенклатура,
	|					Т.ПартияТМЦВЭксплуатации
	|				ИЗ
	|					втСписаниеТЦМ КАК Т)) КАК ХозрасчетныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСписаниеТЦМ КАК втСписаниеТЦМ
	|		ПО ХозрасчетныйОстатки.Субконто1 = втСписаниеТЦМ.Номенклатура
	|			И ХозрасчетныйОстатки.Субконто2 = втСписаниеТЦМ.ПартияТМЦВЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ПартияТМЦВЭксплуатации;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Функция формирует табличный документ с типовой печатной формой ОЗ-1
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьОЗ3(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);

	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОЗ3";
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_ОЗ3");
	
	ПервыйДокумент = Истина;
	
	ВидТМЦВЭксплуатации = Новый Массив;
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив);
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.БиблиотечныеФонды);
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеИзЭксплуатации.Номер 	        КАК НомерАкта,
		|	СписаниеИзЭксплуатации.Дата	            КАК ДатаАкта,
		|	СписаниеИзЭксплуатации.Организация      КАК Организация,
		|	СписаниеИзЭксплуатации.Подразделение	КАК СдалоПодразделение,
		|	""Списання"" 							КАК ВидОперации
		|ИЗ
		|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
		|
		|ГДЕ
		|	СписаниеИзЭксплуатации.Ссылка = &Ссылка";

		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ВидТМЦ", ВидТМЦВЭксплуатации);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеИзЭксплуатацииТовары.Номенклатура,
		|   СписаниеИзЭксплуатацииТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
		|	СписаниеИзЭксплуатацииТовары.Характеристика,
		|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК Сотрудник,
		|	СУММА(СписаниеИзЭксплуатацииТовары.Количество) КАК Количество,
		|	МИНИМУМ(СписаниеИзЭксплуатацииТовары.НомерСтроки) КАК НомерСтроки
		|ИЗ
		|	Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
		|ГДЕ
		|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В(&ВидТМЦ)
		|	И СписаниеИзЭксплуатацииТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СписаниеИзЭксплуатацииТовары.Номенклатура,
		|	СписаниеИзЭксплуатацииТовары.Характеристика,
		|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабДокумент.Вывести(ОбластьМакета);
			Возврат ТабДокумент;
		КонецЕсли;
	
		Выборка = РезультатЗапроса.Выбрать();
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);
		
		Пока Выборка.Следующий() Цикл

			ОбластьМакета = Макет.ПолучитьОбласть("ОЗ3");
			Параметры     = ОбластьМакета.Параметры;
			
			Параметры.Заполнить(Шапка);
			Параметры.Заполнить(Выборка);
			Параметры.Заполнить(ВыборкаПоКомиссии);

			Параметры.НаименованиеОС = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
													Выборка.НоменклатураНаименование,
													Выборка.Характеристика);

			
			СведенияОбОрганизации	= ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Ссылка.Организация, Ссылка.Дата);	
			Параметры.Организация	= ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,",,);
			Параметры.ЕДРПОУ		= СведенияОбОрганизации.КодПоЕДРПОУ;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;
	
КонецФункции // ПечатьОЗ1()

// Функция формирует печатную форму документа
//
Функция ПечатьМШ7(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Подразделение) КАК Подразделение,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.Префикс КАК Префикс,
	|	Документ.ЧерезКого КАК МОЛ,
	|	""Списання"" КАК КодОперации,
	|	Документ.Товары.(
	|		НомерСтроки КАК НомерПП,
	|		Номенклатура КАК Номенклатура,
	|       Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|		Номенклатура.Код КАК НоменклатурныйНомерКод,
	|		Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|		Характеристика.НаименованиеПолное КАК Характеристика,
	|		Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|		Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|		Количество КАК Количество,
	|		ПартияТМЦВЭксплуатации.Дата КАК ДатаПоступленияВЭксплуатацию,
	|		ПартияТМЦВЭксплуатации.СрокЭксплуатации КАК СрокСлужбы,
	|		ФизическоеЛицо  КАК Сотрудник
	|	) КАК ТаблицаТовары
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Товары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ = &ВидТМЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерПП";
	
	ЗапросПоДокументам.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросПоДокументам.УстановитьПараметр("ВидТМЦ",Перечисления.ВидыТМЦВЭксплуатации.МалоценныйБыстроизнашивающийсяПредмет);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеИзЭксплуатации_МШ7";
	
	ТоварКод = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(ТоварКод) Тогда
		ТоварКод = "Код";
	КонецЕсли;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_МШ7");
	
	
	Если ЗапросПоДокументам.Выполнить().Пустой() Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабДокумент.Вывести(ОбластьМакета);
			
			Возврат ТабДокумент;
			
		КонецЕсли;

	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодписи   = Макет.ПолучитьОбласть("Подвал");
	ОбластьСтрока	 = Макет.ПолучитьОбласть("Строка");
	
	ПервыйДокумент = Истина;
	
	ВыборкаДокументы = ЗапросПоДокументам.Выполнить().Выбрать();
	
	Пока ВыборкаДокументы.Следующий() Цикл
		ВыборкаСтроки = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		СписокФизическихЛиц = Новый Массив;
		Пока ВыборкаСтроки.Следующий() Цикл
			СписокФизическихЛиц.Добавить(ВыборкаСтроки.Сотрудник);
		КонецЦикла;
	КонецЦикла;
		
	ОсновныеСотрудникиФизическихЛиц = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(СписокФизическихЛиц, Истина, ВыборкаДокументы.Организация, ВыборкаДокументы.Дата);
	СписокСотрудников = ОсновныеСотрудникиФизическихЛиц.ВыгрузитьКолонку("Сотрудник");
	Если ЗначениеЗаполнено(СписокСотрудников) Тогда
		ДанныеФизЛица = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, "ФизическоеЛицо, ТабельныйНомер", ВыборкаДокументы.Ссылка.Дата);
	КонецЕсли;
	
	ВыборкаДокументы.Сбросить();
	ВыборкаСтроки.Сбросить();
	
	Пока ВыборкаДокументы.Следующий() Цикл
	
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		ОбластьЗаголовок.Параметры.Заполнить(ВыборкаДокументы);
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация, ВыборкаДокументы.Дата);	
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации  = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,",,КодЯзыкаПечать);
		ОбластьЗаголовок.Параметры.КодПоЕДРПОУ  = СведенияОбОрганизации.КодПоЕДРПОУ;	
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументы.Номер, Ложь, Истина);
		ОбластьЗаголовок.Параметры.ДатаДокумента =  ВыборкаДокументы.Дата;
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабДокумент, Макет, ОбластьЗаголовок, ВыборкаДокументы.Ссылка);
		ТабДокумент.Вывести(ОбластьЗаголовок);
		
		// Вывод строк документа.
		ВыборкаСтроки = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		Пока ВыборкаСтроки.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			ОбластьСтрока.Параметры.ТабельныйНомер	= "";
			Если ДанныеФизЛица <> Неопределено Тогда
				СтрокаТЗ = ДанныеФизЛица.Найти(ВыборкаСтроки.Сотрудник, "ФизическоеЛицо");
			КонецЕсли;
			
			Если СтрокаТЗ <> Неопределено Тогда
				ОбластьСтрока.Параметры.ТабельныйНомер	= СтрокаТЗ.ТабельныйНомер;
			КонецЕсли;
			
			Если ТоварКод = "Артикул" Тогда
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерАртикул;
			Иначе
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерКод;
			КонецЕсли;
			
			ОбластьСтрока.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаСтроки.НоменклатураНаименование,
				ВыборкаСтроки.Характеристика);

			ТабДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		// Вывод подвала.
		ОбластьПодписи.Параметры.Заполнить(ВыборкаДокументы);
		ТабДокумент.Вывести(ОбластьПодписи);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);

	КонецЦикла;
		
	Возврат ТабДокумент;

КонецФункции // ПечатьМШ7()

// Функция формирует печатную форму документа
//
Функция ПечатьМШ8(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Подразделение) КАК Подразделение,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.Префикс КАК Префикс,
	|	Документ.ЧерезКого КАК МОЛ,
	|	""Списання"" КАК КодОперации,
	|	Документ.Товары.(
	|		НомерСтроки КАК НомерПП,
	|		Номенклатура КАК Номенклатура,
	|       Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|		Номенклатура.Код КАК НоменклатурныйНомерКод,
	|		Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|		Характеристика.НаименованиеПолное КАК Характеристика,
	|		Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|		Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|		Количество КАК Количество,
	|		ПартияТМЦВЭксплуатации.Дата КАК ДатаПоступления,
	|		ПартияТМЦВЭксплуатации.СрокЭксплуатации КАК СрокСлужбы,
	|		ФизическоеЛицо  КАК Сотрудник
	|	) КАК ТаблицаТовары
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Товары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ = &ВидТМЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерПП";
	
	ЗапросПоДокументам.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросПоДокументам.УстановитьПараметр("ВидТМЦ",Перечисления.ВидыТМЦВЭксплуатации.МалоценныйБыстроизнашивающийсяПредмет);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеИзЭксплуатации_МШ8";
	
	ТоварКод = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(ТоварКод) Тогда
		ТоварКод = "Код";
	КонецЕсли;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеИзЭксплуатации.ПФ_MXL_UK_МШ8");
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если ЗапросПоДокументам.Выполнить().Пустой() Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабДокумент.Вывести(ОбластьМакета);
			
			Возврат ТабДокумент;
			
		КонецЕсли;

	
	ОбластьЗаголовок 	= Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока	 	= Макет.ПолучитьОбласть("Строка");
	ОбластьПодписи   	= Макет.ПолучитьОбласть("Подвал");
	ОбластьМеталлолом   = Макет.ПолучитьОбласть("Металлолом");
	
	ПервыйДокумент = Истина;
	
	ВыборкаДокументы = ЗапросПоДокументам.Выполнить().Выбрать();
	Пока ВыборкаДокументы.Следующий() Цикл
	
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		ОбластьЗаголовок.Параметры.Заполнить(ВыборкаДокументы);
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация, ВыборкаДокументы.Дата);	
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации  = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,",,КодЯзыкаПечать);
		ОбластьЗаголовок.Параметры.КодПоЕДРПОУ  = СведенияОбОрганизации.КодПоЕДРПОУ;
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументы.Номер, Ложь, Истина);
		ОбластьЗаголовок.Параметры.ДатаДокумента =  ВыборкаДокументы.Дата;
		ОбластьЗаголовок.Параметры.Валюта = ВалютаРегламентированногоУчета;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабДокумент, Макет, ОбластьЗаголовок, ВыборкаДокументы.Ссылка);
		ТабДокумент.Вывести(ОбластьЗаголовок);
		
		// Вывод строк документа.
		ВыборкаСтроки = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		Пока ВыборкаСтроки.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			
			Если ТоварКод = "Артикул" Тогда
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерАртикул;
			Иначе
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерКод;
			КонецЕсли;
			
			ОбластьСтрока.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаСтроки.НоменклатураНаименование,
				ВыборкаСтроки.Характеристика);
				
			ТабДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		// Вывод подвала.
		ОбластьПодписи.Параметры.Заполнить(ВыборкаДокументы);
		ТабДокумент.Вывести(ОбластьПодписи);
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(ВыборкаДокументы.Ссылка);
		
		ОбластьМеталлолом.Параметры.Заполнить(ВыборкаПоКомиссии);
		
		ТабДокумент.Вывести(ОбластьМеталлолом);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);

	КонецЦикла;
		
	Возврат ТабДокумент;

КонецФункции // ПечатьМШ8()

// Функция получает данные для формирования печатной формы М-11
//
Функция ПолучитьДанныеДляПечатнойФормыМ11(ПараметрыПечати, МассивДокументов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивДокументов, 
		МенеджерВременныхТаблиц,
		,
		Новый Структура("Руководитель, ГлавныйБухгалтер", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	   |	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)КАК Упаковка,
	|	Товары.Количество КАК Количество,
	|	Товары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	Товары.ФизическоеЛицо,
	|	НЕОПРЕДЕЛЕНО КАК ФизическоеЛицоПолучатель
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 		[1] - Выборка по шапкам документов
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.Дата КАК ДатаСоставления,
	|	Документы.Организация КАК Организация,
	|	Документы.Организация.Префикс КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	Документы.Подразделение КАК Подразделение,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК РуководительОтправителя,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителяОтправителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтерОтправителя,
	|	""Списання"" КАК КодВидаОперации,
	|	НЕОПРЕДЕЛЕНО КАК Заказ,
	|	НЕОПРЕДЕЛЕНО КАК УчетныйВидЦены,
	|	НЕОПРЕДЕЛЕНО КАК УчетныйВидЦеныВалютаЦены
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК Документы
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО Документы.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	Документы.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК КладовщикОтправитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщикаОтправителя,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	Товары.Номенклатура.Код КАК НоменклатурныйНомерКод,
	|	Товары.Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|	Товары.Характеристика.НаименованиеПолное КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО КАК Упаковка,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	Товары.Количество КАК Количество,
	|	0 КАК Сумма,
	|	0 КАК Цена,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка,
	|	Склад
	|";
	
		
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакет();
	// Пакет запросов:
	// 		[0] - Временная таблица по табличной части документа
   	// 		[1] - Выборка по шапкам документов
	// 		[2] - Выборка по табличным частям документов
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПакетаЗапросов[1],
		РезультатПакетаЗапросов[2]);
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Ведомость МШ-7 
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МШ7";
	КомандаПечати.Представление = НСтр("ru='Ведомость МШ-7';uk='Відомість МШ-7'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Ведомость МШ-8
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МШ8";
	КомандаПечати.Представление = НСтр("ru='Ведомость МШ-8';uk='Відомість МШ-8'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	// Требование-накладная (М-11)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьМ11";
	КомандаПечати.Идентификатор = "М11";
	КомандаПечати.Представление = НСтр("ru='Требование-накладная (М-11)';uk='Вимога-накладна (М-11)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОЗ3";
	КомандаПечати.Идентификатор = "ОЗ3";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-3';uk='Форма ОЗ-3'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьФормОСБюджет";
	КомандаПечати.Идентификатор = "АктСписанияОСБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт списания основных средств (частичной ликвидации)';uk='Акт списання основних засобів (часткової ліквідації)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьФормОСБюджет";
	КомандаПечати.Идентификатор = "АктСписанияОСГруппаБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт списания группы основных средств';uk='Акт списання групи основних засобів'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МШ7") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МШ7", НСтр("ru='Ведомость МШ-7';uk='Відомість МШ-7'"),
			ПечатьМШ7(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета), , ИмяМакета);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МШ8") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МШ8", НСтр("ru='Ведомость МШ-8';uk='Відомість МШ-8'"),
			ПечатьМШ8(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета), , ИмяМакета);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОЗ3(ПараметрыПечати, Ссылка) Экспорт
	
	ВидТМЦВЭксплуатации = Новый Массив;
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив);
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.БиблиотечныеФонды);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатации.Номер 	        КАК НомерАкта,
	|	СписаниеИзЭксплуатации.Дата	            КАК ДатаАкта,
	|	СписаниеИзЭксплуатации.Организация      КАК Организация,
	|	СписаниеИзЭксплуатации.Подразделение	КАК СдалоПодразделение,
	|	""Списання"" 							КАК ВидОперации
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|
	|ГДЕ
	|	СписаниеИзЭксплуатации.Ссылка = &Ссылка";

	ВыборкаПоШапке = Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидТМЦ", ВидТМЦВЭксплуатации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатацииТовары.Номенклатура,
	|   СписаниеИзЭксплуатацииТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	СписаниеИзЭксплуатацииТовары.Характеристика,
	|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК Сотрудник,
	|	СУММА(СписаниеИзЭксплуатацииТовары.Количество) КАК Количество,
	|	МИНИМУМ(СписаниеИзЭксплуатацииТовары.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|ГДЕ
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В(&ВидТМЦ)
	|	И СписаниеИзЭксплуатацииТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеИзЭксплуатацииТовары.Номенклатура,
	|	СписаниеИзЭксплуатацииТовары.Характеристика,
	|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	ВыборкаПоОС = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС", ВыборкаПоШапке, ВыборкаПоОС);
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыАктСписанияОС_2016(ПараметрыПечати, Ссылка) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатации.Номер КАК НомерАкта,
	|	СписаниеИзЭксплуатации.Дата КАК ДатаАкта,
	|	СписаниеИзЭксплуатации.Организация КАК Организация,
	|	СписаниеИзЭксплуатации.Подразделение КАК СдалоПодразделение,
	|	СписаниеИзЭксплуатации.Основание КАК Основание
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|ГДЕ
	|	СписаниеИзЭксплуатации.Ссылка = &Ссылка";

	ВыборкаПоШапке = Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
		МассивСубконто = Новый Массив;
		МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);
		Запрос.УстановитьПараметр("Субконто", МассивСубконто);
		ВидТМЦВЭксплуатации = Новый Массив;
		ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив);
		ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.БиблиотечныеФонды);
		Запрос.УстановитьПараметр("ВидТМЦ", ВидТМЦВЭксплуатации);
		Запрос.УстановитьПараметр("МоментДокумента", Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("Организация"    , Ссылка.Организация);
		Запрос.УстановитьПараметр("Подразделение"  , Ссылка.Подразделение);
		Запрос.УстановитьПараметр("СчетМЦ"         , ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыКоличественно);
	Запрос.Текст =   
	
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатацииТовары.Номенклатура КАК Номенклатура,
	|	СписаниеИзЭксплуатацииТовары.Номенклатура.Код КАК ИнвентарныйНомер,
	|	СписаниеИзЭксплуатацииТовары.Номенклатура.НаименованиеПолное КАК НаименованиеОС,
	|	СписаниеИзЭксплуатацииТовары.Характеристика КАК Характеристика,
	|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК МОЛ,
	|	СписаниеИзЭксплуатацииТовары.Количество КАК Количество,
	|	0 КАК ЛиквидационнаяСтоимость,
	|	Хозрасчетный.СубконтоКт2.Дата КАК ДатаВвода,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Хозрасчетный.КоличествоОборотКт = 0
	|				ТОГДА Хозрасчетный.СуммаОборот
	|			ИНАЧЕ Хозрасчетный.СуммаОборот / Хозрасчетный.КоличествоОборотКт
	|		КОНЕЦ, 0) КАК ПервоначальнаяСтоимость
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&МоментДокумента, &МоментДокумента, , , , СчетКт = &СчетМЦ, &Субконто, ) КАК Хозрасчетный
	|		ПО СписаниеИзЭксплуатацииТовары.Номенклатура = Хозрасчетный.СубконтоКт1
	|			И СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации = Хозрасчетный.СубконтоКт2
	|ГДЕ
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В(&ВидТМЦ)
	|	И СписаниеИзЭксплуатацииТовары.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	МОЛ";

	ВыборкаПоОС = Запрос.Выполнить();
	
	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	Хозрасчетный.СчетДт КАК СчетДт,
	|	Хозрасчетный.СчетКт КАК СчетКт,
	|	Хозрасчетный.Сумма КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.НомерСтроки";
	
	ВыборкаБУ = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ", ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ);
	
КонецФункции //ПолучитьДанныеДляПечатнойФормыАктСписанияОС_2016()

Функция ПолучитьДанныеДляПечатнойФормыАктСписанияГруппаОС_2016(ПараметрыПечати, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатации.Номер КАК НомерАкта,
	|	СписаниеИзЭксплуатации.Дата КАК ДатаАкта,
	|	СписаниеИзЭксплуатации.Организация КАК Организация,
	|	СписаниеИзЭксплуатации.Подразделение КАК СдалоПодразделение,
	|	СписаниеИзЭксплуатации.Основание КАК Основание
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|ГДЕ
	|	СписаниеИзЭксплуатации.Ссылка = &Ссылка";

	ВыборкаПоШапке = Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);
	Запрос.УстановитьПараметр("Субконто", МассивСубконто);
	ВидТМЦВЭксплуатации = Новый Массив;
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив);
	ВидТМЦВЭксплуатации.Добавить(Перечисления.ВидыТМЦВЭксплуатации.БиблиотечныеФонды);
	Запрос.УстановитьПараметр("ВидТМЦ", ВидТМЦВЭксплуатации);
	Запрос.УстановитьПараметр("МоментДокумента", Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("Организация"    , Ссылка.Организация);
	Запрос.УстановитьПараметр("Подразделение"  , Ссылка.Подразделение);
	Запрос.УстановитьПараметр("СчетМЦ"         , ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыКоличественно);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеИзЭксплуатацииТовары.Номенклатура КАК Номенклатура,
	|	СписаниеИзЭксплуатацииТовары.Номенклатура.Код КАК ИнвентарныйНомер,
	|	СписаниеИзЭксплуатацииТовары.Характеристика КАК Характеристика,
	|	СписаниеИзЭксплуатацииТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	СписаниеИзЭксплуатацииТовары.ФизическоеЛицо КАК МОЛ,
	|	СписаниеИзЭксплуатацииТовары.Количество КАК Количество,
	|	Хозрасчетный.СубконтоКт2.Дата КАК ДатаВвода,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Хозрасчетный.КоличествоОборотКт = 0
	|				ТОГДА Хозрасчетный.СуммаОборот
	|			ИНАЧЕ Хозрасчетный.СуммаОборот / Хозрасчетный.КоличествоОборотКт
	|		КОНЕЦ, 0) КАК ПервоначальнаяСтоимость
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&МоментДокумента, &МоментДокумента, , , , СчетКт = &СчетМЦ, &Субконто, ) КАК Хозрасчетный
	|		ПО СписаниеИзЭксплуатацииТовары.Номенклатура = Хозрасчетный.СубконтоКт1
	|			И СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации = Хозрасчетный.СубконтоКт2
	|ГДЕ
	|	СписаниеИзЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В(&ВидТМЦ)
	|	И СписаниеИзЭксплуатацииТовары.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	МОЛ";

	ВыборкаПоОС = Запрос.Выполнить();
	
	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	Хозрасчетный.СчетДт КАК СчетДт,
	|	Хозрасчетный.СчетКт КАК СчетКт,
	|	Хозрасчетный.Сумма КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.НомерСтроки";
	
	ВыборкаБУ = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ", ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ);
		
КонецФункции //ПолучитьДанныеДляПечатнойФормыАктСписанияГруппаОС_2016()


#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
