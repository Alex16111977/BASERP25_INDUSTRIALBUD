
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	Параметры.Свойство("ПараметрыТоваров", ПараметрыТоваров);
	Параметры.Свойство("ПараметрыАналогов", ПараметрыАналогов);
	
	ЗаполнитьДанные(ПараметрыТоваров, ПараметрыАналогов);
	
	УстановитьВидимостьОстатков(Элементы, Параметры.ПараметрыОбщие.ПоказатьОстатки);
	
	ИмяСтраницы = ?(Материалы.Количество() = 1, "Аналоги", "Материалы");
	
	Если ИмяСтраницы = "Аналоги" Тогда
		
		Элементы.Материалы.ТекущаяСтрока = 0;
		
		Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаОстаткиМатериалов;
		
		Элементы.КнопкаОтменитьПодбор.Видимость = Истина;
		Элементы.ДатаДействияРазрешений.Видимость = Истина;
		
		Элементы.НадписьШаг2.Заголовок = НСтр("ru='Подбор аналогов';uk='Підбір аналогів'");
		
		УстановитьПредставлениеРазрешений(Материалы[0].Номенклатура, Материалы[0].Характеристика)
		
	КонецЕсли;	
	
	УстановитьЗаголовокГиперссылкиОстаткиИДоступностьТоваров(ИмяСтраницы);
	
	УстановитьТекущуюСтраницу(ЭтаФорма, ИмяСтраницы);
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ФормаОтметкиЭлементов" Тогда
		
		МассивНомеровЗамен = Новый Массив;
		Для каждого ТекущаяЗамена Из ВыбранноеЗначение Цикл
			Если ТекущаяЗамена.Пометка Тогда
				МассивНомеровЗамен.Добавить(ТекущаяЗамена.Значение);	
			КонецЕсли;	
		КонецЦикла;
		
		Если МассивНомеровЗамен.Количество() > 0 Тогда
			ОтменитьПодборы(МассивНомеровЗамен);
		КонецЕсли;
		
		УстановитьНадписьОтсутствиеАналогов(ЭтаФорма);
			
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимПодбораАналоговПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
	
	УстановитьОтборПоДоступнымАналогам(
		Элементы.Аналоги.ОтборСтрок, ТекущиеДанные.КлючСвязи, РежимПодбораАналогов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если ДатаДействияРазрешений = ДатаДействияРазрешенийДоИзменения Тогда
		Возврат;
	КонецЕсли;	
	
	Если ПорцииПодбора.Количество() > 0 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ДатаПриИзмененииЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Необходимо отменить сохраненные подборы для выполнения операции. Отменить подборы?';uk='Необхідно скасувати збережені підбори для виконання операції. Скасувати підбори?'");
		
		ПоказатьВопрос(
			ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 45, КодВозвратаДиалога.Нет,, КодВозвратаДиалога.Нет);
		
	Иначе
		ДатаПриИзмененииНаСервере(Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ДатаДействияРазрешений = ДатаДействияРазрешенийДоИзменения;
		Возврат;	
	КонецЕсли;
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ОтменитьПодборы = Истина)
	
	ДатаДействияРазрешенийДоИзменения = ДатаДействияРазрешений;
	
	Если ОтменитьПодборы Тогда
		МаксимальныйНомерПодбора = 0;
		ПорцииПодбора.Очистить();
	КонецЕсли;
	
	МассивНомеровСтрок = Новый Массив;
	ВыделенныеСтроки = Элементы.Материалы.ВыделенныеСтроки;
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		МассивНомеровСтрок.Добавить(Материалы.НайтиПоИдентификатору(ИдентификаторСтроки).НомерСтроки);	
	КонецЦикла;	
	
	ЗаполнитьДанные(ПараметрыТоваров, ПараметрыАналогов, ДатаДействияРазрешений);
	
	УстановитьТекущуюСтроку = Истина;
	Элементы.Материалы.ВыделенныеСтроки.Очистить();
	Для каждого НомерСтроки Из МассивНомеровСтрок Цикл
		
		ИдентификаторСтроки = Материалы.НайтиСтроки(Новый Структура("НомерСтроки", НомерСтроки))[0].ПолучитьИдентификатор();
		
		Если УстановитьТекущуюСтроку Тогда
			Элементы.Материалы.ТекущаяСтрока = ИдентификаторСтроки;
			УстановитьТекущуюСтроку = Ложь;
		Иначе	
			Элементы.Материалы.ВыделенныеСтроки.Добавить(ИдентификаторСтроки);
		КонецЕсли;	
		
	КонецЦикла;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАналоги Тогда
		УстановитьПредставлениеРазрешений(Материалы[0].Номенклатура, Материалы[0].Характеристика);
		ЗаполнитьИтоговыеПоказатели(ЭтаФорма);
		УстановитьНадписьОтсутствиеАналогов(ЭтаФорма);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", Новый Структура("Ключ", ТекущиеДанные.Номенклатура), ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАналоги

&НаКлиенте
Процедура АналогиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	АналогиКоличествоДоИзменения = Элементы.Аналоги.ТекущиеДанные.Количество;	
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.Аналоги.ТекущиеДанные;
	
	Если Не ОтменаРедактирования И ТекущиеДанные.Количество <> АналогиКоличествоДоИзменения Тогда
		
		ПроверитьВозможностьПересчета(Отказ);
		
		Если Не Отказ Тогда
			ЗаполнитьКоличествоПоРазрешению();
		КонецЕсли;	
		
	Иначе
		
		ТекущиеДанные.Количество = АналогиКоличествоДоИзменения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.АналогиОстатокНаСкладе Тогда
		
		КоэффициентПересчета = ПолучитьКоэффициентПересчета();
		
		Если КоэффициентПересчета > 0 Тогда
			
			ТекущиеДанные = Элементы.Аналоги.ТекущиеДанные;
			
			ТекущиеДанные.Количество = ТекущиеДанные.Количество + ТекущиеДанные.Норматив
				* КоэффициентПересчета * ТекущиеДанные.ДанныеУпаковки.Числитель / ТекущиеДанные.ДанныеУпаковки.Знаменатель;
			
			ЗаполнитьКоличествоПоРазрешению();
			
		КонецЕсли;	

	ИначеЕсли Поле = Элементы.АналогиНоменклатура Тогда
		
		ТекущиеДанные = Элементы.Аналоги.ТекущиеДанные;
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", Новый Структура("Ключ", ТекущиеДанные.Номенклатура), ЭтаФорма);
		
	ИначеЕсли Поле = Элементы.АналогиРазрешениеПредставление Тогда	
		
		ТекущиеДанные = Элементы.Аналоги.ТекущиеДанные;
		ПоказатьЗначение(, ТекущиеДанные.Разрешение);
				
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалы

&НаКлиенте
Процедура МатериалыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.МатериалыЕстьАналогиМатериала Тогда
		
		ПерейтиКПодборуАналогов(Неопределено);
		
	ИначеЕсли Поле = Элементы.МатериалыНоменклатура Тогда
		
		ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", Новый Структура("Ключ", ТекущиеДанные.Номенклатура), ЭтаФорма);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗамену(Команда)
	
	МассивСтрок = Материалы.НайтиСтроки(Новый Структура("Количество", 0));
	Если Материалы.Количество() > МассивСтрок.Количество() Тогда
		ОповеститьОВыборе(Новый Структура("АдресВХранилище", ПолучитьРезультатПодбора()));
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюЗамен(Команда)
	
	ТекстПредупреждения = "";
	
	МассивВыделенныхСтрок = Элементы.Материалы.ВыделенныеСтроки;
	
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru='Для отображения истории замен нужно выбрать строку(и).';uk='Для відображення історії замін потрібно вибрати рядок(и).'");
		
	Иначе	
		
		МассивНомеровСтрок = Новый Массив;
		
		ВыбраннаяНоменклатура = Новый Соответствие;
		
		Для каждого ИдентификаторСтроки Из МассивВыделенныхСтрок Цикл
			
			ТекущаяСтрока = Материалы.НайтиПоИдентификатору(ИдентификаторСтроки);
			
			ЗначениеХарактеристики = ВыбраннаяНоменклатура.Получить(ТекущаяСтрока.Номенклатура);
			Если ЗначениеХарактеристики <> ТекущаяСтрока.Характеристика Тогда
				
				Если ВыбраннаяНоменклатура.Количество() > 0 Тогда
					
					ТекстПредупреждения = СтрШаблон(
						НСтр("ru='Одновременно можно выбрать строки только с одинаковой номенклатурой%1.';uk='Одночасно можна вибрати рядки тільки з однаковою номенклатурою %1.'"),
						?(ИспользоватьХарактеристикиНоменклатуры, СтрШаблон(НСтр("ru='%1(характеристикой)';uk=' %1(характеристикою)'"), " "), ""));
					
					Прервать;
					
				Иначе
					ВыбраннаяНоменклатура.Вставить(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Характеристика);
				КонецЕсли;
				
			КонецЕсли;    
			
			Если ТекущаяСтрока.Количество > 0 Тогда
				МассивНомеровСтрок.Добавить(ТекущаяСтрока.НомерСтрокиИсходный);
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ТекстПредупреждения) И МассивНомеровСтрок.Количество() = 0 Тогда
			ТекстПредупреждения = НСтр("ru='В выделенных строках замены отсутствуют.';uk='У виділених рядках заміни відсутні.'");	
		КонецЕсли;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения, 45);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Материалы.НайтиПоИдентификатору(МассивВыделенныхСтрок[0]);
	
	ПредставлениеНоменклатуры = """" 
								+ ТекущаяСтрока.НоменклатураПредставление 
								+ ?(ЗначениеЗаполнено(ТекущаяСтрока.Характеристика), " / " + ТекущаяСтрока.ХарактеристикаПредставление, "") 
								+ """";
					 
	СписокПодборов = ПолучитьПорцииПодобранныхАналогов(
		ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Характеристика, МассивНомеровСтрок);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокЗначений", СписокПодборов);
	ПараметрыФормы.Вставить("Заголовок", СтрШаблон(НСтр("ru='История замен %1';uk='Історія замін %1'"), ПредставлениеНоменклатуры));
	ПараметрыФормы.Вставить("ЗаголовокКнопкаОК", НСтр("ru='Отменить';uk='Скасувати'"));
	ПараметрыФормы.Вставить("ЗаголовокКнопкаОтмена", НСтр("ru='Закрыть';uk='Закрити'"));
	
	ОткрытьФорму("ОбщаяФорма.ФормаОтметкиЭлементов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОстатки(Команда)
	
	УстановитьВидимостьОстатков(Элементы, Не Элементы.ФормаПоказатьОстатки.Пометка);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКПодборуАналогов(Команда)
	
	ТекстПредупреждения = "";
	
	МассивВыделенныхСтрок = Элементы.Материалы.ВыделенныеСтроки;
	
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru='Необходимо выбрать строку(и)';uk='Необхідно вибрати рядок(ки)'");
		
	Иначе
		
		ИсходныйМассивВыделенныхСтрок = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивВыделенныхСтрок);
		
		Элементы.Материалы.ТекущаяСтрока = ИсходныйМассивВыделенныхСтрок[0];
		
		ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
		
		Если Не ТекущиеДанные.ЕстьАналогиМатериала Тогда
			
			ТекстПредупреждения = НСтр("ru='Для выделенной строки аналоги не найдены';uk='Для виділенного рядка аналоги не знайдені'");
			
		Иначе
			
			Если ИсходныйМассивВыделенныхСтрок.Количество() > МассивВыделенныхСтрок.Количество() Тогда
				Для каждого ИдентификаторСтроки Из ИсходныйМассивВыделенныхСтрок Цикл
					Если МассивВыделенныхСтрок.Найти(ИдентификаторСтроки) = Неопределено Тогда  
						МассивВыделенныхСтрок.Добавить(ИдентификаторСтроки);	
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;
			
			ВыбраннаяНоменклатура = Новый Соответствие;
			КлючиСвязиСпецификаций = Новый Массив;
			
			Для каждого ИдентификаторСтроки Из МассивВыделенныхСтрок Цикл
				
				ТекущаяСтрока = Материалы.НайтиПоИдентификатору(ИдентификаторСтроки);
				
				Если ТекущаяСтрока.Требуется = ТекущаяСтрока.Количество Тогда
					
					ТекстПредупреждения = СтрШаблон(
						НСтр("ru='В строке №%1 все требуемое количество заменено.';uk='У рядку № %1 всю необхідну кількість замінено.'"), ТекущаяСтрока.НомерСтроки);
					
					Прервать;
					
				Иначе	
					
					ЗначениеХарактеристики = ВыбраннаяНоменклатура.Получить(ТекущаяСтрока.Номенклатура);
					Если ЗначениеХарактеристики <> ТекущаяСтрока.Характеристика Тогда
						
						Если ВыбраннаяНоменклатура.Количество() > 0 Тогда
							
							ТекстПредупреждения = СтрШаблон(
								НСтр("ru='Одновременно можно выбрать строки только с одинаковой номенклатурой%1.';uk='Одночасно можна вибрати рядки тільки з однаковою номенклатурою %1.'"),
								?(ИспользоватьХарактеристикиНоменклатуры, СтрШаблон(НСтр("ru='%1(характеристикой)';uk=' %1(характеристикою)'"), " "), ""));
							
							Прервать;
							
						Иначе
							ВыбраннаяНоменклатура.Вставить(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Характеристика);
						КонецЕсли;
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ТекущаяСтрока.КлючСвязиСпецификация) 
					   И КлючиСвязиСпецификаций.Найти(ТекущаяСтрока.КлючСвязиСпецификация) = Неопределено Тогда
					   	КлючиСвязиСпецификаций.Добавить(ТекущаяСтрока.КлючСвязиСпецификация);
					КонецЕсли;   
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не ЗначениеЗаполнено(ТекстПредупреждения) И КлючиСвязиСпецификаций.Количество() > 1 Тогда
				
				Для каждого КлючСвязиСпецификация Из КлючиСвязиСпецификаций Цикл
					
					МассивСтрокПоКлючам = Аналоги.НайтиСтроки(Новый Структура("КлючСвязиСпецификация", КлючСвязиСпецификация));
					Для каждого СтрокаТаблицы Из МассивСтрокПоКлючам Цикл
						
						МассивСтрок = Аналоги.НайтиСтроки(Новый Структура("Разрешение, ЭтоАналог", СтрокаТаблицы.Разрешение, Ложь));
						Если МассивСтрок.Количество() > 0 Тогда
							
							ТекстПредупреждения = НСтр("ru='Для выбранных позиций нет подходящих разрешений на замену.';uk='Для вибраних позицій немає відповідних дозволів на заміну.'");
							
							Прервать;
							
						КонецЕсли;	
						
					КонецЦикла;	
					
				КонецЦикла;	
				
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения, 45);
		Возврат;
	КонецЕсли;
	
	УстановитьПредставлениеРазрешений(ТекущиеДанные.Номенклатура, ТекущиеДанные.Характеристика);
	УстановитьТекущуюСтраницу(ЭтаФорма, "Аналоги", ТекущиеДанные.КлючСвязи, РежимПодбораАналогов);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьТекущийПодбор(Команда)
	
	ОбработатьПодборАналогов(Ложь);
	
	Если Материалы.Количество() > 1 Тогда
		УстановитьТекущуюСтраницу(ЭтаФорма, "Материалы");
	Иначе
		ЗаполнитьИтоговыеПоказатели(ЭтаФорма);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПодбор(Команда)
	
	ОбработатьПодборАналогов(Истина);
	
	Если Материалы.Количество() > 1 Тогда
		УстановитьТекущуюСтраницу(ЭтаФорма, "Материалы");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОстаткиИДоступностьТоваровОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура(
		"СформироватьПриОткрытии, КлючВарианта, ФиксированныеНастройки", 
		Истина, "ПоНоменклатуреКонтекст", Новый НастройкиКомпоновкиДанных);
	
	ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ЭлементОтбора = ПараметрыФормы.ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура");
		ЭлементОтбора.ПравоеЗначение = ТекущиеДанные.Номенклатура;
		
		ЭлементОтбора = ПараметрыФормы.ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Характеристика");
		ЭлементОтбора.ПравоеЗначение = ТекущиеДанные.Характеристика;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Склад) Тогда
		
			ЭлементОтбора = ПараметрыФормы.ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
			ЭлементОтбора.ПравоеЗначение = ТекущиеДанные.Склад;
			ЭлементОтбора.ВидСравнения = 
				?(НавигационнаяСсылкаФорматированнойСтроки = "ОстаткиИДоступностьТоваров", 
					ВидСравненияКомпоновкиДанных.Равно, 
					ВидСравненияКомпоновкиДанных.НеРавно);
			
		КонецЕсли;	
			
	КонецЕсли;	
	
	ОткрытьФорму("Отчет.ОстаткиИДоступностьТоваров.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

#Область Материалы

&НаСервере
Процедура ЗаполнитьДанные(ПараметрыТоваров, ПараметрыАналогов, ДатаДействия = Неопределено)
	
	ТаблицаТоваров = АналогиМатериалов.ПолучитьТаблицуТоваров(ПараметрыТоваров);
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	АналогиМатериалов.ЗаполнитьДанныеПараметра(ПараметрыАналогов, "ТаблицаПараметров");
	АналогиМатериалов.ЗаполнитьДанныеПараметра(ПараметрыАналогов, "КорректировкиОстатков");
	
	Если ДатаДействия = Неопределено Тогда
		ДатаДействияРазрешений = ПараметрыАналогов.ТаблицаПараметров[0].ДатаДействияРазрешений;
		ДатаДействияРазрешенийДоИзменения = ДатаДействияРазрешений;
	Иначе
		ПараметрыАналогов.ТаблицаПараметров.ЗаполнитьЗначения(ДатаДействияРазрешений, "ДатаДействияРазрешений");
	КонецЕсли;
	
	ПолучитьСкладИзОбеспечение = Не ПараметрыАналогов.Свойство("Склад") Или ПараметрыАналогов.Склад = Неопределено;
	
	ПараметрыОтборов = Новый Структура(
		"КорректироватьОстатки, ТекстыЗаменВЗапросах", 
		ПараметрыАналогов.Свойство("КорректировкиОстатков"), 
		Новый Соответствие);
		
	ПараметрыОтборов.ТекстыЗаменВЗапросах.Вставить("//ОтборПоМатериалам_", "");
	ПараметрыОтборов.ТекстыЗаменВЗапросах.Вставить("//ОтборПоАналогам_", "");
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(АналогиМатериалов.ТекстЗапросаТовары());
	МассивТекстов.Добавить(АналогиМатериалов.ТекстЗапросаТаблицаМатериалов(Истина));
	МассивТекстов.Добавить(АналогиМатериалов.ТекстЗапросаТаблицаАналогов(ПолучитьСкладИзОбеспечение));
	МассивТекстов.Добавить(АналогиМатериалов.ТекстЗапросаТаблицаОстатков(ПараметрыОтборов));
	МассивТекстов.Добавить(УправлениеДаннымиОбИзделиях.ТекстЗапросаДанныеУпаковок(СтрРазделить("ТаблицаМатериалов,ТаблицаАналогов",",")));
	МассивТекстов.Добавить(ТекстЗапросаЗаполненияТаблиц());
	
	Если ПолучитьСкладИзОбеспечение И ПараметрыАналогов.Свойство("Склад") Тогда
		ПараметрыАналогов.Удалить("Склад");
		ПараметрыАналогов.Вставить("СкладПоУмолчанию", Справочники.Склады.СкладПоУмолчанию());
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	Запрос.УстановитьПараметр("Товары", ТаблицаТоваров);
	Запрос.УстановитьПараметр(
		"ПустойУникальныйИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Для каждого ПараметрАналогов Из ПараметрыАналогов Цикл
		Запрос.УстановитьПараметр(ПараметрАналогов.Ключ, ПараметрАналогов.Значение);
	КонецЦикла;	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();  
	
	ТаблицаМатериаловРазрешений = МассивРезультатов[МассивРезультатов.ВГраница() - 2].Выгрузить();
	ТаблицаМатериаловРазрешений.Индексы.Добавить("Разрешение");
	ТаблицаМатериаловРазрешений.Индексы.Добавить("Разрешение, КонтролироватьКратность");
	
	ТаблицаАналоговРазрешений = МассивРезультатов[МассивРезультатов.ВГраница() - 1].Выгрузить();
	ТаблицаАналоговРазрешений.Индексы.Добавить("Разрешение");
	
	ТаблицаДанныеУпаковок = МассивРезультатов[МассивРезультатов.ВГраница() - 4].Выгрузить();
	УправлениеДаннымиОбИзделиях.ЗаполнитьДанныеУпаковокВНабореДанных(ТаблицаМатериаловРазрешений, ТаблицаДанныеУпаковок);
	УправлениеДаннымиОбИзделиях.ЗаполнитьДанныеУпаковокВНабореДанных(ТаблицаАналоговРазрешений, ТаблицаДанныеУпаковок);
	
	ДанныеРазрешение = Новый Структура(
		"ТаблицаМатериаловРазрешений, ТаблицаАналоговРазрешений, Разрешение", 
		 ТаблицаМатериаловРазрешений, ТаблицаАналоговРазрешений);
	
	Материалы.Очистить();
	Аналоги.Очистить();
	
	НомерСтрокиМатериалы = 0;
	НомерСтрокиАналоги = 0;
	ВыборкаТовары = МассивРезультатов[МассивРезультатов.ВГраница()].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовары.Следующий() Цикл
		
		НомерСтрокиМатериалы = НомерСтрокиМатериалы + 1;
		
		СтрокаМатериалы = Материалы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаМатериалы, ВыборкаТовары);
		СтрокаМатериалы.НомерСтроки = НомерСтрокиМатериалы;
		СтрокаМатериалы.КлючСвязи = КлючСвязи(ВыборкаТовары.НомерСтрокиИсходный);
		
		Если СтрокаМатериалы.ЕстьАналогиМатериала Тогда
			
			ДанныеНоменклатуры = Новый Структура("НомерСтрокиИсходный, Номенклатура, Характеристика, КлючСвязиСпецификация");
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, СтрокаМатериалы);
			
			Выборка = ВыборкаТовары.Выбрать();
			
			ДанныеПодсказки = СтруктураДанныхПодсказки(Выборка.Количество());
			
			Пока Выборка.Следующий() Цикл
				
				ДанныеРазрешение.Разрешение = Выборка.Разрешение;
				
				ОбработатьРазрешение(ДанныеРазрешение, ДанныеНоменклатуры, ДанныеПодсказки, НомерСтрокиАналоги);
				
			КонецЦикла;	
			
			СтрокаМатериалы.ПодсказкаВозможностьЗамены = ДанныеПодсказки.Текст;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей();
	
	ПараметрыТоваров.Вставить("Товары", ПоместитьВоВременноеХранилище(ТаблицаТоваров, УникальныйИдентификатор));
	
	ПараметрыАналогов.Вставить(
		"ТаблицаПараметров", ПоместитьВоВременноеХранилище(ПараметрыАналогов.ТаблицаПараметров, УникальныйИдентификатор));
	
	ПараметрыАналогов.Вставить(
		"КорректировкиОстатков", 
			?(ПараметрыАналогов.Свойство("КорректировкиОстатков"), 
				ПоместитьВоВременноеХранилище(ПараметрыАналогов.КорректировкиОстатков, УникальныйИдентификатор), Неопределено));
	
КонецПроцедуры

&НаСервере
Функция СтруктураДанныхПодсказки(КоличествоРазрешений)
	
	СтрокаВариант = ОбщегоНазначенияУТКлиентСервер.СклонениеСлова(
		КоличествоРазрешений, НСтр("ru='вариант';uk='варіант'"), НСтр("ru='варианта';uk='варіанту'"), НСтр("ru='вариантов';uk='варіантів'"), "м");
	
	ДанныеПодсказки = Новый Структура;
	ДанныеПодсказки.Вставить("Текст", "");
	ДанныеПодсказки.Вставить("КоличествоРазрешений", КоличествоРазрешений);
	ДанныеПодсказки.Вставить("ЕстьОстатки", Ложь);
	ДанныеПодсказки.Вставить("СтрокаВариант", СтрокаВариант);
	
	Возврат ДанныеПодсказки;
	
КонецФункции

&НаСервере
Функция КлючСвязи(Идентификатор, Полный = Истина)
	
	ЗначениеКлючаСвязи = 
		?(ТипЗнч(Идентификатор) = Тип("Число"), 
			Формат(Идентификатор, СтрШаблон("ЧГ=; ЧФ='%1'", ?(Полный, ";Ч;", "Ч;"))), 
			СтрШаблон(?(Полный, ";%1;", "%1;"), Идентификатор)); 
	
	Возврат ЗначениеКлючаСвязи;	
	
КонецФункции

#КонецОбласти

#Область Аналоги

&НаСервере
Процедура ОбработатьРазрешение(ПараметрыРазрешение, ПараметрыНоменклатуры, ПараметрыПодсказки, НомерСтроки)
	
	КлючСвязиИД = ПараметрыНоменклатуры.НомерСтрокиИсходный;
	
	СтруктураОтбора = Новый Структура("Разрешение", ПараметрыРазрешение.Разрешение);
	
	ДанныеРазрешение = 
		Новый Структура("Аналоги, Материалы, КонтролироватьКратность",
			ПараметрыРазрешение.ТаблицаАналоговРазрешений.НайтиСтроки(СтруктураОтбора),
			ПараметрыРазрешение.ТаблицаМатериаловРазрешений.НайтиСтроки(СтруктураОтбора),
			ПараметрыРазрешение.ТаблицаМатериаловРазрешений.НайтиСтроки(
				Новый Структура("Разрешение, КонтролироватьКратность", ПараметрыРазрешение.Разрешение, Истина)).Количество() > 0);
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество() > 0 Тогда
		
		ПустойУникальныйИдентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
		
		Для каждого СтрокаАналоги Из МассивСтрок Цикл
			
			СтрокаАналоги.КлючСвязиРазрешение = СтрокаАналоги.КлючСвязиРазрешение + КлючСвязи(КлючСвязиИД, Ложь);
			
			Если Не СтрокаАналоги.ЭтоАналог 
			   И СтрокаАналоги.Номенклатура = ПараметрыНоменклатуры.Номенклатура 
			   И СтрокаАналоги.Характеристика = ПараметрыНоменклатуры.Характеристика 
			   И (СтрокаАналоги.КлючСвязиСпецификация = ПараметрыНоменклатуры.КлючСвязиСпецификация
			   		Или СтрокаАналоги.КлючСвязиСпецификация = ПустойУникальныйИдентификатор)Тогда
			   
				СтрокаАналоги.КлючСвязи = 
					СтрокаАналоги.КлючСвязи + КлючСвязи(КлючСвязиИД, Не ЗначениеЗаполнено(СтрокаАналоги.КлючСвязи)); 
					
			КонецЕсли;
				
		КонецЦикла;
		
	Иначе	
		ДобавитьРазрешение(ДанныеРазрешение, ПараметрыНоменклатуры, ПараметрыПодсказки, КлючСвязи(КлючСвязиИД), НомерСтроки);
		МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	
	Если Не ПараметрыПодсказки.ЕстьОстатки Тогда
		
		ПараметрыПодсказки.ЕстьОстатки = МассивСтрок[0].ЕстьОстатки;
		ПодсказкаОстатки = ?(ПараметрыПодсказки.ЕстьОстатки, НСтр("ru='есть остатки';uk='є залишки'"), НСтр("ru='нет остатков';uk='немає залишків'"));  
		
		СтрокаМатериалы = ДанныеРазрешение.Материалы[0];
		СтрокаАналоги = ДанныеРазрешение.Аналоги[0];
		
		МатериалыНормативЕдХранения = СтрокаМатериалы.Норматив
			* СтрокаМатериалы.ДанныеУпаковки.Числитель / СтрокаМатериалы.ДанныеУпаковки.Знаменатель;
		АналогиНормативЕдХранения = СтрокаАналоги.Норматив
			* СтрокаАналоги.ДанныеУпаковки.Числитель / СтрокаАналоги.ДанныеУпаковки.Знаменатель;
		
		Если ПараметрыПодсказки.КоличествоРазрешений = 1
		   И ДанныеРазрешение.Материалы.Количество() = 1 
		   И ДанныеРазрешение.Аналоги.Количество() = 1 
		   И МатериалыНормативЕдХранения = 1
		   И АналогиНормативЕдХранения = 1 Тогда
		   	ПодсказкаВариант = Строка(СтрокаАналоги.Номенклатура);
		Иначе
			ПодсказкаВариант = Формат(ПараметрыПодсказки.КоличествоРазрешений, "ЧГ=") + " " + ПараметрыПодсказки.СтрокаВариант;
		КонецЕсли;
		
		ПараметрыПодсказки.Текст = СтрШаблон("%1; %2", ПодсказкаВариант, ПодсказкаОстатки);
			
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьРазрешение(ПараметрыРазрешение, ПараметрыНоменклатуры, ПараметрыПодсказки, КлючСвязи, НомерСтроки)
	
	РазрешениеПредставление = "";
	
	Для каждого СтрокаАналоги Из ПараметрыРазрешение.Аналоги Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		СтрокаТаблицы = Аналоги.Добавить();
		
		Если Не ЗначениеЗаполнено(РазрешениеПредставление) Тогда
			
			РазрешениеПредставление = СтрШаблон(
				"%1 %2 %3 %4", "№", СтрокаАналоги.Номер, НСтр("ru='от';uk='від'"), Формат(СтрокаАналоги.Дата, "ДЛФ=D"));
			
			СтрокаТаблицы.ОтобразитьРазрешение = Истина;
			 
			ЕстьОстатки = СтрокаАналоги.КоэффициентНаличияНаСкладе >= 1;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаАналоги);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПараметрыРазрешение, "КонтролироватьКратность");
		
		СтрокаТаблицы.ЕстьОстатки = ЕстьОстатки;
		СтрокаТаблицы.НомерСтроки = НомерСтроки;
		СтрокаТаблицы.КлючСвязиРазрешение = КлючСвязи;
		
		СтрокаТаблицы.НормативПредставление = ПредставлениеНорматива(СтрокаТаблицы.Норматив
			* СтрокаТаблицы.ДанныеУпаковки.Числитель / СтрокаТаблицы.ДанныеУпаковки.Знаменатель);
		
	КонецЦикла;
	
	ПустойУникальныйИдентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	
	Для каждого СтрокаМатериалы Из ПараметрыРазрешение.Материалы Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		СтрокаТаблицы = Аналоги.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаМатериалы);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПараметрыРазрешение, "КонтролироватьКратность");
		
		НормативЕдХранения = СтрокаТаблицы.Норматив * СтрокаТаблицы.ДанныеУпаковки.Числитель / СтрокаТаблицы.ДанныеУпаковки.Знаменатель;
		
		СтрокаТаблицы.НормативПредставление   = ПредставлениеНорматива(НормативЕдХранения);
		
		СтрокаТаблицы.РазрешениеПредставление = РазрешениеПредставление 
			+ ?(НормативЕдХранения = 1, 
				"", 
				СтрШаблон(НСтр("ru='%1(на замену %2 %3)';uk=' %1(на заміну %2 %3)'"), 
						  " ", СтрокаТаблицы.НормативПредставление, СтрокаТаблицы.ЕдиницаИзмеренияПредставление));
			  
		СтрокаТаблицы.ЕстьОстатки = ЕстьОстатки;
		СтрокаТаблицы.НомерСтроки = НомерСтроки;
		СтрокаТаблицы.КлючСвязиРазрешение = КлючСвязи;
		
		Если СтрокаТаблицы.Номенклатура = ПараметрыНоменклатуры.Номенклатура 
		   И СтрокаТаблицы.Характеристика = ПараметрыНоменклатуры.Характеристика
		   И (СтрокаТаблицы.КлючСвязиСпецификация = ПараметрыНоменклатуры.КлючСвязиСпецификация 
		   		Или СтрокаТаблицы.КлючСвязиСпецификация = ПустойУникальныйИдентификатор) Тогда
			СтрокаТаблицы.КлючСвязи = КлючСвязи;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьКоличествоПоРазрешению()
	
	ТекущаяСтрокаАналоги = Аналоги.НайтиПоИдентификатору(Элементы.Аналоги.ТекущаяСтрока);
	ТекущаяСтрокаМатериалы = Материалы.НайтиПоИдентификатору(Элементы.Материалы.ТекущаяСтрока);
	
	КоэффициентРасчета = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
		Ложь, ТекущаяСтрокаАналоги.Количество, ТекущаяСтрокаАналоги.Норматив, ТекущаяСтрокаАналоги.ДанныеУпаковки);
	
	КлючСвязиТекущейСтроки = ТекущаяСтрокаМатериалы.КлючСвязи;
	
	МассивПодсказок = Новый Массив;
	
	СтруктураОтбора = Новый Структура("Разрешение, ЭтоАналог", ТекущаяСтрокаАналоги.Разрешение);
	
	СтруктураОтбора.ЭтоАналог = Ложь;
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаАналоги Из МассивСтрок Цикл
		
		БазаРаспределения = Окр(СтрокаАналоги.Норматив * КоэффициентРасчета
			* СтрокаАналоги.ДанныеУпаковки.Числитель / СтрокаАналоги.ДанныеУпаковки.Знаменатель - СтрокаАналоги.Количество, 3);
		
		Если БазаРаспределения = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаАналоги.Количество = СтрокаАналоги.Норматив * КоэффициентРасчета
				* СтрокаАналоги.ДанныеУпаковки.Числитель / СтрокаАналоги.ДанныеУпаковки.Знаменатель;
		
		Если СтрокаАналоги.Количество > 0 Тогда
			МассивПодсказок.Добавить(СтрокаПодсказки(СтрокаАналоги));
		КонецЕсли;	
		
		Если СтрНайти(СтрокаАналоги.КлючСвязи, КлючСвязиТекущейСтроки) > 0 Тогда
			
			МассивВыделенныхСтрок = Элементы.Материалы.ВыделенныеСтроки;
			Для каждого ВыделеннаяСтрока Из МассивВыделенныхСтрок Цикл
				
				СтрокаМатериалы = Материалы.НайтиПоИдентификатору(ВыделеннаяСтрока);
				УстановитьКоличествоВСтроке(СтрокаМатериалы, БазаРаспределения);
				
				Если БазаРаспределения = 0 Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЕсли;	
		
		Если БазаРаспределения <> 0 Тогда
		
			МассивКлючейСвязи = СтрРазделить(СтрокаАналоги.КлючСвязи, ";", Ложь);
			Для каждого КлючСвязиИД Из МассивКлючейСвязи Цикл
				
				КлючСвязи = КлючСвязи(КлючСвязиИД);
				Если КлючСвязи = КлючСвязиТекущейСтроки Тогда
					Продолжить;
				КонецЕсли;	
				
				СтрокаМатериалы = Материалы.НайтиСтроки(Новый Структура("КлючСвязи", КлючСвязи))[0];
				УстановитьКоличествоВСтроке(СтрокаМатериалы, БазаРаспределения);
				
				Если БазаРаспределения = 0 Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьИтоговыеПоказатели(ЭтаФорма);
	
	ПодсказкаЗаменено = ?(МассивПодсказок.Количество() > 0, НСтр("ru='Заменено';uk='Замінено'") + ": " + СтрСоединить(МассивПодсказок, "; "), "");
	
	СтруктураОтбора.ЭтоАналог = Истина;
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаАналоги Из МассивСтрок Цикл
		
		СтрокаАналоги.ПодсказкаЗаменено = ПодсказкаЗаменено;
		
		Если СтрокаАналоги.НомерСтроки <> ТекущаяСтрокаАналоги.НомерСтроки Тогда
			СтрокаАналоги.Количество = СтрокаАналоги.Норматив * КоэффициентРасчета
				* СтрокаАналоги.ДанныеУпаковки.Числитель / СтрокаАналоги.ДанныеУпаковки.Знаменатель;
		КонецЕсли;
		
		ОстатокНаСкладеДоИзменения = СтрокаАналоги.ОстатокНаСкладе;
		
		СтрокаАналоги.ОстатокНаСкладе = 
			СтрокаАналоги.ОстатокНаСкладеПодтверждено 
			- ПолучитьОбщееКоличествоАналоги(СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика, СтрокаАналоги.ЭтоАналог);
		
		Если СтрокаАналоги.ОстатокНаСкладе <> ОстатокНаСкладеДоИзменения Тогда
			УстановитьКоличествоДоступныхОстатков(СтрокаАналоги);
		КонецЕсли;		
			
	КонецЦикла;
	
	УстановитьНадписьОтсутствиеАналогов(ЭтаФорма);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоДоступныхОстатков(СтрокаДанных)
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, ЭтоАналог");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаДанных);
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаАналоги Из МассивСтрок Цикл
	
		ЗаполнитьЗначенияСвойств(СтрокаАналоги, СтрокаДанных, "ОстатокНаСкладе");
			
		УстановитьПризнакЕстьОстатки(СтрокаАналоги);
				
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьПризнакЕстьОстатки(СтрокаДанных)
	
	ЕстьОстатки = СтрокаДанных.ОстатокНаСкладе >= СтрокаДанных.Норматив
		* СтрокаДанных.ДанныеУпаковки.Числитель / СтрокаДанных.ДанныеУпаковки.Знаменатель;
	Если СтрокаДанных.ЕстьОстатки = ЕстьОстатки Тогда
		Возврат;
	КонецЕсли;	
	
	УстановитьЕстьОстатки = Истина;
	
	СтруктураОтбора = Новый Структура("Разрешение, ЕстьОстатки, ЭтоАналог");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаДанных);
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Если ЕстьОстатки Тогда
		Для каждого СтрокаАналоги Из МассивСтрок Цикл
			Если СтрокаАналоги.ОстатокНаСкладе < СтрокаДанных.Норматив
				* СтрокаДанных.ДанныеУпаковки.Числитель / СтрокаДанных.ДанныеУпаковки.Знаменатель Тогда
				УстановитьЕстьОстатки = Ложь;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	СтруктураОтбора.Удалить("ЭтоАналог");
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Если УстановитьЕстьОстатки Тогда
		Для каждого СтрокаАналоги Из МассивСтрок Цикл
			СтрокаАналоги.ЕстьОстатки = ЕстьОстатки;		
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьКоличествоВСтроке(СтрокаТаблицы, ЗначениеКоличество)
	
	Если ЗначениеКоличество > 0	
	   И СтрокаТаблицы.Количество < СтрокаТаблицы.Требуется
	 Или ЗначениеКоличество < 0	
	   И СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПодтверждено > 0 Тогда
		
		Знак = ?(ЗначениеКоличество < 0, -1, 1);
		
		КоличествоОстаток = ?(ЗначениеКоличество > 0, 
							  СтрокаТаблицы.Требуется - СтрокаТаблицы.Количество, 
							  СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПодтверждено);
				
		КоличествоРаспределения = Мин(КоличествоОстаток, ЗначениеКоличество * Знак);
		
		СтрокаТаблицы.Количество = СтрокаТаблицы.Количество + КоличествоРаспределения * Знак;
			
		ЗначениеКоличество = ЗначениеКоличество - КоличествоРаспределения * Знак;
		
	КонецЕсли;	
		
КонецПроцедуры	

&НаСервере
Функция ПолучитьРезультатПодбора()
	
	Если  Аналоги.Количество() > Аналоги.НайтиСтроки(Новый Структура("Количество", 0 )).Количество() Тогда
		ОбработатьПодборАналогов(Истина);
	КонецЕсли;	
	
	ТаблицаПодбора = ПорцииПодбора.Выгрузить(
		, "НомерПодбора, НомерСтрокиИсходный, КлючСвязи, "
		  + "Номенклатура, Характеристика, Количество, "
		  + "ОстатокНаСкладе, Склад, Разрешение, ЭтоАналог");
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаПодбора, УникальныйИдентификатор);
	
КонецФункции	

&НаКлиенте
Процедура ПроверитьВозможностьПересчета(Отказ)
	
	ТекстСообщения = "";
	
	ТекущиеДанныеАналоги = Элементы.Аналоги.ТекущиеДанные;
	
	КоэффициентНорматива = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
		Ложь, ТекущиеДанныеАналоги.Количество, ТекущиеДанныеАналоги.Норматив, ТекущиеДанныеАналоги.ДанныеУпаковки);
	
	Если ТекущиеДанныеАналоги.КонтролироватьКратность И КоэффициентНорматива <> Цел(КоэффициентНорматива) Тогда
		
		ТекстСообщения = НСтр("ru='Введите количество кратное нормативу.';uk='Введіть кількість кратну нормативу.'");
		
	Иначе
	
		ТекущиеДанныеМатериалы = Элементы.Материалы.ТекущиеДанные;
		
		СтруктураОтбора = Новый Структура("Разрешение, Номенклатура, Характеристика, ЭтоАналог");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущиеДанныеАналоги, "Разрешение");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущиеДанныеМатериалы, "Номенклатура, Характеристика");
		
		СтруктураОтбора.ЭтоАналог = Ложь;
		
		СтрокаАналоги = Аналоги.НайтиСтроки(СтруктураОтбора)[0];
		
		АналогиКоличество = ТекущиеДанныеАналоги.Количество;
		АналогиНорматив = ТекущиеДанныеАналоги.Норматив;
		АналогиДанныеУпаковки = ТекущиеДанныеАналоги.ДанныеУпаковки;
		
		МатериалыНорматив = СтрокаАналоги.Норматив;
		МатериалыДанныеУпаковки = СтрокаАналоги.ДанныеУпаковки;
			
		КоличествоРазницаАналог = (АналогиКоличество - АналогиКоличествоДоИзменения)
			* ?(АналогиКоличествоДоИзменения > АналогиКоличество, -1, 1);
			
		КоэффициентРасчета = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
					Ложь, КоличествоРазницаАналог, АналогиНорматив, АналогиДанныеУпаковки);
		
		КоличествоРазница = Окр(МатериалыНорматив * КоэффициентРасчета
					* МатериалыДанныеУпаковки.Числитель / МатериалыДанныеУпаковки.Знаменатель, 3);
		КоличествоГраница = ?(АналогиКоличествоДоИзменения > АналогиКоличество, ИтогоЗаменено, ИтогоКЗамене);
		
		Если КоличествоРазница > КоличествоГраница Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru='Для выполнения замены не хватает %1 %2 материала %3';uk='Для виконання заміни не вистачає %1 %2 матеріалу %3'"), 
				Окр(КоличествоРазница - КоличествоГраница, 3),
				СтрокаАналоги.ЕдиницаИзмеренияПредставление,
				НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика));
			
		ИначеЕсли АналогиКоличествоДоИзменения < АналогиКоличество Тогда
			
			СтруктураОтбора.Удалить("Номенклатура");
			СтруктураОтбора.Удалить("Характеристика");
			
			МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
			Если МассивСтрок.Количество() > 1 Тогда
				
				Для каждого СтрокаАналоги Из МассивСтрок Цикл
					
					Если СтрокаАналоги.Номенклатура = ТекущиеДанныеМатериалы.Номенклатура
					   И СтрокаАналоги.Характеристика = ТекущиеДанныеМатериалы.Характеристика Тогда
					   	Продолжить;
					КонецЕсли;
					
					ИтогоТребуетсяМатериала = ПолучитьОбщееКоличествоТребуемогоМатериала(
						СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика);
					
					ИтогоЗамененоМатериала = ПолучитьОбщееКоличествоАналоги(
						СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика, СтрокаАналоги.ЭтоАналог);
					
					МатериалыНорматив = СтрокаАналоги.Норматив;
					МатериалыДанныеУпаковки = СтрокаАналоги.ДанныеУпаковки;
					
					КоличествоРазница = Окр(МатериалыНорматив * КоэффициентРасчета
								* МатериалыДанныеУпаковки.Числитель / МатериалыДанныеУпаковки.Знаменатель, 3);
					КоличествоГраница = ИтогоТребуетсяМатериала - ИтогоЗамененоМатериала;
					
					Если КоличествоРазница > КоличествоГраница Тогда
						
						ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "") 
							+ СтрШаблон(
								НСтр("ru='%1 %2 материала %3';uk=' %1 %2 матеріалу %3'"), 
								КоличествоРазница - КоличествоГраница,
								СтрокаАналоги.ЕдиницаИзмеренияПредставление,	
								НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика));
						
					КонецЕсли;
					           				
				КонецЦикла;	
				
				Если ЗначениеЗаполнено(ТекстСообщения) Тогда
					
					ТекстСообщения = НСтр("ru='Для выполнения замены не хватает';uk='Для виконання заміни не вистачає'") 
								   + ?(СтрНайти(ТекстСообщения, Символы.ПС) > 0, Символы.ПС, " ")
								   + ТекстСообщения;
					
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЕсли;
	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			, 
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Аналоги", ТекущиеДанныеАналоги.НомерСтроки, "Количество"),
			, 
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКоэффициентПересчета()
	
	КоэффициентПересчета = 0;
	
	Если ИтогоКЗамене = 0 Тогда
		Возврат КоэффициентПересчета;
	КонецЕсли;
	
	ТекущиеДанныеАналоги = Элементы.Аналоги.ТекущиеДанные;
	Если ТекущиеДанныеАналоги.ОстатокНаСкладе = 0 Или Не ТекущиеДанныеАналоги.ЕстьОстатки Тогда
		Возврат КоэффициентПересчета;
	КонецЕсли;
	
	ТекущиеДанныеМатериалы = Элементы.Материалы.ТекущиеДанные;
	
	СтруктураОтбора = Новый Структура("Разрешение, Номенклатура, Характеристика, ЭтоАналог");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущиеДанныеАналоги, "Разрешение");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущиеДанныеМатериалы, "Номенклатура, Характеристика");
	
	СтруктураОтбора.ЭтоАналог = Ложь;
	
	СтрокаАналоги = Аналоги.НайтиСтроки(СтруктураОтбора)[0];
	
	МатериалыНорматив = СтрокаАналоги.Норматив;
	МатериалДанныеУпаковки = СтрокаАналоги.ДанныеУпаковки;
	
	КоэффициентПересчета = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
		СтрокаАналоги.КонтролироватьКратность, ИтогоКЗамене, МатериалыНорматив, МатериалДанныеУпаковки);
	
	Если КоэффициентПересчета > 0 Тогда
		
		СтруктураОтбора.Удалить("Номенклатура");
		СтруктураОтбора.Удалить("Характеристика");
		
		МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
		Если МассивСтрок.Количество() > 1 Тогда
			
			Для каждого СтрокаАналоги Из МассивСтрок Цикл
				
				Если СтрокаАналоги.Номенклатура = ТекущиеДанныеМатериалы.Номенклатура
				   И СтрокаАналоги.Характеристика = ТекущиеДанныеМатериалы.Характеристика Тогда
				   	Продолжить;
				КонецЕсли;
				
				ИтогоТребуетсяМатериала = ПолучитьОбщееКоличествоТребуемогоМатериала(
					СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика);
				
				ИтогоЗамененоМатериала = ПолучитьОбщееКоличествоАналоги(
					СтрокаАналоги.Номенклатура, СтрокаАналоги.Характеристика, СтрокаАналоги.ЭтоАналог);
				
				МатериалыНорматив = СтрокаАналоги.Норматив;
				МатериалДанныеУпаковки = СтрокаАналоги.ДанныеУпаковки;
				
				КоэффициентПересчета = Мин(
					КоэффициентПересчета, 
					АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
						СтрокаАналоги.КонтролироватьКратность, ИтогоТребуетсяМатериала - ИтогоЗамененоМатериала,
						МатериалыНорматив, МатериалДанныеУпаковки));
				
				Если КоэффициентПересчета = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;	
		
		КонецЕсли;	
	
	КонецЕсли;
	
	Если КоэффициентПересчета > 0 Тогда
	
		СтруктураОтбора.ЭтоАналог = Истина;
		
		МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаАналоги Из МассивСтрок Цикл
			
			АналогиНорматив = СтрокаАналоги.Норматив;
			АналогиДанныеУпаковки = СтрокаАналоги.ДанныеУпаковки;
			
			КоэффициентПересчета = Мин(
				КоэффициентПересчета,
				АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
					СтрокаАналоги.КонтролироватьКратность, СтрокаАналоги.ОстатокНаСкладе, АналогиНорматив, АналогиДанныеУпаковки));
			
			Если КоэффициентПересчета = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат КоэффициентПересчета;
	
КонецФункции	

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыТабличныхЧастей()
	
	СтруктураДействий = Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются");
	СтруктураДействий.ЗаполнитьПризнакХарактеристикиИспользуются = 
		Новый Структура("Номенклатура", "ХарактеристикиИспользуются");
		
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Материалы, СтруктураДействий);
	
	СтруктураДействий = Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются");
	СтруктураДействий.ЗаполнитьПризнакХарактеристикиИспользуются = 
		Новый Структура("Номенклатура", "ХарактеристикиИспользуются");
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Аналоги, СтруктураДействий);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтрокаПодсказки(СтрокаАналоги)
	
	Если ЗначениеЗаполнено(СтрокаАналоги.Характеристика) Тогда
		
		СтрокаПодсказки = 
			СтрШаблон("%1 %2%3%4 %5 %6%7", 
				СтрокаАналоги.НоменклатураПредставление, 
				"(", 
				СтрокаАналоги.ХарактеристикаПредставление, 
				")", 
				НСтр("ru='в кол-ве';uk='у кількості'"), 
				Формат(СтрокаАналоги.Количество, "ЧГ="),
				СтрокаАналоги.ЕдиницаИзмеренияПредставление);
			
	Иначе
		
		СтрокаПодсказки = 
			СтрШаблон("%1 %2 %3%4", 
				СтрокаАналоги.НоменклатураПредставление, 
				НСтр("ru='в кол-ве';uk='у кількості'"), 
				Формат(СтрокаАналоги.Количество, "ЧГ="),
				СтрокаАналоги.ЕдиницаИзмеренияПредставление);
		
	КонецЕсли;	
	
	Возврат СтрокаПодсказки;
	
КонецФункции	

&НаСервере
Процедура ОтменитьПодборы(НомераПодборов)
	
	ИндексТекущегоПодбора = НомераПодборов.Найти(-1); 
	Если ИндексТекущегоПодбора <> Неопределено Тогда
		
		ОбработатьПодборАналогов(Ложь);
		
		НомераПодборов.Удалить(ИндексТекущегоПодбора);
		
	КонецЕсли;
	
	Если НомераПодборов.Количество() > 0 Тогда
		
		ОтменитьПорцииПодборов(НомераПодборов);
		
	КонецЕсли;
	
	ЗаполнитьИтоговыеПоказатели(ЭтаФорма);
	
	ОбновитьПодсказкиВозможностьЗамены();
	
КонецПроцедуры	

&НаСервере
Процедура ОтменитьПорцииПодборов(НомераПодборов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорцииПодбора.НомерПодбора КАК НомерПодбора,
	|	ПорцииПодбора.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	ПорцииПодбора.Номенклатура КАК Номенклатура,
	|	ПорцииПодбора.Характеристика КАК Характеристика,
	|	ПорцииПодбора.Количество КАК Количество,
	|	ПорцииПодбора.ОстатокНаСкладе КАК ОстатокНаСкладе,
	|	ПорцииПодбора.ЭтоАналог КАК ЭтоАналог
	|ПОМЕСТИТЬ ПорцииПодбора
	|ИЗ
	|	&ПорцииПодбора КАК ПорцииПодбора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерПодбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналоги.НомерСтроки КАК НомерСтроки,
	|	Аналоги.Номенклатура КАК Номенклатура,
	|	Аналоги.Характеристика КАК Характеристика,
	|	Аналоги.Количество КАК Количество,
	|	Аналоги.ОстатокНаСкладеПодтверждено КАК ОстатокНаСкладеПодтверждено,
	|	Аналоги.ЭтоАналог КАК ЭтоАналог
	|ПОМЕСТИТЬ Аналоги
	|ИЗ
	|	&Аналоги КАК Аналоги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоАналог,
	|	ОстатокНаСкладеПодтверждено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорцииПодбора.НомерПодбора КАК НомерПодбора,
	|	ПорцииПодбора.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	ПорцииПодбора.Номенклатура КАК Номенклатура,
	|	ПорцииПодбора.Характеристика КАК Характеристика,
	|	ПорцииПодбора.Количество КАК Количество,
	|	ПорцииПодбора.ОстатокНаСкладе КАК ОстатокНаСкладе,
	|	ПорцииПодбора.ЭтоАналог КАК ЭтоАналог
	|ПОМЕСТИТЬ ОтменяемыеПодборы
	|ИЗ
	|	ПорцииПодбора КАК ПорцииПодбора
	|ГДЕ
	|	ПорцииПодбора.НомерПодбора В(&МассивНомеров)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоАналог,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковВРазрезеПодбора.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатковВРазрезеПодбора.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаОстатковВРазрезеПодбора.ОстатокНаСкладе) КАК ОстатокНаСкладе
	|ПОМЕСТИТЬ ОстаткиПодборов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтменяемыеПодборы.НомерПодбора КАК НомерПодбора,
	|		ОтменяемыеПодборы.Номенклатура КАК Номенклатура,
	|		ОтменяемыеПодборы.Характеристика КАК Характеристика,
	|		МАКСИМУМ(ОтменяемыеПодборы.ОстатокНаСкладе) КАК ОстатокНаСкладе
	|	ИЗ
	|		ОтменяемыеПодборы КАК ОтменяемыеПодборы
	|	ГДЕ
	|		ОтменяемыеПодборы.ЭтоАналог
	|		И ОтменяемыеПодборы.ОстатокНаСкладе > 0
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОтменяемыеПодборы.НомерПодбора,
	|		ОтменяемыеПодборы.Номенклатура,
	|		ОтменяемыеПодборы.Характеристика) КАК ТаблицаОстатковВРазрезеПодбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатковВРазрезеПодбора.Номенклатура,
	|	ТаблицаОстатковВРазрезеПодбора.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорцииПодбора.НомерПодбора КАК НомерПодбора,
	|	ПорцииПодбора.Номенклатура КАК Номенклатура,
	|	ПорцииПодбора.Характеристика КАК Характеристика,
	|	СУММА(ПорцииПодбора.Количество) КАК Количество,
	|	МАКСИМУМ(ПорцииПодбора.ОстатокНаСкладе) КАК ОстатокНаСкладе,
	|	МАКСИМУМ(ОстаткиПодборов.ОстатокНаСкладе) КАК ОстатокНаСкладеОтменяемый
	|ПОМЕСТИТЬ КорректируемыеПодборы
	|ИЗ
	|	ПорцииПодбора КАК ПорцииПодбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПодборов КАК ОстаткиПодборов
	|		ПО ПорцииПодбора.Номенклатура = ОстаткиПодборов.Номенклатура
	|			И ПорцииПодбора.Характеристика = ОстаткиПодборов.Характеристика
	|ГДЕ
	|	ПорцииПодбора.ЭтоАналог
	|	И НЕ ПорцииПодбора.НомерПодбора В (&МассивНомеров)
	|	И ОстаткиПодборов.ОстатокНаСкладе ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ПорцииПодбора.НомерПодбора,
	|	ПорцииПодбора.Номенклатура,
	|	ПорцииПодбора.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПорцииПодбора.Количество) > МАКСИМУМ(ПорцииПодбора.ОстатокНаСкладе)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректируемыеПодборы.Номенклатура КАК Номенклатура,
	|	КорректируемыеПодборы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА СУММА(КорректируемыеПодборы.Количество) - СУММА(КорректируемыеПодборы.ОстатокНаСкладе) > МАКСИМУМ(КорректируемыеПодборы.ОстатокНаСкладеОтменяемый)
	|			ТОГДА МАКСИМУМ(КорректируемыеПодборы.ОстатокНаСкладеОтменяемый)
	|		ИНАЧЕ СУММА(КорректируемыеПодборы.Количество) - СУММА(КорректируемыеПодборы.ОстатокНаСкладе)
	|	КОНЕЦ КАК Остаток
	|ПОМЕСТИТЬ КорректируемыеОстатки
	|ИЗ
	|	КорректируемыеПодборы КАК КорректируемыеПодборы
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректируемыеПодборы.Номенклатура,
	|	КорректируемыеПодборы.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналоги.Номенклатура КАК Номенклатура,
	|	Аналоги.Характеристика КАК Характеристика,
	|	МАКСИМУМ(Аналоги.Количество) КАК Количество,
	|	МАКСИМУМ(Аналоги.ОстатокНаСкладеПодтверждено) КАК ОстатокНаСкладеПодтверждено
	|ПОМЕСТИТЬ ОстаткиАналогов
	|ИЗ
	|	Аналоги КАК Аналоги
	|ГДЕ
	|	Аналоги.ЭтоАналог
	|	И Аналоги.ОстатокНаСкладеПодтверждено > 0
	|	И ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ОстаткиПодборов КАК ОстаткиПодборов
	|			ГДЕ
	|				ОстаткиПодборов.Номенклатура = ОстаткиПодборов.Номенклатура
	|				И ОстаткиПодборов.Характеристика = ОстаткиПодборов.Характеристика)
	|
	|СГРУППИРОВАТЬ ПО
	|	Аналоги.Номенклатура,
	|	Аналоги.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректируемыеПодборы.НомерПодбора КАК НомерПодбора,
	|	КорректируемыеПодборы.Номенклатура КАК Номенклатура,
	|	КорректируемыеПодборы.Характеристика КАК Характеристика,
	|	КорректируемыеПодборы.Количество - КорректируемыеПодборы.ОстатокНаСкладе КАК КоличествоКЗаполнению,
	|	КорректируемыеОстатки.Остаток КАК ОстатокКРаспределению
	|ИЗ
	|	КорректируемыеПодборы КАК КорректируемыеПодборы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректируемыеОстатки КАК КорректируемыеОстатки
	|		ПО КорректируемыеПодборы.Номенклатура = КорректируемыеОстатки.Номенклатура
	|			И КорректируемыеПодборы.Характеристика = КорректируемыеОстатки.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПодбора
	|ИТОГИ
	|	МАКСИМУМ(ОстатокКРаспределению)
	|ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналоги.НомерСтроки КАК НомерСтроки,
	|	ОстаткиПодборов.ОстатокНаСкладе + ЕСТЬNULL(ОстаткиАналогов.ОстатокНаСкладеПодтверждено, 0) - ЕСТЬNULL(КорректируемыеОстатки.Остаток, 0) - ЕСТЬNULL(ОстаткиАналогов.Количество, 0) КАК ОстатокНаСкладе,
	|	ОстаткиПодборов.ОстатокНаСкладе + ЕСТЬNULL(ОстаткиАналогов.ОстатокНаСкладеПодтверждено, 0) - ЕСТЬNULL(КорректируемыеОстатки.Остаток, 0) КАК ОстатокНаСкладеПодтверждено
	|ИЗ
	|	ОстаткиПодборов КАК ОстаткиПодборов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиАналогов КАК ОстаткиАналогов
	|		ПО ОстаткиПодборов.Номенклатура = ОстаткиАналогов.Номенклатура
	|			И ОстаткиПодборов.Характеристика = ОстаткиАналогов.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Аналоги КАК Аналоги
	|		ПО ОстаткиПодборов.Номенклатура = Аналоги.Номенклатура
	|			И ОстаткиПодборов.Характеристика = Аналоги.Характеристика
	|			И (Аналоги.ЭтоАналог)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректируемыеОстатки КАК КорректируемыеОстатки
	|		ПО ОстаткиПодборов.Номенклатура = КорректируемыеОстатки.Номенклатура
	|			И ОстаткиПодборов.Характеристика = КорректируемыеОстатки.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтменяемыеПодборы.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	СУММА(ОтменяемыеПодборы.Количество) КАК Количество
	|ИЗ
	|	ОтменяемыеПодборы КАК ОтменяемыеПодборы
	|ГДЕ
	|	НЕ ОтменяемыеПодборы.ЭтоАналог
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтменяемыеПодборы.НомерСтрокиИсходный";
	
	
	Запрос.УстановитьПараметр(
		"ПорцииПодбора", 
		ПорцииПодбора.Выгрузить(
			, "НомерПодбора, НомерСтрокиИсходный, Номенклатура, Характеристика, Количество, ОстатокНаСкладе, ЭтоАналог"));
			
	Запрос.УстановитьПараметр(
		"Аналоги", 
		Аналоги.Выгрузить(
			, "НомерСтроки, Номенклатура, Характеристика, Количество, ОстатокНаСкладеПодтверждено, ЭтоАналог"));	
		
	Запрос.УстановитьПараметр("МассивНомеров", НомераПодборов);	
		
	МассивРезультатов = Запрос.ВыполнитьПакет();  
	
	ВыборкаМатериалы = МассивРезультатов[МассивРезультатов.ВГраница()].Выбрать();
	Пока ВыборкаМатериалы.Следующий() Цикл
		СтрокаМатериалы = Материалы.НайтиСтроки(Новый Структура("НомерСтрокиИсходный", ВыборкаМатериалы.НомерСтрокиИсходный))[0];
		СтрокаМатериалы.Количество = СтрокаМатериалы.Количество - ВыборкаМатериалы.Количество;
		СтрокаМатериалы.КоличествоПодтверждено = СтрокаМатериалы.КоличествоПодтверждено - ВыборкаМатериалы.Количество;
	КонецЦикла;
	
	ВыборкаАналоги = МассивРезультатов[МассивРезультатов.ВГраница() - 1].Выбрать();
	Пока ВыборкаАналоги.Следующий() Цикл
		СтрокаАналоги = Аналоги[ВыборкаАналоги.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(СтрокаАналоги, ВыборкаАналоги, "ОстатокНаСкладе, ОстатокНаСкладеПодтверждено");
		УстановитьПризнакЕстьОстатки(СтрокаАналоги);
	КонецЦикла;
	
	Для каждого НомерПодбора Из НомераПодборов Цикл
		МассивСтрок = ПорцииПодбора.НайтиСтроки(Новый Структура("НомерПодбора", НомерПодбора));
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			ПорцииПодбора.Удалить(СтрокаТаблицы);	
		КонецЦикла;	
	КонецЦикла;	
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, НомерПодбора");
	
	ВыборкаНоменклатура = МассивРезультатов[МассивРезультатов.ВГраница() - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаХарактеристика.Следующий() Цикл
			
			ОстатокКРаспределению = ВыборкаХарактеристика.ОстатокКРаспределению;
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаХарактеристика,, "НомерПодбора");
			
			ВыборкаНомерПодбора = ВыборкаХарактеристика.Выбрать();
			Пока ВыборкаНомерПодбора.Следующий() Цикл
				
				ОстатокВПодбор = Мин(ОстатокКРаспределению, ВыборкаНомерПодбора.КоличествоКЗаполнению);
				
				ОстатокКРаспределению = ОстатокКРаспределению - ОстатокВПодбор;
				
				ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаНомерПодбора, "НомерПодбора");
				
				МассивСтрок = ПорцииПодбора.НайтиСтроки(СтруктураОтбора);
				Для каждого СтрокаТаблицы Из МассивСтрок Цикл
					СтрокаТаблицы.ОстатокНаСкладе = СтрокаТаблицы.ОстатокНаСкладе + ОстатокВПодбор;
				КонецЦикла;
				
				Если ОстатокКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЦикла;	
		
	КонецЦикла;	
	    	
КонецПроцедуры	

&НаСервере
Процедура ОбработатьПодборАналогов(Подтвердить = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Материалы.НомерСтроки КАК НомерСтроки,
	|	Материалы.Номенклатура КАК Номенклатура,
	|	Материалы.Характеристика КАК Характеристика,
	|	Материалы.Количество КАК Количество,
	|	Материалы.КоличествоПодтверждено КАК КоличествоПодтверждено,
	|	Материалы.НомерСтрокиИсходный КАК НомерСтрокиИсходный
	|ПОМЕСТИТЬ Материалы
	|ИЗ
	|	&Материалы КАК Материалы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналоги.НомерСтроки КАК НомерСтроки,
	|	Аналоги.Номенклатура КАК Номенклатура,
	|	Аналоги.Характеристика КАК Характеристика,
	|	Аналоги.Количество КАК Количество,
	|	Аналоги.ОстатокНаСкладе КАК ОстатокНаСкладе,
	|	Аналоги.ОстатокНаСкладеПодтверждено КАК ОстатокНаСкладеПодтверждено,
	|	Аналоги.Склад КАК Склад,
	|	Аналоги.Разрешение КАК Разрешение,
	|	Аналоги.РазрешениеПредставление КАК РазрешениеПредставление,
	|	Аналоги.ЕдиницаИзмеренияПредставление КАК ЕдиницаИзмеренияПредставление,
	|	Аналоги.ЭтоАналог КАК ЭтоАналог
	|ПОМЕСТИТЬ Аналоги
	|ИЗ
	|	&Аналоги КАК Аналоги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.НомерСтроки КАК НомерСтроки,
	|	Материалы.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	Материалы.Номенклатура КАК Номенклатура,
	|	Материалы.Характеристика КАК Характеристика,
	|	Материалы.Количество КАК Количество,
	|	Материалы.КоличествоПодтверждено КАК КоличествоПодтверждено,
	|	Материалы.Количество - Материалы.КоличествоПодтверждено КАК КоличествоКРаспределению
	|ИЗ
	|	Материалы КАК Материалы
	|ГДЕ
	|	Материалы.Количество > Материалы.КоличествоПодтверждено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналоги.НомерСтроки КАК НомерСтроки,
	|	Аналоги.Номенклатура КАК Номенклатура,
	|	Аналоги.Характеристика КАК Характеристика,
	|	Аналоги.Количество КАК Количество,
	|	Аналоги.ОстатокНаСкладеПодтверждено - Аналоги.ОстатокНаСкладе КАК ОстатокНаСкладе,
	|	Аналоги.Склад КАК Склад,
	|	Аналоги.Разрешение КАК Разрешение,
	|	Аналоги.РазрешениеПредставление КАК РазрешениеПредставление,
	|	Аналоги.ЕдиницаИзмеренияПредставление КАК ЕдиницаИзмеренияПредставление,
	|	Аналоги.ЭтоАналог КАК ЭтоАналог
	|ИЗ
	|	Аналоги КАК Аналоги
	|ГДЕ
	|	Аналоги.Количество > 0";
	
	Запрос.УстановитьПараметр(
		"Материалы", 
		Материалы.Выгрузить(
			, 
			"НомерСтроки, Номенклатура, Характеристика, Количество, КоличествоПодтверждено, НомерСтрокиИсходный"));
	
	Запрос.УстановитьПараметр(
		"Аналоги", 
		Аналоги.Выгрузить(
			, 
			"НомерСтроки, Разрешение, РазрешениеПредставление, ЭтоАналог, Номенклатура, Характеристика,"
			+ " Количество, ОстатокНаСкладе, ОстатокНаСкладеПодтверждено, Склад, ЕдиницаИзмеренияПредставление"));	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаМатериалы = МассивРезультатов[МассивРезультатов.ВГраница() - 1].Выгрузить(); // см. АналогиМатериалов.СлужебнаяСтруктураТаблицыЗначений
	
	ТаблицаАналоги = МассивРезультатов[МассивРезультатов.ВГраница()].Выгрузить(); // см. АналогиМатериалов.СлужебнаяСтруктураТаблицыЗначений
	ТаблицаАналоги.Индексы.Добавить("Номенклатура, Характеристика, ЭтоАналог");
	
	ПостфиксПриемник = ?(Подтвердить, "Подтверждено", "");
	ПостфиксИсточник = ?(Подтвердить, "", "Подтверждено");
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, ЭтоАналог");
	СтруктураОчисткиАналогов = Новый Структура("Количество, ПодсказкаЗаменено", 0, ""); 
	
	Если Подтвердить Тогда
		
		КлючСвязи = ";" 
				  + СтрЗаменить(СтрСоединить(ТаблицаМатериалы.Выгрузитьколонку("НомерСтрокиИсходный"), ";"), Символы.НПП, "") 
				  + ";";
		
		МаксимальныйНомерПодбора = МаксимальныйНомерПодбора + 1;
		
		СтруктураОтбора.ЭтоАналог = Ложь;
						
	КонецЕсли;	
	
	Для каждого СтрокаТаблицы Из ТаблицаМатериалы Цикл
		
		СтрокаМатериалы = Материалы[СтрокаТаблицы.НомерСтроки - 1];
		
		СтрокаМатериалы["Количество" + ПостфиксПриемник] = СтрокаМатериалы["Количество" + ПостфиксИсточник];
			
		Если Подтвердить Тогда
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
			
			КоличествоКРаспределению = СтрокаТаблицы.КоличествоКРаспределению;
			
			МассивСтрок = ТаблицаАналоги.НайтиСтроки(СтруктураОтбора);
			ИндексСтрок = МассивСтрок.ВГраница();
			Пока ИндексСтрок > -1 Цикл
				
				ИндексСтрок = ИндексСтрок - 1;
				
				СтрокаТаблицыАналоги = МассивСтрок[0];
				
				КоличествоПорции = Мин(КоличествоКРаспределению, СтрокаТаблицыАналоги.Количество);
				
				ПорцияПодбора = ПорцииПодбора.Добавить();
				ЗаполнитьЗначенияСвойств(ПорцияПодбора, СтрокаТаблицыАналоги,, "Количество");
				ЗаполнитьЗначенияСвойств(ПорцияПодбора, СтрокаМатериалы, "НомерСтрокиИсходный");
				ПорцияПодбора.Количество = КоличествоПорции;
				ПорцияПодбора.НомерПодбора = МаксимальныйНомерПодбора;
				ПорцияПодбора.КлючСвязи = КлючСвязи;
				
				СтрокаТаблицыАналоги.Количество = СтрокаТаблицыАналоги.Количество - КоличествоПорции;
				Если СтрокаТаблицыАналоги.Количество = 0 Тогда
					
					ЗаполнитьЗначенияСвойств(Аналоги[СтрокаТаблицыАналоги.НомерСтроки - 1], СтруктураОчисткиАналогов);
					
					ТаблицаАналоги.Удалить(СтрокаТаблицыАналоги);
					МассивСтрок.Удалить(0);
					
				КонецЕсли;	
				
				КоличествоКРаспределению = КоличествоКРаспределению - КоличествоПорции;
				Если КоличествоКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЕсли;	
			
	КонецЦикла;
	
	НоменклатураОстатков = Новый Соответствие;
	
	Для каждого СтрокаТаблицы Из ТаблицаАналоги Цикл
		
		СтрокаАналоги = Аналоги[СтрокаТаблицы.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(СтрокаАналоги, СтруктураОчисткиАналогов);
		
		Если СтрокаАналоги.ЭтоАналог
		   И НоменклатураОстатков.Получить(СтрокаТаблицы.Номенклатура) <> СтрокаТаблицы.Характеристика Тогда
			
			НоменклатураОстатков.Вставить(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Характеристика);
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
			
			МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаАналоги Из МассивСтрок Цикл
				
				СтрокаАналоги["ОстатокНаСкладе" + ПостфиксПриемник] = СтрокаАналоги["ОстатокНаСкладе" + ПостфиксИсточник];
				
				Если Не Подтвердить Тогда
					УстановитьПризнакЕстьОстатки(СтрокаАналоги);
				КонецЕсли;
				
			КонецЦикла;	
			
		КонецЕсли;
		
		Если Подтвердить Тогда
			
			ПорцияПодбора = ПорцииПодбора.Добавить();
			ЗаполнитьЗначенияСвойств(ПорцияПодбора, СтрокаТаблицы);
			ПорцияПодбора.НомерПодбора = МаксимальныйНомерПодбора;
			ПорцияПодбора.КлючСвязи = КлючСвязи;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Подтвердить Тогда
		ОбновитьПодсказкиВозможностьЗамены();
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьИтоговыеПоказатели(Форма)
	
	ИтогоТребуется = 0;
	ИтогоЗаменено = 0;
	
	Элементы = Форма.Элементы;
	
	МассивВыделенныхСтрок = Элементы.Материалы.ВыделенныеСтроки;
	Для каждого ВыделеннаяСтрока Из МассивВыделенныхСтрок Цикл
		
		ТекущаяСтрока = Форма.Материалы.НайтиПоИдентификатору(ВыделеннаяСтрока);
		
		ИтогоТребуется = ИтогоТребуется + ТекущаяСтрока.Требуется - ТекущаяСтрока.КоличествоПодтверждено;
		ИтогоЗаменено = ИтогоЗаменено + ТекущаяСтрока.Количество - ТекущаяСтрока.КоличествоПодтверждено;
		
	КонецЦикла;
	
	Форма.ИтогоТребуется = ИтогоТребуется; 
	Форма.ИтогоЗаменено = ИтогоЗаменено;
	Форма.ИтогоКЗамене = ИтогоТребуется - ИтогоЗаменено;
	
КонецПроцедуры	

&НаСервере
Функция ПолучитьПорцииПодобранныхАналогов(Номенклатура, Характеристика, НомераСтрок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорцииПодбора.НомерПодбора КАК НомерПодбора,
	|	ПорцииПодбора.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	ПорцииПодбора.Номенклатура КАК Номенклатура,
	|	ПорцииПодбора.Характеристика КАК Характеристика,
	|	ПорцииПодбора.Количество КАК Количество
	|ПОМЕСТИТЬ ПорцииПодбора
	|ИЗ
	|	&ПорцииПодбора КАК ПорцииПодбора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	НомерСтрокиИсходный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорцииПодбора.НомерПодбора КАК НомерПодбора,
	|	ВЫРАЗИТЬ(ПорцииПодбора.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	СУММА(ПорцииПодбора.Количество) КАК Количество
	|ИЗ
	|	ПорцииПодбора КАК ПорцииПодбора
	|ГДЕ
	|	ПорцииПодбора.Номенклатура = &Номенклатура
	|	И ПорцииПодбора.Характеристика = &Характеристика
	|	И ПорцииПодбора.НомерСтрокиИсходный В(&МассивНомеровСтрок)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПорцииПодбора.НомерПодбора,
	|	ПорцииПодбора.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПодбора";
	
	Запрос.УстановитьПараметр(
		"ПорцииПодбора", 
		ПорцииПодбора.Выгрузить(, "НомерПодбора, НомерСтрокиИсходный, Номенклатура, Характеристика, Количество"));
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("МассивНомеровСтрок", НомераСтрок);
	
	Результат = Запрос.Выполнить();
	
	СписокПодборов = Новый СписокЗначений;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СписокПодборов.Добавить(
			Выборка.НомерПодбора, 
			СтрШаблон("%1 %2 %3", СтрШаблон(НСтр("ru='Заменено%1';uk='Замінено %1'"), " "), Выборка.Количество, Выборка.ЕдиницаИзмерения));
		
	КонецЦикла;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАналоги
	   И Аналоги.Количество() > Аналоги.НайтиСтроки(Новый Структура("Количество", 0)).Количество() Тогда
		СписокПодборов.Добавить(-1, НСтр("ru='Текущая';uk='Поточна'"));    	
	КонецЕсли;   
	
	Возврат СписокПодборов;
	
КонецФункции

&НаСервере
Функция ПолучитьОбщееКоличествоАналоги(Номенклатура, Характеристика, ЭтоАналог)
	
	Количество = 0;
	
	СтруктураОтбора = Новый Структура(
		"Номенклатура, Характеристика, ЭтоАналог", 
		 Номенклатура, Характеристика, ЭтоАналог);
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		Количество = Количество + СтрокаТаблицы.Количество;	
	КонецЦикла;	
	
	Возврат Количество;
	
КонецФункции

&НаСервере
Функция ПолучитьОбщееКоличествоТребуемогоМатериала(Номенклатура, Характеристика)
	
	Количество = 0;
	
	СтруктураОтбора = Новый Структура(
		"Номенклатура, Характеристика", 
		 Номенклатура, Характеристика);
	
	МассивСтрок = Материалы.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		Количество = Количество + СтрокаТаблицы.Требуется - СтрокаТаблицы.КоличествоПодтверждено;
	КонецЦикла;
	
	Возврат Количество;
	
КонецФункции	

&НаСервере
Функция ОбновитьПодсказкиВозможностьЗамены()
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, ЭтоАналог");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, Материалы.НайтиПоИдентификатору(Элементы.Материалы.ВыделенныеСтроки[0]));
	СтруктураОтбора.ЭтоАналог = Ложь;
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	
	СтруктураОтбора.Вставить("ЕстьОстатки", Истина);
	УстановитьПодсказкуНаличиеОстатков(
		СтруктураОтбора.Номенклатура, СтруктураОтбора.Характеристика, Аналоги.НайтиСтроки(СтруктураОтбора).Количество() > 0);
	
	ОбработаннаяНоменклатура = Новый Соответствие;
	ОбработаннаяНоменклатура.Вставить(СтруктураОтбора.Номенклатура, СтруктураОтбора.Характеристика);
	
	Для каждого СтрокаАналоги Из МассивСтрок Цикл
		
		СтруктураОтбораРазрешений = Новый Структура("Разрешение, ЭтоАналог");
		ЗаполнитьЗначенияСвойств(СтруктураОтбораРазрешений, СтрокаАналоги);
		
		МассивСтрокРазрешений = Аналоги.НайтиСтроки(СтруктураОтбораРазрешений);
		Для каждого СтрокаТаблицы Из МассивСтрокРазрешений Цикл
			
			Если СтрокаТаблицы.Характеристика = ОбработаннаяНоменклатура.Получить(СтрокаТаблицы.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;	
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы, "Номенклатура, Характеристика");
			
			УстановитьПодсказкуНаличиеОстатков(
				СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Характеристика, Аналоги.НайтиСтроки(СтруктураОтбора).Количество() > 0);
				
			ОбработаннаяНоменклатура.Вставить(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Характеристика);
			
		КонецЦикла;	
		
	КонецЦикла;	
		
КонецФункции	

&НаСервере
Процедура УстановитьПодсказкуНаличиеОстатков(Номенклатура, Характеристика, ЕстьОстатки)
	
	ПодсказкаЕстьОстатки = НСтр("ru='есть остатки';uk='є залишки'");
	ПодсказкаНетОстатков = НСтр("ru='нет остатков';uk='немає залишків'");
	
	ЗаменяемаяПодсказка = ?(ЕстьОстатки, ПодсказкаНетОстатков, ПодсказкаЕстьОстатки);
	АктуальнаяПодсказка = ?(ЕстьОстатки, ПодсказкаЕстьОстатки, ПодсказкаНетОстатков);
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика);
	
	МассивСтрок = Материалы.НайтиСтроки(СтруктураОтбора);
	Если СтрНайти(МассивСтрок[0].ПодсказкаВозможностьЗамены, ЗаменяемаяПодсказка) > 0 Тогда
		
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			
			СтрокаТаблицы.ПодсказкаВозможностьЗамены = 
				СтрЗаменить(СтрокаТаблицы.ПодсказкаВозможностьЗамены, ЗаменяемаяПодсказка, АктуальнаяПодсказка);
				
		КонецЦикла;
			
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Функция ТекстЗапросаЗаполненияТаблиц()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтрокиИсходный,
	|	МАКСИМУМ(Товары.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(Товары.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(Товары.КлючСвязиСпецификация) КАК КлючСвязиСпецификация,
	|	МАКСИМУМ(Товары.Количество) КАК Количество,
	|	МАКСИМУМ(Товары.Склад) КАК Склад,
	|	МАКСИМУМ(Товары.Комментарий) КАК Комментарий,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.Склад = ТаблицаОстатков.Склад
	|				ТОГДА ТаблицаОстатков.КоличествоОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОстатокНаСкладе,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.Склад <> ТаблицаОстатков.Склад
	|				ТОГДА ТаблицаОстатков.КоличествоОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОстатокНаДругихСкладах
	|ПОМЕСТИТЬ ТоварыСОстатками
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО Товары.Номенклатура = ТаблицаОстатков.Номенклатура
	|			И Товары.Характеристика = ТаблицаОстатков.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	КлючСвязиСпецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМатериалов.Разрешение КАК Разрешение,
	|	ТаблицаМатериалов.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалов.Характеристика КАК Характеристика,
	|	ТаблицаМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	ТаблицаМатериалов.КоличествоУпаковок КАК Норматив,
	|	ТаблицаМатериалов.Упаковка КАК Упаковка,
	|	НЕОПРЕДЕЛЕНО КАК ДанныеУпаковки,
	|	ТаблицаМатериалов.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук) КАК КонтролироватьКратность,
	|	ТаблицаМатериалов.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ТаблицаМатериалов.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ТаблицаМатериалов.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияПредставление 
	|ИЗ
	|	ТаблицаМатериалов КАК ТаблицаМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналогов.Разрешение КАК Разрешение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаАналогов.Разрешение) = ТИП(Документ.РазрешениеНаЗаменуМатериалов)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаАналогов.Разрешение КАК Документ.РазрешениеНаЗаменуМатериалов).Номер
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаАналогов.Разрешение) = ТИП(Документ.КорректировкаРегистров)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаАналогов.Разрешение КАК Документ.КорректировкаРегистров).Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаАналогов.Разрешение) = ТИП(Документ.РазрешениеНаЗаменуМатериалов)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаАналогов.Разрешение КАК Документ.РазрешениеНаЗаменуМатериалов).Дата
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаАналогов.Разрешение) = ТИП(Документ.КорректировкаРегистров)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаАналогов.Разрешение КАК Документ.КорректировкаРегистров).Дата
	|	КОНЕЦ КАК Дата,
	|	ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|	ТаблицаАналогов.Характеристика КАК Характеристика,
	|	ТаблицаАналогов.КоличествоУпаковок КАК Норматив,
	|	ТаблицаАналогов.Упаковка КАК Упаковка,
	|	НЕОПРЕДЕЛЕНО КАК ДанныеУпаковки,
	|	ТаблицаАналогов.Склад КАК Склад,
	|	ЕСТЬNULL(ТаблицаОстатков.КоличествоОстаток, 0) КАК ОстатокНаСкладе,
	|	ЕСТЬNULL(ТаблицаОстатков.КоличествоОстаток, 0) КАК ОстатокНаСкладеПодтверждено,
	|	ЕСТЬNULL(ТаблицаОстатков.КоличествоОстаток, 0) / (ТаблицаАналогов.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)) КАК КоэффициентНаличияНаСкладе,
	|	ИСТИНА КАК ЭтоАналог
	|ИЗ
	|	ТаблицаАналогов КАК ТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО ТаблицаАналогов.Номенклатура = ТаблицаОстатков.Номенклатура
	|			И ТаблицаАналогов.Характеристика = ТаблицаОстатков.Характеристика
	|			И ТаблицаАналогов.Склад = ТаблицаОстатков.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоэффициентНаличияНаСкладе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСОстатками.НомерСтрокиИсходный КАК НомерСтрокиИсходный,
	|	ТоварыСОстатками.Номенклатура КАК Номенклатура,
	|	ТоварыСОстатками.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ТоварыСОстатками.Характеристика КАК Характеристика,
	|	ТоварыСОстатками.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ТоварыСОстатками.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	ТоварыСОстатками.Количество КАК Требуется,
	|	ТоварыСОстатками.Склад КАК Склад,
	|	ТоварыСОстатками.КоличествоОстатокНаСкладе КАК ОстатокНаСкладе,
	|	ТоварыСОстатками.КоличествоОстатокНаДругихСкладах КАК ОстатокНаДругихСкладах,
	|	ТоварыСОстатками.Комментарий КАК Комментарий,
	|	ТаблицаМатериалов.Разрешение ЕСТЬ НЕ NULL КАК ЕстьАналогиМатериала,
	|	ТаблицаМатериалов.Разрешение КАК Разрешение,
	|	ТаблицаМатериалов.Приоритет КАК Приоритет,
	|	ТаблицаМатериалов.Период КАК Период
	|ИЗ
	|	ТоварыСОстатками КАК ТоварыСОстатками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМатериалов КАК ТаблицаМатериалов
	|		ПО ТоварыСОстатками.Номенклатура = ТаблицаМатериалов.Номенклатура
	|			И ТоварыСОстатками.Характеристика = ТаблицаМатериалов.Характеристика
	|			И (ТоварыСОстатками.КлючСвязиСпецификация = ТаблицаМатериалов.КлючСвязиСпецификация
	|				ИЛИ ТаблицаМатериалов.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиИсходный,
	|	Приоритет УБЫВ,
	|	Период УБЫВ,
	|	Разрешение УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(Номенклатура),
	|	МАКСИМУМ(НоменклатураПредставление),
	|	МАКСИМУМ(Характеристика),
	|	МАКСИМУМ(ХарактеристикаПредставление),
	|	МАКСИМУМ(КлючСвязиСпецификация),
	|	МАКСИМУМ(Требуется),
	|	МАКСИМУМ(Склад),
	|	МАКСИМУМ(Комментарий),
	|	МАКСИМУМ(ОстатокНаСкладе),
	|	МАКСИМУМ(ОстатокНаДругихСкладах),
	|	МАКСИМУМ(ЕстьАналогиМатериала)
	|ПО
	|	НомерСтрокиИсходный";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаАналогов.Упаковка",
			"ТаблицаАналогов.Номенклатура"));
	
	Возврат ТекстЗапроса;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеНорматива(Норматив)
	
	ТекущийНорматив = Норматив;
	Для Разрядность = 0 По 15 Цикл
		Если Цел(ТекущийНорматив) = ТекущийНорматив Тогда
			Прервать;
		КонецЕсли;
		ТекущийНорматив = ТекущийНорматив * 10;
	КонецЦикла;
	
	Возврат Формат(Норматив, СтрШаблон("ЧДЦ=%1;", Макс(3, Разрядность)));
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Оформление

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	УстановитьУсловноеОформлениеМатериалы();
	
	УстановитьУсловноеОформлениеАналоги();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеМатериалы()
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтаФорма, "МатериалыХарактеристика", "Материалы.ХарактеристикиИспользуются");
	
	//ЕстьАналогиМатериала, Требуется: ЦветТекста
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Материалы.Имя);
	
	ГруппаОтбораИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.ЕстьАналогиМатериала");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.Требуется");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.Количество");
	

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//Количество: Шрифт
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыКоличество.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.Количество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	
	//Количество: ЦветТекста
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыКоличество.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИтогоЗаменено.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.Требуется");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Материалы.Количество");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеАналоги()
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтаФорма, "АналогиХарактеристика", "Аналоги.ХарактеристикиИспользуются");
	
	//Аналоги: Видимость
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Аналоги.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АналогиНоменклатураЕдиницаИзмерения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Аналоги.ЭтоАналог");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//РазрешениеПредставление: ЦветТекста, Шрифт
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АналогиРазрешениеПредставление.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Аналоги.РазрешениеПредставление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,,, Истина));
	
	//ПодсказкаЗаменено: ЦветТекста, Шрифт
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АналогиПодсказкаЗаменено.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Аналоги.ПодсказкаЗаменено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(ШрифтыСтиля.МелкийШрифтТекста,,, Истина));
	
	//ИтогоЗаменено: Шрифт
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИтогоЗаменено.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИтогоЗаменено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьОстатков(Элементы, ПризнакПоказатьОстатки)
	
	Если ПризнакПоказатьОстатки = Неопределено Тогда
		ПризнакПоказатьОстатки = Ложь;
		Элементы.ФормаПоказатьОстатки.Видимость = ПризнакПоказатьОстатки;
		Элементы.РежимПодбораАналогов.Видимость = ПризнакПоказатьОстатки;
	КонецЕсли;
	
	Элементы.ФормаПоказатьОстатки.Пометка = ПризнакПоказатьОстатки;
	
	Элементы.МатериалыСклад.Видимость = ПризнакПоказатьОстатки;
	Элементы.МатериалыОстатокНаСкладе.Видимость = ПризнакПоказатьОстатки;
	Элементы.МатериалыОстатокНаДругихСкладах.Видимость = ПризнакПоказатьОстатки;
	
	Элементы.АналогиЕстьОстатки.Видимость = ПризнакПоказатьОстатки;
	Элементы.АналогиОстатокНаСкладе.Видимость = ПризнакПоказатьОстатки;
	Элементы.АналогиСклад.Видимость = ПризнакПоказатьОстатки;
		
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Форма, ИмяСтраницы, КлючСвязи = Неопределено, Режим = 0)
	
	Элементы = Форма.Элементы;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы["Страница" + ИмяСтраницы];
	
	Если ИмяСтраницы = "Аналоги" Тогда
		ОформитьМатериалыНаСтраницеАналогов(Форма);
		УстановитьОтборПоДоступнымАналогам(Элементы[ИмяСтраницы].ОтборСтрок, КлючСвязи, Режим);
		УстановитьНадписьОтсутствиеАналогов(Форма);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОформитьМатериалыНаСтраницеАналогов(Форма)
	
	Элементы = Форма.Элементы;
	
	ДанныеТекущейСтроки = Форма.Материалы.НайтиПоИдентификатору(Элементы.Материалы.ТекущаяСтрока);
	
	Форма.НоменклатураПредставление = ДанныеТекущейСтроки.НоменклатураПредставление;
	Если ЗначениеЗаполнено(ДанныеТекущейСтроки.Характеристика) Тогда
		Форма.НоменклатураПредставление = 
			Форма.НоменклатураПредставление + " / " + ДанныеТекущейСтроки.ХарактеристикаПредставление;
	КонецЕсли;	
	
	ЗаполнитьИтоговыеПоказатели(Форма);
			
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоДоступнымАналогам(Отбор, КлючСвязи = Неопределено, Режим = 0)

	СтруктураОтбора = Новый Структура;
	
	Если КлючСвязи <> Неопределено Тогда
		СтруктураОтбора.Вставить("КлючСвязиРазрешение", КлючСвязи);
	КонецЕсли;	
	
	Если Режим = 1 Тогда
		СтруктураОтбора.Вставить("ЕстьОстатки", Истина);	
	КонецЕсли;	
	
	Отбор = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеРазрешений(Номенклатура, Характеристика)
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, ЭтоАналог", Номенклатура, Характеристика, Ложь);
	СтруктураОтбораРазрешение = Новый Структура("ОтобразитьРазрешение, ЭтоАналог, Разрешение", Истина, Истина);
	
	МассивСтрок = Аналоги.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбораРазрешение, СтрокаТаблицы, "Разрешение");
		
		ЗаполнитьЗначенияСвойств(
			Аналоги.НайтиСтроки(СтруктураОтбораРазрешение)[0], СтрокаТаблицы, "РазрешениеПредставление");
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНадписьОтсутствиеАналогов(Форма)

	ТекстНадписи = "";
	
	Если Форма.Аналоги.Количество() = 0 Тогда
		ТекстНадписи = НСтр("ru='Аналоги на указанную дату не найдены';uk='Аналоги на зазначену дату не знайдені'");
	ИначеЕсли Форма.ПорцииПодбора.Количество() = 0 
	        И Форма.Аналоги.Количество() = Форма.Аналоги.НайтиСтроки(Новый Структура("Количество", 0)).Количество() Тогда
		ТекстНадписи = НСтр("ru='Ни один аналог не выбран';uk='Жоден аналог не обрано'");
	КонецЕсли;
	
	Форма.Элементы.НадписьОтсутствиеАналогов.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокГиперссылкиОстаткиИДоступностьТоваров(ИмяСтраницы)
	
	ФрагментыГиперссылки = Новый Массив;
	ФрагментыГиперссылки.Добавить(НСтр("ru='См. также:';uk='Див. також:'"));
	ФрагментыГиперссылки.Добавить(" ");
	
	ФрагментыГиперссылки.Добавить(
		Новый ФорматированнаяСтрока(
			НСтр("ru='Остатки и доступность товаров;';uk='Залишки і доступність товарів;'"),,,, "ОстаткиИДоступностьТоваров"));
			
	ФрагментыГиперссылки.Добавить(" ");
	
	ФрагментыГиперссылки.Добавить(
		Новый ФорматированнаяСтрока(
			НСтр("ru='Остатки и доступность товаров на других складах';uk='Залишки і доступність товарів на інших складах'"),,,, "ОстаткиИДоступностьТоваровНаДругихСкладах"));
	
	ЭлементГиперссылка = Элементы["ГиперссылкаОстаткиИДоступность" + ?(ИмяСтраницы = "Аналоги", "Материалов", "Товаров")];//ДекорацияФормы - 
	ЭлементГиперссылка.Заголовок = Новый ФорматированнаяСтрока(ФрагментыГиперссылки);
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти