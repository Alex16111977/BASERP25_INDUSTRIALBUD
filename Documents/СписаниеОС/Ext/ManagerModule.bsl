
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		
		УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры, ПрочиеРасходы(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры, ПартииПрочихРасходов(ТекстыЗапроса, Регистры));
		
		УчетОСВызовСервера.ПереоценкаОСБухгалтерскийУчет(ТекстыЗапроса, Регистры, ПереоценкаОСБухгалтерскийУчет(ТекстыЗапроса, Регистры));

		УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры, ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете());
		
		ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
	   		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПрочееОприходованиеТоваров.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//++ НЕ УТКА
	Документы.СписаниеОСМеждународныйУчет.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//-- НЕ УТКА
	Документы.НалоговаяНакладная.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Списание ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании	 - ТаблицаЗначений	 - Таблица с командами создания на основании. Для изменения.
//  	См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//  Порядок						 - Число			 - Порядок команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеОС";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	Истина							КАК СписаниеОстаточнойСтоимости,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	Документ.СписаниеОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДопПараметры);
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаСписанияОстаточнойСтоимости", Реквизиты.Период);
	Запрос.УстановитьПараметр("ТекущееСостояниеОбъектов", Истина);
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Списание ОС';uk='Списання ОЗ'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СписаниеОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ДобавитьВременныеТаблицы(ТекстыЗапроса, "ТекущееСостояниеОбъектов");
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, "Документ.СписаниеОС");
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ОсновноеСредство,
	|	втПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втПорядокУчетаОС.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	
	|
	|	
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьБУ, 0) КАК ОстаточнаяСтоимостьБУ,
	|
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьНУ, 0)
	|		+ ЕСТЬNULL(втТекущееСостояниеОбъектов.КорректировкаСтоимостиНУ, 0) КАК ОстаточнаяСтоимостьНУ
	|
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТекущееСостояниеОбъектов КАК втТекущееСостояниеОбъектов
	|		ПО ТаблицаДокумента.ОсновноеСредство = втТекущееСостояниеОбъектов.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОС
	|		ПО ТаблицаДокумента.ОсновноеСредство = втПорядокУчетаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПредыдущаяПереоценкаСтоимости(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущаяПереоценкаСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	 
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, "Документ.СписаниеОС");
	
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица остатков доп капитала
	|"+
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|	ЕСТЬNULL(ПереоценкаОСБухгалтерскийУчетОбороты.СуммаПереоценкиПриход, 0) КАК СуммаДооценки,
	|	ЕСТЬNULL(ПереоценкаОСБухгалтерскийУчетОбороты.СуммаПереоценкиРасход, 0) КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимостиДанные
	|ИЗ
	|	втСписокОС КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаОСБухгалтерскийУчет.Обороты(
	|				,
	|				&КонецМесяца,
	|				Регистратор,
	|				Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							втСписокОС КАК Т)) КАК ПереоценкаОСБухгалтерскийУчетОбороты
	|		ПО СписокОС.ОсновноеСредство = ПереоценкаОСБухгалтерскийУчетОбороты.ОсновноеСредство
	|			И СписокОС.Организация = ПереоценкаОСБухгалтерскийУчетОбороты.Организация
	|			И (ПереоценкаОСБухгалтерскийУчетОбороты.Регистратор <> &Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущаяПереоценкаСтоимостиДанные.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(втПредыдущаяПереоценкаСтоимостиДанные.СуммаДооценки) КАК СуммаДооценки,
	|	СУММА(втПредыдущаяПереоценкаСтоимостиДанные.СуммаУценки) КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимостиСгруппированная
	|ИЗ
	|	втПредыдущаяПереоценкаСтоимостиДанные КАК втПредыдущаяПереоценкаСтоимостиДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	втПредыдущаяПереоценкаСтоимостиДанные.ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная.ОбъектУчета КАК ОбъектУчета,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимости
	|ИЗ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная КАК втПредыдущаяПереоценкаСтоимостиСгруппированная
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСуммаУменьшенияДооценкиПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втСуммаУменьшенияДооценкиПоДокументу";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втАмортизацияПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(втНачисленнаяАмортизация.СуммаУменьшенияДооценки) КАК Сумма
	|ПОМЕСТИТЬ втСуммаУменьшенияДооценкиПоДокументу
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Функция ПереоценкаОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПереоценкаОСБухгалтерскийУчет", Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ВременнаяТаблицаСуммаУменьшенияДооценкиПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаПредыдущаяПереоценкаСтоимости(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ // Выбытие ОС
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ЕСТЬNULL(ПредыдущаяПереоценкаСтоимости.СуммаДооценки, 0) 
	|	- ЕСТЬNULL(ПредыдущаяПереоценкаСтоимости.СуммаУценки, 0) 
	|	- ЕСТЬNULL(СуммаУменьшенияДооценкиПоДокументу.Сумма, 0) КАК СуммаПереоценки
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценкаСтоимости КАК ПредыдущаяПереоценкаСтоимости
	|		ПО ТаблицаОС.ОсновноеСредство = ПредыдущаяПереоценкаСтоимости.ОбъектУчета
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСуммаУменьшенияДооценкиПоДокументу КАК СуммаУменьшенияДооценкиПоДокументу
	|		ПО ТаблицаОС.ОсновноеСредство = СуммаУменьшенияДооценкиПоДокументу.ОбъектУчета
	|		
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ЕСТЬNULL(ПредыдущаяПереоценкаСтоимости.СуммаДооценки, 0)
	|		- ЕСТЬNULL(ПредыдущаяПереоценкаСтоимости.СуммаУценки,0)
	|		- ЕСТЬNULL(СуммаУменьшенияДооценкиПоДокументу.Сумма, 0)) > 0";
	
КонецФункции


Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, "Документ.СписаниеОС");
	
	Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ КАК НачислятьАмортизациюНУ,
	|	ПорядокУчета.НалоговоеНазначение               КАК НалоговоеНазначение,
	|	ПорядокУчета.НалоговаяГруппаОС                 КАК НалоговаяГруппаОС
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(&Период, Регистратор <> &Ссылка
	|		И Организация = &Организация
	|		И ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				втСписокОС КАК СписокОС)) КАК ПорядокУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&СобытиеОС КАК Событие,
	|
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента,
	|	0 КАК СуммаЗатратБУ,
	|	0 КАК СуммаЗатратНУ
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПрочиеРасходы", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СуммаСНДСРегл,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|		ПО &СтатьяРасходов = ПВХСтатьиРасходов.Ссылка
	|		
	|ГДЕ
	|	НЕ ПВХСтатьиРасходов.ВариантРаспределенияРасходовУпр В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)
	|                  
	|";
	
КонецФункции

Функция ПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПартииПрочихРасходов", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|";
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете()
	
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ПорядокУчетаОСБУ.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|		&Дата,
	|		Регистратор<>&Ссылка
	|		И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ТаблицаОС КАК Т)) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	ПорядокУчетаОСБУ.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету)";
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&Проведен                               КАК Проведен,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.СписаниеОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизации("СписаниеОС"));
	ТекстыВременныхТаблиц.Добавить(УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам());
	ТекстыВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции


Функция ТекстОтраженияВРеглУчете() Экспорт
	Возврат ТекстОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(УчетОСВызовСервера.ТекстОтраженияВРеглУчетеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(СписаниеОстаточнойСтоимостиОС());
	ТекстыОтражения.Добавить(СписаниеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(СписаниеОстатковДопКапиталаОС());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции


Функция СписаниеОстаточнойСтоимостиОС()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|//  Списание остаточной стоимости ОС на расходы (Дт СчетРасходов:: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьБУ, 0) КАК Сумма,
	
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	 |	втПервоначальныеСведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	
	|
	|	""%1"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка    
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Списание остаточной стоимости ОС на расходы';uk='Списання залишкової вартості ОЗ на витрати'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СписаниеНачисленнойАмортизации()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации (Дт Счет начисления амортизации:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втПервоначальныеСведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втПервоначальныеСведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И втПервоначальныеСведения.ЕстьСчетАмортизации
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Списание начисленной амортизации';uk='Списання нарахованої амортизації'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции


Функция СписаниеОстатковДопКапиталаОС()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание дополнительного капитала (Дт СчетДооценки :: Кт СчетНераспределеннойПрибыли)
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(-ОстаткиДопКапитала.СуммаОстаток, 0) - ЕСТЬNULL(втНачисленнаяАмортизация.СуммаУменьшенияДооценки, 0)КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,	
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДооценкаОсновныхСредств) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|   НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|		ПО Строки.ОсновноеСредство = втНачисленнаяАмортизация.ОбъектУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаМесяцОкончание,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДооценкаОсновныхСредств),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|				) КАК ОстаткиДопКапитала
	|		ПО Строки.ОсновноеСредство = ОстаткиДопКапитала.Субконто1
	|		И  Операция.Организация = ОстаткиДопКапитала.Организация
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (ЕСТЬNULL(-ОстаткиДопКапитала.СуммаОстаток, 0) 
	|		- ЕСТЬNULL(втНачисленнаяАмортизация.СуммаУменьшенияДооценки, 0)) > 0
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Списание дополнительного капитала';uk='Списання додаткового капіталу'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Форма ОЗ-3
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОЗ3";
	КомандаПечати.Идентификатор = "ОЗ3";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-3';uk='Форма ОЗ-3'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;	

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьФормОСБюджет";
	КомандаПечати.Идентификатор = "АктСписанияОСБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт списания основных средств (частичной ликвидации)';uk='Акт списання основних засобів (часткової ліквідації)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;	

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОЗ3(ПараметрыПечати, Ссылка) Экспорт
	
	// Функция формирует табличный документ с печатной формой ОЗ-3
// 
//Функция ПечатьОЗ3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка);
		Запрос.УстановитьПараметр("ТекДата",        Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеОС.Дата                           КАК ДатаАкта,
		|	СписаниеОС.Номер                          КАК НомерАкта,
		|	ВЫРАЗИТЬ(СписаниеОС.Организация.НаименованиеПолное 
		|	                    КАК СТРОКА(1000))     КАК Организация,
		|	СписаниеОС.Организация.КодПоЕДРПОУ               КАК ЕДРПОУ,
		|	СписаниеОС.ПричинаСписания                КАК ПричинаСписания,
		|	СписаниеОС.ПричинаСписания.Код            КАК КодПричины
		|ИЗ
		|	Документ.СписаниеОС                       КАК СписаниеОС
		|
		|ГДЕ
		|	СписаниеОС.Ссылка = &Ссылка";
		
		ВыборкаПоШапке = Запрос.Выполнить();
		
		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка"        , Ссылка);
		Запрос.УстановитьПараметр("СписокОС"      , СписокОС);
		Запрос.УстановитьПараметр("ТекДата"       , Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.УстановитьПараметр("СостояниеВвода", Перечисления.СостоянияОС.ПринятоКУчету);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
	//	|	ПервоначальныеСведенияБУ.ИнвентарныйНомер КАК ИнвентарныйНомер,
	//	|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	    |	ПервоначальныеСведенияБУ.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимостьБУ  КАК ПервоначальнаяСтоимость,
		|	ПорядокУчетаОС.СчетУчета КАК СчетКт,
		|	СписаниеОСОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	СписаниеОСОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
		|	СписаниеОСОС.ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
		|	ДанныеСчетаРасходов.Счет КАК СчетДт,
		|	ДанныеСчетУчета.СуммаОстаток - ДанныеСчетАмортизации.СуммаОборотКт КАК ОстаточнаяСтоимость,
		|	ДанныеСчетАмортизации.СуммаОборотКт КАК Амортизация,
		|	МестонахождениеОС.Местонахождение КАК СдалоПодразделение,
		|	ПервоначальныеСведенияБУ.ДатаВводаВЭксплуатациюБУ КАК ДатаВвода
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|				&ТекДата,
		|				ОсновноеСредство В (&СписокОС)
		|					И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияБУ
		|		ПО СписаниеОСОС.ОсновноеСредство = ПервоначальныеСведенияБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ПорядокУчетаОС.СрезПоследних(
		|		                    &ТекДата,
		|		                    ОсновноеСредство В (&СписокОС)
		|		                    И Организация = &ТекОрганизация) КАК ПорядокУчетаОС
		|		ПО СписаниеОСОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(
		|				&ТекДата,
		|				Организация = &ТекОрганизация
		|					И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО СписаниеОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
		|				&ТекДата,
		|				Счет В
		|					(ВЫБРАТЬ
		|						Т.СчетУчета
		|					ИЗ
		|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
		|							И Организация = &ТекОрганизация) КАК Т),
		|				,
		|				(Организация, Подразделение, Субконто1) В
		|					(ВЫБРАТЬ
		|						ДокументСписания.Организация,
		|						ДокументСписания.Подразделение,
		|						СписаниеОСОС.ОсновноеСредство
		|					ИЗ
		|						Документ.СписаниеОС.ОС КАК СписаниеОСОС
		|							ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК ДокументСписания
		|							ПО
		|								СписаниеОСОС.Ссылка = ДокументСписания.Ссылка
		|					ГДЕ
		|						ДокументСписания.Ссылка = &Ссылка)) КАК ДанныеСчетУчета
		|		ПО СписаниеОСОС.ОсновноеСредство = ДанныеСчетУчета.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|				,
		|				&ТекДата,
		|				,
		|				,
		|				Счет В
		|					(ВЫБРАТЬ
		|						Т.СчетНачисленияАмортизации
		|					ИЗ
		|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
		|							И Организация = &ТекОрганизация) КАК Т),
		|				,
		|				(Организация, Подразделение, Субконто1) В
		|					(ВЫБРАТЬ
		|						ДокументСписания.Организация,
		|						ДокументСписания.Подразделение,
		|						СписаниеОСОС.ОсновноеСредство
		|					ИЗ
		|						Документ.СписаниеОС.ОС КАК СписаниеОСОС
		|							ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК ДокументСписания
		|							ПО
		|								СписаниеОСОС.Ссылка = ДокументСписания.Ссылка
		|					ГДЕ
		|						ДокументСписания.Ссылка = &Ссылка)) КАК ДанныеСчетАмортизации 
		|		ПО (СписаниеОСОС.ОсновноеСредство = ДанныеСчетАмортизации.Субконто1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
		|				,
		|				&ТекДата,
		|				,
		|				,
		|				,
		|				(Организация, Подразделение, Субконто1) В
		|					(ВЫБРАТЬ
		|						ДокументСписания.Организация,
		|						ДокументСписания.Подразделение,
		|						ДокументСписания.СтатьяРасходов
		|					ИЗ
		|						Документ.СписаниеОС КАК ДокументСписания
		|					ГДЕ
		|						ДокументСписания.Ссылка = &Ссылка),
		|				КорСчет В
		|					(ВЫБРАТЬ
		|						Т.СчетУчета
		|					ИЗ
		|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
		|							И Организация = &ТекОрганизация) КАК Т),
		|				) КАК ДанныеСчетаРасходов
		|		ПО СписаниеОСОС.ОсновноеСредство = ДанныеСчетаРасходов.КорСубконто1
		|ГДЕ
		|	СписаниеОСОС.Ссылка = &Ссылка";
		
		ВыборкаПоОС = Запрос.Выполнить();
		
		Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС", ВыборкаПоШапке, ВыборкаПоОС);

КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыАктСписанияОС_2016(ПараметрыПечати, Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",         Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеОС.Дата КАК ДатаАкта,
	|	СписаниеОС.Номер КАК НомерАкта,
	|	СписаниеОС.Организация КАК Организация,
	|	СписаниеОС.Подразделение КАК СдалоПодразделение
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС
	|ГДЕ
	|	СписаниеОС.Ссылка = &Ссылка";
	
	ВыборкаПоШапке = Запрос.Выполнить();
	
	СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка"        , Ссылка);
	Запрос.УстановитьПараметр("СписокОС"      , СписокОС);
	Запрос.УстановитьПараметр("ТекДата"       , Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
	Запрос.УстановитьПараметр("СостояниеВвода", Перечисления.СостоянияОС.ПринятоКУчету);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияБУ.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ПорядокУчетаОС.СчетУчета КАК СчетКт,
	|	СписаниеОСОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
	|	СписаниеОСОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	СписаниеОСОС.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
	|	СписаниеОСОС.ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
	|	ДанныеСчетаРасходов.Счет КАК СчетДт,
	|	ДанныеСчетАмортизации.СуммаОборотКт КАК Амортизация,
	|	МестонахождениеОС.Местонахождение КАК СдалоПодразделение,
	|	ПервоначальныеСведенияБУ.ДатаВводаВЭксплуатациюБУ КАК ДатаВвода,
	|	МестонахождениеОС.МОЛ КАК МОЛ,
	|	ПараметрыАмортизацииОСБУСрезПоследних.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&ТекДата,
	|				ОсновноеСредство В (&СписокОС)
	|					И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияБУ
	|		ПО СписаниеОСОС.ОсновноеСредство = ПервоначальныеСведенияБУ.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|				&ТекДата,
	|				ОсновноеСредство В (&СписокОС)
	|					И Организация = &ТекОрганизация) КАК ПорядокУчетаОС
	|		ПО СписаниеОСОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(
	|				&ТекДата,
	|				Организация = &ТекОрганизация
	|					И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
	|		ПО СписаниеОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ТекДата,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчета
	|					ИЗ
	|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
	|							И Организация = &ТекОрганизация) КАК Т),
	|				,
	|				(Организация, Подразделение, Субконто1) В
	|					(ВЫБРАТЬ
	|						ДокументСписания.Организация,
	|						ДокументСписания.Подразделение,
	|						СписаниеОСОС.ОсновноеСредство
	|					ИЗ
	|						Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|							ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК ДокументСписания
	|							ПО
	|								СписаниеОСОС.Ссылка = ДокументСписания.Ссылка
	|					ГДЕ
	|						ДокументСписания.Ссылка = &Ссылка)) КАК ДанныеСчетУчета
	|		ПО СписаниеОСОС.ОсновноеСредство = ДанныеСчетУчета.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|				,
	|				&ТекДата,
	|				,
	|				,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетНачисленияАмортизации
	|					ИЗ
	|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
	|							И Организация = &ТекОрганизация) КАК Т),
	|				,
	|				(Организация, Подразделение, Субконто1) В
	|					(ВЫБРАТЬ
	|						ДокументСписания.Организация,
	|						ДокументСписания.Подразделение,
	|						СписаниеОСОС.ОсновноеСредство
	|					ИЗ
	|						Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|							ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК ДокументСписания
	|							ПО
	|								СписаниеОСОС.Ссылка = ДокументСписания.Ссылка
	|					ГДЕ
	|						ДокументСписания.Ссылка = &Ссылка)) КАК ДанныеСчетАмортизации
	|		ПО СписаниеОСОС.ОсновноеСредство = ДанныеСчетАмортизации.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
	|				,
	|				&ТекДата,
	|				,
	|				,
	|				,
	|				(Организация, Подразделение, Субконто1) В
	|					(ВЫБРАТЬ
	|						ДокументСписания.Организация,
	|						ДокументСписания.Подразделение,
	|						ДокументСписания.СтатьяРасходов
	|					ИЗ
	|						Документ.СписаниеОС КАК ДокументСписания
	|					ГДЕ
	|						ДокументСписания.Ссылка = &Ссылка),
	|				КорСчет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчета
	|					ИЗ
	|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ТекДата, ОсновноеСредство В (&СписокОС)
	|							И Организация = &ТекОрганизация) КАК Т),
	|				) КАК ДанныеСчетаРасходов
	|		ПО СписаниеОСОС.ОсновноеСредство = ДанныеСчетаРасходов.КорСубконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|				&ТекДата,
	|				ОсновноеСредство В (&СписокОС)
	|					И Организация = &ТекОрганизация) КАК ПараметрыАмортизацииОСБУСрезПоследних
	|		ПО СписаниеОСОС.ОсновноеСредство = ПараметрыАмортизацииОСБУСрезПоследних.ОсновноеСредство
	|ГДЕ
	|	СписаниеОСОС.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	МОЛ";
	
	ВыборкаПоОС = Запрос.Выполнить();
	
	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	Хозрасчетный.СчетДт КАК СчетДт,
	|	Хозрасчетный.СчетКт КАК СчетКт,
	|	Хозрасчетный.Сумма КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.НомерСтроки";
	
	ВыборкаБУ = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ", ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ);
	
КонецФункции  //ПолучитьДанныеДляПечатнойФормыАктСписанияОС_2016()

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетОСВызовСервера.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
