
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивы.ОбработатьПараметрыФормыПереданныеИзЗакрытияМесяца(ЭтаФорма);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДоначислениеВНУ(Команда)

	ПараметрыФормы = Новый Структура("ДоначислениеВНУ", Истина);	
	ОткрытьФорму("Документ.АмортизацияОС.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция СоздатьДоначисленияВНУНаСервере()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	   "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	   |	УчетнаяПолитикаОрганизацийЕН.Организация КАК Организация
	   |ПОМЕСТИТЬ ОрганизацииКОбработке
	   |ИЗ
	   |	РегистрСведений.НастройкиСистемыНалогообложения КАК УчетнаяПолитикаОрганизацийЕН
	   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК УчетнаяПолитикаОрганизацийПрибыль
	   |		ПО УчетнаяПолитикаОрганизацийЕН.Организация = УчетнаяПолитикаОрганизацийПрибыль.Организация
	   |			И (УчетнаяПолитикаОрганизацийПрибыль.Период > УчетнаяПолитикаОрганизацийЕН.Период)
	   |			И (УчетнаяПолитикаОрганизацийПрибыль.ПлательщикНалогаНаПрибыль = ИСТИНА)
	   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЕН КАК НастройкиУчетаЕН
	   |			ПО УчетнаяПолитикаОрганизацийЕН.Организация = НастройкиУчетаЕН.Организация
 	   |			И (УчетнаяПолитикаОрганизацийЕН.Период = НАЧАЛОПЕРИОДА(НастройкиУчетаЕН.Период, МЕСЯЦ))
 	   |			И НастройкиУчетаЕН.ГруппаПлательщикаЕдиногоНалога = ЗНАЧЕНИЕ(Перечисление.ГруппыПлательщиковЕдиногоНалога.ТретьяГруппаОсобая)
       |
	   |ГДЕ
	   |	УчетнаяПолитикаОрганизацийЕН.ПлательщикЕН = ИСТИНА
	   |;
	   |
	   |////////////////////////////////////////////////////////////////////////////////
	   |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	   |	СтатусыПлательщиковЕдиногоНалога.Организация КАК Организация,
	   |	НАЧАЛОПЕРИОДА(СтатусыПлательщиковЕдиногоНалога.Период, МЕСЯЦ) КАК ПериодНачало,
	   |	ВЫБОР
	   |		КОГДА МИНИМУМ(ДОБАВИТЬКДАТЕ(СтатусыПлательщиковЕдиногоНалогаОкончание.Период, СЕКУНДА, -1)) < &МаксимальныйПериод
	   |			ТОГДА МИНИМУМ(ДОБАВИТЬКДАТЕ(СтатусыПлательщиковЕдиногоНалогаОкончание.Период, СЕКУНДА, -1))
	   |		ИНАЧЕ &МаксимальныйПериод
	   |	КОНЕЦ КАК ПериодКонец
	   |ИЗ
	   |	РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалога
	   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОрганизацииКОбработке КАК ОрганизацииКОбработке
	   |		ПО СтатусыПлательщиковЕдиногоНалога.Организация = ОрганизацииКОбработке.Организация
	   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалогаОкончание
	   |		ПО (СтатусыПлательщиковЕдиногоНалогаОкончание.Организация = СтатусыПлательщиковЕдиногоНалога.Организация)
	   |			И (СтатусыПлательщиковЕдиногоНалогаОкончание.Период > СтатусыПлательщиковЕдиногоНалога.Период)
	   |			И (СтатусыПлательщиковЕдиногоНалогаОкончание.ГруппаПлательщикаЕдиногоНалога <> ЗНАЧЕНИЕ(Перечисление.ГруппыПлательщиковЕдиногоНалога.ТретьяГруппаОсобая))
	   |ГДЕ
	   |	СтатусыПлательщиковЕдиногоНалога.ГруппаПлательщикаЕдиногоНалога = ЗНАЧЕНИЕ(Перечисление.ГруппыПлательщиковЕдиногоНалога.ТретьяГруппаОсобая)
	   |	И СтатусыПлательщиковЕдиногоНалога.ПлательщикЕН = ИСТИНА
	   |
	   |СГРУППИРОВАТЬ ПО
	   |	СтатусыПлательщиковЕдиногоНалога.Организация,
	   |	НАЧАЛОПЕРИОДА(СтатусыПлательщиковЕдиногоНалога.Период, МЕСЯЦ)
	   |
	   |УПОРЯДОЧИТЬ ПО
	   |	Организация,
	   |	ПериодНачало"; 
	МаксимальныйПериод = '20230731235959';
	Запрос.УстановитьПараметр("МаксимальныйПериод", МаксимальныйПериод);
	
	ТаблицаПериодов = Запрос.Выполнить().Выгрузить();    
	
	Если ТаблицаПериодов.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПериодов.Организация КАК Организация,
		|	ТаблицаПериодов.ПериодНачало КАК ПериодНачало,
		|	ТаблицаПериодов.ПериодКонец КАК ПериодКонец
		|ПОМЕСТИТЬ ТаблицаПериодов
		|ИЗ
		|	&ТаблицаПериодов КАК ТаблицаПериодов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПериодов.Организация КАК Организация,
		|	НАЧАЛОПЕРИОДА(АмортизацияОС.Дата, МЕСЯЦ) КАК ПериодДокумента
		|ИЗ
		|	ТаблицаПериодов КАК ТаблицаПериодов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияОС КАК АмортизацияОС
		|		ПО ТаблицаПериодов.Организация = АмортизацияОС.Организация
		|			И (АмортизацияОС.ДоначислениеВНУ = Истина)
		|			И (АмортизацияОС.Дата > ТаблицаПериодов.ПериодНачало)
		|			И (АмортизацияОС.Дата <= ТаблицаПериодов.ПериодКонец)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	ПериодДокумента"; 
	
	Запрос.УстановитьПараметр("ТаблицаПериодов", ТаблицаПериодов);
	ВыборкаСуществующиеДокументы = Запрос.Выполнить().Выбрать();
	
	Для каждого СтрокаПериодов Из ТаблицаПериодов Цикл
		ТекущийМесяц = СтрокаПериодов.ПериодНачало;
		ВыборкаСуществующиеДокументы.Сбросить();
		Пока ТекущийМесяц < СтрокаПериодов.ПериодКонец Цикл
			
			СтруктураПоиска = Новый Структура("Организация, ПериодДокумента");
			СтруктураПоиска.Организация = СтрокаПериодов.Организация;
			СтруктураПоиска.ПериодДокумента = ТекущийМесяц;
			Если ВыборкаСуществующиеДокументы.НайтиСледующий(СтруктураПоиска) = Ложь Тогда

				НовыйДокумент = Документы.АмортизацияОС.СоздатьДокумент();
				НовыйДокумент.Организация = СтрокаПериодов.Организация;
				НовыйДокумент.Дата = КонецМесяца(ТекущийМесяц);
				НовыйДокумент.ДоначислениеВНУ = Истина; 
				НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
				НовыйДокумент.Комментарий = "#Сформовано автоматично";
				НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
				
			КонецЕсли;
			ТекущийМесяц = НачалоМесяца(ДобавитьМесяц(ТекущийМесяц, 1));
		КонецЦикла;
	КонецЦикла;
	
    Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура КомандаГрупповоеСозданиеДоначислениеВНУ(Команда) 
	
	ТекстВопроса = НСтр("ru='Будут сформированы непроведенные документы 
                         |""Амортизация ОС"" с доначислением в налоговом учете 
                         |для всех организаций за все периоды единого налога 2%. Продолжить?'
                         |;uk='Будуть сформовані непроведені документи
                         |""Амортизація ОЗ"" з донарахуванням у податковому обліку
                         |для всіх організацій за всі періоди єдиного податку 2%. Продовжити?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаГрупповоеСозданиеДоначислениеВНУЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаГрупповоеСозданиеДоначислениеВНУЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Результат = СоздатьДоначисленияВНУНаСервере();
		Если Результат = Ложь Тогда 
			ТекстПредупреждения = НСтр("ru='Нет организаций, которые были плательщиками единого налога 2%, а затем стали плательщиками налога на прибыль. 
                                |Групповое создание доначислений амортизации в налоговом учете не требуется.'
                                |;uk='Немає організацій, які були платниками єдиного податку 2%, а згодом стали платниками податку на прибуток.
                                |Групове створення донарахувань амортизації у податковому обліку не потрібне.'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
		КонецЕсли;
		ЭтаФорма.Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти