#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Регламентная операция".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегламентнаяОперация) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РегламентнаяОперация.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РегламентнаяОперация);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРеглУчет";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиЭтаповЗакрытияМесяца
//++ НЕ УТ
#Область РасчетКурсовыхРазниц

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетКурсовыхРазниц(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетКурсовыхРазниц,
		Ложь, Истина, Ложь,
		Перечисления.ОперацииЗакрытияМесяца.РасчетНалоговыхРазницПослеПереходаСЕН);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.ОтражениеДокументовВРегламентированномУчете);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Рассчитать';uk='Розрахувати'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РегламентнаяОперация.Использование_РасчетКурсовыхРазниц");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РегламентнаяОперация.Выполнить_РасчетКурсовыхРазниц");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаЛокализация.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперация();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц);
	
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РасчетКурсовыхРазниц(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.ПроверитьИспользованиеРегламентированногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.УстановитьПараметр("ВестиУУНаПланеСчетов", 
		ПолучитьФункциональнуюОпцию("ВестиУУНаПланеСчетовХозрасчетный")
		И НачалоМесяца(ПараметрыОбработчика.ПараметрыРасчета.Период) >= Константы.ДатаНачалаУУНаПланеСчетовХозрасчетный.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка,
	|	Хозрасчетный.Валютный,
	|	Хозрасчетный.НалоговыйУчет,
	|	Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов,
	|	Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|ПОМЕСТИТЬ втСчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Валютный
	|	И НЕ (Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|			ИЛИ &ВестиУУНаПланеСчетов И Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Счета учета расчетов с поставщиками
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаАвтоматизированногоУчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.Ссылка В ИЕРАРХИИ (
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиКредиторами),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиДебиторами),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыданнымАвансам),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученным)
	|		)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И Не ПланСчетов.Забалансовый
	|	И Не ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|	
	| ОБЪЕДИНИТЬ ВСЕ 
	|/////////////////////////////////////////////////////////////////////////////
	|// Другие автоматизированные счета
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.Ссылка В ИЕРАРХИИ 
	|		(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КраткосрочныеКредитыБанковВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДолгосрочныеКредитыБанковВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТекущаяЗадолженностьПоДолгосрочнымОбязательствамВИностраннойВалюте)
	|		 )
	|	И Не ПланСчетов.ЗапретитьИспользоватьВПроводках
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.Счет                    КАК Счет,
	|	Т.Организация             КАК Организация,
	|	Т.Подразделение           КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Субконто1               КАК Субконто1,
	|	Т.Субконто2               КАК Субконто2,
	|	Т.Субконто3               КАК Субконто3,
	|	Т.Валюта                  КАК Валюта,
	|	Т.ВалютнаяСуммаОстаток    КАК ВалютнаяСуммаОстаток,
	|	Т.СуммаОстаток            КАК СуммаБУОстаток,
	|	Т.СуммаУУОстаток          КАК СуммаУУОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаКонецПериода,
    |			(Счет В (ВЫБРАТЬ Т.Ссылка ИЗ втСчета КАК Т)) И (Счет НЕ В (ВЫБРАТЬ Ссылка ИЗ втСчетаАвтоматизированногоУчета)),
	|			,
	|			Организация В (&МассивОрганизаций)) КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация
	|ИЗ
	|	втОстатки КАК Т";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, РезультатЗапроса.Выгрузить().Количество());
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru='Нет остатков по переоцениваемым валютным счетам в регистре бухгалтерии ""Хозрасчетный"".';uk='Немає залишків по переоцінюваних валютних рахунках в регістрі бухгалтерії ""Госпрозрахунковий"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Период,
	|	Т.Валюта,
	|	Т.Курс,
	|	Т.Кратность
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Период,
	|	Т.Валюта,
	|	Т.Курс,
	|	Т.Кратность
	|ПОМЕСТИТЬ втКурсУУ
	|ИЗ
	|	втКурсы КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО Т.Валюта = Константы.ВалютаУправленческогоУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	СУММА(Данные.ОстатокРегл)        КАК ОстатокРегл,
	|	СУММА(Данные.ОстатокПоКурсу)     КАК ОстатокПоКурсу,
	|	СУММА(Данные.АбсолютнаяРазница)  КАК АбсолютнаяРазница,
	|	СУММА(Данные.КурсоваяРазница)    КАК КурсоваяРазница,
	|	
	|	СУММА(Данные.ОстатокПоКурсуУУ)    КАК ОстатокПоКурсуУУ,
	|	СУММА(Данные.АбсолютнаяРазницаУУ) КАК АбсолютнаяРазницаУУ,
	|	СУММА(Данные.КурсоваяРазницаУУ)   КАК КурсоваяРазницаУУ
	|ПОМЕСТИТЬ ДанныеДляПереоценки
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		Остатки.СуммаБУОстаток КАК ОстатокРегл,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ОстатокПоКурсу,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность - Остатки.СуммаБУОстаток КАК АбсолютнаяРазница,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(31,2)) - Остатки.СуммаБУОстаток КАК КурсоваяРазница,
	|		
	|		0 КАК ОстатокПоКурсуУУ,
	|		0 КАК АбсолютнаяРазницаУУ,
	|		0 КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|	ГДЕ
	|		НЕ Остатки.Счет.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|		И Остатки.Счет НЕ В (ВЫБРАТЬ Ссылка ИЗ втСчетаАвтоматизированногоУчета)
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(31,2)) <> Остатки.СуммаБУОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		0 КАК ОстатокРегл,
	|		0 КАК ОстатокПоКурсу,
	|		0 КАК АбсолютнаяРазница,
	|		0 КАК КурсоваяРазница,
	|		
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ОстатокПоКурсуУУ,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность - Остатки.СуммаУУОстаток КАК АбсолютнаяРазницаУУ,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ЧИСЛО(31,2)) - Остатки.СуммаУУОстаток КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсУУ КАК КурсУУ
	|			ПО ИСТИНА
	|	ГДЕ
	|		&ВестиУУНаПланеСчетов
	|		И НЕ Остатки.Счет.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|		И Остатки.Счет НЕ В (ВЫБРАТЬ Ссылка ИЗ втСчетаАвтоматизированногоУчета)
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ЧИСЛО(31,2)) <> Остатки.СуммаУУОстаток
	|	) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Счет,
	|	Данные.Организация,
	|	Данные.Подразделение,
	|	Данные.НаправлениеДеятельности,
	|	Данные.Субконто1,
	|	Данные.Субконто2,
	|	Данные.Субконто3,
	|	Данные.Валюта,
	|	Данные.ОстатокВалюты
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация
	|ИЗ
	|	ДанныеДляПереоценки КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, Выборка.Количество());
	
	Пока Выборка.Следующий() Цикл
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='По организации ""%1"" не выполнен расчет курсовых разниц.';uk='По організації ""%1"" не виконано розрахунок курсових різниць.'",ОбщегоНазначения.КодОсновногоЯзыка()),
				Выборка.Организация));
		
	КонецЦикла;
	
КонецПроцедуры

Процедура Выполнить_РасчетКурсовыхРазниц(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаЛокализация.СформироватьДокументРегламентнаяОперация(ПараметрыОбработчика, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
КонецПроцедуры

#КонецОбласти

#Область РасчетНалоговыхРазницПослеПереходаСЕН

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетНалоговыхРазницПослеПереходаСЕН(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(
		ТаблицаЭтапов, 
		ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетНалоговыхРазницПослеПереходаСЕН,
		Ложь, 
		Истина, 
		Ложь,
		Перечисления.ОперацииЗакрытияМесяца.ОтражениеДокументовВРегламентированномУчете
	);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Рассчитать';uk='Розрахувати'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РегламентнаяОперация.Использование_РасчетНалоговыхРазницПослеПереходаСЕН");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РегламентнаяОперация.Выполнить_РасчетНалоговыхРазницПослеПереходаСЕН");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаЛокализация.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперация();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетНалоговыхРазницПослеПереходаСЕН);
	
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РасчетНалоговыхРазницПослеПереходаСЕН(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.ПроверитьИспользованиеРегламентированногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросОрганизацииЕН = Новый Запрос;  
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(ЗапросОрганизацииЕН, ПараметрыОбработчика);
	ЗапросОрганизацииЕН.Текст = "            
	|ВЫБРАТЬ
	|   СтатусыПлательщиковЕдиногоНалога.Организация
	|ИЗ
	|	РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалога
	|ГДЕ
	|	СтатусыПлательщиковЕдиногоНалога.Организация В (&МассивОрганизаций)
	|	И СтатусыПлательщиковЕдиногоНалога.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|   И СтатусыПлательщиковЕдиногоНалога.ПлательщикЕН
	|	И СтатусыПлательщиковЕдиногоНалога.Период < &КонецПериода
	|";
	
	НетОрганизацийСЕН = ЗапросОрганизацииЕН.Выполнить().Пустой();
	Если НетОрганизацийСЕН Тогда       
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru='Организации не использовали ранее ЕН.';uk='Організації раніше не використовували ЄП,'",ОбщегоНазначения.КодОсновногоЯзыка())
		);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	Запрос.Текст = "            
	|ВЫБРАТЬ
	|   СтатусыПлательщиковЕдиногоНалога.Организация,
	|	СтатусыПлательщиковЕдиногоНалога.Период,
	|	СтатусыПлательщиковЕдиногоНалога.ПлательщикЕН
	|ПОМЕСТИТЬ ИсторияЕН
	|ИЗ
	|	РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалога
	|ГДЕ
	|	СтатусыПлательщиковЕдиногоНалога.Организация В (&МассивОрганизаций)
	|	И СтатусыПлательщиковЕдиногоНалога.Период < &КонДата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияЕН.Период КАК НачалоПериода,
	|	ИсторияЕН.Организация КАК Организация,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ИсторияЕН1.Период ЕСТЬ NULL
	|				ТОГДА &КонДата
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(ИсторияЕН1.Период, СЕКУНДА, -1)
	|		КОНЕЦ) КАК КонецПериода
	|ПОМЕСТИТЬ ПериодыЕН
	|ИЗ
	|	ИсторияЕН КАК ИсторияЕН
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсторияЕН КАК ИсторияЕН1
	|		ПО ИсторияЕН.Период < ИсторияЕН1.Период
	|          И ИсторияЕН.Организация = ИсторияЕН1.Организация
	|ГДЕ
	|	ИсторияЕН.ПлательщикЕН
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияЕН.Организация,
	|	ИсторияЕН.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РасчетыСКлиентамиПоДокументам.Долг) КАК Долг,
	|	СУММА(РасчетыСКлиентамиПоДокументам.Предоплата) КАК Предоплата,
	|	РасчетыСКлиентамиПоДокументам.Валюта КАК Валюта,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентамиПоДокументам.Регистратор,
	|	МИНИМУМ(РасчетыСКлиентамиПоДокументам.Период) КАК Период,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоДокументам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоДокументам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоДокументам.Регистратор
	|	КОНЕЦ КАК ДокументРеализации
	|ПОМЕСТИТЬ ВторыеСобытия
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН
	|		ПО (РасчетыСКлиентамиПоДокументам.Период МЕЖДУ ПериодыЕН.НачалоПериода И ПериодыЕН.КонецПериода
	|          И РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация = ПериодыЕН.Организация
	|		)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН1
	|		ПО (РасчетыСКлиентамиПоДокументам.РасчетныйДокумент.Дата МЕЖДУ ПериодыЕН1.НачалоПериода И ПериодыЕН1.КонецПериода
	|          И РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация = ПериодыЕН1.Организация
	|		)
	|ГДЕ
	|	РасчетыСКлиентамиПоДокументам.Период МЕЖДУ &НачДата И &КонДата
	|	И РасчетыСКлиентамиПоДокументам.ХозяйственнаяОперация В ИЕРАРХИИ(&ХозяйственныеОперации)
	|	И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				И РасчетыСКлиентамиПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ РасчетыСКлиентамиПоДокументам.Долг > 0
	|				И РасчетыСКлиентамиПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|	И ПериодыЕН.НачалоПериода ЕСТЬ NULL
	|	И РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И НЕ &НоваяАрхитектураРасчетов
	|
	|СГРУППИРОВАТЬ ПО       
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентамиПоДокументам.Регистратор,
	|	РасчетыСКлиентамиПоДокументам.Валюта,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоДокументам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоДокументам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоДокументам.Регистратор
	|	КОНЕЦ  
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(РасчетыСКлиентамиПоСрокам.Долг),
	|	СУММА(РасчетыСКлиентамиПоСрокам.Предоплата),
	|	РасчетыСКлиентамиПоСрокам.Валюта,  
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор,
	|	МИНИМУМ(РасчетыСКлиентамиПоСрокам.Период) КАК Период,
	|	РасчетыСКлиентамиПоСрокам.РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоСрокам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоСрокам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоСрокам.ДокументРегистратор
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН
	|		ПО (РасчетыСКлиентамиПоСрокам.Период МЕЖДУ ПериодыЕН.НачалоПериода И ПериодыЕН.КонецПериода
	|          И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация = ПериодыЕН.Организация
	|		)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН1
	|		ПО (РасчетыСКлиентамиПоСрокам.ДатаВозникновения МЕЖДУ ПериодыЕН1.НачалоПериода И ПериодыЕН1.КонецПериода
	|          И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация = ПериодыЕН1.Организация
	|		)
	|ГДЕ
	|	РасчетыСКлиентамиПоСрокам.Период МЕЖДУ &НачДата И &КонДата
	|	И РасчетыСКлиентамиПоСрокам.ХозяйственнаяОперация В(&ХозяйственныеОперацииНоваяАрхитектура)
	|	И РасчетыСКлиентамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ПериодыЕН.НачалоПериода ЕСТЬ NULL
	|	И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И &НоваяАрхитектураРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентамиПоСрокам.РасчетныйДокумент,
	|	РасчетыСКлиентамиПоСрокам.Валюта,  
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоСрокам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоСрокам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоСрокам.ДокументРегистратор
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВторыеСобытия.ДокументРеализации,
	|	ВторыеСобытия.Организация
	|ПОМЕСТИТЬ ДокументыРеализации
	|ИЗ
	|	ВторыеСобытия КАК ВторыеСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ВыручкаИСебестоимостьПродаж.Регистратор,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов КАК Валюта,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СтоимостьРегл + ВыручкаИСебестоимостьПродаж.ДопРасходыРегл + ВыручкаИСебестоимостьПродаж.ТрудозатратыРегл + ВыручкаИСебестоимостьПродаж.ПостатейныеПостоянныеРегл + ВыручкаИСебестоимостьПродаж.ПостатейныеПеременныеРегл) КАК Себестоимость,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл) КАК Доход,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВВалютеВзаиморасчетов) КАК Сумма
	|ПОМЕСТИТЬ ФинансовыеПоказатели
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации КАК ДокументыРеализации
	|		ПО ВыручкаИСебестоимостьПродаж.Регистратор = ДокументыРеализации.ДокументРеализации
	|			И ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация = ДокументыРеализации.Организация	
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация,
	|	ВыручкаИСебестоимостьПродаж.Регистратор,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ        
	|	ВторыеСобытия.Организация КАК Организация,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ФинансовыеПоказатели.Сумма, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Доход * ВторыеСобытия.Долг / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК Число(15, 2)) КАК СуммаОплата2,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|			КОГДА ВторыеСобытия.Период < &ДатаИзмененияРедакцииНКУ
	|				ТОГДА 0	   
	|			КОГДА ЕСТЬNULL(ФинансовыеПоказатели.Сумма, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Себестоимость * ВторыеСобытия.Долг / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК Число(15, 2)) КАК СуммаПродажа1,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|			КОГДА ВторыеСобытия.Период < &ДатаИзмененияРедакцииНКУ
	|				ТОГДА 0	   
	|			КОГДА ЕСТЬNULL(ФинансовыеПоказатели.Сумма, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Доход * ВторыеСобытия.Предоплата / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК Число(15, 2)) КАК СуммаОплата1,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|			КОГДА ВторыеСобытия.Период < &ДатаИзмененияРедакцииНКУ
	|				ТОГДА 0	   
	|			КОГДА ЕСТЬNULL(ФинансовыеПоказатели.Сумма, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Себестоимость * ВторыеСобытия.Предоплата / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК Число(15, 2)) КАК СуммаПродажа2
	|ПОМЕСТИТЬ РасчетныеДанные
	|ИЗ
	|	ВторыеСобытия КАК ВторыеСобытия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФинансовыеПоказатели КАК ФинансовыеПоказатели
	|		ПО ВторыеСобытия.ДокументРеализации = ФинансовыеПоказатели.Регистратор
	|			И ВторыеСобытия.Валюта = ФинансовыеПоказатели.Валюта
	|			И ВторыеСобытия.Организация = ФинансовыеПоказатели.Организация
	|СГРУППИРОВАТЬ ПО
	|	ВторыеСобытия.Организация
	|;            
	|ВЫБРАТЬ        
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|   СУММА(ВЫБОР 
	|		КОГДА ХозрасчетныйОбороты.Субконто1 = &СубконтоОплата1
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт 
	|		ИНАЧЕ 0 
	|   КОНЕЦ) КАК СуммаОплата1,
	|   СУММА(ВЫБОР 
	|		КОГДА ХозрасчетныйОбороты.Субконто1 = &СубконтоОплата2
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт 
	|		ИНАЧЕ 0 
	|   КОНЕЦ) КАК СуммаОплата2,
	|   СУММА(ВЫБОР 
	|		КОГДА ХозрасчетныйОбороты.Субконто1 = &СубконтоПродажа1
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт 
	|		ИНАЧЕ 0 
	|   КОНЕЦ) КАК СуммаПродажа1,
	|   СУММА(ВЫБОР 
	|		КОГДА ХозрасчетныйОбороты.Субконто1 = &СубконтоПродажа2
	|			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт 
	|		ИНАЧЕ 0 
	|   КОНЕЦ) КАК СуммаПродажа2
	|ПОМЕСТИТЬ ФактическиеДанные
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|		&НачДата, 
	|		&КонДата, 
	|		, 
	|		Счет = &СчетРИ,
	|		&МассивСубконто, 
	|		Организация В (&МассивОрганизаций)
	|		,
	|		, 
	|	) КАК ХозрасчетныйОбороты
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Организация
	|;
	|ВЫБРАТЬ        
	|	ЕСТЬNULL(РасчетныеДанные.Организация, ФактическиеДанные.Организация) КАК Организация,  
	|	ЕСТЬNULL(РасчетныеДанные.СуммаОплата1, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаОплата1, 0) КАК РазницаСуммаОплата1,
	|	ЕСТЬNULL(РасчетныеДанные.СуммаОплата2, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаОплата2, 0) КАК РазницаСуммаОплата2,
	|	ЕСТЬNULL(РасчетныеДанные.СуммаПродажа1, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаПродажа1, 0) КАК РазницаСуммаПродажа1,	
	|	ЕСТЬNULL(РасчетныеДанные.СуммаПродажа2, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаПродажа2, 0) КАК РазницаСуммаПродажа2	
	|ИЗ
	|	РасчетныеДанные КАК РасчетныеДанные
	|		ПОЛНОЕ СОЕДИНЕНИЕ ФактическиеДанные КАК ФактическиеДанные
	|		ПО РасчетныеДанные.Организация = ФактическиеДанные.Организация
	|ГДЕ                 
	|	(ЕСТЬNULL(РасчетныеДанные.СуммаОплата1, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаОплата1, 0)) <> 0
	|	ИЛИ (ЕСТЬNULL(РасчетныеДанные.СуммаОплата2, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаОплата2, 0)) <> 0
	|	ИЛИ (ЕСТЬNULL(РасчетныеДанные.СуммаПродажа1, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаПродажа1, 0)) <> 0
	|	ИЛИ (ЕСТЬNULL(РасчетныеДанные.СуммаПродажа2, 0) - ЕСТЬNULL(ФактическиеДанные.СуммаПродажа2, 0)) <> 0
	|;                                                   
	|ВЫБРАТЬ        
	|	РасчетныеДанные.Организация КАК Организация,
	|	РасчетныеДанные.СуммаОплата1 КАК СуммаОплата1,
	|	РасчетныеДанные.СуммаОплата2 КАК СуммаОплата2,
	|	РасчетныеДанные.СуммаПродажа1 КАК СуммаПродажа1,
	|	РасчетныеДанные.СуммаПродажа2 КАК СуммаПродажа2
	|ИЗ
	|	РасчетныеДанные КАК РасчетныеДанные
	|ГДЕ                 
	|	ЕСТЬNULL(РасчетныеДанные.СуммаОплата1, 0)  <> 0
	|	ИЛИ ЕСТЬNULL(РасчетныеДанные.СуммаОплата2, 0) <> 0
	|	ИЛИ ЕСТЬNULL(РасчетныеДанные.СуммаПродажа1, 0) <> 0
	|	ИЛИ ЕСТЬNULL(РасчетныеДанные.СуммаПродажа1, 0) <> 0
	|
	|";
	
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(ПараметрыОбработчика.ПараметрыРасчета.Период));
	Запрос.УстановитьПараметр("КонДата", КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.Период));
	Запрос.УстановитьПараметр("ДатаИзмененияРедакцииНКУ", '20230801');
	
	МассивХО = Новый Массив;
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионера);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ПереносАванса);
	Запрос.УстановитьПараметр("ХозяйственныеОперации", МассивХО);
	
	МассивХО_НоваяАрхитектура = Новый Массив;
	МассивХО_НоваяАрхитектура.Добавить(Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
	МассивХО_НоваяАрхитектура.Добавить(Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента);
	Запрос.УстановитьПараметр("ХозяйственныеОперацииНоваяАрхитектура", МассивХО_НоваяАрхитектура);
	
	Запрос.УстановитьПараметр("НоваяАрхитектураРасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	
	Запрос.УстановитьПараметр("СчетРИ", ПланыСчетов.Хозрасчетный.РазницыПоНалогуНаПрибыльРучныеКорректировки);
	МассивСубконто = Новый Массив();
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиНалоговыхДеклараций);
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);                                              
	Запрос.УстановитьПараметр("СубконтоОплата1", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_Аванс_ЕН);
	Запрос.УстановитьПараметр("СубконтоОплата2", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_ОплатаТоваров_ЕН);
	Запрос.УстановитьПараметр("СубконтоПродажа1", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_СС_Товаров_ЕН);
	Запрос.УстановитьПараметр("СубконтоПродажа2", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_СС_Товаров);
	
    РезультатыЗапросов = Запрос.ВыполнитьПакет();
	КоличествоПакетов  = РезультатыЗапросов.Количество();                                    
	
	НеТребуется 		= РезультатыЗапросов[КоличествоПакетов - 1].Пустой();
	ВыполненоПравильно 	= РезультатыЗапросов[КоличествоПакетов - 2].Пустой();
	Требуется 			= НЕ НеТребуется;
    
    Если ВыполненоПравильно И Требуется Тогда
    ИначеЕсли ВыполненоПравильно И НеТребуется Тогда
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru='Расчет налоговых разниц после перехода с ЕН не требуется.';uk='У розрахунку податкових різниць після переходу з ЄП немає потреби.'",ОбщегоНазначения.КодОсновногоЯзыка())
		);
    ИначеЕсли НЕ ВыполненоПравильно И Требуется Тогда
		Выборка = РезультатыЗапросов[КоличествоПакетов - 1].Выбрать();
		
		ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, Выборка.Количество());
		
		Пока Выборка.Следующий() Цикл
			
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
				ПараметрыОбработчика,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='По организации ""%1"" не выполнен расчет налоговых разниц после перехода с ЕН.';uk='По організації ""%1"" не виконаний розрахунок податкових різниць після переходу з ЄП.'",ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Организация
				)
			);
			
		КонецЦикла;
    Иначе
    КонецЕсли;
    
КонецПроцедуры

Процедура Выполнить_РасчетНалоговыхРазницПослеПереходаСЕН(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаЛокализация.СформироватьДокументРегламентнаяОперация(ПараметрыОбработчика, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР(); 
		
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции

#КонецОбласти


#Область ПроводкиРеглУчетаУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстЗапроса = "";
	
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

// Создает и проводит документы "Регламентная операция" за указанный месяц.
//
//	Параметры:
//		Период - Дата - Начало месяца, в котором необходимо создать документы.
//		МассивОпераций - Массив - Содержит массив типов регламентных операций (значений перечисления "ТипыРегламентныхОпераций")
//		Организация - Массив, Неопределено - Список организаций по которым формируются документы. Если список пустой,
//												то документы формируются по всем организациям.
//		Отказ - Булево - Используется при вызове из формы закрытия месяца. При установке в "Истина" - дальнейшие операции
//							выполняться не будут.
//		ПроверятьНеобходимостьРаспределения - Булево - Признак того, что операцию выполнять только в случае если требуется. В противном случае выполнятся безусловно.
//		ПроводитьДокументы - Булево - Признак того, что созданные документы необходимо провести.
//
// Возвращаемое значение:
//		РезультатРасчета - Структура - структура, содержащая:
//			* МассивДокументов - Массив - массив ссылок на созданные/измененные документы
//			* ТекстОшибки - Строка - текст описание ошибки; заполнен при Отказ = Истина
//			* ТипОперации - ПеречислениеСсылка.ТипыРегламентныхОпераций - операция, завершившаяся с ошибкой
//			* Организация - СправочникСсылка.Организации - организация, по которой возникла ошибка.
//
Функция РассчитатьРегламентныеОперации(Период, МассивОпераций, Организация = Неопределено, Отказ = Ложь,
	ПроверятьНеобходимостьРаспределения = Ложь,	ПроводитьДокументы = Истина) Экспорт
	
	РезультатРасчета = Новый Структура;
	РезультатРасчета.Вставить("МассивДокументов", Новый Массив);
	РезультатРасчета.Вставить("СписокОшибок", Новый Массив);
	РезультатРасчета.Вставить("ТекстОшибки", "");
	РезультатРасчета.Вставить("ДокументСОшибкой");
	РезультатРасчета.Вставить("КоличествоДанных", 0);
	РезультатРасчета.Вставить("ТипОперации");
	РезультатРасчета.Вставить("Организация");
	
	ОперацииРеглУчета = Новый Массив;
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ПереоценкаСуммыВВалютеФинОтчетности);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода);
	
	
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		МассивОрганизаций = Организация;
	ИначеЕсли ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		МассивОрганизаций = Организация.ВыгрузитьЗначения();
	Иначе
		МассивОрганизаций = Новый Массив;
		Если ЗначениеЗаполнено(Организация) Тогда
			МассивОрганизаций.Добавить(Организация);
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаОпераций = Новый ТаблицаЗначений;
	ТаблицаОпераций.Колонки.Добавить("ТипОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРегламентныхОпераций"));
	ТаблицаОпераций.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	Для Сч = 1 По МассивОпераций.Количество() Цикл
		СтрокаОперации = ТаблицаОпераций.Добавить();
		СтрокаОперации.ТипОперации = МассивОпераций[Сч-1];
		СтрокаОперации.Порядок = Сч;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОпераций.Порядок      КАК Порядок,
	|	ТаблицаОпераций.ТипОперации  КАК ТипОперации
	|ПОМЕСТИТЬ ВтТипыОпераций
	|ИЗ &ТаблицаОпераций КАК ТаблицаОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеСправочника.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ПОМЕСТИТЬ ВтОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка В (&МассивОрганизаций)
	|			ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|			ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка                    КАК Организация,
	|	Организации.ГоловнаяОрганизация       КАК ГоловнаяОрганизация,
	|	Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ТипыРегламентныхОпераций.ТипОперации  КАК ТипОперации,
	|	ТипыРегламентныхОпераций.Порядок      КАК Порядок
	|ПОМЕСТИТЬ ВтТипыОперацийПоОрганизациям
	|ИЗ
	|	ВтОрганизации КАК Организации,
	|	ВтТипыОпераций КАК ТипыРегламентныхОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	МАКСИМУМ(ЕСТЬNULL(РегламентнаяОперацияПроведена.Ссылка, РегламентнаяОперацияЗаписана.Ссылка)) КАК Ссылка
	|ИЗ
	|	ВтТипыОперацийПоОрганизациям КАК ТипыОпераций
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияПроведена
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияПроведена.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияПроведена.ТипОперации
	|		И РегламентнаяОперацияПроведена.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И РегламентнаяОперацияПроведена.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияЗаписана
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияЗаписана.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияЗаписана.ТипОперации
	|		И РегламентнаяОперацияЗаписана.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ РегламентнаяОперацияЗаписана.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК НастройкиУчетаНалогаНаПрибыль
	|		ПО ТипыОпераций.ГоловнаяОрганизация = НастройкиУчетаНалогаНаПрибыль.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК НастройкиСистемыНалогообложения
	|		ПО ТипыОпераций.ГоловнаяОрганизация = НастройкиСистемыНалогообложения.Организация
	|СГРУППИРОВАТЬ ПО
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	ТипыОпераций.Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыОпераций.Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Ссылка,
	|	РегламентнаяОперация.Организация КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтОрганизации КАК Организации
	|	ПО
	|		РегламентнаяОперация.Организация = Организации.Ссылка 
	|ГДЕ
	|	РегламентнаяОперация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РегламентнаяОперация.Проведен
	|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ЗакрытиеГода)
	|
	|";

	Запрос.УстановитьПараметр("ДатаНачала", 			 НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 			 КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("ТаблицаОпераций", 		 ТаблицаОпераций);
	Запрос.УстановитьПараметр("МассивОрганизаций", 		 МассивОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 	 НЕ ЗначениеЗаполнено(МассивОрганизаций));
	
	Результат = Запрос.ВыполнитьПакет();

	ТаблицаОпераций     = Результат[3].Выгрузить();
	ТаблицаЗакрытияГода = Результат[4].Выгрузить();
	ИмяСобытия		= НСтр("ru='РегламентнаяОперация';uk='РегламентнаяОперация'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
	Для Каждого Строка Из ТаблицаОпераций Цикл
		
		КоличествоОбработанныхДанных = 0;
	
		Если Строка.Организация = Справочники.Организации.УправленческаяОрганизация
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалоговыхРазницПослеПереходаСЕН
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода) Тогда
			Продолжить;
		ИначеЕсли Строка.ОбособленноеПодразделение
				И НЕ ОперацияИспользуетсяДляОбособленногоПодразделения(Строка.ТипОперации) Тогда
			Продолжить; // Эти операции выполняются только для головной организации
		ИначеЕсли ОперацииРеглУчета.Найти(Строка.ТипОперации) <> Неопределено
			И (Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") ИЛИ Период < Константы.ДатаНачалаВеденияРеглУчета.Получить()) Тогда
			Продолжить; // Эта операция выполняется только при включенной опции регламентированного учета
		ИначеЕсли НЕ РеглУчетСервер.НастройкиУчета().ДополнительноВедетсяУчетВВалютеФинОтчетности
			И Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПереоценкаСуммыВВалютеФинОтчетности Тогда   
			Продолжить; // Эта операция выполняются только при ведении управленческого учета на плане счетов в валюте ФО
		КонецЕсли;
		
		Если Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата Тогда
			
			// Отменим проведение закрытия года, т.к. они оперируют одними данными
			СтруктураПоиска = Новый Структура("Организация", Строка.Организация);
			ЗакрытияГода = ТаблицаЗакрытияГода.НайтиСтроки(СтруктураПоиска);
			Для каждого ЗакрытиеГода Из ЗакрытияГода Цикл
				ЗакрытиеГодаДокумент = ЗакрытиеГода.Ссылка.ПолучитьОбъект();
				РезультатРасчета.МассивДокументов.Добавить(ЗакрытиеГода.Ссылка);
				Попытка 
					// Проверим, что документ находится в доступном для изменения периоде.
					ОписаниеОшибкиДаты = "";
					Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ЗакрытиеГодаДокумент, , ОписаниеОшибкиДаты) Тогда
						ОписаниеОшибкиДаты = РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
						ВызватьИсключение ОписаниеОшибкиДаты;
					КонецЕсли;
					ЗакрытиеГодаДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					РезультатРасчета.ТипОперации = Строка.ТипОперации;
					РезультатРасчета.Организация = Строка.Организация;
					РезультатРасчета.ДокументСОшибкой = ЗакрытиеГода.Ссылка;
					
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					Возврат РезультатРасчета;
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			РеглОперация = Документы.РегламентнаяОперация.СоздатьДокумент();
			РеглОперация.Дата = КонецМесяца(Период);
			РеглОперация.ТипОперации = Строка.ТипОперации;
			РеглОперация.Организация = Строка.Организация;
		Иначе
			РеглОперация = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		// Проверим, что документ находится в доступном для изменения периоде.
		ОписаниеОшибкиДаты = "";
		
		Если ДатыЗапретаИзменения.ИзменениеЗапрещено(РеглОперация, , ОписаниеОшибкиДаты) Тогда
			Отказ = Истина;
			ТекстОшибки = РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
			РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
			РезультатРасчета.ТипОперации = Строка.ТипОперации;
			РезультатРасчета.Организация = Строка.Организация;
			РезультатРасчета.ДокументСОшибкой = Строка.Ссылка;
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			Возврат РезультатРасчета;
		КонецЕсли;
		
		Если РеглОперация.Проведен Тогда
			РеглОперация.ДополнительныеСвойства.Вставить("ОчисткаДляПоследующегоПроведения",Истина);
			Попытка 
				РеглОперация.Записать(РежимЗаписиДокумента.ОтменаПроведения)
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				Возврат РезультатРасчета;
			КонецПопытки;
		КонецЕсли;
		
		РеглОперация.ДополнительныеСвойства.Вставить("ВыводитьСообщенияВЖурналРегистрации", Истина);
		РеглОперация.Записать(РежимЗаписиДокумента.Запись);
		РезультатРасчета.МассивДокументов.Добавить(РеглОперация.Ссылка);
		
		Если ПроводитьДокументы Тогда
			
			СообщенияПроверки = РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьЗаполнениеОбъектаСПерехватомСообщений(РеглОперация);
			
			Если ЗначениеЗаполнено(СообщенияПроверки) Тогда
				
				Отказ = Истина;
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='При проверке документа ""%1"" были обнаружены ошибки:
|%2'
|;uk='При перевірці документа ""%1"" були виявлені помилки:
|%2'"),
					СокрЛП(РеглОперация.Ссылка),
					РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(СообщенияПроверки, " - "));
				
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				
				Возврат РезультатРасчета;
				
			КонецЕсли;
				
			Попытка
				
				РеглОперация.Записать(РежимЗаписиДокумента.Проведение);
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("КоличествоОбработанныхДанных") Тогда
					КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + РеглОперация.ДополнительныеСвойства.КоличествоОбработанныхДанных;
				КонецЕсли; 
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("СписокОшибок") Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
						РезультатРасчета.СписокОшибок, 
						РеглОперация.ДополнительныеСвойства.СписокОшибок);
				КонецЕсли;
				
			Исключение
				
				Отказ = Истина;
				
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("СписокОшибок")
					И РеглОперация.ДополнительныеСвойства.СписокОшибок.Количество() <> 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
						РезультатРасчета.СписокОшибок, 
						РеглОперация.ДополнительныеСвойства.СписокОшибок);
					
				Иначе
					
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
				КонецЕсли; 
				
				Возврат РезультатРасчета;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатРасчета.Вставить("КоличествоДанных", КоличествоОбработанныхДанных);
	
	Возврат РезультатРасчета;
	
КонецФункции

// Возвращает признак необходимости выполнения указанной операции для обособленного подразделения.
//
Функция ОперацияИспользуетсяДляОбособленногоПодразделения(ТипОперации) Экспорт
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата                    КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры



// Прочее

Функция ТаблицыПроведения(НомераТаблиц, Результат)
	
	ТаблицыПроведения = Новый Структура;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если НРег(Лев(НомерТаблицы.Ключ, 7)) = "таблица" Тогда
			ТаблицыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицыПроведения;
	
КонецФункции


// Расчет курсовых разниц

Процедура РасчетКурсовыхРазниц(ПараметрыРасчета, Отказ) Экспорт
	
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	Запрос = Новый Запрос(ТекстЗапросаРасчетКурсовыхРазниц());
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НаДату", Период);
	
	НастройкиРеглУчета = РеглУчетСервер.НастройкиУчета(); 
	УправленческийУчетНаПланеСчетовРегл = НастройкиРеглУчета.ДополнительноВедетсяУправленческийУчет;
	ДатаНачалаВеденияУУ = НастройкиРеглУчета.ДатаНачалаВеденияУправленческогоУчета;
	Запрос.УстановитьПараметр("ВестиУУНаПланеСчетов", УправленческийУчетНаПланеСчетовРегл И Период >= ДатаНачалаВеденияУУ);
	Запрос.УстановитьПараметр("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);
	
	СтатьяДДС = Новый Структура("ВидСубконто, Прибыль, Убыток");
	СтатьяДДС.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств;
	СтатьяДДС.Прибыль = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль;
	СтатьяДДС.Убыток = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток;
	
	Содержание = НСтр("ru='Курсовые разницы по данным регламентированного учета';uk='Курсові різниці за даними регламентованого обліку'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
	
	КурсоваяРазница = Новый Структура("Ресурс, Сумма");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ПроводкиДокумента.Добавить();
		Проводка.Активность = Истина;
		Проводка.Период = Период;
		Проводка.Организация = ПараметрыРасчета.Организация;
		Проводка.Содержание = Содержание;
		
		КурсоваяРазница.Ресурс = "Сумма";
		КурсоваяРазница.Сумма = Выборка.КурсоваяРазница;
		ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
		
		Если УправленческийУчетНаПланеСчетовРегл Тогда
			
			КурсоваяРазница.Ресурс = "СуммаУУ";
			КурсоваяРазница.Сумма = Выборка.КурсоваяРазницаУУ;
			
			Если Выборка.РазныйЗнакКусовыхРазниц Тогда
				// Добавим новую проводку для отражение курсовой разницы по УУ
				Проводка = ПроводкиДокумента.Добавить();
				Проводка.Активность = Истина;
				Проводка.Период = Период;
				Проводка.Организация = ПараметрыРасчета.Организация;
				
				ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
			Иначе
				// Заполним данные в текущей проводке
				ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;// по валютным остаткам
	
	ПроводкиДокумента.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, ПараметрыСтатьиДДС = Неопределено)
	
	Перем КорСчет, КорСубконто, КорВидСубконто;
	
	СуммаРазницы = Макс(КурсоваяРазница.Сумма, -КурсоваяРазница.Сумма);
	
	Если КурсоваяРазница.Сумма > 0 Тогда // отразим прибыль
		Счет = "Дт";
		Кор = "Кт";
		КорУчетПоПодразделениям = Выборка.СчетДоходовУчетПоПодразделениям;
		КорУчетПоНаправлениям = Выборка.СчетДоходовУчетПоНаправлениямДеятельности;
		Если Не Выборка.СчетЗабалансовый Тогда
			КорСчет = Выборка.СчетДоходов;
			КорСубконто = Выборка.СтатьяДоходов;
            КорВидСубконто = Выборка.ВидСубконтоДоходы;
		КонецЕсли;
		СтатьяДДС = ?(ПараметрыСтатьиДДС = Неопределено, Неопределено, ПараметрыСтатьиДДС.Прибыль);
		
	ИначеЕсли КурсоваяРазница.Сумма < 0 Тогда // отразим убытки
		Счет = "Кт";
		Кор = "Дт";
		КорУчетПоПодразделениям = Выборка.СчетРасходовУчетПоПодразделениям;
		КорУчетПоНаправлениям = Выборка.СчетРасходовУчетПоНаправлениямДеятельности;
		Если Не Выборка.СчетЗабалансовый Тогда
			КорСчет = Выборка.СчетРасходов;
			КорСубконто = Выборка.СтатьяРасходов;
            КорВидСубконто = Выборка.ВидСубконтоРасходы;
		КонецЕсли;
		СтатьяДДС = ?(ПараметрыСтатьиДДС = Неопределено, Неопределено, ПараметрыСтатьиДДС.Убыток);
	Иначе // проводку не заполняем
		Возврат;
	КонецЕсли;
	
	// заполним счет и аналитику возникновения курсовой разницы
	Проводка["Счет"+Счет] = Выборка.Счет;
	Проводка["Валюта"+Счет] = Выборка.Валюта;
	Проводка["Подразделение"+Счет] = Выборка.Подразделение;
	Проводка["НаправлениеДеятельности"+Счет] = Выборка.НаправлениеДеятельности;
	ВидыСубконто = Выборка.ВидыСубконтоСчета.Выгрузить();
	Для Каждого стр Из ВидыСубконто Цикл
		Проводка["Субконто" + Счет][стр.ВидСубконто] = Выборка["Субконто"+стр.НомерСтроки];
		Если СтатьяДДС <> Неопределено И стр.ВидСубконто = ПараметрыСтатьиДДС.ВидСубконто И стр.ТолькоОбороты Тогда
			Проводка["Субконто" + Счет][стр.ВидСубконто] = СтатьяДДС;
		КонецЕсли;
	КонецЦикла;
	
	// заполним счет учета прибыли\убытка от курсовой разницы
	Проводка["Счет" + Кор] = КорСчет;
	Если КорУчетПоПодразделениям Тогда
		Проводка["Подразделение"+Кор] = Выборка.Подразделение;
	КонецЕсли;
	Если КорУчетПоНаправлениям Тогда
		Проводка["НаправлениеДеятельности"+Кор] = Выборка.НаправлениеДеятельности;
	КонецЕсли;
	Если ЗначениеЗаполнено(КорВидСубконто) И  КорСчет.ВидыСубконто.Найти(КорВидСубконто, "ВидСубконто") <> Неопределено Тогда
		Проводка["Субконто" + Кор][КорВидСубконто] = КорСубконто;
	КонецЕсли;
	
	Проводка[КурсоваяРазница.Ресурс] = СуммаРазницы;
	
КонецПроцедуры

Функция ТекстЗапросаРасчетКурсовыхРазниц()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ПОМЕСТИТЬ втКурсУУ
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату, Валюта = &ВалютаУпр) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату, ) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Счета учета расчетов с поставщиками
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаАвтоматизированногоУчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.Ссылка В ИЕРАРХИИ (
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиКредиторами),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСДругимиДебиторами),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыданнымАвансам),
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученным)
	|		)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И Не ПланСчетов.Забалансовый
	|	И Не ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|	
	| ОБЪЕДИНИТЬ ВСЕ 
	|/////////////////////////////////////////////////////////////////////////////
	|// Другие автоматизированные счета
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.Ссылка В ИЕРАРХИИ 
	|		(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КраткосрочныеКредитыБанковВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДолгосрочныеКредитыБанковВИностраннойВалюте),
	|		 ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТекущаяЗадолженностьПоДолгосрочнымОбязательствамВИностраннойВалюте)
	|		 )
	|	И Не ПланСчетов.ЗапретитьИспользоватьВПроводках
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка,
	|	Хозрасчетный.Валютный,
	|	Хозрасчетный.НалоговыйУчет,
	|	Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов,
	|	Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|ПОМЕСТИТЬ втСчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Валютный
	|	И Хозрасчетный.Ссылка НЕ В (ВЫБРАТЬ Ссылка ИЗ втСчетаАвтоматизированногоУчета)
	|	И НЕ Хозрасчетный.Забалансовый
	|	И НЕ (Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|			ИЛИ &ВестиУУНаПланеСчетов И Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Счет                    КАК Счет,
	|	Остатки.Организация             КАК Организация,
	|	Остатки.Подразделение           КАК Подразделение,
	|	Остатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Остатки.Субконто1               КАК Субконто1,
	|	Остатки.Субконто2               КАК Субконто2,
	|	Остатки.Субконто3               КАК Субконто3,
	|	Остатки.Валюта                  КАК Валюта,
	|	Остатки.ВалютнаяСуммаОстаток    КАК ВалютнаяСуммаОстаток,
	|	Остатки.СуммаОстаток            КАК ОстатокБУОстаток,
	|	Остатки.СуммаУУОстаток          КАК СуммаУУОстаток
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаОстатков,
	|			Счет В (ВЫБРАТЬ Т.Ссылка ИЗ втСчета КАК Т),
	|			,
	|			Организация = &Организация) КАК Остатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	СУММА(Данные.ОстатокРегл)        КАК ОстатокРегл,
	|	СУММА(Данные.ОстатокПоКурсу)     КАК ОстатокПоКурсу,
	|	СУММА(Данные.АбсолютнаяРазница)  КАК АбсолютнаяРазница,
	|	СУММА(Данные.КурсоваяРазница)    КАК КурсоваяРазница,
	|	
	|	СУММА(Данные.ОстатокПоКурсуУУ)    КАК ОстатокПоКурсуУУ,
	|	СУММА(Данные.АбсолютнаяРазницаУУ) КАК АбсолютнаяРазницаУУ,
	|	СУММА(Данные.КурсоваяРазницаУУ)   КАК КурсоваяРазницаУУ
	|ПОМЕСТИТЬ втКурсовыеРазницы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		Остатки.ОстатокБУОстаток КАК ОстатокРегл,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ОстатокПоКурсу,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность - Остатки.ОстатокБУОстаток КАК АбсолютнаяРазница,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(31,2)) - Остатки.ОстатокБУОстаток КАК КурсоваяРазница,
	|		
	|		0 КАК ОстатокПоКурсуУУ,
	|		0 КАК АбсолютнаяРазницаУУ,
	|		0 КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|	ГДЕ
	|		НЕ Остатки.Счет.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(31,2)) <> Остатки.ОстатокБУОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		0 КАК ОстатокРегл,
	|		0 КАК ОстатокПоКурсу,
	|		0 КАК АбсолютнаяРазница,
	|		0 КАК КурсоваяРазница,
	|		
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ОстатокПоКурсуУУ,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность - Остатки.СуммаУУОстаток КАК АбсолютнаяРазницаУУ,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ЧИСЛО(31,2)) - Остатки.СуммаУУОстаток КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсУУ КАК КурсУУ
	|			ПО ИСТИНА
	|	ГДЕ
	|		&ВестиУУНаПланеСчетов
	|		И НЕ Остатки.Счет.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.Курс / Курсы.Кратность / КурсУУ.Курс * КурсУУ.Кратность КАК ЧИСЛО(31,2)) <> Остатки.СуммаУУОстаток
	|	) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Счет,
	|	Данные.Организация,
	|	Данные.Подразделение,
	|	Данные.НаправлениеДеятельности,
	|	Данные.Субконто1,
	|	Данные.Субконто2,
	|	Данные.Субконто3,
	|	Данные.Валюта,
	|	Данные.ОстатокВалюты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	Данные.ОстатокРегл               КАК ОстатокРегл,
	|	Данные.ОстатокПоКурсу            КАК ОстатокПоКурсу,
	|	Данные.АбсолютнаяРазница         КАК АбсолютнаяРазница,
	|	Данные.КурсоваяРазница           КАК КурсоваяРазница,
	|	
	|	Данные.ОстатокПоКурсуУУ          КАК ОстатокПоКурсуУУ,
	|	Данные.АбсолютнаяРазницаУУ       КАК АбсолютнаяРазницаУУ,
	|	Данные.КурсоваяРазницаУУ         КАК КурсоваяРазницаУУ,
	|	
	|	ВЫБОР 
	|		КОГДА Данные.КурсоваяРазница < 0 И Данные.КурсоваяРазницаУУ > 0 
	|			ТОГДА ИСТИНА
	|		КОГДА Данные.КурсоваяРазница > 0 И Данные.КурсоваяРазницаУУ < 0 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                            КАК РазныйЗнакКусовыхРазниц, 
	|	
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА ЕСТЬNULL(НастройкиПоАналитикеДоходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДругойДополнительныйКапитал))
	|		ИНАЧЕ ЕСТЬNULL(НастройкиПоАналитикеДоходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтОперационнойКурсовойРазницы))
	|	КОНЕЦ КАК СчетДоходов,
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) 
	|	КОНЕЦ КАК СтатьяДоходов,
	|	
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА ЕСТЬNULL(НастройкиПоАналитикеДоходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДругойДополнительныйКапитал))
	|		ИНАЧЕ ЕСТЬNULL(НастройкиПоАналитикеДоходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗатратыОтОперационнойКурсовойРазницы))
	|	КОНЕЦ КАК СчетРасходов,
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) 
	|	КОНЕЦ КАК СтатьяРасходов,
	|	
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДоходов) 
	|	КОНЕЦ КАК ВидСубконтоДоходы,
	|	ВЫБОР КОГДА Данные.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НеоплаченныйКапитал)) 
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат) 
	|	КОНЕЦ КАК ВидСубконтоРасходы
	|
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втКурсовыеРазницы КАК Данные
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитикеРасходов
	|	ПО 
	|		НастройкиПоАналитикеРасходов.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|		И НастройкиПоАналитикеРасходов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитикеРасходов.АналитикаУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		И НастройкиПоАналитикеРасходов.МестоУчета = НЕОПРЕДЕЛЕНО
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитикеДоходов
	|	ПО 
	|		НастройкиПоАналитикеДоходов.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)
	|		И НастройкиПоАналитикеДоходов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитикеДоходов.АналитикаУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|		И НастройкиПоАналитикеДоходов.МестоУчета = НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Счет.ВидыСубконто         КАК ВидыСубконтоСчета,
	|	Данные.Счет.Валютный             КАК СчетВалютный,
	|	Данные.Счет.Забалансовый         КАК СчетЗабалансовый,
	|	Данные.Счет.НалоговыйУчет        КАК НалоговыйУчет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	Данные.ОстатокРегл               КАК ОстатокРегл,
	|	Данные.ОстатокПоКурсу            КАК ОстатокПоКурсу,
	|	Данные.АбсолютнаяРазница         КАК АбсолютнаяРазница,
	|	Данные.КурсоваяРазница           КАК КурсоваяРазница,
	|	
	|	Данные.ОстатокПоКурсуУУ          КАК ОстатокПоКурсуУУ,
	|	Данные.АбсолютнаяРазницаУУ       КАК АбсолютнаяРазницаУУ,
	|	Данные.КурсоваяРазницаУУ         КАК КурсоваяРазницаУУ,
	|	
	|	Данные.РазныйЗнакКусовыхРазниц   КАК РазныйЗнакКусовыхРазниц, 
	|	
	|	Данные.СчетДоходов КАК СчетДоходов,
	|	Данные.СчетДоходов.УчетПоПодразделениям КАК СчетДоходовУчетПоПодразделениям,
	|	Данные.СчетДоходов.УчетПоНаправлениямДеятельности КАК СчетДоходовУчетПоНаправлениямДеятельности,
	|	Данные.СтатьяДоходов КАК СтатьяДоходов,
	|	
	|	Данные.СчетРасходов КАК СчетРасходов,
	|	Данные.СчетРасходов.УчетПоПодразделениям КАК СчетРасходовУчетПоПодразделениям,
	|	Данные.СчетРасходов.УчетПоНаправлениямДеятельности КАК СчетРасходовУчетПоНаправлениямДеятельности,
	|	Данные.СтатьяРасходов КАК СтатьяРасходов,
	|	
    |	Данные.ВидСубконтоДоходы  КАК ВидСубконтоДоходы,
    |	Данные.ВидСубконтоРасходы КАК ВидСубконтоРасходы
	|
	|ИЗ
	|	втДанные КАК Данные
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура РасчетКурсовыхРазницФО(ПараметрыРасчета, Отказ) Экспорт
	
	НастройкиРеглУчета = РеглУчетСервер.НастройкиУчета();
	
	Если НЕ НастройкиРеглУчета.ДополнительноВедетсяУчетВВалютеФинОтчетности Тогда
		Возврат;
	КонецЕсли;
	
    
    Если Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить() = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.БУ Тогда
		ТекущаяВалюта = Константы.ВалютаРегламентированногоУчета.Получить();
		ПолеИсточника = "Остатки.СуммаОстаток";
	ИначеЕсли Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить() = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.УУ Тогда
		ТекущаяВалюта = Константы.ВалютаУправленческогоУчета.Получить();
		ПолеИсточника = "Остатки.СуммаУУОстаток";
	Иначе
		Возврат;
	КонецЕсли;
	НоваяВалюта = НастройкиРеглУчета.ВалютаФинОтчетности;
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	КоэффициентПересчетаВалютыФО = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(ТекущаяВалюта, НоваяВалюта, Период);
	
	ТекстЗапроса = ТекстЗапросаРасчетКурсовыхРазницФО();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникДляПересчета", ПолеИсточника);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КоэффициентПересчета", КоэффициентПересчетаВалютыФО);
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);

	Содержание = НСтр("ru='Курсовые разницы при переоценке сумм в валюте фин. отчетности';uk='Курсові різниці при переоцінці сум у валюті фін. звітності'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
	
	КурсоваяРазница = Новый Структура("Ресурс, Сумма");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ПроводкиДокумента.Добавить();
		Проводка.Активность = Истина;
		Проводка.Период = Период;
		Проводка.Организация = ПараметрыРасчета.Организация;
		Проводка.Содержание = Содержание;
		
		КурсоваяРазница.Ресурс = "СуммаФО";
		КурсоваяРазница.Сумма = Выборка.КурсоваяРазница;
		ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница);
		ДебетКредит = ?(Проводка.СчетДт = ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности Или Проводка.СчетДт.Пустая(), "Дт", "Кт");
		Проводка["Подразделение"+ДебетКредит] = Неопределено;
		Проводка["НаправлениеДеятельности"+ДебетКредит] = Неопределено;
		
	КонецЦикла;// по валютным остаткам
	
	ПроводкиДокумента.Записать();
	
КонецПроцедуры

Функция ТекстЗапросаРасчетКурсовыхРазницФО()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Остатки.Счет,
	|	Остатки.Организация,
	|	Остатки.Валюта,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Счет.ВидыСубконто КАК ВидыСубконтоСчета,
	|	Остатки.Счет.Валютный КАК СчетВалютный,
	|	Остатки.Счет.Забалансовый КАК СчетЗабалансовый,
	|	Остатки.Счет.НалоговыйУчет КАК НалоговыйУчет,
	|	Остатки.Субконто1,
	|	Остатки.Субконто2,
	|	Остатки.Субконто3,
	|
	|	Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|	Остатки.СуммаОстаток КАК ОстатокРегл,
	|	Остатки.СуммаУУОстаток КАК ОстатокУпр,
	|	Остатки.СуммаФООстаток КАК ОстатокФО,
	|	&ИсточникДляПересчета * &КоэффициентПересчета КАК ОстатокПоКурсу,
	|	&ИсточникДляПересчета * &КоэффициентПересчета - Остатки.СуммаФООстаток КАК АбсолютнаяРазница,
	|	(ВЫРАЗИТЬ(&ИсточникДляПересчета * &КоэффициентПересчета КАК ЧИСЛО(31,2))) - Остатки.СуммаФООстаток КАК КурсоваяРазница,
	|
    |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РезультатОперационнойДеятельности) КАК СчетДоходов,
	|	ЛОЖЬ КАК СчетДоходовУчетПоПодразделениям,
	|	ЛОЖЬ КАК СчетДоходовУчетПоНаправлениямДеятельности,
    |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РезультатОперационнойДеятельности) КАК СчетРасходов,
	|	ЛОЖЬ КАК СчетРасходовУчетПоПодразделениям,
	|	ЛОЖЬ КАК СчетРасходовУчетПоНаправлениямДеятельности,
    |	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
    |	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
    |	НЕОПРЕДЕЛЕНО КАК ВидСубконтоДоходы,
    |	НЕОПРЕДЕЛЕНО КАК ВидСубконтоРасходы
	|	
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ГраницаОстатков,
    |		НЕ Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ФинансовыеРезультаты)),
	|		,
	|		Организация = &Организация) КАК Остатки
	|ГДЕ
	|	(ВЫРАЗИТЬ(&ИсточникДляПересчета * &КоэффициентПересчета КАК ЧИСЛО(31,2))) <> Остатки.СуммаФООстаток";
	
	Возврат ТекстЗапроса;
	
КонецФункции



// Формирует соответствие счетов доходов (расходов) субсчетам финансовых результатов.
//
// Возвращаемое значение:
//   Соответствие, в котором ключом является счет доходов (расходов), а значением счет финансового результата.
//
Функция ПолучитьСоответствиеСчетовДоходовИРасходовУКР() Экспорт

	// Операционная деятельность
	СоответствиеСчетов = Новый Соответствие;
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДоходыОтРеализации, 		ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругойОперационныйДоход, 	ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.МатериальныеЗатраты, 		ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ЗатратыНаОплатуТруда, 		ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ОтчисленияНаСоциальныеМероприятия, 	
																					ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.Амортизация, 				ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругиеОперационныеЗатраты, ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.СебестоимостьРеализации, 	ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.АдминистративныеРасходы,	ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.РасходыНаСбыт, 			ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельностиГруппа, 
																					ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.НалогНаПрибыль,			ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);

	
	// Финансовая деятельность
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДоходОтУчастияВКапитале,	ПланыСчетов.Хозрасчетный.РезультатФинансовыхОпераций);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ПрочиеФинансовыеДоходы,	ПланыСчетов.Хозрасчетный.РезультатФинансовыхОпераций);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ФинансовыеЗатраты,			ПланыСчетов.Хозрасчетный.РезультатФинансовыхОпераций);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ПотериОтУчастияВКапитале,	ПланыСчетов.Хозрасчетный.РезультатФинансовыхОпераций);
	
	// Другая обычная деятельность
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругиеДоходы,				ПланыСчетов.Хозрасчетный.РезультатДругойОбычнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругиеЗатратыПоЭлементам,	ПланыСчетов.Хозрасчетный.РезультатДругойОбычнойДеятельности);
	СоответствиеСчетов.Вставить(ПланыСчетов.Хозрасчетный.ДругиеЗатратыДеятельности,	ПланыСчетов.Хозрасчетный.РезультатДругойОбычнойДеятельности);
	
	
	// Всем подчиненным счетам установим такое же соответствие.
	МассивСчетов = Новый Массив;
	Для каждого Элемент Из СоответствиеСчетов Цикл
		МассивСчетов.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Хозрасчетный.Ссылка КАК Ссылка,
	               |	Хозрасчетный.Код КАК Код,
	               |	Хозрасчетный.Родитель
	               |ИЗ
	               |	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	               |
	               |ГДЕ
	               |	Хозрасчетный.Ссылка В ИЕРАРХИИ(&МассивСчетов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Хозрасчетный.Ссылка.Код
	               |
	               |ИТОГИ КОЛИЧЕСТВО(Код) ПО
	               |	Ссылка ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	ВыборкаПодчиненных = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПодчиненных.Следующий() Цикл
	
		Если СоответствиеСчетов[ВыборкаПодчиненных.Ссылка] = Неопределено 
			И СоответствиеСчетов[ВыборкаПодчиненных.Родитель] <> Неопределено Тогда
		
			СоответствиеСчетов.Вставить(ВыборкаПодчиненных.Ссылка, СоответствиеСчетов[ВыборкаПодчиненных.Родитель]);
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	Возврат СоответствиеСчетов;

КонецФункции // ПолучитьСоответствиеСчетовДоходовИРасходовУКР()

// Устанавливает значение субконто в проводке по счету-назначению, если на нем есть такой же вид 
//  субконто, как и на счете-источнике.
//
// Параметры
//  СчетИсточника		– ПланСчетовСсылка – счет, откуда берется информация о субконто
//  СчетНазначение		– ПланСчетовСсылка – счет, по которому формируется проводка
//  ПроводкаСубконто	– РегистрБухгалтерииСубконто – коллекция субконто в проводке
//  НомерСубконтоИсточника - Число - номер субконто по счету источника, значение которого передается
//  ЗначениеСубконто 	- Произвольный - значение субконто для установки в проводку
//
Процедура УстановитьСовпадающееПоВидуСубконто(СчетИсточник, СчетНазначение, ПроводкаСубконто, НомерСубконтоИсточника, ЗначениеСубконто)

	Если ЗначениеСубконто = NULL Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НомерСубконтоИсточника > СчетИсточник.ВидыСубконто.Количество() Тогда
			
		Возврат;
			
	КонецЕсли;
	
	ВидСубконто = СчетИсточник.ВидыСубконто[НомерСубконтоИсточника - 1].ВидСубконто;
	
	Если СчетНазначение.ВидыСубконто.Найти(ВидСубконто) <> Неопределено Тогда
	
		ПроводкаСубконто.Вставить(ВидСубконто, ЗначениеСубконто);
	
	КонецЕсли;

КонецПроцедуры // УстановитьСовпадающееПоВидуСубконто()

Процедура ФормированиеФинансовогоРезультатаУКР(ПараметрыРасчета, Отказ) Экспорт
	
	// Составим соответствие счетов доходов (расходов) субсчетам финансовых результатов.
	СоответствиеСчетов = ПолучитьСоответствиеСчетовДоходовИРасходовУКР();
	
	// Определим остатки на счетах доходов и расходов
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	               |	ХозрасчетныйОстатки.Счет.Вид КАК ВидСчета,
	               |	ХозрасчетныйОстатки.Счет.Код КАК КодСчета,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	               |	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	               |	ХозрасчетныйОстатки.СуммаУУОстаток КАК СуммаУУОстаток,
	               |	ХозрасчетныйОстатки.СуммаФООстаток КАК СуммаФООстаток,
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецМесяца, Счет В (&МассивСчетов), , Организация = &Организация) КАК ХозрасчетныйОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КодСчета";
				   
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	
	МассивСчетов = Новый Массив;
	Для каждого Элемент Из СоответствиеСчетов Цикл
		МассивСчетов.Добавить(Элемент.Ключ);
	КонецЦикла; 
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	ВыборкаОстатков = Запрос.Выполнить().Выбрать();
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);
	
	Пока ВыборкаОстатков.Следующий() Цикл
		
		СчетФинансовыхРезультатов = СоответствиеСчетов[ВыборкаОстатков.Счет];
		Если СчетФинансовыхРезультатов = Неопределено Тогда
			Продолжить;			
		КонецЕсли;
		
		Если ВыборкаОстатков.ВидСчета = ВидСчета.Активный Тогда
			
			Проводка = ПроводкиДокумента.Добавить();
			Проводка.Период                    = Период;
			Проводка.Организация               = ПараметрыРасчета.Организация;
			
			Проводка.СчетДт                    = СчетФинансовыхРезультатов;
			Проводка.ПодразделениеДт		   = ВыборкаОстатков.Подразделение;
			// Стараемся "передать" информацию о субконто из доходов (расходов) в фин. рез-ты
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 1, ВыборкаОстатков.Субконто1);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 2, ВыборкаОстатков.Субконто2);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 3, ВыборкаОстатков.Субконто3);
			
			Проводка.СчетКт                    = ВыборкаОстатков.Счет;
			Проводка.ПодразделениеКт		   = ВыборкаОстатков.Подразделение;
			Проводка.НаправлениеДеятельностиКт = ВыборкаОстатков.НаправлениеДеятельности;
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 1, ВыборкаОстатков.Субконто1);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 2, ВыборкаОстатков.Субконто2);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 3, ВыборкаОстатков.Субконто3);
			
			Проводка.Сумма                     = ВыборкаОстатков.СуммаОстаток;
			Проводка.СуммаУУ                   = ВыборкаОстатков.СуммаУУОстаток;
			Проводка.СуммаФО                   = ВыборкаОстатков.СуммаФООстаток;
			Проводка.Содержание                = НСтр("ru='Финансовые результаты: закрытие расходов';uk='Фінансові результати: закриття витрат'", МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
			
		Иначе
			
			Проводка = ПроводкиДокумента.Добавить();
			Проводка.Период                    = Период;
			Проводка.Организация               = ПараметрыРасчета.Организация;
			
			Проводка.СчетДт                    = ВыборкаОстатков.Счет;
			Проводка.ПодразделениеДт		   = ВыборкаОстатков.Подразделение;
			Проводка.НаправлениеДеятельностиДт = ВыборкаОстатков.НаправлениеДеятельности;
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 1, ВыборкаОстатков.Субконто1);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 2, ВыборкаОстатков.Субконто2);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетДт, Проводка.СубконтоДт, 3, ВыборкаОстатков.Субконто3);
			
			Проводка.СчетКт                    = СчетФинансовыхРезультатов;
			Проводка.ПодразделениеКт		   = ВыборкаОстатков.Подразделение;
			// Стараемся "передать" информацию о субконто из доходов (расходов) в фин. рез-ты
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 1, ВыборкаОстатков.Субконто1);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 2, ВыборкаОстатков.Субконто2);
			УстановитьСовпадающееПоВидуСубконто(ВыборкаОстатков.Счет, Проводка.СчетКт, Проводка.СубконтоКт, 3, ВыборкаОстатков.Субконто3);
			
			Проводка.Сумма                     = - ВыборкаОстатков.СуммаОстаток;
			Проводка.СуммаУУ                   = - ВыборкаОстатков.СуммаУУОстаток;
			Проводка.СуммаФО                   = - ВыборкаОстатков.СуммаФООстаток;
			
			Проводка.Содержание                = НСтр("ru='Финансовые результаты: закрытие доходов';uk='Фінансові результати: закриття доходів'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	ПроводкиДокумента.Записать();
		
КонецПроцедуры // ФормированиеФинансовогоРезультатаУКР

// Возвращает массив со счетами финансовых результатов
// 				и использованной прибыли
//
// Возвращаемое значение:
//   Массив 
//
Функция ПолучитьМассивСчетовФинансовыхРезультатов()

	МассивСчетов = Новый Массив;
	
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РезультатДругойОбычнойДеятельности);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РезультатФинансовыхОпераций);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПрибыльИспользованнаяВОтчетномПериоде);
	
	Возврат МассивСчетов;
	
КонецФункции // ПолучитьМассивСчетовФинансовыхРезультатов()

Процедура ЗакрытиеГодаУКР(ПараметрыРасчета, Отказ) Экспорт
	
	// Определим остатки на счетах финансовых результатов и использованной прибыли
	
	// Определим остатки на счетах
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	               |	ХозрасчетныйОстатки.СуммаУУОстатокДт КАК СуммаУУОстатокДт,
	               |	ХозрасчетныйОстатки.СуммаУУОстатокКт КАК СуммаУУОстатокКт,
	               |	ХозрасчетныйОстатки.СуммаФООстатокДт КАК СуммаФООстатокДт,
	               |	ХозрасчетныйОстатки.СуммаФООстатокКт КАК СуммаФООстатокКт,
	               |	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстатокДт,
	               |	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаОстатокКт
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецМесяца, Счет В (&МассивСчетов), , Организация = &Организация) КАК ХозрасчетныйОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ХозрасчетныйОстатки.Счет.Код";
				   
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	МассивСчетов = ПолучитьМассивСчетовФинансовыхРезультатов();
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	
	ФинансовыйРезультат = ТаблицаОстатков.Итог("СуммаОстатокДт") - ТаблицаОстатков.Итог("СуммаОстатокКт");
	
	СчетПрибылиУбытка = Неопределено;
	Если ФинансовыйРезультат < 0 Тогда
		
		// Прибыль
		СчетПрибылиУбытка = ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
		
	Иначе
		
		// Убыток
		СчетПрибылиУбытка = ПланыСчетов.Хозрасчетный.НепокрытыйУбыток;
		
	КонецЕсли;
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);

	// Закрываем все остатки на счет прибыли (убытка)
	Для каждого СтрокаОстатка Из ТаблицаОстатков Цикл
		
		Остаток = СтрокаОстатка.СуммаОстатокДт - СтрокаОстатка.СуммаОстатокКт;
		ОстатокУУ = СтрокаОстатка.СуммаУУОстатокДт - СтрокаОстатка.СуммаУУОстатокКт;
		ОстатокФО = СтрокаОстатка.СуммаФООстатокДт - СтрокаОстатка.СуммаФООстатокКт;
		
		Если Остаток > 0 Тогда
		
			Проводка = ПроводкиДокумента.Добавить();
			Проводка.Период                    = Период;
			Проводка.Организация               = ПараметрыРасчета.Организация;
			Проводка.СчетДт                    = СчетПрибылиУбытка;
			
			Проводка.СчетКт                    = СтрокаОстатка.Счет;
			Проводка.ПодразделениеКт		   = СтрокаОстатка.Подразделение;
			Проводка.НаправлениеДеятельностиКт = СтрокаОстатка.НаправлениеДеятельности;
			
			Проводка.Сумма                     = Остаток;
			Проводка.СуммаУУ                   = ОстатокУУ;
			Проводка.СуммаФО                   = ОстатокФО;
			Проводка.Содержание                = НСтр("ru='Финансовые результаты: формирование прибыли/убытка';uk='Фінансові результати: формування прибутку/збитку'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
			
		Иначе
			
			Проводка = ПроводкиДокумента.Добавить();
			Проводка.Период                    = Период;
			Проводка.Организация               = ПараметрыРасчета.Организация;
			
			Проводка.СчетДт                    = СтрокаОстатка.Счет;
			Проводка.ПодразделениеДт		   = СтрокаОстатка.Подразделение;
			Проводка.НаправлениеДеятельностиДт = СтрокаОстатка.НаправлениеДеятельности;
			
			Проводка.СчетКт                    = СчетПрибылиУбытка;
			
			Проводка.Сумма                     = -Остаток;
			Проводка.СуммаУУ                   = -ОстатокУУ;
			Проводка.СуммаФО                   = -ОстатокФО;
			Проводка.Содержание                = НСтр("ru='Финансовые результаты: формирование прибыли/убытка';uk='Фінансові результати: формування прибутку/збитку'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
		
		КонецЕсли; 
	
	КонецЦикла; 

	ПроводкиДокумента.Записать();
		
КонецПроцедуры // ЗакрытиеГодаУКР

Процедура РасчетНалоговыхРазницПослеПереходаСЕН(СтруктураШапкиДокумента, Отказ) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыПлательщиковЕдиногоНалога.Период КАК Период,
	|	СтатусыПлательщиковЕдиногоНалога.ПлательщикЕН КАК ПлательщикЕН
	|ПОМЕСТИТЬ ИсторияЕН
	|ИЗ
	|	РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалога
	|ГДЕ
	|	СтатусыПлательщиковЕдиногоНалога.Организация = &Организация
	|	И СтатусыПлательщиковЕдиногоНалога.Период < &КонДата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияЕН.Период КАК НачалоПериода,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ИсторияЕН1.Период ЕСТЬ NULL
	|				ТОГДА &КонДата
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(ИсторияЕН1.Период, СЕКУНДА, -1)
	|		КОНЕЦ) КАК КонецПериода
	|ПОМЕСТИТЬ ПериодыЕН
	|ИЗ
	|	ИсторияЕН КАК ИсторияЕН
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсторияЕН КАК ИсторияЕН1
	|		ПО ИсторияЕН.Период < ИсторияЕН1.Период
	|ГДЕ
	|	ИсторияЕН.ПлательщикЕН
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияЕН.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РасчетыСКлиентамиПоДокументам.Долг) КАК Долг,
	|	СУММА(РасчетыСКлиентамиПоДокументам.Предоплата) КАК Предоплата,
	|	РасчетыСКлиентамиПоДокументам.Валюта КАК Валюта,
	|	РасчетыСКлиентамиПоДокументам.Регистратор КАК Регистратор,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоДокументам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоДокументам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоДокументам.Регистратор
	|	КОНЕЦ КАК ДокументРеализации
	|ПОМЕСТИТЬ ВторыеСобытия
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН
	|		ПО (РасчетыСКлиентамиПоДокументам.Период МЕЖДУ ПериодыЕН.НачалоПериода И ПериодыЕН.КонецПериода)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН1
	|		ПО (РасчетыСКлиентамиПоДокументам.РасчетныйДокумент.Дата МЕЖДУ ПериодыЕН1.НачалоПериода И ПериодыЕН1.КонецПериода)
	|ГДЕ
	|	РасчетыСКлиентамиПоДокументам.Период МЕЖДУ &НачДата И &КонДата
	|	И РасчетыСКлиентамиПоДокументам.ХозяйственнаяОперация В ИЕРАРХИИ(&ХозяйственныеОперации)
	|	И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				И РасчетыСКлиентамиПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ РасчетыСКлиентамиПоДокументам.Долг > 0
	|				И РасчетыСКлиентамиПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|	И ПериодыЕН.НачалоПериода ЕСТЬ NULL
	|	И РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация = &Организация
	|	И НЕ &НоваяАрхитектураРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентамиПоДокументам.Регистратор,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент,
	|	РасчетыСКлиентамиПоДокументам.Валюта,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоДокументам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоДокументам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоДокументам.Регистратор
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(РасчетыСКлиентамиПоСрокам.Долг),
	|	СУММА(РасчетыСКлиентамиПоСрокам.Предоплата),
	|	РасчетыСКлиентамиПоСрокам.Валюта,
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор,
	|	РасчетыСКлиентамиПоСрокам.РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоСрокам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоСрокам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоСрокам.ДокументРегистратор
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН
	|		ПО (РасчетыСКлиентамиПоСрокам.Период МЕЖДУ ПериодыЕН.НачалоПериода И ПериодыЕН.КонецПериода)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЕН КАК ПериодыЕН1
	|		ПО (РасчетыСКлиентамиПоСрокам.ДатаВозникновения МЕЖДУ ПериодыЕН1.НачалоПериода И ПериодыЕН1.КонецПериода)
	|ГДЕ
	|	РасчетыСКлиентамиПоСрокам.Период МЕЖДУ &НачДата И &КонДата
	|	И РасчетыСКлиентамиПоСрокам.ХозяйственнаяОперация В(&ХозяйственныеОперацииНоваяАрхитектура)
	|	И РасчетыСКлиентамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ПериодыЕН.НачалоПериода ЕСТЬ NULL
	|	И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация = &Организация
	|	И &НоваяАрхитектураРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентамиПоСрокам.РасчетныйДокумент,
	|	РасчетыСКлиентамиПоСрокам.Валюта,
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор,
	|	ВЫБОР
	|		КОГДА РасчетыСКлиентамиПоСрокам.Долг <> 0
	|			ТОГДА РасчетыСКлиентамиПоСрокам.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентамиПоСрокам.ДокументРегистратор
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВторыеСобытия.ДокументРеализации КАК ДокументРеализации
	|ПОМЕСТИТЬ ДокументыРеализации
	|ИЗ
	|	ВторыеСобытия КАК ВторыеСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов КАК Валюта,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СтоимостьРегл + ВыручкаИСебестоимостьПродаж.ДопРасходыРегл + ВыручкаИСебестоимостьПродаж.ТрудозатратыРегл + ВыручкаИСебестоимостьПродаж.ПостатейныеПостоянныеРегл + ВыручкаИСебестоимостьПродаж.ПостатейныеПеременныеРегл) КАК Себестоимость,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл) КАК Доход,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВВалютеВзаиморасчетов) КАК Сумма
	|ПОМЕСТИТЬ ФинансовыеПоказатели
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации КАК ДокументыРеализации
	|		ПО ВыручкаИСебестоимостьПродаж.Регистратор = ДокументыРеализации.ДокументРеализации
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродаж.Регистратор,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ФинансовыеПоказатели.Сумма = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Доход * ВторыеСобытия.Долг / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК СуммаОплата2,
	|	СУММА(ВЫБОР
	|			КОГДА ФинансовыеПоказатели.Сумма = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Себестоимость * ВторыеСобытия.Долг / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК СуммаПродажа1,
	|	СУММА(ВЫБОР
	|			КОГДА ФинансовыеПоказатели.Сумма = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Доход * ВторыеСобытия.Предоплата / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК СуммаОплата1,
	|	СУММА(ВЫБОР
	|			КОГДА ФинансовыеПоказатели.Сумма = 0
	|				ТОГДА 0
	|			ИНАЧЕ ФинансовыеПоказатели.Себестоимость * ВторыеСобытия.Предоплата / ФинансовыеПоказатели.Сумма
	|		КОНЕЦ) КАК СуммаПродажа2
	|ИЗ
	|	ВторыеСобытия КАК ВторыеСобытия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФинансовыеПоказатели КАК ФинансовыеПоказатели
	|		ПО ВторыеСобытия.ДокументРеализации = ФинансовыеПоказатели.Регистратор
	|			И ВторыеСобытия.Валюта = ФинансовыеПоказатели.Валюта";
	
	Запрос.УстановитьПараметр("НачДата", СтруктураШапкиДокумента.НачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.КонДата);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	
	МассивХО = Новый Массив;
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионера);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	МассивХО.Добавить(Перечисления.ХозяйственныеОперации.ПереносАванса);	
	Запрос.УстановитьПараметр("ХозяйственныеОперации", МассивХО);

	МассивХО_НоваяАрхитектура = Новый Массив;
	МассивХО_НоваяАрхитектура.Добавить(Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
	МассивХО_НоваяАрхитектура.Добавить(Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента);
	Запрос.УстановитьПараметр("ХозяйственныеОперацииНоваяАрхитектура", МассивХО_НоваяАрхитектура);
	
	Запрос.УстановитьПараметр("НоваяАрхитектураРасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
	
	СчетРИ = ПланыСчетов.Хозрасчетный.РазницыПоНалогуНаПрибыльРучныеКорректировки;

	СуммыРИПоСтатьям = Новый Структура;
	//до 01.08.2023 налоговая разница существовала только по статье 4.1.5.1.
	Если СтруктураШапкиДокумента.КонДата < '20230801' Тогда
		СуммыРИПоСтатьям.Вставить("Оплата2", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_ОплатаТоваров_ЕН);
	Иначе
		СуммыРИПоСтатьям.Вставить("Оплата1", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_Аванс_ЕН);
		СуммыРИПоСтатьям.Вставить("Оплата2", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_ОплатаТоваров_ЕН);
		СуммыРИПоСтатьям.Вставить("Продажа1", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_СС_Товаров_ЕН);
		СуммыРИПоСтатьям.Вставить("Продажа2", Справочники.СтатьиНалоговыхДеклараций.НП15_РИ_Фин_СС_Товаров);
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		
		Для каждого КЗ Из СуммыРИПоСтатьям Цикл
	
			Если ЗначениеЗаполнено(Выборка["Сумма" + КЗ.Ключ]) Тогда
				
				Проводка = ПроводкиДокумента.Добавить();
				Проводка.Период                    = СтруктураШапкиДокумента.Дата;
				Проводка.Организация               = СтруктураШапкиДокумента.Организация;
				
				Проводка.СчетДт                    = СчетРИ;
				Проводка.СубконтоДт.СтатьиНалоговыхДеклараций = КЗ.Значение;
				
				Проводка.Сумма                     = Выборка["Сумма" + КЗ.Ключ];
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	ПроводкиДокумента.Записать();
	
КонецПроцедуры

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации, ТипОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РегламентнаяОперация);
	ВходящиеДанные.Вставить(Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.КурсыВалют);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
		
	КонецЕсли;
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
