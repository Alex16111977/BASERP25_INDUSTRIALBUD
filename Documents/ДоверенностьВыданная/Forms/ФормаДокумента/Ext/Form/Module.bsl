
&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

&НаКлиенте
Перем ПараметрыДляЗаписи Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ЕстьПравоНаДобавлениеДокументов = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ДокументыФизическихЛиц); 
	
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		Элементы.ФизЛицо.Заголовок = НСтр("ru='Сотрудник';uk='Співробітник'");
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
		СтруктураХарактеристикиНоменклатуры = Новый Структура;
		СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
		СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
		
		НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, СтруктураХарактеристикиНоменклатуры);
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	УстановитьВидимостьДоступностьПоСтатусуДоверенности();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
		Оповестить("Запись_ДоверенностьНаПолучениеТоваров", ПараметрыЗаписи, Объект.Ссылка);
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	ОбщегоНазначенияУТКлиент.ВыполнитьДействияПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика =
		Настройки.Получить("НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика");
	Если НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика Тогда
		Элементы.ФормаНеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика.Пометка = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НеВыполнятьПроверкуПередЗаписью Тогда
		НеВыполнятьПроверкуПередЗаписью = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ЕстьДоступКНоменклатуреПоставщика Тогда
		
		Отказ = Истина;
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
		
		ЗакупкиКлиент.ПроверитьСопоставленнуюНоменклатуруПоставщика(
			Объект,
			Новый ОписаниеОповещения("ПередЗаписьюПроверитьСопоставленнуюНоменклатуруПоставщикаЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика);
		Возврат;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.ЗаписатьОбъектПриНеобходимости(ЭтотОбъект, ПараметрыЗаписи, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПроверитьСопоставленнуюНоменклатуруПоставщикаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Нет Тогда
		
		ОбщегоНазначенияУТКлиент.ЗаписатьОбъектПриНеобходимости(ЭтотОбъект, ДополнительныеПараметры.ПараметрыЗаписи,,Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДокументФизЛица" Тогда
		ОчиститьДанныеПаспорта(ЭтаФорма);
		ЗаполнитьСпискокУдостоверенийЛичности();
		Объект.ПредставлениеДокументаФизЛица = Параметр.Представление;
		Объект.ВидДокументаФизЛица = Параметр.ВидДокумента;
		Объект.СерияДокументаФизЛица = Параметр.ПаспортСерия;
		Объект.НомерДокументаФизЛица = Параметр.ПаспортНомер;
		Объект.КемВыданДокументФизлица = Параметр.ПаспортВыдан;
		Объект.ДатаВыдачиДокументаФизЛица = Параметр.ПаспортДатаВыдачи;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ПринудительноЗакрытьФорму = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ДокументОснованиеПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииСервер()
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		ПриИзмененииФизЛицаСервер();
	Иначе
		Элементы.Должность.СписокВыбора.Очистить();
		Элементы.УдостоверениеЛичности.СписокВыбора.Очистить();
		СписокДолжностейЗагружен = Истина;
		Объект.ПредставлениеДокументаФизЛица = "";
		ОчиститьДанныеПаспорта(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияПриИзменении(Элемент)
	
	Объект.ДатаОкончанияДействия = РассчитатьДатуОкончанияДействия(НачалоДня(Объект.Дата), Объект.СрокДействия);
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияДействияПриИзменении(Элемент)
	
	Объект.СрокДействия = РассчитатьСрокДействия(Объект.Дата, Объект.ДатаОкончанияДействия);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент) 
	
	Если Не ЗначениеЗаполнено (Объект.Партнер) Тогда
		Возврат;
	КонецЕсли;
	ПриИзмененииПартнераСервер();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура УдостоверениеЛичностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ФизЛицо", Объект.ФизЛицо);
		ОткрытьФорму("РегистрСведений.ДокументыФизическихЛиц.ФормаЗаписи", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПредставлениеДокументаФизЛица", ВыбранноеЗначение);
		НайденныеСтроки = СписокДокументов.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество()>0 Тогда
			НайденныйДокумент = НайденныеСтроки.Получить(0);
			ЗаполнитьЗначенияСвойств(Объект, НайденныйДокумент);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдостоверениеЛичностиОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьДанныеПаспорта(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УдостоверениеЛичностиАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПодбора = Новый Структура("СтрокаПоиска, ФизЛицо");
	ПараметрыПодбора.СтрокаПоиска = Текст;
	ПараметрыПодбора.ФизЛицо = Объект.ФизЛицо;
	ДанныеВыбора = ПолучитьДанныеВыбораДокумента(ПараметрыПодбора);
	Если ЕстьПравоНаДобавлениеДокументов Тогда
		ДанныеВыбора.Вставить(0,Неопределено, НСтр("ru='<Создать новый>';uk='<Створити новий>'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдостоверениеЛичностиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыПодбора = Новый Структура("СтрокаПоиска, ФизЛицо");
		ПараметрыПодбора.СтрокаПоиска = Текст;
		ПараметрыПодбора.ФизЛицо = Объект.ФизЛицо;
		ДанныеВыбора = ПолучитьДанныеВыбораДокумента(ПараметрыПодбора);
		Если ЕстьПравоНаДобавлениеДокументов Тогда
			ДанныеВыбора.Вставить(0,Неопределено, НСтр("ru='<Создать новый>';uk='<Створити новий>'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	СтатусПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЗакупкиКлиент.ПриВыбореНоменклатурыПоставщика(Объект, Поле, "ТоварыНоменклатураПоставщика", НСтр("ru='Поставщик';uk='Постачальник'"));
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПоставщикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоНоменклатуреПоставщика");
	СтруктураДействий.Вставить("ПроверитьСопоставленнуюНоменклатуруПоставщика",
		ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(Объект,
			НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоставщикаПоНоменклатуре", Объект.Партнер);
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ПроверитьСопоставленнуюНоменклатуруПоставщика",
		ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(Объект,
			НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика));
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "Товары"));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоставщикаПоНоменклатуре", Объект.Партнер);
	СтруктураДействий.Вставить("ПроверитьСопоставленнуюНоменклатуруПоставщика",
		ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(Объект,
			НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика));
	СтруктураДействий.Вставить("ХарактеристикаПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "Товары"));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоставщикаПоНоменклатуре", Объект.Партнер);
	СтруктураДействий.Вставить("ПроверитьСопоставленнуюНоменклатуруПоставщика",
		ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(Объект,
			НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика));
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "ТоварыНоменклатураПоставщика" Тогда
		Если ЕстьДоступКНоменклатуреПоставщика Тогда
			ЗакупкиКлиент.ЗаполнитьСписокВыбораНоменклатурыПоставщика(Объект.Партнер,
				Элементы.Товары.ТекущиеДанные,
				Элементы.ТоварыНоменклатураПоставщика.СписокВыбора);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЦенности
#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПоказатьНоменклатуруПоставщика(Команда)
	
	ЗакупкиКлиент.ПоказатьНоменклатуруПоставщика(Объект.Партнер, Объект.Ссылка, Элементы.Товары.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика(Команда)
	
	НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика = Не НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика;
	Элементы.ФормаНеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика.Пометка =
		НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика;
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтроку(Команда)
	
	ТаблицаФормы  = Элементы.Товары;
	ДанныеТаблицы = Объект.Товары;
	
	РаботаСТабличнымиЧастямиКлиент.РазбитьСтроку(ДанныеТаблицы, ТаблицаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруПоНоменклатуреПоставщика(Команда)
	
	Если Не ОбщегоНазначенияУТКлиент.ПроверитьНаличиеСтрокВСписке(Объект.Товары, "Товары") Тогда
		Возврат;
	КонецЕсли;
	
	АдресТоваровВХранилище = ПоместитьТоварыВоВременноеХранилищеСервер();
	ЗакупкиКлиент.ОткрытьФормуЗаполненияНоменклатурыПоставщика(
		Новый ОписаниеОповещения("ЗаполнитьНоменклатуруПоНоменклатуреПоставщикаЗавершение", ЭтотОбъект),
		АдресТоваровВХранилище,
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруПоНоменклатуреПоставщикаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ЗаполнитьТоварыИзВременногоХранилищаСервер(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма, Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) ТОгда
		ТекстСообщения = НСтр("ru='Не заполнено основание.';uk='Не заповнено підставу.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru='Список товаров к получению по доверенности будет перезаполнен по основанию. Продолжить?';uk='Список товарів до отримання за дорученням буде перезаповнен за підставою. Продовжити?'");
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Продолжить';uk='Продовжити'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить';uk='Скасувати'"));
		
		ЗаголовокОкнаВопроса = НСтр("ru='Заполнение товаров к получению';uk='Заповнення товарів до отримання'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок, , КодВозвратаДиалога.Да, ЗаголовокОкнаВопроса);
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоОснованиюСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоОснованиюСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма, Истина);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
КонецПроцедуры

// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура РассчитатьСостояние(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьВыполнениеКомандыПослеПодтвержденияЗаписи", ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
		ТекстВопроса = НСтр("ru='Документ еще не записан.
|Рассчитать статус возможно только после записи данных.
|Данные будут записаны.'
|;uk='Документ ще не записаний.
|Розрахувати статус можливо тільки після запису даних.
|Дані будуть записані.'");
		
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ВыполнитьОбработкуОповещения(Обработчик, КодВозвратаДиалога.ОК);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьВыполнениеКомандыПослеПодтвержденияЗаписи(Знач Ответ = Неопределено, Знач ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = Неопределено ИЛИ Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		ОчиститьСообщения();
		Записать();
		Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ТекущийСтатус = Объект.Статус;
	ТекущееПоступлениеТоваров = Объект.ЕстьПоступлениеТоваров;
	РассчитатьСостояниеНаСервере();
	Если НЕ ТекущийСтатус = Объект.Статус
		ИЛИ НЕ ТекущееПоступлениеТоваров = Объект.ЕстьПоступлениеТоваров Тогда
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ОбработатьЗаписьОбъекта()
	
	ОбщегоНазначенияУТКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ПараметрыДляЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, "ТоварыЕдиницаИзмерения");
		
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатураПоставщика.Имя);
		
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Партнер");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<выберите поставщика>';uk='<виберіть постачальника>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);

КонецПроцедуры

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура ПриИзмененииФизЛицаСервер()
	
	МассивДолжностейСотрудника = ДолжностиСотрудника(Объект.ФизЛицо);
	Если МассивДолжностейСотрудника.Количество() > 0 Тогда
		Объект.Должность = МассивДолжностейСотрудника[0];
	КонецЕсли;
	Элементы.УдостоверениеЛичности.СписокВыбора.Очистить();
	ПолучитьПоследниеПаспортныеДанныеСтрокой();
	ЗаполнитьСписокВыбораДолжность();
	ЗаполнитьСпискокУдостоверенийЛичности();
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПартнераСервер()
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
	ЗакупкиСервер.ЗаполнитьНоменклатуруПоставщикаВТаблице(Объект.Товары, Объект.Партнер);
	
	СтруктураХарактеристикиНоменклатуры = Новый Структура;
	СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, СтруктураХарактеристикиНоменклатуры);
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    = Объект.Организация;
	СтруктураПараметров.БанковскийСчет = Объект.БанковскийСчет;
	Объект.БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	ОтветственныеЛицаСервер.ПриИзмененииСвязанныхРеквизитовДокумента(Объект);
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииСервер()
	
	ОтветственныеЛицаСервер.ПриИзмененииСвязанныхРеквизитовДокумента(Объект);
КонецПроцедуры

#КонецОбласти

#Область СостояниеДоверенности

&НаСервере
Процедура РассчитатьСостояниеНаСервере()
	
	Если НЕ (Объект.Статус = Перечисления.СтатусыДоверенностей.Выдана
			ИЛИ Объект.Статус = Перечисления.СтатусыДоверенностей.ИспользованаЧастично) Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектов = Новый Массив();
	МассивОбъектов.Добавить(Объект.Ссылка);
	
	ТаблицаСостояния = Документы.ДоверенностьВыданная.РассчитатьСостояниеДоверенности(МассивОбъектов);
	
	Если ТаблицаСостояния.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Документы.ДоверенностьВыданная.РассчитатьСтатусДоверенности(Объект,
		ТаблицаСостояния[0].ЕстьПоступлениеТоваров);

	УстановитьСостояниеДоверенности();
	СтатусПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДоверенности()
	
	СостояниеДоверенности = "";
	Если Объект.Статус = Перечисления.СтатусыДоверенностей.НеВыдана Тогда
		Возврат;
	КонецЕсли;
	ТекстНетПоступленияТоваров = НСтр("ru='Нет поступления товаров (МЦ)';uk='Немає надходження товарів (МЦ)'");
	ТекстЕстьПоступленияТоваров = НСтр("ru='Есть поступления товаров (МЦ)';uk='Є надходження товарів (МЦ)'");
		Если Объект.ЕстьПоступлениеТоваров Тогда
			СостояниеДоверенности = ТекстЕстьПоступленияТоваров;
		Иначе
			СостояниеДоверенности = ТекстНетПоступленияТоваров;
		КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СтатусПриИзмененииНаСервере()
	
	ВыданаИспользована = НЕ Объект.Статус = Перечисления.СтатусыДоверенностей.НеВыдана;
	Элементы.Страницы.ТолькоПросмотр = ВыданаИспользована;
	Элементы.ТоварыРазбитьСтроку.Доступность = НЕ ВыданаИспользована;
	Элементы.ТоварыЗаполнитьПоОснованию.Доступность = НЕ ВыданаИспользована;
	Элементы.ТоварыЗаполнитьНоменклатуруПоНоменклатуреПоставщика.Доступность = НЕ ВыданаИспользована;
	
	ДоступностьРассчета = Ложь;
		ДоступностьРассчета = Объект.Статус = Перечисления.СтатусыДоверенностей.Выдана;
		
	Элементы.РассчитатьСостояние.Доступность = ДоступностьРассчета;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	ЗаполнитьСписокВыбораДолжность();
	ЗаполнитьСпискокУдостоверенийЛичности();   

		СтруктураХарактеристикиНоменклатуры = Новый Структура;
		СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
		СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
		
		НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, СтруктураХарактеристикиНоменклатуры);
	УстановитьВидимостьДоступностьПоСтатусуДоверенности();
	УстановитьСостояниеДоверенности();
	СтатусПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьПоСтатусуДоверенности()
	
	СтатусДоступен = ПраваПользователяПовтИсп.ЗаписьВыданнойДоверенностиВОкончательномСтатусе();
	ВыданнаяДоверенность = НЕ Объект.Статус = Перечисления.СтатусыДоверенностей.НеВыдана;
	ТолькоПросмотр = (ВыданнаяДоверенность И НЕ СтатусДоступен) ИЛИ Объект.ПометкаУдаления;
	
	Элементы.Статус.Доступность = СтатусДоступен;
	
	Если ВыданнаяДоверенность Тогда
		СтатусНеВыдана = Элементы.Статус.СписокВыбора.НайтиПоЗначению(Перечисления.СтатусыДоверенностей.НеВыдана);
		Если НЕ СтатусНеВыдана = Неопределено Тогда
			Элементы.Статус.СписокВыбора.Удалить(СтатусНеВыдана);
		КонецЕсли;
		Элементы.ДокументОснование.Видимость = Истина;
		Элементы.ДокументОснование1.Видимость = Ложь;
	Иначе
		Элементы.ДокументОснование.Видимость = Ложь;
		Элементы.ДокументОснование1.Видимость = Истина;
	КонецЕсли;
	
	Элементы.Ответственный.Видимость = ВыданнаяДоверенность;
	
	ЕстьДоступКНоменклатуреПоставщика = ПравоДоступа("Чтение", Метаданные.Справочники.НоменклатураПоставщиков);
	Элементы.ТоварыЗаполнитьНоменклатуруПоНоменклатуреПоставщика.Видимость = ЕстьДоступКНоменклатуреПоставщика;
	Элементы.ТоварыНоменклатураПоставщика.ТолькоПросмотр = НЕ ЕстьДоступКНоменклатуреПоставщика;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДолжностиСотрудника(ФизЛицо)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Доверенность.Должность КАК Должность,
	|	МАКСИМУМ(Доверенность.Дата) КАК Дата
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенность
	|ГДЕ
	|	Доверенность.ФизЛицо = &ФизЛицо
	|	И Доверенность.Должность <> """"
	|	И НЕ Доверенность.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	Доверенность.Должность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ");
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Должность");
	
КонецФункции

&НаСервереБезКонтекста
Функция РассчитатьДатуОкончанияДействия(ДатаНачалаДействия, СрокДействия)
	
	ДатаОкончанияДействия = Дата(1,1,1);
	Если ЗначениеЗаполнено(ДатаНачалаДействия) Тогда
		Если СрокДействия > 0 Тогда
			ОсновнойКалендарьПредприятия = Константы.ОсновнойКалендарьПредприятия.Получить();
			Если ЗначениеЗаполнено(ОсновнойКалендарьПредприятия) Тогда
				ДатаОкончанияДействия = 
					КалендарныеГрафики.ДатаПоКалендарю(ОсновнойКалендарьПредприятия, ДатаНачалаДействия, СрокДействия);
			Иначе
				ДатаОкончанияДействия = ДатаНачалаДействия + СрокДействия * 86400;
			КонецЕсли;
			Возврат ДатаОкончанияДействия;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция РассчитатьСрокДействия(ДатаНачалаДействия, ДатаОкончанияДействия)
	
	СрокДействия = 0;
	Если ЗначениеЗаполнено(ДатаОкончанияДействия) И ДатаОкончанияДействия >= ДатаНачалаДействия Тогда
		ОсновнойКалендарьПредприятия = Константы.ОсновнойКалендарьПредприятия.Получить();
		Если ЗначениеЗаполнено(ОсновнойКалендарьПредприятия) Тогда
			Попытка
				СрокДействия = КалендарныеГрафики.РазностьДатПоКалендарю(ОсновнойКалендарьПредприятия, ДатаНачалаДействия, ДатаОкончанияДействия);
			Исключение
				СрокДействия = 0;
			КонецПопытки;
		Иначе
			СрокДействия = (НачалоДня(ДатаОкончанияДействия) - НачалоДня(ДатаНачалаДействия)) / 86400;
		КонецЕсли;
	КонецЕсли;
	Возврат СрокДействия;
КонецФункции

&НаСервере
Функция ПоместитьТоварыВоВременноеХранилищеСервер()
	
	Возврат ЗакупкиСервер.ПоместитьТоварыВоВременноеХранилище(Объект.Товары);
КонецФункции

&НаСервере
Процедура ЗаполнитьТоварыИзВременногоХранилищаСервер(АдресВоВременномХранилище)
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище); // ДокументТабличнаяЧасть.ДоверенностьВыданная.Товары
	Для каждого ТекСтрока Из ТаблицаТоваров Цикл
		МассивСтрокКоллекции = Объект.Товары.НайтиСтроки(Новый Структура("НомерСтроки", ТекСтрока.НомерСтроки));
		Если МассивСтрокКоллекции.Количество() <> 1 Тогда
			Продолжить;
		Иначе
			СтрокаКоллекции = МассивСтрокКоллекции[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаКоллекции, ТекСтрока, "Номенклатура, Характеристика, Упаковка");
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить(
			"ПроверитьСопоставленнуюНоменклатуруПоставщика",
			ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(
				Объект,
				НеВыполнятьПроверкуСопоставленнойНоменклатурыПоставщика));
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаКоллекции, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
	СтруктураХарактеристикиНоменклатуры = Новый Структура;
	СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	СтруктураХарактеристикиНоменклатуры.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, СтруктураХарактеристикиНоменклатуры);
КонецПроцедуры

&НаСервере
Процедура ПолучитьПоследниеПаспортныеДанныеСтрокой()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК СерияДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК НомерДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыданДокументФизлица,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи Как ДатаВыдачиДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Представление КАК ПредставлениеДокументаФизЛица
	|ПОМЕСТИТЬ Паспорт
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо = &ФизЛицо
	|	И ДокументыФизическихЛицСрезПоследних.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.Паспорт)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК СерияДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК НомерДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыданДокументФизлица,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи Как ДатаВыдачиДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Представление КАК ПредставлениеДокументаФизЛица
	|ПОМЕСТИТЬ ДокументУдостоверяющийЛичность
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность
	|	И ДокументыФизическихЛицСрезПоследних.Физлицо = &ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыФизическихЛицСрезПоследних.Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|       КОГДА Паспорт.ВидДокументаФизЛица ЕСТЬ NULL
	|			ТОГДА ДокументУдостоверяющийЛичность.ВидДокументаФизЛица
	|		ИНАЧЕ Паспорт.ВидДокументаФизЛица
	|	КОНЕЦ КАК ВидДокументаФизЛица
	|ПОМЕСТИТЬ ВыбранныйВидДокумента
	|ИЗ
	|	Паспорт КАК Паспорт
	|		ПОЛНОЕ СОЕДИНЕНИЕ ДокументУдостоверяющийЛичность КАК ДокументУдостоверяющийЛичность
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК СерияДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК НомерДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыданДокументФизлица,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи Как ДатаВыдачиДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Представление КАК ПредставлениеДокументаФизЛица
	|ИЗ
	|	ВыбранныйВидДокумента КАК ВыбранныйВидДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
	|		ПО ВыбранныйВидДокумента.ВидДокументаФизЛица = ДокументыФизическихЛицСрезПоследних.ВидДокумента
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо = &ФизЛицо";
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Объект,ВыборкаДетальныеЗаписи);
	Иначе
		ОчиститьДанныеПаспорта(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискокУдостоверенийЛичности()
	
	Элементы.УдостоверениеЛичности.СписокВыбора.Очистить();
	СписокДокументов.Очистить();
	СписокДокументов.Загрузить(ДокументыФизЛица(Элементы.УдостоверениеЛичности,Объект.ФизЛицо));
	Для каждого Документ Из СписокДокументов Цикл
		Элементы.УдостоверениеЛичности.СписокВыбора.Добавить(Документ.ПредставлениеДокументаФизЛица,
			Документ.ПредставлениеДокументаФизЛица);
	КонецЦикла; 
КонецПроцедуры

&НаСервере
Функция ДокументыФизЛица(ЭлементУдостоверениеЛичности, ФизЛицо)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК СерияДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК НомерДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыданДокументФизлица,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачиДокументаФизЛица,
	|	ДокументыФизическихЛицСрезПоследних.Представление КАК ПредставлениеДокументаФизЛица
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо = &ФизЛицо
	|	И ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВыдачиДокументаФизЛица УБЫВ";
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	ТаблицаДокументовФизЛица = Новый ТаблицаЗначений;
	ТаблицаДокументовФизЛица.Колонки.Добавить("ВидДокументаФизЛица",
		Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовФизическихЛиц"));
	ТаблицаДокументовФизЛица.Колонки.Добавить("СерияДокументаФизЛица", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументовФизЛица.Колонки.Добавить("НомерДокументаФизЛица", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументовФизЛица.Колонки.Добавить("КемВыданДокументФизлица", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументовФизЛица.Колонки.Добавить("ДатаВыдачиДокументаФизЛица", Новый ОписаниеТипов("Дата"));
	ТаблицаДокументовФизЛица.Колонки.Добавить("ПредставлениеДокументаФизЛица", Новый ОписаниеТипов("Строка"));
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовыйДокумент = ТаблицаДокументовФизЛица.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйДокумент,ВыборкаДетальныеЗаписи);
	КонецЦикла;
	Возврат ТаблицаДокументовФизЛица;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеПаспорта(Форма)
	
	Форма.Объект.ВидДокументаФизЛица = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПустаяСсылка");
	Форма.Объект.КемВыданДокументФизлица = "";
	Форма.Объект.ДатаВыдачиДокументаФизЛица = "";
	Форма.Объект.НомерДокументаФизЛица = "";
	Форма.Объект.ПредставлениеДокументаФизЛица = "";
	Форма.Объект.СерияДокументаФизЛица = "";
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеВыбораДокумента(Параметры)
	
	ДанныеВыбора = Новый СписокЗначений;
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДокументыФизическихЛицСрезПоследних.Представление КАК ПредставлениеДокумента
			|ИЗ
			|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
			|ГДЕ
			|	ДокументыФизическихЛицСрезПоследних.Физлицо = &Физлицо
			|	И ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность
			|	И ДокументыФизическихЛицСрезПоследних.Представление ПОДОБНО &СтрокаПоиска";
		Запрос.УстановитьПараметр("Физлицо", Параметры.Физлицо);
		Запрос.УстановитьПараметр("СтрокаПоиска","%" + СокрЛП(Параметры.СтрокаПоиска) + "%");
		Результат = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ДанныеВыбора.Добавить(ВыборкаДетальныеЗаписи.ПредставлениеДокумента,ВыборкаДетальныеЗаписи.ПредставлениеДокумента);
		КонецЦикла;
	КонецЕсли;
	Возврат ДанныеВыбора;
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораДолжность()
	
	Если Не СписокДолжностейЗагружен И ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		МассивДолжностейСотрудника = ДолжностиСотрудника(Объект.ФизЛицо);
		Элементы.Должность.СписокВыбора.Очистить();
		Для Каждого ДолжностьСотрудника Из МассивДолжностейСотрудника Цикл
			Элементы.Должность.СписокВыбора.Добавить(ДолжностьСотрудника);
		КонецЦикла;
		СписокДолжностейЗагружен = Истина;
	КонецЕсли;
КонецПроцедуры
&НаСервере
Процедура ЗаполнитьПоОснованиюСервер()
	
	ДанныеЗаполнения = Объект.ДокументОснование;
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ЗаполнитьПоЗаказуПоставщику(ДанныеЗаполнения);                                	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		ЗаполнитьПоЗаявкеНаВозвратТоваров(ДанныеЗаполнения);
	//++ НЕ УТКА
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказДавальца") Тогда
		ЗаполнитьПоЗаказуДавальца(ДанныеЗаполнения);
	//-- НЕ УТКА
	//++ НЕ УТ
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПереработчику") Тогда
		ЗаполнитьПоЗаказуПереработчику(ДанныеЗаполнения);
	//-- НЕ УТ
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоЗаказуПоставщику(Знач ДокументОснование)

	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаказТовары.Ссылка КАК Ссылка,
	               |	ЗаказТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	               |	ЗаказТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказТовары.Характеристика КАК Характеристика,
	               |	ЗаказТовары.Упаковка КАК Упаковка,
	               |	ЗаказТовары.КодСтроки КАК КодСтроки
	               |ПОМЕСТИТЬ ДанныеЗаказа
	               |ИЗ
	               |	Документ.ЗаказПоставщику.Товары КАК ЗаказТовары
	               |ГДЕ
	               |	ЗаказТовары.Ссылка = &ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
	               |	ЗаказТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказТовары.Характеристика КАК Характеристика,
	               |	ЗаказТовары.Упаковка КАК Упаковка,
	               |	ЗаказыПоставщикам.КОформлениюОстаток / ВЫБОР
	               |		КОГДА ЗаказТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	               |			ТОГДА 1
	               |		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	               |	КОНЕЦ КАК КоличествоУпаковок
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	               |			,
	               |			ЗаказПоставщику = &ДокументОснование
	               |				И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
				   |					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК ЗаказыПоставщикам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеЗаказа КАК ЗаказТовары
	               |		ПО ЗаказыПоставщикам.КодСтроки = ЗаказТовары.КодСтроки
	               |ГДЕ
	               |	ЗаказыПоставщикам.КОформлениюОстаток > 0";
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаказТовары.Упаковка",
		"ЗаказТовары.Номенклатура"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	РезультатыЗапроса = Запрос.Выполнить();
	Объект.Товары.Загрузить(РезультатыЗапроса.Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоЗаявкеНаВозвратТоваров(Знач ДокументОснование)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказТовары.Ссылка КАК Ссылка,
	|	ЗаказТовары.Номенклатура КАК Номенклатура,
	|	ЗаказТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказТовары.Характеристика КАК Характеристика,
	|	ЗаказТовары.Упаковка КАК Упаковка,
	|	ЗаказТовары.КодСтроки КАК КодСтроки
	|ПОМЕСТИТЬ ДанныеПотоварам
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ВозвращаемыеТовары КАК ЗаказТовары
	|ГДЕ
	|	ЗаказТовары.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказТовары.Номенклатура КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураПоставщика,
	|	ЗаказТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказТовары.Характеристика КАК Характеристика,
	|	ЗаказТовары.Упаковка КАК Упаковка,
	|	ЗаявкаНаВозврат.КОформлениюОстаток / ВЫБОР
	|		КОГДА ЗаказТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов.Остатки(
	|			,
	|			ЗаявкаНаВозвратТоваровОтКлиента = &ДокументОснование
	|				И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК ЗаявкаНаВозврат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПотоварам КАК ЗаказТовары
	|		ПО ЗаявкаНаВозврат.КодСтроки = ЗаказТовары.КодСтроки
	|ГДЕ
	|	&ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|	И ЗаявкаНаВозврат.КОформлениюОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказТовары.Номенклатура,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗаказТовары.Номенклатура.ЕдиницаИзмерения,
	|	ЗаказТовары.Характеристика,
	|	ЗаказТовары.Упаковка,
	|	ЗаказТовары.КоличествоУпаковок
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ВозвращаемыеТовары КАК ЗаказТовары
	|ГДЕ
	|	НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|	И ЗаказТовары.Ссылка = &ДокументОснование
	|	И ЗаказТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаказТовары.Упаковка",
		"ЗаказТовары.Номенклатура"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
		ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	РезультатыЗапроса = Запрос.Выполнить();
	Объект.Товары.Загрузить(РезультатыЗапроса.Выгрузить());
КонецПроцедуры

//++ НЕ УТ
&НаСервере
Процедура ЗаполнитьПоЗаказуПереработчику(Знач ДокументОснование)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураПоставщика,
	|	ЗаказыПоставщикам.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказыПоставщикам.Характеристика КАК Характеристика,
	|
	|	ЕСТЬNULL(ЗаказТовары.Упаковка, 
	//++ НЕ УТКА
	|		ЕСТЬNULL(ЭтапТовары.Упаковка,
	//-- НЕ УТКА
	|		ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	//++ НЕ УТКА
	|			)
	//-- НЕ УТКА
	|		) КАК Упаковка,
	|
	|	ЗаказыПоставщикам.КОформлениюОстаток / ВЫБОР
	|		КОГДА НЕ ЗаказТовары.Упаковка ЕСТЬ NULL
	|				И ЗаказТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА &ТекстЗапросаКоэффициентУпаковки1
	//++ НЕ УТКА
	|		КОГДА НЕ ЭтапТовары.Упаковка ЕСТЬ NULL
	|				И ЭтапТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА &ТекстЗапросаКоэффициентУпаковки2
	//-- НЕ УТКА
	|		ИНАЧЕ 1 
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, ЗаказПоставщику = &ДокументОснование) КАК ЗаказыПоставщикам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Продукция КАК ЗаказТовары
	|		ПО ЗаказыПоставщикам.ЗаказПоставщику = ЗаказТовары.Ссылка
	|			И ЗаказыПоставщикам.КодСтроки = ЗаказТовары.КодСтроки
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Услуги КАК ТаблицаУслуги
	|		ПО ЗаказыПоставщикам.ЗаказПоставщику = ТаблицаУслуги.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапТовары
	|		ПО ТаблицаУслуги.Распоряжение = ЭтапТовары.Ссылка
	|			И ЗаказыПоставщикам.КодСтроки = ЭтапТовары.КодСтроки
	//-- НЕ УТКА
	|ГДЕ
	|	ЗаказыПоставщикам.КОформлениюОстаток > 0
	|	И (НЕ ЗаказТовары.Ссылка ЕСТЬ NULL
	//++ НЕ УТКА
	|		ИЛИ НЕ ЭтапТовары.Ссылка ЕСТЬ NULL
	//-- НЕ УТКА
	|		)
	|	И ЗаказыПоставщикам.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаказТовары.Упаковка",
		"ЗаказТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЭтапТовары.Упаковка",
		"ЭтапТовары.Номенклатура"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	РезультатыЗапроса = Запрос.Выполнить();
	Объект.Товары.Загрузить(РезультатыЗапроса.Выгрузить());
	
КонецПроцедуры
//-- НЕ УТ

//++ НЕ УТКА
&НаСервере
Процедура ЗаполнитьПоЗаказуДавальца(Знач ДокументОснование)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказТовары.Ссылка КАК Ссылка,
	|	ЗаказТовары.Номенклатура КАК Номенклатура,
	|	ЗаказТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказТовары.Характеристика КАК Характеристика,
	|	ЗаказТовары.Упаковка КАК Упаковка,
	|	ЗаказТовары.КодСтроки КАК КодСтроки
	|ПОМЕСТИТЬ ДанныеПоТоварам
	|ИЗ
	|	Документ.ЗаказДавальца.Материалы КАК ЗаказТовары
	|ГДЕ
	|	ЗаказТовары.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказТовары.Номенклатура КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураПоставщика,
	|	ЗаказТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказТовары.Характеристика КАК Характеристика,
	|	ЗаказТовары.Упаковка КАК Упаковка,
	|	ЗаказыПоставщикам.КОформлениюОстаток / ВЫБОР
	|		КОГДА ЗаказТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	|			,
	|			ЗаказПоставщику = &ДокументОснование
	|				И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК ЗаказыПоставщикам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоТоварам КАК ЗаказТовары
	|		ПО ЗаказыПоставщикам.КодСтроки = ЗаказТовары.КодСтроки
	|ГДЕ
	|	ЗаказыПоставщикам.КОформлениюОстаток > 0";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаказТовары.Упаковка",
		"ЗаказТовары.Номенклатура"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	РезультатыЗапроса = Запрос.Выполнить();
	Объект.Товары.Загрузить(РезультатыЗапроса.Выгрузить());
КонецПроцедуры


//-- НЕ УТКА

#КонецОбласти

#КонецОбласти
