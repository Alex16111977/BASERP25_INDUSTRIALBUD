#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	ДоверенностьВыданнаяЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос.УстановитьПараметр("Ссылка", Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ДоверенностьВыданнаяЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ДоверенностьВыданнаяЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Добавляет команду создания документа "Доверенность".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ДоверенностьВыданная) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ДоверенностьВыданная.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ДоверенностьВыданная);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьДоверенностиНаПолучениеТМЦ";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	ДоверенностьВыданнаяЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

// Возвращает срок действия последней введенной ответственным в систему доверенности.
//
// Возвращаемое значение:
//	Число - срок действия последней доверенности.
//
Функция ПолучитьСрокДействияПоследнейДоверенности() Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Доверенность.СрокДействия КАК СрокДействия
		|ИЗ
		|	Документ.ДоверенностьВыданная КАК Доверенность
		|ГДЕ
		|	НЕ Доверенность.ПометкаУдаления
		|	И Доверенность.Ответственный = &Ответственный
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ
		|");
		
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СрокДействия;
	КонецЕсли;
	
КонецФункции

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//	НовыйСтатус - Строка - Имя нового статуса
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса.
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыДоверенностей[НовыйСтатус];
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус) КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатусСовпадает,
	|	ИСТИНА КАК Проведен, // Для документа доверенности свойство <Проведенение> установлено в <Запретить>
	|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ЗаписьПроведением
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
//	НовыйСтатус - Перечисление - Новый статус
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки.
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Возврат Истина; // Дополнительные проверки для доверенности отсутствуют
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизЛицо)
	|	И ЗначениеРазрешено(Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Печать

Функция ДоверенностиПоЗаказам(МассивОбъектов) Экспорт
	
	ДоверенностиПоЗаказам = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Ссылка
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенность
	|ГДЕ
	|	НЕ Доверенность.ПометкаУдаления
	|	И Доверенность.ДокументОснование В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ДоверенностиПоЗаказам = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДоверенностиПоЗаказам;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Если ПравоДоступа("Изменение", Метаданные.Документы.ДоверенностьВыданная) Тогда
		// М-2 (Доверенность)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "М2";
		КомандаПечати.Представление = НСтр("ru='Доверенность (М-2)';uk='Довіреність (М-2)'");
		КомандаПечати.ДополнительныеПараметры.Вставить("Тип", "ДоверенностьМ2");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	ДоверенностьВыданнаяЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем АдресКомплектаПечатныхФорм;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М2") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М2", НСтр("ru='Доверенность на получение ТМЦ (М-2)';uk='Довіреність на отримання ТМЦ (М-2)'"), СформироватьПечатнуюФорму(ПараметрыПечати["Тип"], МассивОбъектов, ОбъектыПечати));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КомплектДокументов") Тогда
		Если ТипЗнч(ПараметрыПечати) = Тип("Структура") И ПараметрыПечати.Свойство("АдресКомплектаПечатныхФорм", АдресКомплектаПечатныхФорм) Тогда
			КомплектПечатныхФорм = ПолучитьИзВременногоХранилища(АдресКомплектаПечатныхФорм);
		КонецЕсли;
		Если КомплектПечатныхФорм.Количество()>0 Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "КомплектДокументов", НСтр("ru='Комплект документов';uk='Комплект документів'"), СформироватьПечатнуюФорму(КомплектПечатныхФорм[0].Имя, МассивОбъектов, ОбъектыПечати));
		КонецЕсли;
	КонецЕсли;
	
	ДоверенностьВыданнаяЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
		
КонецПроцедуры


Функция КомплектПечатныхФорм() Экспорт
	
	КомплектПечатныхФорм = РегистрыСведений.НастройкиПечатиОбъектов.ПодготовитьКомплектПечатныхФорм();
	
	ДоверенностьВыданнаяЛокализация.КомплектПечатныхФорм(КомплектПечатныхФорм);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "М2", НСтр("ru='Доверенность на получение ТМЦ (М-2)';uk='Довіреність на отримання ТМЦ (М-2)'"), 1);
	Возврат КомплектПечатныхФорм;
	
КонецФункции

// Заполняет структуру данными о получателях печатных форм.
// Параметры:
// 	СтруктураДанныхОбъектаПечати - см. ФормированиеПечатныхФорм.ЗаполнитьДанныеСтруктурыПолучателяПоТипуОбъекта.СтруктураДанныхОбъектаПечати
// 
Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Контрагент";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда 
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФорму(Тип, МассивОбъектов, ОбъектыПечати) Экспорт
	КодЯзыкаПечать = "uk";	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Ссылка КАК Ссылка,
	|	Доверенность.Номер КАК Номер,
	|	Доверенность.Дата КАК ДатаДокумента,
	|	Доверенность.Организация КАК Организация,
	|	Доверенность.Организация.Префикс КАК Префикс,
	|	ТаблицаОтветственныеЛица.РуководительПолноеФИО  КАК ФИОРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерПолноеФИО КАК ФИОГлавногоБухгалтера,
	|	Доверенность.ФизЛицо КАК ФизЛицо,
	|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|	Доверенность.ДатаОкончанияДействия КАК СрокДействия,
	|	Доверенность.Должность КАК Должность,
	|	Доверенность.Ответственный КАК Ответственный,
	|	Доверенность.БанковскийСчет КАК БанковскийСчет,
	|	Доверенность.Контрагент.НаименованиеПолное КАК ПоставщикПредставление,
	|	Доверенность.ДокументОснование КАК ДокументОснование,
	|	Доверенность.ДокументОснование.Номер КАК НомерОснования,
	|	Доверенность.ДокументОснование.Дата КАК ДатаОснования,
	|	Доверенность.ДокументОснование.Организация.Префикс КАК ПрефиксОснования,
	|	Доверенность.Товары.(
	|		НомерСтроки КАК Номер,
	|		ПРЕДСТАВЛЕНИЕ(Доверенность.Товары.НоменклатураПоставщика) КАК НоменклатураПоставщика,
	|		Номенклатура.НаименованиеПолное КАК Номенклатура,
	|		Характеристика.НаименованиеПолное КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ Доверенность.Товары.Упаковка.Наименование
	|		КОНЕЦ КАК Упаковка,
	|		&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияПредставление,
	|		КоличествоУпаковок КАК КоличествоУпаковок
	|	),
	|	Доверенность.МатериальныеЦенности.(
	|		НомерСтроки КАК Номер,
	|		МатериальнаяЦенность КАК МатериальнаяЦенность,
	|		Количество КАК Количество,
	|		ПРЕДСТАВЛЕНИЕ(Доверенность.МатериальныеЦенности.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление
	|	),
	|	Доверенность.ВидДокументаФизЛица КАК ВидДокумента,
	|	Доверенность.СерияДокументаФизЛица КАК ПаспортСерия,
	|	Доверенность.НомерДокументаФизЛица КАК ПаспортНомер,
	|	Доверенность.КемВыданДокументФизлица КАК ПаспортВыдан,
	|	Доверенность.ДатаВыдачиДокументаФизЛица КАК ПаспортДатаВыдачи
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенность
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО Доверенность.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	Доверенность.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Доверенность.Товары.Упаковка",
			"Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"Доверенность.Товары.Упаковка",
			"Доверенность.Товары.Номенклатура"));
	
	ДанныеПечати = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДоверенностьВыданная.ПФ_MXL_UK_М2");
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		НомерДокументаНаПечать        = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Ложь, Истина);
		ФамилияИмяОтчествоДоверенного = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.ФизЛицо, ДанныеПечати.ДатаДокумента);
		
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
			ДанныеПечати.Организация,
			ДанныеПечати.ДатаДокумента,
			,
			ДанныеПечати.БанковскийСчет);
		
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			СведенияОбОрганизации, 
			"ЮридическийАдрес",,КодЯзыкаПечать);
		КодПоОКУД = "";
		ОбластьМакета = Макет.ПолучитьОбласть("ЛицеваяСторона");

		СтруктураДанныхШапка = Новый Структура;
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		СтруктураДанныхШапка.Вставить("НомерДокумента", НомерДокументаНаПечать);
		СтруктураДанныхШапка.Вставить("ДатаДокумента", Формат(ДанныеПечати.ДатаДокумента, "Л = " + КодЯзыкаПечать + "; ДФ='дд ММММ гггг ""р.'"));
		СтруктураДанныхШапка.Вставить("СрокДействия",  Формат(ДанныеПечати.СрокДействия,  "Л = " + КодЯзыкаПечать + "; ДФ='дд ММММ гггг ""р.'"));
		СтруктураДанныхШапка.Вставить("КомуВыдана",                      ?(НЕ ЗначениеЗаполнено(ДанныеПечати.Должность),"",ДанныеПечати.Должность + " ") + ДанныеПечати.ФизЛицо);
		СтруктураДанныхШапка.Вставить("ОрганизацияПредставление",        СведенияОбОрганизации.ПолноеНаименование);
		СтруктураДанныхШапка.Вставить("ОрганизацияПредставление",        СведенияОбОрганизации.НаименованиеДСТУ);
		СтруктураДанныхШапка.Вставить("КодПоЕДРПОУ",                     СведенияОбОрганизации.КодПоЕДРПОУ);
		СтруктураДанныхШапка.Вставить("РеквизитыОрганизации",            ПредставлениеОрганизации);
		СтруктураДанныхШапка.Вставить("НомерРасчетногоСчетаОрганизации", СведенияОбОрганизации.НомерСчета);
		СтруктураДанныхШапка.Вставить("БанкОрганизации",                 СведенияОбОрганизации.Банк);
		СтруктураДанныхШапка.Вставить("МФОБанкаОрганизации",             СведенияОбОрганизации.МФО);
		СтруктураДанныхШапка.Вставить("ПодтверждающийДокументВид",       ДанныеПечати.ВидДокумента);
		СтруктураДанныхШапка.Вставить("ПодтверждающийДокументСерия",     ДанныеПечати.ПаспортСерия);
		СтруктураДанныхШапка.Вставить("ПодтверждающийДокументНомер",     ДанныеПечати.ПаспортНомер);
		СтруктураДанныхШапка.Вставить("ПодтверждающийДокументКемВыдан",  ДанныеПечати.ПаспортВыдан);
		СтруктураДанныхШапка.Вставить("ПодтверждающийДокументДатаВыдачи",ДанныеПечати.ПаспортДатаВыдачи);

		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Заполнить таблицу
		ОбластьМакета = Макет.ПолучитьОбласть("ОбратнаяСторонаШапка");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСпереносом");

		ВыборкаСтрокТовары = ДанныеПечати.Товары.Выбрать();
		ВыборкаСтрокМатериальныеЦенности = ДанныеПечати.МатериальныеЦенности.Выбрать();
		КоличествоСтрок = 0;
		НомерПоследнейСтроки = 0;
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			
			Если ЗначениеЗаполнено(ВыборкаСтрокТовары.НоменклатураПоставщика) Тогда
			
				МатериальнаяЦенность = ВыборкаСтрокТовары.НоменклатураПоставщика;
				
			Иначе
			
				МатериальнаяЦенность = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					ВыборкаСтрокТовары.Номенклатура,
					ВыборкаСтрокТовары.Характеристика);
				
			КонецЕсли;
			
			СтруктураДанныхСтрокаТаблицы = Новый Структура;
			СтруктураДанныхСтрокаТаблицы.Вставить("МатериальнаяЦенность", МатериальнаяЦенность);
			СтруктураДанныхСтрокаТаблицы.Вставить("КоличествоПрописью",
				?(ВыборкаСтрокТовары.КоличествоУпаковок = 0,
				"",
				Строка(ВыборкаСтрокТовары.КоличествоУпаковок) + " (" + 
 				ФормированиеПечатныхФорм.КоличествоПрописью(ВыборкаСтрокТовары.КоличествоУпаковок,КодЯзыкаПечать) + ")"));
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСтрокаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			КоличествоСтрок = КоличествоСтрок + 1;
			
		КонецЦикла;
		
		Пока ВыборкаСтрокМатериальныеЦенности.Следующий() Цикл
		
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокМатериальныеЦенности);
			СтруктураДанныхСтрокаТаблицы = Новый Структура;
			НомерПоследнейСтроки = ВыборкаСтрокМатериальныеЦенности.Номер + КоличествоСтрок;
			СтруктураДанныхСтрокаТаблицы.Вставить("Номер", НомерПоследнейСтроки);
			СтруктураДанныхСтрокаТаблицы.Вставить("КоличествоПрописью",
				?(ВыборкаСтрокМатериальныеЦенности.Количество = 0,
				"",
				Строка(ВыборкаСтрокМатериальныеЦенности.Количество) + " (" + 
				ФормированиеПечатныхФорм.КоличествоПрописью(ВыборкаСтрокМатериальныеЦенности.Количество,КодЯзыкаПечать) + ")"));
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСтрокаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		// Заполнить реквизиты подвала
		ОбластьМакета = Макет.ПолучитьОбласть("ОбратнаяСторонаПодвал");
		
		ОбластьМакета.Параметры.ФИОРуководителя = ФизическиеЛицаКлиентСервер.ИмяФамилияВФорматеДСТУ(ДанныеПечати.ФИОРуководителя);
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = ФизическиеЛицаКлиентСервер.ИмяФамилияВФорматеДСТУ(ДанныеПечати.ФИОГлавногоБухгалтера);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
	
КонецФункции
#КонецОбласти

#Область ТекущиеДела

// Заполняет список текущих дел пользователя.
// Параметры: 
//     ТекущиеДела - ТаблицаЗначений - см. ТекущиеДелаСлужебный.НоваяТаблицаТекущихДел.
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	ИмяФормы = "Документ.ДоверенностьВыданная.Форма.ФормаСпискаДокументов";
	ИспользованиеДоверенностей = ПолучитьФункциональнуюОпцию("ИспользоватьДоверенностиНаПолучениеТМЦ");
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСлужебный.ОбщиеПараметрыЗапросов();
	
	// Определим доступны ли текущему пользователю показатели группы
	Доступность =
		(ОбщиеПараметрыЗапросов.ЭтоПолноправныйПользователь
			Или ПравоДоступа("Просмотр", Метаданные.Документы.ДоверенностьВыданная))
		И (ПравоДоступа("Добавление", Метаданные.Документы.ДоверенностьВыданная)
			ИЛИ ПравоДоступа("Изменение", Метаданные.Документы.ДоверенностьВыданная))
		И ИспользованиеДоверенностей;
	
	Если НЕ Доступность Тогда
		Возврат;
	КонецЕсли;
	
	ДоступностьРаспоряженийТовары = ИспользованиеДоверенностей
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаявкиНаВозвратТоваровОтКлиентов);

	// Документы.
	ЧтениеЗаказПоставщику = ПравоДоступа("Чтение", Метаданные.Документы.ЗаказПоставщику)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказПоставщику);
	ЧтениеЗаказКлиента = ПравоДоступа("Чтение", Метаданные.Документы.ЗаказКлиента)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказКлиента);
	ЧтениеЗаявкаНаВозвратТоваровОтКлиента = ПравоДоступа("Чтение", Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента);
	ЧтениеАктВыполненныхРабот = ПравоДоступа("Чтение", Метаданные.Документы.АктВыполненныхРабот)
		И ПравоДоступа("Просмотр", Метаданные.Документы.АктВыполненныхРабот);
	ЧтениеРеализацияУслугПрочихАктивов = ПравоДоступа("Чтение", Метаданные.Документы.РеализацияУслугПрочихАктивов)
		И ПравоДоступа("Просмотр", Метаданные.Документы.РеализацияУслугПрочихАктивов);
	ЧтениеОтчетКомиссионера = ПравоДоступа("Чтение", Метаданные.Документы.ОтчетКомиссионера)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ОтчетКомиссионера);
	ЧтениеРеализацияТоваровУслуг = ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваровУслуг)
		И ПравоДоступа("Просмотр", Метаданные.Документы.РеализацияТоваровУслуг);
		
	//++ НЕ УТ
	ЧтениеЗаказПереработчику = ПравоДоступа("Чтение", Метаданные.Документы.ЗаказПереработчику)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказПереработчику);
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ЧтениеЗаказДавальца = ПравоДоступа("Чтение", Метаданные.Документы.ЗаказДавальца)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказДавальца);
	ЧтениеОтчетДавальцу = ПравоДоступа("Чтение", Метаданные.Документы.ОтчетДавальцу)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ОтчетДавальцу);
	//-- НЕ УТКА
	
	ЧтениеВозвратТоваровПоставщику = ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровПоставщику)
		И ПравоДоступа("Просмотр", Метаданные.Документы.ВозвратТоваровПоставщику);
	
	// Справочники.
	ЧтениеДоговорыКонтрагентов = ПравоДоступа("Чтение", Метаданные.Справочники.ДоговорыКонтрагентов)
		И ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКонтрагентов);
	
	// Расчет показателей
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Доверенности.Ссылка,
	|	Доверенности.ДокументОснование,
	|	Доверенности.Статус,
	|	Доверенности.ДатаОкончанияДействия,
	|	Доверенности.Ответственный
	|ПОМЕСТИТЬ ВыданныеДоверенности
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенности
	|ГДЕ
	|	НЕ Доверенности.ПометкаУдаления
	|;";

	Если ДоступностьРаспоряженийТовары Тогда
		ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаРаспоряженийПоступление.ЗаказПоставщику КАК Ссылка
		|ПОМЕСТИТЬ РаспоряженияПоступление
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, ТИПЗНАЧЕНИЯ(ЗаказПоставщику) В (&ТипыДокументов)) КАК ТаблицаРаспоряженийПоступление
		|ГДЕ
		|	ТаблицаРаспоряженийПоступление.КОформлениюОстаток > 0
		|	И ТИПЗНАЧЕНИЯ(ТаблицаРаспоряженийПоступление.ЗаказПоставщику) В (&ТипыДокументов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаРаспоряженийВозврат.ЗаявкаНаВозвратТоваровОтКлиента
		|ИЗ
		|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов.Остатки(, ТИПЗНАЧЕНИЯ(ЗаявкаНаВозвратТоваровОтКлиента) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)) КАК ТаблицаРаспоряженийВозврат
		|ГДЕ
		|	&ИспользоватьРасширенныеВозможностиЗаказаКлиента
		|	И ТаблицаРаспоряженийВозврат.КОформлениюОстаток > 0
		|	И ТИПЗНАЧЕНИЯ(ТаблицаРаспоряженийВозврат.ЗаявкаНаВозвратТоваровОтКлиента) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаРаспоряженийВозвратН.Ссылка
		|ИЗ
		|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ТаблицаРаспоряженийВозвратН
		|ГДЕ
		|	НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
		|	И ТаблицаРаспоряженийВозвратН.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РаспоряженияПоступление.Ссылка) КАК КоличествоПоступление
		|ПОМЕСТИТЬ РаспоряженияНаДоставку
		|ИЗ
		|	РаспоряженияПоступление КАК РаспоряженияПоступление
		|;";
		Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
			ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
			
		ТипыДокументов = Новый Массив;
		Если ЧтениеЗаказПоставщику Тогда
			ТипыДокументов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		КонецЕсли;
		
		//++ НЕ УТ
		Если ЧтениеЗаказПереработчику Тогда
			ТипыДокументов.Добавить(Тип("ДокументСсылка.ЗаказПереработчику"));
		КонецЕсли;
		//-- НЕ УТ
		
		//++ НЕ УТКА
		Если ЧтениеЗаказДавальца Тогда
			ТипыДокументов.Добавить(Тип("ДокументСсылка.ЗаказДавальца"));
		КонецЕсли;
		//-- НЕ УТКА
		
		Запрос.УстановитьПараметр("ТипыДокументов", ТипыДокументов);
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	NULL КАК РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров,
//	|	NULL КАК РаспоряженияНаОформлениеДоверенностиНаПолучениеДС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДокументДоверенность.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.Выдана)
	|				ТОГДА ДокументДоверенность.Ссылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ВыданныеДоверенности,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДокументДоверенность.ДатаОкончанияДействия <= &ДатаАктуальности
	|					И ДокументДоверенность.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ДокументДоверенность.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.Выдана)
	|				ТОГДА ДокументДоверенность.Ссылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ВыданныеДоверенностиИстекающиеНаСегодня,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДокументДоверенность.ДатаОкончанияДействия < &ДатаАктуальности
	|					И ДокументДоверенность.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ДокументДоверенность.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.Выдана)
	|				ТОГДА ДокументДоверенность.Ссылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ВыданныеДоверенностиПросроченные
	|ПОМЕСТИТЬ ТаблицаПоказателей
	|ИЗ
	|	ВыданныеДоверенности КАК ДокументДоверенность
	|ГДЕ
	|	ДокументДоверенность.Ответственный = &Пользователь
	|";
	
	Если ДоступностьРаспоряженийТовары Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РаспоряженияНаДоставку.КоличествоПоступление,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ РаспоряженияНаДоставку";
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	ТекущиеДелаСлужебный.УстановитьОбщиеПараметрыЗапросов(Запрос, ОбщиеПараметрыЗапросов);
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|//%Д1 	СУММА(ТаблицаПоказателей.РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров) КАК РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров,
	|		СУММА(ТаблицаПоказателей.ВыданныеДоверенности) КАК ВыданныеДоверенности,
	|		СУММА(ТаблицаПоказателей.ВыданныеДоверенностиИстекающиеНаСегодня) КАК ВыданныеДоверенностиИстекающиеНаСегодня,
	|		СУММА(ТаблицаПоказателей.ВыданныеДоверенностиПросроченные) КАК ВыданныеДоверенностиПросроченные
	|ИЗ
	|	ТаблицаПоказателей КАК ТаблицаПоказателей";
	
	// Уберем служебные коментарии из текста запроса - "разблокируем" строки запроса
	Если ДоступностьРаспоряженийТовары Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//%Д1", "     ");
	КонецЕсли;
	Результат = ТекущиеДелаСлужебный.ЧисловыеПоказателиТекущихДел(Запрос);
	
	// Заполнение дел.
	// ДоверенностиНаПолучениеТМЦ
	// РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров
	ЕстьРаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров = Ложь;
	Если ДоступностьРаспоряженийТовары Тогда
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Состояние", Перечисления.СтатусыДоверенностей.НеВыдана);
		ПараметрыОтбора.Вставить("Актуальность", "");
		ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
		ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
		ПараметрыОтбора.Вставить("ФизЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
		ПараметрыОтбора.Вставить("ТребуетсяДоверенность", Истина);
		ПараметрыОтбора.Вставить("ЕстьДоверенность", Ложь);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбора);
		ПараметрыФормы.Вставить("СостояниеРаспоряжения", НСтр("ru='Требующие создания доверенности';uk='Потребують створення довіреності'"));
		ПараметрыФормы.Вставить("НаличиеДоверенности", Истина);
		ПараметрыФормы.Вставить("ИмяТекущейСтраницы", "СтраницаРаспоряженияНаОформление");
		
		ДелоРодитель = ТекущиеДела.Добавить();
		ДелоРодитель.Идентификатор  = "ДоверенностиНаПолучениеТМЦ";
		ДелоРодитель.Представление  = НСтр("ru='Доверенности на получение ТМЦ';uk='Довіреності на отримання ТМЦ'");
		ДелоРодитель.Владелец       = Метаданные.Подсистемы.Закупки;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = "РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров";
		Дело.ЕстьДела       = Результат.РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров > 0;
		Дело.Представление  = НСтр("ru='Основания для оформления (получение товаров)';uk='Підстави для оформлення (отримання товарів)'");
		Дело.Количество     = Результат.РаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров;
		Дело.Важное         = Ложь;
		Дело.Форма          = ИмяФормы;
		Дело.ПараметрыФормы = ПараметрыФормы;
		Дело.Владелец       = "ДоверенностиНаПолучениеТМЦ";
		
		ЕстьРаспоряженияНаОформлениеДоверенностиНаПолучениеТоваров = Дело.ЕстьДела;
		ДелоРодитель.ЕстьДела = Дело.ЕстьДела;
	КонецЕсли;
	

	ДелоРодительФинансы = ТекущиеДела.Добавить();
	ДелоРодительФинансы.Идентификатор  = "ВыданныеДоверенностиРодитель";
	ДелоРодительФинансы.Представление  = НСтр("ru='Выданные доверенности';uk='Видані довіреності'");
	ДелоРодительФинансы.Владелец       = Метаданные.Подсистемы.Казначейство;
	
	// ВыданныеДоверенности
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Перечисления.СтатусыДоверенностей.Выдана);
	ПараметрыОтбора.Вставить("Актуальность", "");
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("ФизЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ПараметрыОтбора.Вставить("ЕстьДоверенность", Ложь);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	ПараметрыФормы.Вставить("СостояниеРаспоряжения", НСтр("ru='Требующие создания доверенности';uk='Потребують створення довіреності'"));
	ПараметрыФормы.Вставить("НаличиеДоверенности", Истина);
	ПараметрыФормы.Вставить("ИмяТекущейСтраницы", "СтраницаДоверенностиНаПолучениеТоваров");
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ВыданныеДоверенности";
	Дело.ЕстьДела       = Результат.ВыданныеДоверенности > 0;
	Дело.Представление  = НСтр("ru='Выданные доверенности';uk='Видані довіреності'");
	Дело.Количество     = Результат.ВыданныеДоверенности;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Владелец       = "ВыданныеДоверенностиРодитель";
	
	// ВыданныеДоверенностиИстекающиеНаСегодня
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Перечисления.СтатусыДоверенностей.Выдана);
	ПараметрыОтбора.Вставить("Актуальность", НСтр("ru='Сегодня';uk='Сьогодні'"));
	
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("ФизЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ПараметрыОтбора.Вставить("ЕстьДоверенность", Ложь);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	ПараметрыФормы.Вставить("СостояниеРаспоряжения", НСтр("ru='Требующие создания доверенности';uk='Потребують створення довіреності'"));
	ПараметрыФормы.Вставить("НаличиеДоверенности", Истина);
	ПараметрыФормы.Вставить("ИмяТекущейСтраницы", "СтраницаДоверенностиНаПолучениеТоваров");
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ВыданныеДоверенностиИстекающиеНаСегодня";
	Дело.ЕстьДела       = Результат.ВыданныеДоверенностиИстекающиеНаСегодня > 0;
	Дело.Представление  = НСтр("ru='Доверенности, истекающие сегодня';uk='Довіреності, що закінчуються сьогодні'");
	Дело.Количество     = Результат.ВыданныеДоверенностиИстекающиеНаСегодня;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Владелец       = "ВыданныеДоверенностиРодитель";
	
	// ВыданныеДоверенностиПросроченные
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Перечисления.СтатусыДоверенностей.Выдана);
	ПараметрыОтбора.Вставить("Просрочен", "Сегодня");
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Ответственный", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("ФизЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ПараметрыОтбора.Вставить("ЕстьДоверенность", Ложь);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	ПараметрыФормы.Вставить("СостояниеРаспоряжения", НСтр("ru='Требующие создания доверенности';uk='Потребують створення довіреності'"));
	ПараметрыФормы.Вставить("НаличиеДоверенности", Истина);
	ПараметрыФормы.Вставить("ИмяТекущейСтраницы", "СтраницаДоверенностиНаПолучениеТоваров");
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ВыданныеДоверенностиПросроченные";
	Дело.ЕстьДела       = Результат.ВыданныеДоверенностиПросроченные > 0;
	Дело.Представление  = НСтр("ru='Просроченные доверенности';uk='Прострочені довіреності'");
	Дело.Количество     = Результат.ВыданныеДоверенностиПросроченные;
	Дело.Важное         = Истина;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Владелец       = "ВыданныеДоверенностиРодитель";
	
	Если Результат.ВыданныеДоверенности > 0
		Или Результат.ВыданныеДоверенностиИстекающиеНаСегодня > 0
		Или Результат.ВыданныеДоверенностиПросроченные > 0 Тогда
		ДелоРодительФинансы.ЕстьДела = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт
	
#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.ДоверенностьВыданная.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("391a8428-05ca-476e-89b4-0916f408a10e");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ДоверенностьВыданная.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Документ.ДоверенностьВыданная";
	Обработчик.ИзменяемыеОбъекты = "Документ.ДоверенностьВыданная";
	Обработчик.БлокируемыеОбъекты = "Документ.ДоверенностьВыданная";
	Обработчик.Комментарий = НСтр("ru='Устанавливается реквизит ""Количество позиций"" в документе ""Доверенность выданная""';uk='Встановлюється реквізит ""Кількість позицій"" у документі ""Довіреність видана""'");

#КонецОбласти
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДоверенностьМатериальныеЦенности.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ДоверенностьМатериальныеЦенности
	|ГДЕ
	|	НЕ ДоверенностьМатериальныеЦенности.ПометкаУдаления
	|	И ДоверенностьМатериальныеЦенности.КоличествоПозиций = 0
	|	И НЕ ДоверенностьМатериальныеЦенности.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.НеВыдана)
	|	И ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.ДоверенностьВыданная.МатериальныеЦенности КАК МатериальныеЦенностиТЧ
	|			ГДЕ
	|				МатериальныеЦенностиТЧ.Ссылка = ДоверенностьМатериальныеЦенности.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоверенностьТовары.Ссылка
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ДоверенностьТовары
	|ГДЕ
	|	НЕ ДоверенностьТовары.ПометкаУдаления
	|	И ДоверенностьТовары.КоличествоПозиций = 0
	|	И НЕ ДоверенностьТовары.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.НеВыдана)
	|	И ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.ДоверенностьВыданная.Товары КАК ТоварыТЧ
	|			ГДЕ
	|				ТоварыТЧ.Ссылка = ДоверенностьТовары.Ссылка)
	|");
	
	Результат = Запрос.Выполнить().Выгрузить();
	Ссылки = Результат.ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Ссылки
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ДоверенностьВыданная";
	
	МетаданныеДокумента = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц
	);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
		
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			
			Если ДокументОбъект <> Неопределено Тогда
				
				ДокументОбъект.КоличествоПозиций = ДокументОбъект.Товары.Количество() + ДокументОбъект.МатериальныеЦенности.Количество();
				
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(
					ДокументОбъект,
					,
					,
					РежимЗаписиДокумента.Запись
				);
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


#КонецОбласти

#Область Прочее

#Область ФормированиеГиперссылкиВЖурналеЗакупок 

Функция ТекстЗапросаРаспоряженийНаТовары(НетДоверенности = Неопределено) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(Доверенности.Ссылка) КАК СозданаДоверенность,
	|	Доверенности.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Доверенности
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенности
	|ГДЕ
	|	НЕ Доверенности.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	Доверенности.ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлановоеПоступление.Распоряжение.Ссылка КАК Ссылка,
	|	ПлановоеПоступление.Распоряжение.ПометкаУдаления КАК ПометкаУдаления,
	|	ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) КАК ТипРаспоряжения,
	|	ПлановоеПоступление.Распоряжение.Дата КАК Дата,
	|	ПлановоеПоступление.Распоряжение.Номер КАК Номер,
	|	ПлановоеПоступление.Распоряжение.Партнер КАК Партнер,
	|	ПлановоеПоступление.Распоряжение.Контрагент КАК Контрагент,
	|	ПлановоеПоступление.Распоряжение.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА
	//++ НЕ УТ
	|			ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) = ТИП(Документ.ЗаказПереработчику) ИЛИ
	//-- НЕ УТ
	|				ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ПлановоеПоступление.Распоряжение.Склад
	|	КОНЕЦ КАК Склад,
	|	ПлановоеПоступление.Распоряжение.Валюта КАК Валюта,
	|	ПлановоеПоступление.Распоряжение.Менеджер КАК Менеджер,
	|	ПлановоеПоступление.Распоряжение.СуммаДокумента КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Приоритеты.ПустаяСсылка) КАК Приоритет,
	|	ПлановоеПоступление.Распоряжение.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПРЕДСТАВЛЕНИЕ(ПлановоеПоступление.Распоряжение.ХозяйственнаяОперация) КАК ХозяйственнаяОперацияПредставление,
	|	ПлановоеПоступление.Распоряжение.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА Доверенности.СозданаДоверенность ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДоверенность,
	|	1 КАК КартинкаПриоритета
	|ИЗ
	|	#ТекстЗапросаПлановоеПоступление КАК ПлановоеПоступление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Доверенности КАК Доверенности
	|		ПО ПлановоеПоступление.Распоряжение = Доверенности.ДокументОснование
	|ГДЕ
	|	НЕ ПлановоеПоступление.Распоряжение.Ссылка ЕСТЬ NULL 
	|	И (ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) = ТИП(Документ.ЗаказПоставщику)
	//++ НЕ УТ
	|			ИЛИ ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) = ТИП(Документ.ЗаказПереработчику)
	//-- НЕ УТ
	//++ НЕ УТКА
	|			ИЛИ ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) = ТИП(Документ.ЗаказДавальца)
	//-- НЕ УТКА
	|			ИЛИ ТИПЗНАЧЕНИЯ(ПлановоеПоступление.Распоряжение) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
	|	И &УсловиеОтбора";
	
	ТекстЗапросаПлановоеПоступление = Новый Массив;
	Если ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам) Тогда
		ТекстЗапросаПлановоеПоступление.Добавить("
		|ВЫБРАТЬ
		|		ЗаказыПоставщикам.ЗаказПоставщику КАК Распоряжение
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам.Остатки КАК ЗаказыПоставщикам
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЗаказыПоставщикам.ЗаказПоставщику
		|	
		|	ИМЕЮЩИЕ
		|		СУММА(ЗаказыПоставщикам.КОформлениюОстаток) > 0");
	КонецЕсли;
	Если ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаявкиНаВозвратТоваровОтКлиентов) Тогда
		ТекстЗапросаПлановоеПоступление.Добавить("
		|	ВЫБРАТЬ
		|		ЗаявкиНаВозврат.ЗаявкаНаВозвратТоваровОтКлиента КАК Распоряжение
		|	ИЗ
		|		РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов.Остатки КАК ЗаявкиНаВозврат
		|	ГДЕ
		|		&ИспользоватьРасширенныеВозможностиЗаказаКлиента
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЗаявкиНаВозврат.ЗаявкаНаВозвратТоваровОтКлиента
		|	
		|	ИМЕЮЩИЕ
		|		СУММА(ЗаявкиНаВозврат.КОформлениюОстаток) > 0");
	КонецЕсли;
	Если ПравоДоступа("Просмотр", Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента) Тогда
		ТекстЗапросаПлановоеПоступление.Добавить("
		|	ВЫБРАТЬ
		|		ЗаявкиНаВозврат.Ссылка КАК Распоряжение
		|	ИЗ
		|		Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкиНаВозврат
		|	ГДЕ
		|		НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
		|		И ЗаявкиНаВозврат.Проведен");
	КонецЕсли;
	Если ТекстЗапросаПлановоеПоступление.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='У пользователя недостаточно прав для исполнения операции над базой данных';uk='У користувача недостатньо прав для виконання операції над базою даних'");
	КонецЕсли;
	ТекстЗапросаПлановоеПоступление = СтрСоединить(ТекстЗапросаПлановоеПоступление,"
		|ОБЪЕДИНИТЬ ВСЕ
		|");
	ТекстЗапросаПлановоеПоступление = "("+ТекстЗапросаПлановоеПоступление+")";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТекстЗапросаПлановоеПоступление",ТекстЗапросаПлановоеПоступление);
	
	УсловиеОтбора = "";
	
	Если ЗначениеЗаполнено(НетДоверенности) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", "Доверенности.СозданаДоверенность ЕСТЬ NULL");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &УсловиеОтбора", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция СформироватьГиперссылкуСмТакжеВРаботе(Параметры) Экспорт
	
	ИспользоватьРасширенныеВозможностиЗаказаКлиента = ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента");
	Если Не (ПолучитьФункциональнуюОпцию("ИспользоватьДоверенностиНаПолучениеТМЦ")
			И ПравоДоступа("Чтение", Метаданные.Документы.ДоверенностьВыданная)
			И (ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам)
				Или ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаявкиНаВозвратТоваровОтКлиентов)
					И ИспользоватьРасширенныеВозможностиЗаказаКлиента
				Или ПравоДоступа("Чтение", Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента)
					И Не ИспользоватьРасширенныеВозможностиЗаказаКлиента)) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаРаспоряженийНаТовары(Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ 1");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
			ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	ТекстГиперссылки = НСтр("ru='Доверенности';uk='Довіреності'");
	
	ИмяФормыРабочееМестоДоверенности = "Документ.ДоверенностьВыданная.Форма.ФормаСпискаДокументов";
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,,
			ИмяФормыРабочееМестоДоверенности);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,,
			ИмяФормыРабочееМестоДоверенности);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область РасчетСтатусаДоверенности

Функция РассчитатьСостояниеДоверенности(МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоверенностьНаТовары.Ссылка КАК Ссылка,
	|	ДоверенностьНаТовары.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ДоверенностиНаТовары
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ДоверенностьНаТовары
	|ГДЕ
	|	ДоверенностьНаТовары.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.Выдана)
	|	И ДоверенностьНаТовары.Ссылка В(&МассивСсылок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоверенностиНаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Поступившие
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоверенностиНаТовары КАК ДоверенностиНаТовары
	|		ПО (ДоверенностиНаТовары.ДокументОснование = РасчетыСПоставщиками.ОбъектРасчетов.Объект)
	|			И (РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВИдДвиженияНакопления.Расход))
	|			И (НЕ РасчетыСПоставщиками.КПоступлению = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументДоверенностьВыданная.Ссылка КАК Ссылка,
	|	ДокументДоверенностьВыданная.ПометкаУдаления КАК ПометкаУдаления,
	|	ПРЕДСТАВЛЕНИЕ(ДокументДоверенностьВыданная.Ссылка) КАК Представление,
	|	НЕ Поступившие.Ссылка ЕСТЬ NULL КАК ЕстьПоступлениеТоваров
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ДокументДоверенностьВыданная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоверенностиНаТовары КАК ДоверенностиНаТовары
	|		ПО (ДоверенностиНаТовары.Ссылка = ДокументДоверенностьВыданная.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Поступившие КАК Поступившие
	|		ПО (Поступившие.Ссылка = ДокументДоверенностьВыданная.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументДоверенностьВыданная.Ссылка,
	|	ДокументДоверенностьВыданная.ПометкаУдаления,
	|	ПРЕДСТАВЛЕНИЕ(ДокументДоверенностьВыданная.Ссылка),
	|	ЛОЖЬ
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК ДокументДоверенностьВыданная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДоверенностиНаТовары
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Поступившие
	|;
	|";
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ТаблицаДоверенностей = Запрос.Выполнить().Выгрузить();
	ТаблицаДоверенностей.Индексы.Добавить("Ссылка");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТаблицаДоверенностей;
КонецФункции

Процедура РассчитатьСтатусДоверенности(Объект, Знач ЕстьПоступлениеТоваров = Ложь) Экспорт	
		Объект.ЕстьПоступлениеТоваров = ЕстьПоступлениеТоваров;
		Если ЕстьПоступлениеТоваров Тогда
			Объект.Статус = Перечисления.СтатусыДоверенностей.Использована;
		КонецЕсли;
КонецПроцедуры

Процедура РассчитатьСтатусДоверенностей(ПараметрыМетода, АдресХранилища) Экспорт
	
	ШаблонОшибкиПомеченНаУдаление = НСтр("ru='Документ %1 помечен на удаление. Невозможно рассчитать статус';uk='Документ %1 відмічений для вилучення. Неможливо розрахувати статус'");
	ШаблонОшибкиЗаблокировать = НСтр("ru='Не удалось заблокировать %1. %2';uk='Не вдалося заблокувати %1. %2'");
	ШаблонОшибкиЗаписать = НСтр("ru='Не удалось записать %1. %2';uk='Не вдалося записати %1. %2'");
	
	Если ПараметрыМетода.МассивДокументов.Количество() = 0 Тогда
		ВыбранныеДокументы = ДоверенностиЗаПоследнийМесяц();
	Иначе
		ВыбранныеДокументы = ПараметрыМетода.МассивДокументов;
	КонецЕсли;
	
	СоставДокументов = Документы.ДоверенностьВыданная.РассчитатьСостояниеДоверенности(ВыбранныеДокументы);
	
	КоличествоОбработанных = 0;
	Для каждого СтрокаДокумента Из СоставДокументов Цикл
		
		Если СтрокаДокумента.ПометкаУдаления Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(ШаблонОшибкиПомеченНаУдаление, СтрокаДокумента.Представление), СтрокаДокумента.Ссылка);
			Продолжить;
		КонецЕсли;
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(СтрокаДокумента.Ссылка);
		Исключение
			ТекстОшибки = СтрШаблон(ШаблонОшибкиЗаблокировать,
				СтрокаДокумента.Представление, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, СтрокаДокумента.Ссылка);
			Продолжить;
		КонецПопытки;
		
		Объект = СтрокаДокумента.Ссылка.ПолучитьОбъект(); // ДокументОбъект.ДоверенностьВыданная
		
		РассчитатьСтатусДоверенности(Объект,
			СтрокаДокумента.ЕстьПоступлениеТоваров);
		Попытка
			Объект.Записать(РежимЗаписиДокумента.Запись);
			КоличествоОбработанных = КоличествоОбработанных + 1;
		Исключение
			ТекстОшибки = СтрШаблон(ШаблонОшибкиЗаписать,
				СтрокаДокумента.Представление, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, СтрокаДокумента.Ссылка);
		КонецПопытки;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(КоличествоОбработанных, АдресХранилища);
КонецПроцедуры

Функция ДоверенностиЗаПоследнийМесяц()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Доверенности.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДоверенностьВыданная КАК Доверенности
	|ГДЕ
	|	НЕ Доверенности.ПометкаУдаления
	|	И Доверенности.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ), МЕСЯЦ, -1)
	|	И (Доверенности.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.Выдана)
	|			ИЛИ Доверенности.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоверенностей.ИспользованаЧастично)
	|)";

	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
