#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Сотрудники.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.БронированиеГражданПребывающихВЗапасе;
	ОписаниеСоставаОбъекта = ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	ОписаниеСоставаОбъекта.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	Возврат ОписаниеСоставаОбъекта;
	
КонецФункции

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Список";
	КомандаПечати.Представление = НСтр("ru='Список для бронирования';uk='Список для бронювання'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Список") Тогда
		ТабличныйДокумент = ТабличныйДокументСписок(МассивОбъектов[0], ОбъектыПечати);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"Список", НСтр("ru='Список для бронирования';uk='Список для бронювання'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ТабличныйДокументСписок(МассивОбъектов, ОбъектыПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.БронированиеГражданПребывающихВЗапасе.ПФ_MXL_UK_Список");
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	//Шапка 
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(МассивОбъектов.Организация, МассивОбъектов.Дата);
	
	ОбластьШапка.Параметры.Организация = СведенияОбОрганизации.ПолноеНаименование;
	ТекстОрганизация = СведенияОбОрганизации.ПолноеНаименование + ", " + СведенияОбОрганизации.ЮридическийАдрес + ", " + СведенияОбОрганизации.КодПоЕДРПОУ;	
	
	
	ТабДокумент.Вывести(ОбластьШапка);
	
	//Заголовок
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
    ТабДокумент.Вывести(ОбластьЗаголовок);	
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	НомерСтроки = 1;
	
	РезультатЗапросаПоСтрокам = ПолучитьЗапросДляПечатиСписок(МассивОбъектов.Ссылка);
	Для Каждого Строка ИЗ РезультатЗапросаПоСтрокам Цикл
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ОбластьСтрока.Параметры.ФИО	= Строка.Фамилия + " " + Строка.Имя + " " + Строка.Отчество;
		ОбластьСтрока.Параметры.ГодРождения	= ?(ЗначениеЗаполнено(Строка.ДатаРождения), Формат(Год(Строка.ДатаРождения),"ЧГ=0"), "");
		ОбластьСтрока.Параметры.Должность	= Строка.Должность;
		Если ЗначениеЗаполнено(Строка.Звание) Тогда
			ОбластьСтрока.Параметры.ЗваниеПрофиль	= Строка.Звание + ", " + Строка.ВУС + ?(ЗначениеЗаполнено(Строка.Профиль), " ("+МультиязычностьУкр.ПолучитьЛокализованноеПредставление(Строка.Профиль, "uk")+")", "");
		Иначе
			ОбластьСтрока.Параметры.ЗваниеПрофиль	= "";
		КонецЕсли;	
		ОбластьСтрока.Параметры.ТекстОрганизация	= ТекстОрганизация;
		
		ТабДокумент.Присоединить(ОбластьСтрока);
		НомерСтроки = НомерСтроки +1;
	КонецЦикла;

	ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
	
	РуководителиОрганизации = ОтветственныеЛицаБП.ОтветственныеЛица(МассивОбъектов.Организация, МассивОбъектов.Дата);
	
	ОбластьПодпись.Параметры.ДолжностьРуководителя = РуководителиОрганизации.РуководительДолжностьПредставление;
	Если ЗначениеЗаполнено(РуководителиОрганизации.Руководитель) Тогда
		ОбластьПодпись.Параметры.ФИОРуководителя	   = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РуководителиОрганизации.Руководитель), "ИОФамилия")[0].ИОФамилия;
	КонецЕсли;	
	
	ОбластьПодпись.Параметры.ДатаДокумента = Формат(МассивОбъектов.Дата, "Л=uk; ДЛФ=DD");
	
	ТабДокумент.Вывести(ОбластьПодпись);
	
	Возврат ТабДокумент;
	
КонецФункции	

Функция ПолучитьЗапросДляПечатиСписок(Документ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("Дата", Документ.Дата);
	Запрос.УстановитьПараметр("Ссылка", Документ);

	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БронированиеГражданПребывающихВЗапасе.ФизическоеЛицо
		|ПОМЕСТИТЬ ВыборкаПоДокументу
		|ИЗ
		|	Документ.БронированиеГражданПребывающихВЗапасе.Сотрудники КАК БронированиеГражданПребывающихВЗапасе
		|ГДЕ
		|	БронированиеГражданПребывающихВЗапасе.Ссылка = &Ссылка
		|";
	Запрос.Выполнить();
	СоздатьВТКадровыхДанных(Запрос, Документ.Организация, Документ.Дата);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыборкаПоДокументу.ФизическоеЛицо,
		|	ФизическиеЛица.ДатаРождения,
		|	ФИОФизическихЛицСрезПоследних.Фамилия,
		|	ФИОФизическихЛицСрезПоследних.Имя,
		|	ФИОФизическихЛицСрезПоследних.Отчество,
		|	КадровыеДанные.Должность.Наименование КАК Должность,
		|	ЕСТЬNULL(ВоинскийУчетСрезПоследних.Звание.Наименование, """") КАК Звание,
		|	ЕСТЬNULL(ВоинскийУчетСрезПоследних.ВУС, """") КАК ВУС,
		|	ЕСТЬNULL(ВоинскийУчетСрезПоследних.Профиль, ЗНАЧЕНИЕ(Перечисление.ПрофилиОфицеров.ПустаяСсылка)) КАК Профиль 
		|ИЗ
		|	ВыборкаПоДокументу КАК ВыборкаПоДокументу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО ВыборкаПоДокументу.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанные КАК КадровыеДанные
		|		ПО ВыборкаПоДокументу.ФизическоеЛицо = КадровыеДанные.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&Дата, ) КАК ФИОФизическихЛицСрезПоследних
		|		ПО ВыборкаПоДокументу.ФизическоеЛицо = ФИОФизическихЛицСрезПоследних.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет.СрезПоследних(&Дата, ) КАК ВоинскийУчетСрезПоследних
		|		ПО ВыборкаПоДокументу.ФизическоеЛицо = ВоинскийУчетСрезПоследних.ФизическоеЛицо";
	
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	

Процедура СоздатьВТКадровыхДанных(Запрос, Организация, ДатаСобытия)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	 |	ВыборкаПоДокументу.ФизическоеЛицо
     |ИЗ
	 |	ВыборкаПоДокументу КАК ВыборкаПоДокументу";
	
	СписокФизЛиц = Запрос.Выполнить().Выгрузить();
		
	ОсновныеСотрудникиФизическихЛиц = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(СписокФизЛиц, Истина, Организация, ДатаСобытия);
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОсновныеСотрудникиФизическихЛиц.ВыгрузитьКолонку("Сотрудник"), "Должность", ДатаСобытия);
	Запрос.УстановитьПараметр("КадровыеДанные", КадровыеДанные);
	

	Запрос.Текст =
	"ВЫБРАТЬ
	 |	КадровыеДанные.ФизическоеЛицо,
	 |	КадровыеДанные.Должность
	 |ПОМЕСТИТЬ ВТКадровыеДанные
     |ИЗ
	 |	&КадровыеДанные КАК КадровыеДанные";
	Запрос.Выполнить();

КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли