#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	МассивНесогласованныхСтатусов = Новый Массив;
	МассивНесогласованныхСтатусов.Добавить(Перечисления.СтатусыНалоговыхДокументов.КПроверке);
	МассивНесогласованныхСтатусов.Добавить(Перечисления.СтатусыНалоговыхДокументов.Сформирован);
	
	ОбщегоНазначенияУТ.ИзменитьПризнакСогласованностиДокумента(
		ЭтотОбъект,
		РежимЗаписи,
		МассивНесогласованныхСтатусов);

	Если ЗначениеЗаполнено(СтатусРегистрацииВЕРНН) И СтатусРегистрацииВЕРНН <> Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.НеЗарегистрированВЕРНН
		И СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТекущийДокумент Тогда
		СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ФормироватьПриложение2;
	КонецЕсли;
	
	Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ФормироватьПриложение2 Тогда
		РазрешенаАвтоКорректировка = Ложь;
	Иначе
		РазрешенаАвтоКорректировка = Истина;
	КонецЕсли;
	
	ЭтоУсловнаяПродажа = (ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
		Или ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа);
	
	Если НалоговаяНакладнаяНеЗарегистрированаВИБ И ЗначениеЗаполнено(НалоговаяНакладная) Тогда
		НалоговаяНакладная = Неопределено;
	КонецЕсли;
	Если НЕ НалоговаяНакладнаяНеЗарегистрированаВИБ И (ЗначениеЗаполнено(НалоговаяНакладнаяНомер) ИЛИ ЗначениеЗаполнено(НалоговаяНакладнаяДата)) Тогда
		НалоговаяНакладнаяНомер = Неопределено;
		НалоговаяНакладнаяДата = Неопределено;
	КонецЕсли;
	
	Если НЕ НалоговаяНакладнаяНеЗарегистрированаВИБ И ЗначениеЗаполнено(ОбособленноеПодразделение) Тогда
		ОбособленноеПодразделение = НалоговаяНакладная.ОбособленноеПодразделение;
	КонецЕсли;
	
	Если ЭтоУсловнаяПродажа
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации Тогда
		ОчиститьВзаиморасчеты = Истина;
	Иначе
		ОчиститьВзаиморасчеты = Ложь;
	КонецЕсли;
	
	Если ЭтоУсловнаяПродажа
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента Тогда
		ОбъектРасчетов = Неопределено;
	КонецЕсли;
	
	Если ОчиститьВзаиморасчеты Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		Партнер = Справочники.Партнеры.ПустаяСсылка();
		Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ДатаДоговора = Неопределено;
		НомерДоговора = Неопределено;
		ВидДоговора = Неопределено;
	КонецЕсли;
	
	Если ЭтоУсловнаяПродажа Тогда
		Сводная = Истина;
	КонецЕсли;
	
	Если ЭтоУсловнаяПродажа
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента Тогда
		Товары.Очистить();
	КонецЕсли;
	
	Если НЕ СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
		ТоварыПоДаннымПользователя.Очистить();
	КонецЕсли;

	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить("Товары");
	ТабличныеЧасти.Добавить("ТоварыПоДаннымПользователя");
	Для каждого ИмяТабличнойЧасти Из ТабличныеЧасти Цикл
		Для каждого СтрокаТовары Из ЭтотОбъект[ИмяТабличнойЧасти] Цикл
			// Это обычный товар или услуга
			Если ЗначениеЗаполнено(СтрокаТовары.Номенклатура) И ЗначениеЗаполнено(СтрокаТовары.ЕдиницаИзмерения) Тогда
				СтрокаТовары.ЕдиницаИзмерения = Неопределено;	
			КонецЕсли;
			// Это прочий актив
			Если Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) И ЗначениеЗаполнено(СтрокаТовары.Упаковка) Тогда
				СтрокаТовары.Упаковка = Неопределено;	
			КонецЕсли;
			Если ОчиститьВзаиморасчеты Тогда
				СтрокаТовары.ОбъектРасчетов = Неопределено;
			КонецЕсли;
		КонецЦикла;		
	КонецЦикла;

// Для табличной части "ТоварыПоДаннымПользователя"
	НаименованиеОчищаемыхРеквизитов = Новый Массив;
	
	Если НЕ ЭтоУсловнаяПродажа Тогда
		НаименованиеОчищаемыхРеквизитов.Добавить("НалоговоеНазначение");
		НаименованиеОчищаемыхРеквизитов.Добавить("НалоговоеНазначениеПоФактуУсловнаяПродажа");
		НаименованиеОчищаемыхРеквизитов.Добавить("ОбъектЗаполненияСодержанияУсловнаяПродажа");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа Тогда
		НаименованиеОчищаемыхРеквизитов.Добавить("СтатьяРасходовУсловнаяПродажа");
		НаименованиеОчищаемыхРеквизитов.Добавить("АналитикаРасходовУсловнаяПродажа");
	КонецЕсли;
	
	Для каждого СтрокаТовары Из ТоварыПоДаннымПользователя Цикл
		Для Каждого ЭлементМассива Из НаименованиеОчищаемыхРеквизитов Цикл
			Если ЗначениеЗаполнено(СтрокаТовары[СокрЛП(ЭлементМассива)]) Тогда
				СтрокаТовары[СокрЛП(ЭлементМассива)] = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;		
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		Партнер = Справочники.Партнеры.НашеПредприятие;
	КонецЕсли;

	
	Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
		СуммаДокумента = ТоварыПоДаннымПользователя.Итог("СуммаСНДС");
	Иначе
		СуммаДокумента = Товары.Итог("СуммаСНДС");
	КонецЕсли;	

	Если СуммаДокумента <> 0 Тогда
		// поставим флаг "Требует регистрация в реестре"
		Если Дата >= '2015-01-01' Тогда
			ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			ЭлектронныйДокумент = Истина;
		ИначеЕсли ЗначениеЗаполнено(НалоговаяНакладная) И НалоговаяНакладная.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных Тогда
			
			ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			
		Иначе
			
			Если ТипПричиныНевыдачиПокупателю >= 1
				И Дата >= '2014-03-01' Тогда
				
				ЭлектронныйДокумент = Ложь;
			    ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
				
			КонецЕсли;
			
			Если ТипПричиныНевыдачиПокупателю > 0 Тогда
				// мы не управляем флажком ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных
				// для налоговых накладных, которые не выдаются покупателю
				// согласно разъяснению ЕБНЗ такие налоговые не должны регистрироваться в Едином реестре
				
			ИначеЕсли ЭлектронныйДокумент Тогда
				
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				
			Иначе	
				
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
			
				Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
					НДСРегл = ТоварыПоДаннымПользователя.Итог("СуммаНДСРегл");
				Иначе
					НДСРегл = Товары.Итог("СуммаНДСРегл");
				КонецЕсли;
				
				Если Дата >= '20120101' Тогда
					// или сумма НДС в документе больше 10 000 грн
					Если НДСРегл > 10000 Тогда
						ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
						
					// или имеются подакцизные/импортированные товары
					// этот факт определим так - если в строке указан код УКТЗЭД - считаем что условие выполняется.
					ИначеЕсли   Товары.НайтиСтроки(Новый Структура("НомерГТД", Справочники.НоменклатураГТД.ПустаяСсылка())).Количество() 	<> Товары.Количество() Тогда
						ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
					КонецЕсли;
				ИначеЕсли Дата >= '20110701' Тогда
					// сумма НДС в документе больше 100 000 грн
					Если НДСРегл > 100000 Тогда
						ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
					КонецЕсли;
				ИначеЕсли Дата >= '20110401' Тогда
					// сумма НДС в документе больше 500 000 грн
					Если НДСРегл > 500000 Тогда
						ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
					КонецЕсли;
				ИначеЕсли Дата >= '20110101' Тогда
					// сумма НДС в документе больше 1 000 000 грн
					Если НДСРегл > 1000000 Тогда
						ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина
				   И ЗначениеЗаполнено(НалоговаяНакладная)
				   И НалоговаяНакладная.СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.НеЗарегистрированВЕРНН Тогда
				   
				   Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Перед выгрузкой документа: %1 в Единый реестр налоговых накладных"
	"необходимо также зарегистрировать исходную налоговую накладную: %2!';uk='Перед вивантаженням документа: %1 до Єдиного реєстра податкових накладних"
	"необхідно також зареєструвати в ньому податкову накладну: %2!'"), Ссылка, НалоговаяНакладная), СтатусСообщения.Важное);
				   
				КонецЕсли;
				
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

	ДоступенФлагПереоценка =  
		 (ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации ИЛИ
	      ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации ИЛИ
	      ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации) И 
		 (ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка);		  
	Если Не ДоступенФлагПереоценка Тогда		  
		Переоценка = Ложь;	
	КонецЕсли;	

	Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
		ДокументУменьшаетНО = ТоварыПоДаннымПользователя.Итог("СуммаНДСРегл") < 0;
	Иначе
		ДокументУменьшаетНО = Товары.Итог("СуммаНДСРегл") < 0;
	КонецЕсли;
	Если ТипПричиныНевыдачиПокупателю = 0
		И ДокументУменьшаетНО Тогда
		РегистрируетсяВЕРННПокупателем = Истина;
	Иначе
		РегистрируетсяВЕРННПокупателем = Ложь;
	КонецЕсли;
	
	Если НЕ СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.ЗарегистрированВЕРНН Тогда
		ДатаВключенияВЕдиныйРеестрНалоговыхНакладных = '0001-01-01';
	ИначеЕсли НачалоМесяца(Дата) = НачалоМесяца(ДатаВключенияВЕдиныйРеестрНалоговыхНакладных) Тогда
		ДатаВключенияВЕдиныйРеестрНалоговыхНакладных = Дата;
	Иначе	
		ДатаВключенияВЕдиныйРеестрНалоговыхНакладных = НачалоМесяца(ДатаВключенияВЕдиныйРеестрНалоговыхНакладных);		
	КонецЕсли;

	НДСИсходящийСервер.ПроверитьНомерНалоговогоДокументаПередЗаписью(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.НалоговаяНакладная") Тогда

		ЗаполнитьДокументНаОснованииНалоговойНакладной(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
		ИмяАктивнойТЧ = "ТоварыПоДаннымПользователя";
		// Для т.ч. Товары корректность заполнения гарантируется системой
	Иначе
		ИмяАктивнойТЧ = "Товары";
		// Т.ч. ТоварыПоДаннымПользователя будет очищена ПриЗаписи(...)
	КонецЕсли;
	ПрефиксРеквизитовНеАктивнойТЧ = ?(ИмяАктивнойТЧ = "Товары", "ТоварыПоДаннымПользователя", "Товары") + ".";
	Для каждого ИмяПроверяемогоРеквизита Из ПроверяемыеРеквизиты Цикл
		Если Лев(ИмяПроверяемогоРеквизита, СтрДлина(ПрефиксРеквизитовНеАктивнойТЧ)) = ПрефиксРеквизитовНеАктивнойТЧ Тогда
			МассивНепроверяемыхРеквизитов.Добавить(ИмяПроверяемогоРеквизита);
		КонецЕсли;
	КонецЦикла;
	ПараметрыПроверкиЗаполненияХарактеристик = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверкиЗаполненияХарактеристик.ИмяТЧ = ИмяАктивнойТЧ;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверкиЗаполненияХарактеристик);

	Если ЗначениеЗаполнено(Организация) И Не НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Организация, Дата) Тогда
		ТекстОшибки = НСтр("ru='Организация не является плательщиком НДС';uk='Організація не є платником ПДВ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"Организация",
			,
			Отказ
		);
	КонецЕсли;

	Если ЗначениеЗаполнено(НалоговаяНакладная)
	 // Для Продажи ниже обычной цены в НН Партнер и Контрагент используются только в качестве дополнительного отбора	
	 И НЕ (ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены) Тогда	

		АналитикаП2 = РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(ЭтотОбъект);
		АналитикаНН = РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(НалоговаяНакладная);
		
		Если НЕ АналитикаП2 = АналитикаНН Тогда
		
			ТекстОшибки = НСтр("ru='Не совпадает аналитика взаиморасчетов с корректируемой Налоговой накладной';uk='Не збігається аналітика взаєморозрахунків з коригованою Податковою накладною'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				"НалоговаяНакладная",
				,
				Отказ
			);
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипПричиныНевыдачиПокупателю = 22 И (Товары.Найти(501, "КодПричины") <> Неопределено ИЛИ ТоварыПоДаннымПользователя.Найти(501, "КодПричины") <> Неопределено) И НЕ ЗначениеЗаполнено(ДатаГТД) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВключенияВЕдиныйРеестрНалоговыхНакладных");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='В документе не указана дата ГТД. Документ не будет включен в декларацию НДС и не будет иметь проводок.';uk='У документі не вказано дату ВМД. Документ не буде включений до декларації ПДВ та не матиме проводок.'"));
	ИначеЕсли ДокументУменьшаетНО Тогда
		Если НЕ СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.ЗарегистрированВЕРНН Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ДатаВключенияВЕдиныйРеестрНалоговыхНакладных");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Документ не зарегистрирован в ЕРНН и не указан период включения в Декларацию по НДС (реквизит ""Включить в Декларацию за:"")! Проводки сформированы частично.';uk='Документ не зареєстрований в ЄРПН і не зазначений період включення до Декларації з ПДВ (реквізит """"Включити до Декларації за:"""")! Проводки сформовані частково.'"),
				,
				"Объект.СтатусРегистрацииВЕРНН",
				,);
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВключенияВЕдиныйРеестрНалоговыхНакладных");
	КонецЕсли;

	Если Статус = Перечисления.СтатусыНалоговыхДокументов.Проверен Тогда
		Если НалоговаяНакладнаяНеЗарегистрированаВИБ Тогда
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладная");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладнаяНомер");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладнаяДата");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладная");
		МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладнаяНомер");
		МассивНепроверяемыхРеквизитов.Добавить("НалоговаяНакладнаяДата");
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("Партнер");
	КонецЕсли;

	Если '2014-12-01' > Дата  Тогда
		Если НДСИсходящийСервер.ВДокументеЕстьСтавкаНДС7ИДругиеСтавки(ЭтотОбъект) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

	Если Не ВключаетсяВУточняющийРасчет Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаОтгрузкиОплаты");
	КонецЕсли;
	
	Если НЕ (ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа) Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ТоварыПоДаннымПользователя.НалоговоеНазначение"); 
		МассивНепроверяемыхРеквизитов.Добавить("ТоварыПоДаннымПользователя.НалоговоеНазначениеПоФактуУсловнаяПродажа");
	КонецЕсли; 
	
	МассивНепроверяемыхРеквизитов.Добавить("ТоварыПоДаннымПользователя.ОбъектЗаполненияСодержанияУсловнаяПродажа");

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	// Выбор статей и аналитик.
	ПараметрыВыбораСтатейИАналитик = Документы.Приложение2КНалоговойНакладной.ПараметрыВыбораСтатейИАналитик(ВидОперации);
	ПараметрыВыбораСтатейИАналитик[0].ДоступностьПоОперации = ПараметрыВыбораСтатейИАналитик[0].ДоступностьПоОперации 
		И ТоварыПоДаннымПользователя.Итог("СуммаНДС") <> ТоварыПоДаннымПользователя.Итог("СуммаНДСРеглПоРегиструУсловнаяПродажа");
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты,
		ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Согласован             = Ложь;
	Статус                 = Перечисления.СтатусыНалоговыхДокументов.КПроверке;
	СтатусВыдачиПокупателю = Перечисления.СтатусыВыдачиПокупателюНалоговыхДокументов.НеВыданПокупателю;
	СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.НеЗарегистрированВЕРНН;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	НДСИсходящийСервер.УстановитьПрефиксНалоговогоДокумента(ЭтотОбъект, Префикс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
	
	Валюта = НДСОбщегоНазначенияСервер.ПолучитьВалютуРегламентированногоУчета(Валюта);
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	ОбособленноеПодразделение = НДСИсходящийСервер.ОпределитьОбособленноеПодразделениеПоУмолчанию(
		Организация, 
		Договор
	);
	
	КтоВыписалНалоговуюНакладную = НДСИсходящийСервер.ОпределитьОтветственногоЗаВыпискуНалоговыхДокументов(
		Организация, 
		Договор
	);
	
	Если ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации Тогда
	 	Если Не ЗначениеЗаполнено(Партнер) Тогда
			Партнер = Справочники.Партнеры.РозничныйПокупатель;
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииНалоговойНакладной(ДокументОснование)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговаяНакладная.ОбособленноеПодразделение,
	|	НалоговаяНакладная.Валюта,
	|	НалоговаяНакладная.Организация,
	|	НалоговаяНакладная.Контрагент,
	|	НалоговаяНакладная.Партнер,
	|	НалоговаяНакладная.ВидОперации,
	|	НалоговаяНакладная.Договор,
	|	НалоговаяНакладная.ТипПричиныНевыдачиПокупателю,
	|	НалоговаяНакладная.СпецРежимНалогообложения,
	|	НалоговаяНакладная.ЛьготаНДС,
	|	НалоговаяНакладная.ПодтверждаетсяГТД,
	|	НалоговаяНакладная.НомерГТД,
	|	НалоговаяНакладная.ДатаГТД,
	|	НалоговаяНакладная.ЛьготаНДСОписание,
	|	НалоговаяНакладная.НаправлениеДеятельности,
	|	НалоговаяНакладная.Ссылка КАК НалоговаяНакладная
	|ИЗ
	|	Документ.НалоговаяНакладная КАК НалоговаяНакладная
	|ГДЕ
	|	НалоговаяНакладная.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;  
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ;
	ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка;
	
	ЗаполнитьТоварыПоТекущемуСостояниюНалоговойНакладной();
	
КонецПроцедуры

Процедура ЗаполнитьТоварыПоТекущемуСостояниюНалоговойНакладной() Экспорт
	
	ТоварыПоДаннымПользователя.Очистить();
	
	СоставНН = Документы.НалоговаяНакладная.СостояниеСтрокНалоговойНакладнойСУчетомКорректировок(НалоговаяНакладная);

	НомерГруппыКорректировки = 1;
	НомераСтрокСОшибками = Новый Массив;

	КоличествоСтрокНН = СоставНН.Количество();
	Если КоличествоСтрокНН = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МаксНомерСтрокиНН = СоставНН[КоличествоСтрокНН - 1].НомерСтрокиНН + 1;

	Для каждого ДанныеСтрокиНН Из СоставНН Цикл
		
		Если ДанныеСтрокиНН.ЕстьОшибки Тогда
			НомераСтрокСОшибками.Добавить(ДанныеСтрокиНН.НомерСтрокиНН);
			Продолжить;
		КонецЕсли;
		
		Если НЕ ДанныеСтрокиНН.ЕстьОстаток Тогда
			Продолжить;
		КонецЕсли;

		Для каждого СтрокаСостава Из ДанныеСтрокиНН.Состав Цикл
			
			НоваяСтрока = ТоварыПоДаннымПользователя.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
			НоваяСтрока.ЭтоКорректировкаКоличества = Истина;
			
			НоваяСтрока.НомерСтрокиНН = МаксНомерСтрокиНН;
			НоваяСтрока.НомерГруппы = НомерГруппыКорректировки;
			Если ТипПричиныНевыдачиПокупателю = 22 Тогда
				НоваяСтрока.КодПричины = 501;  
			Иначе
				НоваяСтрока.КодПричины = 104;  
			КонецЕсли;                      

			Документы.Приложение2КНалоговойНакладной.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ВидОперации, НоваяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТипПричиныНевыдачиПокупателю, НоваяСтрока.КодПричины);
			
			НоваяСтрокаСторно = ТоварыПоДаннымПользователя.Вставить(НоваяСтрока.НомерСтроки - 1);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСторно, НоваяСтрока);
			НоваяСтрокаСторно.НомерСтрокиНН = ДанныеСтрокиНН.НомерСтрокиНН;
			
			НоваяСтрокаСторно.Количество = - НоваяСтрокаСторно.Количество;
			НоваяСтрокаСторно.КоличествоУпаковок = - НоваяСтрокаСторно.КоличествоУпаковок;
			НоваяСтрокаСторно.Вес = - НоваяСтрокаСторно.Вес;

			НоваяСтрокаСторно.СуммаБезНДС = - НоваяСтрокаСторно.СуммаБезНДС;
			НоваяСтрокаСторно.СуммаНДС = - НоваяСтрокаСторно.СуммаНДС;
			НоваяСтрокаСторно.СуммаСНДС = - НоваяСтрокаСторно.СуммаСНДС;
			НоваяСтрокаСторно.СуммаБезНДСРегл = - НоваяСтрокаСторно.СуммаБезНДСРегл;
			НоваяСтрокаСторно.СуммаНДСРегл = - НоваяСтрокаСторно.СуммаНДСРегл;
			
		КонецЦикла;
		
		МаксНомерСтрокиНН = МаксНомерСтрокиНН + 1;
		НомерГруппыКорректировки = НомерГруппыКорректировки + 1;
		
	КонецЦикла; 
	
	Документы.Приложение2КНалоговойНакладной.ЗаполнитьСуммыРеглВТабличныхЧастях(ЭтотОбъект, "ТоварыПоДаннымПользователя");
	
	Если НомераСтрокСОшибками.Количество() Тогда
		
		МассивПричинОшибок = Новый Массив;
		ИндексНуля = НомераСтрокСОшибками.Найти(0);
		Если ИндексНуля <> Неопределено Тогда
			МассивПричинОшибок.Добавить(НСтр("ru='- Не указан номер строки НН в документе корректировки.';uk='- Не вказано номер рядка ПН у документі коригування.'"));
			НомераСтрокСОшибками.Удалить(ИндексНуля);
		КонецЕсли;                         
		
		Если НомераСтрокСОшибками.Количество() Тогда
			МассивПричинОшибок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='- Не полностью сторнированы строки с №№ %1 в предыдущих корректировках.';uk='- Не повністю сторновані рядки з №№ %1 у попередніх коригуваннях.'"), СтрСоединить(НомераСтрокСОшибками, ", ")));
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не может быть определено текущее состояние (с учетом предыдущих корректировок) строк документа информационной базы %1 по следующим причинам:
            |%2'
            |;uk='Неможливо визначити поточний стан (з урахуванням попередніх коригувань) рядків документа інформаційної бази %1 з наступних причин:
            |%2'"), НалоговаяНакладная, СтрСоединить(МассивПричинОшибок, Символы.ПС));
			
		Сообщить(ТекстСообщения);	
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область Прочее


Процедура ЗаполнитьРеквизитыПослеАвтоформирования(ЭтоНовыйОбъект = Истина) Экспорт
	
	Если ЭтоНовыйОбъект Тогда
		
		Если ЗначениеЗаполнено(НалоговаяНакладная) Тогда
			ОбособленноеПодразделение = НалоговаяНакладная.ОбособленноеПодразделение;
		КонецЕсли;
		
		Если СтатусВыдачиПокупателю = Перечисления.СтатусыВыдачиПокупателюНалоговыхДокументов.ВыданПокупателю Тогда
			СтатусВыдачиПокупателю = Перечисления.СтатусыВыдачиПокупателюНалоговыхДокументов.ПодлежитПовторнойВыдаче;
		Иначе
			СтатусВыдачиПокупателю = Перечисления.СтатусыВыдачиПокупателюНалоговыхДокументов.НеВыданПокупателю;
		КонецЕсли;
		
		СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.НеЗарегистрированВЕРНН;
		
		Если ЗначениеЗаполнено(НалоговаяНакладная) Тогда
			Если НЕ ЗначениеЗаполнено(ВидОперацииВозвратКорректировка) Тогда
				ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка;
			КонецЕсли;
				
			Если ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка
				ИЛИ ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.КорректировкаВозврата Тогда
	    		ВидОперации = НалоговаяНакладная.ВидОперации;
			КонецЕсли;
			
			СпецРежимНалогообложения = НалоговаяНакладная.СпецРежимНалогообложения;
			ТипПричиныНевыдачиПокупателю = НалоговаяНакладная.ТипПричиныНевыдачиПокупателю;
		Иначе
			ВалютаРегУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		КонецЕсли;
		
		Если ТипПричиныНевыдачиПокупателю = 7 Тогда
			ДатаОтгрузкиОплаты = Дата;
		КонецЕсли;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КоличествоСтрок = Товары.Количество();
	
	Для Ин=1 по КоличествоСтрок Цикл
		
		СтрокаТовары = Товары[КоличествоСтрок - Ин];
		
		Если СтрокаТовары.КоличествоУпаковок = 0 И СтрокаТовары.СуммаСНДС = 0 Тогда
			Товары.Удалить(СтрокаТовары);
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТовары.ДокументПоставки) И СтрокаТовары.НеПересчитыватьСумму Тогда
			СтрокаТовары.НеПересчитыватьСумму = Ложь;
		КонецЕсли; 
					
		СтрокаТовары.ЭтоКорректировкаКоличества = НЕ Переоценка;
		
		СтрокаТовары.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(СтрокаТовары.СуммаСНДС, СтрокаТовары.СтавкаНДС, Истина);
		СтрокаТовары.СуммаБезНДС = СтрокаТовары.СуммаСНДС - СтрокаТовары.СуммаНДС;
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьВесКГ");

		Если НЕ ЗначениеЗаполнено(СтрокаТовары.СтатьяДекларацииНДСНалоговыеОбязательства) Тогда
			Документы.Приложение2КНалоговойНакладной.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ВидОперации, СтрокаТовары.СтатьяДекларацииНДСНалоговыеОбязательства, ТипПричиныНевыдачиПокупателю, СтрокаТовары.КодПричины);
		КонецЕсли;
		
		Если ЭтоНовыйОбъект ИЛИ НЕ ЗначениеЗаполнено(СтрокаТовары.КодНоменклатурыПоКлассификатору) Тогда
			НДСИсходящийСервер.ЗаполнитьНоменклатуруГТДПоУмолчанию(СтрокаТовары);
		КонецЕсли;

		НДСИсходящийСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;	
	
	Если  ЭтоНовыйОбъект Тогда
		

		
		ОбособленноеПодразделение = НДСИсходящийСервер.ОпределитьОбособленноеПодразделениеПоУмолчанию(
			Организация, 
			Договор
		);
		
		КтоВыписалНалоговуюНакладную = НДСИсходящийСервер.ОпределитьОтветственногоЗаВыпискуНалоговыхДокументов(
			Организация, 
			Договор
		);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтатусАвтокорректировки) Тогда
		СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТекущийДокумент;
	КонецЕсли;
	
	Если ЭтоНовыйОбъект И Не ЗначениеЗаполнено(НалоговаяНакладная) Тогда
		Документы.Приложение2КНалоговойНакладной.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(ЭтотОбъект);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоАвтоформирование", Истина);
	
	Если ЗначениеЗаполнено(ДатаОтгрузкиОплаты) И (НачалоМесяца(ДатаОтгрузкиОплаты) <> НачалоМесяца(Дата)) Тогда
		ВключаетсяВУточняющийРасчет = Истина;
	КонецЕсли;
	
	НалоговаяНакладнаяНеЗарегистрированаВИБ = НЕ ЗначениеЗаполнено(НалоговаяНакладная);

	ОпределитьНебходимостьВключенияВЕРННПоУмолчанию();

	Документы.Приложение2КНалоговойНакладной.ЗаполнитьСуммыРеглВТабличныхЧастях(ЭтотОбъект, "Товары");

КонецПроцедуры

Процедура ЗаполнитьРеквизитыПослеФормированияПоУсловнымПродажам() Экспорт
	
	Валюта = НДСОбщегоНазначенияСервер.ПолучитьВалютуРегламентированногоУчета(Валюта);
	
	ОбособленноеПодразделение = НДСИсходящийСервер.ОпределитьОбособленноеПодразделениеПоУмолчанию(
		Организация, 
		Договор
	);
	
	КтоВыписалНалоговуюНакладную = НДСИсходящийСервер.ОпределитьОтветственногоЗаВыпискуНалоговыхДокументов(
		Организация, 
		Договор
	);
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	КоличествоСтрок = ТоварыПоДаннымПользователя.Количество();
	
	Для Ин=1 по КоличествоСтрок Цикл
		
		СтрокаТовары = ТоварыПоДаннымПользователя[КоличествоСтрок - Ин];
		
		СтрокаТовары.ЭтоКорректировкаКоличества = НЕ Переоценка;
		
		СтрокаТовары.СуммаСНДС = СтрокаТовары.СуммаБезНДС + СтрокаТовары.СуммаНДС;
		
		СтрокаТовары.Упаковка = ПодборТоваровВызовСервера.ПолучитьУпаковкуХранения(СтрокаТовары.Номенклатура);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаЦеныСкидкиВПродажахВТЧ(ЭтотОбъект, Истина));
		СтруктураДействий.Вставить("ПересчитатьВесКГ");
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовары.СтатьяДекларацииНДСНалоговыеОбязательства) Тогда
			Документы.Приложение2КНалоговойНакладной.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ВидОперации, СтрокаТовары.СтатьяДекларацииНДСНалоговыеОбязательства, ТипПричиныНевыдачиПокупателю, СтрокаТовары.КодПричины);
	КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовары.КодНоменклатурыПоКлассификатору) Тогда
			НДСИсходящийСервер.ЗаполнитьНоменклатуруГТДПоУмолчанию(СтрокаТовары, Ложь);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовары.Номенклатура) И ЗначениеЗаполнено(СтрокаТовары.ОбъектЗаполненияСодержанияУсловнаяПродажа)
			И СтрокаТовары.Количество = 0 Тогда
			Документы.Приложение2КНалоговойНакладной.ЗаполнитьСодержаниеДляУсловнойПродажи(СтрокаТовары.ОбъектЗаполненияСодержанияУсловнаяПродажа, СтрокаТовары.Содержание);
		КонецЕсли;

		НДСИсходящийСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	
	Если НЕ ЗначениеЗаполнено(СтатусАвтокорректировки) Тогда
		СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТипПричиныНевыдачиПокупателю) Тогда
		Документы.Приложение2КНалоговойНакладной.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(ЭтотОбъект);	
	КонецЕсли;	
	
	ОпределитьНебходимостьВключенияВЕРННПоУмолчанию();

	Документы.Приложение2КНалоговойНакладной.ЗаполнитьСуммыРеглВТабличныхЧастях(ЭтотОбъект, "ТоварыПоДаннымПользователя");

КонецПроцедуры


Процедура ОпределитьНебходимостьВключенияВЕРННПоУмолчанию()
	
	Если СтатусРегистрацииВЕРНН = Перечисления.СтатусыРегистрацииВЕРНННалоговыхДокументов.НеЗарегистрированВЕРНН
		ИЛИ НЕ ЗначениеЗаполнено(СтатусРегистрацииВЕРНН) Тогда
		
		Если СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
			Табчасть = ТоварыПоДаннымПользователя;
		Иначе
			Табчасть = Товары;
		КонецЕсли;	
		
		СтрокаПоказателей = "КоличествоУпаковок, Количество, СуммаБезНДСРегл, СуммаНДСРегл";
		СтруктураПоказателей = Новый Структура(СтрокаПоказателей);
		ЕстьСущественныеИзменения = Ложь;
		
		ТЗ = Табчасть.Выгрузить();
		ТЗ.Свернуть("Номенклатура, Содержание, Характеристика, КодНоменклатурыПоКлассификатору, Упаковка, ЕдиницаИзмерения, ЦенаРегл, СтавкаНДС, КодПричины, ЭтоКорректировкаКоличества", СтрокаПоказателей);
		Для каждого СтрокаТЗ Из ТЗ Цикл
			
			Для каждого Показатель Из СтруктураПоказателей Цикл
				Если ЗначениеЗаполнено(СтрокаТЗ[Показатель.Ключ]) Тогда
					ЕстьСущественныеИзменения = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьСущественныеИзменения Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = ЕстьСущественныеИзменения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
