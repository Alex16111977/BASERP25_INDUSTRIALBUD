
&НаКлиенте
Процедура ЗаявлениеРасчетВФССПриИзменении(Элемент)
	Объект.РасчетыСФондами.Очистить();
	ЗаявлениеРасчетВФССПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаявлениеРасчетВФССПриИзмененииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.ЗаявлениеРасчетВФСС) Тогда
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РасчетыСФондамиПоПособиямОстатки.Организация,
	               |	РасчетыСФондамиПоПособиямОстатки.Месяц,
	               |	РасчетыСФондамиПоПособиямОстатки.Заявление,
	               |	РасчетыСФондамиПоПособиямОстатки.ФондСоциальногоСтрахования,
	               |	РасчетыСФондамиПоПособиямОстатки.СтатьяРасчетов,
	               |	РасчетыСФондамиПоПособиямОстатки.Сотрудник КАК Сотрудник,
	               |	РасчетыСФондамиПоПособиямОстатки.ДокументОснование,
	               |	СУММА(РасчетыСФондамиПоПособиямОстатки.СуммаОстаток) КАК Сумма
	               |ПОМЕСТИТЬ Остатки
	               |ИЗ
	               |	РегистрНакопления.РасчетыСФондамиПоПособиям.Остатки(
	               |			&Дата,
	               |			Заявление = &Заявление
	               |				И Организация = &Организация) КАК РасчетыСФондамиПоПособиямОстатки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РасчетыСФондамиПоПособиямОстатки.Организация,
	               |	РасчетыСФондамиПоПособиямОстатки.Месяц,
	               |	РасчетыСФондамиПоПособиямОстатки.ФондСоциальногоСтрахования,
	               |	РасчетыСФондамиПоПособиямОстатки.Заявление,
	               |	РасчетыСФондамиПоПособиямОстатки.Сотрудник,
	               |	РасчетыСФондамиПоПособиямОстатки.СтатьяРасчетов,
	               |	РасчетыСФондамиПоПособиямОстатки.ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Остатки.Организация,
	               |	Остатки.Месяц,
	               |	Остатки.Заявление,
	               |	Остатки.ФондСоциальногоСтрахования,
	               |	Остатки.СтатьяРасчетов,
	               |	Остатки.Сотрудник КАК Сотрудник,
	               |	Остатки.ДокументОснование,
	               |	Остатки.Сумма,
	               |	ЕСТЬNULL(РасчетыСФондамиПоПособиям.КоличествоДней, 0) КАК КоличествоДней,
	               |	ЕСТЬNULL(РасчетыСФондамиПоПособиям.СуммаЛьготыЧернобыльцы, 0) КАК СуммаЛьготЧернобыльцы
	               |ИЗ
	               |	Остатки КАК Остатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСФондамиПоПособиям КАК РасчетыСФондамиПоПособиям
	               |		ПО Остатки.Заявление = РасчетыСФондамиПоПособиям.Регистратор
	               |			И Остатки.Сотрудник = РасчетыСФондамиПоПособиям.Сотрудник
	               |			И Остатки.ДокументОснование = РасчетыСФондамиПоПособиям.ДокументОснование
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник";
	Запрос.УстановитьПараметр("Заявление", Объект.ЗаявлениеРасчетВФСС);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() = 0 Тогда
		//Сообщить что нет данных
	Иначе
		Для Каждого Строка Из Таблица Цикл
			НовСтрока = Объект.РасчетыСФондами.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	Объект.ФондСоциальногоСтрахования = Объект.ЗаявлениеРасчетВФСС.ФондСоциальногоСтрахования;
	ЗаполнитьМесяцПоДатеТабличнойЧасти();
	КлючевыеРеквизиты();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСФондамиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный,",
		"Объект.Организация",
		"Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ЗаявлениеРасчетВФССПриИзмененииНаСервере();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.РасчетыСФондами.Очистить();
		ЗаявлениеРасчетВФССПриИзмененииНаСервере();
	КонецЕсли;
	
	КлючевыеРеквизиты();
	ЗаполнитьМесяцПоДатеТабличнойЧасти();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры  

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМесяцПоДатеТабличнойЧасти()
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.РасчетыСФондами, "Месяц", "МесяцСтрокой");
	
КонецПроцедуры	

&НаСервере
Процедура КлючевыеРеквизиты()
	ЗарплатаКадрыРасширенный.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма, КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов());
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма, , КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов(), КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении());
КонецПроцедуры	

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",	НСтр("ru='организации';uk='організації'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "ЗаявлениеРасчетВФСС",	НСтр("ru='заявления';uk='заяви'")));
	
	Возврат Массив
КонецФункции

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	Если Объект.РасчетыСФондами.Количество() = 0 Тогда
		Объект.ЗаявлениеРасчетВФСС = ПредопределенноеЗначение("Документ.ЗаявлениеРасчетВФСС.ПустаяСсылка");
		Объект.ФондСоциальногоСтрахования = ПредопределенноеЗначение("Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить("Объект.РасчетыСФондами");
	Возврат Массив
	
КонецФункции 

&НаКлиенте
Процедура ЗаполнитьПоЗаявлению(Команда)
	
	Объект.РасчетыСФондами.Очистить();
	ЗаявлениеРасчетВФССПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаполнитьМесяцПоДатеТабличнойЧасти();
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства
#КонецОбласти

