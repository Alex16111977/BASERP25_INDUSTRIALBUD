
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СвойстваФайла = ПолучитьСвойстваПрисоединенногоФайла(ПараметрКоманды);
	
	СообщениеОбОшибке = "";
	
	Если НЕ СвойстваФайла.ФайлРегистраУчетаПрисоединен Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru='%1 не содержит присоединенного файла';uk=' %1 не містить приєднаного файлу'"), ПараметрКоманды);
	КонецЕсли;
	
	Если ПустаяСтрока(СообщениеОбОшибке) И НЕ СвойстваФайла.ИспользоватьЭП Тогда
		СообщениеОбОшибке = НСтр("ru='Для подписи регистра включите использование ЭП в настройках программы';uk='Щоб підписати регістр увімкніть використання ЕП в налаштуваннях програми.'");
	КонецЕсли;
	
	Если ПустаяСтрока(СообщениеОбОшибке) И СвойстваФайла.ПодписанЭП Тогда
		СообщениеОбОшибке = СтрШаблон(НСтр("ru='%1 уже подписан ЭП';uk=' %1 вже підписаний ЕП'"), ПараметрКоманды);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ПараметрКоманды);
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.ПодписатьФайл(СвойстваФайла.ПрисоединенныйФайл, Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСвойстваПрисоединенногоФайла(ДокументРегистр)
	
	СвойстваФайла = Документы.РегистрУчета.ПолучитьСвойстваПрисоединенногоФайлаРегистра(ДокументРегистр, Истина);
	
	СвойстваФайла.Вставить("ИспользоватьЭП", ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи"));
	
	Возврат СвойстваФайла;
	
КонецФункции

#КонецОбласти
