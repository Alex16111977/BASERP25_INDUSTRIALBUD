#Если Не ТолстыйКлиентУправляемоеПриложение Или Сервер Тогда

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПодтверждениеВыплатЗаСчетФСС;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Уведомление";
	КомандаПечати.Представление = НСтр("ru='Уведомление';uk='Повідомлення'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Уведомление") Тогда
		ТабличныйДокумент = ТабличныйДокументУведомление(МассивОбъектов[0], ОбъектыПечати);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"Уведомление", НСтр("ru='Уведомление о выплатах за счет ФСС';uk='Повідомлення про виплати за рахунок ФСС'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	

КонецПроцедуры

Функция ТабличныйДокументУведомление(МассивОбъектов, ОбъектыПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПодтверждениеВыплатЗаСчетФСС.ПФ_MXL_UK_Уведомление");
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	//Шапка 
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Организация = МассивОбъектов.Организация.НаименованиеПолное;
	ОбластьШапка.Параметры.КодПоЕДРПОУ = МассивОбъектов.КодПоЕДРПОУ;
	
	 Если Не ПустаяСтрока(МассивОбъектов.АдресОрганизации) Тогда
		 СтруктураАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(
		 МассивОбъектов.АдресОрганизации,
		 ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));
		 УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеАдреса(СтруктураАдреса, ОбластьШапка.Параметры.Адрес);
	 Иначе
		 ОбластьШапка.Параметры.Адрес = НСтр("ru='<Не заполнен>';uk='<Не заповнена>'");
	 КонецЕсли;
	 
	 Если Не ПустаяСтрока(МассивОбъектов.ТелефонОрганизации) Тогда
		СтруктураТелефона=ЗарплатаКадры.СтруктураТелефонаИзXML(
		МассивОбъектов.ТелефонОрганизации,
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации"));
		ОбластьШапка.Параметры.Телефон = УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеТелефона(СтруктураТелефона.КодСтраны, 
																					  СтруктураТелефона.КодГорода, 
																					  СтруктураТелефона.НомерТелефона, 
																					  СтруктураТелефона.Добавочный, 
																					  СтруктураТелефона.Комментарий);
	Иначе
		ОбластьШапка.Параметры.Телефон = НСтр("ru='<Не заполнен>';uk='<Не заповнений>'");
	КонецЕсли;
	 
	ТабДокумент.Вывести(ОбластьШапка);
	
	//Заголовок
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
    ТабДокумент.Вывести(ОбластьЗаголовок);	
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	НомерСтроки = 1;
	
	РезультатЗапросаПоСтрокам = ПолучитьЗапросДляПечатиВыплаты(МассивОбъектов.Ссылка);
	Для Каждого Строка ИЗ РезультатЗапросаПоСтрокам Цикл
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ОбластьСтрока.Параметры.Фамилия	= Строка.Фамилия;
		ОбластьСтрока.Параметры.Имя	= Строка.Имя;
		ОбластьСтрока.Параметры.Отчество = Строка.Отчество;
		ОбластьСтрока.Параметры.КодПоДРФО = Строка.КодПоДРФО;
		Если Строка.СвидетельствоСерия <> "" ИЛИ Строка.СвидетельствоНомер <> "" Тогда
			ОбластьСтрока.Параметры.Серия = Строка.СвидетельствоСерия;
			ОбластьСтрока.Параметры.Номер = Строка.СвидетельствоНомер;
		Иначе	
			СерияНомер = УчетПособийСоциальногоСтрахования.РазложитьНомерБольничного(Строка.НомерБольничного);
			ОбластьСтрока.Параметры.Серия = СерияНомер.Серия;
			ОбластьСтрока.Параметры.Номер = СерияНомер.Номер;
		КонецЕсли;	
		ОбластьСтрока.Параметры.Сумма = Формат(Строка.Сумма,"ЧЦ=15; ЧДЦ=2; ЧН=; ЧГ=");
		ОбластьСтрока.Параметры.ДатаВыплаты = Формат(Строка.ДатаВыплаты,"ДФ=dd.MM.yyyy");
		ТабДокумент.Присоединить(ОбластьСтрока);
		НомерСтроки = НомерСтроки +1;
	КонецЦикла;

	ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
	
	СписокФизическихЛиц = Новый СписокЗначений;
	СписокФизическихЛиц.Добавить(МассивОбъектов.Руководитель);
	СписокФизическихЛиц.Добавить(МассивОбъектов.ГлавныйБухгалтер);
	ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество,", 
		МассивОбъектов.Дата);
	Руководитель = ТаблицаКадровыеДанныеФизическихЛиц.Найти(МассивОбъектов.Руководитель,"ФизиЧескоеЛицо");
	Если Руководитель <> Неопределено Тогда
		ОбластьПодпись.Параметры.ФИОРуководителя = Руководитель.Фамилия + " " + Руководитель.Имя + " " + Руководитель.Отчество;
	КонецЕсли;
	Бухгалтер = ТаблицаКадровыеДанныеФизическихЛиц.Найти(МассивОбъектов.ГлавныйБухгалтер,"ФизиЧескоеЛицо");
	Если Руководитель <> Неопределено Тогда
		ОбластьПодпись.Параметры.ФИОБухгалтера = Бухгалтер.Фамилия + " " + Бухгалтер.Имя + " " + Бухгалтер.Отчество;
	КонецЕсли;
	
	ОбластьПодпись.Параметры.ДатаДокумента = Формат(МассивОбъектов.Дата, "Л=uk; ДЛФ=DD");
	
	ТабДокумент.Вывести(ОбластьПодпись);
	
	Возврат ТабДокумент;
	
КонецФункции	

Функция ПолучитьЗапросДляПечатиВыплаты(Документ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВЫБОР
		|		КОГДА ПодтверждениеВыплатЗаСчетФСС.ПособиеСтороннемуЛицу
		|   		ТОГДА ПодтверждениеВыплатЗаСчетФСС.СтороннееЛицо.КодПоДРФО
		|   		ИНАЧЕ ПодтверждениеВыплатЗаСчетФСС.Сотрудник.ФизическоеЛицо.КодПоДРФО
		|	КОНЕЦ КАК КодПоДРФО,
		|	ЕстьNULL(БольничныйЛист.НомерЛисткаНетрудоспособности,"""") КАК НомерБольничного,
		|	ЕстьNULL(ЕдиновременноеПособиеЗаСчетФСС.СвидетельствоСерия,"""") КАК СвидетельствоСерия,
		|	ЕстьNULL(ЕдиновременноеПособиеЗаСчетФСС.СвидетельствоНомер,"""") КАК СвидетельствоНомер,
		|	ПодтверждениеВыплатЗаСчетФСС.Сумма,
		|	ПодтверждениеВыплатЗаСчетФСС.ДатаВыплаты,
        |	ВЫБОР
		|		КОГДА ПодтверждениеВыплатЗаСчетФСС.ПособиеСтороннемуЛицу
		|   		ТОГДА ПодтверждениеВыплатЗаСчетФСС.СтороннееЛицо
		|   		ИНАЧЕ ПодтверждениеВыплатЗаСчетФСС.Сотрудник.ФизическоеЛицо
		|	КОНЕЦ КАК СотрудникФизическоеЛицо
		|ПОМЕСТИТЬ ВыборкаПоДокументу
		|ИЗ
		|	Документ.ПодтверждениеВыплатЗаСчетФСС.Выплаты КАК ПодтверждениеВыплатЗаСчетФСС
		|   ЛЕВОЕ СОЕДИНЕНИЕ
		|   Документ.БольничныйЛист КАК БольничныйЛист
		|   ПО БольничныйЛист.Ссылка = ПодтверждениеВыплатЗаСчетФСС.ДокументОснование
		|   ЛЕВОЕ СОЕДИНЕНИЕ
		|   Документ.ЕдиновременноеПособиеЗаСчетФСС КАК ЕдиновременноеПособиеЗаСчетФСС
		|   ПО ЕдиновременноеПособиеЗаСчетФСС.Ссылка = ПодтверждениеВыплатЗаСчетФСС.ДокументОснование
		|ГДЕ
		|	ПодтверждениеВыплатЗаСчетФСС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыборкаПоДокументу.НомерБольничного,
		|	ВыборкаПоДокументу.СвидетельствоСерия,
		|	ВыборкаПоДокументу.СвидетельствоНомер,
		|	ВыборкаПоДокументу.КодПоДРФО,
		|	ВыборкаПоДокументу.Сумма,
		|	ВыборкаПоДокументу.ДатаВыплаты,
		|	ФИОФизическихЛицСрезПоследних.Фамилия,
		|	ФИОФизическихЛицСрезПоследних.Имя,
		|	ФИОФизическихЛицСрезПоследних.Отчество
		|ИЗ
		|	ВыборкаПоДокументу КАК ВыборкаПоДокументу
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&Дата, ) КАК ФИОФизическихЛицСрезПоследних
		|		ПО ВыборкаПоДокументу.СотрудникФизическоеЛицо = ФИОФизическихЛицСрезПоследних.ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("Дата", Документ.Дата);
	Запрос.УстановитьПараметр("Ссылка", Документ);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	
	

#КонецОбласти


#КонецЕсли
