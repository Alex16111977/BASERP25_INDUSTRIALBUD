#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет условия закупок
//
// Параметры:
//	УсловияЗакупок - Структура - Структура для заполнения.
//
Процедура ЗаполнитьУсловияЗакупок(Знач УсловияЗакупок) Экспорт
	
	Если УсловияЗакупок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Валюта = УсловияЗакупок.Валюта;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Организация)
		И УсловияЗакупок.Организация <> Организация Тогда
		
		Организация = УсловияЗакупок.Организация;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Склад) Тогда
		Склад = УсловияЗакупок.Склад;
	КонецЕсли;
	
	НаправлениеДеятельности = УсловияЗакупок.НаправлениеДеятельности;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Контрагент)
		И УсловияЗакупок.Контрагент <> Контрагент Тогда
		
		Контрагент = УсловияЗакупок.Контрагент;
		
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	
	Если ЗначениеЗаполнено(УсловияЗакупок.ХозяйственнаяОперация) Тогда
		
		ОперацияПоСоглашению		= ХозяйственнаяОперация;
		ОперацииРаздельнойЗакупки	= ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
										УсловияЗакупок.ХозяйственнаяОперация);
		
		Если ОперацииРаздельнойЗакупки.Найти(ХозяйственнаяОперация) = Неопределено Тогда
			
			Если УсловияЗакупок.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика Тогда
				
				Если ОперацииРаздельнойЗакупки.Найти(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка) <> Неопределено Тогда
					ОперацияПоСоглашению = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка;
				ИначеЕсли ОперацииРаздельнойЗакупки.Найти(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути) <> Неопределено Тогда
					ОперацияПоСоглашению = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути;
				КонецЕсли;
				
			ИначеЕсли УсловияЗакупок.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
				
				Если ОперацииРаздельнойЗакупки.Найти(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути) <> Неопределено Тогда
					ОперацияПоСоглашению = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		ХозяйственнаяОперация = ОперацияПоСоглашению;
		
	КонецЕсли;
	
	Если УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов <> Неопределено
		И УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
		
		ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки();
		ХозяйственнаяОперацияДоговора = ПарыОперацийРаздельнойЗакупки.Получить(ХозяйственнаяОперация);
		
		ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
		ДопПараметры.ВалютаВзаиморасчетов = Валюта;
		ДопПараметры.ВариантОформленияЗакупок = ВариантыОформленияЗакупок();
		
		Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ЭтотОбъект, ХозяйственнаяОперацияДоговора, ДопПараметры);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности") Тогда
			НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(НаправлениеДеятельности, Соглашение, Договор);
		КонецЕсли;
		
	КонецЕсли;
	
	ЦенаВключаетНДС   = УсловияЗакупок.ЦенаВключаетНДС;
	ВидЦеныПоставщика = УсловияЗакупок.ВидЦеныПоставщика;
	
КонецПроцедуры

// Заполняет условия закупок по торговому соглашению с поставщиком
//
// Параметры:
//	ПересчитатьЦены - Булево - Истина, если необходимо пересчитать цены в табличной части документа.
//
Процедура ЗаполнитьУсловияЗакупокПоУмолчанию(ПересчитатьЦены = Истина) Экспорт
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		
		ХозяйственныеОперации = Новый Массив;
		Хозяйственныеоперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
			ХозяйственныеОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
		КонецЕсли;
		
		ПараметрыОтбораСоглашения = Новый Структура;
		ПараметрыОтбораСоглашения.Вставить("ВыбранноеСоглашение",                     Соглашение);
		ПараметрыОтбораСоглашения.Вставить("ХозяйственныеОперации",                   ХозяйственныеОперации);
		ПараметрыОтбораСоглашения.Вставить("УчитыватьГруппыСкладов",                  Истина);
		ПараметрыОтбораСоглашения.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Истина);
		
		УсловияЗакупокПоУмолчанию = ЗакупкиСервер.ПолучитьУсловияЗакупокПоУмолчанию(Партнер, ПараметрыОтбораСоглашения);
		
		ЦеныЗаполнены = Ложь;
		
		Если УсловияЗакупокПоУмолчанию <> Неопределено Тогда
			
			Если Соглашение <> УсловияЗакупокПоУмолчанию.Соглашение
				И ЗначениеЗаполнено(УсловияЗакупокПоУмолчанию.Соглашение) Тогда
				
				Соглашение = УсловияЗакупокПоУмолчанию.Соглашение;
				ЗаполнитьУсловияЗакупок(УсловияЗакупокПоУмолчанию);
				
				Если ПересчитатьЦены И ЗначениеЗаполнено(Соглашение) Тогда
					
					СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
					
					ПараметрыЗаполнения = Новый Структура;
					ПараметрыЗаполнения.Вставить("ПоляЗаполнения",     "Цена, СтавкаНДС");
					ПараметрыЗаполнения.Вставить("Дата",               Дата);
					ПараметрыЗаполнения.Вставить("Валюта",             Валюта);
					ПараметрыЗаполнения.Вставить("Соглашение",         Соглашение);
					ПараметрыЗаполнения.Вставить("ВидЦеныПоставщика",  ВидЦеныПоставщика);
                    ПараметрыЗаполнения.Вставить("НалогообложениеНДС", НалогообложениеНДСПоУмолчанию());
					
					ДействияСоСтроками = Новый Структура;
					ДействияСоСтроками.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
					ДействияСоСтроками.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
					ДействияСоСтроками.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
					
					ЦеныЗаполнены = ЗакупкиСервер.ЗаполнитьЦены(Товары, Неопределено, ПараметрыЗаполнения, ДействияСоСтроками);
					
				КонецЕсли;
				
			Иначе
				ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
				Соглашение = УсловияЗакупокПоУмолчанию.Соглашение;
			КонецЕсли;
			
		Иначе
			
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
			Соглашение = Неопределено;
			
			ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
			ДопПараметры.ВалютаВзаиморасчетов = Валюта;
			ДопПараметры.ВариантОформленияЗакупок = ВариантыОформленияЗакупок();
			Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ЭтотОбъект, ХозяйственнаяОперация, ДопПараметры);
			
			ХозяйственнаяОперация = ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупки("Документ.ПоступлениеТоваровНаСклад",
																						Соглашение,
																						Договор)[0];
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ЗатратыСервер.ПроверитьИспользованиеПартионногоУчета22(ЭтотОбъект, Дата, Отказ);
	
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);
	
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
												НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПоступлениеТоваровНаСклад),
												Отказ,
												МассивНепроверяемыхРеквизитов);
	
	ХозяйственныеОперацииИмпорта = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
		Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.НомерГТД");
	Если (ПолучитьФункциональнуюОпцию("ЗапретитьПоступлениеТоваровБезНомеровГТД")
			Или ПолучитьФункциональнуюОпцию("ПроверятьЗаполнениеНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути"))
		И ХозяйственныеОперацииИмпорта.Найти(ХозяйственнаяОперация) <> Неопределено Тогда
		ЗапасыСервер.ПроверитьЗаполнениеНомеровГТД(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		МассивНепроверяемыхРеквизитов.Добавить("ВидЦеныПоставщика");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДС");
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не Отказ И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ЗакупкиСервер.ПроверитьКорректностьЗаполненияДокументаЗакупки(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровНаСкладЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Перем СкладПоступления;
	Перем ХозяйственнаяОперацияПриемки;
	Перем ЗаполнятьПоНакладной;
	Перем ЗаполнятьПоОрдеру;
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			
			ДанныеЗаполнения.Свойство("СкладПоступления",             СкладПоступления);
			ДанныеЗаполнения.Свойство("ХозяйственнаяОперацияПриемки", ХозяйственнаяОперацияПриемки);
			ДанныеЗаполнения.Свойство("ЗаполнятьПоНакладной",         ЗаполнятьПоНакладной);
			ДанныеЗаполнения.Свойство("ЗаполнятьПоОрдеру",            ЗаполнятьПоОрдеру);
		
			Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				
				ЗаполнитьДокументНаОснованииЗаказаПоставщику(ДанныеЗаполнения.ДокументОснование,
															СкладПоступления,
															ХозяйственнаяОперацияПриемки,
															ЗаполнятьПоНакладной,
															ЗаполнятьПоОрдеру);
				
			КонецЕсли;
			
			Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("СправочникСсылка.СоглашенияСПоставщиками") Тогда
				
				ЗаполнитьДокументНаОснованииСоглашенияСПоставщиком(ДанныеЗаполнения.ДокументОснование,
																	СкладПоступления,
																	ХозяйственнаяОперацияПриемки,
																	ЗаполнятьПоНакладной);
				
			КонецЕсли;
			
			Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				
				ЗаполнитьДокументНаОснованииДоговора(ДанныеЗаполнения.ДокументОснование,
													СкладПоступления,
													ХозяйственнаяОперацияПриемки,
													ЗаполнятьПоНакладной,
													ЗаполнятьПоОрдеру);
				
			КонецЕсли;
			
			Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
				
				ЗаполнитьДокументНаОснованииПриобретенияТоваров(ДанныеЗаполнения.ДокументОснование,
																СкладПоступления,
																ХозяйственнаяОперацияПриемки,
																ЗаполнятьПоНакладной);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ПоступлениеТоваровНаСкладЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ХозяйственныеОперацииИмпорта     = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
											Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути Тогда
		
		Для Каждого Строка Из Товары Цикл
			Строка.Цена      = 0;
			Строка.Сумма     = 0;
			Строка.СуммаНДС  = 0;
			Строка.СуммаСНДС = 0;
		КонецЦикла;
		
		ВидЦеныПоставщика = Справочники.ВидыЦенПоставщиков.ПустаяСсылка();
		Валюта = Справочники.Валюты.ПустаяСсылка();
		
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект,
		Документы.ПоступлениеТоваровНаСклад);
	
	ОбщегоНазначенияУТ.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
    
    УчетНДСУП.ЗаполнитьНалоговыеНазначенияВТабличныхЧастяхПередЗаписьюДокументаПоступление(
    	ЭтотОбъект,
    	"Товары"
    );
	
	СуммаДокумента = Товары.Итог("СуммаСНДС");
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ОсновнаяХозОперация = ЗакупкиСервер.ОсновнаяХозяйственнаяОперацияРаздельнойЗакупки(ХозяйственнаяОперация);
		
		МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(
			ОсновнаяХозОперация,
			Склад,
			Подразделение,
			Партнер,
			Договор);
			
		ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
		ИменаПолей.СтатусУказанияСерий = "СтатусУказанияСерийНаСкладах";
		
		РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(
			Товары,
			МестаУчета,
			ИменаПолей);
			
		МестаУчетаУПартнеров = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(
			ХозяйственнаяОперация,
			Договор,
			Подразделение,
			Партнер,
			Договор);
			
		ИменаПолей.АналитикаУчетаНоменклатуры = "АналитикаУчетаНоменклатурыТоварыУПартнеров";
		ИменаПолей.СтатусУказанияСерий        = "СтатусУказанияСерийТоварыУПартнеров";
		
		РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(
			Товары,
			МестаУчетаУПартнеров,
			ИменаПолей);
			
		ЗаполнитьВидыЗапасов(Отказ);
		ЗакупкиСервер.СвязатьНоменклатуруСНоменклатуройПоставщика(Товары, Отказ);
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ВидыЗапасов.Очистить();
	КонецЕсли;
	
	ПоступлениеТоваровНаСкладЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов());
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ВнутреннееТовародвижение.ЗаписатьПодчиненныеНаборамЗаписейДанные(ЭтотОбъект, Отказ);
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровНаСкладЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов());
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ВнутреннееТовародвижение.ЗаписатьПодчиненныеНаборамЗаписейДанные(ЭтотОбъект, Отказ);
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровНаСкладЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ДатаВходящегоДокумента = Дата(1,1,1);
	НомерВходящегоДокумента = "";
	
	Для Каждого ТекСтрока Из Товары Цикл
		
		ТекСтрока.АналитикаУчетаПартий = Справочники.КлючиАналитикиУчетаПартий.ПустаяСсылка();

	КонецЦикла;
	
	УчетНДСУП.СкорректироватьСтавкуНДСВТЧДокумента(ЭтотОбъект, Товары);
	
	Серии.Очистить();
	
	ИнициализироватьДокумент();
	
	ПоступлениеТоваровНаСкладЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровНаСкладЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Менеджер    = Пользователи.ТекущийПользователь();
	Валюта      = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Валюта);
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Склад       = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад);
	
	Распоряжение = ДокументОснованиеПриЗаполнении(ДанныеЗаполнения);
	ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров(Распоряжение, Договор);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииСоглашенияСПоставщиком(Знач ДокументОснование, Знач СкладПоступления = Неопределено, Знач ХозяйственнаяОперацияПриемки = Неопределено, Знач ЗаполнятьПоНакладной = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоглашениеСПоставщиком.Ссылка					КАК Ссылка,
	|	СоглашениеСПоставщиком.Статус					КАК СтатусДокумента,
	|	СоглашениеСПоставщиком.Партнер					КАК Партнер,
	|	СоглашениеСПоставщиком.Контрагент				КАК Контрагент,
	|	СоглашениеСПоставщиком.Ссылка					КАК Соглашение,
	|	СоглашениеСПоставщиком.Организация				КАК Организация,
	|	СоглашениеСПоставщиком.Валюта					КАК Валюта,
	|	СоглашениеСПоставщиком.ВидЦеныПоставщика		КАК ВидЦены,
	|	СоглашениеСПоставщиком.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	СоглашениеСПоставщиком.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА СоглашениеСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ											КАК ЭтоАгентскиеУслуги,
	|	ВЫБОР
	|		КОГДА СоглашениеСПоставщиком.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ											КАК ЕстьОшибкиСтатус
	|ПОМЕСТИТЬ ВТРаспоряжение
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
	|ГДЕ
	|	СоглашениеСПоставщиком.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКПоступлениюОстатки.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлениюОстатки.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлениюОстатки.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлениюОстатки.Назначение			КАК Назначение,
	|	ТоварыКПоступлениюОстатки.Серия					КАК Серия,
	|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток	КАК Количество,
	|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток	КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|			И (НЕ &ОтборПоХозяйственнойОперации
	|				ИЛИ ХозяйственнаяОперация В(&ХозяйственнаяОперация))) КАК ТоварыКПоступлениюОстатки
	|ГДЕ
	|	НЕ &ЗаполнятьПоНакладной
	|	И ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлениюОстатки.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлениюОстатки.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлениюОстатки.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлениюОстатки.Назначение			КАК Назначение,
	|	ТоварыКПоступлениюОстатки.Серия					КАК Серия,
	|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток	КАК Количество,
	|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток	КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|			И (НЕ &ОтборПоХозяйственнойОперации
	|				ИЛИ ХозяйственнаяОперация В(&ХозяйственнаяОперация))) КАК ТоварыКПоступлениюОстатки
	|ГДЕ
	|	&ЗаполнятьПоНакладной
	|	И ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТОтборТоварыОрганизацийУПартнеров
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ТаблицаТовары
	|		ПО ТаблицаРаспоряжение.Ссылка = ТаблицаТовары.ДокументПоступления
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ТаблицаТовары.Номенклатура
	|			И АналитикаУчетаНоменклатуры.Характеристика = ТаблицаТовары.Характеристика
	|			И АналитикаУчетаНоменклатуры.Серия = ТаблицаТовары.Серия
	|			И АналитикаУчетаНоменклатуры.Назначение = ТаблицаТовары.Назначение
	|			И АналитикаУчетаНоменклатуры.Партнер = ТаблицаРаспоряжение.Партнер
	|			И АналитикаУчетаНоменклатуры.Контрагент = ТаблицаРаспоряжение.Контрагент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРаспоряжение.Ссылка					КАК Ссылка,
	|	ТаблицаРаспоряжение.Соглашение				КАК Соглашение,
	|	ТаблицаРаспоряжение.Партнер					КАК Партнер,
	|	ТаблицаРаспоряжение.Контрагент				КАК Контрагент,
	|	ТаблицаРаспоряжение.Организация				КАК Организация,
	|	ТаблицаРаспоряжение.СтатусДокумента			КАК СтатусДокумента,
	|	ТаблицаРаспоряжение.Валюта					КАК Валюта,
	|	ТаблицаРаспоряжение.ВидЦены					КАК ВидЦены,
	|	ТаблицаРаспоряжение.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	ТаблицаРаспоряжение.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ТаблицаРаспоряжение.ЭтоАгентскиеУслуги		КАК ЭтоАгентскиеУслуги,
	|	ТаблицаРаспоряжение.ЕстьОшибкиСтатус		КАК ЕстьОшибкиСтатус
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.ДокументПоступления	КАК ДокументПоступления,
	|	ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|	ТаблицаТовары.Характеристика		КАК Характеристика,
	|	ТаблицаТовары.Назначение			КАК Назначение,
	|	ТаблицаТовары.Серия					КАК Серия,
	|	ТаблицаТовары.Количество			КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок	КАК КоличествоУпаковок
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура	КАК Номенклатура,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика	КАК Характеристика,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия			КАК Серия,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение		КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД									КАК НомерГТД,
	|	ТоварыОрганизаций.КоличествоОстаток							КАК Передано
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			АналитикаУчетаНоменклатуры
	|				В (ВЫБРАТЬ
	|						ТаблицаОтбор.АналитикаУчетаНоменклатуры
	|					ИЗ
	|						ВТОтборТоварыОрганизацийУПартнеров КАК ТаблицаОтбор)) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоОстаток > 0";
		
	Запрос.УстановитьПараметр("ДокументОснование",				ДокументОснование);
	Запрос.УстановитьПараметр("СкладПоступления",				СкладПоступления);
	Запрос.УстановитьПараметр("СкладУказан",					ЗначениеЗаполнено(СкладПоступления));
	Запрос.УстановитьПараметр("ЗаполнятьПоНакладной",			ЗаполнятьПоНакладной = Истина);
	Запрос.УстановитьПараметр("ОтборПоХозяйственнойОперации",	ЗначениеЗаполнено(ХозяйственнаяОперацияПриемки));
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",			ХозяйственнаяОперацияПриемки);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка				= РезультатЗапроса[3].Выбрать();
	ТЗТовары			= РезультатЗапроса[4].Выгрузить();
	ТЗТоварыУПартнеров	= РезультатЗапроса[5].Выгрузить();
	
	Выборка.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив;
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыСоглашенийСПоставщиками.Действует);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(Выборка.Соглашение,
															Выборка.СтатусДокумента,
															,
															Выборка.ЕстьОшибкиСтатус,
															МассивДопустимыхСтатусов);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСоглашения(,Выборка.ЭтоАгентскиеУслуги);
	
	Распоряжение = ДокументОснование;
	
	ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки(Истина);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ПарыОперацийРаздельнойЗакупки.Получить(ХозяйственнаяОперацияПриемки);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ?(Не ЗначениеЗаполнено(ХозяйственнаяОперацияПриемкиФактическоеПоступление),
															ХозяйственнаяОперацияПриемки,
															ХозяйственнаяОперацияПриемкиФактическоеПоступление);
	
	ХозяйственнаяОперация = ХозяйственнаяОперацияПриемкиФактическоеПоступление;
	
	Если ЗначениеЗаполнено(СкладПоступления) Тогда
		Склад = СкладПоступления;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьНомераГТД(ТЗТовары, ТЗТоварыУПартнеров);
	
	Товары.Загрузить(ТЗТовары);
	
	ПересчитатьКоличествоУпаковокЗаполнитьСтавкуНДС();
	ЗаполнитьЦеныПоСоглашению();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииЗаказаПоставщику(ДокументОснование,
														СкладПоступления = Неопределено,
														ХозяйственнаяОперацияПриемки = Неопределено,
														ЗаполнятьПоНакладной = Ложь,
														ЗаполнятьПоОрдеру = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка					КАК Ссылка,
	|	ЗаказПоставщику.Соглашение				КАК Соглашение,
	|	ЗаказПоставщику.Партнер					КАК Партнер,
	|	ЗаказПоставщику.Контрагент				КАК Контрагент,
	|	ЗаказПоставщику.Организация				КАК Организация,
	|	ЗаказПоставщику.Статус					КАК СтатусДокумента,
	|	ЗаказПоставщику.Договор					КАК Договор,
	|	ЗаказПоставщику.Сделка					КАК Сделка,
	|	ЗаказПоставщику.Валюта					КАК Валюта,
	|	ЕСТЬNULL(Соглашения.ВидЦеныПоставщика, ЗНАЧЕНИЕ(Справочник.ВидыЦенПоставщиков.ПустаяСсылка)) КАК ВидЦеныПоставщика,
	|	ЗаказПоставщику.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	ЗаказПоставщику.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	НЕ ЗаказПоставщику.Проведен				КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщику.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.НеСогласован),
	|										ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Согласован))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК ЕстьОшибкиСтатус
	|ПОМЕСТИТЬ ВТРаспоряжение
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК Соглашения
	|		ПО ЗаказПоставщику.Соглашение = Соглашения.Ссылка
	|ГДЕ
	|	ЗаказПоставщику.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Договор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток КАК Количество,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	) КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнятьПоЗаказу
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток КАК Количество,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	) КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнятьПоНакладной
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток КАК Количество,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	) КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнятьПоОрдеру
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	УпаковкиТоваров.ДокументПоступления			КАК ДокументПоступления,
	|	УпаковкиТоваров.Номенклатура				КАК Номенклатура,
	|	УпаковкиТоваров.Характеристика				КАК Характеристика,
	|	УпаковкиТоваров.Назначение					КАК Назначение,
	|	УпаковкиТоваров.Серия						КАК Серия,
	|	УпаковкиТоваров.Упаковка					КАК Упаковка,
	|	&ТекстЗапросаКоэффициентУпаковки			КАК КоэффициентУпаковки,
	|	СУММА(УпаковкиТоваров.Количество)			КАК Количество,
	|	СУММА(УпаковкиТоваров.КоличествоУпаковок)	КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТУпаковкиТоваров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|		ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|		ТоварыКПоступлению.Назначение			КАК Назначение,
	|		ТоварыКПоступлению.Серия				КАК Серия,
	|		ТоварыЗаказа.Упаковка					КАК Упаковка,
	|		СУММА(ТоварыЗаказа.Количество)			КАК Количество,
	|		СУММА(ТоварыЗаказа.КоличествоУпаковок)	КАК КоличествоУпаковок
	|	ИЗ
	|		ВТТовары КАК ТоварыКПоступлению
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ТоварыЗаказа
	|			ПО ТоварыКПоступлению.ДокументПоступления = ТоварыЗаказа.Ссылка
	|				И ТоварыКПоступлению.Номенклатура = ТоварыЗаказа.Номенклатура
	|				И ТоварыКПоступлению.Характеристика = ТоварыЗаказа.Характеристика
	|				И ТоварыКПоступлению.Назначение = ТоварыЗаказа.Назначение
	|	ГДЕ
	|		НЕ ЕСТЬNULL(ТоварыЗаказа.Отменено, ЛОЖЬ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТоварыКПоступлению.ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура,
	|		ТоварыКПоступлению.Характеристика,
	|		ТоварыКПоступлению.Назначение,
	|		ТоварыКПоступлению.Серия,
	|		ТоварыЗаказа.Упаковка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|		ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|		ТоварыКПоступлению.Назначение			КАК Назначение,
	|		ТоварыКПоступлению.Серия				КАК Серия,
	|		ТоварыПоступления.Упаковка				КАК Упаковка,
	|		-СУММА(ТоварыПоступления.Количество)	КАК Количество,
	|		-СУММА(ТоварыПоступления.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ВТТовары КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад КАК ПоступлениеНаСклад
	|		ПО ТоварыКПоступлению.ДокументПоступления = ПоступлениеНаСклад.Распоряжение
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад.Товары КАК ТоварыПоступления
	|		ПО ПоступлениеНаСклад.Ссылка = ТоварыПоступления.Ссылка
	|			И ТоварыКПоступлению.Номенклатура = ТоварыПоступления.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ТоварыПоступления.Характеристика
	|			И ТоварыКПоступлению.Назначение = ТоварыПоступления.Назначение
	|	ГДЕ
	|		ЕСТЬNULL(ПоступлениеНаСклад.Проведен, ЛОЖЬ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТоварыКПоступлению.ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура,
	|		ТоварыКПоступлению.Характеристика,
	|		ТоварыКПоступлению.Назначение,
	|		ТоварыКПоступлению.Серия,
	|		ТоварыПоступления.Упаковка
	|	) КАК УпаковкиТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	УпаковкиТоваров.ДокументПоступления,
	|	УпаковкиТоваров.Номенклатура,
	|	УпаковкиТоваров.Характеристика,
	|	УпаковкиТоваров.Назначение,
	|	УпаковкиТоваров.Серия,
	|	УпаковкиТоваров.Упаковка
	|
	|ИМЕЮЩИЕ
	|	СУММА(УпаковкиТоваров.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.Упаковка				КАК Упаковка,
	|	ТоварыКПоступлению.КоэффициентУпаковки	КАК КоэффициентУпаковки,
	|	ТоварыКПоступлению.Количество			КАК Количество,
	|	ТоварыКПоступлению.КоличествоУпаковок	КАК КоличествоУпаковок,
	|	ТоварыЗаказа.Цена						КАК Цена,
	|	ТоварыЗаказа.СтавкаНДС					КАК СтавкаНДС
	|ПОМЕСТИТЬ ВТЦеныТоваров
	|ИЗ
	|	ВТУпаковкиТоваров КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ТоварыЗаказа
	|		ПО ТоварыКПоступлению.ДокументПоступления = ТоварыЗаказа.Ссылка
	|			И ТоварыКПоступлению.Номенклатура = ТоварыЗаказа.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ТоварыЗаказа.Характеристика
	|			И ТоварыКПоступлению.Назначение = ТоварыЗаказа.Назначение
	|			И ТоварыКПоступлению.Упаковка = ТоварыЗаказа.Упаковка
	|ГДЕ
	|	НЕ ЕСТЬNULL(ТоварыЗаказа.Отменено, ЛОЖЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления				КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура						КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика					КАК Характеристика,
	|	ТоварыКПоступлению.Назначение						КАК Назначение,
	|	ТоварыКПоступлению.Серия							КАК Серия,
	|	ТоварыКПоступлению.Упаковка							КАК Упаковка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТоварыКПоступлению.Цена)		КАК КоличествоЦен,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТоварыКПоступлению.СтавкаНДС)	КАК КоличествоСтавокНДС
	|ПОМЕСТИТЬ ВТЦеныТоваровГруппировка
	|ИЗ
	|	ВТЦеныТоваров КАК ТоварыКПоступлению
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКПоступлению.ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура,
	|	ТоварыКПоступлению.Характеристика,
	|	ТоварыКПоступлению.Назначение,
	|	ТоварыКПоступлению.Серия,
	|	ТоварыКПоступлению.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ
	|	АналитикаУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТОтборТоварыОрганизацийУПартнеров
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ТаблицаТовары
	|		ПО ТаблицаРаспоряжение.Ссылка = ТаблицаТовары.ДокументПоступления
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ТаблицаТовары.Номенклатура
	|			И АналитикаУчетаНоменклатуры.Характеристика = ТаблицаТовары.Характеристика
	|			И АналитикаУчетаНоменклатуры.Назначение = ТаблицаТовары.Назначение
	|			И АналитикаУчетаНоменклатуры.Серия = ТаблицаТовары.Серия
	|			И АналитикаУчетаНоменклатуры.Договор = ТаблицаРаспоряжение.Договор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ
	|	ТаблицаРаспоряжение.Ссылка					КАК Ссылка,
	|	ТаблицаРаспоряжение.Соглашение				КАК Соглашение,
	|	ТаблицаРаспоряжение.Партнер					КАК Партнер,
	|	ТаблицаРаспоряжение.Контрагент				КАК Контрагент,
	|	ТаблицаРаспоряжение.Организация				КАК Организация,
	|	ТаблицаРаспоряжение.СтатусДокумента			КАК СтатусДокумента,
	|	ТаблицаРаспоряжение.Договор					КАК Договор,
	|	ТаблицаРаспоряжение.Сделка					КАК Сделка,
	|	ТаблицаРаспоряжение.Валюта					КАК Валюта,
	|	ТаблицаРаспоряжение.ВидЦеныПоставщика		КАК ВидЦеныПоставщика,
	|	ТаблицаРаспоряжение.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	ТаблицаРаспоряжение.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ТаблицаРаспоряжение.ЕстьОшибкиПроведен		КАК ЕстьОшибкиПроведен,
	|	ТаблицаРаспоряжение.ЕстьОшибкиСтатус		КАК ЕстьОшибкиСтатус
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ
	|	ТаблицаТовары.ДокументПоступления	КАК ДокументПоступления,
	|	ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|	ТаблицаТовары.Характеристика		КАК Характеристика,
	|	ТаблицаТовары.Назначение			КАК Назначение,
	|	ТаблицаТовары.Серия					КАК Серия,
	|	ТаблицаТовары.Количество			КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок	КАК КоличествоУпаковок
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.Упаковка				КАК Упаковка,
	|	ТоварыКПоступлению.КоэффициентУпаковки	КАК КоэффициентУпаковки,
	|	ТоварыКПоступлению.Количество			КАК Количество,
	|	ТоварыКПоступлению.КоличествоУпаковок	КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА ЦеныТоваров.КоличествоЦен = 1
	|			ТОГДА ТоварыКПоступлению.Цена
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК Цена,
	|	ВЫБОР
	|		КОГДА ЦеныТоваров.КоличествоСтавокНДС = 1
	|			ТОГДА ТоварыКПоступлению.СтавкаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СтавкаНДС
	|ИЗ
	|	ВТЦеныТоваров КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦеныТоваровГруппировка ЦеныТоваров
	|		ПО ТоварыКПоступлению.ДокументПоступления = ЦеныТоваров.ДокументПоступления
	|			И ТоварыКПоступлению.Номенклатура = ЦеныТоваров.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ЦеныТоваров.Характеристика
	|			И ТоварыКПоступлению.Назначение = ЦеныТоваров.Назначение
	|			И ТоварыКПоступлению.Серия = ЦеныТоваров.Серия
	|			И ТоварыКПоступлению.Упаковка = ЦеныТоваров.Упаковка
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоэффициентУпаковки УБЫВ
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 9
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура	КАК Номенклатура,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика	КАК Характеристика,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия			КАК Серия,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение		КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД									КАК НомерГТД,
	|	ТоварыОрганизаций.КоличествоОстаток							КАК Передано
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			(АналитикаУчетаНоменклатуры) В
	|				(ВЫБРАТЬ
	|					ТаблицаОтбор.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВТОтборТоварыОрганизацийУПартнеров КАК ТаблицаОтбор)
	|	) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоОстаток > 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
								"&ТекстЗапросаКоэффициентУпаковки",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
									"УпаковкиТоваров.Упаковка",
									"УпаковкиТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("ДокументОснование",		ДокументОснование);
	Запрос.УстановитьПараметр("СкладПоступления",		СкладПоступления);
	Запрос.УстановитьПараметр("СкладУказан",			ЗначениеЗаполнено(СкладПоступления));
	Запрос.УстановитьПараметр("ЗаполнятьПоЗаказу",		Не (ЗаполнятьПоНакладной = Истина Или ЗаполнятьПоОрдеру = Истина));
	Запрос.УстановитьПараметр("ЗаполнятьПоНакладной",	ЗаполнятьПоНакладной = Истина);
	Запрос.УстановитьПараметр("ЗаполнятьПоОрдеру",		ЗаполнятьПоОрдеру = Истина);
	
	РезультатЗапроса	= Запрос.ВыполнитьПакет();
	Выборка				= РезультатЗапроса[6].Выбрать();
	ТЗТовары			= РезультатЗапроса[7].Выгрузить();
	ТЗУпаковки			= РезультатЗапроса[8].Выгрузить();
	ТЗТоварыУПартнеров	= РезультатЗапроса[9].Выгрузить();
	
	Выборка.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив;
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПоставщикам.Подтвержден);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПоставщикам.Закрыт);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ДокументОснование,
															Выборка.СтатусДокумента,
															Выборка.ЕстьОшибкиПроведен,
															Выборка.ЕстьОшибкиСтатус,
															МассивДопустимыхСтатусов);
	
	Распоряжение	= ДокументОснование;
	Склад			= СкладПоступления;
	
	ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки(Истина);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ПарыОперацийРаздельнойЗакупки.Получить(ХозяйственнаяОперацияПриемки);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ?(Не ЗначениеЗаполнено(ХозяйственнаяОперацияПриемкиФактическоеПоступление),
															ХозяйственнаяОперацияПриемки,
															ХозяйственнаяОперацияПриемкиФактическоеПоступление);
	
	ХозяйственнаяОперация = ХозяйственнаяОперацияПриемкиФактическоеПоступление;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьНомераГТД(ТЗТовары, ТЗТоварыУПартнеров);
	ЗаполнитьУпаковкиЦены(ТЗТовары, ТЗУпаковки);
	ПересчитатьКоличествоУпаковокЗаполнитьСтавкуНДС();
	ЗаполнитьЦеныПоСоглашению(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПриобретенияТоваров(Знач ДокументОснование, Знач СкладПоступления = Неопределено, Знач ХозяйственнаяОперацияПриемки = Неопределено, Знач ЗаполнятьПоНакладной = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриобретениеТоваров.Ссылка					КАК Ссылка,
	|	ПриобретениеТоваров.Соглашение				КАК Соглашение,
	|	ПриобретениеТоваров.Партнер					КАК Партнер,
	|	ПриобретениеТоваров.Контрагент				КАК Контрагент,
	|	ПриобретениеТоваров.Организация				КАК Организация,
	|	ПриобретениеТоваров.Договор					КАК Договор,
	|	ПриобретениеТоваров.Сделка					КАК Сделка,
	|	ПриобретениеТоваров.Валюта					КАК Валюта,
	|	ЕСТЬNULL(Соглашения.ВидЦеныПоставщика, ЗНАЧЕНИЕ(Справочник.ВидыЦенПоставщиков.ПустаяСсылка)) КАК ВидЦеныПоставщика,
	|	ПриобретениеТоваров.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	ПриобретениеТоваров.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	НЕ ПриобретениеТоваров.Проведен				КАК ЕстьОшибкиПроведен,
	|	ЛОЖЬ										КАК ЕстьОшибкиСтатус
	|ПОМЕСТИТЬ ВТРаспоряжение
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК Соглашения
	|		ПО ПриобретениеТоваров.Соглашение = Соглашения.Ссылка
	|ГДЕ
	|	ПриобретениеТоваров.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Договор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток КАК Количество,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	) КАК ТоварыКПоступлению
	|ГДЕ
	|	НЕ &ЗаполнятьПоНакладной
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток КАК Количество,
	|	ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &ДокументОснование
	|			И ВЫБОР
	|				КОГДА &СкладУказан
	|					ТОГДА Склад = &СкладПоступления
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	) КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнятьПоНакладной
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоНакладнымОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	УпаковкиТоваров.ДокументПоступления			КАК ДокументПоступления,
	|	УпаковкиТоваров.Номенклатура				КАК Номенклатура,
	|	УпаковкиТоваров.Характеристика				КАК Характеристика,
	|	УпаковкиТоваров.Назначение					КАК Назначение,
	|	УпаковкиТоваров.Серия						КАК Серия,
	|	УпаковкиТоваров.Упаковка					КАК Упаковка,
	|	&ТекстЗапросаКоэффициентУпаковки			КАК КоэффициентУпаковки,
	|	СУММА(УпаковкиТоваров.Количество)			КАК Количество,
	|	СУММА(УпаковкиТоваров.КоличествоУпаковок)	КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТУпаковкиТоваров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|		ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|		ТоварыКПоступлению.Назначение			КАК Назначение,
	|		ТоварыКПоступлению.Серия				КАК Серия,
	|		ТоварыПриобретения.Упаковка				КАК Упаковка,
	|		СУММА(ТоварыПриобретения.Количество)	КАК Количество,
	|		СУММА(ТоварыПриобретения.КоличествоУпаковок) КАК КоличествоУпаковок
	|	ИЗ
	|		ВТТовары КАК ТоварыКПоступлению
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыПриобретения
	|			ПО ТоварыКПоступлению.ДокументПоступления = ТоварыПриобретения.Ссылка
	|				И ТоварыКПоступлению.Номенклатура = ТоварыПриобретения.Номенклатура
	|				И ТоварыКПоступлению.Характеристика = ТоварыПриобретения.Характеристика
	|				И ТоварыКПоступлению.Назначение = ТоварыПриобретения.Назначение
	|				И ВЫБОР
	|					КОГДА ТоварыПриобретения.СтатусУказанияСерий В(14, 18)
	|						ТОГДА ТоварыКПоступлению.Серия = ТоварыПриобретения.Серия
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТоварыКПоступлению.ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура,
	|		ТоварыКПоступлению.Характеристика,
	|		ТоварыКПоступлению.Назначение,
	|		ТоварыКПоступлению.Серия,
	|		ТоварыПриобретения.Упаковка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|		ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|		ТоварыКПоступлению.Назначение			КАК Назначение,
	|		ТоварыКПоступлению.Серия				КАК Серия,
	|		ТоварыПоступления.Упаковка				КАК Упаковка,
	|		-СУММА(ТоварыПоступления.Количество)	КАК Количество,
	|		-СУММА(ТоварыПоступления.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ВТТовары КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад КАК ПоступлениеНаСклад
	|		ПО ТоварыКПоступлению.ДокументПоступления = ПоступлениеНаСклад.Распоряжение
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад.Товары КАК ТоварыПоступления
	|		ПО ПоступлениеНаСклад.Ссылка = ТоварыПоступления.Ссылка
	|			И ТоварыКПоступлению.Номенклатура = ТоварыПоступления.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ТоварыПоступления.Характеристика
	|			И ТоварыКПоступлению.Назначение = ТоварыПоступления.Назначение
	|				И ВЫБОР
	|					КОГДА ТоварыПоступления.СтатусУказанияСерий В(14, 18)
	|						ТОГДА ТоварыКПоступлению.Серия = ТоварыПоступления.Серия
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|	ГДЕ
	|		ЕСТЬNULL(ПоступлениеНаСклад.Проведен, ЛОЖЬ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТоварыКПоступлению.ДокументПоступления,
	|		ТоварыКПоступлению.Номенклатура,
	|		ТоварыКПоступлению.Характеристика,
	|		ТоварыКПоступлению.Назначение,
	|		ТоварыКПоступлению.Серия,
	|		ТоварыПоступления.Упаковка
	|	) КАК УпаковкиТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	УпаковкиТоваров.ДокументПоступления,
	|	УпаковкиТоваров.Номенклатура,
	|	УпаковкиТоваров.Характеристика,
	|	УпаковкиТоваров.Назначение,
	|	УпаковкиТоваров.Серия,
	|	УпаковкиТоваров.Упаковка
	|
	|ИМЕЮЩИЕ
	|	СУММА(УпаковкиТоваров.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.Упаковка				КАК Упаковка,
	|	ТоварыКПоступлению.КоэффициентУпаковки	КАК КоэффициентУпаковки,
	|	ТоварыКПоступлению.Количество			КАК Количество,
	|	ТоварыКПоступлению.КоличествоУпаковок	КАК КоличествоУпаковок,
	|	ТоварыПриобретения.Цена					КАК Цена,
	|	ТоварыПриобретения.СтавкаНДС			КАК СтавкаНДС
	|ПОМЕСТИТЬ ВТЦеныТоваров
	|ИЗ
	|	ВТУпаковкиТоваров КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыПриобретения
	|		ПО ТоварыКПоступлению.ДокументПоступления = ТоварыПриобретения.Ссылка
	|			И ТоварыКПоступлению.Номенклатура = ТоварыПриобретения.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ТоварыПриобретения.Характеристика
	|			И ТоварыКПоступлению.Назначение = ТоварыПриобретения.Назначение
	|				И ВЫБОР
	|					КОГДА ТоварыПриобретения.СтатусУказанияСерий В(14, 18)
	|						ТОГДА ТоварыКПоступлению.Серия = ТоварыПриобретения.Серия
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|			И ТоварыКПоступлению.Упаковка = ТоварыПриобретения.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления				КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура						КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика					КАК Характеристика,
	|	ТоварыКПоступлению.Назначение						КАК Назначение,
	|	ТоварыКПоступлению.Серия							КАК Серия,
	|	ТоварыКПоступлению.Упаковка							КАК Упаковка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТоварыКПоступлению.Цена)		КАК КоличествоЦен,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТоварыКПоступлению.СтавкаНДС)	КАК КоличествоСтавокНДС
	|ПОМЕСТИТЬ ВТЦеныТоваровГруппировка
	|ИЗ
	|	ВТЦеныТоваров КАК ТоварыКПоступлению
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКПоступлению.ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура,
	|	ТоварыКПоступлению.Характеристика,
	|	ТоварыКПоступлению.Назначение,
	|	ТоварыКПоступлению.Серия,
	|	ТоварыКПоступлению.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Упаковка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ
	|	АналитикаУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТОтборТоварыОрганизацийУПартнеров
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ТаблицаТовары
	|		ПО ТаблицаРаспоряжение.Ссылка = ТаблицаТовары.ДокументПоступления
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ТаблицаТовары.Номенклатура
	|			И АналитикаУчетаНоменклатуры.Характеристика = ТаблицаТовары.Характеристика
	|			И АналитикаУчетаНоменклатуры.Назначение = ТаблицаТовары.Назначение
	|			И АналитикаУчетаНоменклатуры.Серия = ТаблицаТовары.Серия
	|			И АналитикаУчетаНоменклатуры.Договор = ТаблицаРаспоряжение.Договор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ
	|	ТаблицаРаспоряжение.Ссылка					КАК Ссылка,
	|	ТаблицаРаспоряжение.Соглашение				КАК Соглашение,
	|	ТаблицаРаспоряжение.Партнер					КАК Партнер,
	|	ТаблицаРаспоряжение.Контрагент				КАК Контрагент,
	|	ТаблицаРаспоряжение.Организация				КАК Организация,
	|	ТаблицаРаспоряжение.Договор					КАК Договор,
	|	ТаблицаРаспоряжение.Сделка					КАК Сделка,
	|	ТаблицаРаспоряжение.Валюта					КАК Валюта,
	|	ТаблицаРаспоряжение.ВидЦеныПоставщика		КАК ВидЦеныПоставщика,
	|	ТаблицаРаспоряжение.ЦенаВключаетНДС			КАК ЦенаВключаетНДС,
	|	ТаблицаРаспоряжение.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ТаблицаРаспоряжение.ЕстьОшибкиПроведен		КАК ЕстьОшибкиПроведен,
	|	ТаблицаРаспоряжение.ЕстьОшибкиСтатус		КАК ЕстьОшибкиСтатус
	|ИЗ
	|	ВТРаспоряжение КАК ТаблицаРаспоряжение
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ
	|	ТаблицаТовары.ДокументПоступления	КАК ДокументПоступления,
	|	ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|	ТаблицаТовары.Характеристика		КАК Характеристика,
	|	ТаблицаТовары.Назначение			КАК Назначение,
	|	ТаблицаТовары.Серия					КАК Серия,
	|	ТаблицаТовары.Количество			КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок	КАК КоличествоУпаковок
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыКПоступлению.ДокументПоступления	КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика		КАК Характеристика,
	|	ТоварыКПоступлению.Назначение			КАК Назначение,
	|	ТоварыКПоступлению.Серия				КАК Серия,
	|	ТоварыКПоступлению.Упаковка				КАК Упаковка,
	|	ТоварыКПоступлению.КоэффициентУпаковки	КАК КоэффициентУпаковки,
	|	ТоварыКПоступлению.Количество			КАК Количество,
	|	ТоварыКПоступлению.КоличествоУпаковок	КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА ЦеныТоваров.КоличествоЦен = 1
	|			ТОГДА ТоварыКПоступлению.Цена
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК Цена,
	|	ВЫБОР
	|		КОГДА ЦеныТоваров.КоличествоСтавокНДС = 1
	|			ТОГДА ТоварыКПоступлению.СтавкаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СтавкаНДС
	|ИЗ
	|	ВТЦеныТоваров КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦеныТоваровГруппировка ЦеныТоваров
	|		ПО ТоварыКПоступлению.ДокументПоступления = ЦеныТоваров.ДокументПоступления
	|			И ТоварыКПоступлению.Номенклатура = ЦеныТоваров.Номенклатура
	|			И ТоварыКПоступлению.Характеристика = ЦеныТоваров.Характеристика
	|			И ТоварыКПоступлению.Назначение = ЦеныТоваров.Назначение
	|			И ТоварыКПоступлению.Серия = ЦеныТоваров.Серия
	|			И ТоварыКПоступлению.Упаковка = ЦеныТоваров.Упаковка
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоэффициентУпаковки УБЫВ
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 9
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура	КАК Номенклатура,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика	КАК Характеристика,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия			КАК Серия,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение		КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД									КАК НомерГТД,
	|	ТоварыОрганизаций.КоличествоОстаток							КАК Передано
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			(АналитикаУчетаНоменклатуры) В
	|				(ВЫБРАТЬ
	|					ТаблицаОтбор.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВТОтборТоварыОрганизацийУПартнеров КАК ТаблицаОтбор)
	|	) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоОстаток > 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
								"&ТекстЗапросаКоэффициентУпаковки",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
									"УпаковкиТоваров.Упаковка",
									"УпаковкиТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("ДокументОснование",		ДокументОснование);
	Запрос.УстановитьПараметр("СкладПоступления",		СкладПоступления);
	Запрос.УстановитьПараметр("СкладУказан",			ЗначениеЗаполнено(СкладПоступления));
	Запрос.УстановитьПараметр("ЗаполнятьПоНакладной",	ЗаполнятьПоНакладной = Истина);
	
	РезультатЗапроса	= Запрос.ВыполнитьПакет();
	Выборка				= РезультатЗапроса[6].Выбрать();
	ТЗТовары			= РезультатЗапроса[7].Выгрузить();
	ТЗУпаковки			= РезультатЗапроса[8].Выгрузить();
	ТЗТоварыУПартнеров	= РезультатЗапроса[9].Выгрузить();
	
	Выборка.Следующий();
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ДокументОснование,
															Неопределено,
															Выборка.ЕстьОшибкиПроведен,
															Выборка.ЕстьОшибкиСтатус);
	
	Распоряжение	= ДокументОснование;
	Склад			= СкладПоступления;
	
	ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки(Истина);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ПарыОперацийРаздельнойЗакупки.Получить(ХозяйственнаяОперацияПриемки);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ?(Не ЗначениеЗаполнено(ХозяйственнаяОперацияПриемкиФактическоеПоступление),
															ХозяйственнаяОперацияПриемки,
															ХозяйственнаяОперацияПриемкиФактическоеПоступление);
	
	ХозяйственнаяОперация = ХозяйственнаяОперацияПриемкиФактическоеПоступление;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьНомераГТД(ТЗТовары, ТЗТоварыУПартнеров);
	ЗаполнитьУпаковкиЦены(ТЗТовары, ТЗУпаковки);
	ПересчитатьКоличествоУпаковокЗаполнитьСтавкуНДС();
	ЗаполнитьЦеныПоСоглашению(Ложь);
	
КонецПроцедуры

// Заполняет номера ГТД в таблице ТЗТоварыУПартнеров, источником выступает ТЗТовары
// 
// Параметры:
// 	ТЗТовары - см. НакладныеСервер.РаспределитьКоличествоИЗаполнить.ТаблицаПриемник
// 	ТЗТоварыУПартнеров - см. НакладныеСервер.РаспределитьКоличествоИЗаполнить.ТаблицаИсточник
Процедура ЗаполнитьНомераГТД(ТЗТовары, ТЗТоварыУПартнеров)
	
	ТЗТовары.Колонки.Добавить("Передано", Новый ОписаниеТипов("Число"));
	ТЗТовары.Колонки.Добавить("НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НоменклатураГТД"));
	
	Ключ = "Номенклатура, Характеристика, Серия, Назначение";
	
	Условие = "ПО [Количество]";
	НакладныеСервер.РаспределитьКоличествоИЗаполнить(ТЗТоварыУПартнеров, ТЗТовары, "Передано", Ключ, Условие, Ложь, "НомерГТД");
	
КонецПроцедуры

// Параметры:
// 	ТЗТовары - ТаблицаЗначений - 
// 	ТЗУпаковки - ТаблицаЗначений - 
Процедура ЗаполнитьУпаковкиЦены(ТЗТовары, ТЗУпаковки)
	
	СвойстваУпаковок	= "Упаковка, Количество, КоличествоУпаковок, Цена, СтавкаНДС";
	СвойстваТоваров		= "ДокументПоступления, Номенклатура, Характеристика, Назначение";
	СвойстваПоиска		= Новый Структура(СвойстваТоваров);
	
	ТЗУпаковки.Индексы.Добавить(СвойстваТоваров);
	
	Для Каждого СтрокаТоваров Из ТЗТовары Цикл
		
		СтрокаТЧ = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТоваров);
		ЗаполнитьЗначенияСвойств(СвойстваПоиска, СтрокаТоваров);
		
		Количество			= СтрокаТоваров.Количество;
		УпаковкиТовара		= ТЗУпаковки.НайтиСтроки(СвойстваПоиска);
		УдаляемыеУпаковки	= Новый Массив;
		
		Для Каждого СтрокаУпаковок Из УпаковкиТовара Цикл
			
			Если Количество >= СтрокаУпаковок.Количество Тогда
				
				НоваяСтрокаТЧ = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ, , СвойстваУпаковок);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаУпаковок, СвойстваУпаковок);
				ЗаполнитьСуммовыеПоказателиСтрокиТоваров(НоваяСтрокаТЧ);
				
				Количество = Количество - СтрокаУпаковок.Количество;
				
				Если Количество > 0 Тогда
					СтрокаТЧ.Количество			= Количество;
					СтрокаТЧ.КоличествоУпаковок	= Количество;
					
					ЗаполнитьСуммовыеПоказателиСтрокиТоваров(СтрокаТЧ);
				Иначе
					Товары.Удалить(СтрокаТЧ);
				КонецЕсли;
				
				УдаляемыеУпаковки.Добавить(СтрокаУпаковок);
				
			КонецЕсли;
			
			Если Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого УдаляемаяСтрока Из УдаляемыеУпаковки Цикл
			ТЗУпаковки.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументОснованиеПриЗаполнении(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ДокументОснование")
		И (ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику")
			Или ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
			Или ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("СправочникСсылка.СоглашенияСПоставщиками")
			Или ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
		
		Возврат ДанныеЗаполнения.ДокументОснование;

	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьДокументНаОснованииДоговора(ДокументОснование,
												СкладПоступления = Неопределено,
												ХозяйственнаяОперацияПриемки = Неопределено,
												ЗаполнятьПоНакладной = Ложь,
												ЗаполнятьПоОрдеру = Ложь)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Договор.Ссылка КАК Ссылка,
		|	Договор.Ссылка КАК Договор,
		|	Договор.Партнер КАК Партнер,
		|	Договор.Контрагент КАК Контрагент,
		|	Договор.Организация КАК Организация,
		|	Договор.Статус КАК СтатусДокумента,
		|	ВЫБОР
		|		КОГДА Договор.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиСтатус,
		|	ВЫБОР
		|		КОГДА Договор.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоАгентскиеУслуги,
		|	Договор.НаправлениеДеятельности КАК НаправлениеДеятельности
		|ПОМЕСТИТЬ ВТРаспоряжение
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК Договор
		|ГДЕ
		|	Договор.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления,
		|	ТоварыКПоступлениюОстатки.Номенклатура,
		|	ТоварыКПоступлениюОстатки.Характеристика,
		|	ТоварыКПоступлениюОстатки.Назначение,
		|	ТоварыКПоступлениюОстатки.Серия,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток КАК Количество,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(,ДокументПоступления = &ДокументОснование 
		|		И ВЫБОР КОГДА &СкладУказан
		|			ТОГДА Склад = &СкладПоступления
		|			ИНАЧЕ ИСТИНА
		|	КОНЕЦ) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	НЕ &ЗаполнятьПоНакладной
		|	И НЕ &ЗаполнятьПоОрдеру
		|	И ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления,
		|	ТоварыКПоступлениюОстатки.Номенклатура,
		|	ТоварыКПоступлениюОстатки.Характеристика,
		|	ТоварыКПоступлениюОстатки.Назначение,
		|	ТоварыКПоступлениюОстатки.Серия,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоОрдерамОстаток КАК Количество,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоОрдерамОстаток КАК КоличествоУпаковок
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(,ДокументПоступления = &ДокументОснование 
		|		И ВЫБОР КОГДА &СкладУказан
		|			ТОГДА Склад = &СкладПоступления
		|			ИНАЧЕ ИСТИНА
		|	КОНЕЦ) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	НЕ &ЗаполнятьПоНакладной
		|	И &ЗаполнятьПоОрдеру
		|	И ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоОрдерамОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления,
		|	ТоварыКПоступлениюОстатки.Номенклатура,
		|	ТоварыКПоступлениюОстатки.Характеристика,
		|	ТоварыКПоступлениюОстатки.Назначение,
		|	ТоварыКПоступлениюОстатки.Серия,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток КАК Количество,
		|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток КАК КоличествоУпаковок
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(,ДокументПоступления = &ДокументОснование 
		|		И ВЫБОР КОГДА &СкладУказан
		|			ТОГДА Склад = &СкладПоступления
		|			ИНАЧЕ ИСТИНА
		|	КОНЕЦ) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	&ЗаполнятьПоНакладной
		|	И ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоНакладнымОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АналитикаУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ВТОтборТоварыОрганизацийУПартнеров
		|ИЗ
		|	ВТРаспоряжение КАК ТаблицаРаспоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ТаблицаТовары
		|	ПО ТаблицаРаспоряжение.Ссылка = ТаблицаТовары.ДокументПоступления
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|	ПО АналитикаУчетаНоменклатуры.Номенклатура = ТаблицаТовары.Номенклатура
		|	И АналитикаУчетаНоменклатуры.Характеристика = ТаблицаТовары.Характеристика
		|	И АналитикаУчетаНоменклатуры.Серия = ТаблицаТовары.Серия
		|	И АналитикаУчетаНоменклатуры.Назначение = ТаблицаТовары.Назначение
		|	И АналитикаУчетаНоменклатуры.Договор = ТаблицаРаспоряжение.Договор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРаспоряжение.Ссылка          КАК Ссылка,
		|	ТаблицаРаспоряжение.Договор         КАК Договор,
		|	ТаблицаРаспоряжение.Партнер         КАК Партнер,
		|	ТаблицаРаспоряжение.Контрагент      КАК Контрагент,
		|	ТаблицаРаспоряжение.Организация     КАК Организация,
		|	ТаблицаРаспоряжение.СтатусДокумента КАК СтатусДокумента,
		|	ТаблицаРаспоряжение.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ТаблицаРаспоряжение.ЭтоАгентскиеУслуги КАК ЭтоАгентскиеУслуги,
		|	ТаблицаРаспоряжение.ЕстьОшибкиСтатус   КАК ЕстьОшибкиСтатус
		|ИЗ
		|	ВТРаспоряжение КАК ТаблицаРаспоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТовары.ДокументПоступления КАК ДокументПоступления,
		|	ТаблицаТовары.Номенклатура        КАК Номенклатура,
		|	ТаблицаТовары.Характеристика      КАК Характеристика,
		|	ТаблицаТовары.Назначение          КАК Назначение,
		|	ТаблицаТовары.Серия               КАК Серия,
		|	ТаблицаТовары.Количество          КАК Количество,
		|	ТаблицаТовары.КоличествоУпаковок  КАК КоличествоУпаковок
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ТоварыОрганизаций.НомерГТД                                  КАК НомерГТД,
		|	ТоварыОрганизаций.КоличествоОстаток                         КАК Передано
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			,
		|			(АналитикаУчетаНоменклатуры)
		|				В (ВЫБРАТЬ
		|						ТаблицаОтбор.АналитикаУчетаНоменклатуры
		|					ИЗ
		|						ВТОтборТоварыОрганизацийУПартнеров КАК ТаблицаОтбор
		|				)) КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.КоличествоОстаток > 0");
		
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("СкладПоступления",  СкладПоступления);
	Запрос.УстановитьПараметр("СкладУказан",       ЗначениеЗаполнено(СкладПоступления));
	Запрос.УстановитьПараметр("ЗаполнятьПоНакладной", ЗаполнятьПоНакладной = Истина);
	Запрос.УстановитьПараметр("ЗаполнятьПоОрдеру", ЗаполнятьПоОрдеру = Истина);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка            = РезультатЗапроса[3].Выбрать();
	ТЗТовары           = РезультатЗапроса[4].Выгрузить();
	ТЗТоварыУПартнеров = РезультатЗапроса[5].Выгрузить();
	Выборка.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыДоговоровКонтрагентов.Действует);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		Выборка.Договор,
		Выборка.СтатусДокумента,
		,
		Выборка.ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);
	
	ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки(Истина);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ПарыОперацийРаздельнойЗакупки.Получить(ХозяйственнаяОперацияПриемки);
	ХозяйственнаяОперацияПриемкиФактическоеПоступление = ?(Не ЗначениеЗаполнено(ХозяйственнаяОперацияПриемкиФактическоеПоступление),
		ХозяйственнаяОперацияПриемки,
		ХозяйственнаяОперацияПриемкиФактическоеПоступление);
	
	Распоряжение          = ДокументОснование;
	ХозяйственнаяОперация = ХозяйственнаяОперацияПриемкиФактическоеПоступление;
	
	Если ЗначениеЗаполнено(СкладПоступления) Тогда
		Склад = СкладПоступления;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьНомераГТД(ТЗТовары, ТЗТоварыУПартнеров);
	
	Товары.Загрузить(ТЗТовары);
	
	ПересчитатьКоличествоУпаковокЗаполнитьСтавкуНДС(Истина);
	ЗаполнитьЦеныПоСоглашению();
	
КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

// Функция формирует временные таблицы с данными документа.
//  Используется для заполнения видов запасов.
//
// Параметры:
//  ДляТоваровУПартнеров - Булево - Подменяет аналитику учета номенклатуры
//  УчетПоНомерамГТД - Булево - Отбор номенклатуры с признаком Вести учет по номерам ГТД
// 
// Возвращаемое значение:
//  МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Функция ВременныеТаблицыДанныхДокумента(ДляТоваровУПартнеров, УчетПоНомерамГТД = Ложь)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	&Организация КАК Организация,
	|	&Склад КАК Склад,
	|	&МестоХранения КАК МестоХранения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Валюта КАК Валюта,
    |	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначениеОрганизации,
	|	ЛОЖЬ КАК ЕстьСделкиВТабличнойЧасти,
	|	&ТипЗапасов КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	&ТаблицаТоваровАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерийТоварыУПартнеров = 18
	|			ИЛИ (ТаблицаТоваров.СтатусУказанияСерийТоварыУПартнеров = 0
	|				И ТаблицаТоваров.СтатусУказанияСерийНаСкладах = 14)
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТаблицаТоваров.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаТоваров.НалоговоеНазначение КАК ЦелевоеНалоговоеНазначение,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Цена КАК Цена,
	|	ТаблицаТоваров.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	&Склад КАК Склад,
	|	ТаблицаТоваров.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаТоваров.ЦелевоеНалоговоеНазначение КАК ЦелевоеНалоговоеНазначение,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.Цена КАК Цена,
	|	ТаблицаТоваров.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаВознаграждения,
	|	0 КАК СуммаНДСВознаграждения,
	|	ИСТИНА КАК ПодбиратьВидыЗапасов,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	&ВидЦены КАК ВидЦены
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ВестиУчетПоГТД = &УчетПоНомерамГТД
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&ТаблицаВидыЗапасовАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаВидыЗапасов.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасовПолучателя,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаВидыЗапасов.СуммаНДС КАК СуммаНДС,
	|	ТаблицаВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК СуммаВознаграждения,
	|	0 КАК СуммаНДСВознаграждения,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	Аналитика.МестоХранения КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЛОЖЬ КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура.ВестиУчетПоГТД = &УчетПоНомерамГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &Проведен
	|			ТОГДА ТаблицаТоваров.ВидЗапасов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	КОНЕЦ КАК ТекущийВидЗапасов,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	&Организация КАК Организация,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
    |   ТаблицаТоваров.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаТоваров.ЦелевоеНалоговоеНазначение КАК ЦелевоеНалоговоеНазначение,
    |	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначениеОрганизации,
	|	НЕОПРЕДЕЛЕНО КАК ВладелецТовара,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЦенПоставщиков.ПустаяСсылка) КАК ВидЦены
	|ПОМЕСТИТЬ ИсходнаяТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ТаблицаТоваров.ВидЗапасов = ВидыЗапасов.Ссылка
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И (ТаблицаТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|			ИЛИ ВидыЗапасов.ТипЗапасов <> &ТипЗапасов
	|			ИЛИ ВидыЗапасов.Организация <> &Организация
	|			ИЛИ ВидыЗапасов.НалоговоеНазначение <> ТаблицаТоваров.НалоговоеНазначение 
	|			ИЛИ &ПерезаполнитьВидыЗапасов)";
	
	Если ДляТоваровУПартнеров Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаТоваровАналитикаУчетаНоменклатуры", "ТаблицаТоваров.АналитикаУчетаНоменклатурыТоварыУПартнеров");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаВидыЗапасовАналитикаУчетаНоменклатуры", "ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыТоварыУПартнеров");
		
	Иначе

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаТоваровАналитикаУчетаНоменклатуры", "ТаблицаТоваров.АналитикаУчетаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаВидыЗапасовАналитикаУчетаНоменклатуры", "ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры");
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",         Товары);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов",     ВидыЗапасов);
	Запрос.УстановитьПараметр("Ссылка",                 Ссылка);
	Запрос.УстановитьПараметр("Дата",                   Дата);
	Запрос.УстановитьПараметр("Партнер",                Партнер);
	Запрос.УстановитьПараметр("Контрагент",             Контрагент);
	Запрос.УстановитьПараметр("Соглашение",             Соглашение);
	Запрос.УстановитьПараметр("Договор",                Договор);
	Запрос.УстановитьПараметр("Организация",            Организация);
	Запрос.УстановитьПараметр("Склад",                  Склад);
	Запрос.УстановитьПараметр("МестоХранения",          Склад);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", 	ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Менеджер",               Менеджер);
	Запрос.УстановитьПараметр("Валюта",                 Валюта);
	Запрос.УстановитьПараметр("ТипЗапасов",     		ТипЗапасовПоХозОперации());
	Запрос.УстановитьПараметр("ВидЦены",				ВидЦеныПоставщика);
	Запрос.УстановитьПараметр("Проведен",				Проведен);
	Запрос.УстановитьПараметр("УчетПоНомерамГТД",		УчетПоНомерамГТД);
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Организация, Дата));

	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект, Запрос);
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Процедура ЗаполнитьНоменклатуруТоварыУПартнеров() Экспорт
	
	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры");
	Для Каждого СтрокаТоваров Из Товары Цикл
		
		КоличествоТоваров = СтрокаТоваров.Количество;
		СуммаСНДС = СтрокаТоваров.СуммаСНДС;
		СуммаНДС = СтрокаТоваров.СуммаНДС;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоваров);
		СтруктураПоиска.АналитикаУчетаНоменклатуры = СтрокаТоваров.АналитикаУчетаНоменклатурыТоварыУПартнеров;
		
		НайденныеСтроки = ВидыЗапасов.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаЗапасов Из НайденныеСтроки Цикл
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			
			НоваяСтрока.АналитикаУчетаНоменклатурыТоварыУПартнеров = СтрокаТоваров.АналитикаУчетаНоменклатурыТоварыУПартнеров;
			НоваяСтрока.АналитикаУчетаНоменклатуры = СтрокаТоваров.АналитикаУчетаНоменклатуры;
			
			НоваяСтрока.ВидЗапасовНаСкладе						   = СтрокаТоваров.ВидЗапасов;
			НоваяСтрока.Количество								   = Количество;
			Если КоличествоТоваров <> 0 Тогда
				НоваяСтрока.СуммаСНДС = Количество * СуммаСНДС / КоличествоТоваров;
				НоваяСтрока.СуммаНДС = Количество * СуммаНДС / КоличествоТоваров;
			КонецЕсли;
			
			СтрокаЗапасов.Количество = СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			СтрокаЗапасов.СуммаСНДС = СтрокаЗапасов.СуммаСНДС - НоваяСтрока.СуммаСНДС;
			СтрокаЗапасов.СуммаНДС = СтрокаЗапасов.СуммаНДС - НоваяСтрока.СуммаНДС;
			
			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
			СуммаСНДС = СуммаСНДС - НоваяСтрока.СуммаСНДС;
			СуммаНДС = СуммаНДС - НоваяСтрока.СуммаНДС;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивУдаляемыхСтрок = ВидыЗапасов.НайтиСтроки(Новый Структура("Количество", 0));
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц  = ВременныеТаблицыДанныхДокумента(Ложь, Ложь);
	ПерезаполнитьВидыЗапасов = ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов");
	
	ЗапасыСервер.ЗаполнитьВидыЗапасовПоУмолчанию(МенеджерВременныхТаблиц, Товары);
	
	МенеджерВременныхТаблиц  = ВременныеТаблицыДанныхДокумента(Ложь, Истина);
	ЗапасыСервер.ЗаполнитьВидыЗапасовПоУмолчанию(МенеджерВременныхТаблиц, Товары);
	
	ВидыЗапасовБазовые = ВидыЗапасов.Выгрузить();
	ВидыЗапасовБезГТД = ВидыЗапасов.ВыгрузитьКолонки();
	ВидыЗапасовСГТД = ВидыЗапасов.ВыгрузитьКолонки();
	
	// Заполнение товаров с выключенным признаком контроля по номерам ГТД
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента(Истина, Ложь);
	
	ВидыЗапасовПерезаполнены = Ложь;
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ЗапасыСервер.ПроверитьИзменениеТоваровПоКоличествуИСумме(МенеджерВременныхТаблиц) Тогда
		
		ДополнительныеСвойства.Вставить("КонтролироватьНомераГТД", Ложь); // разрешаем списывать остатки по пустым номерам ГТД
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов(Ложь);
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоОстаткамКОформлению(ЭтотОбъект, МенеджерВременныхТаблиц, Отказ, ПараметрыЗаполнения);
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД, СтавкаНДС", "Количество, СуммаСНДС, СуммаНДС");
		ВидыЗапасовБезГТД = ВидыЗапасов.Выгрузить();
		ВидыЗапасовПерезаполнены = Истина;
		
	КонецЕсли;
	
	// Заполнение товаров с включенным признаком контроля по номерам ГТД
	ВидыЗапасов.Загрузить(ВидыЗапасовБазовые);
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента(Истина, Истина);
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ЗапасыСервер.ПроверитьИзменениеТоваровПоКоличествуИСумме(МенеджерВременныхТаблиц) Тогда
		
		КонтролироватьНомераГТД = (ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути")
									И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути)
									Или (ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути")
									И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
		
		Если Не КонтролироватьНомераГТД Тогда
			ДополнительныеСвойства.Вставить("КонтролироватьНомераГТД", Ложь); // разрешаем списывать остатки по пустым номерам ГТД
		Иначе
			ДополнительныеСвойства.Вставить("КонтролироватьНомераГТД", Истина);
		КонецЕсли;
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов(Истина);
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоОстаткамКОформлению(ЭтотОбъект, МенеджерВременныхТаблиц, Отказ, ПараметрыЗаполнения);
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД, СтавкаНДС", "Количество, СуммаСНДС, СуммаНДС");
		ВидыЗапасовСГТД = ВидыЗапасов.Выгрузить();
		ВидыЗапасовПерезаполнены = Истина;
	
	КонецЕсли;
	
	Если ВидыЗапасовПерезаполнены Тогда
		ОбщиеВидыЗапасов = ВидыЗапасов.ВыгрузитьКолонки();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВидыЗапасовБезГТД, ОбщиеВидыЗапасов);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВидыЗапасовСГТД, ОбщиеВидыЗапасов);
		
		ВидыЗапасов.Загрузить(ОбщиеВидыЗапасов);
		
		ЗаполнитьНоменклатуруТоварыУПартнеров();
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыЗаполненияВидовЗапасов(УчетПоНомерамГТД = Ложь)
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	
	ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Истина;
	ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ТаблицаРеквизитовВидовЗапасовПоУмолчанию();
	ПараметрыЗаполнения.ИмяТаблицыОстатков = "ТоварыУПартнеровПоложительныйОстаток";
	
	КонтролироватьНомераГТД = (ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути")
								И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути)
								Или (ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути")
								И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
	
	Если Не КонтролироватьНомераГТД Тогда
		ПараметрыЗаполнения.БезОтбораПоНомерамГТД = Истина;
	КонецЕсли;
	
	Если УчетПоНомерамГТД Тогда
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию = Не КонтролироватьНомераГТД;
	Иначе
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию = Истина;
	КонецЕсли;
	
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Очистить();
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(ТипЗапасовПоХозОперации());
	ПараметрыЗаполнения.ОтборыВидовЗапасов.Организация = Организация;
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ВидЦены = ВидЦеныПоставщика;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТаблицаРеквизитовВидовЗапасовПоУмолчанию()
	
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию = Товары.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры"); // ТаблицаЗначений
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"ВладелецТовара", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"ТипЗапасов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗапасов"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"ТипЗапасовРезервы", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗапасов"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"ВидЗапасов", Новый ОписаниеТипов("СправочникСсылка.ВидыЗапасов"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НоменклатураГТД"));
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.Колонки.Добавить(
		"Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.ЗаполнитьЗначения(Партнер, "ВладелецТовара");
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.ЗаполнитьЗначения(Договор, "Договор");
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.ЗаполнитьЗначения(Организация, "Организация");
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.ЗаполнитьЗначения(ТипЗапасовПоХозОперации(), "ТипЗапасов");
	ТаблицаРеквизитовВидовЗапасовПоУмолчанию.ЗаполнитьЗначения(Контрагент, "Контрагент");
	
	Возврат ТаблицаРеквизитовВидовЗапасовПоУмолчанию;
	
КонецФункции

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Организация, Дата, Партнер, Склад, Договор, ХозяйственнаяОперация, Контрагент";
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц, Ссылка, ИменаРеквизитов);
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ТипЗапасовПоХозОперации()

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути Тогда
		
		Возврат Перечисления.ТипыЗапасов.СобственныйТоварВПути;
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка Тогда
		
		Возврат Перечисления.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке;
		
	Иначе
		Возврат Перечисления.ТипыЗапасов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ВариантыОформленияЗакупок()
	
	ВариантыОформленияЗакупок = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки") Тогда
		ВариантыОформленияЗакупок.Добавить(Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставки);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков") Тогда
		ВариантыОформленияЗакупок.Добавить(Перечисления.ВариантыОформленияЗакупок.ТоварыВПути);
	КонецЕсли;
	
	Возврат ВариантыОформленияЗакупок;
	
КонецФункции

Процедура ЗаполнитьСуммовыеПоказателиСтрокиТоваров(СтрокаТоваров)
	
	ДействияСоСтрокой = Новый Структура;
	
	Если ЗначениеЗаполнено(СтрокаТоваров.Цена) Тогда
		ПараметрыПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
		
		ДействияСоСтрокой.Вставить("ПересчитатьСумму",		"КоличествоУпаковок");
		ДействияСоСтрокой.Вставить("ПересчитатьСуммуНДС",	ПараметрыПересчетаСуммы);
		ДействияСоСтрокой.Вставить("ПересчитатьСуммуСНДС",	ПараметрыПересчетаСуммы);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
        ПараметрыЗаполненияНДС = Новый Структура("НалогообложениеНДС, Дата", НалогообложениеНДСПоУмолчанию(), Дата);
		
		ДействияСоСтрокой.Вставить("ЗаполнитьСтавкуНДС", ПараметрыЗаполненияНДС);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДействияСоСтрокой) Тогда
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, ДействияСоСтрокой, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьКоличествоУпаковокЗаполнитьСтавкуНДС(ЗаполнитьСтавкуНДС = Ложь)
	
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок",
										ОбработкаТабличнойЧастиКлиентСервер.СтруктураПересчетаКоличестваУпаковок());
	
	Если ЗаполнитьСтавкуНДС Тогда
        ЗаполнитьСтавкуНДС = Новый Структура("НалогообложениеНДС, Дата", НалогообложениеНДСПоУмолчанию(), Дата);
		
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ЗаполнитьСтавкуНДС);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

Процедура ЗаполнитьЦеныПоСоглашению(ЗаполнитьВсеСтроки = Истина)
	
	СтрокиБезЦен = Неопределено;
	
	Если Не ЗаполнитьВсеСтроки Тогда
		СтрокиБезЦен = Новый Массив;
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Цена) Тогда
				СтрокиБезЦен.Добавить(СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения",		"Цена, СтавкаНДС");
	ПараметрыЗаполнения.Вставить("Дата",				Дата);
	ПараметрыЗаполнения.Вставить("Валюта",				Валюта);
	ПараметрыЗаполнения.Вставить("Соглашение",			Соглашение);
	ПараметрыЗаполнения.Вставить("ВидЦеныПоставщика",	ВидЦеныПоставщика);
    ПараметрыЗаполнения.Вставить("НалогообложениеНДС",	НалогообложениеНДСПоУмолчанию());
	
	ДействияСоСтроками = Новый Структура;
	ДействияСоСтроками.Вставить("ПересчитатьСумму",		"КоличествоУпаковок");
	ДействияСоСтроками.Вставить("ПересчитатьСуммуНДС",	СтруктураПересчетаСуммы);
	ДействияСоСтроками.Вставить("ПересчитатьСуммуСНДС",	СтруктураПересчетаСуммы);
	
	ЗакупкиСервер.ЗаполнитьЦены(Товары, СтрокиБезЦен, ПараметрыЗаполнения, ДействияСоСтроками);
	
КонецПроцедуры

Функция НалогообложениеНДСПоУмолчанию()
    ПараметрыЗаполнения = Документы.ПоступлениеТоваровНаСклад.ПараметрыЗаполненияНалогообложенияНДСЗакупки(ЭтотОбъект);
    НалогообложениеНДСПоУмолчанию = Неопределено; 
    УчетНДСУП.ЗаполнитьНалогообложениеНДСЗакупки(НалогообложениеНДСПоУмолчанию, ПараметрыЗаполнения);
    Возврат НалогообложениеНДСПоУмолчанию;
КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли
