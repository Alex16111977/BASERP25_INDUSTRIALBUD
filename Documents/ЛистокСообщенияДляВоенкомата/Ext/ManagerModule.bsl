
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_UK_ЛистокСообщения2023";
	КомандаПечати.Представление = НСтр("ru='Листок сообщения';uk='Листок повідомлення'");

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_UK_ЛистокСообщения";
	КомандаПечати.Представление = НСтр("ru='Листок сообщения (до 2023 г.)';uk='Листок повідомлення (до 2023 р.)'");
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_UK_ЛистокСообщения") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ПФ_MXL_UK_ЛистокСообщения", НСтр("ru='Листок сообщения';uk='Листок повідомлення'"),
				ПечатнаяФормаЛисткаСообщения(МассивОбъектов, ОбъектыПечати), ,
				"Документ.ЛистокСообщенияДляВоенкомата.ПФ_MXL_UK_ЛистокСообщения");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_UK_ЛистокСообщения2023") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ПФ_MXL_UK_ЛистокСообщения2023", НСтр("ru='Листок сообщения';uk='Листок повідомлення'"),
				ПечатнаяФормаЛисткаСообщения2023(МассивОбъектов, ОбъектыПечати), ,
				"Документ.ЛистокСообщенияДляВоенкомата.ПФ_MXL_UK_ЛистокСообщения2023");
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФУНКЦИИ ПЕЧАТИ БЛАНКОВ ВОИНСКОГО УЧЕТА

Функция ПечатнаяФормаЛисткаСообщения2023(МассивОбъектов, ОбъектыПечати)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистокСообщения2023";
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистокСообщенияДляВоенкомата.ПФ_MXL_UK_ЛистокСообщения2023");
	
	ОбластьМакетаЛисток = Макет.ПолучитьОбласть("Листок"); 
	
	ДанныеДляПечати = ДанныеДляПечатиЛисткаСообщения2023(МассивОбъектов).Выбрать();
	
	Пока ДанныеДляПечати.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
//++ ЕРП ЗИК		
		ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ДанныеДляПечати.Организация, ДанныеДляПечати.Дата);

		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ДанныеДляПечати);
		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ОтветственныеЛица);
		ТаблицаФИО = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ОтветственныеЛица.Руководитель, "ИОФамилия");
		Если ТаблицаФИО.Количество() > 0 Тогда
			ОбластьМакетаЛисток.Параметры.Руководитель = ТаблицаФИО[0].ИОФамилия;
		КонецЕсли;	
//~		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ДанныеДляПечати);
//-- ЕРП ЗИК		
		
		Если ТипЗнч(ДанныеДляПечати.ДатаВыдачи) = Тип("Дата") Тогда
			ОбластьМакетаЛисток.Параметры.ДатаВыдачи = Формат(ДанныеДляПечати.ДатаВыдачи, "Л=uk_UA; ДЛФ=DD"); 
		КонецЕсли;
		
		МассивДлинСтрок = Новый Массив;
		МассивДлинСтрок.Добавить(66);
		МассивДлинСтрок.Добавить(96);
		
		РезультирующаяСтрока = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.ОписаниеИзменений, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрока) <= 2, СтрЧислоСтрок(РезультирующаяСтрока), 2)  Цикл
			ОбластьМакетаЛисток.Параметры["СменаДанных"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрока, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		
		МассивДлинСтрок.Установить(0,55);
		
		РезультирующаяСтрокаВоенкомат = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.Военкомат, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрокаВоенкомат) <= 2, СтрЧислоСтрок(РезультирующаяСтрокаВоенкомат), 2)  Цикл
			ОбластьМакетаЛисток.Параметры["Военкомат"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрокаВоенкомат, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		
		МассивДлинСтрок.Установить(0,63);
		
		РезультирующаяСтрокаАдрес = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.АдресМестаПроживания, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрокаАдрес) <= 2, СтрЧислоСтрок(РезультирующаяСтрокаАдрес), 2)  Цикл
			ОбластьМакетаЛисток.Параметры["АдресМестаПроживания"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрокаАдрес, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		ТабДокумент.Вывести(ОбластьМакетаЛисток); 
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДляПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПечатнаяФормаЛисткаСообщения(МассивОбъектов, ОбъектыПечати)
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистокСообщения";
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	//Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистокСообщенияДляВоенкомата.ПФ_MXL_ЛистокСообщения");
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистокСообщенияДляВоенкомата.ПФ_MXL_UK_ЛистокСообщения");
	
	ОбластьМакетаЛисток = Макет.ПолучитьОбласть("Листок"); 
	
	ДанныеДляПечати = ДанныеДляПечатиЛисткаСообщения(МассивОбъектов).Выбрать();
	
	Пока ДанныеДляПечати.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
//++ ЕРП ЗИК		
		ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ДанныеДляПечати.Организация, ДанныеДляПечати.Дата);

		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ДанныеДляПечати);
		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ОтветственныеЛица);
//~		ЗаполнитьЗначенияСвойств(ОбластьМакетаЛисток.Параметры, ДанныеДляПечати);
//-- ЕРП ЗИК		
		
		
		Если ТипЗнч(ДанныеДляПечати.ДатаВыдачи) = Тип("Дата") Тогда
			ОбластьМакетаЛисток.Параметры.ДатаВыдачи = Формат(ДанныеДляПечати.ДатаВыдачи, "Л=uk_UA; ДЛФ=DD"); 
		КонецЕсли;
		
		
		
		
		МассивДлинСтрок = Новый Массив;
		МассивДлинСтрок.Добавить(66);
		МассивДлинСтрок.Добавить(96);
		
		РезультирующаяСтрока = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.ОписаниеИзменений, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрока) <= 4, СтрЧислоСтрок(РезультирующаяСтрока), 4)  Цикл
			ОбластьМакетаЛисток.Параметры["СменаДанных"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрока, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		
		МассивДлинСтрок.Установить(0,55);
		
		РезультирующаяСтрокаВоенкомат = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.Военкомат, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрокаВоенкомат) <= 2, СтрЧислоСтрок(РезультирующаяСтрокаВоенкомат), 2)  Цикл
			ОбластьМакетаЛисток.Параметры["Военкомат"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрокаВоенкомат, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		
		МассивДлинСтрок.Установить(0,63);
		
		РезультирующаяСтрокаАдрес = ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ДанныеДляПечати.АдресМестаПроживания, МассивДлинСтрок);
		Для Счетчик = 1 По ?(СтрЧислоСтрок(РезультирующаяСтрокаАдрес) <= 2, СтрЧислоСтрок(РезультирующаяСтрокаАдрес), 2)  Цикл
			ОбластьМакетаЛисток.Параметры["АдресМестаПроживания"+Счетчик] = СтрЗаменить(СтрПолучитьСтроку(РезультирующаяСтрокаАдрес, Счетчик),Символы.ПС, "");  
		КонецЦикла;
		
		Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(ОбластьМакетаЛисток.Параметры.Подразделение) Тогда
			ОбластьМакетаЛисток.Параметры.Подразделение = ОбластьМакетаЛисток.Параметры.Подразделение.ПолноеНаименование();
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакетаЛисток); 
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДляПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ДанныеДляПечатиЛисткаСообщения2023(МассивСсылок)

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛистокСообщенияДляВоенкомата.Ссылка КАК Ссылка,
	|	ЛистокСообщенияДляВоенкомата.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛистокСообщенияДляВоенкомата.ДатаРождения КАК ДатаРождения,
	|	ЛистокСообщенияДляВоенкомата.Звание КАК Звание,
	|	ЛистокСообщенияДляВоенкомата.ВУС КАК ВУС,
	|	ЛистокСообщенияДляВоенкомата.АдресМестаПроживанияПредставление КАК АдресМестаПроживания,
	|	ЛистокСообщенияДляВоенкомата.ОписаниеИзменений,
	|	ЛистокСообщенияДляВоенкомата.Основание,
	|	ЛистокСообщенияДляВоенкомата.НомерРеестра,
	|	ЛистокСообщенияДляВоенкомата.Организация,
	|	ЛистокСообщенияДляВоенкомата.Дата
	|ПОМЕСТИТЬ ДанныеЛистка
	|ИЗ
	|	Документ.ЛистокСообщенияДляВоенкомата КАК ЛистокСообщенияДляВоенкомата
	|ГДЕ
	|	ЛистокСообщенияДляВоенкомата.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЕСТЬNULL(ВоинскийУчет.Военкомат, 0) КАК Военкомат,
	|	ДанныеЛистка.Ссылка,
	|	ДанныеЛистка.ФизическоеЛицо,
	|	ДанныеЛистка.ДатаРождения,
	|	ДанныеЛистка.Звание,
	|	ДанныеЛистка.ВУС,
	|	ДанныеЛистка.АдресМестаПроживания,
	|	ДанныеЛистка.ОписаниеИзменений,
	|	ДанныеЛистка.Основание,
	|	ДанныеЛистка.НомерРеестра,
	|	ДанныеЛистка.Организация,
	|	ДанныеЛистка.Дата
	|ПОМЕСТИТЬ ПромежуточнаяТаблица
	|ИЗ
	|	ДанныеЛистка КАК ДанныеЛистка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет КАК ВоинскийУчет
	|		ПО ДанныеЛистка.ФизическоеЛицо = ВоинскийУчет.ФизическоеЛицо
	|			И ДанныеЛистка.Дата >= ВоинскийУчет.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВоинскийУчет.Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ПромежуточнаяТаблица.Военкомат,
	|	ПромежуточнаяТаблица.Ссылка,
	|	ПромежуточнаяТаблица.ФизическоеЛицо,
	|	ПромежуточнаяТаблица.ДатаРождения,
	|	ПромежуточнаяТаблица.Звание,
	|	ПромежуточнаяТаблица.ВУС,
	|	ПромежуточнаяТаблица.АдресМестаПроживания,
	|	ПромежуточнаяТаблица.ОписаниеИзменений,
	|	ПромежуточнаяТаблица.Основание,
	|	ПромежуточнаяТаблица.НомерРеестра,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Серия, """") КАК Серия,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Номер, """") КАК Номер,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.ДатаВыдачи, """") КАК ДатаВыдачи,
	|	ПромежуточнаяТаблица.Организация,
	|	ПромежуточнаяТаблица.Дата
	|ИЗ
	|	ПромежуточнаяТаблица КАК ПромежуточнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|		ПО ПромежуточнаяТаблица.ФизическоеЛицо = ДокументыФизическихЛиц.Физлицо
	|			И ПромежуточнаяТаблица.Дата >= ДокументыФизическихЛиц.Период
	|			И ДокументыФизическихЛиц.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ВоенныйБилет)
	|" ;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ДанныеДляПечатиЛисткаСообщения(МассивСсылок)

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛистокСообщенияДляВоенкомата.Ссылка КАК Ссылка,
	|	ЛистокСообщенияДляВоенкомата.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛистокСообщенияДляВоенкомата.ДатаРождения КАК ДатаРождения,
	|	ЛистокСообщенияДляВоенкомата.Звание КАК Звание,
	|	ЛистокСообщенияДляВоенкомата.ВУС КАК ВУС,
	|	ЛистокСообщенияДляВоенкомата.АдресМестаПроживанияПредставление КАК АдресМестаПроживания,
	|	ЛистокСообщенияДляВоенкомата.ОписаниеИзменений,
	|	ЛистокСообщенияДляВоенкомата.Организация,
	|	ЛистокСообщенияДляВоенкомата.Дата
	|ПОМЕСТИТЬ ДанныеЛистка
	|ИЗ
	|	Документ.ЛистокСообщенияДляВоенкомата КАК ЛистокСообщенияДляВоенкомата
	|ГДЕ
	|	ЛистокСообщенияДляВоенкомата.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЕСТЬNULL(ВоинскийУчет.Военкомат, 0) КАК Военкомат,
	|	ДанныеЛистка.Ссылка,
	|	ДанныеЛистка.ФизическоеЛицо,
	|	ДанныеЛистка.ДатаРождения,
	|	ДанныеЛистка.Звание,
	|	ДанныеЛистка.ВУС,
	|	ДанныеЛистка.АдресМестаПроживания,
	|	ДанныеЛистка.ОписаниеИзменений,
	|	ДанныеЛистка.Организация,
	|	ДанныеЛистка.Дата
	|ПОМЕСТИТЬ ПромежуточнаяТаблица
	|ИЗ
	|	ДанныеЛистка КАК ДанныеЛистка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет КАК ВоинскийУчет
	|		ПО ДанныеЛистка.ФизическоеЛицо = ВоинскийУчет.ФизическоеЛицо
	|			И ДанныеЛистка.Дата >= ВоинскийУчет.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВоинскийУчет.Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ПромежуточнаяТаблица.Военкомат,
	|	ПромежуточнаяТаблица.Ссылка,
	|	ПромежуточнаяТаблица.ФизическоеЛицо,
	|	ПромежуточнаяТаблица.ДатаРождения,
	|	ПромежуточнаяТаблица.Звание,
	|	ПромежуточнаяТаблица.ВУС,
	|	ПромежуточнаяТаблица.АдресМестаПроживания,
	|	ПромежуточнаяТаблица.ОписаниеИзменений,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Серия, ""_____"") КАК Серия,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Номер, ""__________"") КАК Номер,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.ДатаВыдачи, ""___  _______ 20__ р."") КАК ДатаВыдачи,
	|	ПромежуточнаяТаблица.Организация,
	|	ПромежуточнаяТаблица.Дата
	|ИЗ
	|	ПромежуточнаяТаблица КАК ПромежуточнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|		ПО ПромежуточнаяТаблица.ФизическоеЛицо = ДокументыФизическихЛиц.Физлицо
	|			И ПромежуточнаяТаблица.Дата >= ДокументыФизическихЛиц.Период
	|			И ДокументыФизическихЛиц.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ВоенныйБилет)" ;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Сотрудник.ФизическоеЛицо)";
КонецПроцедуры

#КонецОбласти

#Область МеханизмФиксацииИзменений

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксацииВторичныхДанных(ФиксацияОписаниеФиксацииРеквизитов(), , ФиксацияОписанияТЧ())
КонецФункции

Функция ФиксацияОписаниеФиксацииРеквизитов()
	
	ОписаниеФиксацииРеквизитов = Новый Соответствие;
	
	// ЗАПОЛНЯЕМЫЕ АВТОМАТИЧЕСКИ
	ОписаниеФиксацииРеквизитов.Вставить("Подразделение", 						ФиксацияОписаниеФиксацииРеквизита("Подразделение", "ГруппаОсновныеРеквизиты", "Сотрудник", НСтр("ru='Подразделение заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и используйте соответствующие документы. Если вы хотите изменить подразделение только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Підрозділ заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"" і використовуйте відповідні документи. Якщо ви хочете змінити підрозділ тільки в цьому документі, натисніть ""Так"" продовжити редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("ДатаРождения", 						ФиксацияОписаниеФиксацииРеквизита("ДатаРождения", "ГруппаИнформацияЛеваяКолонка", "Сотрудник", НСтр("ru='Дата рождения заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить дату рождения только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Дата народження заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити дату народження тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("Звание", 								ФиксацияОписаниеФиксацииРеквизита("Звание", "ГруппаИнформацияЛеваяКолонка", "Сотрудник", НСтр("ru='Звание заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить звание только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Звання заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити звання тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("ВУС", 									ФиксацияОписаниеФиксацииРеквизита("ВУС", "ГруппаИнформацияЛеваяКолонка", "Сотрудник", НСтр("ru='ВУС заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить ВУС только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='ВОС заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити ВОС тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("Образование", 							ФиксацияОписаниеФиксацииРеквизита("Образование", "ГруппаИнформацияПраваяКолонка", "Сотрудник", НСтр("ru='Образование заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить образование только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Освіта заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити освіту тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("Должность", 							ФиксацияОписаниеФиксацииРеквизита("Должность", "ГруппаИнформацияЛеваяКолонка", "Сотрудник", НСтр("ru='Должность заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и используйте соответствующие документы. Если вы хотите изменить должность только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Посада заповнюється автоматично. Якщо ви хочете виправити дані, натисніть ""Ні"" і використовуйте відповідні документи. Якщо ви хочете змінити посаду тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("АдресМестаПроживанияПредставление", 	ФиксацияОписаниеФиксацииРеквизита("АдресМестаПроживанияПредставление", "ГруппаИнформацияПраваяКолонка", "Сотрудник", НСтр("ru='Адрес заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить адрес только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Адреса заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити адресу тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("СемейноеПоложение", 					ФиксацияОписаниеФиксацииРеквизита("СемейноеПоложение", "ГруппаИнформацияПраваяКолонка", "Сотрудник", НСтр("ru='Семейное положение заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить семейное положение только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Сімейний стан заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви хочете змінити сімейний стан тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("НомерРеестра", 						ФиксацияОписаниеФиксацииРеквизита("НомерРеестра", "ГруппаИнформацияЛеваяКолонка", "Сотрудник", НСтр("ru='Номер в реестре заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и затем используйте ссылку ""Редактировать карточку сотрудника"". Если вы хотите изменить номер в реестре только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Номер у реєстрі заповнюється автоматично. Якщо ви хочете виправити дані, натисніть ""Ні"", а потім скористайтеся посиланням ""Редагувати картку співробітника"". Якщо ви бажаєте змінити номер у реєстрі тільки в цьому документі, натисніть ""Так"" і продовжіть редагування'")));
	ОписаниеФиксацииРеквизитов.Вставить("ОтветственныйЗаВУР", 					ФиксацияОписаниеФиксацииРеквизита("ОтветственныйЗаВУР", "СтраницаОсновное", "Организация", НСтр("ru='Ответственный за ВУ заполняется автоматически. Если вы хотите исправить данные, нажмите ""Нет"" и отредактируйте данные организации. Если вы хотите изменить ответственного только в этом документе, нажмите ""Да"" и продолжите редактирование';uk='Відповідальний за ВО заповнюється автоматично. Якщо ви хочете виправити дані, натисніть кнопку ""Ні"" і відредагуйте дані організації. Якщо ви хочете змінити відповідального тільки в цьому документі, натисніть ""Так"" продовжіть редагування'")));
	
	Возврат ОписаниеФиксацииРеквизитов;  
	
КонецФункции 

Функция ФиксацияОписанияТЧ()
	
	СтруктураКлючевыхПолей = Новый Структура;
	
	Возврат	СтруктураКлючевыхПолей
КонецФункции

Функция ФиксацияОписаниеФиксацииРеквизита(ИмяРеквизита,
	ИмяГруппы, 
	ОснованиеЗаполнения,
	СтрокаПредупреждения =  "",
	Используется = Истина, 
	РеквизитСтроки = Ложь,
	ФиксацияГруппы = Ложь, 
	Путь = "",
	ОтображатьПредупреждение = Истина)
	
	ФиксацияРеквизита = ФиксацияВторичныхДанныхВДокументах.СтруктураПараметровОписанияФиксацииРеквизитов();
	ФиксацияРеквизита.Вставить("ИмяРеквизита", ИмяРеквизита);
	ФиксацияРеквизита.Вставить("Используется", Используется);
	ФиксацияРеквизита.Вставить("ИмяГруппы", ИмяГруппы);
	ФиксацияРеквизита.Вставить("ФиксацияГруппы", ФиксацияГруппы);
	ФиксацияРеквизита.Вставить("ОснованиеЗаполнения", ОснованиеЗаполнения);
	ФиксацияРеквизита.Вставить("Путь", Путь);
	ФиксацияРеквизита.Вставить("ОтображатьПредупреждение", ОтображатьПредупреждение);
	Если СтрокаПредупреждения <> "" Тогда
		ФиксацияРеквизита.Вставить("СтрокаПредупреждения", СтрокаПредупреждения);
	КонецЕсли;
	ФиксацияРеквизита.Вставить("РеквизитСтроки", РеквизитСтроки);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксацииРеквизита(ФиксацияРеквизита)
КонецФункции 

#КонецОбласти

#КонецЕсли