#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//               (см. функцию ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов)
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Организация", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор, ДокументОснование";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Подразделение", Параметры);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("РасчетыСПодотчетниками");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	АвансовыйОтчетЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДенежныхСредств(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДенежныеСредстваКонтрагент(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДСРасчетНалоговогоКредита(Запрос, ТекстыЗапроса, Регистры);
		
		
		АвансовыйОтчетЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Функция определяет реквизиты выбранного авансового отчета.
//
// Параметры:
//  АвансовыйОтчет - ДокументСсылка.АвансовыйОтчет - Ссылка на документ.
//
// Возвращаемое значение:
//	Структура - реквизиты авансового отчета.
//
Функция РеквизитыДокумента(АвансовыйОтчет) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &АвансовыйОтчет
	|");
	
	Запрос.УстановитьПараметр("АвансовыйОтчет", АвансовыйОтчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Дата = Выборка.Дата;
		Организация = Выборка.Организация;
		Валюта = Выборка.Валюта;
		ПодотчетноеЛицо = Выборка.ПодотчетноеЛицо;
		Подразделение = Выборка.Подразделение;
	Иначе
		Дата = Дата(1,1,1);
		Организация = Справочники.Организации.ПустаяСсылка();
		Валюта = Справочники.Валюты.ПустаяСсылка();
		ПодотчетноеЛицо = Справочники.Пользователи.ПустаяСсылка();
		Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Дата", Дата);
	СтруктураРеквизитов.Вставить("Организация", Организация);
	СтруктураРеквизитов.Вставить("Валюта", Валюта);
	СтруктураРеквизитов.Вставить("ПодотчетноеЛицо", ПодотчетноеЛицо);
	СтруктураРеквизитов.Вставить("Подразделение", Подразделение);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Включает документ закупки в авансовый отчет или исключает его
// 
// Параметры:
// 	Ссылка - ДОкументСсылка.АвансовыйОтчет - Авансовый отчет
// 	ДокументыКИзменению -Массив - Ссылки на документы закупки
// 	ДополнительныеСвойства - Стурктура - Дополнительные свойства для выполнения процедуры
// 	Отказ - Булево - Признак неуспешного выполнения процедуры
//
Процедура УдалитьДобавитьДокументыЗакупки(Ссылка, ДокументыКИзменению, ДополнительныеСвойства, Отказ) Экспорт
	
	РедактируемыйДокументЗакупки = Неопределено;
	ДополнительныеСвойства.Свойство("РедактируемыйДокументЗакупки", РедактируемыйДокументЗакупки);
	
	Для каждого ДокументКИзменению Из ДокументыКИзменению Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ДокументОбъект = ДокументКИзменению.ПолучитьОбъект();
			
			Если ДокументКИзменению <> РедактируемыйДокументЗакупки Тогда
				Попытка
					ДокументОбъект.Заблокировать();
				Исключение
					ТекстСообщения = СтрШаблон(
						НСтр("ru='%1 находится в процессе редактирования пользователем или системой и не может быть изменено.';uk='%1 знаходиться в процесі редагування користувачем або системою і не може бути змінено.'"),
						ДокументОбъект);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					ВызватьИсключение;
				КонецПопытки;
			КонецЕсли;
			
			ДокументОбъект.АвансовыйОтчет = Ссылка;
			ДокументОбъект.ДополнительныеСвойства.Вставить("НеОбновлятьАвансовыйОтчет");
			ДокументОбъект.Записать();
			
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваУПодотчетныхЛиц.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
			
			ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(ДокументОбъект.Ссылка,
				"ДенежныеСредстваУПодотчетныхЛиц");
			
			УстановитьПривилегированныйРежим(Истина);
			НаборЗаписей.Загрузить(ТаблицыДляДвижений["ТаблицаДенежныеСредстваУПодотчетныхЛиц"]);
			НаборЗаписей.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстСообщения = НСтр("ru='Не удалось изменить документ закупки по причине: %1';uk='Не вдалося змінити документ купівлі через: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			ТекстОшибки = НСтр("ru='Не удалось изменить документ закупки.';uk='Не вдалося змінити документ купівлі.'",ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(
				ТекстОшибки,
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				
			Отказ = Истина;
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат АвансовыйОтчетЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат АвансовыйОтчетЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

// Возвращает параметры настройки счетов учета в документе.
// 
// Параметры:
// 	Статус - ПеречислениеСсылка.СтатусыАвансовогоОтчета - Статус авансового отчета.
// 	
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. НастройкаСчетовУчета.ПараметрыНастройкиСчетаУчетаОперации).
//
Функция ПараметрыНастройкиСчетовУчета(Статус) Экспорт
	
	МассивПараметров = Новый Массив();
	
	#Область НастройкаСчетаОтраженияСписанияРасходов
	ПараметрыНастройки = НастройкаСчетовУчета.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект.ПрочиеРасходы";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьи";
	
	ПараметрыНастройки.СчетУчета = "СчетУчета";
	ПараметрыНастройки.Субконто1 = "Субконто1";
	ПараметрыНастройки.Субконто2 = "Субконто2";
	ПараметрыНастройки.Субконто3 = "Субконто3";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "Объект.ПрочиеРасходы.АналитикаАктивовПассивов";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПрочиеРасходыПредставлениеОтраженияВРеглУчете");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции
//-- НЕ УТ

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	Статус - ПеречислениеСсылка.СтатусыАвансовогоОтчета - Статус авансового отчета.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик(Статус) Экспорт
	
	МассивПаметровВыбора = Новый Массив;
	
	#Область СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.ПрочиеРасходы";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("ПрочиеРасходыСтатьяРасходов");
	
	СтатусУтвержден = Статус = Перечисления.СтатусыАвансовогоОтчета.Утвержден;
	#Область АналитикаАктивовПассивов
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = СтатусУтвержден;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("ПрочиеРасходыАналитикаАктивовПассивов");
	#КонецОбласти
	
	#Область АналитикаРасходов
	ПараметрыВыбора.ВыборСтатьиРасходов = СтатусУтвержден;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("ПрочиеРасходыАналитикаРасходов");
	#КонецОбласти
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
		
	Возврат МассивПаметровВыбора;
	
КонецФункции

// Определяет свойства полей формы в зависимости от данных
//
// Параметры:
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условиия применения настройки
//		* Свойства - Структура - имена и значения свойств
//
Процедура ЗаполнитьНастройкиПолейФормы(Настройки) Экспорт
	
	Финансы = ФинансоваяОтчетностьСервер;
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ОплатаПоставщикамСуммаВзаиморасчетов");
	Элемент.Поля.Добавить("ОднаВалюта");
	Элемент.Поля.Добавить("НесколькоВалют");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНесколькоВалют", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПрочиеРасходыГруппаРасходы");
	Элемент.Поля.Добавить("ПрочиеРасходыГруппаНДС");
	Элемент.Поля.Добавить("ПрочиеРасходыЗаполнитьПодразделение");
	Финансы.НовыйОтбор(Элемент.Условие, "Статус", Перечисления.СтатусыАвансовогоОтчета.Утвержден);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПрочиеРасходыОтклонено");
	Элемент.Поля.Добавить("ПрочиеРасходыПричинаОтклонения");
	Финансы.НовыйОтбор(Элемент.Условие, "Статус", Перечисления.СтатусыАвансовогоОтчета.Утвержден);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьСтатусыАвансовыхОтчетов", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Валюта");
	Финансы.НовыйОтбор(Элемент.Условие, "Мультивалютный", Ложь);
	Элемент.Свойства.Вставить("Доступность");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Получено");
	Элемент.Поля.Добавить("Израсходовано");
	Элемент.Поля.Добавить("ОстатокПерерасход");
	Элемент.Поля.Добавить("Отклонено");
	Элемент.Поля.Добавить("ТекущаяВалюта1");
	Элемент.Поля.Добавить("ТекущаяВалюта2");
	Финансы.НовыйОтбор(Элемент.Условие, "Мультивалютный", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДокументыЗакупкиВалюта");
	Элемент.Поля.Добавить("ПрочиеРасходыВалюта");
	Элемент.Поля.Добавить("ОплатаПоставщикамВалюта");
	Элемент.Поля.Добавить("КонвертацияВалюты");
	Элемент.Поля.Добавить("ГруппаМультивалютныеСуммы");
	Финансы.НовыйОтбор(Элемент.Условие, "Мультивалютный", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДокументыЗакупки");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ПечатьЕдиногоАвансовогоОтчета", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДокументыЗакупкиЦельВыдачи");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.КонтролироватьВыдачуПодОтчетВРазрезеЦелей", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
КонецПроцедуры

// Определяет входящие в авансовый отчет документы закупки
//
// Параметры:
//    Ссылка - ДокументСсылка.АвансовыйОтчет - ссылка на авансовый отчет
Функция СписокДокументовЗакупки(Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.АвансовыйОтчет = &Ссылка
	|	И ДанныеДокумента.Ссылка.Проведен
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(ПодотчетноеЛицо)
	|	И ЗначениеРазрешено(ОплатаПоставщикам.Поставщик, NULL КАК ИСТИНА)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область УчетНДС

Функция ПараметрыЗаполненияНалоговогоНазначения(Документ) Экспорт
	
	Реквизиты = Новый Структура("Дата, Организация");
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, Реквизиты);
	Иначе
		ЗаполнитьЗначенияСвойств(Реквизиты, Документ);
	КонецЕсли;
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалоговогоНазначения();
	ПараметрыЗаполнения.Дата = Реквизиты.Дата;
	ПараметрыЗаполнения.Организация = Реквизиты.Организация;
	
	ПараметрыЗаполнения.ПриобретениеНаСтатьи = Истина;
	
    ПараметрыЗаполнения.ЕстьСтавкаНДС        = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

// Возвращает параметры механизма взаиморасчетов.
// 
// Параметры:
// 	Статус - ПеречислениеСсылка.СтатусыАвансовогоОтчета - Статус авансового отчета.
// 
// Возвращаемое значение:
// 	Структура - Структура параметров функций механизма взаиморасчетов (См. ВзаиморасчетыСервер.ПараметрыМеханизма)
//
Функция ПараметрыВзаиморасчеты(Статус = Неопределено) Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	СтруктураПараметров.ИзменяетПланОплаты               = Статус = Перечисления.СтатусыАвансовогоОтчета.Утвержден;
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки     = Ложь;
	СтруктураПараметров.ИзменяетРасчетыСтрокой           = 
		"ИсточникДанных.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)";
	
	СтруктураПараметров.СуммаДокумента                   = "";
	
	СтруктураПараметров.Курс                             = "";
	СтруктураПараметров.Кратность                        = "";
	СтруктураПараметров.ПорядокРасчетов                  = "";
	СтруктураПараметров.ФормаОплаты                      = "";
	СтруктураПараметров.ОплатаВВалюте                    = "";
		
	СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа  = "Объект.ОплатаПоставщикам";
	СтруктураПараметров.Партнер                          = "Объект.ОплатаПоставщикам.Поставщик";
	СтруктураПараметров.Соглашение                       = "";
	СтруктураПараметров.Договор                          = "";
	СтруктураПараметров.Контрагент                       = "Объект.ОплатаПоставщикам.Контрагент";
	СтруктураПараметров.ГруппаФинансовогоУчета           = "";
	СтруктураПараметров.БанковскийСчетОрганизации        = "Объект.БанковскийСчет";
	СтруктураПараметров.БанковскийСчетКонтрагента        = "";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.НаправлениеДеятельности          = "";
	СтруктураПараметров.Дата                             = "Объект.Дата"; 
	
	СтруктураПараметров.ЭлементыФормы.ПодборВРасшифровкуПлатежа = "ПодборПоОстаткам";
	СтруктураПараметров.ЭлементыФормы.РасшифровкаПлатежа = "ОплатаПоставщикам";
	
	Возврат СтруктураПараметров;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АвансовыйОтчет";
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Истина);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметровПроведения();
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Дата                    КАК Период,
	|	ДанныеДокумента.Номер                   КАК Номер,
	|	ДанныеДокумента.Валюта                  КАК Валюта,
	|	ДанныеДокумента.Мультивалютный          КАК Мультивалютный,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо         КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.Автор                   КАК Автор,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.СуммаИзрасходовано      КАК СуммаИзрасходовано,
	|	ДанныеДокумента.Статус                  КАК Статус,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен                КАК Проведен
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения(Реквизиты) Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	Значения = Новый Структура;
	Значения.Вставить("ИдентификаторМетаданных",                       ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АвансовыйОтчет"));
	Значения.Вставить("ХозяйственнаяОперация",                         Перечисления.ХозяйственныеОперации.ПрочиеРасходыПодотчетногоЛица);
	Значения.Вставить("ВалютаРегламентированногоУчета",                Константы.ВалютаРегламентированногоУчета.Получить());
	Значения.Вставить("ВалютаУправленческогоУчета",                    Константы.ВалютаУправленческогоУчета.Получить());
	Значения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",         ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	Значения.Вставить("КонтролироватьВыдачуПодОтчетВРазрезеЦелей",     ПолучитьФункциональнуюОпцию("КонтролироватьВыдачуПодОтчетВРазрезеЦелей"));
	
	Если Реквизиты <> Неопределено Тогда
		Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.Валюта, Неопределено, Реквизиты.Период);
			
		Значения.Вставить("КоэффициентПересчетаВВалютуУпр",            Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		Значения.Вставить("КоэффициентПересчетаВВалютуРегл",           Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		Значения.Вставить("НомерНаПечать",                             ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	 	Значения.Вставить("ОрганизацияПлательщикНДС",        		   НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Реквизиты.Организация, Реквизиты.Период));
		Значения.Вставить("НалоговоеНазначениеОрганизации", 		   Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	КонецЕсли;
	
	Значения.Вставить("ВидПоставки",                     Перечисления.ВидыПоставки.Поставка);
	
	Возврат Значения;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос)

	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПоПартнерамИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОплата.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаОплата.Поставщик  КАК Партнер,
	|	ТаблицаОплата.Контрагент КАК Контрагент,
	|	ТаблицаОплата.ОбъектРасчетов.Договор КАК Договор,
	|	ТаблицаОплата.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|
	|ПОМЕСТИТЬ ТаблицаОбъектовРасчетов
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТаблицаОплата
	|	
	|ГДЕ
	|	ТаблицаОплата.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НЕОПРЕДЕЛЕНО,
	|	ПрочиеРасходы.Контрагент.Партнер,
	|	ПрочиеРасходы.Контрагент,
	|	&ПустойДоговор,
	|	ПрочиеРасходы.НаправлениеДеятельности
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|	И ПрочиеРасходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НеНДС)
	|	И НЕ ПрочиеРасходы.Отменено
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация                       				КАК Организация,
	|	ТаблицаОбъектовРасчетов.Контрагент 				КАК Контрагент,
	|	ТаблицаОбъектовРасчетов.Партнер    				КАК Партнер,
	|	ТаблицаОбъектовРасчетов.Договор    				КАК Договор,
	|	ТаблицаОбъектовРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация = Аналитика.Организация
	|		И ТаблицаОбъектовРасчетов.Контрагент = Аналитика.Контрагент
	|		И ТаблицаОбъектовРасчетов.Партнер = Аналитика.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	Аналитика.КлючАналитики ЕСТЬ NULL
	|");
	
	ЗапросАналитик.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("Организация", Запрос.Параметры.Организация);
	ЗапросАналитик.УстановитьПараметр("ПустойДоговор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПоПартнерамИнициализированы", Истина);

КонецПроцедуры

Функция ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса)
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);
	
	ИмяРегистра = "ТаблицаОплатаПоставщикам";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаОплатаПоставщикам.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОплатаПоставщикам.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	ТаблицаОплатаПоставщикам.Сумма КАК Сумма,
	|	ТаблицаОплатаПоставщикам.Валюта КАК Валюта,
	|	ТаблицаОплатаПоставщикам.СуммаВзаиморасчетов,
	|	ТаблицаОплатаПоставщикам.ВалютаВзаиморасчетов,
	|
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам
	|
	|ПОМЕСТИТЬ ТаблицаОплатаПоставщикам
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТаблицаОплатаПоставщикам
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|	ПО
	|		ТаблицаОплатаПоставщикам.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|		И ТаблицаОплатаПоставщикам.Контрагент = ТаблицаОбъектовРасчетов.Контрагент
	|		И ТаблицаОплатаПоставщикам.Поставщик = ТаблицаОбъектовРасчетов.Партнер
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация = Аналитика.Организация
	|		И ТаблицаОбъектовРасчетов.Контрагент = Аналитика.Контрагент
	|		И ТаблицаОбъектовРасчетов.Партнер = Аналитика.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаОплатаПоставщикам.Ссылка = &Ссылка
	|	И ТаблицаОплатаПоставщикам.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТКурсыВалютУпр";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КурсВалюты.Валюта КАК Валюта,
	|	КурсВалюты.Курс * КурсВалютыУпр.Кратность / (КурсВалюты.Кратность * КурсВалютыУпр.Курс) КАК КоэффициентПересчета
	|ПОМЕСТИТЬ ВТКурсыВалютУпр
	|ИЗ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсВалюты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|	ПО (ИСТИНА)
	|ГДЕ
	|	КурсВалюты.Кратность <> 0
	|	И КурсВалютыУпр.Курс <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТКурсыВалютРегл";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КурсВалюты.Валюта КАК Валюта,
	|	КурсВалюты.Курс * КурсВалютыРегл.Кратность / (КурсВалюты.Кратность * КурсВалютыРегл.Курс) КАК КоэффициентПересчета
	|ПОМЕСТИТЬ ВТКурсыВалютРегл
	|ИЗ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсВалюты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаРегламентированногоУчета) КАК КурсВалютыРегл
	|	ПО (ИСТИНА)
	|ГДЕ
	|	КурсВалюты.Кратность <> 0
	|	И КурсВалютыРегл.Курс <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ТаблицаКурсовыхРазниц";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Валюта КАК ВалютаКонвертации,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств,
	|	
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКонвертации * ТаблицаКурсыВалютУпрКонвертации.КоэффициентПересчета КАК ЧИСЛО(31,2))
	|		- ВЫРАЗИТЬ(ДанныеДокумента.Сумма * ТаблицаКурсыВалютУпр.КоэффициентПересчета КАК ЧИСЛО(31,2))
	|	КАК СуммаУпр,
	|	
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКонвертации * ТаблицаКурсыВалютРеглКонвертации.КоэффициентПересчета КАК ЧИСЛО(31,2))
	|		- ВЫРАЗИТЬ(ДанныеДокумента.Сумма * ТаблицаКурсыВалютРегл.КоэффициентПересчета КАК ЧИСЛО(31,2))
	|	КАК СуммаРегл
	|
	|ПОМЕСТИТЬ ТаблицаКурсовыхРазниц
	|ИЗ
	|	Документ.АвансовыйОтчет.КонвертацияВалюты КАК ДанныеДокумента
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ДанныеДокумента.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпрКонвертации
	|		ПО ТаблицаКурсыВалютУпрКонвертации.Валюта = ДанныеДокумента.ВалютаКонвертации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ДанныеДокумента.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРеглКонвертации
	|		ПО ТаблицаКурсыВалютРеглКонвертации.Валюта = ДанныеДокумента.ВалютаКонвертации
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Ссылка.Мультивалютный
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваУПодотчетныхЛиц";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	
	|	ПрочиеРасходы.Валюта КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ПрочиеРасходы.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	ПрочиеРасходы.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	
	|	ПрочиеРасходы.Сумма КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходыПодотчетногоЛица) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ПрочиеРасходы.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ПрочиеРасходы.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ПрочиеРасходы.Валюта
	|
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|	И НЕ ПрочиеРасходы.Отменено
	|	И ПрочиеРасходы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	
	|	ОплатаПоставщикам.Валюта КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ОплатаПоставщикам.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	ОплатаПоставщикам.Сумма КАК Сумма,
	|	ВЫБОР КОГДА ОплатаПоставщикам.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|		ТОГДА ОплатаПоставщикам.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ОплатаПоставщикам.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР КОГДА ОплатаПоставщикам.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И ОплатаПоставщикам.Валюта <> &ВалютаРегламентированногоУчета
	|		ТОГДА ОплатаПоставщикам.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ОплатаПоставщикам.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРегл,
	
	|	ОплатаПоставщикам.Сумма КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщикуПодотчетнымЛицом) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ОплатаПоставщикам.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ОплатаПоставщикам.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ОплатаПоставщикам.Валюта
	|	
	|ГДЕ
	|	ОплатаПоставщикам.Ссылка = &Ссылка
	|	И ОплатаПоставщикам.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|ОБЪЕДИНИТЬ ВСЕ // Конвертация
	|	
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	
	|	КонвертацияВалюты.Валюта КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		КонвертацияВалюты.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	КонвертацияВалюты.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(КонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(КонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	КонвертацияВалюты.Сумма КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалютыПодотчетнымЛицом) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		КонвертацияВалюты.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.КонвертацияВалюты КАК КонвертацияВалюты
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = КонвертацияВалюты.ВалютаКонвертации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = КонвертацияВалюты.ВалютаКонвертации
	|
	|ГДЕ
	|	КонвертацияВалюты.Ссылка = &Ссылка
	|	И &Мультивалютный
	|	И КонвертацияВалюты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|ОБЪЕДИНИТЬ ВСЕ // Конвертация
	|	
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	
	|	КонвертацияВалюты.ВалютаКонвертации КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		КонвертацияВалюты.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	КонвертацияВалюты.СуммаКонвертации КАК Сумма,
	|	ВЫРАЗИТЬ(КонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(КонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	КонвертацияВалюты.СуммаКонвертации КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалютыПодотчетнымЛицом) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		КонвертацияВалюты.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.КонвертацияВалюты КАК КонвертацияВалюты
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = КонвертацияВалюты.ВалютаКонвертации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = КонвертацияВалюты.ВалютаКонвертации
	|
	|ГДЕ
	|	КонвертацияВалюты.Ссылка = &Ссылка
	|	И &Мультивалютный
	|	И КонвертацияВалюты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Отрицательные курсовые разницы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	|
	|	ТаблицаКурсовыхРазниц.ВалютаКонвертации КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	0 КАК Сумма,
	|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаУпр,
	|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
	|	0 КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|	
	|ГДЕ
	|	ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Положительные курсовые разницы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	|
	|	ТаблицаКурсовыхРазниц.ВалютаКонвертации КАК Валюта,
	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	0 КАК Сумма,
	|	ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаУпр,
	|	ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
	|	0 КАК КОтчету,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|	
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|	
	|ГДЕ
	|	ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК СтатьяДоходов,
	|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|	&Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) КАК ХозяйственнаяОперация,
	|
	|	ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
	|	
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ТаблицаКурсовыхРазниц.СуммаУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ТаблицаКурсовыхРазниц.СуммаУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпрБезНДС,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ТаблицаКурсовыхРазниц.СуммаРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРеглБезНДС,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ТаблицаКурсовыхРазниц.СуммаРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл
	|
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И (ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаРасходов.Подразделение КАК Подразделение,
	|	ТаблицаРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаРасходов.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	// Рассчитаем сумму в валюте управленческого учета
	|	ТаблицаРасходов.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК СуммаСНДС,
	|
	|	ВЫРАЗИТЬ((ТаблицаРасходов.СуммаСНДС - ТаблицаРасходов.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ((ТаблицаРасходов.СуммаСНДС - ТаблицаРасходов.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|
	|    (ТаблицаРасходов.СуммаСНДС - ТаблицаРасходов.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК СуммаСНДСРегл,
	|	
	|	(ТаблицаРасходов.СуммаСНДС - ТаблицаРасходов.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК Реквизиты
	|		ПО ТаблицаРасходов.Ссылка = Реквизиты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ТаблицаРасходов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ТаблицаРасходов.Валюта
	|ГДЕ
	|	ТаблицаРасходов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ТаблицаРасходов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И ТаблицаРасходов.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ ТаблицаРасходов.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаРасходов.Подразделение КАК Подразделение,
	|	ТаблицаРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаРасходов.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|
	|	ВЫРАЗИТЬ(ТаблицаРасходов.СуммаНДС * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|	ТаблицаРасходов.СуммаНДС * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК СуммаСНДСРегл,
	|	ТаблицаРасходов.СуммаНДС * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНДСНаРасходы) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК Реквизиты
	|		ПО ТаблицаРасходов.Ссылка = Реквизиты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ТаблицаРасходов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ТаблицаРасходов.Валюта
	|ГДЕ
	|	ТаблицаРасходов.Ссылка = &Ссылка
	|	И ТаблицаРасходов.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ ТаблицаРасходов.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|
	|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаСНДС,
	|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаБезНДС,
	|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаБезНДСУпр,
	|	
	|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаСНДСРегл,
	|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|
	|ГДЕ
	|	ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	ПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПрочиеРасходы.СтатьяРасходов КАК Статья,
	|	ПрочиеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|
	|	// Рассчитаем сумму в валюте управленческого учета
	|	ВЫРАЗИТЬ(ПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК Сумма
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ПрочиеРасходы.Валюта
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ПрочиеРасходы.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И ПрочиеРасходы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ ПрочиеРасходы.Отменено
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ТекстЗапросаДанныхДокументов = "
	|ВЫБРАТЬ
	|	""ПрочиеРасходы"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	ТаблицаДокумента.Ссылка.Дата КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ОплатаПоставщикам"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""КонвертацияВалюты"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ВЫБОР КОГДА ТаблицаДокумента.ВалютаКонвертации <> &ВалютаРегламентированногоУчета ТОГДА
	|		ТаблицаДокумента.ВалютаКонвертации
	|	ИНАЧЕ
	|		ТаблицаДокумента.Валюта
	|	КОНЕЦ КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА ТаблицаДокумента.ВалютаКонвертации <> &ВалютаРегламентированногоУчета ТОГДА
	|		ТаблицаДокумента.СуммаКонвертации
	|	ИНАЧЕ
	|		ТаблицаДокумента.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов
	|ИЗ
	|	Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.Мультивалютный
	|	И ТаблицаДокумента.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанныхДокументов);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период										КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)		КАК ВидДвижения,
	|	&Организация 								КАК Организация,
	|	ПрочиеРасходы.Подразделение 				КАК Подразделение,
	|	ПрочиеРасходы.НаправлениеДеятельности		КАК НаправлениеДеятельности,
	|	ПрочиеРасходы.СтатьяРасходов				КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов				КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаАктивовПассивов,
	|	&Ссылка 									КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО 								КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|
	|	// Рассчитаем сумму в валюте управленческого учета.
	|	ВЫРАЗИТЬ(ПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) Стоимость,
	|	ВЫРАЗИТЬ((ПрочиеРасходы.СуммаСНДС - ПрочиеРасходы.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	ВЫРАЗИТЬ((ПрочиеРасходы.СуммаСНДС - ПрочиеРасходы.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы) КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ПрочиеРасходы.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ПрочиеРасходы.Валюта
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|	И ПрочиеРасходы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ ПрочиеРасходы.Отменено
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	//++ НЕ УТ
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Период										КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 		КАК ВидДвижения,
	|	&Организация 								КАК Организация,
	|	Расходы.Подразделение 						КАК Подразделение,
	|	Расходы.НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	Расходы.СтатьяРасходов 						КАК СтатьяРасходов,
	|	Расходы.АналитикаРасходов 					КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаАктивовПассивов,
	|	&Ссылка 									КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО 								КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	ВЫРАЗИТЬ((Расходы.СуммаСНДС - Расходы.СуммаНДС) * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) СтоимостьРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК Расходы
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Расходы.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = Расходы.Валюта
	|
	|ГДЕ
	|	Расходы.Ссылка = &Ссылка
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|	И Расходы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ Расходы.Отменено
	|";
	//-- НЕ УТ
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныхСредств(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДенежныхСредств";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	Значение(Перечисление.ХозяйственныеОперации.КонвертацияВалютыПодотчетнымЛицом) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ДенежныеСредства,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКонвертацияВалюты.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаКонвертацияВалюты.Валюта КАК Валюта,
	|
	|	ДанныеДокумента.ПодотчетноеЛицо КАК КорДенежныеСредства,
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК КорТипДенежныхСредств,
	|	ТаблицаКонвертацияВалюты.ВалютаКонвертации КАК КорВалюта,
	|
	|	ВЫРАЗИТЬ(ТаблицаКонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ТаблицаКонвертацияВалюты.СуммаКонвертации * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ТаблицаКонвертацияВалюты.Сумма КАК СуммаВВалюте,
	|	ТаблицаКонвертацияВалюты.СуммаКонвертации КАК СуммаВКорВалюте,
	|
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникКорГФУДенежныхСредств
	|ИЗ
	|	Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаКонвертацияВалюты
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК ДанныеДокумента
	|	ПО ТаблицаКонвертацияВалюты.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ТаблицаКонвертацияВалюты.ВалютаКонвертации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ТаблицаКонвертацияВалюты.ВалютаКонвертации
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДенежныеСредстваДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	Значение(Перечисление.ХозяйственныеОперации.ПрочиеРасходыПодотчетногоЛица) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ТаблицаПрочиеРасходы.Подразделение КАК ПодразделениеДоходовРасходов,
	|
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ДенежныеСредства,
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаПрочиеРасходы.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	
	|	ТаблицаПрочиеРасходы.Валюта КАК Валюта,
	|	
	|	ТаблицаПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ТаблицаПрочиеРасходы.СтатьяРасходов КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	ТаблицаПрочиеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаПрочиеРасходы.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ВЫРАЗИТЬ(ТаблицаПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ТаблицаПрочиеРасходы.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ТаблицаПрочиеРасходы.Сумма КАК СуммаВВалюте,
	|	ВЫРАЗИТЬ(ТаблицаПрочиеРасходы.СуммаНДС * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ТаблицаПрочиеРасходы.СуммаНДС * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2)) КАК СуммаНДСРегл,
	|	ТаблицаПрочиеРасходы.СуммаНДС КАК СуммаНДСВВалютеПлатежа,
	|
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|	ТаблицаПрочиеРасходы.СтатьяРасходов КАК ИсточникГФУДоходовРасходов,
	|	ТаблицаПрочиеРасходы.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаПрочиеРасходы
	|		ПО
	|			ТаблицаПрочиеРасходы.Ссылка = &Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ТаблицаПрочиеРасходы.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ТаблицаПрочиеРасходы.Валюта
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	И НЕ ТаблицаПрочиеРасходы.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Расчет доходов от курсовых разниц
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Подразделение КАК ПодразделениеДоходовРасходов,
	|
	|	&ПодотчетноеЛицо КАК ДенежныеСредства,
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаКурсовыхРазниц.ВалютаКонвертации КАК Валюта,
	|
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиСтатьи,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК СтатьяДоходовРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	
	|	ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
	|	ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
	|	0 КАК СуммаВВалюте,
	
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаНДСРегл,
	|	0 КАК СуммаНДСВВалютеПлатежа,
	|
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК ИсточникГФУДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|
	|ГДЕ
	|	ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Расчет расходов от курсовых разниц
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Подразделение КАК ПодразделениеДоходовРасходов,
	|
	|	&ПодотчетноеЛицо КАК ДенежныеСредства,
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаКурсовыхРазниц.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаКурсовыхРазниц.ВалютаКонвертации КАК Валюта,
	|
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиСтатьи,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|
	|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
	|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
	|	0 КАК СуммаВВалюте,
	
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаНДСРегл,
	|	0 КАК СуммаНДСВВалютеПлатежа,
	|
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК ИсточникГФУДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
	|
	|ИЗ
	|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
	|
	|ГДЕ
	|	ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваКонтрагент(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДенежныеСредстваКонтрагент";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаОплатаПоставщикам", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютУпр", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютУпр(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТКурсыВалютРегл", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКурсыВалютРегл(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|	ДанныеДокумента.ДенежныеСредства КАК ДенежныеСредства,
	|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ВалютаПлатежа КАК ВалютаПлатежа,
	|
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|	
	|	СУММА(ДанныеДокумента.СуммаОплаты) КАК СуммаОплаты,
	|	СУММА(ДанныеДокумента.СуммаОплатыРегл) КАК СуммаОплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаОплатыВВалютеПлатежа) КАК СуммаОплатыВВалютеПлатежа,
	|
	|	СУММА(ДанныеДокумента.СуммаПостоплаты) КАК СуммаПостоплаты,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыРегл) КАК СуммаПостоплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыВВалютеПлатежа) КАК СуммаПостоплатыВВалютеПлатежа,
	|
	|	СУММА(ДанныеДокумента.СуммаПредоплаты) КАК СуммаПредоплаты,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыРегл) КАК СуммаПредоплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыВВалютеПлатежа) КАК СуммаПредоплатыВВалютеПлатежа,
	|
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|
	|	СУММА(ДанныеДокумента.СуммаОплатыВВалютеВзаиморасчетов) КАК СуммаОплатыВВалютеВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыВВалютеВзаиморасчетов) КАК СуммаПостоплатыВВалютеВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыВВалютеВзаиморасчетов) КАК СуммаПредоплатыВВалютеВзаиморасчетов,
	|
	|	ДанныеДокумента.ИсточникГФУДенежныхСредств КАК ИсточникГФУДенежныхСредств,
	|	ДанныеДокумента.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|	ЛОЖЬ КАК ОтложенноеПроведение 
	|
	|ИЗ (
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата КАК Период,
	|		Значение(Перечисление.ХозяйственныеОперации.ОплатаПоставщикуПодотчетнымЛицом) КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.Организация КАК Организация,
	|		ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|		ДанныеДокумента.ПодотчетноеЛицо КАК ДенежныеСредства,
	|		ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|			ТаблицаОплатаПоставщикам.СтатьяДвиженияДенежныхСредств
	|		КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	
	|		ТаблицаОплатаПоставщикам.Валюта КАК ВалютаПлатежа,
	|
	|		ТаблицаОплатаПоставщикам.Поставщик КАК Партнер,
	|		ТаблицаОплатаПоставщикам.Контрагент КАК Контрагент,
	|		ТаблицаОплатаПоставщикам.ОбъектРасчетов.Договор КАК Договор,
	|		ТаблицаОплатаПоставщикам.ОбъектРасчетов КАК ОбъектРасчетов,
	
	|		ВЫБОР КОГДА ТаблицаОплатаПоставщикам.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаОплатаПоставщикам.СуммаВзаиморасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаОплатаПоставщикам.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютУпр.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2))
	|		КОНЕЦ КАК СуммаОплаты,
	
	|		ВЫБОР КОГДА ТаблицаОплатаПоставщикам.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И ТаблицаОплатаПоставщикам.Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаОплатаПоставщикам.СуммаВзаиморасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаОплатаПоставщикам.Сумма * ЕСТЬNULL(ТаблицаКурсыВалютРегл.КоэффициентПересчета, 0) КАК ЧИСЛО(31,2))
	|		КОНЕЦ КАК СуммаОплатыРегл,
	
	|		ТаблицаОплатаПоставщикам.Сумма КАК СуммаОплатыВВалютеПлатежа,
	|
	|		0 КАК СуммаПостоплаты,
	|		0 КАК СуммаПостоплатыРегл,	
	|		0 КАК СуммаПостоплатыВВалютеПлатежа,
	|		
	|		0 КАК СуммаПредоплаты,
	|		0 КАК СуммаПредоплатыРегл,
	|		0 КАК СуммаПредоплатыВВалютеПлатежа,
	|
	|		ТаблицаОплатаПоставщикам.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|
	|		ТаблицаОплатаПоставщикам.СуммаВзаиморасчетов КАК СуммаОплатыВВалютеВзаиморасчетов,
	|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
	|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
	|
	|		НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
	|		ТаблицаОплатаПоставщикам.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|	ИЗ
	|		Документ.АвансовыйОтчет КАК ДанныеДокумента
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТаблицаОплатаПоставщикам
	|		ПО
	|			ДанныеДокумента.Ссылка = ТаблицаОплатаПоставщикам.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютУпр КАК ТаблицаКурсыВалютУпр
	|		ПО ТаблицаКурсыВалютУпр.Валюта = ТаблицаОплатаПоставщикам.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютРегл КАК ТаблицаКурсыВалютРегл
	|		ПО ТаблицаКурсыВалютРегл.Валюта = ТаблицаОплатаПоставщикам.Валюта
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка
	|		И ДанныеДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|	) КАК ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ДенежныеСредства,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ВалютаПлатежа,	
	|	ДанныеДокумента.Партнер,
	|	ДанныеДокумента.Контрагент,
	|	ДанныеДокумента.Договор,
	|	ДанныеДокумента.ОбъектРасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов,
	|	ДанныеДокумента.ИсточникГФУДенежныхСредств,
	|	ДанныеДокумента.ИсточникГФУРасчетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|
	|	ДанныеРегистра.ДенежныеСредства,
	|	ДанныеРегистра.ТипДенежныхСредств,
	|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств,
	|	ДанныеРегистра.ВалютаПлатежа,
	|
	|	ДанныеРегистра.Партнер,
	|	ДанныеРегистра.Контрагент,
	|	ДанныеРегистра.Договор,
	|	ДанныеРегистра.ОбъектРасчетов,
	|	ДанныеРегистра.РасчетныйДокумент,
	|
	|	ДанныеРегистра.СуммаОплаты,
	|	ДанныеРегистра.СуммаОплатыРегл,
	|	ДанныеРегистра.СуммаОплатыВВалютеПлатежа,
	|
	|	ДанныеРегистра.СуммаПостоплаты,
	|	ДанныеРегистра.СуммаПостоплатыРегл,
	|	ДанныеРегистра.СуммаПостоплатыВВалютеПлатежа,
	|
	|	ДанныеРегистра.СуммаПредоплаты,
	|	ДанныеРегистра.СуммаПредоплатыРегл,
	|	ДанныеРегистра.СуммаПредоплатыВВалютеПлатежа,
	|
	|	ДанныеРегистра.ВалютаВзаиморасчетов,
	|
	|	ДанныеРегистра.СуммаОплатыВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаПостоплатыВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаПредоплатыВВалютеВзаиморасчетов,
	|
	|	ДанныеРегистра.ИсточникГФУДенежныхСредств,
	|	ДанныеРегистра.ИсточникГФУРасчетов,
	|	ИСТИНА 
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Регистратор = &Ссылка
	|	И ОтложенноеПроведение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	НЕОПРЕДЕЛЕНО                            КАК Партнер,
	|	НЕОПРЕДЕЛЕНО                            КАК МестоХранения,
	|	&ПодотчетноеЛицо                        КАК Контрагент,
	|	&Подразделение                          КАК Подразделение,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&Статус                                 КАК Статус,
	|	&Автор                                  КАК Ответственный,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО                            КАК Дополнительно,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&СуммаИзрасходовано
	//++ Локализация
	//++ НЕ УТ
	|		+ СУММА(ЕСТЬNULL(ПоступлениеДенежныхДокументов.СуммаДокумента, 0))
	//-- НЕ УТ
	//-- Локализация
	|		+ СУММА(ЕСТЬNULL(ПриобретениеТоваровУслуг.СуммаДокумента, 0)) КАК Сумма,
	|	&Валюта                                 КАК Валюта,
	|	НЕОПРЕДЕЛЕНО                            КАК Договор,
	|	НЕОПРЕДЕЛЕНО                            КАК НаправлениеДеятельности,
	|	&Период                                 КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|	ПО
	|		ПриобретениеТоваровУслуг.АвансовыйОтчет = ДанныеДокумента.Ссылка
	|		И ПриобретениеТоваровУслуг.Проведен
	|		И НЕ &Мультивалютный
	|	
	//++ Локализация
	//++ НЕ УТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеДенежныхДокументов КАК ПоступлениеДенежныхДокументов
	|	ПО
	|		ПоступлениеДенежныхДокументов.АвансовыйОтчет = ДанныеДокумента.Ссылка
	|		И ПоступлениеДенежныхДокументов.Проведен
	|		И НЕ &Мультивалютный
	//-- НЕ УТ
	//-- Локализация
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстОплата = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                  КАК Ссылка,
		|	Таблица.Ссылка.Дата                                             КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                            КАК НомерРегистратора,
		|
		|	Таблица.Поставщик                                               КАК Партнер,
		|	Таблица.Ссылка.Организация                                      КАК Организация,
		|	Таблица.Контрагент                                              КАК Контрагент,
		|	Таблица.ОбъектРасчетов.Договор                                  КАК Договор,
		|	Таблица.ОбъектРасчетов.НаправлениеДеятельности                  КАК НаправлениеДеятельности,
		|	
		|	Таблица.ВалютаВзаиморасчетов                                    КАК ВалютаВзаиморасчетов,
		|	Таблица.ОбъектРасчетов                                          КАК ОбъектРасчетов,
		|	Таблица.Сумма                                                   КАК Сумма,
		|	Таблица.СуммаВзаиморасчетов                                     КАК СуммаВзаиморасчетов,
		|	0                                                               КАК УвеличениеОплачивается,
		|	0                                                               КАК УменьшениеОплачивается,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщику)   КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
		|	Неопределено                                                    КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)                     КАК ФормаОплаты,
		|	Таблица.Валюта                                                  КАК ВалютаДокумента,
		|	Таблица.Ссылка.Дата                                             КАК ДатаКурса,
		|	Неопределено                                                    КАК СвязанныйДокумент
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)";
	
	ВзаиморасчетыСервер.ПроведениеОплатыПоставщику(Запрос, ТекстыЗапроса, Регистры, ТекстОплата);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса)
	
	ТекстВыборкаПоляАналитик = "
	|ВЫБРАТЬ
	|	""ПрочиеРасходы""                        КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки             КАК НомерСтроки,
	|	ТаблицаДокумента.Контрагент.Партнер      КАК Поставщик,
	|	ТаблицаДокумента.Контрагент              КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС               КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НЕ Статьи.Ссылка ЕСТЬ NULL
	|			ТОГДА Статьи.ВидЦенности
	//++ НЕ УТ
	|		КОГДА ТаблицаДокумента.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			  И ТаблицаДокумента.АналитикаАктивовПассивов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|		КОГДА ТаблицаДокумента.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			  И ТаблицаДокумента.АналитикаАктивовПассивов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	//-- НЕ УТ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ КАК ВидЦенности,
	|	0 КАК КодСтроки
	|ПОМЕСТИТЬ ВТПоляАналитикУчетаПартий
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокумента.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО ТаблицаДокумента.СтатьяРасходов = Статьи.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаПартий.ТекстЗапросаВтТаблицаАналитикУчетаПартий(ТекстВыборкаПоляАналитик, Запрос, ТекстыЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры)
    
    ИмяРегистра = "НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено) КАК АналитикаУчетаПоПартнерам,
	|                        
	|	Неопределено КАК ОбъектРасчетов,	
	|
	|	&ВидПоставки КАК ВидПоставки,
	|
	|	ПрочиеРасходы.СтавкаНДС КАК СтавкаНДС,
	|	ПрочиеРасходы.НалоговоеНазначение КАК НалоговоеНазначение,
	|	СУММА(ПрочиеРасходы.СуммаСНДС) КАК СуммаВзаиморасчетов,
	|	СУММА(ВЫБОР КОГДА (НЕ ПрочиеРасходы.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)) ТОГДА
	|		(ПрочиеРасходы.СуммаНДС - ПрочиеРасходы.СуммаНДСПропорционально)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК СуммаНДСКредит,
	|
	|	НЕОПРЕДЕЛЕНО КАК ДатаВходящегоНалоговогоДокумента,
	|	&Ссылка КАК ДокументПоставки
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация  = Аналитика.Организация
	|		И ПрочиеРасходы.Контрагент = Аналитика.Контрагент
 	|		И ЕСТЬNULL(ПрочиеРасходы.Контрагент.Партнер,ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) = Аналитика.Партнер
	|		И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = Аналитика.Договор
	|		И ПрочиеРасходы.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|		
	|ГДЕ
	|   &ОрганизацияПлательщикНДС
	|   И &ВалютаРегламентированногоУчета = &Валюта
	|	И ПрочиеРасходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НеНДС)
	|	И ПрочиеРасходы.Ссылка = &Ссылка
	|	И НЕ ПрочиеРасходы.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено),
	|	ПрочиеРасходы.СтавкаНДС,
	|	ПрочиеРасходы.НалоговоеНазначение
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаНДСРасчетНалоговогоКредита(Запрос, ТекстыЗапроса, Регистры)
    
    ИмяРегистра = "НДСРасчетНалоговогоКредита";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено) КАК АналитикаУчетаПоПартнерам,
	|
	|	Неопределено КАК ОбъектРасчетов,	
	|
	|	&ВидПоставки КАК ВидПоставки,
	|
	|	&Ссылка КАК ДокументПоставки,
	|	ЗНАЧЕНИЕ(Перечисление.МоментыОпределенияНалоговойБазы.ПустаяСсылка) КАК МоментОпределенияБазыНДС,
	|
	|	СУММА(ПрочиеРасходы.СуммаСНДС) КАК СуммаПоставкиТребующаяРегистрацииНН,
	|   0 КАК СуммаПоставкиНеТребующаяРегистрацииНН
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация  = Аналитика.Организация
	|		И ПрочиеРасходы.Контрагент = Аналитика.Контрагент
 	|		И ЕСТЬNULL(ПрочиеРасходы.Контрагент.Партнер,ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) = Аналитика.Партнер
	|		И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = Аналитика.Договор
	|		И ПрочиеРасходы.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	
	|ГДЕ
	|	&ОрганизацияПлательщикНДС
	|   И &ВалютаРегламентированногоУчета = &Валюта
	|	И ПрочиеРасходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НеНДС)
	|	И ПрочиеРасходы.Ссылка = &Ссылка
	|	И НЕ ПрочиеРасходы.Отменено
	|	
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.ЗаявкаНаРасходованиеДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.РасходныйКассовыйОрдер.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.СписаниеБезналичныхДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	АвансовыйОтчетЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

#КонецОбласти

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.АвансовыйОтчет) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.АвансовыйОтчет.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.АвансовыйОтчет);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	АвансовыйОтчетЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Авансовый отчет
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АвансовыйОтчет1";
	КомандаПечати.Представление = НСтр("ru='Авансовый отчет';uk='Авансовий звіт'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	//++ НЕ УТ
	КомандаПечати.Обработчик = "РеглУчетКлиент.ПечатьСРасширеннойПроверкойОтраженияВРеглУчете";
	//-- НЕ УТ
	
	АвансовыйОтчетЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	

	АвансовыйОтчетЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет1")
		Или УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет2")
		Или УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет3")
		Или УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет4") Тогда
		
		ДатаНачалаПечатиЕдиногоАвансовогоОтчета = Константы.ДатаНачалаПечатиЕдиногоАвансовогоОтчета.Получить();
		ЕдиныйАвансовыйОтчетБезусловно = Не Константы.ВидимостьДатыНачалаПечатиЕдиногоАвансовогоОтчета.Получить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивОбъектов)
		|	И (ДанныеДокумента.Дата >= &ДатаНачалаПечатиЕдиногоАвансовогоОтчета
		|		И &ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана
		|		ИЛИ &ЕдиныйАвансовыйОтчетБезусловно)
		|";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.УстановитьПараметр("ДатаНачалаПечатиЕдиногоАвансовогоОтчета", ДатаНачалаПечатиЕдиногоАвансовогоОтчета);
		Запрос.УстановитьПараметр("ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана", ЗначениеЗаполнено(ДатаНачалаПечатиЕдиногоАвансовогоОтчета));
		Запрос.УстановитьПараметр("ЕдиныйАвансовыйОтчетБезусловно", ЕдиныйАвансовыйОтчетБезусловно);
		
		НовыеАвансовыеОтчеты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		Если НовыеАвансовыеОтчеты.Количество() Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"АвансовыйОтчет1",
				НСтр("ru='Авансовый отчет';uk='Авансовий звіт'"),
				СформироватьПечатнуюФормуАвансовогоОтчета(НовыеАвансовыеОтчеты, ОбъектыПечати, ПараметрыПечати));
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивОбъектов)
		|	И (ДанныеДокумента.Дата < &ДатаНачалаПечатиЕдиногоАвансовогоОтчета
		|		ИЛИ НЕ &ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана
		|			И НЕ &ЕдиныйАвансовыйОтчетБезусловно)
		|;
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивОбъектов)
		|	И (ДанныеДокумента.Дата < &ДатаНачалаПечатиЕдиногоАвансовогоОтчета
		|		ИЛИ НЕ &ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана
		|			И НЕ &ЕдиныйАвансовыйОтчетБезусловно)
		|;
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивОбъектов)
		|;
		//++ НЕ УТ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивОбъектов)
		|	И (ДанныеДокумента.Дата < &ДатаНачалаПечатиЕдиногоАвансовогоОтчета
		|		ИЛИ НЕ &ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана
		|			И НЕ &ЕдиныйАвансовыйОтчетБезусловно)
		|;
		//-- НЕ УТ
		|";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.УстановитьПараметр("ДатаНачалаПечатиЕдиногоАвансовогоОтчета", ДатаНачалаПечатиЕдиногоАвансовогоОтчета);
		Запрос.УстановитьПараметр("ДатаНачалаПечатиЕдиногоАвансовогоОтчетаУказана", ЗначениеЗаполнено(ДатаНачалаПечатиЕдиногоАвансовогоОтчета));
		Запрос.УстановитьПараметр("ЕдиныйАвансовыйОтчетБезусловно", ЕдиныйАвансовыйОтчетБезусловно);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		Для Инд = 0 По РезультатЗапроса.ВГраница() Цикл
			СтарыеАвансовыеОтчеты = РезультатЗапроса[Инд].Выгрузить().ВыгрузитьКолонку("Ссылка");
			Если СтарыеАвансовыеОтчеты.Количество() Тогда
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм,
					"АвансовыйОтчет" + Строка(Инд + 1),
					НСтр("ru='Авансовый отчет';uk='Авансовий звіт'"),
					СформироватьПечатнуюФормуАвансовогоОтчетаСтараяВерсия(СтарыеАвансовыеОтчеты, ОбъектыПечати, ПараметрыПечати));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуАвансовогоОтчета(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ШаблонОшибки = НСтр("ru='Печать авансового отчета используется только для документов с операцией ""Закупка через подотчетное лицо"".';uk='Друк авансового звіту використовується тільки для документів з операцією ""Купівля через підзвітну особу"".'");
	
	ИспользуетсяРеглУчет = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АвансовыйОтчет";

	ПервыйДокумент = Истина;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.Мультивалютный = ЛОЖЬ";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	МассивОбъектовОднаВалюта = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.Мультивалютный = ИСТИНА";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	МассивОбъектовНесколькоВалют = Запрос.Выполнить().Выгрузить();
	Если ЗначениеЗаполнено(МассивОбъектовОднаВалюта) Тогда 
	
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = ТекстЗапросаПечати(ИспользуетсяРеглУчет, Ложь);	
		Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектовОднаВалюта);
		Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		
		РегистраторыДенежныеСредстваВыдано = ВводОстатковСервер.ДоступныеТипыВводаОстатков();
		РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		//++ НЕ УТ
		РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.ВыбытиеДенежныхДокументов"));
		//-- НЕ УТ
		Запрос.УстановитьПараметр("РегистраторыДенежныеСредстваВыдано", РегистраторыДенежныеСредстваВыдано);
		
		//++ НЕ УТ
		МассивНеОтраженныхДокументов = Неопределено;
		Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
			ПараметрыПечати.Свойство("ДокументыБезПроводок", МассивНеОтраженныхДокументов);
		КонецЕсли;
		Запрос.УстановитьПараметр("МассивНеОтраженныхДокументов", МассивНеОтраженныхДокументов);
		//-- НЕ УТ
		
		УстановитьПривилегированныйРежим(Истина);
		МассивРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		УстановитьПривилегированныйРежим(Ложь);
		
		Выборка = МассивРезультатов[13].Выбрать();                                                    
		ВыборкаПоказатели = МассивРезультатов[12].Выбрать();
		ВыборкаКонвертация = МассивРезультатов[15].Выбрать();
		ВыборкаОборотнаяСторона = МассивРезультатов[14].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ТаблицаПолученныхАвансов = МассивРезультатов[16].Выгрузить();

		Если ИспользуетсяРеглУчет Тогда
			Проводки = МассивРезультатов[19].Выгрузить();
		Иначе
			Проводки = МассивРезультатов[17].Выгрузить();
		КонецЕсли;
		
		Если МассивРезультатов[13].Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ШаблонОшибки);
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
		
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
 			Если Выборка.ДатаДокумента >=  Дата("20230713") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет01072023");
	 		ИначеЕсли Выборка.ДатаДокумента >=  Дата("20160419000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет19042016");
			ИначеЕсли Выборка.ДатаДокумента >=  Дата("20151106000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет06112015");
			ИначеЕсли Выборка.ДатаДокумента >=  Дата("20140204000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет04022014");
			Иначе
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет");
			КонецЕсли;                 
			
			КодЯзыкаПечать = "uk"; // только на украинском
			
			Если ПервыйДокумент Тогда
				ПервыйДокумент = Ложь;
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			
			ОбластьМакета.Параметры.Заполнить(Выборка);

			ПараметрыЗаголовка = Новый Структура;
			ПараметрыЗаголовка.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер));
			
			СведенияОбОрганизации	 = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаДокумента);	
			ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование",,КодЯзыкаПечать);
			
			ПараметрыЗаголовка.Вставить("Дата", Выборка.ДатаДокумента);
			ПараметрыЗаголовка.Вставить("ДатаКоротко", Формат(Выборка.ДатаДокумента, "ДФ=dd.MM.yyyy"));
			ПараметрыЗаголовка.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
			
			Если Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоЕДРПОУ,",Ложь);
			Иначе
				ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоДРФО,",Ложь);
			КонецЕсли;
			Для Инд = 1 По 8 Цикл
				ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
			КонецЦикла; 
			// если организация - физ.лицо нужно "добавить" две ячейки
			Если НЕ Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				Для Инд = 9 По 10 Цикл
					ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
				КонецЦикла; 
				Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
				ОбластьМакета.Область("КодОрганизации9").Обвести(Линия,Линия,Линия,Линия);
				ОбластьМакета.Область("КодОрганизации10").Обвести(Линия,Линия,Линия,Линия);
			КонецЕсли;
			
			//++ НЕ УТ
			ПараметрыПолученияДанных = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
			ПараметрыПолученияДанных.Организация = Выборка.Организация;
			ПараметрыПолученияДанных.НачалоПериода = Выборка.ДатаДокумента;
			ПараметрыПолученияДанных.ОкончаниеПериода = Выборка.ДатаДокумента;
			ПараметрыПолученияДанных.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ПодотчетноеЛицо);
			
			Сотрудники = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияДанных);
			
			ПолучаемыеДанные = Новый Массив;
			ПолучаемыеДанные.Добавить("Должность");
			ПолучаемыеДанные.Добавить("ТабельныйНомер");

			КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина, // Только разрешенные 
				Сотрудники.ВыгрузитьКолонку("Сотрудник"), 
				ПолучаемыеДанные, 
				Выборка.ДатаДокумента);
				
			Если КадровыеДанные.Количество() > 0 Тогда
				ПараметрыЗаголовка.Вставить("ДолжностьПодотчетногоЛицаПредставление", КадровыеДанные[0].Должность);
			КонецЕсли;
			//-- НЕ УТ
			
			ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛица", Выборка.ПредставлениеПодотчетногоЛица);
			
			ПодотчетноеЛицоФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Выборка.ПредставлениеПодотчетногоЛица)," ");
				
			КоличествоПодстрок = ПодотчетноеЛицоФИО.Количество();
			ПодотчетноеЛицоФамилия            = ?(КоличествоПодстрок > 0,ПодотчетноеЛицоФИО[0],"");
			ПодотчетноеЛицоИмя                = ?(КоличествоПодстрок > 1,ПодотчетноеЛицоФИО[1],"");
			ПодотчетноеЛицоОтчество           = ?(КоличествоПодстрок > 2,ПодотчетноеЛицоФИО[2],"");
			
			ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛицаКратко", ПодотчетноеЛицоФамилия + ?(ПодотчетноеЛицоИмя = "" , "" , " " + Лев(ПодотчетноеЛицоИмя, 1) + ".") + ?(ПодотчетноеЛицоОтчество = "", "", Лев(ПодотчетноеЛицоОтчество, 1) + "."));
			
			Для Инд = 1 По 10 Цикл
				ПараметрыЗаголовка.Вставить("ДРФОПодотчетногоЛица_" + Инд, Сред(Выборка.ДРФОПодотчетногоЛица, Инд, 1));
			КонецЦикла;
			
			СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
			
			ПолученоИзКассы = 0; ПолученоПоБанковскимКартам = 0;
			ПолученоИзКассыВВалюте = ""; ПолученоПоБанковскимКартамВВалюте = "";
			НачальныйОстаток = 0; НачальныйПерерасход = 0;
			КонечныйОстаток = 0; КонечныйПерерасход = 0;
			Израсходовано = 0;

				Пока ВыборкаПоказатели.НайтиСледующий(СтруктураПоиска) Цикл
					Если Выборка.Валюта <> ВыборкаПоказатели.Валюта Тогда
		            	Продолжить;
					КонецЕсли;
					Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
						ПолученоИзКассы = ПолученоИзКассы + ВыборкаПоказатели.СуммаВыданоНаличнымиРегл;
						ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам + ВыборкаПоказатели.СуммаВыданоКартойРегл;
						
						НачальныйОстаток = НачальныйОстаток + ВыборкаПоказатели.НачальныйОстатокРегл;
						НачальныйПерерасход = НачальныйПерерасход + ВыборкаПоказатели.НачальныйПерерасходРегл;
						КонечныйОстаток = КонечныйОстаток + ВыборкаПоказатели.КонечныйОстатокРегл;
						КонечныйПерерасход = КонечныйПерерасход + ВыборкаПоказатели.КонечныйПерерасходРегл;
						Израсходовано = Израсходовано + ВыборкаПоказатели.СуммаИзрасходованоРегл;
					Иначе 
						ПолученоИзКассы = ПолученоИзКассы + ВыборкаПоказатели.СуммаВыданоНаличными;
						ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам + ВыборкаПоказатели.СуммаВыданоКартой;
						
						НачальныйОстаток = НачальныйОстаток + ВыборкаПоказатели.НачальныйОстаток;
						НачальныйПерерасход = НачальныйПерерасход + ВыборкаПоказатели.НачальныйПерерасход;
						КонечныйОстаток = КонечныйОстаток + ВыборкаПоказатели.КонечныйОстаток;
						КонечныйПерерасход = КонечныйПерерасход + ВыборкаПоказатели.КонечныйПерерасход;
						Израсходовано = Израсходовано + ВыборкаПоказатели.СуммаИзрасходовано;					
					КонецЕсли;
				КонецЦикла;
				
				ВыборкаПоказатели.Сбросить();
				
				ВыводитьВалюту = Истина;
				Если НачальныйПерерасход > 0 И (ПолученоИзКассы > 0 Или ПолученоПоБанковскимКартам > 0) Тогда
					ВыводитьВалюту = Ложь;
				КонецЕсли;
				
				Если НачальныйПерерасход > 0 Тогда
					СуммаВозмещения = Мин(НачальныйПерерасход, ПолученоИзКассы);
					ПолученоИзКассы = ПолученоИзКассы - СуммаВозмещения;
					НачальныйПерерасход = НачальныйПерерасход - СуммаВозмещения;
				КонецЕсли;
				
				Если НачальныйПерерасход > 0 Тогда
					СуммаВозмещения = Мин(НачальныйПерерасход, ПолученоПоБанковскимКартам);
					ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам - СуммаВозмещения;
					НачальныйПерерасход = НачальныйПерерасход - СуммаВозмещения;
				КонецЕсли;
				
				ИтогоПолучено = ПолученоИзКассы + ПолученоПоБанковскимКартам; 
				
				ПараметрыЗаголовка.Вставить("ИтогВсегоПолучено", ИтогоПолучено);
				ПараметрыЗаголовка.Вставить("НачальныйОстаток", НачальныйОстаток);
				ПараметрыЗаголовка.Вставить("НачальныйПерерасход", НачальныйПерерасход);
				ПараметрыЗаголовка.Вставить("КонечныйОстаток", КонечныйОстаток);
				ПараметрыЗаголовка.Вставить("КонечныйПерерасход", КонечныйПерерасход);
				ПараметрыЗаголовка.Вставить("Израсходовано", Израсходовано);
				
				ОстатокНаКонец = КонечныйОстаток - КонечныйПерерасход;
				ПараметрыЗаголовка.Вставить("ВалютаЗаголовокКолонки", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
				Если ОстатокНаКонец >= 0  Тогда
					//Остаток надо возвращать в валюте
					ПараметрыЗаголовка.Вставить("ВалютаОстатокПерерасход", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
				Иначе
					//Перерасход надо возвращать в гривнях
					ПараметрыЗаголовка.Вставить("ВалютаОстатокПерерасход", "грн,коп.");
				КонецЕсли;

				СуммаДокументаФормат = Формат(Израсходовано, "ЧЦ=15; ЧДЦ=2;");
				ПараметрыЗаголовка.Вставить("СуммаСНДСЧислом", СуммаДокументаФормат);
				ПараметрыЗаголовка.Вставить("СуммаДокумента", СуммаДокументаФормат);
				ПараметрыЗаголовка.Вставить("ВалютаДокумента", Выборка.Валюта);
				ПараметрыЗаголовка.Вставить("СуммаОтчетПодтверждаю", РаботаСКурсамиВалют.СформироватьСуммуПрописью(
					Израсходовано, 
					Выборка.Валюта,
					Ложь, // ВыводитьСуммуБезКопеек
					КодЯзыкаПечать 
				));
				
				ПараметрыЗаголовка.Вставить("ВалютаДокументаРасписка",?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн, коп",Строка(Выборка.Валюта)));
						
				Счетчик = 0;
				Проводки.Свернуть("Регистратор, СчетДт, СчетКт, Валюта", "Сумма");
				СтруктураПоиска = Новый Структура("Регистратор, Валюта", Выборка.Ссылка, Выборка.Валюта);
				НайденныеПроводки = Проводки.НайтиСтроки(СтруктураПоиска);
				Для каждого Проводка из НайденныеПроводки Цикл
					Счетчик = Счетчик + 1;
					Если Счетчик < 11 Тогда
						ПараметрыЗаголовка.Вставить("СчетДт"  + Счетчик, Проводка.СчетДт);
						ПараметрыЗаголовка.Вставить("СчетКт" + Счетчик, Проводка.СчетКт);
						ПараметрыЗаголовка.Вставить("Сумма" + Счетчик, Проводка.Сумма);
						ПараметрыЗаголовка.Вставить("СуммаБезКопеек" + Счетчик, Цел(Проводка.Сумма));
						ПараметрыЗаголовка.Вставить("СуммаКопейки" + Счетчик, Формат((Проводка.Сумма - Цел(Проводка.Сумма)) * 100, "ЧН=00"));
					Иначе
						Если Счетчик = 11 Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				Индекс=1;
				СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);			
				НайденныеТаблицыПолученныхАвансов = ТаблицаПолученныхАвансов.НайтиСтроки(СтруктураПоиска);
				Для Каждого ПолученыйАванс Из НайденныеТаблицыПолученныхАвансов Цикл	
					
					Если Выборка.Валюта <> ПолученыйАванс.Валюта Тогда
		            	Продолжить;
					КонецЕсли;
			
					Если ПолученыйАванс.СуммаВыданоНаличными > 0  Или ПолученыйАванс.СуммаВыданоНаличнымиРегл > 0 Тогда
						//++ НЕ УТ
						Если ТипЗнч(ПолученыйАванс.Регистратор) = Тип("ДокументСсылка.ВыбытиеДенежныхДокументов") Тогда
							ДокументАванса = "Видаток грошових документів №" + 
									ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПолученыйАванс.РегистраторНомер,Ложь,Истина) + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));
						Иначе
						//-- НЕ УТ
							ДокументАванса = "Видатковий касовий ордер №" + ПолученыйАванс.РегистраторНомер + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));
						//++ НЕ УТ
						КонецЕсли; 
						//-- НЕ УТ
					
					ИначеЕсли  ПолученыйАванс.СуммаВыданоКартой > 0 Или ПолученыйАванс.СуммаВыданоКартойРегл > 0 Тогда
						ДокументАванса = "Платіжне доручення вихідне №" + ПолученыйАванс.РегистраторНомер + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));					
					Иначе
					     Продолжить;
					КонецЕсли;
					
					ПараметрыЗаголовка.Вставить("Документ" + Индекс, ДокументАванса);

					Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
						ПараметрыЗаголовка.Вставить("Получено" + Индекс, ПолученыйАванс.СуммаВыданоНаличнымиРегл + ПолученыйАванс.СуммаВыданоКартойРегл);
					Иначе
						ПараметрыЗаголовка.Вставить("Получено" + Индекс, ПолученыйАванс.СуммаВыданоНаличными + ПолученыйАванс.СуммаВыданоКартой);
					КонецЕсли;  
					
					Индекс=Индекс+1;
					
					Если Индекс = 4 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;

				ОбластьМакета.Параметры.Заполнить(ПараметрыЗаголовка);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Оборотная сторона
				ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				
				ПараметрыТаблицы = Новый Структура;
				ПараметрыТаблицы.Вставить("ВалютаЗаголовокКолонки", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
				ОбластьМакета.Параметры.Заполнить(ПараметрыТаблицы);
			
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				
				ИтогоПоОтчету = 0;
				ИтогоПоОтчетуВВалюте = 0;
				
				НомерСтроки = 0;
				
				СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
				ВыборкаОборотнаяСторона.НайтиСледующий(СтруктураПоиска);
				
				ОбработанныеНомераСтрок = Новый Массив;
				ТекущийРаздел = 0;
				
				ВыборкаПоРасходам = ВыборкаОборотнаяСторона.Выбрать();
				Пока ВыборкаПоРасходам.Следующий() Цикл
					
					Если ВыборкаПоРасходам.Валюта <> Выборка.Валюта Тогда
						Продолжить;	
					КонецЕсли; 

					
					Если ОбработанныеНомераСтрок.Найти(ВыборкаПоРасходам.НомерСтроки) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ОбработанныеНомераСтрок.Добавить(ВыборкаПоРасходам.НомерСтроки);
					
					ОбластьМакета.Параметры.Заполнить(ВыборкаПоРасходам);
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ОбластьМакета.Параметры.Дата = ВыборкаПоРасходам.ДокументДата; 
					
					ОснованиеПлатежа = СокрЛП(ВыборкаПоРасходам.НаименованиеРасхода);
						
					
					//++ НЕ УТ
					Если ТипЗнч(ВыборкаПоРасходам.Ссылка) <> Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
						ОснованиеПлатежа = ОснованиеПлатежа + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоРасходам.ДокументНомер);		
					КонецЕсли;
					//-- НЕ УТ

					 ОбластьМакета.Параметры.ОснованиеПлатежа = ОснованиеПлатежа;
					
					Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
						ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ПоОтчету;
						ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ПоОтчету;
					Иначе
						ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ПоОтчетуВВалюте;
						ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ПоОтчетуВВалюте;
					КонецЕсли; 

					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				
				// Выводим подвал авансового отчета
				ОбластьМакета = Макет.ПолучитьОбласть("Итого");
				
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ОбластьМакета.Параметры.СуммаСНДС = ИтогоПоОтчету;
				ТабличныйДокумент.Вывести(ОбластьМакета);

				ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				ВыборкаОборотнаяСторона.Сбросить();
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент,
				НомерСтрокиНачало,
				ОбъектыПечати,
				Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(МассивОбъектовНесколькоВалют) Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = ТекстЗапросаПечати(ИспользуетсяРеглУчет, Истина);	
		Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектовНесколькоВалют);
		Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		
		РегистраторыДенежныеСредстваВыдано = ВводОстатковСервер.ДоступныеТипыВводаОстатков();
		РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		//++ НЕ УТ
		РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.ВыбытиеДенежныхДокументов"));
		//-- НЕ УТ
		Запрос.УстановитьПараметр("РегистраторыДенежныеСредстваВыдано", РегистраторыДенежныеСредстваВыдано);
		
		//++ НЕ УТ
		МассивНеОтраженныхДокументов = Неопределено;
		Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
			ПараметрыПечати.Свойство("ДокументыБезПроводок", МассивНеОтраженныхДокументов);
		КонецЕсли;
		Запрос.УстановитьПараметр("МассивНеОтраженныхДокументов", МассивНеОтраженныхДокументов);
		//-- НЕ УТ
		
		УстановитьПривилегированныйРежим(Истина);
		МассивРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		УстановитьПривилегированныйРежим(Ложь);
		
		Выборка = МассивРезультатов[13].Выбрать();                                                    
		ВыборкаПоказатели = МассивРезультатов[12].Выбрать();
		ВыборкаКонвертация = МассивРезультатов[15].Выбрать();
		ВыборкаОборотнаяСторона = МассивРезультатов[14].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ТаблицаПолученныхАвансов = МассивРезультатов[16].Выгрузить();

		Если ИспользуетсяРеглУчет Тогда
			Проводки = МассивРезультатов[19].Выгрузить();
		Иначе
			Проводки = МассивРезультатов[17].Выгрузить();
		КонецЕсли;
		
		Если МассивРезультатов[13].Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ШаблонОшибки);
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
		
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
 			Если Выборка.ДатаДокумента >=  Дата("20230713") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет01072023");
	 		ИначеЕсли Выборка.ДатаДокумента >=  Дата("20160419000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет19042016");
			ИначеЕсли Выборка.ДатаДокумента >=  Дата("20151106000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет06112015");
			ИначеЕсли Выборка.ДатаДокумента >=  Дата("20140204000000") Тогда
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет04022014");
			Иначе
				Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет");
			КонецЕсли;                 
			
			КодЯзыкаПечать = "uk"; // только на украинском
			
			Если ПервыйДокумент Тогда
				ПервыйДокумент = Ложь;
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			
			ОбластьМакета.Параметры.Заполнить(Выборка);

			ПараметрыЗаголовка = Новый Структура;
			ПараметрыЗаголовка.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер));
			
			СведенияОбОрганизации	 = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаДокумента);	
			ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование",,КодЯзыкаПечать);
			
			ПараметрыЗаголовка.Вставить("Дата", Выборка.ДатаДокумента);
			ПараметрыЗаголовка.Вставить("ДатаКоротко", Формат(Выборка.ДатаДокумента, "ДФ=dd.MM.yyyy"));
			ПараметрыЗаголовка.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
			
			Если Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоЕДРПОУ,",Ложь);
			Иначе
				ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоДРФО,",Ложь);
			КонецЕсли;
			Для Инд = 1 По 8 Цикл
				ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
			КонецЦикла; 
			// если организация - физ.лицо нужно "добавить" две ячейки
			Если НЕ Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				Для Инд = 9 По 10 Цикл
					ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
				КонецЦикла; 
				Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
				ОбластьМакета.Область("КодОрганизации9").Обвести(Линия,Линия,Линия,Линия);
				ОбластьМакета.Область("КодОрганизации10").Обвести(Линия,Линия,Линия,Линия);
			КонецЕсли;
			
			//++ НЕ УТ
			ПараметрыПолученияДанных = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
			ПараметрыПолученияДанных.Организация = Выборка.Организация;
			ПараметрыПолученияДанных.НачалоПериода = Выборка.ДатаДокумента;
			ПараметрыПолученияДанных.ОкончаниеПериода = Выборка.ДатаДокумента;
			ПараметрыПолученияДанных.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ПодотчетноеЛицо);
			
			Сотрудники = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияДанных);
			
			ПолучаемыеДанные = Новый Массив;
			ПолучаемыеДанные.Добавить("Должность");
			ПолучаемыеДанные.Добавить("ТабельныйНомер");

			КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина, // Только разрешенные 
				Сотрудники.ВыгрузитьКолонку("Сотрудник"), 
				ПолучаемыеДанные, 
				Выборка.ДатаДокумента);
				
			Если КадровыеДанные.Количество() > 0 Тогда
				ПараметрыЗаголовка.Вставить("ДолжностьПодотчетногоЛицаПредставление", КадровыеДанные[0].Должность);
			КонецЕсли;
			//-- НЕ УТ
			
			ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛица", Выборка.ПредставлениеПодотчетногоЛица);
			
			ПодотчетноеЛицоФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Выборка.ПредставлениеПодотчетногоЛица)," ");
				
			КоличествоПодстрок = ПодотчетноеЛицоФИО.Количество();
			ПодотчетноеЛицоФамилия            = ?(КоличествоПодстрок > 0,ПодотчетноеЛицоФИО[0],"");
			ПодотчетноеЛицоИмя                = ?(КоличествоПодстрок > 1,ПодотчетноеЛицоФИО[1],"");
			ПодотчетноеЛицоОтчество           = ?(КоличествоПодстрок > 2,ПодотчетноеЛицоФИО[2],"");
			
			ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛицаКратко", ПодотчетноеЛицоФамилия + ?(ПодотчетноеЛицоИмя = "" , "" , " " + Лев(ПодотчетноеЛицоИмя, 1) + ".") + ?(ПодотчетноеЛицоОтчество = "", "", Лев(ПодотчетноеЛицоОтчество, 1) + "."));
			
			Для Инд = 1 По 10 Цикл
				ПараметрыЗаголовка.Вставить("ДРФОПодотчетногоЛица_" + Инд, Сред(Выборка.ДРФОПодотчетногоЛица, Инд, 1));
			КонецЦикла;
			
			СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
			
			ПолученоИзКассы = 0; ПолученоПоБанковскимКартам = 0;
			ПолученоИзКассыВВалюте = ""; ПолученоПоБанковскимКартамВВалюте = "";
			НачальныйОстаток = 0; НачальныйПерерасход = 0;
			КонечныйОстаток = 0; КонечныйПерерасход = 0;
			Израсходовано = 0;
			
				Пока ВыборкаПоказатели.НайтиСледующий(СтруктураПоиска) Цикл
					
					ПолученоИзКассы = ПолученоИзКассы + ВыборкаПоказатели.СуммаВыданоНаличнымиРегл;
					ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам + ВыборкаПоказатели.СуммаВыданоКартойРегл;
					
					НачальныйОстаток = НачальныйОстаток + ВыборкаПоказатели.НачальныйОстатокРегл;
					НачальныйПерерасход = НачальныйПерерасход + ВыборкаПоказатели.НачальныйПерерасходРегл;
					КонечныйОстаток = КонечныйОстаток + ВыборкаПоказатели.КонечныйОстатокРегл;
					КонечныйПерерасход = КонечныйПерерасход + ВыборкаПоказатели.КонечныйПерерасходРегл;
					Израсходовано = Израсходовано + ВыборкаПоказатели.СуммаИзрасходованоРегл;
					
				КонецЦикла;
				
				ВыборкаПоказатели.Сбросить();
				
				ВыводитьВалюту = Истина;
				Если НачальныйПерерасход > 0 И (ПолученоИзКассы > 0 Или ПолученоПоБанковскимКартам > 0) Тогда
					ВыводитьВалюту = Ложь;
				КонецЕсли;
				
				Если НачальныйПерерасход > 0 Тогда
					СуммаВозмещения = Мин(НачальныйПерерасход, ПолученоИзКассы);
					ПолученоИзКассы = ПолученоИзКассы - СуммаВозмещения;
					НачальныйПерерасход = НачальныйПерерасход - СуммаВозмещения;
				КонецЕсли;
				
				Если НачальныйПерерасход > 0 Тогда
					СуммаВозмещения = Мин(НачальныйПерерасход, ПолученоПоБанковскимКартам);
					ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам - СуммаВозмещения;
					НачальныйПерерасход = НачальныйПерерасход - СуммаВозмещения;
				КонецЕсли;
				
				ИтогоПолучено = ПолученоИзКассы + ПолученоПоБанковскимКартам; 
				
				ПараметрыЗаголовка.Вставить("ИтогВсегоПолучено", ИтогоПолучено);
				ПараметрыЗаголовка.Вставить("НачальныйОстаток", НачальныйОстаток);
				ПараметрыЗаголовка.Вставить("НачальныйПерерасход", НачальныйПерерасход);
				ПараметрыЗаголовка.Вставить("КонечныйОстаток", КонечныйОстаток);
				ПараметрыЗаголовка.Вставить("КонечныйПерерасход", КонечныйПерерасход);
				ПараметрыЗаголовка.Вставить("Израсходовано", Израсходовано);
				
				ОстатокНаКонец = КонечныйОстаток - КонечныйПерерасход;
				ПараметрыЗаголовка.Вставить("ВалютаОстатокПерерасход", "грн,коп.");
				ПараметрыЗаголовка.Вставить("ВалютаЗаголовокКолонки", "грн,коп.");

				СуммаДокументаФормат = Формат(Израсходовано, "ЧЦ=15; ЧДЦ=2;");
				ПараметрыЗаголовка.Вставить("СуммаСНДСЧислом", СуммаДокументаФормат);
				ПараметрыЗаголовка.Вставить("СуммаДокумента", СуммаДокументаФормат);
				ПараметрыЗаголовка.Вставить("ВалютаДокумента", "грн, коп");
				ПараметрыЗаголовка.Вставить("СуммаОтчетПодтверждаю", РаботаСКурсамиВалют.СформироватьСуммуПрописью(
					Израсходовано, 
					ВалютаРегламентированногоУчета,
					Ложь, // ВыводитьСуммуБезКопеек
					КодЯзыкаПечать 
				));
				
				ПараметрыЗаголовка.Вставить("ВалютаДокументаРасписка","грн, коп");
						
				Счетчик = 0;
				Проводки.Свернуть("Регистратор, СчетДт, СчетКт, Валюта", "Сумма, СуммаБУ");

				СтруктураПоиска = Новый Структура("Регистратор", Выборка.Ссылка);			
				НайденныеПроводки = Проводки.НайтиСтроки(СтруктураПоиска);
				Для каждого Проводка из НайденныеПроводки Цикл
					Счетчик = Счетчик + 1;
					Если Счетчик < 11 Тогда
						ПараметрыЗаголовка.Вставить("СчетДт"  + Счетчик, Проводка.СчетДт);
						ПараметрыЗаголовка.Вставить("СчетКт"    + Счетчик, Проводка.СчетКт);
						ПараметрыЗаголовка.Вставить("Сумма" + Счетчик, Проводка.СуммаБУ);
						ПараметрыЗаголовка.Вставить("СуммаБезКопеек" + Счетчик, Цел(Проводка.СуммаБУ));
						ПараметрыЗаголовка.Вставить("СуммаКопейки"   + Счетчик, Формат((Проводка.СуммаБУ - Цел(Проводка.СуммаБУ)) * 100, "ЧН=00"));
					Иначе
						Если Счетчик = 11 Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Индекс=1;
				
				СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);			
				НайденныеТаблицыПолученныхАвансов = ТаблицаПолученныхАвансов.НайтиСтроки(СтруктураПоиска);
				Для Каждого ПолученыйАванс Из НайденныеТаблицыПолученныхАвансов Цикл
	
					Если ПолученыйАванс.СуммаВыданоНаличными > 0  Или ПолученыйАванс.СуммаВыданоНаличнымиРегл > 0 Тогда
						//++ НЕ УТ
						Если ТипЗнч(ПолученыйАванс.Регистратор) = Тип("ДокументСсылка.ВыбытиеДенежныхДокументов") Тогда
							ДокументАванса = "Видаток грошових документів №" + 
									ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПолученыйАванс.РегистраторНомер,Ложь,Истина) + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));
						Иначе
						//-- НЕ УТ
							ДокументАванса = "Видатковий касовий ордер №" + ПолученыйАванс.РегистраторНомер + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));
						//++ НЕ УТ
						КонецЕсли; 
						//-- НЕ УТ
					
					ИначеЕсли  ПолученыйАванс.СуммаВыданоКартой > 0 Или ПолученыйАванс.СуммаВыданоКартойРегл > 0 Тогда
						ДокументАванса = "Платіжне доручення вихідне №" + ПолученыйАванс.РегистраторНомер + " від " 
									+ Строка(Формат(ПолученыйАванс.РегистраторДата, "ДФ=dd.MM.yyyy"));					
					Иначе
					     Продолжить;
					КонецЕсли;
					
					ПараметрыЗаголовка.Вставить("Документ" + Индекс, ДокументАванса);
					ПараметрыЗаголовка.Вставить("Получено" + Индекс, ПолученыйАванс.СуммаВыданоНаличнымиРегл + ПолученыйАванс.СуммаВыданоКартойРегл);
				
					Индекс=Индекс+1;
					
					Если Индекс = 4 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;

				ОбластьМакета.Параметры.Заполнить(ПараметрыЗаголовка);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Оборотная сторона
				ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
				ОбластьМакета.Параметры.Заполнить(Выборка);

				ПараметрыТаблицы = Новый Структура;
				ПараметрыТаблицы.Вставить("ВалютаЗаголовокКолонки", "грн,коп.");
				ОбластьМакета.Параметры.Заполнить(ПараметрыТаблицы);
							
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				
				ИтогоПоОтчету = 0;
				ИтогоПоОтчетуВВалюте = 0;
				
				НомерСтроки = 0;
				
				СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
				ВыборкаОборотнаяСторона.НайтиСледующий(СтруктураПоиска);
				
				ОбработанныеНомераСтрок = Новый Массив;
				ТекущийРаздел = 0;
				
				ВыборкаПоРасходам = ВыборкаОборотнаяСторона.Выбрать();
				Пока ВыборкаПоРасходам.Следующий() Цикл
										
					Если ОбработанныеНомераСтрок.Найти(ВыборкаПоРасходам.НомерСтроки) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ОбработанныеНомераСтрок.Добавить(ВыборкаПоРасходам.НомерСтроки);
					
					ОбластьМакета.Параметры.Заполнить(ВыборкаПоРасходам);
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ОбластьМакета.Параметры.Дата = ВыборкаПоРасходам.ДокументДата; 
					
					ОснованиеПлатежа = СокрЛП(ВыборкаПоРасходам.НаименованиеРасхода);
					
					//++ НЕ УТ
					Если ТипЗнч(ВыборкаПоРасходам.Ссылка) <> Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
						ОснованиеПлатежа = ОснованиеПлатежа + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоРасходам.ДокументНомер);		
					КонецЕсли;
					//-- НЕ УТ

					 ОбластьМакета.Параметры.ОснованиеПлатежа = ОснованиеПлатежа;
					
					Если ТипЗнч(ВыборкаПоРасходам.ДокументДвижения) <> Тип("ДокументСсылка.АвансовыйОтчет") 
						И ВыборкаПоРасходам.Валюта <> ВалютаРегламентированногоУчета Тогда
						ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ДокументДвижения.СуммаВзаиморасчетов;
						ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ДокументДвижения.СуммаВзаиморасчетов;
					ИначеЕсли ВыборкаПоРасходам.СуммаРегл <> NULL Тогда 
						ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.СуммаРегл;
						ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.СуммаРегл;					
					Иначе
						ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ПоОтчету;
						ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ПоОтчету;
					КонецЕсли; 
					
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				
				// Выводим подвал авансового отчета
				ОбластьМакета = Макет.ПолучитьОбласть("Итого");
				
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ОбластьМакета.Параметры.СуммаСНДС                 	  = ИтогоПоОтчету;
				ТабличныйДокумент.Вывести(ОбластьМакета);

				ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				ВыборкаОборотнаяСторона.Сбросить();

			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент,
				НомерСтрокиНачало,
				ОбъектыПечати,
				Выборка.Ссылка);
		КонецЦикла;
			
	КонецЕсли;	

	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуАвансовогоОтчетаСтараяВерсия(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ШаблонОшибки = НСтр("ru='Печать авансового отчета используется только для документов с операцией ""Закупка через подотчетное лицо"".';uk='Друк авансового звіту використовується тільки для документів з операцією ""Купівля через підзвітну особу"".'");
	
	ИспользуетсяРеглУчет = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АвансовыйОтчет";
	ТабличныйДокумент.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Ссылка,
	|	ДанныеДокумента.МоментВремени     КАК МоментВремени,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо   КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.Представление КАК ПредставлениеВалютыДокумента	
	|
	|ПОМЕСТИТЬ АвансовыйОтчетНаПечатьВТ1
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.МоментВремени,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.Представление КАК ПредставлениеВалютыДокумента
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.МоментВремени,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.Представление КАК ПредставлениеВалютыДокумента
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.МоментВремени,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо,
	|	ДанныеДокумента.Валюта,
	|	ДанныеДокумента.Валюта.Представление
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПодотчетника)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Ссылка.МоментВремени,
	|	ДанныеДокумента.Ссылка.Организация,
	|	ДанныеДокумента.Ссылка.ПодотчетноеЛицо,
	|	ДанныеДокумента.Валюта,
	|	ДанныеДокумента.Валюта.Представление
	//-- НЕ УТ
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПодотчетноеЛицо
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	АвансовыйОтчетНаПечатьВТ1.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ1.Организация,
	|	АвансовыйОтчетНаПечатьВТ1.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ1.МоментВремени,
	|	МАКСИМУМ(ДенежныеСредства.Период) КАК ПериодПредыдущийОтчет,
	|	АвансовыйОтчетНаПечатьВТ1.Валюта
	|
	|ПОМЕСТИТЬ АвансовыйОтчетНаПечатьВТ2
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ1
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|	ПО
	|		ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ1.Организация
	|		И ДенежныеСредства.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ1.ПодотчетноеЛицо
	|		И ДенежныеСредства.МоментВремени < АвансовыйОтчетНаПечатьВТ1.МоментВремени
	|		И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) В (
	|			ТИП(Документ.АвансовыйОтчет),
	|			ТИП(Документ.ПриобретениеТоваровУслуг),
	|			ТИП(Документ.ПриобретениеУслугПрочихАктивов)
	//++ НЕ УТ
	|			, ТИП(Документ.ПоступлениеДенежныхДокументов)
	//-- НЕ УТ
	|		)
	|			И АвансовыйОтчетНаПечатьВТ1.Валюта = ДенежныеСредства.Валюта
	|	
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетНаПечатьВТ1.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ1.Организация,
	|	АвансовыйОтчетНаПечатьВТ1.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ1.МоментВремени,
	|	АвансовыйОтчетНаПечатьВТ1.Валюта	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АвансовыйОтчетНаПечатьВТ2.Организация,
	|	АвансовыйОтчетНаПечатьВТ2.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ2.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ2.МоментВремени,
	|	ДенежныеСредства.Регистратор КАК ПредыдущийОтчет,
	|	ДенежныеСредства.МоментВремени КАК МоментВремениПредыдущийОтчет,
	|	АвансовыйОтчетНаПечатьВТ2.Валюта
	|
	|ПОМЕСТИТЬ АвансовыйОтчетНаПечатьВТ3
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ2 КАК АвансовыйОтчетНаПечатьВТ2
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|	ПО
	|		ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ2.Организация
	|		И ДенежныеСредства.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ2.ПодотчетноеЛицо
	|		И ДенежныеСредства.Период = АвансовыйОтчетНаПечатьВТ2.ПериодПредыдущийОтчет
	|		И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) В (
	|			ТИП(Документ.АвансовыйОтчет),
	|			ТИП(Документ.ПриобретениеТоваровУслуг),
	|			ТИП(Документ.ПриобретениеУслугПрочихАктивов)
	//++ НЕ УТ
	|			, ТИП(Документ.ПоступлениеДенежныхДокументов)
	//-- НЕ УТ
	|		)
	|			И АвансовыйОтчетНаПечатьВТ2.Валюта = ДенежныеСредства.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	АвансовыйОтчетНаПечатьВТ2.Организация,
	|	АвансовыйОтчетНаПечатьВТ2.ПодотчетноеЛицо,
	|	Ссылка,
	|	ПредыдущийОтчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 3
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	ДенежныеСредстваВыдано.Валюта,
	|	ДенежныеСредстваВыдано.Регистратор КАК ДокументАванса,
	|	ДенежныеСредстваВыдано.Регистратор.Дата КАК ДокументАвансаДата,
	|	ВЫБОР 
	|		КОГДА ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
	|			ВЫРАЗИТЬ(ДенежныеСредстваВыдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
	|		КОГДА ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
	|			ВЫРАЗИТЬ(ДенежныеСредстваВыдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
	|		ИНАЧЕ ДенежныеСредстваВыдано.Регистратор.Номер
	|	КОНЕЦ КАК ДокументАвансаНомер,
	|	
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК ПолученоНаличными,
	|
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДенежныеСредстваВыдано.Валюта <> &ВалютаРегламентированногоУчета
	|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК ПолученоНаличнымиВВалюте,
	|
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК ПолученоБезналичными,
	|
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДенежныеСредстваВыдано.Валюта <> &ВалютаРегламентированногоУчета
	|			И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК ПолученоБезналичнымиВВалюте,
	|
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И ДенежныеСредстваВыдано.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеНеизрасходованныхПодотчетныхСумм)) ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК СуммаВозвращено,
	|
	|	СУММА(ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И ДенежныеСредстваВыдано.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеНеизрасходованныхПодотчетныхСумм)) ТОГДА
	|			ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ) КАК СуммаВозвращеноВВалюте
	|
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ3
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваВыдано
	|	ПО
	|		((ДенежныеСредстваВыдано.МоментВремени > АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ИЛИ АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ЕСТЬ NULL)
	|			И ДенежныеСредстваВыдано.МоментВремени < АвансовыйОтчетНаПечатьВТ3.МоментВремени)
	|		И ДенежныеСредстваВыдано.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
	|		И ДенежныеСредстваВыдано.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
	|			И АвансовыйОтчетНаПечатьВТ3.Валюта = ДенежныеСредстваВыдано.Валюта
	|ГДЕ
	|	НЕ ДенежныеСредстваВыдано.Регистратор ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	ДенежныеСредстваВыдано.Валюта,
	|	ДенежныеСредстваВыдано.Регистратор,
	|	ДенежныеСредстваВыдано.Регистратор.Дата,
	|	ВЫБОР
	|		КОГДА ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|			ТОГДА ВЫРАЗИТЬ(ДенежныеСредстваВыдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
	|		КОГДА ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|			ТОГДА ВЫРАЗИТЬ(ДенежныеСредстваВыдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
	|		ИНАЧЕ ДенежныеСредстваВыдано.Регистратор.Номер
	|	КОНЕЦ               
	|
	|ИТОГИ ПО
	|	Ссылка	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 4
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ3.Валюта,
	|	
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетНаПечатьВТ3.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаРеглКонечныйОстаток, 0) > 0
	|						ТОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаРеглКонечныйОстаток, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаКонечныйОстаток, 0) > 0
	|					ТОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаКонечныйОстаток, 0)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК НачальныйОстаток,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетНаПечатьВТ3.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаРеглКонечныйОстаток, 0) < 0
	|						ТОГДА -ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаРеглКонечныйОстаток, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаКонечныйОстаток, 0) < 0
	|					ТОГДА -ЕСТЬNULL(ДенежныеСредстваПредыдущийОтчет.СуммаКонечныйОстаток, 0)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК НачальныйПерерасход,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетНаПечатьВТ3.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРеглРасход, 0)
	|		ИНАЧЕ ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРасход, 0)
	|	КОНЕЦ КАК Израсходовано,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетНаПечатьВТ3.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРеглКонечныйОстаток, 0) > 0
	|						ТОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРеглКонечныйОстаток, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаКонечныйОстаток, 0) > 0
	|					ТОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаКонечныйОстаток, 0)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК КонечныйОстаток,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетНаПечатьВТ3.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРеглКонечныйОстаток, 0) < 0
	|						ТОГДА -ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаРеглКонечныйОстаток, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаКонечныйОстаток, 0) < 0
	|					ТОГДА -ЕСТЬNULL(ДенежныеСредстваТекущийОтчет.СуммаКонечныйОстаток, 0)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК КонечныйПерерасход
	|
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ3
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Регистратор,,
	|			(Организация, Подотчетноелицо) В (ВЫБРАТЬ Организация, Подотчетноелицо ИЗ АвансовыйОтчетНаПечатьВТ1)) КАК ДенежныеСредстваПредыдущийОтчет
	|	ПО
	|		ДенежныеСредстваПредыдущийОтчет.Регистратор = АвансовыйОтчетНаПечатьВТ3.ПредыдущийОтчет
	|		И ДенежныеСредстваПредыдущийОтчет.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
	|		И ДенежныеСредстваПредыдущийОтчет.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
	|		И АвансовыйОтчетНаПечатьВТ3.Валюта = ДенежныеСредстваПредыдущийОтчет.Валюта
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Регистратор,,
	|			(Организация, Подотчетноелицо) В (ВЫБРАТЬ Организация, Подотчетноелицо ИЗ АвансовыйОтчетНаПечатьВТ1)) КАК ДенежныеСредстваТекущийОтчет
	|	ПО
	|		ДенежныеСредстваТекущийОтчет.Регистратор = АвансовыйОтчетНаПечатьВТ3.Ссылка
	|		И ДенежныеСредстваТекущийОтчет.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
	|		И ДенежныеСредстваТекущийОтчет.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
	|		И АвансовыйОтчетНаПечатьВТ3.Валюта = ДенежныеСредстваТекущийОтчет.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 5
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Ссылка.Номер КАК Номер,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Ссылка.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.ПодотчетноеЛицо.КодПоДРФО КАК ДРФОПодотчетногоЛица,
	|	ДанныеДокумента.ПодотчетноеЛицо.Представление КАК ПредставлениеПодотчетногоЛица,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК ПредставлениеОрганизации,
	|	ДанныеДокумента.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
	|	ДанныеДокумента.Ссылка.НазначениеАванса КАК НазначениеАванса,
	|	ДанныеДокумента.Ссылка.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ссылка.Подразделение.Представление КАК ПредставлениеПодразделения,
	|	ДанныеДокумента.Ссылка.Валюта КАК Валюта,
	|	ДанныеДокумента.Ссылка.Валюта.Представление КАК ПредставлениеВалютыДокумента,
	
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.Ссылка) = ТИП(Документ.АвансовыйОтчет) ТОГДА
	|		ДанныеДокумента.Ссылка.Мультивалютный
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Мультивалютный,
	
	|	ДанныеДокумента.Ссылка.КоличествоДокументов КАК КоличествоДокументов
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ1 КАК ДанныеДокумента
	|	
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 6
	|	ПрочиеРасходы.Ссылка                           КАК Ссылка,
	|	ВЫБОР КОГДА ПрочиеРасходы.ЭтоСуточные = ИСТИНА ТОГДА
	|		ПрочиеРасходы.НаименованиеВходящегоДокумента
	|	ИНАЧЕ
	|		""Контр. """""" + ПрочиеРасходы.Контрагент.НаименованиеПолное + """""""" + "", "" + (ВЫРАЗИТЬ(ПрочиеРасходы.НаименованиеВходящегоДокумента КАК СТРОКА(100))) + "" № "" 
	|	КОНЕЦ КАК НаименованиеРасхода,
	|	ПрочиеРасходы.НомерВходящегоДокумента          КАК ДокументНомер,
	|	ПрочиеРасходы.ДатаВходящегоДокумента           КАК ДокументДата,
	|	ВЫБОР КОГДА ПрочиеРасходы.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ПрочиеРасходы.Ссылка.Валюта
	|	ИНАЧЕ
	|		ПрочиеРасходы.Валюта
	|	КОНЕЦ КАК Валюта,
	|	
	|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
	|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
	|	КОНЕЦ КАК ПоОтчету,
	
	|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
	|		ПрочиеРасходы.Сумма
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК ПоОтчетуВВалюте,
	|	
	|	1 КАК НомерРаздела,
	|	ПрочиеРасходы.НомерСтроки КАК НомерСтроки
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДвиженияПрочиеРасходы
	|	ПО
	|		ДвиженияПрочиеРасходы.Регистратор = ПрочиеРасходы.Ссылка
	|		И ДвиженияПрочиеРасходы.СтатьяДоходовРасходов = ПрочиеРасходы.СтатьяРасходов
	|		И ДвиженияПрочиеРасходы.АналитикаРасходов = ПрочиеРасходы.АналитикаРасходов
	|		И ДвиженияПрочиеРасходы.АналитикаАктивовПассивов = ПрочиеРасходы.АналитикаАктивовПассивов
	|		И ДвиженияПрочиеРасходы.Подразделение = ПрочиеРасходы.Подразделение
	|		И ДвиженияПрочиеРасходы.СуммаВВалюте = ПрочиеРасходы.Сумма
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Регистратор = ПрочиеРасходы.Ссылка
	|		И Суммы.ИдентификаторСтроки = ПрочиеРасходы.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|		
	|	
	|ГДЕ
	|	ПрочиеРасходы.Ссылка В (&МассивДокументов)
	|	И НЕ ПрочиеРасходы.Отменено
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ОплатаПоставщикам.Ссылка,
	|	""Контр. """""" + ОплатаПоставщикам.Контрагент.НаименованиеПолное + """""""" + "", накладна № "",
	|	ОплатаПоставщикам.НомерВходящегоДокумента,
	|	ОплатаПоставщикам.ДатаВходящегоДокумента,
	|	ВЫБОР КОГДА ОплатаПоставщикам.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ОплатаПоставщикам.Ссылка.Валюта
	|	ИНАЧЕ
	|		ОплатаПоставщикам.Валюта
	|	КОНЕЦ КАК Валюта,
	|	ОплатаПоставщикам.Сумма,
	|	ВЫБОР
	|		КОГДА ОплатаПоставщикам.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
	|			ОплатаПоставщикам.Сумма
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ,		
	|	2 КАК НомерРаздела,
	|	ОплатаПоставщикам.НомерСтроки КАК НомерСтроки
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
	|	
	|ГДЕ
	|	ОплатаПоставщикам.Ссылка В (&МассивДокументов)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	""Контр. """""" + ДанныеДокумента.Контрагент.НаименованиеПолное + """""""" + "", накладна № "",
	|	ДанныеДокумента.НомерВходящегоДокумента,
	|	ДанныеДокумента.ДатаВходящегоДокумента,
	|	ДанныеДокумента.Валюта,
	|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
	|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ,
	
	|	0 КАК НомерРаздела,
	|	0 КАК НомерСтроки
	|
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|	ПО
	|		ДенежныеСредства.Регистратор = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|	
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Контрагент.НаименованиеПолное,
	|	НЕОПРЕДЕЛЕНО,
	|	ДенежныеСредства.Валюта,
	|	НЕОПРЕДЕЛЕНО,
	|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
	|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ,
	
	|	0 КАК НомерРаздела,
	|	0 КАК НомерСтроки
	|
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|	ПО
	|		ДенежныеСредства.Регистратор = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПодотчетника)
	|	
	//-- НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	""Контр. """""" + ДанныеДокумента.Контрагент.НаименованиеПолное + """""""" + "", накладна № "",
	|	ДанныеДокумента.НомерВходящегоДокумента,
	|	ДанныеДокумента.ДатаВходящегоДокумента,
	|	ДанныеДокумента.Валюта,
	|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
	|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ,
	
	|	0 КАК НомерРаздела,
	|	0 КАК НомерСтроки
	|
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|	ПО
	|		ДенежныеСредства.Регистратор = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРаздела,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 7
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	ТаблицаКонвертацияВалюты.Валюта КАК ВалютаИсходная,
	|	ТаблицаКонвертацияВалюты.Сумма КАК СуммаИсходная,
	|	ТаблицаКонвертацияВалюты.ВалютаКонвертации КАК ВалютаКонвертации,
	|	ТаблицаКонвертацияВалюты.СуммаКонвертации КАК СуммаКонвертации
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ3
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаКонвертацияВалюты
	|	ПО
	|		ТаблицаКонвертацияВалюты.Ссылка = АвансовыйОтчетНаПечатьВТ3.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 8
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ3.МоментВремени,
	|	АвансовыйОтчетНаПечатьВТ3.ПредыдущийОтчет,
	|	ЕСТЬNULL(АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет, ДАТАВРЕМЯ(1,1,1,0,0,1)) КАК МоментВремениПредыдущийОтчет
	|
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ3 КАК АвансовыйОтчетНаПечатьВТ3
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // 9
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	ДенежныеСредстваТекущийОтчет.Валюта,
	|	ЕСТЬNULL(СУММА(ДенежныеСредстваТекущийОтчет.Сумма), 0) КАК Сумма
	|
	|ИЗ
	|	АвансовыйОтчетНаПечатьВТ3
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваТекущийОтчет
	|	ПО
	|		ДенежныеСредстваТекущийОтчет.Регистратор = АвансовыйОтчетНаПечатьВТ3.Ссылка
	|		И ДенежныеСредстваТекущийОтчет.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
	|		И ДенежныеСредстваТекущийОтчет.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
	|		И ДенежныеСредстваТекущийОтчет.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДенежныеСредстваТекущийОтчет.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
	|	АвансовыйОтчетНаПечатьВТ3.Организация,
	|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
	|	ДенежныеСредстваТекущийОтчет.Валюта
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Если Не ИспользуетсяРеглУчет Тогда
		Запрос.Текст = Запрос.Текст + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОплатаПоставщикам.Ссылка КАК Регистратор,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""631"" 
		|	ИНАЧЕ
		|		""632""
		|	КОНЕЦ КАК СчетДт,
		|	ОплатаПоставщикам.Контрагент КАК ЗначениеСубконто,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтроки,
		|	ОплатаПоставщикам.Ссылка.Валюта КАК Валюта,
		|	ОплатаПоставщикам.Сумма
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
		|ГДЕ
		|	ОплатаПоставщикам.Ссылка В (&МассивДокументов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрочиеРасходы.Ссылка КАК Регистратор,
		|	ПрочиеРасходы.СтатьяРасходов.КорреспондирующийСчет,
		|	ПрочиеРасходы.СтатьяРасходов КАК ЗначениеСубконто,
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	ПрочиеРасходы.НомерСтроки КАК НомерСтроки,
		|	ПрочиеРасходы.Ссылка.Валюта,
		
		|	ВЫБОР КОГДА НЕ ПрочиеРасходы.Отменено ТОГДА
		|		ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|		ИНАЧЕ
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДвиженияПрочиеРасходы
		|		ПО
		|			ДвиженияПрочиеРасходы.Регистратор = ПрочиеРасходы.Ссылка
		|			И ДвиженияПрочиеРасходы.НомерСтроки = ПрочиеРасходы.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|		ПО
		|			Суммы.Регистратор = ПрочиеРасходы.Ссылка
		|			И Суммы.ИдентификаторСтроки = ПрочиеРасходы.ИдентификаторСтроки
		|			И Суммы.СуммаБезНДСРегл <> 0
		|ГДЕ
		|	ПрочиеРасходы.Ссылка В (&МассивДокументов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка КАК Регистратор,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар) ТОГДА
		|		""281""
		|	ИНАЧЕ
		|		""92""
		|	КОНЕЦ КАК СчетДт,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	1 КАК НомерСтроки,
		|	ПриобретениеТоваровУслугТовары.Ссылка.Валюта,
		|	ВЫБОР
		|		КОГДА ПриобретениеТоваровУслугТовары.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
		|			ТОГДА СУММА(ПриобретениеТоваровУслугТовары.СуммаСНДС) - СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС)
		|		ИНАЧЕ СУММА(ПриобретениеТоваровУслугТовары.СуммаСНДС)
		|	КОНЕЦ
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка В (&МассивДокументов)
		|	
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Ссылка,
		|	ПриобретениеТоваровУслугТовары.НалоговоеНазначение,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар) ТОГДА
		|		""281""
		|	ИНАЧЕ
		|		""92""
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Ссылка КАК Регистратор,
		|	""6442"" КАК СчетДт,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	2 КАК НомерСтроки,
		|	ПриобретениеТоваровУслугТовары.Ссылка.Валюта,
		|	СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС) КАК Сумма
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка В (&МассивДокументов)
		|	
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Ссылка,
		|	ПриобретениеТоваровУслугТовары.Ссылка.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|";
	//++ НЕ УТ
	Иначе
		Запрос.Текст = Запрос.Текст + "
		|ВЫБРАТЬ
		|	Хозрасчетный.Регистратор КАК Регистратор,
		|	Хозрасчетный.Период КАК Период,
		|	Хозрасчетный.СчетДт.Код КАК СчетДт,
		|	Хозрасчетный.СчетКт.Код КАК СчетКт,
		|	Хозрасчетный.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(Хозрасчетный.ВалютнаяСуммаДт, ЕСТЬNULL(Хозрасчетный.ВалютнаяСуммаКт, Хозрасчетный.Сумма)) КАК Сумма,
		|	ЕСТЬNULL(Хозрасчетный.ВалютаДт, ЕСТЬNULL(Хозрасчетный.ВалютаКт, &ВалютаРегламентированногоУчета)) КАК Валюта
		|ПОМЕСТИТЬ Проводки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Регистратор В (&МассивДокументов)
		|	И НЕ Хозрасчетный.СчетДт.Забалансовый
		|	И НЕ Хозрасчетный.СчетКт.Забалансовый
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Проводки.Регистратор КАК Регистратор,
		|	Проводки.СчетДт КАК СчетДт,
		|	Проводки.СчетКт КАК СчетКт,
		|	МИНИМУМ(Проводки.НомерСтроки) КАК НомерСтроки,
		|	СУММА(Проводки.Сумма) КАК Сумма,
		|	Проводки.Валюта КАК Валюта
		|ИЗ
		|	Проводки КАК Проводки
		|
		|СГРУППИРОВАТЬ ПО
		|	Проводки.Регистратор,
		|	Проводки.СчетДт,
		|	Проводки.СчетКт,
		|	Проводки.Валюта		
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|";
	//-- НЕ УТ
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	РегистраторыДенежныеСредстваВыдано = ВводОстатковСервер.ДоступныеТипыВводаОстатков();
	РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	//++ НЕ УТ
	РегистраторыДенежныеСредстваВыдано.Добавить(Тип("ДокументСсылка.ВыбытиеДенежныхДокументов"));
	//-- НЕ УТ
	Запрос.УстановитьПараметр("РегистраторыДенежныеСредстваВыдано", РегистраторыДенежныеСредстваВыдано);
	
	
	//++ НЕ УТ
	МассивНеОтраженныхДокументов = Неопределено;
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		ПараметрыПечати.Свойство("ДокументыБезПроводок", МассивНеОтраженныхДокументов);
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивНеОтраженныхДокументов", МассивНеОтраженныхДокументов);
	//-- НЕ УТ
	
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = МассивРезультатов[5].Выбрать();
	ВыборкаОстатки = МассивРезультатов[4].Выбрать();
	ТаблицаПолученныхАвансов = МассивРезультатов[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаКонвертация = МассивРезультатов[7].Выбрать();
	ВыборкаДатыОстатков = МассивРезультатов[8].Выбрать();
	ВыборкаИзрасходованоВВалюте = МассивРезультатов[9].Выбрать();
	
	ВыборкаОборотнаяСторона = МассивРезультатов[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ИспользуетсяРеглУчет Тогда
		Проводки = МассивРезультатов[11].Выгрузить();
	Иначе
		Проводки = МассивРезультатов[10].Выгрузить();
	КонецЕсли;
	
	Если МассивРезультатов[5].Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ШаблонОшибки);
	КонецЕсли;
	
	ПервыйДокумент = Истина;
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
 		Если Выборка.ДатаДокумента >=  Дата("20230713") Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет01072023");
 		ИначеЕсли Выборка.ДатаДокумента >=  Дата("20160419000000") Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет19042016");
		ИначеЕсли Выборка.ДатаДокумента >=  Дата("20151106000000") Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет06112015");
		ИначеЕсли Выборка.ДатаДокумента >=  Дата("20140204000000") Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет04022014");
		Иначе
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвансовыйОтчет.ПФ_MXL_UK_АвансовыйОтчет");
		КонецЕсли;                 
		
		КодЯзыкаПечать = "uk"; // только на украинском
				
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ОбластьМакета.Параметры.Заполнить(Выборка);
		

		ПараметрыЗаголовка = Новый Структура;
		ПараметрыЗаголовка.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер));
		
		СведенияОбОрганизации	 = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаДокумента);	
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование",,КодЯзыкаПечать);
		
		ПараметрыЗаголовка.Вставить("Дата", Выборка.ДатаДокумента);
		ПараметрыЗаголовка.Вставить("ДатаКоротко", Формат(Выборка.ДатаДокумента, "ДФ=dd.MM.yyyy"));
		ПараметрыЗаголовка.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
		
		Если Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоЕДРПОУ,",Ложь);
		Иначе
			ЕДРПОУОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "КодПоДРФО,",Ложь);
		КонецЕсли;
		Для Инд = 1 По 8 Цикл
			ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
		КонецЦикла; 
		// если организация - физ.лицо нужно "добавить" две ячейки
		Если НЕ Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			Для Инд = 9 По 10 Цикл
				ПараметрыЗаголовка.Вставить("ЕДРПОУОрганизации_" + Инд, Сред(ЕДРПОУОрганизации, Инд, 1));
			КонецЦикла; 
			Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
			ОбластьМакета.Область("КодОрганизации9").Обвести(Линия,Линия,Линия,Линия);
			ОбластьМакета.Область("КодОрганизации10").Обвести(Линия,Линия,Линия,Линия);
		КонецЕсли;
		
		//++ НЕ УТ
		ПараметрыПолученияДанных = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияДанных.Организация = Выборка.Организация;
		ПараметрыПолученияДанных.НачалоПериода = Выборка.ДатаДокумента;
		ПараметрыПолученияДанных.ОкончаниеПериода = Выборка.ДатаДокумента;
		ПараметрыПолученияДанных.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ПодотчетноеЛицо);
		
		Сотрудники = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияДанных);
		
		ПолучаемыеДанные = Новый Массив;
		ПолучаемыеДанные.Добавить("Должность");
		ПолучаемыеДанные.Добавить("ТабельныйНомер");

		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
			Истина, // Только разрешенные
			Сотрудники.ВыгрузитьКолонку("Сотрудник"),
			ПолучаемыеДанные,
			Выборка.ДатаДокумента);
			
		Если КадровыеДанные.Количество() > 0 Тогда
			ПараметрыЗаголовка.Вставить("ДолжностьПодотчетногоЛицаПредставление", КадровыеДанные[0].Должность);
		КонецЕсли;
		//-- НЕ УТ
		
		ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛица", Выборка.ПредставлениеПодотчетногоЛица);
		
		ПодотчетноеЛицоФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Выборка.ПредставлениеПодотчетногоЛица)," ");
			
		КоличествоПодстрок = ПодотчетноеЛицоФИО.Количество();
		ПодотчетноеЛицоФамилия            = ?(КоличествоПодстрок > 0,ПодотчетноеЛицоФИО[0],"");
		ПодотчетноеЛицоИмя                = ?(КоличествоПодстрок > 1,ПодотчетноеЛицоФИО[1],"");
		ПодотчетноеЛицоОтчество           = ?(КоличествоПодстрок > 2,ПодотчетноеЛицоФИО[2],"");
		
		ПараметрыЗаголовка.Вставить("ФИОПодотчетногоЛицаКратко", ПодотчетноеЛицоФамилия + ?(ПодотчетноеЛицоИмя = "" , "" , " " + Лев(ПодотчетноеЛицоИмя, 1) + ".") + ?(ПодотчетноеЛицоОтчество = "", "", Лев(ПодотчетноеЛицоОтчество, 1) + "."));

		Для Инд = 1 По 10 Цикл
			ПараметрыЗаголовка.Вставить("ДРФОПодотчетногоЛица_" + Инд, Сред(Выборка.ДРФОПодотчетногоЛица, Инд, 1));
		КонецЦикла;
		
		// Получение суммы наличными и безналичными
		СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
		ТаблицаПолученныхАвансов.Сбросить();
		Если ТаблицаПолученныхАвансов.НайтиСледующий(СтруктураПоиска) Тогда
			
			ПолученоИзКассы = 0;
			ПолученоПоБанковскимКартам = 0;
					
			ВыборкаДокументыАванса = ТаблицаПолученныхАвансов.Выбрать();
			Индекс=1;
			Пока ВыборкаДокументыАванса.Следующий() Цикл
				
				Если Выборка.Валюта <> ВыборкаДокументыАванса.Валюта Тогда
					Продолжить;	
				КонецЕсли;
				
				Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
					ПолученоИзКассы = ПолученоИзКассы + ВыборкаДокументыАванса.ПолученоНаличными;
					ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам + ВыборкаДокументыАванса.ПолученоБезналичными;
				Иначе
					ПолученоИзКассы = ПолученоИзКассы + ВыборкаДокументыАванса.ПолученоНаличнымиВВалюте;
					ПолученоПоБанковскимКартам = ПолученоПоБанковскимКартам + ВыборкаДокументыАванса.ПолученоБезналичнымиВВалюте;
				КонецЕсли; 
				
				Если ВыборкаДокументыАванса.ПолученоНаличными >0  Тогда
					
					//++ НЕ УТ
					Если ТипЗнч(ВыборкаДокументыАванса.ДокументАванса) = Тип("ДокументСсылка.ВыбытиеДенежныхДокументов") Тогда
						ДокументАванса = "Видаток грошових документів №" + 
								ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументыАванса.ДокументАвансаНомер,Ложь,Истина) + " від " 
								+ Строка(Формат(ВыборкаДокументыАванса.ДокументАвансаДата, "ДФ=dd.MM.yyyy"));
					Иначе
					//-- НЕ УТ
						ДокументАванса = "Видатковий касовий ордер №" + ВыборкаДокументыАванса.ДокументАвансаНомер + " від " 
								+ Строка(Формат(ВыборкаДокументыАванса.ДокументАвансаДата, "ДФ=dd.MM.yyyy"));
					//++ НЕ УТ
					КонецЕсли;  
					//-- НЕ УТ
								
				ИначеЕсли  ВыборкаДокументыАванса.ПолученоБезналичными >0  Тогда
					ДокументАванса = "Платіжне доручення вихідне №" + ВыборкаДокументыАванса.ДокументАвансаНомер + " від " 
								+ Строка(Формат(ВыборкаДокументыАванса.ДокументАвансаДата, "ДФ=dd.MM.yyyy"));
								
				Иначе
				     Продолжить;
				
				КонецЕсли;
				
				ПараметрыЗаголовка.Вставить("Документ" + Индекс, ДокументАванса);

				Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
					ПараметрыЗаголовка.Вставить("Получено" + Индекс, ВыборкаДокументыАванса.ПолученоНаличными + ВыборкаДокументыАванса.ПолученоБезналичными);
				Иначе
					ПараметрыЗаголовка.Вставить("Получено" + Индекс, ВыборкаДокументыАванса.ПолученоНаличнымиВВалюте + ВыборкаДокументыАванса.ПолученоБезналичнымиВВалюте);
				КонецЕсли; 
				
				Индекс=Индекс+1;
				
				Если Индекс = 4 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		
		Иначе
			ПолученоИзКассы = 0;
			ПолученоПоБанковскимКартам = 0; 
		КонецЕсли;
		
		ИтогоПолучено = ПолученоИзКассы + ПолученоПоБанковскимКартам; 
		
		ПараметрыЗаголовка.Вставить("ИтогВсегоПолучено", ИтогоПолучено);
		
		СтруктураПоиска.Вставить("Валюта", Выборка.Валюта);
		
		ВыборкаОстатки.Сбросить();
		Если ВыборкаОстатки.НайтиСледующий(СтруктураПоиска) Тогда
			ОбластьМакета.Параметры.Заполнить(ВыборкаОстатки);
			ОстатокНаКонец = ВыборкаОстатки.КонечныйОстаток - ВыборкаОстатки.КонечныйПерерасход;
			ИтогоИзрасходовано = ВыборкаОстатки.Израсходовано;
		Иначе
			ОстатокНаКонец = ИтогоПолучено;
			ИтогоИзрасходовано = 0;
		КонецЕсли;
		
		ПараметрыЗаголовка.Вставить("ВалютаЗаголовокКолонки", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
		Если ОстатокНаКонец >= 0  Тогда
			//Остаток надо возвращать в валюте
			ПараметрыЗаголовка.Вставить("ВалютаОстатокПерерасход", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
		Иначе
			//Перерасход надо возвращать в гривнях
			ПараметрыЗаголовка.Вставить("ВалютаОстатокПерерасход", "грн,коп.");
		КонецЕсли;
		
		ПараметрыЗаголовка.Вставить("СуммаОтчетПодтверждаю", РаботаСКурсамиВалют.СформироватьСуммуПрописью(
			ИтогоИзрасходовано,
			Выборка.Валюта,
			Ложь, // ВыводитьСуммуБезКопеек
			КодЯзыкаПечать 
		));

		СуммаДокументаФормат = Формат(ИтогоИзрасходовано, "ЧЦ=15; ЧДЦ=2;");
		ПараметрыЗаголовка.Вставить("СуммаСНДСЧислом", СуммаДокументаФормат); 
		ПараметрыЗаголовка.Вставить("СуммаДокумента", СуммаДокументаФормат); 
		ПараметрыЗаголовка.Вставить("ВалютаДокумента", Выборка.Валюта);
		
		ПараметрыЗаголовка.Вставить("ВалютаДокументаРасписка",?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн, коп",Строка(Выборка.Валюта)));
		
		Счетчик = 0;
		Проводки.Свернуть("Регистратор, СчетДт, СчетКт, Валюта", "Сумма");
		СтруктураПоиска = Новый Структура("Регистратор, Валюта", Выборка.Ссылка, Выборка.Валюта);
		НайденныеПроводки = Проводки.НайтиСтроки(СтруктураПоиска);
		Для каждого Проводка из НайденныеПроводки Цикл
			Счетчик = Счетчик + 1;
			Если Счетчик < 11 Тогда
				ПараметрыЗаголовка.Вставить("СчетДт"  + Счетчик, Проводка.СчетДт);
				ПараметрыЗаголовка.Вставить("СчетКт"    + Счетчик, Проводка.СчетКт);
				ПараметрыЗаголовка.Вставить("Сумма" + Счетчик, Проводка.Сумма);
				ПараметрыЗаголовка.Вставить("СуммаБезКопеек" + Счетчик, Цел(Проводка.Сумма));
				ПараметрыЗаголовка.Вставить("СуммаКопейки"   + Счетчик, Формат((Проводка.Сумма - Цел(Проводка.Сумма)) * 100, "ЧН=00"));
			Иначе
				Если Счетчик = 11 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаголовка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Оборотная сторона
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьМакета.Параметры.Заполнить(Выборка);

		ПараметрыТаблицы = Новый Структура;
		ПараметрыТаблицы.Вставить("ВалютаЗаголовокКолонки", ?(Выборка.Валюта = ВалютаРегламентированногоУчета, "грн,коп.",Строка(Выборка.ПредставлениеВалютыДокумента)));
		ОбластьМакета.Параметры.Заполнить(ПараметрыТаблицы);

		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим табличные части
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		
		ИтогоПоОтчету        = 0;
		СуммаВВалюте = 0;
		
		НомерСтроки = 0;
		СтруктураПоиска = Новый Структура("Ссылка", Выборка.Ссылка);
		ВыборкаОборотнаяСторона.Сбросить();
		ВыборкаОборотнаяСторона.НайтиСледующий(СтруктураПоиска);
		
		ВыборкаПоРасходам = ВыборкаОборотнаяСторона.Выбрать();
 		Пока ВыборкаПоРасходам.Следующий() Цикл
			
			Если ВыборкаПоРасходам.Валюта <> Выборка.Валюта Тогда
				Продолжить;	
			КонецЕсли; 
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
			ОбластьМакета.Параметры.Дата = ВыборкаПоРасходам.ДокументДата; 
			
			ОснованиеПлатежа = СокрЛП(ВыборкаПоРасходам.НаименованиеРасхода);
			//++ НЕ УТ
			Если ТипЗнч(ВыборкаПоРасходам.Ссылка) <> Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
				ОснованиеПлатежа = ОснованиеПлатежа + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоРасходам.ДокументНомер);		
			КонецЕсли;
			//-- НЕ УТ
			
			ОбластьМакета.Параметры.ОснованиеПлатежа = ОснованиеПлатежа;

			Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда
				ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ПоОтчету;
				ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ПоОтчету;
			Иначе
				ОбластьМакета.Параметры.СуммаСНДС = ВыборкаПоРасходам.ПоОтчетуВВалюте;
				ИтогоПоОтчету = ИтогоПоОтчету + ВыборкаПоРасходам.ПоОтчетуВВалюте;
			КонецЕсли; 
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		// Выводим подвал авансового отчета
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.СуммаСНДС                 	  = ИтогоПоОтчету;
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			Выборка.Ссылка
		);
		
	КонецЦикла;
			
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ТекстЗапросаПечати(ИспользуетсяРеглУчет, НесколькоВалют)	

	Если НЕ НесколькоВалют Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДанныеРегистра.Организация КАК Организация,
		|	ДанныеРегистра.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ДанныеРегистра.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ДанныеРегистра.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|			ТОГДА ДанныеРегистра.Регистратор
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|			ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|	КОНЕЦ КАК Ссылка,
		|	МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодОтчет,
		|	МИНИМУМ(ДанныеРегистра.Период) КАК НачальныйПериодОтчет
		|ПОМЕСТИТЬ ВТПериоды1
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|ГДЕ
		|	(ДанныеРегистра.Регистратор В (&МассивДокументов)
		|			ИЛИ ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет В (&МассивДокументов)
		//++ НЕ УТ
		|			ИЛИ ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет В (&МассивДокументов)
		//-- НЕ УТ
		|			)

		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Организация,
		|	ДанныеРегистра.ПодотчетноеЛицо,
		|	ДанныеРегистра.Подразделение,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|			ТОГДА ДанныеРегистра.Регистратор
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|		КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|			ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|	КОНЕЦ
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация КАК Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение КАК Подразделение,
		|	ВТПериоды.Ссылка КАК Ссылка,
		|	ВТПериоды.НачальныйПериодОтчет КАК НачальныйПериодОтчет,
		|	МАКСИМУМ(ДанныеРегистра.Регистратор) КАК Регистратор
		|ПОМЕСТИТЬ ВТПериоды2
		|ИЗ
		|	ВТПериоды1 КАК ВТПериоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|		ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|			И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|			И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|			И (ДанныеРегистра.Период = ВТПериоды.ПериодОтчет)
		|			И (ВЫБОР
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|					ТОГДА ДанныеРегистра.Регистратор
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|					ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|			КОНЕЦ = ВТПериоды.Ссылка)

		|СГРУППИРОВАТЬ ПО
		|	ВТПериоды.Организация,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.НачальныйПериодОтчет
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация КАК Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение КАК Подразделение,
		|	ВТПериоды.Ссылка КАК Ссылка,
		|	ВТПериоды.НачальныйПериодОтчет КАК НачальныйПериодОтчет,
		|	ВТПериоды.Регистратор КАК Регистратор,
		|	ВЫБОР
		|		КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.АвансовыйОтчет).МоментВремени
		|		КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|		КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|	КОНЕЦ КАК МоментВремениТекущийОтчет
		|ПОМЕСТИТЬ ВТПериоды3
		|ИЗ
		|	ВТПериоды2 КАК ВТПериоды
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация КАК Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение КАК Подразделение,
		|	ВТПериоды.Ссылка КАК Ссылка,
		|	ВТПериоды.Регистратор КАК Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет КАК МоментВремениТекущийОтчет,
		|	МАКСИМУМ(ДанныеРегистра.Регистратор) КАК НачальныйРегистратор
		|ПОМЕСТИТЬ ВТПериоды4
		|ИЗ
		|	ВТПериоды3 КАК ВТПериоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|		ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|			И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|			И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|			И (ДанныеРегистра.Период = ВТПериоды.НачальныйПериодОтчет)
		|			И (ВЫБОР
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|					ТОГДА ДанныеРегистра.Регистратор
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|					ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|			КОНЕЦ = ВТПериоды.Ссылка)

		|СГРУППИРОВАТЬ ПО
		|	ВТПериоды.Организация,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация КАК Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение КАК Подразделение,
		|	ВТПериоды.Ссылка КАК Ссылка,
		|	ВТПериоды.Регистратор КАК Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет,
		|	ВЫБОР
		|		КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.АвансовыйОтчет
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.АвансовыйОтчет).МоментВремени
		|		КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|		КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|			ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|	КОНЕЦ КАК НачальныйМоментВремениТекущийОтчет
		|ПОМЕСТИТЬ ВТПериоды5
		|ИЗ
		|	ВТПериоды4 КАК ВТПериоды
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация КАК Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение КАК Подразделение,
		|	ВТПериоды.Ссылка КАК Ссылка,
		|	ВТПериоды.Регистратор КАК Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет КАК МоментВремениТекущийОтчет,
		|	МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодПредыдущийОтчет
		|ПОМЕСТИТЬ ВТПериоды6
		|ИЗ
		|	ВТПериоды5 КАК ВТПериоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|		ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|			И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|			И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|			И (ДанныеРегистра.МоментВремени < ВТПериоды.НачальныйМоментВремениТекущийОтчет)
		|			И (ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		//++ НЕ УТ
		|				ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		//-- НЕ УТ
		|			)

		|СГРУППИРОВАТЬ ПО
		|	ВТПериоды.Организация,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет
		|;

		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПериоды.Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет,
		|	МАКСИМУМ(ДанныеРегистра.Регистратор) КАК ПредыдущийОтчет
		|ПОМЕСТИТЬ ВТПериоды7
		|ИЗ
		|	ВТПериоды6 КАК ВТПериоды

		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|	ПО
		|		ДанныеРегистра.Организация = ВТПериоды.Организация
		|		И ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо
		|		И ДанныеРегистра.Подразделение = ВТПериоды.Подразделение
		|		И ДанныеРегистра.Период = ВТПериоды.ПериодПредыдущийОтчет	
		|		И (ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|			ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		//++ НЕ УТ
		|			ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		//-- НЕ УТ
		|			)

		|СГРУППИРОВАТЬ ПО
		|	ВТПериоды.Организация,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет
		|;
		|	
		|ВЫБРАТЬ
		|	ВТПериоды.Организация,
		|	ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ВТПериоды.ПодотчетноеЛицо,
		|	ВТПериоды.Подразделение,
		|	ВТПериоды.Ссылка,
		|	ВТПериоды.Регистратор,
		|	ВТПериоды.МоментВремениТекущийОтчет,
		|	ВЫБОР
		|		КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.АвансовыйОтчет ТОГДА
		|			ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.АвансовыйОтчет).МоментВремени
		|		КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.ПриобретениеТоваровУслуг ТОГДА
		|			ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|		КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.ПоступлениеДенежныхДокументов ТОГДА
		|			ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|	КОНЕЦ КАК МоментВремениПредыдущийОтчет
		|ПОМЕСТИТЬ АвансовыйОтчетНаПечатьВТ3
		|ИЗ
		|	ВТПериоды7 КАК ВТПериоды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ // 8
		|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|	ДенежныеСредстваВыдано.Валюта,
		|	
		|	СУММА(ВЫБОР
		|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
		|			ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаВыданоНаличными,
		|	
		|	СУММА(ВЫБОР
		|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
		|			ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаВыданоНаличнымиРегл,
		|
		|	СУММА(ВЫБОР
		|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаВыданоКартой,
		|	
		|	СУММА(ВЫБОР
		|		КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаВыданоКартойРегл,
		|	ДенежныеСредстваВыдано.Регистратор
		|
		|ПОМЕСТИТЬ Выдано
		|ИЗ
		|	АвансовыйОтчетНаПечатьВТ3
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваВыдано
		|	ПО
		|		((ДенежныеСредстваВыдано.МоментВремени > АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ИЛИ АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ЕСТЬ NULL)
		|			И ДенежныеСредстваВыдано.МоментВремени < АвансовыйОтчетНаПечатьВТ3.МоментВремениТекущийОтчет)
		|		И ДенежныеСредстваВыдано.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|		И ДенежныеСредстваВыдано.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|		И ДенежныеСредстваВыдано.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|	ДенежныеСредстваВыдано.Валюта,
		|	ДенежныеСредстваВыдано.Регистратор
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 9
		|	Израсходовано.Ссылка КАК Ссылка,
		|	Израсходовано.Валюта КАК Валюта,
		|	СУММА(Израсходовано.Сумма) КАК СуммаИзрасходовано,
		|	СУММА(Израсходовано.СуммаРегл) КАК СуммаИзрасходованоРегл
		|ПОМЕСТИТЬ Израсходовано
		|ИЗ (
		|	ВЫБРАТЬ
		|		АвансовыйОтчетНаПечатьВТ3.Ссылка КАК Ссылка,
		|		ДенежныеСредства.Валюта,
		|		ЕСТЬNULL(СУММА(ДенежныеСредства.Сумма), 0) КАК Сумма,
		|		ЕСТЬNULL(СУММА(ДенежныеСредства.СуммаРегл), 0) КАК СуммаРегл
		|	ИЗ
		|		АвансовыйОтчетНаПечатьВТ3
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
		|		ПО
		|			ДенежныеСредства.Регистратор = АвансовыйОтчетНаПечатьВТ3.Ссылка
		|			И ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И ДенежныеСредства.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
		|				И ДенежныеСредства.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалютыПодотчетнымЛицом)
		|				И ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|				И ДенежныеСредства.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|	СГРУППИРОВАТЬ ПО
		|		АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|		ДенежныеСредства.Валюта
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеДокумента.АвансовыйОтчет КАК Ссылка,
		|		ДенежныеСредства.Валюта,
		|		ДенежныеСредства.СуммаРасход КАК Сумма,
		|		ДенежныеСредства.СуммаРеглРасход КАК СуммаРегл
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(,, Регистратор) КАК ДенежныеСредства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
		|		ПО ДанныеДокумента.Ссылка = ДенежныеСредства.Регистратор
		|	ГДЕ
		|		ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|		
		//++ НЕ УТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеДокумента.АвансовыйОтчет КАК Ссылка,
		|		ДенежныеСредства.Валюта,
		|		ДенежныеСредства.СуммаРасход КАК Сумма,
		|		ДенежныеСредства.СуммаРеглРасход КАК СуммаРегл
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(,, Регистратор) КАК ДенежныеСредства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|		ПО ДанныеДокумента.Ссылка = ДенежныеСредства.Регистратор
		|	ГДЕ
		|		ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		//-- НЕ УТ
		|	) КАК Израсходовано
		|СГРУППИРОВАТЬ ПО
		|	Израсходовано.Ссылка,
		|	Израсходовано.Валюта
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 10
		|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|	ДенежныеСредства.Организация,
		|	ДенежныеСредства.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ДенежныеСредства.Подразделение,
		|	ДенежныеСредства.ПодотчетноеЛицо,
		|	ДенежныеСредства.Подразделение,
		|	ДенежныеСредства.Валюта,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаКонечныйОстаток, 0) > 0 ТОГДА
		|			ДенежныеСредства.СуммаКонечныйОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК КонечныйОстаток,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаКонечныйОстаток, 0) < 0 ТОГДА
		|			-ДенежныеСредства.СуммаКонечныйОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК КонечныйПерерасход,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаРеглКонечныйОстаток, 0) > 0 ТОГДА
		|			ДенежныеСредства.СуммаРеглКонечныйОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК КонечныйОстатокРегл,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаРеглКонечныйОстаток, 0) < 0 ТОГДА
		|			-ДенежныеСредства.СуммаРеглКонечныйОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК КонечныйПерерасходРегл
		|ПОМЕСТИТЬ ТекущийАвансВрем
		|ИЗ
		|	АвансовыйОтчетНаПечатьВТ3
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Регистратор,,
		|			(Организация, Подотчетноелицо, Подразделение) В (ВЫБРАТЬ Организация, Подотчетноелицо, Подразделение ИЗ ВТПериоды1)) КАК ДенежныеСредства
		|	ПО
		|		ДенежныеСредства.Регистратор = АвансовыйОтчетНаПечатьВТ3.Регистратор
		|		И ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|		И ДенежныеСредства.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|		И ДенежныеСредства.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 11
		|	ТекущийАвансВрем.Ссылка,
		|	ТекущийАвансВрем.Валюта,
		|	СУММА(ТекущийАвансВрем.КонечныйОстаток),
		|	СУММА(ТекущийАвансВрем.КонечныйПерерасход),
		|	СУММА(ТекущийАвансВрем.КонечныйОстатокРегл),
		|	СУММА(ТекущийАвансВрем.КонечныйПерерасходРегл)
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	ТекущийАвансВрем
		|СГРУППИРОВАТЬ ПО
		|	ТекущийАвансВрем.Ссылка,
		|	ТекущийАвансВрем.Валюта
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 12
		|	ДанныеШапки.Ссылка КАК Ссылка,
		|	ДанныеШапки.Валюта КАК Валюта,
		|	СУММА(ДанныеШапки.СуммаВыданоНаличными)   КАК СуммаВыданоНаличными,
		|	СУММА(ДанныеШапки.СуммаВыданоКартой)      КАК СуммаВыданоКартой,
		|	СУММА(ДанныеШапки.КонечныйОстаток)        КАК КонечныйОстаток,
		|	СУММА(ДанныеШапки.КонечныйПерерасход)     КАК КонечныйПерерасход,
		|	СУММА(ДанныеШапки.СуммаИзрасходовано)     КАК СуммаИзрасходовано,
		|	ВЫБОР
		|		КОГДА СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой) > 0 ТОГДА
		|			СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НачальныйОстаток,
		|	ВЫБОР
		|		КОГДА СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой) < 0 ТОГДА
		|			-1 * (СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НачальныйПерерасход,
		|	СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл)   КАК СуммаВыданоНаличнымиРегл,
		|	СУММА(ДанныеШапки.СуммаВыданоКартойРегл)      КАК СуммаВыданоКартойРегл,
		|	СУММА(ДанныеШапки.КонечныйОстатокРегл)        КАК КонечныйОстатокРегл,
		|	СУММА(ДанныеШапки.КонечныйПерерасходРегл)     КАК КонечныйПерерасходРегл,
		|	СУММА(ДанныеШапки.СуммаИзрасходованоРегл)     КАК СуммаИзрасходованоРегл,
		|	ВЫБОР
		|		КОГДА СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл) > 0 ТОГДА
		|			СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НачальныйОстатокРегл,
		|	ВЫБОР
		|		КОГДА СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл) < 0 ТОГДА
		|			-1 * (СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НачальныйПерерасходРегл
		|ИЗ
		|(ВЫБРАТЬ
		|	Выдано.Ссылка КАК Ссылка,
		|	Выдано.Валюта КАК Валюта,
		|	Выдано.СуммаВыданоНаличными     КАК СуммаВыданоНаличными,
		|	Выдано.СуммаВыданоКартой        КАК СуммаВыданоКартой,
		|	0                               КАК КонечныйОстаток,
		|	0                               КАК КонечныйПерерасход,
		|	0                               КАК СуммаИзрасходовано,
		|	Выдано.СуммаВыданоНаличнымиРегл КАК СуммаВыданоНаличнымиРегл,
		|	Выдано.СуммаВыданоКартойРегл    КАК СуммаВыданоКартойРегл,
		|	0                               КАК КонечныйОстатокРегл,
		|	0                               КАК КонечныйПерерасходРегл,
		|	0                               КАК СуммаИзрасходованоРегл
		|ИЗ
		|	Выдано
		|ГДЕ
		|	Выдано.СуммаВыданоНаличными <> 0 ИЛИ Выдано.СуммаВыданоКартой <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Остатки.Ссылка,
		|	Остатки.Валюта,
		|	0,
		|	0,
		|	Остатки.КонечныйОстаток,
		|	Остатки.КонечныйПерерасход,
		|	0,
		|	0,
		|	0,
		|	Остатки.КонечныйОстатокРегл,
		|	Остатки.КонечныйПерерасходРегл,
		|	0	
		|ИЗ
		|	Остатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Израсходовано.Ссылка КАК Ссылка,
		|	Израсходовано.Валюта КАК Валюта,
		|	0,
		|	0,
		|	0,
		|	0,
		|	Израсходовано.СуммаИзрасходовано,
		|	0,
		|	0,
		|	0,
		|	0,
		|	Израсходовано.СуммаИзрасходованоРегл
		|ИЗ
		|	Израсходовано) КАК ДанныеШапки
		|СГРУППИРОВАТЬ ПО
		|	ДанныеШапки.Ссылка,
		|	ДанныеШапки.Валюта
		|
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 13
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	ДанныеДокумента.Номер КАК Номер,
		|	ДанныеДокумента.Организация.Префикс КАК Префикс,
		|	ДанныеДокумента.Дата КАК ДатаДокумента,
		|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ДанныеДокумента.ПодотчетноеЛицо.КодПоДРФО КАК ДРФОПодотчетногоЛица,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.ПодотчетноеЛицо) КАК ПредставлениеПодотчетногоЛица,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ДанныеДокумента.НазначениеАванса КАК НазначениеАванса,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Подразделение) КАК ПредставлениеПодразделения,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Валюта) КАК ПредставлениеВалютыДокумента,
		|	ДанныеДокумента.Мультивалютный КАК Мультивалютный,
		|	ДанныеДокумента.КоличествоДокументов КАК КоличествоДокументов
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|	
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаДокумента,
		|	Номер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ // 14
		|	ПрочиеРасходы.Ссылка                           КАК Ссылка,
		|	ПрочиеРасходы.Ссылка                           КАК ДокументДвижения,
		|	ВЫБОР КОГДА ПрочиеРасходы.ЭтоСуточные = ИСТИНА ТОГДА
		|		ПрочиеРасходы.НаименованиеВходящегоДокумента
		|	ИНАЧЕ
		|		""Контр. """""" + ПрочиеРасходы.Контрагент.НаименованиеПолное + """""""" + "", "" + (ВЫРАЗИТЬ(ПрочиеРасходы.НаименованиеВходящегоДокумента КАК СТРОКА(100))) + "" № "" 
		|	КОНЕЦ	КАК НаименованиеРасхода,
		|	ПрочиеРасходы.НомерВходящегоДокумента          КАК ДокументНомер,
		|	ПрочиеРасходы.ДатаВходящегоДокумента           КАК ДокументДата,
		|	ПрочиеРасходы.СтатьяРасходов                   КАК СтатьяРасходов,
		|	ПрочиеРасходы.АналитикаРасходов                КАК АналитикаРасходов,
		|	ПрочиеРасходы.Контрагент                       КАК Контрагент,
		|	NULL                                           КАК КонтрагентОплаты,
		|	ВЫБОР КОГДА ПрочиеРасходы.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
		|		ПрочиеРасходы.Ссылка.Валюта
		|	ИНАЧЕ
		|		ПрочиеРасходы.Валюта
		|	КОНЕЦ КАК Валюта,
		|	ПрочиеРасходы.СчетУчета                        КАК СчетУчета,
		|	ВЫБОР КОГДА НЕ ПрочиеРасходы.Отменено ТОГДА
		|		ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|		ИНАЧЕ
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПоУчету,
		
		|	ВЫБОР КОГДА НЕ ПрочиеРасходы.Отменено ТОГДА
		|		ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ПрочиеРасходы.Сумма
		|		ИНАЧЕ
		|			0
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПоУчетуВВалюте,
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|	ИНАЧЕ
		|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
		|	КОНЕЦ КАК ПоОтчету,
		
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|		ПрочиеРасходы.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПоОтчетуВВалюте,
		|	
		|	ЕСТЬNULL(Суммы.СуммаНДСРегл, ПрочиеРасходы.СуммаНДС) КАК СуммаНДС,
		
		|	1 КАК НомерРаздела,
		|	ПрочиеРасходы.ИдентификаторСтроки КАК НомерСтроки,
		|	ПрочиеРасходы.НомерСтроки КАК НомерСтрокиУпорядочивания,
		|	NULL КАК СуммаРегл
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДвиженияПрочиеРасходы
		|	ПО
		|		ДвиженияПрочиеРасходы.Регистратор = ПрочиеРасходы.Ссылка
		|		И ДвиженияПрочиеРасходы.СтатьяДоходовРасходов = ПрочиеРасходы.СтатьяРасходов
		|		И ДвиженияПрочиеРасходы.АналитикаРасходов = ПрочиеРасходы.АналитикаРасходов
		|		И ДвиженияПрочиеРасходы.АналитикаАктивовПассивов = ПрочиеРасходы.АналитикаАктивовПассивов
		|		И ДвиженияПрочиеРасходы.Подразделение = ПрочиеРасходы.Подразделение
		|		И ДвиженияПрочиеРасходы.СуммаВВалюте = ПрочиеРасходы.Сумма
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|	ПО
		|		Суммы.Регистратор = ПрочиеРасходы.Ссылка
		|		И Суммы.ИдентификаторСтроки = ПрочиеРасходы.ИдентификаторСтроки
		|		И Суммы.СуммаБезНДСРегл <> 0
		|		
		|	
		|ГДЕ
		|	ПрочиеРасходы.Ссылка В (&МассивДокументов)
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ОплатаПоставщикам.Ссылка,
		|	ОплатаПоставщикам.Ссылка,
		|	""Контр. """""" + ОплатаПоставщикам.Контрагент.НаименованиеПолное + """""""" + "", накладна № "",
		|	ОплатаПоставщикам.НомерВходящегоДокумента,
		|	ОплатаПоставщикам.ДатаВходящегоДокумента,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ОплатаПоставщикам.Контрагент,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
		|		ОплатаПоставщикам.Ссылка.Валюта
		|	ИНАЧЕ
		|		ОплатаПоставщикам.Валюта
		|	КОНЕЦ КАК Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ОплатаПоставщикам.Сумма,
		|	ВЫБОР
		|		КОГДА ОплатаПоставщикам.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ОплатаПоставщикам.Сумма
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	ОплатаПоставщикам.Сумма,
		|	ВЫБОР
		|		КОГДА ОплатаПоставщикам.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ОплатаПоставщикам.Сумма
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	0,
		
		|	2 КАК НомерРаздела,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтроки,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтрокиУпорядочивания,
		|	ДенежныеСредстваУПодотчетныхЛиц.СуммаРегл	
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваУПодотчетныхЛиц
		|				ПО (ДенежныеСредстваУПодотчетныхЛиц.Регистратор = ОплатаПоставщикам.Ссылка
		|				И ДенежныеСредстваУПодотчетныхЛиц.Сумма = ОплатаПоставщикам.Сумма)
		|	
		|ГДЕ
		|	ОплатаПоставщикам.Ссылка В (&МассивДокументов)
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ДокументПриобретение.АвансовыйОтчет,
		|	ДокументПриобретение.Ссылка,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НаименованиеВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НаименованиеВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НаименованиеВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НомерВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НомерВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НомерВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1,1,1) ТОГДА
		|			ДокументПриобретение.ДатаВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.ДатаВходящегоДокумента
		|	КОНЕЦ,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ДокументПриобретение.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	СУММА(ДанныеДокумента.СуммаСНДС),
		|	СУММА(ВЫБОР
		|		КОГДА ДокументПриобретение.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДанныеДокумента.СуммаСНДС, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ),
		|	СУММА(ДанныеДокумента.СуммаСНДС),
		|	СУММА(ВЫБОР
		|		КОГДА ДокументПриобретение.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДанныеДокумента.СуммаСНДС, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ),
		|	0,
		|	
		|	0 КАК НомерРаздела,
		|	МАКСИМУМ(ДанныеДокумента.ИдентификаторСтроки) КАК НомерСтроки,
		|	МАКСИМУМ(ДанныеДокумента.НомерСтроки) КАК НомерСтрокиУпорядочивания,	
		|	NULL
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ДокументПриобретение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ПриобретениеТоваровУслуг.Товары КАК ДанныеДокумента
		|	ПО
		|		ДокументПриобретение.Ссылка = ДанныеДокумента.Ссылка
		|	
		|ГДЕ
		|	ДокументПриобретение.АвансовыйОтчет В (&МассивДокументов)
		|	И ДокументПриобретение.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
		|	И ДокументПриобретение.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокумента.Ссылка,
		|	ДокументПриобретение.Ссылка,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НаименованиеВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НаименованиеВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НаименованиеВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НомерВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НомерВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НомерВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1,1,1) ТОГДА
		|			ДокументПриобретение.ДатаВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.ДатаВходящегоДокумента
		|	КОНЕЦ
		|
		|	
		//++ НЕ УТ
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ДанныеДокумента.АвансовыйОтчет,
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Ссылка.НаименованиеВходящегоДокумента,
		|	ДанныеДокумента.НомерВходящегоДокумента,
		|	ДанныеДокумента.ДатаВходящегоДокумента,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ДенежныеСредства.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	0,
		
		|	0 КАК НомерРаздела,
		|	ДанныеДокумента.Номер КАК НомерСтроки,
		|	ДанныеДокумента.Номер КАК НомерСтрокиУпорядочивания,
		|   NULL
		|ИЗ
		|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
		|	ПО
		|		ДенежныеСредства.Регистратор = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПодотчетника)
		|	
		//-- НЕ УТ
		|
		|УПОРЯДОЧИТЬ ПО                                                        	
		|	НомерРаздела,
		|	НомерСтрокиУпорядочивания
		|
		|ИТОГИ ПО
		|	Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ // 15
		|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|	АвансовыйОтчетНаПечатьВТ3.Организация,
		|	АвансовыйОтчетНаПечатьВТ3.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
		|	ТаблицаКонвертацияВалюты.Валюта КАК ВалютаИсходная,
		|	ТаблицаКонвертацияВалюты.Сумма КАК СуммаИсходная,
		|	ТаблицаКонвертацияВалюты.ВалютаКонвертации КАК ВалютаКонвертации,
		|	ТаблицаКонвертацияВалюты.СуммаКонвертации КАК СуммаКонвертации
		|ИЗ
		|	АвансовыйОтчетНаПечатьВТ3
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаКонвертацияВалюты
		|	ПО
		|		ТаблицаКонвертацияВалюты.Ссылка = АвансовыйОтчетНаПечатьВТ3.Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ //16
		|	МАКСИМУМ(Выдано.Валюта) КАК Валюта,
		|	МАКСИМУМ(Выдано.СуммаВыданоНаличными) КАК СуммаВыданоНаличными,
		|	МАКСИМУМ(Выдано.СуммаВыданоНаличнымиРегл) КАК СуммаВыданоНаличнымиРегл,
		|	МАКСИМУМ(Выдано.СуммаВыданоКартой) КАК СуммаВыданоКартой,
		|	МАКСИМУМ(Выдано.СуммаВыданоКартойРегл) КАК СуммаВыданоКартойРегл,
		|	Выдано.Регистратор КАК Регистратор,
		|	Выдано.Ссылка КАК Ссылка,
		|	Выдано.Регистратор.Дата КАК РегистраторДата,
		|	ВЫБОР 
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
		|		ИНАЧЕ Выдано.Регистратор.Номер
		|	КОНЕЦ КАК РегистраторНомер
		|ИЗ
		|	Выдано КАК Выдано
		|СГРУППИРОВАТЬ ПО
		|	Выдано.Валюта,
		|	Выдано.Ссылка,
		|	Выдано.Регистратор,
		|	ВЫБОР 
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
		|		ИНАЧЕ Выдано.Регистратор.Номер
		|	КОНЕЦ
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
	
	Иначе
		ТекстЗапроса = "		
		|ВЫБРАТЬ
		|		ДанныеРегистра.Организация КАК Организация,
		|		ДанныеРегистра.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ДанныеРегистра.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ДанныеРегистра.Подразделение КАК Подразделение,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ТОГДА ДанныеРегистра.Регистратор
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|		КОНЕЦ КАК Ссылка,
		|		МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодОтчет,
		|		МИНИМУМ(ДанныеРегистра.Период) КАК НачальныйПериодОтчет
		|	ПОМЕСТИТЬ ВТПериоды1
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|	ГДЕ
		|		(ДанныеРегистра.Регистратор В (&МассивДокументов)
		|				ИЛИ ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет В (&МассивДокументов)
		//++ НЕ УТ
		|				ИЛИ ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет В (&МассивДокументов)
		//-- НЕ УТ
		|				)

		|	СГРУППИРОВАТЬ ПО
		|		ДанныеРегистра.Организация,
		|		ДанныеРегистра.ПодотчетноеЛицо,
		|		ДанныеРегистра.Подразделение,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ТОГДА ДанныеРегистра.Регистратор
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|		КОНЕЦ
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация КАК Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение КАК Подразделение,
		|		ВТПериоды.Ссылка КАК Ссылка,
		|		ВТПериоды.НачальныйПериодОтчет КАК НачальныйПериодОтчет,
		|		МАКСИМУМ(ДанныеРегистра.Регистратор) КАК Регистратор
		|	ПОМЕСТИТЬ ВТПериоды2
		|	ИЗ
		|		ВТПериоды1 КАК ВТПериоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|			ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|				И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|				И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|				И (ДанныеРегистра.Период = ВТПериоды.ПериодОтчет)
		|				И (ВЫБОР
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|						ТОГДА ДанныеРегистра.Регистратор
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|						ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|						ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|				КОНЕЦ = ВТПериоды.Ссылка)

		|	СГРУППИРОВАТЬ ПО
		|		ВТПериоды.Организация,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.НачальныйПериодОтчет
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация КАК Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение КАК Подразделение,
		|		ВТПериоды.Ссылка КАК Ссылка,
		|		ВТПериоды.НачальныйПериодОтчет КАК НачальныйПериодОтчет,
		|		ВТПериоды.Регистратор КАК Регистратор,
		|		ВЫБОР
		|			КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.АвансовыйОтчет).МоментВремени
		|			КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|			КОГДА ВТПериоды.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|		КОНЕЦ КАК МоментВремениТекущийОтчет
		|	ПОМЕСТИТЬ ВТПериоды3
		|	ИЗ
		|		ВТПериоды2 КАК ВТПериоды
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация КАК Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение КАК Подразделение,
		|		ВТПериоды.Ссылка КАК Ссылка,
		|		ВТПериоды.Регистратор КАК Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет КАК МоментВремениТекущийОтчет,
		|		МАКСИМУМ(ДанныеРегистра.Регистратор) КАК НачальныйРегистратор
		|	ПОМЕСТИТЬ ВТПериоды4
		|	ИЗ
		|		ВТПериоды3 КАК ВТПериоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|			ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|				И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|				И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|				И (ДанныеРегистра.Период = ВТПериоды.НачальныйПериодОтчет)
		|				И (ВЫБОР
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|						ТОГДА ДанныеРегистра.Регистратор
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|						ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПриобретениеТоваровУслуг).АвансовыйОтчет
		//++ НЕ УТ
		|					КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|						ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ПоступлениеДенежныхДокументов).АвансовыйОтчет
		//-- НЕ УТ
		|				КОНЕЦ = ВТПериоды.Ссылка)

		|	СГРУППИРОВАТЬ ПО
		|		ВТПериоды.Организация,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация КАК Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение КАК Подразделение,
		|		ВТПериоды.Ссылка КАК Ссылка,
		|		ВТПериоды.Регистратор КАК Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет,
		|		ВЫБОР
		|			КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.АвансовыйОтчет).МоментВремени
		|			КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|			КОГДА ВТПериоды.НачальныйРегистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		|				ТОГДА ВЫРАЗИТЬ(ВТПериоды.НачальныйРегистратор КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|		КОНЕЦ КАК НачальныйМоментВремениТекущийОтчет
		|	ПОМЕСТИТЬ ВТПериоды5
		|	ИЗ
		|		ВТПериоды4 КАК ВТПериоды
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация КАК Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение КАК Подразделение,
		|		ВТПериоды.Ссылка КАК Ссылка,
		|		ВТПериоды.Регистратор КАК Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет КАК МоментВремениТекущийОтчет,
		|		МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодПредыдущийОтчет
		|	ПОМЕСТИТЬ ВТПериоды6
		|	ИЗ
		|		ВТПериоды5 КАК ВТПериоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|			ПО (ДанныеРегистра.Организация = ВТПериоды.Организация)
		|				И (ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо)
		|				И (ДанныеРегистра.Подразделение = ВТПериоды.Подразделение)
		|				И (ДанныеРегистра.МоментВремени < ВТПериоды.НачальныйМоментВремениТекущийОтчет)
		|				И (ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|					ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		//++ НЕ УТ
		|					ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		//-- НЕ УТ
		|				)

		|	СГРУППИРОВАТЬ ПО
		|		ВТПериоды.Организация,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет
		|	;

		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет,
		|		МАКСИМУМ(ДанныеРегистра.Регистратор) КАК ПредыдущийОтчет
		|	ПОМЕСТИТЬ ВТПериоды7
		|	ИЗ
		|		ВТПериоды6 КАК ВТПериоды

		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
		|		ПО
		|			ДанныеРегистра.Организация = ВТПериоды.Организация
		|			И ДанныеРегистра.ПодотчетноеЛицо = ВТПериоды.ПодотчетноеЛицо
		|			И ДанныеРегистра.Подразделение = ВТПериоды.Подразделение
		|			И ДанныеРегистра.Период = ВТПериоды.ПериодПредыдущийОтчет	
		|			И (ДанныеРегистра.Регистратор ССЫЛКА Документ.АвансовыйОтчет
		|				ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		//++ НЕ УТ
		|				ИЛИ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеДенежныхДокументов
		//-- НЕ УТ
		|				)

		|	СГРУППИРОВАТЬ ПО
		|		ВТПериоды.Организация,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет
		|	;
		|		
		|	ВЫБРАТЬ
		|		ВТПериоды.Организация,
		|		ВТПериоды.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ВТПериоды.ПодотчетноеЛицо,
		|		ВТПериоды.Подразделение,
		|		ВТПериоды.Ссылка,
		|		ВТПериоды.Регистратор,
		|		ВТПериоды.МоментВремениТекущийОтчет,
		|		ВЫБОР
		|			КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.АвансовыйОтчет ТОГДА
		|				ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.АвансовыйОтчет).МоментВремени
		|			КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.ПриобретениеТоваровУслуг ТОГДА
		|				ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.ПриобретениеТоваровУслуг).МоментВремени
		//++ НЕ УТ
		|			КОГДА ВТПериоды.ПредыдущийОтчет ССЫЛКА Документ.ПоступлениеДенежныхДокументов ТОГДА
		|				ВЫРАЗИТЬ(ВТПериоды.ПредыдущийОтчет КАК Документ.ПоступлениеДенежныхДокументов).МоментВремени
		//-- НЕ УТ
		|		КОНЕЦ КАК МоментВремениПредыдущийОтчет
		|	ПОМЕСТИТЬ АвансовыйОтчетНаПечатьВТ3
		|	ИЗ
		|		ВТПериоды7 КАК ВТПериоды
		|	;
		|	
		|	////////////////////////////////////////////////////////////////////////////////
		|	ВЫБРАТЬ // 8
		|		АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|		ДенежныеСредстваВыдано.Валюта,
		|		
		|		Максимум(ВЫБОР
		|			КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
		|				ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК СуммаВыданоНаличными,
		|		
		|		Максимум(ВЫБОР
		|			КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И ТИПЗНАЧЕНИЯ(ДенежныеСредстваВыдано.Регистратор) В (&РегистраторыДенежныеСредстваВыдано) ТОГДА
		|				ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК СуммаВыданоНаличнымиРегл,
		|	
		|		Максимум(ВЫБОР
		|			КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|				ЕСТЬNULL(ДенежныеСредстваВыдано.Сумма, 0)
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК СуммаВыданоКартой,
		|		
		|		Максимум(ВЫБОР
		|			КОГДА ДенежныеСредстваВыдано.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				И ДенежныеСредстваВыдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|				ЕСТЬNULL(ДенежныеСредстваВыдано.СуммаРегл, 0)
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК СуммаВыданоКартойРегл,
		|		ДенежныеСредстваВыдано.Регистратор
		|	
		|	ПОМЕСТИТЬ Выдано
		|	ИЗ
		|		АвансовыйОтчетНаПечатьВТ3
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваВыдано
		|		ПО
		|			((ДенежныеСредстваВыдано.МоментВремени > АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ИЛИ АвансовыйОтчетНаПечатьВТ3.МоментВремениПредыдущийОтчет ЕСТЬ NULL)
		|				И ДенежныеСредстваВыдано.МоментВремени < АвансовыйОтчетНаПечатьВТ3.МоментВремениТекущийОтчет)
		|			И ДенежныеСредстваВыдано.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|			И ДенежныеСредстваВыдано.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|			И ДенежныеСредстваВыдано.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
		|	СГРУППИРОВАТЬ ПО
		|		АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|		ДенежныеСредстваВыдано.Валюта,
		|		ДенежныеСредстваВыдано.Регистратор
		|	;
		|	////////////////////////////////////////////////////////////////////////////////
		|	
		|	ВЫБРАТЬ // 9
		|		Израсходовано.Ссылка КАК Ссылка,
		|		СУММА(Израсходовано.Сумма) КАК СуммаИзрасходовано,
		|		СУММА(Израсходовано.СуммаРегл) КАК СуммаИзрасходованоРегл
		|	ПОМЕСТИТЬ Израсходовано
		|	ИЗ (
		|		ВЫБРАТЬ
		|			АвансовыйОтчетНаПечатьВТ3.Ссылка КАК Ссылка,
		|			ЕСТЬNULL(СУММА(ДенежныеСредства.Сумма), 0) КАК Сумма,
		|			ЕСТЬNULL(СУММА(ДенежныеСредства.СуммаРегл), 0) КАК СуммаРегл
		|		ИЗ
		|			АвансовыйОтчетНаПечатьВТ3
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ
		|				РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
		|			ПО
		|				ДенежныеСредства.Регистратор = АвансовыйОтчетНаПечатьВТ3.Ссылка
		|				И ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И ДенежныеСредства.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
		|				И ДенежныеСредства.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалютыПодотчетнымЛицом)
		|				И ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|				И ДенежныеСредства.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|		СГРУППИРОВАТЬ ПО
		|			АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|			ДенежныеСредства.Валюта
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ДанныеДокумента.АвансовыйОтчет КАК Ссылка,
		|			ДенежныеСредства.СуммаРасход КАК Сумма,
		|			ДенежныеСредства.СуммаРеглРасход КАК СуммаРегл
		|		ИЗ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(,, Регистратор) КАК ДенежныеСредства
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
		|			ПО ДанныеДокумента.Ссылка = ДенежныеСредства.Регистратор
		|		ГДЕ
		|			ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|			
		//++ НЕ УТ
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ДанныеДокумента.АвансовыйОтчет КАК Ссылка,
		|			ДенежныеСредства.СуммаРасход КАК Сумма,
		|			ДенежныеСредства.СуммаРеглРасход КАК СуммаРегл
		|		ИЗ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(,, Регистратор) КАК ДенежныеСредства
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|			ПО ДанныеДокумента.Ссылка = ДенежныеСредства.Регистратор
		|		ГДЕ
		|			ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		//-- НЕ УТ
		|		) КАК Израсходовано
		|	СГРУППИРОВАТЬ ПО
		|		Израсходовано.Ссылка
		|	;
		|	////////////////////////////////////////////////////////////////////////////////
		|	
		|	ВЫБРАТЬ // 10
		|		АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|		ДенежныеСредства.Организация,
		|		ДенежныеСредства.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|		ДенежныеСредства.Подразделение,
		|		ДенежныеСредства.ПодотчетноеЛицо,
		|		ДенежныеСредства.Подразделение,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаКонечныйОстаток, 0) > 0 ТОГДА
		|				ДенежныеСредства.СуммаКонечныйОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КонечныйОстаток,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаКонечныйОстаток, 0) < 0 ТОГДА
		|				-ДенежныеСредства.СуммаКонечныйОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КонечныйПерерасход,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаРеглКонечныйОстаток, 0) > 0 ТОГДА
		|				ДенежныеСредства.СуммаРеглКонечныйОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КонечныйОстатокРегл,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(ДенежныеСредства.СуммаРеглКонечныйОстаток, 0) < 0 ТОГДА
		|				-ДенежныеСредства.СуммаРеглКонечныйОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КонечныйПерерасходРегл
		|	ПОМЕСТИТЬ ТекущийАвансВрем
		|	ИЗ
		|		АвансовыйОтчетНаПечатьВТ3
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Регистратор,,
		|				(Организация, Подотчетноелицо, Подразделение) В (ВЫБРАТЬ Организация, Подотчетноелицо, Подразделение ИЗ ВТПериоды1)) КАК ДенежныеСредства
		|		ПО
		|			ДенежныеСредства.Регистратор = АвансовыйОтчетНаПечатьВТ3.Регистратор
		|			И ДенежныеСредства.Организация = АвансовыйОтчетНаПечатьВТ3.Организация
		|			И ДенежныеСредства.Подразделение = АвансовыйОтчетНаПечатьВТ3.Подразделение
		|			И ДенежныеСредства.ПодотчетноеЛицо = АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо
		|	;
		|	////////////////////////////////////////////////////////////////////////////////
		|	
		|	ВЫБРАТЬ // 11
		|		ТекущийАвансВрем.Ссылка,
		|		СУММА(ТекущийАвансВрем.КонечныйОстаток),
		|		СУММА(ТекущийАвансВрем.КонечныйПерерасход),
		|		СУММА(ТекущийАвансВрем.КонечныйОстатокРегл),
		|		СУММА(ТекущийАвансВрем.КонечныйПерерасходРегл)
		|	ПОМЕСТИТЬ Остатки
		|	ИЗ
		|		ТекущийАвансВрем
		|	СГРУППИРОВАТЬ ПО
		|		ТекущийАвансВрем.Ссылка
		|			;
		|	////////////////////////////////////////////////////////////////////////////////
		|	
		|	ВЫБРАТЬ // 12
		|		ДанныеШапки.Ссылка КАК Ссылка,
		|		СУММА(ДанныеШапки.СуммаВыданоНаличными)   КАК СуммаВыданоНаличными,
		|		СУММА(ДанныеШапки.СуммаВыданоКартой)      КАК СуммаВыданоКартой,
		|		СУММА(ДанныеШапки.КонечныйОстаток)        КАК КонечныйОстаток,
		|		СУММА(ДанныеШапки.КонечныйПерерасход)     КАК КонечныйПерерасход,
		|		СУММА(ДанныеШапки.СуммаИзрасходовано)     КАК СуммаИзрасходовано,
		|		ВЫБОР
		|			КОГДА СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой) > 0 ТОГДА
		|				СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой)
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НачальныйОстаток,
		|		ВЫБОР	
		|			КОГДА СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой) < 0 ТОГДА
		|				-1 * (СУММА(ДанныеШапки.КонечныйОстаток) - СУММА(ДанныеШапки.КонечныйПерерасход) + СУММА(ДанныеШапки.СуммаИзрасходовано) - СУММА(ДанныеШапки.СуммаВыданоНаличными) - СУММА(ДанныеШапки.СуммаВыданоКартой))
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НачальныйПерерасход,
		|		СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл)   КАК СуммаВыданоНаличнымиРегл,
		|		СУММА(ДанныеШапки.СуммаВыданоКартойРегл)      КАК СуммаВыданоКартойРегл,
		|		СУММА(ДанныеШапки.КонечныйОстатокРегл)        КАК КонечныйОстатокРегл,
		|		СУММА(ДанныеШапки.КонечныйПерерасходРегл)     КАК КонечныйПерерасходРегл,
		|		СУММА(ДанныеШапки.СуммаИзрасходованоРегл)     КАК СуммаИзрасходованоРегл,
		|		ВЫБОР
		|			КОГДА СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл) > 0 ТОГДА
		|				СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл)
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НачальныйОстатокРегл,
		|		ВЫБОР
		|			КОГДА СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл) < 0 ТОГДА
		|				-1 * (СУММА(ДанныеШапки.КонечныйОстатокРегл) - СУММА(ДанныеШапки.КонечныйПерерасходРегл) + СУММА(ДанныеШапки.СуммаИзрасходованоРегл) - СУММА(ДанныеШапки.СуммаВыданоНаличнымиРегл) - СУММА(ДанныеШапки.СуммаВыданоКартойРегл))
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НачальныйПерерасходРегл
		|	ИЗ
		|	(ВЫБРАТЬ
		|		Выдано.Ссылка КАК Ссылка,
		|		Выдано.СуммаВыданоНаличными     КАК СуммаВыданоНаличными,
		|		Выдано.СуммаВыданоКартой        КАК СуммаВыданоКартой,
		|		0                               КАК КонечныйОстаток,
		|		0                               КАК КонечныйПерерасход,
		|		0                               КАК СуммаИзрасходовано,
		|		Выдано.СуммаВыданоНаличнымиРегл КАК СуммаВыданоНаличнымиРегл,
		|		Выдано.СуммаВыданоКартойРегл    КАК СуммаВыданоКартойРегл,
		|		0                               КАК КонечныйОстатокРегл,
		|		0                               КАК КонечныйПерерасходРегл,
		|		0                               КАК СуммаИзрасходованоРегл
		|	ИЗ
		|		Выдано
		|	ГДЕ
		|		Выдано.СуммаВыданоНаличными <> 0 ИЛИ Выдано.СуммаВыданоКартой <> 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Остатки.Ссылка,
		|		0,
		|		0,
		|		Остатки.КонечныйОстаток,
		|		Остатки.КонечныйПерерасход,
		|		0,
		|		0,
		|		0,
		|		Остатки.КонечныйОстатокРегл,
		|		Остатки.КонечныйПерерасходРегл,
		|		0	
		|	ИЗ
		|		Остатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Израсходовано.Ссылка КАК Ссылка,
		|		0,
		|		0,
		|		0,
		|		0,
		|		Израсходовано.СуммаИзрасходовано,
		|		0,
		|		0,
		|		0,
		|		0,
		|		Израсходовано.СуммаИзрасходованоРегл
		|	ИЗ
		|		Израсходовано) КАК ДанныеШапки
		|	СГРУППИРОВАТЬ ПО
		|		ДанныеШапки.Ссылка
		|
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ // 13
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	ДанныеДокумента.Номер КАК Номер,
		|	ДанныеДокумента.Организация.Префикс КАК Префикс,
		|	ДанныеДокумента.Дата КАК ДатаДокумента,
		|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
		|	ДанныеДокумента.ПодотчетноеЛицо.КодПоДРФО КАК ДРФОПодотчетногоЛица,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.ПодотчетноеЛицо) КАК ПредставлениеПодотчетногоЛица,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ДанныеДокумента.НазначениеАванса КАК НазначениеАванса,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Подразделение) КАК ПредставлениеПодразделения,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Валюта) КАК ПредставлениеВалютыДокумента,
		|	ДанныеДокумента.Мультивалютный КАК Мультивалютный,
		|	ДанныеДокумента.КоличествоДокументов КАК КоличествоДокументов
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|	
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаДокумента,
		|	Номер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ // 14
		|	ПрочиеРасходы.Ссылка                           КАК Ссылка,
		|	ПрочиеРасходы.Ссылка                           КАК ДокументДвижения,
		|	ВЫБОР КОГДА ПрочиеРасходы.ЭтоСуточные = ИСТИНА ТОГДА
		|		ПрочиеРасходы.НаименованиеВходящегоДокумента
		|	ИНАЧЕ
		|		""Контр. """""" + ПрочиеРасходы.Контрагент.НаименованиеПолное + """""""" + "", "" + (ВЫРАЗИТЬ(ПрочиеРасходы.НаименованиеВходящегоДокумента КАК СТРОКА(100))) + "" № "" 
		|	КОНЕЦ	КАК НаименованиеРасхода,
		|	ПрочиеРасходы.НомерВходящегоДокумента          КАК ДокументНомер,
		|	ПрочиеРасходы.ДатаВходящегоДокумента           КАК ДокументДата,
		|	ПрочиеРасходы.СтатьяРасходов                   КАК СтатьяРасходов,
		|	ПрочиеРасходы.АналитикаРасходов                КАК АналитикаРасходов,
		|	ПрочиеРасходы.Контрагент                       КАК Контрагент,
		|	NULL                                           КАК КонтрагентОплаты,
		|	ВЫБОР КОГДА ПрочиеРасходы.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
		|		ПрочиеРасходы.Ссылка.Валюта
		|	ИНАЧЕ
		|		ПрочиеРасходы.Валюта
		|	КОНЕЦ КАК Валюта,
		|	ПрочиеРасходы.СчетУчета                        КАК СчетУчета,
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|	ИНАЧЕ
		|		ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
		|	КОНЕЦ КАК ПоОтчету,
		
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|		ПрочиеРасходы.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПоОтчетуВВалюте,
		|	
		|	ЕСТЬNULL(Суммы.СуммаНДСРегл, ПрочиеРасходы.СуммаНДС) КАК СуммаНДС,
		
		|	1 КАК НомерРаздела,
		|	ПрочиеРасходы.ИдентификаторСтроки КАК НомерСтроки,
		|	ПрочиеРасходы.НомерСтроки КАК НомерСтрокиУпорядочивания,
		|	NULL КАК СуммаРегл
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДвиженияПрочиеРасходы
		|	ПО
		|		ДвиженияПрочиеРасходы.Регистратор = ПрочиеРасходы.Ссылка
		|		И ДвиженияПрочиеРасходы.СтатьяДоходовРасходов = ПрочиеРасходы.СтатьяРасходов
		|		И ДвиженияПрочиеРасходы.АналитикаРасходов = ПрочиеРасходы.АналитикаРасходов
		|		И ДвиженияПрочиеРасходы.АналитикаАктивовПассивов = ПрочиеРасходы.АналитикаАктивовПассивов
		|		И ДвиженияПрочиеРасходы.Подразделение = ПрочиеРасходы.Подразделение
		|		И ДвиженияПрочиеРасходы.СуммаВВалюте = ПрочиеРасходы.Сумма
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|	ПО
		|		Суммы.Регистратор = ПрочиеРасходы.Ссылка
		|		И Суммы.ИдентификаторСтроки = ПрочиеРасходы.ИдентификаторСтроки
		|		И Суммы.СуммаБезНДСРегл <> 0
		|		
		|	
		|ГДЕ
		|	ПрочиеРасходы.Ссылка В (&МассивДокументов)
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ОплатаПоставщикам.Ссылка,
		|	ОплатаПоставщикам.Ссылка,
		|	""Контр. """""" + ОплатаПоставщикам.Контрагент.НаименованиеПолное + """""""" + "", накладна № "",
		|	ОплатаПоставщикам.НомерВходящегоДокумента,
		|	ОплатаПоставщикам.ДатаВходящегоДокумента,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ОплатаПоставщикам.Контрагент,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
		|		ОплатаПоставщикам.Ссылка.Валюта
		|	ИНАЧЕ
		|		ОплатаПоставщикам.Валюта
		|	КОНЕЦ КАК Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ОплатаПоставщикам.Сумма,
		|	ВЫБОР
		|		КОГДА ОплатаПоставщикам.Ссылка.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ОплатаПоставщикам.Сумма
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	0,
		
		|	2 КАК НомерРаздела,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтроки,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтрокиУпорядочивания,
		|	ДенежныеСредстваУПодотчетныхЛиц.СуммаРегл	
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредстваУПодотчетныхЛиц
		|				ПО (ДенежныеСредстваУПодотчетныхЛиц.Регистратор = ОплатаПоставщикам.Ссылка
		|				И ДенежныеСредстваУПодотчетныхЛиц.Сумма = ОплатаПоставщикам.Сумма)
		|	
		|ГДЕ
		|	ОплатаПоставщикам.Ссылка В (&МассивДокументов)
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ДокументПриобретение.АвансовыйОтчет,
		|	ДокументПриобретение.Ссылка,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НаименованиеВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НаименованиеВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НаименованиеВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НомерВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НомерВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НомерВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1,1,1) ТОГДА
		|			ДокументПриобретение.ДатаВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.ДатаВходящегоДокумента
		|	КОНЕЦ,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ДокументПриобретение.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	СУММА(ДанныеДокумента.СуммаСНДС),
		|	СУММА(ВЫБОР
		|		КОГДА ДокументПриобретение.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДанныеДокумента.СуммаСНДС, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ),
		|	0,
		|	
		|	0 КАК НомерРаздела,
		|	МАКСИМУМ(ДанныеДокумента.ИдентификаторСтроки) КАК НомерСтроки,
		|	МАКСИМУМ(ДанныеДокумента.НомерСтроки) КАК НомерСтрокиУпорядочивания,	
		|	NULL
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ДокументПриобретение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ПриобретениеТоваровУслуг.Товары КАК ДанныеДокумента
		|	ПО
		|		ДокументПриобретение.Ссылка = ДанныеДокумента.Ссылка
		|	
		|ГДЕ
		|	ДокументПриобретение.АвансовыйОтчет В (&МассивДокументов)
		|	И ДокументПриобретение.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
		|	И ДокументПриобретение.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокумента.Ссылка,
		|	ДокументПриобретение.Ссылка,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НаименованиеВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НаименованиеВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НаименованиеВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.НомерВходящегоДокумента = """" ТОГДА
		|			ДокументПриобретение.НомерВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.НомерВходящегоДокумента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1,1,1) ТОГДА
		|			ДокументПриобретение.ДатаВходящегоДокумента
		|		ИНАЧЕ
		|			ДанныеДокумента.ДатаВходящегоДокумента
		|	КОНЕЦ
		|
		|	
		//++ НЕ УТ
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ДанныеДокумента.АвансовыйОтчет,
		|	ДанныеДокумента.Ссылка,
		|	"""",
		|	ДанныеДокумента.НомерВходящегоДокумента,
		|	ДанныеДокумента.ДатаВходящегоДокумента,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ДенежныеСредства.Валюта,
		|	НЕОПРЕДЕЛЕНО,
		|	ЕСТЬNULL(ДенежныеСредства.СуммаРегл, 0),
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АвансовыйОтчет.Валюта <> &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДенежныеСредства.Сумма, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ,
		|	0,
		
		|	0 КАК НомерРаздела,
		|	ДанныеДокумента.Номер КАК НомерСтроки,
		|	ДанныеДокумента.Номер КАК НомерСтрокиУпорядочивания,
		|   NULL
		|ИЗ
		|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
		|	ПО
		|		ДенежныеСредства.Регистратор = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПодотчетника)
		|	
		//-- НЕ УТ
		|
		|УПОРЯДОЧИТЬ ПО                                                        	
		|	НомерРаздела,
		|	НомерСтрокиУпорядочивания
		|
		|ИТОГИ ПО
		|	Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ // 15
		|	АвансовыйОтчетНаПечатьВТ3.Ссылка,
		|	АвансовыйОтчетНаПечатьВТ3.Организация,
		|	АвансовыйОтчетНаПечатьВТ3.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	АвансовыйОтчетНаПечатьВТ3.ПодотчетноеЛицо,
		|	ТаблицаКонвертацияВалюты.Валюта КАК ВалютаИсходная,
		|	ТаблицаКонвертацияВалюты.Сумма КАК СуммаИсходная,
		|	ТаблицаКонвертацияВалюты.ВалютаКонвертации КАК ВалютаКонвертации,
		|	ТаблицаКонвертацияВалюты.СуммаКонвертации КАК СуммаКонвертации
		|ИЗ
		|	АвансовыйОтчетНаПечатьВТ3
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаКонвертацияВалюты
		|	ПО
		|		ТаблицаКонвертацияВалюты.Ссылка = АвансовыйОтчетНаПечатьВТ3.Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ //16
		|	МАКСИМУМ(Выдано.Валюта) КАК Валюта,
		|	МАКСИМУМ(Выдано.СуммаВыданоНаличными) КАК СуммаВыданоНаличными,
		|	МАКСИМУМ(Выдано.СуммаВыданоНаличнымиРегл) КАК СуммаВыданоНаличнымиРегл,
		|	МАКСИМУМ(Выдано.СуммаВыданоКартой) КАК СуммаВыданоКартой,
		|	МАКСИМУМ(Выдано.СуммаВыданоКартойРегл) КАК СуммаВыданоКартойРегл,
		|	Выдано.Регистратор КАК Регистратор,
		|	Выдано.Ссылка КАК Ссылка,
		|	Выдано.Регистратор.Дата КАК РегистраторДата,
		|	ВЫБОР 
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
		|		ИНАЧЕ Выдано.Регистратор.Номер
		|	КОНЕЦ КАК РегистраторНомер
		|ИЗ
		|	Выдано КАК Выдано
		|СГРУППИРОВАТЬ ПО
		|	Выдано.Валюта,
		|	Выдано.Ссылка,
		|	Выдано.Регистратор,
		|	ВЫБОР 
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.РасходныйКассовыйОрдер).НомерОрдера
		|		КОГДА Выдано.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств ТОГДА
		|			ВЫРАЗИТЬ(Выдано.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).НомерПоручения
		|		ИНАЧЕ Выдано.Регистратор.Номер
		|	КОНЕЦ
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
	
	КонецЕсли;
		
	Если Не ИспользуетсяРеглУчет Тогда
		ТекстЗапроса = ТекстЗапроса + "        //16
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОплатаПоставщикам.Ссылка КАК Регистратор,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""631"" 
		|	ИНАЧЕ
		|		""632""
		|	КОНЕЦ КАК СчетДт,
			|	ОплатаПоставщикам.Контрагент КАК ЗначениеСубконто,
		|	ВЫБОР КОГДА ОплатаПоставщикам.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	ОплатаПоставщикам.НомерСтроки КАК НомерСтроки,
		|	ОплатаПоставщикам.Ссылка.Валюта КАК Валюта,
		|	ОплатаПоставщикам.Сумма,                                    
		|	Суммы.СуммаНДСРегл + Суммы.СуммаБезНДСРегл КАК СуммаБУ
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|		ПО
		|			Суммы.Регистратор = ОплатаПоставщикам.Ссылка
		|			И Суммы.ИдентификаторСтроки = ОплатаПоставщикам.ИдентификаторСтроки
		|			И Суммы.СуммаБезНДСРегл <> 0
		|ГДЕ
		|	ОплатаПоставщикам.Ссылка В (&МассивДокументов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрочиеРасходы.Ссылка КАК Регистратор,
		|	ПрочиеРасходы.СтатьяРасходов.КорреспондирующийСчет,
		|	ПрочиеРасходы.СтатьяРасходов КАК ЗначениеСубконто,
		|	ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	ПрочиеРасходы.НомерСтроки КАК НомерСтроки,
		|	ПрочиеРасходы.Ссылка.Валюта,
		|	ВЫБОР КОГДА НЕ ПрочиеРасходы.Отменено ТОГДА
		|		ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|		ИНАЧЕ        
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.Сумма, ЕСТЬNULL(Суммы.СуммаНДС, 0) + ЕСТЬNULL(Суммы.СуммаБезНДС, 0))
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ,
		|	ВЫБОР КОГДА НЕ ПрочиеРасходы.Отменено ТОГДА
		|		ВЫБОР КОГДА ПрочиеРасходы.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ПрочиеРасходы.Сумма)
		|		ИНАЧЕ        
		|			ЕСТЬNULL(ДвиженияПрочиеРасходы.СуммаРегл, ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) + ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДвиженияПрочиеРасходы
		|		ПО
		|			ДвиженияПрочиеРасходы.Регистратор = ПрочиеРасходы.Ссылка
		|			И ДвиженияПрочиеРасходы.НомерСтроки = ПрочиеРасходы.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|		ПО
		|			Суммы.Регистратор = ПрочиеРасходы.Ссылка
		|			И Суммы.ИдентификаторСтроки = ПрочиеРасходы.ИдентификаторСтроки
		|			И Суммы.СуммаБезНДСРегл <> 0
		|ГДЕ
		|	ПрочиеРасходы.Ссылка В (&МассивДокументов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка.АвансовыйОтчет КАК Регистратор,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар) ТОГДА
		|		""281""
		|	ИНАЧЕ
		|		""92""
		|	КОНЕЦ КАК СчетДт,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА

		|		""3721""
		|	ИНАЧЕ

		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	1 КАК НомерСтроки,
		|	ПриобретениеТоваровУслугТовары.Ссылка.Валюта,
		|	ВЫБОР
		|		КОГДА ПриобретениеТоваровУслугТовары.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
		|			ТОГДА СУММА(ПриобретениеТоваровУслугТовары.СуммаСНДС) - СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС)
		|		ИНАЧЕ СУММА(ПриобретениеТоваровУслугТовары.СуммаСНДС)
		|	КОНЕЦ,
		|	СУММА(Суммы.СуммаБезНДСРегл)		
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|		ПО
		|			Суммы.Регистратор = ПриобретениеТоваровУслугТовары.Ссылка
		|			И Суммы.ИдентификаторСтроки = ПриобретениеТоваровУслугТовары.ИдентификаторСтроки
		|			И Суммы.СуммаБезНДСРегл <> 0
		|	
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка.АвансовыйОтчет В (&МассивДокументов)
		|	
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Ссылка,
		|	ПриобретениеТоваровУслугТовары.НалоговоеНазначение,
		|	ПриобретениеТоваровУслугТовары.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Ссылка.АвансовыйОтчет КАК Регистратор,
		|	""6442"" КАК СчетДт,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР КОГДА ПриобретениеТоваровУслугТовары.Ссылка.Валюта = &ВалютаРегламентированногоУчета ТОГДА
		|		""3721""
		|	ИНАЧЕ
		|		""3722""
		|	КОНЕЦ КАК СчетКт,
		|	2 КАК НомерСтроки,
		|	ПриобретениеТоваровУслугТовары.Ссылка.Валюта,
		|	СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС) КАК Сумма,
		|	СУММА(Суммы.СуммаНДСРегл)
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|		ПО
		|			Суммы.Регистратор = ПриобретениеТоваровУслугТовары.Ссылка
		|			И Суммы.ИдентификаторСтроки = ПриобретениеТоваровУслугТовары.ИдентификаторСтроки
		|			И Суммы.СуммаНДСРегл <> 0
		|	
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка.Ссылка.АвансовыйОтчет В (&МассивДокументов)
		|		И ПриобретениеТоваровУслугТовары.СуммаНДС <> 0
		|	
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|";
	//++ НЕ УТ
	Иначе
		ТекстЗапроса = ТекстЗапроса + "           
		|ВЫБРАТЬ // 17
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Ссылка КАК ДокументДвижения
		|ПОМЕСТИТЬ ДокументыДвижения
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&МассивДокументов)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ДанныеДокумента.АвансовыйОтчет,
		|	ДанныеДокумента.Ссылка
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ДанныеДокумента.АвансовыйОтчет,
		|	ДанныеДокумента.Ссылка
		|ИЗ
		|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.АвансовыйОтчет В (&МассивДокументов)
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументДвижения
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ  //18
		|	ДокументыДвижения.Регистратор КАК Регистратор,
		|	ДокументыДвижения.ДокументДвижения КАК ДокументДвижения,
		|	Хозрасчетный.Период КАК Период,
		|	Хозрасчетный.СчетДт.Код КАК СчетДт,
		|	Хозрасчетный.СчетКт.Код КАК СчетКт,
		|	Хозрасчетный.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(Хозрасчетный.ВалютнаяСуммаДт, ЕСТЬNULL(Хозрасчетный.ВалютнаяСуммаКт, Хозрасчетный.Сумма)) КАК Сумма,
		|	ЕСТЬNULL(Хозрасчетный.ВалютаДт, ЕСТЬNULL(Хозрасчетный.ВалютаКт, &ВалютаРегламентированногоУчета)) КАК Валюта,
		|	Хозрасчетный.Сумма КАК СуммаБУ
		|ПОМЕСТИТЬ Проводки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ДокументыДвижения
		|	ПО
		|		ДокументыДвижения.ДокументДвижения = Хозрасчетный.Регистратор
		|	И НЕ Хозрасчетный.СчетДт.Забалансовый
		|	И НЕ Хозрасчетный.СчетКт.Забалансовый
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Проводки.Регистратор КАК Регистратор,
		|	Проводки.СчетДт КАК СчетДт,
		|	Проводки.СчетКт КАК СчетКт,
		|	МИНИМУМ(Проводки.НомерСтроки) КАК НомерСтроки,
		|	СУММА(Проводки.Сумма) КАК Сумма,
		|	Проводки.Валюта КАК Валюта,
		|	Проводки.СуммаБУ КАК СуммаБУ
		|ИЗ
		|	Проводки КАК Проводки
		|
		|СГРУППИРОВАТЬ ПО
		|	Проводки.Регистратор,
		|	Проводки.СчетДт,
		|	Проводки.СчетКт,
		|	Проводки.Валюта,
		|   Проводки.СуммаБУ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	НомерСтроки 
		|;";
	//-- НЕ УТ
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДополнительныеДокументыПечати(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|ГДЕ
	|	Данныедокумента.АвансовыйОтчет В (&МассивДокументов)
	|	И Данныедокумента.Проведен
	|
	//++ Локализация
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов КАК ДанныеДокумента
	|ГДЕ
	|	Данныедокумента.АвансовыйОтчет В (&МассивДокументов)
	|	И Данныедокумента.Проведен
	//-- НЕ УТ
	//-- Локализация
	|";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.АвансовыйОтчет.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("c381a618-6fbc-41fd-8197-c2844dd2d8ae");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.АвансовыйОтчет.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС.
                                   |Заполняет реквизит ОбъектРасчетов.'
                                   |;uk='Заповнює реквізит СтавкаПДВ. 
                                   |Заповнює реквізит Об''єкт розрахунків'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДвиженияДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";         
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);             

#КонецОбласти

#Область СгенерироватьОбъектыРасчетов

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.АвансовыйОтчет.СгенерироватьОбъектыРасчетов";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("3dd05b2e-45e0-47e6-a8a2-cc4994e15766");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.АвансовыйОтчет.ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Генерирует элементы справочника ОбъектыРасчетов';uk='Генерує елементи довідника Об''ектиРасчетов'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.АвансовыйОтчет.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Документы.АвансовыйОтчет";
	НоваяСтрока.Порядок    = "Удалить"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения
	);

#КонецОбласти

КонецПроцедуры

// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.АвансовыйОтчет";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвансовыйОтчет.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АвансовыйОтчет КАК АвансовыйОтчет
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.АвансовыйОтчет.ПрочиеРасходы КАК АвансовыйОтчетПрочиеРасходы
	|			ГДЕ
	|				АвансовыйОтчет.Ссылка = АвансовыйОтчетПрочиеРасходы.Ссылка
	|				И АвансовыйОтчетПрочиеРасходы.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|				И АвансовыйОтчетПрочиеРасходы.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))
	|	ИЛИ 
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
	|			ГДЕ
	|				АвансовыйОтчет.Ссылка = АвансовыйОтчетОплатаПоставщикам.Ссылка
	|				И (АвансовыйОтчетОплатаПоставщикам.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|				ИЛИ АвансовыйОтчетОплатаПоставщикам.ИдентификаторСтроки = """"))";
	
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("ПрочиеРасходы");
				
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(ДокументОбъект.ОплатаПоставщикам, ОбъектИзменен);
				
				ОбъектыРасчетовСервер.ЗаполнитьОбъектыРасчетов(ДокументОбъект, ОбъектИзменен);
				
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Функция ТекстЗапросаРегистрацияДанныхДляГенерацииПустогоОбъектаРасчетов() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТЧОплатаПоставщикам.Контрагент
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТЧОплатаПоставщикам
	|ГДЕ
	|	ТЧОплатаПоставщикам.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И (
	|		&УсловиеИзменяетРасчеты
	|	)
	|	И ТЧОплатаПоставщикам.Ссылка.Проведен
	|	И ТЧОплатаПоставщикам.Контрагент В (&ОбрабатываемыеДанные)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИзменяетРасчеты", ПараметрыВзаиморасчеты()[0].ИзменяетРасчетыСтрокой);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПолучениеДанныхДляГенерацииПустогоОбъектаРасчетов() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком),
	|	ИсточникДанных.Организация,
	|	ТЧОплатаПоставщикам.Поставщик,
	|	ТЧОплатаПоставщикам.Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ТЧОплатаПоставщикам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК ИсточникДанных
	|		ПО ТЧОплатаПоставщикам.Ссылка = ИсточникДанных.Ссылка
	|ГДЕ
	|	ТЧОплатаПоставщикам.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И (
	|		&УсловиеИзменяетРасчеты
	|	)
	|	И ИсточникДанных.Проведен
	|	И ТЧОплатаПоставщикам.Контрагент В (&ОбрабатываемыеДанные)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИзменяетРасчеты", ПараметрыВзаиморасчеты()[0].ИзменяетРасчетыСтрокой);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов(Параметры) Экспорт
	
	ОбъектыРасчетовСервер.ЗарегистрироватьДанныеДляГенерацииОбъектовРасчета(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

Процедура СгенерироватьОбъектыРасчетов(Параметры) Экспорт
	
	ОбъектыРасчетовСервер.СгенерироватьОбъектыРасчетов(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
