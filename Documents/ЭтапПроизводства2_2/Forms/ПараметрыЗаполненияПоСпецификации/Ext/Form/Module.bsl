
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
		"ИспользоватьВыходныеИзделия, ИспользоватьТрудозатраты, ИспользоватьВидыРабочихЦентров");
	
	Элементы.ЗаполнитьВыходныеИзделия.Видимость = ИспользоватьВыходныеИзделия;
	Элементы.ЗаполнитьТрудозатраты.Видимость = ИспользоватьТрудозатраты;
	Элементы.ЗаполнитьВидыРабочихЦентров.Видимость = ИспользоватьВидыРабочихЦентров;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗаполнитьОбеспечениеМатериаламиИРаботами
		И НЕ ЗаполнитьВыходныеИзделия
		И НЕ ЗаполнитьПобочныеИзделия
		И НЕ ЗаполнитьТрудозатраты
		И НЕ ЗаполнитьВидыРабочихЦентров Тогда
		
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Не выбраны табличные части для заполнения';uk='Не вибрані табличні частини для заповнення'"));
		Возврат;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Если ЗаполнитьОбеспечениеМатериаламиИРаботами Тогда
		Результат.Вставить("ОбеспечениеМатериаламиИРаботами");
	КонецЕсли;
	Если ИспользоватьВыходныеИзделия
			И ЗаполнитьВыходныеИзделия Тогда
		Результат.Вставить("ВыходныеИзделия");
	КонецЕсли;
	Если ЗаполнитьПобочныеИзделия Тогда
		Результат.Вставить("ПобочныеИзделия");
	КонецЕсли;
	Если ИспользоватьТрудозатраты
			И ЗаполнитьТрудозатраты Тогда
		Результат.Вставить("Трудозатраты");
	КонецЕсли;
	Если ИспользоватьВидыРабочихЦентров
			И ЗаполнитьВидыРабочихЦентров Тогда
		Результат.Вставить("ВидыРабочихЦентров");
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ЗаполнитьОбеспечениеМатериаламиИРаботами = Истина;
	ЗаполнитьВыходныеИзделия = Истина;
	ЗаполнитьПобочныеИзделия = Истина;
	ЗаполнитьТрудозатраты = Истина;
	ЗаполнитьВидыРабочихЦентров = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ЗаполнитьОбеспечениеМатериаламиИРаботами = Ложь;
	ЗаполнитьВыходныеИзделия = Ложь;
	ЗаполнитьПобочныеИзделия = Ложь;
	ЗаполнитьТрудозатраты = Ложь;
	ЗаполнитьВидыРабочихЦентров = Ложь;
	
КонецПроцедуры

#КонецОбласти
