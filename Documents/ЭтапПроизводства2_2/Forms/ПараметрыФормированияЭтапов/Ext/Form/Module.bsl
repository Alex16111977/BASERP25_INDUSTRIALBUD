
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючФормы(),
		Неопределено);
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
	КонецЕсли;
	
	Элементы.ЗаменятьМатериалыНаАналоги.Доступность = РезервМатериаловСоСклада;
	
	Если УправлениеПроизводством.ИспользуетсяГрафикПроизводства() Тогда
		Элементы.ПриРасчетеГрафикаЗадействоватьРезерв.Доступность = РассчитатьГрафик;
	Иначе
		Элементы.РассчитатьГрафик.Видимость = Ложь;
		Элементы.ГруппаПараметрыРасчетаГрафика.Видимость = Ложь;
		РассчитатьГрафик = Ложь;
		ПриРасчетеГрафикаЗадействоватьРезерв = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезервМатериаловСоСкладаПриИзменении(Элемент)
	
	Если Не РезервМатериаловСоСклада Тогда
		ЗаменятьМатериалыНаАналоги = Ложь;
	КонецЕсли;
	
	Элементы.ЗаменятьМатериалыНаАналоги.Доступность = РезервМатериаловСоСклада;
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура РезервМатериаловИзЗаказовПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьГрафикПриИзменении(Элемент)
	
	Если НЕ РассчитатьГрафик Тогда
		ПриРасчетеГрафикаЗадействоватьРезерв = Ложь;
	КонецЕсли;
	
	Элементы.ПриРасчетеГрафикаЗадействоватьРезерв.Доступность = РассчитатьГрафик;
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриРасчетеГрафикаЗадействоватьРезервПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьЭтапыКВыполнениюПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменятьМатериалыНаАналогиПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Закрыть(ПараметрыФормированияЭтапов());
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПотребности(Команда)
	
	Отбор = Новый Структура;
	Если Параметры.Распоряжения.Количество() = 1 Тогда
		Отбор.Вставить("Распоряжение", Параметры.Распоряжения[0]);
	Иначе
		Отбор.Вставить("Распоряжение", Параметры.Распоряжения);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ПотребностиКФормированиюЭтаповПроизводства.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьНастройкиФормыКлиент()
	
	Настройки = Новый Структура;
	Настройки.Вставить("РезервМатериаловСоСклада", РезервМатериаловСоСклада);
	Настройки.Вставить("РезервМатериаловИзЗаказов", РезервМатериаловИзЗаказов);
	Настройки.Вставить("ЗаменятьМатериалыНаАналоги", ЗаменятьМатериалыНаАналоги);
	Настройки.Вставить("РассчитатьГрафик", РассчитатьГрафик);
	Настройки.Вставить("ПриРасчетеГрафикаЗадействоватьРезерв", ПриРасчетеГрафикаЗадействоватьРезерв);
	Настройки.Вставить("ПередатьЭтапыКВыполнению", ПередатьЭтапыКВыполнению);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючФормы(), Неопределено, Настройки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Документ.ЭтапПроизводства2_2.Форма.ПараметрыФормированияЭтапов";
	
КонецФункции

&НаСервере
Функция ПараметрыФормированияЭтапов()
	
	Результат = Документы.ЭтапПроизводства2_2.ПараметрыФормированияЭтапов();
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	Результат.Распоряжения = Параметры.Распоряжения;
	Возврат Результат;
	
КонецФункции

#КонецОбласти
