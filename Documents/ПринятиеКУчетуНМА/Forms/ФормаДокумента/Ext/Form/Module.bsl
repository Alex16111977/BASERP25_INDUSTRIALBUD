
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуНМА.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриСозданииЧтенииНаСервере();
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуНМА.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ПринятиеКУчетуНМА", ПараметрыЗаписи, Объект.Ссылка);
	
	ОповеститьОбИзменении(Объект.НематериальныйАктив);

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

	ОбщегоНазначенияУТКлиент.ВыполнитьДействияПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
	ДоходыИРасходыСервер.ПослеЗаписиНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСоставКомиссии(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);

	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	ОткрытьФорму("РегистрСведений.СоставКомиссий.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "РегистрСведений.СоставКомиссий.Форма.ФормаВыбора" Тогда
		ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы



&НаКлиенте
Процедура НачислятьАмортизациюБУПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементовФормы("НачислятьАмортизациюБУ");
	
КонецПроцедуры

&НаКлиенте
Процедура НачислятьАмортизациюНУПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементовФормы("НачислятьАмортизациюНУ");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособНачисленияАмортизацииБУПриИзменении(Элемент)
	
	Объект.СпособНачисленияАмортизацииНУ = Объект.СпособНачисленияАмортизацииБУ;
	ОбновитьДоступностьЭлементовФормы("СпособНачисленияАмортизацииБУ");
	
КонецПроцедуры


&НаКлиенте
Процедура СрокИспользованияБУПриИзменении(Элемент)
	
	Если Объект.СрокИспользованияНУ = 0
		Или СрокИспользованияБУ = Объект.СрокИспользованияНУ Тогда
		
		Объект.СрокИспользованияНУ = Объект.СрокИспользованияБУ;
		СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
			Объект.СрокИспользованияНУ);
		
	КонецЕсли;
	
	СрокИспользованияБУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияБУ);
	
	СрокИспользованияБУ = Объект.СрокИспользованияБУ;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияНУПриИзменении(Элемент)
	
	СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияНУ);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры


&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементовФормы("Организация");
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд


#Область СтандартныеПодсистемы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
	СрокИспользованияБУ = Объект.СрокИспользованияБУ;
	
	ОбновитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьЭлементовФормы(ИзмененныеРеквизиты=Неопределено)
	
	ОбновитьВсе = (ИзмененныеРеквизиты=Неопределено);
	Реквизиты = Новый Структура(ИзмененныеРеквизиты);
	
	Если ОбновитьВсе Тогда
		ЗаполнитьСпискиСпособовАмортизации();

		Элементы.СчетНачисленияАмортизации.Видимость = Истина;
		
		Элементы.СтраницаАмортизация.Заголовок = НСтр("ru='Амортизация';uk='Амортизація'");
		Элементы.СчетУчета.Заголовок = НСтр("ru='Нематериального актива';uk='Нематеріального активу'");
		Элементы.НематериальныйАктив.Заголовок = НСтр("ru='Нематериальный актив';uk='Нематеріальний актив'");
		Элементы.НачислятьАмортизациюБУ.Заголовок = НСтр("ru='Начисление амортизации';uk='Нарахування амортизації'");
		Элементы.СрокИспользованияБУ.Заголовок= НСтр("ru='Срок использования (месяцев)';uk='Строк використання (місяців)'");
		Элементы.СпособНачисленияАмортизацииБУ.Заголовок= НСтр("ru='Способ начисления амортизации';uk='Спосіб нарахування амортизації'");
		Элементы.СрокИспользованияНУ.Заголовок= НСтр("ru='Срок использования (месяцев)';uk='Строк використання (місяців)'");
			
		Реквизиты.Вставить("НачислятьАмортизациюБУ");
		Реквизиты.Вставить("НачислятьАмортизациюНУ");
		
	КонецЕсли; 	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НачислятьАмортизациюБУ") Тогда
		Элементы.СпособНачисленияАмортизацииБУ.Видимость = Объект.НачислятьАмортизациюБУ;
		Элементы.ГруппаСрокИспользованияБУ.Видимость = Объект.НачислятьАмортизациюБУ;
		
		Реквизиты.Вставить("СпособНачисленияАмортизацииБУ")
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НалоговоеНазначение") Тогда
		Элементы.ГруппаНУ.Видимость = НЕ (Объект.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность);
	КонецЕсли;		
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НачислятьАмортизациюБУ") Или Реквизиты.Свойство("НачислятьАмортизациюНУ") Тогда
		Элементы.ОтражениеРасходов.Видимость = Объект.НачислятьАмортизациюБУ Или Объект.НачислятьАмортизациюНУ;
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СпособНачисленияАмортизацииБУ") Тогда
		
		Элементы.СрокИспользованияБУ.АвтоОтметкаНезаполненного = (
			Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный
			Или Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка);
		
		Если Не Элементы.СрокИспользованияБУ.АвтоОтметкаНезаполненного Тогда
			Элементы.СрокИспользованияБУ.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		Элементы.ОбъемНаработкиБУ.Видимость = (
			Объект.НачислятьАмортизациюБУ
			И Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		Элементы.СрокИспользованияБУ.Видимость = НЕ 
			(Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		Элементы.РасшифровкаСрокаПолезногоИспользованияБУ.Видимость = НЕ 
			(Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		
		Реквизиты.Вставить("СпособНачисленияАмортизацииНУ")
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СпособНачисленияАмортизацииНУ") Тогда
		Элементы.СрокИспользованияНУ.Видимость = НЕ 
			(Объект.СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		Элементы.СрокИспользованияНУРасшифровка.Видимость = НЕ 
			(Объект.СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
	КонецЕсли;
	
	Если ОбновитьВсе
		Или Реквизиты.Свойство("НачислятьАмортизациюНУ") Тогда
		
		Элементы.ГруппаСрокИспользованияНУ.Видимость =  Объект.НачислятьАмортизациюНУ;
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СрокИспользованияБУ") Тогда
		СрокИспользованияБУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
			Объект.СрокИспользованияБУ);
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СрокИспользованияНУ") Тогда
		СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
			Объект.СрокИспользованияНУ);
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НалоговоеНазначение") Тогда
	
		Элементы.ГруппаНУ.Видимость = НЕ (Объект.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность);
		
	КонецЕсли;
	
		
	Если ОбновитьВсе Или Реквизиты.Свойство("Организация") Или Реквизиты.Свойство("Дата") Тогда
				
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация, Период", Объект.Организация, Объект.Дата));		
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СчетаУчетаНематериальныйАктивовПоУмолчанию()
	
	СчетаПоУмолчанию = Новый Структура("СчетУчета, СчетНачисленияАмортизации");
	СчетаПоУмолчанию.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ДругиеНематериальныеАктивы");
	СчетаПоУмолчанию.СчетНачисленияАмортизации = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.НакопленнаяАмортизацияНематериальныхАктивов");
		
	Возврат СчетаПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСпискиСпособовАмортизации()
	ЗаполнитьСписокСпособовАмортизацииБУ();
	ЗаполнитьСписокСпособовАмортизацииНУ();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСпособовАмортизацииБУ()
	Элементы.СпособНачисленияАмортизацииБУ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииБУ());
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСпособовАмортизацииНУ()
	Элементы.СпособНачисленияАмортизацииНУ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииНУ());
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСпособовАмортизацииБУ() Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.Линейный);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.УскоренногоУменьшенияОстатка);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.Кумулятивный);
	
	Возврат СписокПеречисления;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокСпособовАмортизацииНУ() Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.Линейный);
	Если Объект.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
	КонецЕсли;
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.УскоренногоУменьшенияОстатка);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииНМА.Кумулятивный);
	
	Возврат СписокПеречисления;
	
КонецФункции

&НаКлиенте
Процедура СпособНачисленияАмортизацииНУНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗаполнитьСписокСпособовАмортизацииНУ();
КонецПроцедуры

&НаКлиенте
Процедура СпособНачисленияАмортизацииНУПриИзменении(Элемент)
		ОбновитьДоступностьЭлементовФормы("СпособНачисленияАмортизацииНУ");
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	СчетУчетаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СчетУчетаПриИзмененииНаСервере()
	
	СчетУчета = Объект.СчетУчета;
	
	Если СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеПриродныхРесурсов Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа1;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеИмущества Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа2;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаЗнакиДляТоваровИУслуг Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа3;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаОбъектыПромышленнойСобственности Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа4;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.АвторскиеИСмежныеСНимиПрава Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа5;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДругиеНематериальныеАктивы Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа6;
	КонецЕсли; 


КонецПроцедуры

&НаКлиенте
Процедура НалоговаяГруппаОСПриИзменении(Элемент)
	НалоговаяГруппаОСПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НалоговаяГруппаОСПриИзмененииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.СчетУчета) Тогда
		
		НалоговаяГруппаОС = Объект.НалоговаяГруппаОС;
		
		Если НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа1 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеПриродныхРесурсов;
		ИначеЕсли НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа2 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеИмущества;
		ИначеЕсли НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа3 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаЗнакиДляТоваровИУслуг;
		ИначеЕсли НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа4 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаОбъектыПромышленнойСобственности;
		ИначеЕсли НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа5 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.АвторскиеИСмежныеСНимиПрава;
		ИначеЕсли НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа6 Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ДругиеНематериальныеАктивы;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговоеНазначениеПриИзменении(Элемент)
	ОбновитьДоступностьЭлементовФормы("НалоговоеНазначение");
КонецПроцедуры



#КонецОбласти
