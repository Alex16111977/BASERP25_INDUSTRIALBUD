
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаМестоУчетаНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Процедура обновляет движения документа принятия к учету после отражения в регл. учете
// Вызывается при отражения в регл. учете из функции "РеглУчетПроведениеСервер.ДополнительнаяОбработкаПриОтраженииДокумента".
//
// Параметры:
// 		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджера временных таблиц проводок в регл. учете.
//
Процедура ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПринятиеКУчетуНМА.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ПринятиеКУчетуНМА.Ссылка = ДокументыКОтражению.Ссылка";
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	
	Если Не РеквизитыДокумента.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, РеквизитыДокумента.Ссылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Неопределено);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, Неопределено);
	
	НачатьТранзакцию();
	
	Попытка
		
		#Область БлокировкаДанных
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрочиеРасходы.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияНМА.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		Блокировка.Заблокировать();
		
		#КонецОбласти
		
				
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМА.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияНМА);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Заполнение регистров НМА после отражения документа в регламентированном учете';uk='Заповнення регістрів НМА після відображення документа в регламентованому обліку'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//++ НЕ УТКА
	Документы.ПринятиеКУчетуНМАМеждународныйУчет.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//-- НЕ УТКА
	
КонецПроцедуры

// Добавляет команду создания документа "Принятие к учету НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПринятиеКУчетуНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПринятиеКУчетуНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПринятиеКУчетуНМА);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПринятиеКУчетуНМА";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.НематериальныйАктив,
	|	ДанныеДокумента.СчетУчета,
	|	ДанныеДокумента.СчетНачисленияАмортизации,
	|	ДанныеДокумента.СчетУчетаДооценокНМА,
	|	ДанныеДокумента.НачислятьАмортизациюБУ,
	|	ДанныеДокумента.СрокИспользованияБУ,
	|	ДанныеДокумента.СпособНачисленияАмортизацииБУ,
	|	ДанныеДокумента.ОбъемНаработкиБУ,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьБУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ,
	|	ДанныеДокумента.СрокИспользованияНУ,
	|	ДанныеДокумента.СпособНачисленияАмортизацииНУ,
	|	ДанныеДокумента.НалоговоеНазначение,
	|	ДанныеДокумента.НалоговаяГруппаОС,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.МОЛ КАК МОЛ,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ГраницаКонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Дата), ВидГраницы.Включая));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПринятиеКУчетуНМА"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПринятиеКУчетуНМА);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ДанныеДокументаИУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втДанныеДокументаИУчета
	|"+
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаОстатокДт, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ЕСТЬNULL(ДанныеУчета.СуммаОстатокДт, 0))
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ГраницаКонецМесяца,
	|				Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеИзготовлениеНематериальныхАктивов)),
	|				,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И НаправлениеДеятельности = &НаправлениеДеятельности
	|					И Субконто1 = &НематериальныйАктив) КАК ДанныеУчета
	|		ПО ПринятиеКУчетуНМА.НематериальныйАктив = ДанныеУчета.Субконто1
	|ГДЕ
	|	ПринятиеКУчетуНМА.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуНМА.НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
	Возврат Текст
	
КонецФункции

Процедура ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втДанныеДокументаИУчета
	|"+
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.Сумма, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ЕСТЬNULL(ДанныеУчета.Сумма, 0))
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				,
	|				,
	|				Регистратор = &Ссылка
	|					И СчетДт = &СчетУчета,
	|				,
	|				) КАК ДанныеУчета
	|		ПО ПринятиеКУчетуНМА.НематериальныйАктив = ДанныеУчета.СубконтоДт1
	|ГДЕ
	|	ПринятиеКУчетуНМА.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуНМА.НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Функция ТаблицаНМА(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеДокументаИУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаНМА
	|"+
	"ВЫБРАТЬ
	|	втДанныеДокументаИУчета.НематериальныйАктив КАК НематериальныйАктив,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	втДанныеДокументаИУчета КАК втДанныеДокументаИУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
	Возврат Текст;
	
КонецФункции


Процедура ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	&НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ТаблицаНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	&СпособНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	&СпособНачисленияАмортизацииНУ  КАК МетодНачисленияАмортизацииНУ,
	|	&Дата КАК ДатаПриобретения,
	|	&Дата КАК ДатаПринятияКУчетуБУ,
	|	&Ссылка КАК ДокументПринятияКУчетуБУ
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&НематериальныйАктив         КАК НематериальныйАктив,
	|	&Организация                 КАК Организация,
	|	&СрокИспользованияБУ         КАК СрокПолезногоИспользованияБУ,
	|	&СрокИспользованияНУ         КАК СрокПолезногоИспользованияНУ,
	|	&ЛиквидационнаяСтоимостьБУ   КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА   КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&НематериальныйАктив                    КАК НематериальныйАктив,
	|	&Организация                            КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.ПринятКУчету) КАК Состояние,
	|	&НачислятьАмортизациюБУ                 КАК НачислятьАмортизациюБУ,
	|	&НачислятьАмортизациюНУ                 КАК НачислятьАмортизациюНУ,
	|	&НалоговаяГруппаОС                      КАК НалоговаяГруппаОС,
	|	&НалоговоеНазначение                    КАК НалоговоеНазначение,
	|	&СтатьяРасходов                         КАК СтатьяРасходовБУ,
	|	&АналитикаРасходов                      КАК АналитикаРасходовБУ";
		
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	&НематериальныйАктив КАК НематериальныйАктив,
	|	&ОбъемНаработкиБУ КАК ОбъемНаработки,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СчетУчета КАК СчетУчета,
	|	&СчетУчетаДооценокНМА КАК СчетДооценки,
	|	&СчетНачисленияАмортизации КАК СчетНачисленияАмортизации";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаМестоУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестоУчетаНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                       КАК Период,
	|	&НематериальныйАктив          КАК НематериальныйАктив,
	|	&Организация                  КАК Организация,
	|	&МОЛ                 		  КАК МОЛ,
	|	&Подразделение                КАК Подразделение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	
	|	&Организация КАК Организация,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры


Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&Проведен                               КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	&НематериальныйАктив                    КАК НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат  ТекстЗапросаВТОтраженияВРеглУчетеУКР();
		
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ВременнаяТаблицаДанныеДокумента());
	ТекстыОтражения.Добавить(ВременнаяТаблицаОстаткиСчетаКапитализации());
	ТекстыОтражения.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
		
КонецФункции

Функция ВременнаяТаблицаДанныеДокумента()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.СчетНачисленияАмортизации,
	|	ТаблицаДокумента.СчетУчетаДооценокНМА,
	|	ТаблицаДокумента.СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов,
	 |	ТаблицаДокумента.НалоговоеНазначение
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
КонецФункции

Функция ВременнаяТаблицаОстаткиСчетаКапитализации()
	
	Возврат
	"ВЫБРАТЬ
	|	ДанныеУчета.Счет КАК Счет,
	|	ДанныеУчета.Субконто1 КАК НематериальныйАктив,
	|	СУММА(ДанныеУчета.СуммаОборот) КАК СуммаБУ
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&ГраницаМесяцОкончание,
	|			Регистратор,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеИзготовлениеНематериальныхАктивов)),
	|			,
	|			(Организация, Подразделение, НаправлениеДеятельности, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.НаправлениеДеятельности,
	|					Т.НематериальныйАктив
	|				ИЗ
	|					втДанныеДокумента КАК Т),
	|			,
	|			) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Субконто1,
	|	ДанныеУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.НематериальныйАктив,
	|	СУММА(втОстатки.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втОстаткиСгруппированные
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.НематериальныйАктив,
	|	втОстатки.СуммаБУ  КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализации
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.НематериальныйАктив = втДанныеДокумента.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.НематериальныйАктив,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализацииСгруппированные
	|ИЗ
	|	втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.НематериальныйАктив";
	
КонецФункции


Функция ТекстОтраженияВРеглУчете() Экспорт
	Возврат ТекстОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ПринятиеКУчету());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции


Функция ПринятиеКУчету()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету  (Дт СчетУчета:: Кт СчетаКапитальныхЗатрат)
	|
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДокумента.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетДт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА втДанныеДокумента.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ втДанныеСчетаКапитализации.СуммаБУ
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|   ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	 |	0 КАК СуммаНУКт,
	 |	0 КАК СуммаПРКт,
	 |	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|ГДЕ
	|	втДанныеСчетаКапитализации.СуммаБУ <> 0
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Принятие к учету НМА';uk='Прийняття до обліку НМА'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// НА-1
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НА1";
	КомандаПечати.Представление = НСтр("ru='Форма НА-1';uk='Форма НА-1'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 10;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НА1") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НА1",
			НСтр("ru='Форма НА-1';uk='Форма НА-1'"), ПечатьНА1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета),, ИмяМакета);
	КонецЕсли;

КонецПроцедуры

// Функция формирует табличный документ с типовой печатной формой НА-1
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНА1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	
	Перем СписокСчетовКт;
	Перем ТЗСчетаКт;
	Перем ТЗПервоначальнаяСтоимость;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПринятиеКУчетуНМА_НА1";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПринятиеКУчетуНМА.ПФ_MXL_UK_НА1");
	
	ПервыйДокумент = Истина;
	
	ТЗСчетаКт = Новый ТаблицаЗначений;
	
	ТЗСчетаКт.Колонки.Добавить("ДокументПринятиеКУчетуНМА",Новый ОписаниеТипов("ДокументСсылка.ПринятиеКУчетуНМА"));
	ТЗСчетаКт.Колонки.Добавить("СписокСчетовКт",Новый ОписаниеТипов("Строка"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ.ПринятиеКУчетуНМА) КАК Регистратор,
	|	ХозрасчетныйДвиженияССубконто.СчетКт КАК СчетКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Активность
	|				И ВЫРАЗИТЬ(Регистратор КАК Документ.ПринятиеКУчетуНМА) В (&МассивОбъектов),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|ИТОГИ ПО
	|	Регистратор";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаРегистратор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРегистратор.Следующий() Цикл
		// Вставить обработку выборки ВыборкаРегистратор
		СписокСчетовКт = "";
		
		ВыборкаДетальныеЗаписи = ВыборкаРегистратор.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СписокСчетовКт = СписокСчетовКт+?(СписокСчетовКт = "",ВыборкаДетальныеЗаписи.СчетКт,", "+ВыборкаДетальныеЗаписи.СчетКт);
		КонецЦикла;
		
		НоваяСтрока = ТЗСчетаКт.Добавить();
		НоваяСтрока.ДокументПринятиеКУчетуНМА = ВыборкаРегистратор.Регистратор;
		НоваяСтрока.СписокСчетовКт 			  = СписокСчетовКт;
		
	КонецЦикла;
	
	ТЗПервоначальнаяСтоимость = Новый ТаблицаЗначений;
	
	ТЗПервоначальнаяСтоимость.Колонки.Добавить("ДокументПринятиеКУчетуНМА",Новый ОписаниеТипов("ДокументСсылка.ПринятиеКУчетуНМА"));
	ТЗПервоначальнаяСтоимость.Колонки.Добавить("НематериальныйАктив",Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
	ТЗПервоначальнаяСтоимость.Колонки.Добавить("ПервоначальнаяСтоимость",Новый ОписаниеТипов("Число"));
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("НематериальныйАктив",Ссылка.НематериальныйАктив);
		
		ПервоначальныеСведенияНМА = РегистрыСведений.ПервоначальныеСведенияНМА.ПолучитьПоследнее(Новый Граница(Ссылка.МоментВремени(),ВидГраницы.Включая),Отбор);		

		НоваяСтрокаПервоначальнаяСтоимость = ТЗПервоначальнаяСтоимость.Добавить();
		НоваяСтрокаПервоначальнаяСтоимость.ДокументПринятиеКУчетуНМА = Ссылка;
		НоваяСтрокаПервоначальнаяСтоимость.НематериальныйАктив 	   	 = Ссылка.НематериальныйАктив;
		НоваяСтрокаПервоначальнаяСтоимость.ПервоначальнаяСтоимость	 = ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ;
		
	КонецЦикла; 
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов" 			 , МассивОбъектов);
	Запрос.УстановитьПараметр("ТЗСчетаКт" 				 , ТЗСчетаКт);
	Запрос.УстановитьПараметр("ТЗПервоначальнаяСтоимость", ТЗПервоначальнаяСтоимость);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗСчетаКт.ДокументПринятиеКУчетуНМА КАК ДокументПринятиеКУчетуНМА,
	|	ТЗСчетаКт.СписокСчетовКт КАК СчетУчетаБУВнеоборотногоАктива
	|ПОМЕСТИТЬ ТЗСчетаКт
	|ИЗ
	|	&ТЗСчетаКт КАК ТЗСчетаКт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПринятиеКУчетуНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗПервоначальнаяСтоимость.ДокументПринятиеКУчетуНМА КАК ДокументПринятиеКУчетуНМА,
	|	ТЗПервоначальнаяСтоимость.НематериальныйАктив КАК НематериальныйАктив,
	|	ТЗПервоначальнаяСтоимость.ПервоначальнаяСтоимость
	|ПОМЕСТИТЬ ТЗПервоначальнаяСтоимость
	|ИЗ
	|	&ТЗПервоначальнаяСтоимость КАК ТЗПервоначальнаяСтоимость
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПринятиеКУчетуНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.Организация.НаименованиеПолное КАК НаименованиеПолноеОрганизации,
	|	ДанныеДокументов.Организация.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	|	ДанныеДокументов.Дата КАК ДатаДок,
	|	ДанныеДокументов.Номер КАК НомерДок,
	|	ДанныеДокументов.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокументов.НематериальныйАктив.НаименованиеПолное КАК НаименованиеПолное,
	|	ДанныеДокументов.НематериальныйАктив.ПрочиеСведения КАК ПрочиеСведения,
	|	ДанныеДокументов.СчетУчета КАК СчетУчетаБУ,
	|	ДанныеДокументов.СрокИспользованияБУ КАК СрокИспользования,
	|	ДанныеДокументов.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокументов.МОЛ
	|ПОМЕСТИТЬ ДанныеДокументов
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.НаименованиеПолноеОрганизации,
	|	ДанныеДокументов.КодПоЕДРПОУ,
	|	ДанныеДокументов.ДатаДок,
	|	ДанныеДокументов.НомерДок,
	|	ДанныеДокументов.НематериальныйАктив,
	|	ДанныеДокументов.НаименованиеПолное,
	|	ДанныеДокументов.ПрочиеСведения,
	|	ДанныеДокументов.СчетУчетаБУ,
	|	ДанныеДокументов.СрокИспользования,
	|	ДанныеДокументов.ЛиквидационнаяСтоимость,
	|	ТЗСчетаКт.СчетУчетаБУВнеоборотногоАктива,
	|	ТЗПервоначальнаяСтоимость.ПервоначальнаяСтоимость,
	|	ДанныеДокументов.МОЛ
	|ИЗ
	|	ДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТЗСчетаКт КАК ТЗСчетаКт
	|		ПО ДанныеДокументов.Ссылка = ТЗСчетаКт.ДокументПринятиеКУчетуНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТЗПервоначальнаяСтоимость КАК ТЗПервоначальнаяСтоимость
	|		ПО ДанныеДокументов.Ссылка = ТЗПервоначальнаяСтоимость.ДокументПринятиеКУчетуНМА";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Обработка выборки ВыборкаСсылка
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("НА1");
		Параметры     = ОбластьМакета.Параметры;
		
		Если Выборка.СрокИспользования <> 0 Тогда
			
			Если Выборка.ПервоначальнаяСтоимость <> 0  Тогда
				Параметры.ГодичнаяСуммаАмортизации = 12 * (Выборка.ПервоначальнаяСтоимость 
				- Выборка.ЛиквидационнаяСтоимость)
				/ Выборка.СрокИспользования;
			Иначе 
				
				Параметры.ГодичнаяСуммаАмортизации = 0;
				
			КонецЕсли; 
			
		КонецЕсли;
		
		Параметры.Заполнить(Выборка);
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Выборка.Ссылка);
        Параметры.Заполнить(ВыборкаПоКомиссии);
		ОсновныеСотрудникиФизическихЛицМОЛ = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(Выборка.МОЛ, Истина, Ссылка.Организация, Ссылка.Дата);
		Если ЗначениеЗаполнено(ОсновныеСотрудникиФизическихЛицМОЛ) Тогда
			Для каждого Строка Из ОсновныеСотрудникиФизическихЛицМОЛ Цикл
			    ДанныеФизЛицаПолучил = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Строка.Сотрудник, "Должность", Ссылка.Дата);			
			КонецЦикла;
			Для каждого СтрокаДанныеПолучил Из ДанныеФизЛицаПолучил Цикл			
				ОбластьМакета.Параметры.МОЛДолжность = СтрокаДанныеПолучил.Должность;
				ОбластьМакета.Параметры.МОЛФИО 			= СтрокаДанныеПолучил.ФизическоеЛицо;			
			КонецЦикла;
		КонецЕсли;
		
		Параметры.КодПоЕДРПОУ = Выборка.КодПоЕДРПОУ;
		
		ОтветственныеЛицаОрганизации = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Ссылка.Организация,Ссылка.Дата);
		Параметры.ФИОРук      = ОтветственныеЛицаОрганизации[Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления.Руководитель.Имя + "Наименование"];
		Параметры.ФИОБух      = ОтветственныеЛицаОрганизации[Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления.ГлавныйБухгалтер.Имя + "Наименование"];
		
		
		ТабДокумент.Вывести(ОбластьМакета);
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Ссылка);
	КонецЦикла;
	Возврат ТабДокумент;

КонецФункции // ПечатьНА1()

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
		
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
