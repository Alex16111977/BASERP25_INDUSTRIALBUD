#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	Документы.ИзменениеПараметровНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
КонецПроцедуры

// Добавляет команду создания документа "Переоценка НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПереоценкаНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПереоценкаНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПереоценкаНМА);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПереоценкаНМА";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		
		ПереоценкаНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
		
		УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры, ПрочиеРасходы(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры, ПартииПрочихРасходов(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ПереоценкаНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДопПараметры);
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаМесяцОкончание", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПереоценкаНМА"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПереоценкаНМА);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ВременнаяТаблицаПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаПоДокументу";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втТаблицаПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка КАК Ссылка,
	|	втТаблицаПоДокументу.НематериальныйАктив КАК ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение,
	|	втТаблицаПоДокументу.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ втТаблицаПоДокументу
	|ИЗ
	|	Документ.ПереоценкаНМА КАК втТаблицаПоДокументу
	|ГДЕ
	|	втТаблицаПоДокументу.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	втТаблицаПоДокументу.НематериальныйАктив,
	|	Организация,
	|	Подразделение";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втАмортизацияПоДокументу";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втАмортизацияПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(втНачисленнаяАмортизация.СуммаБУ) КАК Сумма
	|ПОМЕСТИТЬ втАмортизацияПоДокументу
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчета
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	СчетаБухгалтерскогоУчета.СчетУчета,
	|	СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|				&Период,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК СчетаБухгалтерскогоУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = СчетаБухгалтерскогоУчета.НематериальныйАктив
	|";
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчетаСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втСчетаУчетаСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|";
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаАмортизацииСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаАмортизацииСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетАмортизации КАК Счет
	|ПОМЕСТИТЬ втСчетаАмортизацииСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|";
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчетаСгруппированные.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаСгруппированные
	|ИЗ
	|	втСчетаУчетаСгруппированные КАК втСчетаУчетаСгруппированные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаАмортизацииСгруппированные.Счет
	|ИЗ
	|	втСчетаАмортизацииСгруппированные КАК втСчетаАмортизацииСгруппированные";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиПоСчетам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиПоСчетам
	|"+
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОбъектУчета,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
	|ПОМЕСТИТЬ втОстаткиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					втСчетаСгруппированные КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			(Организация, Подразделение, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.ОбъектУчета
	|				ИЗ
	|					втТаблицаПоДокументу КАК Т)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Счет";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиСтоимости
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ВосстановительнаяСтоимость.Сумма, 0)) КАК ВосстановительнаяСтоимость,
	|	СУММА(ЕСТЬNULL(-НакопленнаяАмортизация.Сумма, 0)) КАК НакопленнаяАмортизация
	|ПОМЕСТИТЬ втОстаткиСтоимости
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втСчетаУчета.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК ВосстановительнаяСтоимость
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ВосстановительнаяСтоимость.ОбъектУчета
	|			И (втСчетаУчета.СчетУчета = ВосстановительнаяСтоимость.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК НакопленнаяАмортизация
	|		ПО втТаблицаПоДокументу.ОбъектУчета = НакопленнаяАмортизация.ОбъектУчета
	|			И (втСчетаУчета.СчетАмортизации = НакопленнаяАмортизация.Счет)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаПоДокументу.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПредыдущаяПереоценкаОбъектовСгруппированная(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущаяПереоценкаОбъектовСгруппированная";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектовСгруппированная
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиПриход, 0)) КАК СуммаДооценки,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиРасход, 0)) КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаОбъектовСгруппированная
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаНМАБухгалтерскийУчет.Обороты(
	|				,
	|				&ГраницаМесяцОкончание,
	|				Регистратор,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК ПереоценкаНМАБухгалтерскийУчетОбороты
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ПереоценкаНМАБухгалтерскийУчетОбороты.НематериальныйАктив
	|			И втТаблицаПоДокументу.Организация = ПереоценкаНМАБухгалтерскийУчетОбороты.Организация
	|			И (ПереоценкаНМАБухгалтерскийУчетОбороты.Регистратор <> &Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаПоДокументу.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПредыдущаяПереоценкаОбъектов(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущаяПереоценкаОбъектов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПредыдущаяПереоценкаОбъектовСгруппированная(ТекстыЗапроса);
	
	Текст =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектов
	|"+
	"ВЫБРАТЬ
	|	втПредыдущаяПереоценкаОбъектовСгруппированная.ОбъектУчета,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки > втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки
	|			ТОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки - втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки > втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки
	|			ТОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки - втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаОбъектов
	|ИЗ
	|	втПредыдущаяПереоценкаОбъектовСгруппированная КАК втПредыдущаяПереоценкаОбъектовСгруппированная";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДляПроведения";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса);
	ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаПредыдущаяПереоценкаОбъектов(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса, "Документ.ПереоценкаНМА"); 
	
	Текст =
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка,
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение,
	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) КАК ВосстановительнаяСтоимость,
	|	ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0) КАК НакопленнаяАмортизация,
	|	втТаблицаПоДокументу.Стоимость КАК НоваяСтоимость,
	|	ВЫБОР
	|		КОГДА втОстаткиСтоимости.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|					< (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|			ТОГДА 0
	|		ИНАЧЕ втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|				- (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|	КОНЕЦ КАК ОстаточнаяСтоимостьСУчетомАмортизации,
	|	ВЫБОР
	|		КОГДА втТаблицаПоДокументу.Стоимость > 
	|				(втОстаткиСтоимости.ВосстановительнаяСтоимость - ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) - ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) 
	|			ТОГДА  втТаблицаПоДокументу.Стоимость 
	|					- (втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|						- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|							- ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втТаблицаПоДокументу.Стоимость < 
	|								(втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|									- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|												- ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) 
	|			ТОГДА  втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|						- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|							-ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0) 
	|								- втТаблицаПоДокументу.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) = 0
	|				ИЛИ втТаблицаПоДокументу.Стоимость < (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))
	|					ИЛИ ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) = 0
	|		ТОГДА 0
	|		ИНАЧЕ - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) + ВЫРАЗИТЬ(((ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) * втТаблицаПоДокументу.Стоимость / (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))) КАК ЧИСЛО(15, 2)) 
	|	КОНЕЦ КАК СуммаДооценкиАмортизации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) = 0
	|				ИЛИ втТаблицаПоДокументу.Стоимость > (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))
	|					ИЛИ ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) = 0
	|		ТОГДА 0
	|		ИНАЧЕ (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) - ВЫРАЗИТЬ(((ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) * втТаблицаПоДокументу.Стоимость / (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))) КАК ЧИСЛО(15, 2)) 
	|	КОНЕЦ КАК СуммаУценкиАмортизации,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаОбъектов.СуммаДооценки, 0) КАК СуммаПрошлойДооценки,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаОбъектов.СуммаУценки, 0) КАК СуммаПрошлойУценки
	|ПОМЕСТИТЬ втДанныеДляПроведения
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиСтоимости КАК втОстаткиСтоимости
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втОстаткиСтоимости.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияПоДокументу КАК втАмортизацияПоДокументу
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втАмортизацияПоДокументу.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценкаОбъектов КАК втПредыдущаяПереоценкаОбъектов
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втПредыдущаяПереоценкаОбъектов.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втПорядокУчетаНМА.НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияНМА(ТекстыЗапроса, "Документ.ПереоценкаНМА");
	
	Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	//|	втПервоначальныеСведенияНМА.СпособПоступления КАК СпособПоступления,
	|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|
	|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ 
	|		+ втДанныеДляПроведения.СуммаДооценки
	|			- втДанныеДляПроведения.СуммаУценки 
	|				+ втДанныеДляПроведения.СуммаДооценкиАмортизации 
	|					- втДанныеДляПроведения.СуммаУценкиАмортизации КАК ПервоначальнаяСтоимостьБУ,
	|
	|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	втПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	втПервоначальныеСведенияНМА.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
	|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
	|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
	|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
	|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
	|	ПО втПервоначальныеСведенияНМА.НематериальныйАктив = втДанныеДляПроведения.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ПереоценкаНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	
	ИмяРегистра = "ПереоценкаНМАБухгалтерскийУчет";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПереоценкаНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	втДанныеДляПроведения.СуммаДооценки КАК СуммаПереоценки
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Ссылка,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	втДанныеДляПроведения.ОбъектУчета,
	|	втДанныеДляПроведения.СуммаУценки
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки  > 0";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПрочиеРасходы", Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ // Уценка восстановительной стоимости
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК СуммаСНДСРегл,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаУценки > втДанныеДляПроведения.СуммаПрошлойДооценки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из доли уценки
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ втДанныеДляПроведения.НакопленнаяАмортизация * (втДанныеДляПроведения.СуммаДооценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ втДанныеДляПроведения.НакопленнаяАмортизация * (втДанныеДляПроведения.СуммаДооценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки < втДанныеДляПроведения.СуммаПрошлойУценки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из уценки
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * (втДанныеДляПроведения.СуммаПрошлойУценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * (втДанныеДляПроведения.СуммаПрошлойУценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки >= втДанныеДляПроведения.СуммаПрошлойУценки
	|";
	
КонецФункции

Функция ПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПартииПрочихРасходов", Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ // Уценка восстановительной стоимости
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаУценки > втДанныеДляПроведения.СуммаПрошлойДооценки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из доли уценки
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * (втДанныеДляПроведения.СуммаДооценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки < втДанныеДляПроведения.СуммаПрошлойУценки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из уценки
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * ((втДанныеДляПроведения.СуммаПрошлойУценки)/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки >= втДанныеДляПроведения.СуммаПрошлойУценки
	|";
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&Проведен                               КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	&НематериальныйАктив                    КАК НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт

	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизации("ПереоценкаНМА"));
	ТекстыВременныхТаблиц.Добавить(УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам());
	
	ТекстыВременныхТаблиц.Добавить(ВТДанныеДокумента());
	ТекстыВременныхТаблиц.Добавить(ВТСчетДооценки());
	ТекстыВременныхТаблиц.Добавить(ВТПредыдущаяПереоценкаОбъектов());
	ТекстыВременныхТаблиц.Добавить(ВТДанныеДляПроведения());
	ТекстыВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

Функция ВТДанныеДокумента()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втТаблицаОбъектов
	|"+
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Дата,
	|	Операция.Организация КАК Организация,
	|	Операция.Подразделение КАК Подразделение,
	|	Операция.НематериальныйАктив КАК ОбъектУчета,
	|	Операция.Стоимость КАК Стоимость,
	|	Операция.СтатьяДоходов,
	|	Операция.АналитикаДоходов,
	|	Операция.СтатьяРасходов,
	|	Операция.АналитикаРасходов
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПереоценкаНМА КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
КонецФункции

Функция ВТСчетДооценки()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ВТСчетаОтражения
	|"+
	"ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	ЕСТЬNULL(СчетаОтражения.СчетДооценки, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетДооценки
	|ПОМЕСТИТЬ втСчетДооценки
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|				&Дата,
	|				Регистратор <> &Ссылка
	|					И НЕ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|					И (Организация, НематериальныйАктив) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.ОбъектУчета
	|						ИЗ
	|							втОбъектыДокумента КАК Т)) КАК СчетаОтражения
	|		ПО втОбъектыДокумента.ОбъектУчета = СчетаОтражения.НематериальныйАктив
	|			И втОбъектыДокумента.Организация = СчетаОтражения.Организация";
	
КонецФункции

Функция ВТПредыдущаяПереоценкаОбъектов()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектов
	|"+
	"ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиПриход, 0)) КАК СуммаДооценки,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиРасход, 0)) КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимостиСгруппированная
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаНМАБухгалтерскийУчет.Обороты(
	|				,
	|				&ГраницаМесяцОкончание,
	|				Регистратор,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втОбъектыДокумента КАК Т)) КАК ПереоценкаНМАБухгалтерскийУчетОбороты
	|		ПО втОбъектыДокумента.ОбъектУчета = ПереоценкаНМАБухгалтерскийУчетОбороты.НематериальныйАктив
	|			И втОбъектыДокумента.Организация = ПереоценкаНМАБухгалтерскийУчетОбороты.Организация
	|			И (ПереоценкаНМАБухгалтерскийУчетОбороты.Регистратор <> &Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втОбъектыДокумента.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная.ОбъектУчета,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимости
	|ИЗ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная КАК втПредыдущаяПереоценкаСтоимостиСгруппированная
	|";
КонецФункции

Функция ВТДанныеДляПроведения()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втДанныеДляПроведения
	|"+
	"ВЫБРАТЬ
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Дата,
	|	втДанныеДокумента.ОбъектУчета,
	|	втДанныеДокумента.Организация КАК Организация,
	|	втДанныеДокумента.Подразделение КАК Подразделение,
	|	втДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов,
	|	втДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов,
	|	втДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	втДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	втСчетаОтражения.СчетУчета КАК СчетУчета,
	|	втСчетаОтражения.СчетНачисленияАмортизации КАК СчетАмортизации,
	|	втСчетДооценки.СчетДооценки КАК СчетДооценки,
	|	втПорядокУчетаНМАБУ.НалоговоеНазначение КАК НалоговоеНазначение,
	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ВосстановительнаяСтоимость,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК НакопленнаяАмортизация,
	|	втДанныеДокумента.Стоимость КАК НоваяСтоимость,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втДанныеДокумента.Стоимость < (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|			ТОГДА 0
	|		ИНАЧЕ втДанныеДокумента.Стоимость - (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|	КОНЕЦ КАК СуммаДооценкиСтоимости,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втДанныеДокумента.Стоимость > (ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) - втДанныеДокумента.Стоимость
	|	КОНЕЦ КАК СуммаУценкиСтоимости,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втДанныеДокумента.Стоимость < (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|				ИЛИ (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) = 0
	|			ТОГДА 0
	|		ИНАЧЕ - втОстаткиПоСчетам.НакопленнаяАмортизацияБУ + ВЫРАЗИТЬ(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ*втДанныеДокумента.Стоимость / (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаДооценкиАмортизации,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втДанныеДокумента.Стоимость > (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|				ИЛИ  (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстаткиПоСчетам.НакопленнаяАмортизацияБУ - ВЫРАЗИТЬ(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ*втДанныеДокумента.Стоимость / (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаУценкиАмортизации,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаСтоимости.СуммаДооценки, 0) КАК СуммаПрошлойДооценкиСтоимости,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаСтоимости.СуммаУценки, 0) КАК СуммаПрошлойУценкиСтоимости
	|ПОМЕСТИТЬ втДанныеДляПроведения
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК втСчетаОтражения
	|		ПО втДанныеДокумента.ОбъектУчета = втСчетаОтражения.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втДанныеДокумента.ОбъектУчета = втОстаткиПоСчетам.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетДооценки КАК втСчетДооценки
	|		ПО втДанныеДокумента.ОбъектУчета = втСчетДооценки.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
	|		ПО втДанныеДокумента.ОбъектУчета = втПорядокУчетаНМАБУ.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценкаСтоимости КАК втПредыдущаяПереоценкаСтоимости
	|		ПО втДанныеДокумента.ОбъектУчета = втПредыдущаяПереоценкаСтоимости.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
	|		ПО втДанныеДокумента.ОбъектУчета = втПорядокУчетаНМА.ОбъектУчета";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	Возврат ТекстОтраженияВРеглУчетеУКР();
		
КонецФункции


Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(УчетНМА.ТекстОтраженияВРеглУчетеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(ДооценкаВосстановительнойСтоимости());
	ТекстыОтражения.Добавить(ДооценкаВосстановительнойСтоимостиИзДолиУценки());
	ТекстыОтражения.Добавить(ДооценкаВосстановительнойСтоимостиИзУценки());
	ТекстыОтражения.Добавить(ДооценкаНакопленнойАмортизации());
	ТекстыОтражения.Добавить(УценкаВосстановительнойСтоимости());
	ТекстыОтражения.Добавить(УценкаВосстановительнойСтоимостиИзДолиДооценки());
	ТекстыОтражения.Добавить(УценкаВосстановительнойСтоимостиИзДооценки());
	ТекстыОтражения.Добавить(УценкаНакопленнойАмортизации());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ДооценкаВосстановительнойСтоимости()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка стоимости (Дт СчетУчета :: Кт СчетДооценки)
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости - втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости > втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";       

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Дооценка стоимости';uk='Дооцінка вартості'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция ДооценкаВосстановительнойСтоимостиИзДолиУценки()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка стоимости из предыдущей уценки (Дт СчетУчета :: Кт 746(742))
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК СубконтоКт1,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт2,
	|	втДанныеДляПроведения.АналитикаДоходов КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости < втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";    

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Дооценка восстановительной стоимости из предыдущей уценки';uk='Дооцінка відновлювальної вартості з попередньої уцінки'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция ДооценкаВосстановительнойСтоимостиИзУценки()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка стоимости из предыдущей уценки (Дт СчетУчета :: Кт 746(742))
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК СубконтоКт1,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт2,
	|	втДанныеДляПроведения.АналитикаДоходов КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости >= втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";  

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Дооценка стоимости из предыдущей уценки';uk='Дооцінка вартості з попередньої уцінки'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция ДооценкаНакопленнойАмортизации()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка накопленной амортизации (Дт СчетУчета :: Кт СчетАмортизации)
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиАмортизации КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетАмортизации КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиАмортизации > 0
	|";     

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Дооценка накопленной амортизации';uk='Дооцінка накопиченої амортизації'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция УценкаВосстановительнойСтоимости()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
 	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка стоимости (Дт 975(972) :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаУценкиСтоимости - втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	втДанныеДляПроведения.СтатьяРасходов КАК АналитикаУчетаДт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|   ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|   ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт1,
	|	втДанныеДляПроведения.СтатьяРасходов КАК СубконтоДт2,
	|	втДанныеДляПроведения.АналитикаРасходов КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО втДанныеДляПроведения.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости > втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости 
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Уценка стоимости';uk='Уцінка вартості'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция УценкаВосстановительнойСтоимостиИзДолиДооценки()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка стоимости из дооценки (Дт СчетДооценки :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаУценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|   ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости < втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости
	|";      

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Уценка стоимости из предыдущей дооценки';uk='Уцінка вартості з попередньої дооцінки'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция УценкаВосстановительнойСтоимостиИзДооценки()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка стоимости из предыдущей дооценки (Дт СчетДооценки :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|   ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости >= втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости
	|";       

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Уценка стоимости из предыдущей дооценки';uk='Уцінка вартості з попередньої дооцінки'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;

КонецФункции

Функция УценкаНакопленнойАмортизации()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка накопленной амортизации (Дт СчетАмортизации :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаУценкиАмортизации КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетАмортизации КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,	
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиАмортизации > 0
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Уценка накопленной амортизации';uk='Уцінка накопиченої амортизації'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетНМА.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

#КонецОбласти
#КонецОбласти

#КонецЕсли
