
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт
	
#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.УдалитьКассоваяСмена.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d70e41c6-87c5-4577-b1b5-79d3a994ad8d");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.УдалитьКассоваяСмена.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ЧитаемыеОбъекты = "Документ.УдалитьКассоваяСмена";
	Обработчик.ИзменяемыеОбъекты = "Документ.УдалитьКассоваяСмена";
	Обработчик.БлокируемыеОбъекты = "Документ.УдалитьКассоваяСмена";
	Обработчик.Комментарий = НСтр("ru='Выполняет перенос данных из документа Кассовая смена (удалить) в документ Кассовая смена.';uk='Виконує перенесення даних із документа Касова зміна (видалити) до документа Касова зміна.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЧекККМ.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетОРозничныхПродажах.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЧекККМВозврат.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратПодарочныхСертификатов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияПодарочныхСертификатов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

#КонецОбласти

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УдалитьКассоваяСмена КАК Таблица
	|ГДЕ
	|	Таблица.НоваяКассоваяСмена = ЗНАЧЕНИЕ(Документ.КассоваяСмена.ПустаяСсылка)
	|";
	
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		МассивСсылок
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.УдалитьКассоваяСмена";
	МетаданныеОбъекта = Метаданные.Документы.УдалитьКассоваяСмена;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось заблокировать документ: %Документ% по причине: %Причина%';uk='Не вдалося заблокувати документ: %Документ% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			    УровеньЖурналаРегистрации.Предупреждение,
			    МетаданныеОбъекта,
			    Выборка.Ссылка,
			    ТекстСообщения
			);
			
			Продолжить;
			
		КонецПопытки;
		
		УдалитьКассоваяСменаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если УдалитьКассоваяСменаОбъект = Неопределено Тогда
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			ЗафиксироватьТранзакцию();
			Продолжить;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УдалитьКассоваяСменаОбъект.НоваяКассоваяСмена) Тогда
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			ЗафиксироватьТранзакцию();
			Продолжить;
			
		КонецЕсли;
		
		НоваяКассоваяСменаОбъект = Документы.КассоваяСмена.СоздатьДокумент();
		
		НоваяКассоваяСменаОбъект.Номер                  = УдалитьКассоваяСменаОбъект.Номер;
		НоваяКассоваяСменаОбъект.Дата                   = УдалитьКассоваяСменаОбъект.Дата;
		НоваяКассоваяСменаОбъект.КассаККМ               = УдалитьКассоваяСменаОбъект.КассаККМ;
		НоваяКассоваяСменаОбъект.Комментарий            = УдалитьКассоваяСменаОбъект.Комментарий;
		НоваяКассоваяСменаОбъект.НачалоКассовойСмены    = УдалитьКассоваяСменаОбъект.НачалоКассовойСмены;
		НоваяКассоваяСменаОбъект.ОкончаниеКассовойСмены = УдалитьКассоваяСменаОбъект.ОкончаниеКассовойСмены;
		НоваяКассоваяСменаОбъект.Организация            = УдалитьКассоваяСменаОбъект.Организация;
		
		Если УдалитьКассоваяСменаОбъект.СтатусКассовойСмены = Перечисления.УдалитьСтатусыКассовойСмены.Закрыта Тогда
			НоваяКассоваяСменаОбъект.Статус = Перечисления.СтатусыКассовойСмены.Закрыта;
			НоваяКассоваяСменаОбъект.СтатусРегламентныхОпераций = Перечисления.СтатусыРегламентныхОперацийКассовойСмены.СозданОтчет;
		ИначеЕсли УдалитьКассоваяСменаОбъект.СтатусКассовойСмены = Перечисления.УдалитьСтатусыКассовойСмены.Открыта Тогда
			НоваяКассоваяСменаОбъект.Статус = Перечисления.СтатусыКассовойСмены.Открыта;
			НоваяКассоваяСменаОбъект.СтатусРегламентныхОпераций = Перечисления.СтатусыРегламентныхОперацийКассовойСмены.ПустаяСсылка();
		Иначе
			НоваяКассоваяСменаОбъект.Статус = Перечисления.СтатусыКассовойСмены.Закрыта;
			НоваяКассоваяСменаОбъект.СтатусРегламентныхОпераций = Перечисления.СтатусыРегламентныхОперацийКассовойСмены.СозданОтчетЧекиЗаархивированы;
		КонецЕсли;
		
		Попытка
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоваяКассоваяСменаОбъект, Истина);
			
			УдалитьКассоваяСменаОбъект.НоваяКассоваяСмена = НоваяКассоваяСменаОбъект.Ссылка;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(УдалитьКассоваяСменаОбъект, Истина);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось обработать документ: %Документ% по причине: %Причина%';uk='Не вдалося обробити документ: %Документ% з причини: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			    УровеньЖурналаРегистрации.Предупреждение,
			    МетаданныеОбъекта,
			    Выборка.Ссылка,
			    ТекстСообщения
			);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

