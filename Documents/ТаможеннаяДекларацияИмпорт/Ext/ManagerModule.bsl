#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	ТаможеннаяДекларацияИмпортЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииРасходовНаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаЗакупки(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДСРеестрПолученныхНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДСУсловныеПродажи(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		
		ТаможеннаяДекларацияИмпортЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ЗаявкаНаРасходованиеДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ТаможеннаяДекларацияИмпортЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Добавляет команду создания документа "Таможенная декларация на импорт".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ТаможеннаяДекларацияИмпорт) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ТаможеннаяДекларацияИмпорт.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ТаможеннаяДекларацияИмпорт);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьИмпортныеЗакупки";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	ТаможеннаяДекларацияИмпортЛокализация.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ТаможеннаяДекларацияИмпортЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ТаможеннаяДекларацияИмпортЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Процедура заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Выбранная хозяйственная операция
//	ВсеРеквизиты - Массив - Массив всех имен реквизитов, зависимых от хозяйственной операции
//	РеквизитыОперации - Массив - Массив имен реквизитов, используемых в выбранной хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, ВсеРеквизиты, РеквизитыОперации) Экспорт
	ВсеРеквизиты = Новый Массив;
	РеквизитыОперации = Новый Массив;
	
	ВсеРеквизиты.Добавить("Соглашение");
	ВсеРеквизиты.Добавить("Договор");
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОформлениеГТДБрокером Тогда
		РеквизитыОперации.Добавить("Соглашение");
		РеквизитыОперации.Добавить("Договор");
	КонецЕсли;
КонецПроцедуры

// Получает реквизиты документа
//
// Параметры:
//  ДокументСсылка	 - ДокументСсылка.ТаможеннаяДекларацияИмпорт - Ссылка на документ.
//
// Возвращаемое значение:
//  Структура - Структура реквизитов.
//
Функция РеквизитыДокумента(ДокументСсылка) Экспорт
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата", '00010101');
	Реквизиты.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно);
	Реквизиты.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Реквизиты.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	Реквизиты.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Реквизиты.Вставить("Договор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	Реквизиты.Вставить("ПорядокРасчетов", Перечисления.ПорядокРасчетов.ПустаяСсылка());
	Реквизиты.Вставить("РасчетыПоДоговору", Ложь);
	Реквизиты.Вставить("ПоЗаказу", Ложь);
	Реквизиты.Вставить("ВалютаВзаиморасчетов", Справочники.Валюты.ПустаяСсылка());
	Реквизиты.Вставить("СуммаДокумента", 0);
	Реквизиты.Вставить("СуммаВзаиморасчетов", 0);
	Реквизиты.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Данные.Дата КАК Дата,
	|	Данные.ВариантОформления КАК ХозяйственнаяОперация,
	|	Данные.Организация КАК Организация,
	|	Данные.Партнер КАК Партнер,
	|	Данные.Контрагент КАК Контрагент,
	|	Данные.Договор КАК Договор,
	|	Данные.ПорядокРасчетов КАК ПорядокРасчетов,
	|	ВЫБОР КОГДА Данные.Договор.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК РасчетыПоДоговору,
	|	ЛОЖЬ КАК ПоЗаказу,
	|	Данные.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Данные.СуммаДокумента КАК СуммаДокумента,
	|	ВЫБОР КОГДА Данные.Проведен ТОГДА
	|		Данные.СуммаВзаиморасчетов
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаВзаиморасчетов,
	|	Данные.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК Данные
	|ГДЕ
	|	Данные.Ссылка = &ДокументСсылка
	|");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	КонецЕсли;
	Возврат Реквизиты;
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе
//
// Параметры:
//  Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//  Структура - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ТаможеннаяДекларацияИмпорт";
	
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьПоСериямСклад", Новый Структура());
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад", Новый Структура());
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПустаяСсылка());
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Склад");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерий.ТолькоСерииДляСебестоимости = Истина;
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	ОперацииТоварыВПути = Новый Массив();
	ОперацииТоварыВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	ОперацииТоварыВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	
	ПараметрыУказанияСерий.ПараметрыЗапроса.Вставить("ХозОперацииТоварыВПути", ОперацииТоварыВПути);
	
	ПараметрыУказанияСерий.ИменаПолейДополнительные.Добавить("ХозяйственнаяОперация");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Дата";
	
	Возврат ИменаРеквизитов;
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Склад,
	|	Товары.Серия,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.ХозяйственнаяОперация,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 14
	|							ИНАЧЕ 13
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|		ПО (ПолитикиУчетаСерий.Склад = Товары.Склад)
	|			И (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|ГДЕ
	|	НЕ Товары.ХозяйственнаяОперация В (&ХозОперацииТоварыВПути)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ВидНоменклатуры.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВидНоменклатуры.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|					ТОГДА
	|						ВЫБОР
	|							КОГДА ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям
	|								ТОГДА
	|									ВЫБОР
	|										КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 18
	|										ИНАЧЕ 17
	|									КОНЕЦ
	|								ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ИЗ
	|	Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидНоменклатуры
	|	ПО ВидНоменклатуры.Ссылка = ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры
	|ГДЕ
	|	Товары.ХозяйственнаяОперация В (&ХозОперацииТоварыВПути)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;

КонецФункции

// Возвращает таблицу используемых статусов документа учитывая зависимости от функциональных опций и иных параметров.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. ОбщегоНазначенияУТ.ТаблицаСтатусовИзменяемыхИзСписка
//
Функция СтатусыДокументаИзменяемыеИзСписка() Экспорт
	
	Таблица = ОбщегоНазначенияУТ.ТаблицаСтатусовИзменяемыхИзСписка();
		
	Возврат Таблица;
	
КонецФункции

//Возвращает количество накладных к оформлению.
//
// Параметры:
//	Параметры - Структура - свойства:
//	* МассивОрганизаций - Массив - массив организаций.
//	* КонецПериода - Дата - ограничение сверху на дату документа приобретения.
//	* БезОграниченияПериода - Булево - не ограничивать по дате документа приобретения
//
// Возвращаемое значение:
//	Число - количество накладных к оформлению.
//
Функция ЕстьНакладныеКОформлению(Параметры) Экспорт
		
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НаДату.ДокументПоступления) КАК КоличествоДокументовКОформлению
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций.Остатки(&ДатаОкончания, ) КАК НаДату
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций.Остатки(, ) КАК НаСейчас
	|		ПО НаДату.АналитикаУчетаНоменклатуры = НаСейчас.АналитикаУчетаНоменклатуры
	|			И НаДату.Поставщик = НаСейчас.Поставщик
	|			И НаДату.Организация = НаСейчас.Организация
	|			И НаДату.ВидЗапасов = НаСейчас.ВидЗапасов
	|			И НаДату.ДокументПоступления = НаСейчас.ДокументПоступления
	|ГДЕ
	|	НаСейчас.КоличествоОстаток > 0
	|	И &УсловиеОтбора";
	
	Если ЗначениеЗаполнено(Параметры.МассивОрганизаций) Тогда
		УсловиеОтбора = "НаДату.Организация В (&МассивОрганизаций)";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", УсловиеОтбора);
		Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.МассивОрганизаций);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &УсловиеОтбора", "");
	КонецЕсли;
	
	Если Параметры.Свойство("БезОграниченияПериода") Тогда
		Запрос.УстановитьПараметр("ДатаОкончания", ТекущаяДатаСеанса());
	Иначе
		Запрос.УстановитьПараметр("ДатаОкончания", Параметры.КонецПериода);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И НЕ Выборка.КоличествоДокументовКОформлению = NULL Тогда
		Возврат Выборка.КоличествоДокументовКОформлению;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ТаможеннаяДекларацияИмпортЛокализация.ТекстОтраженияВРеглУчете();
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТаможеннаяДекларацияИмпортЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();
	
КонецФункции

//-- НЕ УТ

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	Параметры - Структура - Структура, свойства которой содержат значения, 
//            влияющие на ДоступностьПоОперации: ТаможенныйСбор, ТаможенныйШтраф, ОбработкаПроверкиЗаполнения.
// 
// Возвращаемое значение:
// 	Массив - см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик(Параметры) Экспорт
	
	МассивПаметровВыбора = Новый Массив;
	
	#Область СтатьяРасходовСбор
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовСбор";
	
	Если Параметры.Свойство("ТаможенныйСбор") И 
		 Параметры.ТаможенныйСбор = 0 И 
		 Параметры.Свойство("ОбработкаПроверкиЗаполнения") И 
		 Параметры.ОбработкаПроверкиЗаполнения Тогда
		
		ПараметрыВыбора.ДоступностьПоОперации = Ложь;
		
	КонецЕсли;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;	
	
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовСбор";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовСбор");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовСбор");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

// Возвращает параметры механизма взаиморасчетов.
// 
// Возвращаемое значение:
// 	Массив - см. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Функция ПараметрыВзаиморасчеты() Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	СтруктураПараметров.ЭтоПродажаЗакупка                    = Истина;
	СтруктураПараметров.ТипРасчетов                          = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	
	СтруктураПараметров.ВалютаВзаиморасчетов                 = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов                  = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.СуммаДокументаФорма                  = "Форма.СуммаДокумента";
	
	СтруктураПараметров.Курс                                = 1;
	СтруктураПараметров.Кратность                           = 1;
	СтруктураПараметров.ДатаПлатежа                         = "Объект.ДатаПлатежа";
	
	СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа     = "Объект.РасшифровкаПлатежа";
	СтруктураПараметров.Касса                               = "";
	СтруктураПараметров.Менеджер                            = "Объект.Менеджер";
	
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты         = "ДекорацияВалюты";
	СтруктураПараметров.ЭлементыФормы.НадписьЭтапы          = "ДекорацияЭтапыОплаты";
	СтруктураПараметров.ЭлементыФормы.ЗачетОплаты           = "ЗачетОплатыФорма";
	СтруктураПараметров.ЭлементыФормы.СуммаВзаиморасчетовТЧ = "";
	СтруктураПараметров.ВалютыИКурсДокументаТолькоПросмотр  = Истина;
	
	СтруктураПараметров.ОбъектРасчетов                      = "Объект.ОбъектРасчетов";
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ПараметрыЗаполненияНалоговогоНазначения(Объект) Экспорт
    
    ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалоговогоНазначения();
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ТаможеннаяДекларацияИмпорт")
		Или ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ДанныеОбъекта = Объект;
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
        ДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, "Организация,НаправлениеДеятельности");
	КонецЕсли;
	
	ПараметрыЗаполнения.Организация             = Объект.Организация;
	ПараметрыЗаполнения.Дата                    = Объект.Дата;
	ПараметрыЗаполнения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
    ПараметрыЗаполнения.ЕстьСтавкаНДС           = Истина;
	ПараметрыЗаполнения.ИмпортТоваров			= Ложь; //чтобы нулевая ставка обозначала необл. деятельность
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ( ЗначениеРазрешено(Партнер)
	|	ИЛИ ЗначениеРазрешено(Поставщик)
	|	) ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СуммаДокумента(Объект) Экспорт
	Возврат Объект.ТаможенныйСбор + Объект.Товары.Итог("СуммаПошлины") + Объект.Товары.Итог("СуммаАкциза") + Объект.Товары.Итог("СуммаНДС");
КонецФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных; 

КонецФункции

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос) Экспорт
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуВзаиморасчетов") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты =
		РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта, Запрос.Параметры.ВалютаВзаиморасчетов, Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",            Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",           Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуВзаиморасчетов", Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.НомерГТД.Код КАК НомерДекларации,
	|	ДанныеДокумента.НомерГТД.Дата КАК ДатаДекларации,
	|	ДанныеДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы КАК ОтнестиСтоимостьТаможеннойОчисткиНаРасходы,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВЫБОР КОГДА ДанныеДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ВыпущеноСТаможни,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(ДанныеДокумента.Подразделение.ВариантОбособленногоУчетаТоваров, ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПустаяСсылка)) КАК ВариантОбособленногоУчетаТоваров,
	|	ДанныеДокумента.ВариантОформления КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Менеджер КАК Менеджер,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.Соглашение КАК Соглашение,
	|	ДанныеДокумента.КонтрагентПоставщика КАК КонтрагентПоставщика,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ) КАК УчетЗатратПоНД,
	|	ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ) КАК УчетРасчетовСПоставщикамиПоНД,
	|	НЕОПРЕДЕЛЕНО КАК Сделка,
	|	ДанныеДокумента.Статус                 КАК Статус,
	|	ДанныеДокумента.Комментарий            КАК Комментарий,
	|	ДанныеДокумента.СуммаДокумента         КАК СуммаДокумента,
	|	ДанныеДокумента.ПометкаУдаления        КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен               КАК Проведен,
	|	ДанныеДокумента.ФормаОплаты            КАК ФормаОплаты,
	|	ДанныеДокумента.ДатаПлатежа            КАК ДатаПлатежа
	|	
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Номер", Реквизиты.Номер);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Реквизиты));
	Запрос.УстановитьПараметр("ОбъектРасчетов", Реквизиты.ОбъектРасчетов);
	Запрос.УстановитьПараметр("ВыпущеноСТаможни", Реквизиты.ВыпущеноСТаможни);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Контрагент", Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("ВариантОбособленногоУчетаТоваров", Реквизиты.ВариантОбособленногоУчетаТоваров);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Партнер", Реквизиты.Партнер);
	Запрос.УстановитьПараметр("Менеджер", Реквизиты.Менеджер);
	Запрос.УстановитьПараметр("Договор", Реквизиты.Договор);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", Реквизиты.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Соглашение", Реквизиты.Соглашение);
	Запрос.УстановитьПараметр("КонтрагентПоставщика", Реквизиты.КонтрагентПоставщика);
	Запрос.УстановитьПараметр("Статус",                         Реквизиты.Статус);
	Запрос.УстановитьПараметр("НаправлениеДеятельности",        Реквизиты.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("УчетРасчетовСПоставщикамиПоНД",  Реквизиты.УчетРасчетовСПоставщикамиПоНД);
	Запрос.УстановитьПараметр("УчетЗатратПоНД",                 Реквизиты.УчетЗатратПоНД);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",        ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	Запрос.УстановитьПараметр("Комментарий",                    Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("СуммаДокумента",                 Реквизиты.СуммаДокумента);
	Запрос.УстановитьПараметр("ПометкаУдаления",                Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                       Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ФормаОплаты",                    Реквизиты.ФормаОплаты);
	Запрос.УстановитьПараметр("ДатаПлатежа",                    Реквизиты.ДатаПлатежа);
	
	Запрос.УстановитьПараметр("ОрганизацияПлательщикНДС", НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОтнестиСтоимостьТаможеннойОчисткиНаРасходы", Реквизиты.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы);
	
	Запрос.УстановитьПараметр("НомерДекларации", Реквизиты.НомерДекларации);
	Запрос.УстановитьПараметр("ДатаДекларации", Реквизиты.ДатаДекларации);
	
	ХозОперацииЗакупкаПоИмпорту = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
		Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	Запрос.УстановитьПараметр("ХозОперацииЗакупкаПоИмпорту", ХозОперацииЗакупкаПоИмпорту);
	
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ИнформацияПоДоговору = НСтр("ru='По договору ""%Договор%""';uk='За договором ""%Договор%""'",ОбщегоНазначения.КодОсновногоЯзыка());
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Ключи.Номенклатура         КАК Номенклатура,
	|	Ключи.Характеристика       КАК Характеристика,
	|	&ПустоеНазначение          КАК Назначение,
	|	Ключи.Серия                КАК Серия,
	|	Ключи.МестоХранения		   КАК Склад,
	|	Ключи.СтатьяКалькуляции    КАК СтатьяКалькуляции
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО Товары.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Ключи.Номенклатура                  = Аналитика.Номенклатура
	|		И Ключи.Характеристика             = Аналитика.Характеристика
	|		И Ключи.Серия                      = Аналитика.Серия
	|		И Ключи.МестоХранения			   = Аналитика.МестоХранения
	|		И &ПустоеНазначение                = Аналитика.Назначение
	|		И Ключи.СтатьяКалькуляции          = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И Аналитика.Номенклатура ЕСТЬ NULL
	|	И НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|");
	
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("ПустоеНазначение", Справочники.Назначения.ПустаяСсылка());
	ЗапросАналитик.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям", Запрос.Параметры.УчитыватьСебестоимостьТоваровПоНазначениям);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаНоменклатурыИнициализированы", Истина);
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ТаблицаТоваров.Подразделение КАК Подразделение,
	|	ТаблицаТоваров.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаТоваров.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТоваров.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ)
	|			ТОГДА ТаблицаТоваров.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаТоваров.НалоговоеНазначение КАК НалоговоеНазначение,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	СУММА(ТаблицаТоваров.СуммаСНДСУпр) КАК СуммаСНДС,
	|	СУММА(ТаблицаТоваров.СуммаБезНДСУпр) КАК СуммаБезНДС,
	|	СУММА(ТаблицаТоваров.СуммаБезНДСУпрМУ) КАК СуммаБезНДСУпр,
	|	СУММА(ТаблицаТоваров.СуммаСНДСРегл) КАК СуммаСНДСРегл,
	|	СУММА(ТаблицаТоваров.СуммаБезНДСРегл) КАК СуммаБезНДСРегл,
	|	СУММА(ТаблицаТоваров.СуммаНДСРегл) 	КАК НДСРегл,
	|	ТаблицаТоваров.ВариантОформления КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТоваров
	|ГДЕ
	|	&ВыпущеноСТаможни
	|	И ТаблицаТоваров.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Организация,
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.СтатьяРасходов,
	|	ТаблицаТоваров.АналитикаРасходов,
	|	ТаблицаТоваров.НалоговоеНазначение,
	|	ТаблицаТоваров.ВариантОформления,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТоваров.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ)
	|			ТОГДА ТаблицаТоваров.НаправлениеДеятельности
	|	КОНЕЦ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса)
	
	// Создадим временную таблицу "ВтТаблицаАналитикУчетаПартий"
	
	ТекстВыборкаПоляАналитик =
	"ВЫБРАТЬ
	|	""Товары"" 												КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки 							КАК НомерСтроки,
	|	ДанныеДокумента.Партнер									КАК Поставщик,
	|	ДанныеДокумента.Контрагент								КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС 								КАК СтавкаНДС,
	|	ТаблицаДокумента.НалоговоеНазначение					КАК НалоговоеНазначение,	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)				КАК ВидЦенности,
	|	0														КАК КодСтроки
	|ПОМЕСТИТЬ ВТПоляАналитикУчетаПартий
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Документ"" 												КАК ИмяТабличнойЧасти,
	|	0 															КАК НомерСтроки,
	|	ДанныеДокумента.Партнер										КАК Поставщик,
	|	ДанныеДокумента.Контрагент									КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)						КАК СтавкаНДС,
	|	Значение(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)					КАК ВидЦенности,
	|	0															КАК КодСтроки
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаПартий.ТекстЗапросаВтТаблицаАналитикУчетаПартий(ТекстВыборкаПоляАналитик, Запрос, ТекстыЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "СебестоимостьТоваров";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТТоварыКОформлениюТаможенныхДеклараций", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|		ТОГДА ТоварыДокумента.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ТоварыДокумента.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		КОГДА ТоварыДокумента.Склад.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК РазделУчета,
	|	ТоварыДокумента.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыДокумента.Организация КАК Организация,
	|
	//	партионный учет версии 2.2
	|	НЕОПРЕДЕЛЕНО															КАК Партия,
	|	НЕОПРЕДЕЛЕНО															КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|			ВЫБОР
	|				КОГДА &ФормироватьВидыЗапасовПоСделкам И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Сделка
	|				КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|				 И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Подразделение.ВариантОбособленногоУчетаТоваров, НЕОПРЕДЕЛЕНО)
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Менеджер
	|				КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|				 И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Подразделение.ВариантОбособленногоУчетаТоваров, НЕОПРЕДЕЛЕНО)
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Подразделение
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																	КАК АналитикаФинансовогоУчета,
    |	ВЫБОР КОГДА &ПартионныйУчетВерсии21
    |		ТОГДА ТоварыДокумента.НалоговоеНазначение
    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
    |	КОНЕЦ 																	КАК НалоговоеНазначение21,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.НалоговоеНазначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																	КАК НалоговоеНазначение,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.НалоговоеНазначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																	КАК КорНалоговоеНазначение,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																	КАК КорАналитикаУчетаПартий,
	|	СУММА(ТоварыДокумента.СуммаНДСРегл) 									КАК НДСРегл,
    |	СУММА(ТоварыДокумента.СуммаНДСРегл) 									КАК НДСРеглРеквизит,
	|
	|	0 КАК Количество,
	|	0 КАК Стоимость,
	|	СУММА(ТоварыДокумента.СуммаСНДСУпр) 									КАК ДопРасходы,
	|	0 КАК СтоимостьБезНДС,
	|	СУММА(ТоварыДокумента.СуммаБезНДСУпр) 									КАК ДопРасходыБезНДС,
	|
	|	СУММА(ВЫБОР
	|			КОГДА &УправленческийУчетОрганизаций И ТоварыДокумента.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|				ТОГДА ТоварыДокумента.СуммаСНДСУпр
	|			КОГДА &УправленческийУчетОрганизаций
	|				ТОГДА ТоварыДокумента.СуммаБезНДСУпрМУ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДопРасходыУпр,
	|	СУММА(ВЫБОР
	|			КОГДА &УправленческийУчетОрганизаций
	|				ТОГДА ТоварыДокумента.СуммаБезНДСУпрМУ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДопРасходыУпрБезНДС,
    |	СУММА(ТоварыДокумента.СуммаНДСУпрМУ) КАК НДСУпр,  
	|
	|	СУММА(
	|		ВЫБОР КОГДА &ПартионныйУчетВерсии22 ТОГДА 0
	|		КОГДА ТоварыДокумента.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|		ТОГДА
	|			ТоварыДокумента.СуммаСНДСРегл
	|		ИНАЧЕ
	|			ТоварыДокумента.СуммаБезНДСРегл
	|		КОНЕЦ) КАК СтоимостьРегл,
	|	СУММА(
	|		ВЫБОР КОГДА &ПартионныйУчетВерсии22 ТОГДА 0
	|		ИНАЧЕ
	|			ТоварыДокумента.СуммаБезНДСРегл
	|		КОНЕЦ) КАК СтоимостьРеглБезНДС,
	|	СУММА(
	|		ВЫБОР КОГДА НЕ &ПартионныйУчетВерсии22 ТОГДА 0
	|		КОГДА ТоварыДокумента.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|		ТОГДА
	|			ТоварыДокумента.СуммаСНДСРегл
	|		ИНАЧЕ
	|			ТоварыДокумента.СуммаБезНДСРегл
	|		КОНЕЦ) КАК ДопРасходыРегл,
	|	СУММА(
	|		ВЫБОР КОГДА НЕ &ПартионныйУчетВерсии22 ТОГДА 0
	|		ИНАЧЕ
	|			ТоварыДокумента.СуммаБезНДСРегл
	|		КОНЕЦ) КАК ДопРасходыРеглБезНДС,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТТоварыКОформлениюТаможенныхДеклараций.ДокументПоступленияЗаполнен, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыБезПартии) КОНЕЦ) КАК ХозяйственнаяОперация,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТТоварыКОформлениюТаможенныхДеклараций.ДокументПоступленияЗаполнен, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии) КОНЕЦ) КАК ТипЗаписи,
	|	ТоварыДокумента.Подразделение КАК Подразделение,
	|	ТоварыДокумента.ДокументПоступления КАК ДокументИсточник
	|ИЗ
	|	ВтТаблицаТовары КАК ТоварыДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТТоварыКОформлениюТаможенныхДеклараций КАК ВТТоварыКОформлениюТаможенныхДеклараций
	|	ПО
	|		ВТТоварыКОформлениюТаможенныхДеклараций.ДокументПоступления = ТоварыДокумента.ДокументПоступления
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО Ключи.Ссылка = ТоварыДокумента.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО АналитикаБезНазначения.Номенклатура = Ключи.Номенклатура
	|		И АналитикаБезНазначения.Характеристика = Ключи.Характеристика
	|		И АналитикаБезНазначения.Серия = Ключи.Серия
	|		И АналитикаБезНазначения.МестоХранения = Ключи.МестоХранения
	|		И АналитикаБезНазначения.СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	
	|ГДЕ
	|	&ВыпущеноСТаможни
	|	И НЕ ТоварыДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|	И (ТоварыДокумента.СуммаБезНДСРегл <> 0
	|		ИЛИ ТоварыДокумента.СуммаБезНДСУпр <>0
	|		ИЛИ ТоварыДокумента.СуммаНДСРегл <>0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|		ТОГДА ТоварыДокумента.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	ТоварыДокумента.ВидЗапасов,
	|	ТоварыДокумента.Организация,
	|	ТоварыДокумента.Подразделение,
	|	ТоварыДокумента.ДокументПоступления,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|			ВЫБОР
	|				КОГДА &ФормироватьВидыЗапасовПоСделкам И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Сделка
	|				КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|				 И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Подразделение.ВариантОбособленногоУчетаТоваров, НЕОПРЕДЕЛЕНО)
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Менеджер
	|				КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|				 И ЕСТЬNULL(ТоварыДокумента.ДокументПоступления.Подразделение.ВариантОбособленногоУчетаТоваров, НЕОПРЕДЕЛЕНО)
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|					ТОГДА ТоварыДокумента.ДокументПоступления.Подразделение
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
    |	ВЫБОР КОГДА &ПартионныйУчетВерсии21
    |		ТОГДА ТоварыДокумента.НалоговоеНазначение
    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
    |	КОНЕЦ,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.НалоговоеНазначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.НалоговоеНазначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТоварыДокумента.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ТоварыДокумента.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		КОГДА ТоварыДокумента.Склад.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ,
	|	ТоварыДокумента.ВидЗапасов,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТТоварыКОформлениюТаможенныхДеклараций.ДокументПоступленияЗаполнен, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыБезПартии) КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТТоварыКОформлениюТаможенныхДеклараций.ДокументПоступленияЗаполнен, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии) КОНЕЦ)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТоварыДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.Характеристика КАК Характеристика,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТоварыДокумента.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыДокумента.НомерГТД КАК НомерГТД,
	|	ТоварыДокумента.Количество КАК Количество,
	|	ДанныеДокумента.ВариантОформления КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТоварыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|	ПО
	|		ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ВыпущеноСТаможни";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК ДатаПоступления,
	|	Строки.Номенклатура   КАК Номенклатура,
	|	Строки.Характеристика КАК Характеристика,
	|	Строки.Серия          КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	Строки.ВидЗапасов     КАК ВидЗапасов,
	|	Строки.НомерГТД       КАК НомерГТД
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК Даты
	|	ПО
	|		Строки.ВидЗапасов = Даты.ВидЗапасов 
	|		И Строки.Номенклатура = Даты.Номенклатура
	|		И Строки.Характеристика = Даты.Характеристика 
	|		И Строки.Серия = Даты.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = Даты.Назначение
	|		И Строки.НомерГТД = Даты.НомерГТД 
	|ГДЕ
	|	Строки.Ссылка = &Ссылка И &ВыпущеноСТаможни
	|	И (Даты.ДатаПоступления ЕСТЬ NULL ИЛИ Даты.ДатаПоступления < &Период)
	|	И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка),
	|		&ХозОперацииЗакупкаПоИмпорту)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Номенклатура,
	|	Строки.Характеристика,
	|	Строки.Серия,
	|	Строки.ВидЗапасов,
	|	Строки.НомерГТД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса)
	
	ИмяВременнойТаблицы = "ВТТоварыКОформлениюТаможенныхДеклараций";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ТоварыКОформлениюТаможенныхДеклараций.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка), ЛОЖЬ) КАК ДокументПоступленияЗаполнен,
	|	ДанныеДокумента.ДокументПоступления КАК ДокументПоступления
	|ПОМЕСТИТЬ ВТТоварыКОформлениюТаможенныхДеклараций
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций КАК ТоварыКОформлениюТаможенныхДеклараций
	|		ПО ДанныеДокумента.ДокументПоступления = ТоварыКОформлениюТаможенныхДеклараций.Регистратор
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюТаможенныхДеклараций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТТоварыКОформлениюТаможенныхДеклараций", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТоварыДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеДокумента.Поставщик КАК Поставщик,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТоварыДокумента.ВидЗапасов КАК ВидЗапасов,
	|	ВЫБОР 
	|		КОГДА 
	|			ЕСТЬNULL(ВтТоварыКОформлению.ДокументПоступленияЗаполнен,ЛОЖЬ) ТОГДА
	|				ТоварыДокумента.ДокументПоступления
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументПоступления,
	|
	|	ТоварыДокумента.Количество КАК Количество,
	|	ВЫБОР 
	|		КОГДА 
	|			ЕСТЬNULL(ВтТоварыКОформлению.ДокументПоступленияЗаполнен,ЛОЖЬ) ТОГДА
	|				ТоварыДокумента.ТаможеннаяСтоимость
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Сумма,
	|
	|	ТоварыДокумента.НомерГТД КАК НомерГТД
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТоварыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|	ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыКОформлениюТаможенныхДеклараций КАК ВтТоварыКОформлению
	|	ПО ВтТоварыКОформлению.ДокументПоступления = ТоварыДокумента.ДокументПоступления
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ТоварыДокумента.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка),
	|		&ХозОперацииЗакупкаПоИмпорту)
	|	И &ВыпущеноСТаможни
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииРасходовНаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииРасходовНаСебестоимостьТоваров";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 			КАК ВидДвижения,
	|	ТоварыДокумента.Организация 					КАК Организация,
	|	ТоварыДокумента.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	&Ссылка											КАК ДокументПоступления,
	|	ТоварыДокумента.ВидЗапасов 						КАК ВидЗапасов,
	|	ТоварыДокумента.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&Ссылка 										КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО 									КАК СтатьяРасходов,
	|
	|	СУММА(ТоварыДокумента.Количество)				КАК Количество,
	|	СУММА(ТоварыДокумента.СуммаСНДСУпр) 							КАК Стоимость,
	|	СУММА(ТоварыДокумента.СуммаБезНДСУпр) 							КАК СтоимостьБезНДС,
	|	СУММА(ТоварыДокумента.СуммаБезНДСРегл) 							КАК СтоимостьРегл,
	|	СУММА(ТоварыДокумента.СуммаНДСРегл)								КАК НДСРегл,
	|	&Подразделение													КАК ПодразделениеРасходов
	|ИЗ
	|	ВтТаблицаТовары КАК ТоварыДокумента
	|ГДЕ
	|	&ВыпущеноСТаможни
	|	И НЕ ТоварыДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|	И &ПартионныйУчетВерсии21
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокумента.Организация,
	|	ТоварыДокумента.АналитикаУчетаНоменклатуры,
	|	ТоварыДокумента.ВидЗапасов,
	|	ТоварыДокумента.АналитикаУчетаПартий
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =  "
	|ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ТоварыДокумента.Организация                     КАК Организация,
	|	ТоварыДокумента.Подразделение                   КАК Подразделение,
	|	&Ссылка                                         КАК ДокументПоступленияРасходов,
	|	ТоварыДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ТоварыДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО 									КАК АналитикаАктивовПассивов,
	|	ТоварыДокумента.АналитикаУчетаПартийДанныеДокумента КАК АналитикаУчетаПартий,
	|	ТоварыДокумента.ВариантОформления 				КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыДокумента.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ) ТОГДА
	|			ТоварыДокумента.НаправлениеДеятельности
	|	КОНЕЦ 											КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО 									КАК АналитикаУчетаНоменклатуры,
	|	&НалоговоеНазначениеОрганизации 				КАК НалоговоеНазначение,
	|
	|	СУММА(ТоварыДокумента.СуммаСНДСУпр)             КАК Стоимость,
    |	СУММА(ТоварыДокумента.СуммаБезНДСУпр)           КАК СтоимостьБезНДС,
	|	СУММА(ТоварыДокумента.СуммаНДСУпрМУ)	        КАК НДСУпр,
	|	СУММА(ТоварыДокумента.СуммаБезНДСРегл)          КАК СтоимостьРегл,
	|	СУММА(ТоварыДокумента.СуммаНДСРегл)             КАК НДСРегл
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
    |	ВтТаблицаТовары КАК ТоварыДокумента
	|ГДЕ
	|	&ВыпущеноСТаможни 
	|	И ТоварыДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокумента.Организация,
	|	ТоварыДокумента.Подразделение,
	|	ТоварыДокумента.СтатьяРасходов,
	|	ТоварыДокумента.АналитикаРасходов,
	|	ТоварыДокумента.АналитикаУчетаПартийДанныеДокумента,
	|	ТоварыДокумента.ВариантОформления,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыДокумента.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ) ТОГДА
	|			ТоварыДокумента.НаправлениеДеятельности
	|	КОНЕЦ
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗакупки(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "Закупки";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"// Отражение пошлины, акциза, таможенного сбора.
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТоварыДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыДокумента.Склад КАК Склад,
	|	ТоварыДокумента.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|   ТоварыДокумента.ВидЗапасов КАК ВидЗапасов,
	|	ЕСТЬNULL(ТоварыДокумента.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиНоменклатуры,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	ВЫБОР КОГДА &УчетРасчетовСПоставщикамиПоНД ТОГДА
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиКонтрагента,
	|	&Договор КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК Заказ,
	|
	|	0 КАК Количество,
	|
	|	ТоварыДокумента.СуммаСНДСУпр КАК Сумма,
	|	ТоварыДокумента.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	ТоварыДокумента.СуммаСНДСРегл КАК СуммаРегл,
	|	ТоварыДокумента.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ТоварыДокумента.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) ТОГДА
    |       ТоварыДокумента.СуммаСНДСРегл
	|	ИНАЧЕ
	|		ТоварыДокумента.СуммаБезНДСРегл
	|	КОНЕЦ КАК СтоимостьРегл,
	|	ТоварыДокумента.СуммаСНДСУпр КАК ДопРасходы,
	|	ТоварыДокумента.СуммаБезНДСУпр КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	ТоварыДокумента.СуммаСНДСРегл КАК СуммаВВалютеДокумента,
	|	ТоварыДокумента.СуммаБезНДСРегл КАК СуммаБезНДСВВалютеДокумента,
	|	
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВЫРАЗИТЬ(ТоварыДокумента.СуммаСНДСРегл   * &КоэффициентПересчетаВВалютуВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК СуммаВВалютеВзаиморасчетов,
	|	ВЫРАЗИТЬ(ТоварыДокумента.СуммаБезНДСРегл * &КоэффициентПересчетаВВалютуВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТоварыДокумента.ВидЗапасов
	|		ИНАЧЕ ТоварыДокумента.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	&ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаТовары КАК ТоварыДокумента
	|ГДЕ
	|	&ВыпущеноСТаможни
    |	И НЕ ТоварыДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|	И (ТоварыДокумента.СуммаПошлины <> 0
	|	   ИЛИ ТоварыДокумента.СуммаАкциза <> 0 
	|	   ИЛИ ТоварыДокумента.ТаможенныйСбор <> 0
	|	   ИЛИ ТоварыДокумента.СуммаНДС<>0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияКонтрагентДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РасходыНаТаможенныеСборыШтрафы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|
	|	ВЫБОР КОГДА &УчетРасчетовСПоставщикамиПоНД ТОГДА
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиКонтрагента,
	|	&Договор КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	
	|	ТоварыДокумента.СтатьяРасходов КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	ТоварыДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР КОГДА &УчетЗатратПоНД ТОГДА
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиСтатьи,
	|	
	|	СУММА(ТоварыДокумента.СуммаСНДСУпр) КАК Сумма,
	|	СУММА(ТоварыДокумента.СуммаБезНДСУпр) КАК СуммаБезНДС,
	|	СУММА(ТоварыДокумента.СуммаСНДСРегл) КАК СуммаРегл,
	|	СУММА(ТоварыДокумента.СуммаБезНДСРегл) КАК СуммаРеглБезНДС,
	|	
	|	&Валюта КАК Валюта,
	|	СУММА(ТоварыДокумента.СуммаСНДСРегл) КАК СуммаВВалюте,
	|	СУММА(ТоварыДокумента.СуммаБезНДСРегл) КАК СуммаБезНДСВВалюте,
	|	
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	СУММА(ВЫРАЗИТЬ(ТоварыДокумента.СуммаСНДСРегл * &КоэффициентПересчетаВВалютуВзаиморасчетов КАК ЧИСЛО(15, 2))) КАК СуммаВВалютеВзаиморасчетов,
	|	СУММА(ВЫРАЗИТЬ(ТоварыДокумента.СуммаБезНДСРегл * &КоэффициентПересчетаВВалютуВзаиморасчетов КАК ЧИСЛО(15, 2))) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|	ТоварыДокумента.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
    |	ВтТаблицаТовары КАК ТоварыДокумента
	|ГДЕ
	|	&ВыпущеноСТаможни
	|	И ТоварыДокумента.ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|	И (ТоварыДокумента.СуммаПошлины <> 0
	|	   ИЛИ ТоварыДокумента.СуммаАкциза <> 0 
	|	   ИЛИ ТоварыДокумента.ТаможенныйСбор <> 0
	|	   ИЛИ ТоварыДокумента.СуммаНДС<>0
    |   )
    |
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокумента.СтатьяРасходов,
	|	ТоварыДокумента.АналитикаРасходов,
	|	ТоварыДокумента.ОбъектРасчетов
	|"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;


КонецФункции

Функция ТекстЗапросаТаблицаНДСРеестрПолученныхНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСРеестрПолученныхНалоговыхДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Товары.СтатьяДекларацииНДСНалоговыйКредит КАК СтатьяДекларацииНДСНалоговыйКредит,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(Товары.БазаНДСРегл) КАК СуммаБезНДС,
	|	СУММА(Товары.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ВтТаблицаТовары КАК Товары
	|ГДЕ
	|	&ОрганизацияПлательщикНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.СтатьяДекларацииНДСНалоговыйКредит,
	|	Товары.СтавкаНДС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаНДСУсловныеПродажи(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСУсловныеПродажи";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа) КАК ВидУсловнойПродажи,
	|	Товары.Ссылка КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая) КАК НалоговоеНазначение,
	|	Товары.НалоговоеНазначение КАК НалоговоеНазначениеПоФакту,
	|	0 КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорционально * 100 / Товары.СтавкаНДС.Ставка
	|			ИНАЧЕ Товары.БазаНДСРегл
	|		КОНЕЦ) КАК СтоимостьРегл,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорционально
	|			ИНАЧЕ Товары.СуммаНДС
	|		КОНЕЦ) КАК НДСРегл,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорциональноУпр
	|			ИНАЧЕ Товары.СуммаНДСУпрМУ
	|		КОНЕЦ) КАК НДСУпр
	|ИЗ
	|	ВтТаблицаТовары КАК Товары
	|ГДЕ
	|	НЕ Товары.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Облагаемая)
	|	И &ОрганизацияПлательщикНДС
	|	И Товары.СтавкаНДС.Ставка > 0
	|	И НЕ Товары.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.СтавкаНДС,
	|	Товары.НалоговоеНазначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа),
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.СтавкаНДС,
	|	Товары.КодУКТВЭД,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая),
	|	Товары.НалоговоеНазначение,
	|	Товары.Количество,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорционально * 100 / Товары.СтавкаНДС.Ставка
	|			ИНАЧЕ Товары.БазаНДСРегл
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорционально
	|			ИНАЧЕ Товары.СуммаНДС
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА Товары.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА Товары.СуммаНДСПропорциональноУпр
	|			ИНАЧЕ Товары.СуммаНДСУпрМУ
	|		КОНЕЦ)
	|ИЗ
	|	ВтТаблицаТовары КАК Товары
	|ГДЕ
	|	НЕ Товары.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Облагаемая)
	|	И &ОрганизацияПлательщикНДС
	|	И Товары.СтавкаНДС.Ставка > 0
	|	И Товары.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.СтавкаНДС,
	|	Товары.НалоговоеНазначение,
	|	Товары.КодУКТВЭД,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Количество";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходовСбор          КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовСбор       КАК АналитикаРасходов,
	|   ДанныеДокумента.ВариантОформления        	КАК ВариантОформления,
    |   ДанныеДокумента.НаправлениеДеятельности     КАК НаправлениеДеятельности,
    |   ДанныеДокумента.ОбъектРасчетов     			КАК ОбъектРасчетов,
   	|
	|	ТаблицаТовары.НомерСтроки                   КАК НомерСтроки,
	|	ТаблицаТовары.Ссылка                        КАК Ссылка,
	|	ТаблицаТовары.Номенклатура                  КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                КАК Характеристика,
	|	ТаблицаТовары.Серия                         КАК Серия,
	|	ТаблицаТовары.ДокументПоступления           КАК ДокументПоступления,
	|
	|	ТаблицаТовары.НомерГТД                      КАК НомерГТД,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ТаблицаТовары.СтавкаНДС                     КАК СтавкаНДС,
	|	ТаблицаТовары.СтатьяДекларацииНДСНалоговыйКредит 			КАК СтатьяДекларацииНДСНалоговыйКредит,
	|	ТаблицаТовары.КодУКТВЭД                      КАК КодУКТВЭД,
	
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий 			КАК АналитикаУчетаПартий,
	|	ТаблицаАналитикУчетаПартийДокумента.АналитикаУчетаПартий 	КАК АналитикаУчетаПартийДанныеДокумента,
	|
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	ТаблицаТовары.Склад                         КАК Склад,
	|	ТаблицаТовары.СуммаПошлины                  КАК СуммаПошлины,
    |	ТаблицаТовары.СуммаАкциза                   КАК СуммаАкциза,
    |	ТаблицаТовары.ТаможенныйСбор                КАК ТаможенныйСбор,
	|	ТаблицаТовары.СуммаНДС                      КАК СуммаНДС,
	|	ТаблицаТовары.СуммаНДСПропорционально       КАК СуммаНДСПропорционально,
	|	ТаблицаТовары.НалоговоеНазначение           КАК НалоговоеНазначение,
    |	ТаблицаТовары.НалоговоеНазначение.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
    |	ТаблицаТовары.ХозяйственнаяОперация       	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ХозяйственнаяОперация В
	|			(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию),
	|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссиюИмпорт))
	|			ТОГДА ИСТИНА
	|		КОГДА &ОтнестиСтоимостьТаможеннойОчисткиНаРасходы
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОтнестиСтоимостьТаможеннойОчисткиНаРасходы,
	|
	|	ВЫРАЗИТЬ (((ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможенныйСбор+ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2)) КАК СуммаСНДСУпр,
    |	ВЫРАЗИТЬ (((ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможенныйСбор) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2)) КАК СуммаБезНДСУпр,
    |	ВЫРАЗИТЬ (((ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможенныйСбор+ТаблицаТовары.СуммаНДСПропорционально) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2)) КАК СуммаБезНДСУпрМУ,
    |	ВЫРАЗИТЬ (((ТаблицаТовары.СуммаНДС - ТаблицаТовары.СуммаНДСПропорционально) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2)) КАК СуммаНДСУпрМУ,
    |	ВЫРАЗИТЬ (((ТаблицаТовары.СуммаНДСПропорционально) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2)) КАК СуммаНДСПропорциональноУпр,
    |
    |	ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможенныйСбор+ТаблицаТовары.СуммаНДС КАК СуммаСНДСРегл,
    |	ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможенныйСбор + ТаблицаТовары.СуммаНДСПропорционально КАК СуммаБезНДСРегл,
    |	ТаблицаТовары.СуммаНДС - ТаблицаТовары.СуммаНДСПропорционально КАК СуммаНДСРегл,
    |
    |	ТаблицаТовары.СуммаПошлины+ТаблицаТовары.СуммаАкциза+ТаблицаТовары.ТаможеннаяСтоимость КАК БазаНДСРегл
    |
	|
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаблицаТовары
    |
	|	    ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|		ПО 
	|			ТаблицаТовары.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|			ПО ТаблицаАналитикУчетаПартий.НомерСтроки 	    = ТаблицаТовары.НомерСтроки
	|			И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Товары""
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартийДокумента
	|			ПО ТаблицаАналитикУчетаПартий.НомерСтроки 	    = 0
	|			И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Документ""
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтОснований", ТекстыЗапроса) Тогда
		ТекстЗапросаВтОснований(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация   КАК ХозяйственнаяОперация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоличествоМестХранения = 1
	|			ТОГДА ДанныеДокумента.МестоХранения
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                   КАК МестоХранения,
	|	&Подразделение                          КАК Подразделение,
	|	&Менеджер                               КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Валюта                                 КАК Валюта,
	|	&СуммаДокумента                         КАК Сумма,
	|	&Статус                                 КАК Статус,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ДанныеДокумента.ДополнительнаяЗапись    КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору                   КАК Дополнительно,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерДекларации                        КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете
	|ИЗ
	|	ВтОснований КАК ДанныеДокумента";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтОснований(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтОснований";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(ЕСТЬNULL(ДанныеДокумента.ДокументПоступления.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК МестоХранения,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(ДанныеДокумента.ДокументПоступления.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК КоличествоМестХранения,
	|	ДанныеДокумента.ХозяйственнаяОперация <> ТаблицаОсновныхОпераций.ОсновнаяОперация КАК ДополнительнаяЗапись
	|ПОМЕСТИТЬ ВтОснований
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(ДанныеДокумента.ХозяйственнаяОперация) КАК ОсновнаяОперация
	|		ИЗ
	|			Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ДанныеДокумента
	|		ГДЕ
	|			ДанныеДокумента.Ссылка = &Ссылка) КАК ТаблицаОсновныхОпераций
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.ХозяйственнаяОперация <> ТаблицаОсновныхОпераций.ОсновнаяОперация";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента           = "Документ.ТаможеннаяДекларацияИмпорт";
	СинонимТаблицыДокумента      = "";
	ВЗапросеЕстьИсточник         = Истина;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерДекларации",      """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору", """""");
	ПереопределениеРасчетаПараметров.Вставить("ВыпущеноСТаможни",
		"	ВЫБОР КОГДА ДанныеДокумента.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни) ТОГДА
		|		ИСТИНА
		|	ИНАЧЕ
		|		ЛОЖЬ
		|	КОНЕЦ");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	ЗначенияПараметров.Вставить("ХозОперацииЗакупкаПоИмпорту",
		ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВтОснований", ТекстЗапросаВтОснований(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ТоварыОрганизаций" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ТоварыКОформлениюТаможенныхДеклараций" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТоварыКОформлениюТаможенныхДеклараций",
			ТекстЗапросаВТТоварыКОформлениюТаможенныхДеклараций(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров,
			ТекстыЗапросаВременныхТаблиц);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров,
			ТекстыЗапросаВременныхТаблиц);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры)
	
	#Область Закупка
	
	ТекстЗакупка = "
		|ВЫБРАТЬ
		|	ДанныеШапки.Ссылка                                                 КАК Ссылка,
		|	ДанныеШапки.Дата                                                   КАК ДатаРегистратора,
		|	ДанныеШапки.Номер                                                  КАК НомерРегистратора,
		|	
		|	ДанныеШапки.Партнер                                                КАК Партнер,
		|	ДанныеШапки.Организация                                            КАК Организация,
		|	ДанныеШапки.Контрагент                                             КАК Контрагент,
		|	ДанныеШапки.Договор                                                КАК Договор,
		|	ДанныеШапки.НаправлениеДеятельности                                КАК НаправлениеДеятельности,
		|	
		|	ДанныеШапки.ОбъектРасчетов                                         КАК ОбъектРасчетов,
		|	ДанныеШапки.ДатаПлатежа                                            КАК ДатаПлатежа,
		|	Неопределено                                                       КАК ЗаказЗакупки,
		|	
		|	ДанныеШапки.СуммаДокумента                                         КАК Сумма,
		|	ДанныеШапки.СуммаВзаиморасчетов                                    КАК СуммаВзаиморасчетов,
		|	0                                                                  КАК СуммаВзаиморасчетовПоТаре,
		|	0                                                                  КАК КПоступлению,
		|
		|	ДанныеШапки.ПорядокРасчетов                                        КАК ПорядокРасчетов,
		|	ЛОЖЬ                                                               КАК НакладнаяПоЗаказам,
		|	ДанныеШапки.ВалютаВзаиморасчетов                                   КАК ВалютаВзаиморасчетов,
		|	ДанныеШапки.ВариантОформления                                      КАК ХозяйственнаяОперация,
		|	ДанныеШапки.ФормаОплаты                                            КАК ФормаОплаты,
		|	ДанныеШапки.Валюта                                                 КАК ВалютаДокумента,
		|	ДанныеШапки.Дата                                                   КАК ДатаКурса,
		|	Неопределено                                                       КАК СвязанныйДокумент
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеШапки
		|ГДЕ
		|	ДанныеШапки.Ссылка В (&Ссылка)
		|	И &ВыпущеноСТаможни

		|";
	
	#КонецОбласти
	
	#Область УвеличениеПланаОплаты
	
	ТекстПланОплаты = "
		|	ВЫБРАТЬ
		|	ДанныеШапки.Ссылка                                                 КАК Ссылка,
		|	ДанныеШапки.Дата                                                   КАК ДатаРегистратора,
		|	ДанныеШапки.Номер                                                  КАК НомерРегистратора,
		|	ДанныеШапки.ДатаПлатежа                                            КАК ДатаПлатежа,
		|	
		|	ДанныеШапки.Партнер                                                КАК Партнер,
		|	ДанныеШапки.Организация                                            КАК Организация,
		|	ДанныеШапки.Контрагент                                             КАК Контрагент,
		|	ДанныеШапки.Договор                                                КАК Договор,
		|	ДанныеШапки.НаправлениеДеятельности                                КАК НаправлениеДеятельности,
		|	
		|	ДанныеШапки.ОбъектРасчетов                                         КАК ОбъектРасчетов,
		|	ДанныеШапки.ПорядокРасчетов                                        КАК ПорядокРасчетов,
		|	ЛОЖЬ                                                               КАК НакладнаяПоЗаказам,
		|	ЛОЖЬ                                                               КАК СверхЗаказа,
		|	Неопределено                                                       КАК ЗаказЗакупки,
		|	ДанныеШапки.СуммаДокумента                                         КАК КОплате,
		|	ДанныеШапки.ВалютаВзаиморасчетов                                   КАК ВалютаВзаиморасчетов,
		|	ДанныеШапки.ВариантОформления                                      КАК ХозяйственнаяОперация,
		|	ДанныеШапки.ФормаОплаты                                            КАК ФормаОплаты,
		|	ДанныеШапки.Валюта                                                 КАК ВалютаДокумента,
		|	Неопределено                                                       КАК ВариантОплаты,
		|	Неопределено                                                       КАК СвязанныйДокумент
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеШапки
		|ГДЕ
		|	ДанныеШапки.Ссылка В (&Ссылка)
		|";

	#КонецОбласти
	
	#Область ЗачетАвансов
	
	ТекстЗачетАванса = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                    КАК Ссылка,
		|	
		|	Таблица.ОбъектРасчетов                                            КАК ОбъектРасчетовИсточник,
		|	Таблица.Ссылка.ОбъектРасчетов                                     КАК ОбъектРасчетовПриемник,
		|	
		|	Таблица.Ссылка.Партнер                                            КАК Партнер,
		|	Таблица.Ссылка.Организация                                        КАК Организация,
		|	Таблица.Ссылка.Контрагент                                         КАК Контрагент,
		|	Таблица.Ссылка.Договор                                            КАК Договор,
		|	Таблица.Ссылка.НаправлениеДеятельности                            КАК НаправлениеДеятельности,
		|
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                               КАК ВалютаВзаиморасчетов,
		|	Таблица.СуммаВзаиморасчетов                                       КАК СуммаВзаиморасчетов,
		|	Таблица.Ссылка.Валюта                                             КАК ВалютаДокумента,
		|	Таблица.Сумма                                                     КАК Сумма,
		|
		|	Таблица.Ссылка.Дата                                               КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                              КАК НомерРегистратора,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)        КАК ХозяйственнаяОперация
		|	
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт.РасшифровкаПлатежа КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|";
	
	#КонецОбласти
	
	ВзаиморасчетыСервер.ПроведениеЗакупки(Запрос, ТекстыЗапроса, Регистры, ТекстЗакупка, ТекстПланОплаты, ТекстЗачетАванса);
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьПоУмолчанию(Объект) Экспорт
	Объект.Менеджер = Пользователи.ТекущийПользователь();
	Объект.Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Объект.Менеджер, Объект.Подразделение);
	
	Объект.Валюта = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Объект.Валюта);
	Объект.ВалютаВзаиморасчетов = Объект.Валюта;
	
	Объект.Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Объект.Организация);
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    		= Объект.Организация;
	СтруктураПараметров.БанковскийСчет		= Объект.БанковскийСчетОрганизации;
	Объект.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	ВалютаОплаты  = ДенежныеСредстваСервер.ПолучитьВалютуОплаты(Объект.ФормаОплаты, Объект.БанковскийСчетОрганизации);
	Объект.ОплатаВВалюте = ВзаиморасчетыСервер.ПолучитьОплатуВВалютеПоУмолчанию(ВалютаОплаты);
	
КонецПроцедуры

Процедура ЗаполнитьПоУсловиямЗакупок(Объект, УсловияЗакупок) Экспорт
	ЗаполнитьЗначенияСвойств(Объект, УсловияЗакупок, "ФормаОплаты, ГруппаФинансовогоУчета, НаправлениеДеятельности, ОплатаВВалюте");
	Если Не ЗначениеЗаполнено(Объект.ВариантОформления) Тогда
		Объект.ВариантОформления = Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Партнер) Тогда
		Объект.Партнер = УсловияЗакупок.Партнер;
	КонецЕсли;
	Если ЗначениеЗаполнено(УсловияЗакупок.Организация) И УсловияЗакупок.Организация<>Объект.Организация Тогда
		Объект.Организация = УсловияЗакупок.Организация;
	КонецЕсли;
	Если ЗначениеЗаполнено(УсловияЗакупок.Контрагент) И УсловияЗакупок.Контрагент<>Объект.Контрагент Тогда
		Объект.Контрагент = УсловияЗакупок.Контрагент;
	КонецЕсли;
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
	
	Если УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов <> Неопределено И УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
		
		ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
		ДопПараметры.ВалютаВзаиморасчетов = Объект.ВалютаВзаиморасчетов;
		Объект.Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(
			Объект, ОперацииОтбораСоглашенийДоговоров(), ДопПараметры);
		ЗакупкиВызовСервера.ЗаполнитьБанковскиеСчетаПоДоговору(Объект.Договор, Объект.БанковскийСчетОрганизации, Объект.БанковскийСчетКонтрагента);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности") 
			Или ПолучитьФункциональнуюОпцию("ИспользоватьУчетРасчетовСПоставщикамиПоНаправлениямДеятельности") Тогда
			НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, Объект.Соглашение, Объект.Договор);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеДатыПлатежа = ЗакупкиСервер.ПолучитьПоследнююДатуПоГрафику(Объект.Дата, УсловияЗакупок.Соглашение);
	Если ЗначениеЗаполнено(ЗначениеДатыПлатежа) Тогда
		Объект.ДатаПлатежа = ЗначениеДатыПлатежа;
	КонецЕсли;
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    			= Объект.Организация;
	СтруктураПараметров.БанковскийСчет			= Объект.БанковскийСчетОрганизации;
	СтруктураПараметров.НаправлениеДеятельности	= Объект.НаправлениеДеятельности;
	Объект.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	Объект.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Объект.Контрагент, , Объект.БанковскийСчетКонтрагента);
КонецПроцедуры

Процедура ЗаполнитьПоПартнеру(Объект, Партнер) Экспорт
	УсловияЗакупок = Неопределено;
	
	Если Не ЗначениеЗаполнено(Объект.Партнер) Или (Объект.Партнер <> Партнер) Тогда
		Объект.Партнер = Партнер;
	КонецЕсли;
	Если (Объект.ВариантОформления <> Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно) Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ТолькоДляЗакупки",      Ложь);
		ПараметрыОтбора.Вставить("ХозяйственныеОперации", ОперацииОтбораСоглашенийДоговоров());
		ПараметрыОтбора.Вставить("ВыбранноеСоглашение",   Объект.Соглашение);
		
		УсловияЗакупок =
			ЗакупкиСервер.ПолучитьУсловияЗакупокПоУмолчанию(
				Объект.Партнер,
				ПараметрыОтбора);
	КонецЕсли;
	Если ЗначениеЗаполнено(УсловияЗакупок) Тогда
		Если Объект.Соглашение <> УсловияЗакупок.Соглашение
			И ЗначениеЗаполнено(УсловияЗакупок.Соглашение) Тогда
			
			Объект.Соглашение = УсловияЗакупок.Соглашение;
			ЗаполнитьПоУсловиямЗакупок(Объект, УсловияЗакупок);
		Иначе
			Объект.Соглашение = УсловияЗакупок.Соглашение;
		КонецЕсли;
	Иначе
		Контрагент = Объект.Контрагент;
		ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
		БанковскийСчет = ?(Контрагент = Объект.Контрагент, Объект.БанковскийСчетКонтрагента, Неопределено);
		Объект.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Объект.Контрагент, , БанковскийСчет);
		Объект.Соглашение = Неопределено;
	КонецЕсли;
КонецПроцедуры

// Возвращаемое значение:
// 	Структура - содержит:
// * НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности -
// * Товары - ТабличнаяЧасть, Неопределено -
// * ЦенаВключаетНДС - Булево -
// * ВалютаПоступления - СправочникСсылка.Валюты -
// * ДатаПоступления - Неопределено -
// * КонтрагентПоставщика - СправочникСсылка.Контрагенты -
// * Поставщик - СправочникСсылка.Партнеры -
// * Организация - СправочникСсылка.Организации -
Функция СтруктураЗаполнения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("Поставщик", Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("КонтрагентПоставщика", Справочники.Контрагенты.ПустаяСсылка());
	Результат.Вставить("СоглашениеПоставщика", Справочники.СоглашенияСПоставщиками.ПустаяСсылка());
	Результат.Вставить("ДатаПоступления", Неопределено);
	Результат.Вставить("ВалютаПоступления", Справочники.Валюты.ПустаяСсылка());
	Результат.Вставить("ЦенаВключаетНДС", Ложь);
	Результат.Вставить("Товары", Неопределено);
	Результат.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	
	Возврат Результат;
КонецФункции

// Параметры:
// 	Объект - ДокументОбъект.ТаможеннаяДекларацияИмпорт - 
// 	Данные - см. СтруктураЗаполнения
Процедура ЗаполнитьПоДанным(Объект, Данные) Экспорт
	ЗаполнитьЗначенияСвойств(Объект, Данные);
	ЗаполнитьПоУмолчанию(Объект);
	Если Данные.Товары<>Неопределено Тогда
		Если Ложь <> Данные.ЦенаВключаетНДС Тогда
			Для Каждого Товар Из Данные.Товары Цикл // в поле Сумма получим сумму без НДС
				Товар.СуммаСНДС = Товар.СуммаСНДС - Товар.СуммаНДС;
				Товар.Сумма = Товар.СуммаСНДС;
			КонецЦикла;
		КонецЕсли;
		Если Объект.Валюта <> Данные.ВалютаПоступления И ЗначениеЗаполнено(Данные.ВалютаПоступления) Тогда
			ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата),Объект.Дата,ТекущаяДатаСеанса());
			КурсыСтарые = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Данные.ВалютаПоступления, ДатаДокумента);
			КурсыНовые  = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.Валюта, ДатаДокумента);
			Ценообразование.ПересчитатьСуммыТабличнойЧастиВВалюту(
				Данные.Товары, Истина, Данные.ВалютаПоступления, Объект.Валюта, КурсыСтарые, КурсыНовые);
		КонецЕсли;
		
		Для каждого ЭлементБуфера Из Данные.Товары Цикл
			Товар = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(Товар, ЭлементБуфера,
				"Номенклатура, Характеристика, Серия, Склад, Назначение, ВидЗапасов, Упаковка, Количество, КоличествоУпаковок, ДокументПоступления, НалоговоеНазначение, ДоговорПоставщика, ВалютаПоставщика, КодУКТВЭД, СтавкаНДС");
			Товар.ХозяйственнаяОперация = ЭлементБуфера.ХозяйственнаяОперация;
			Товар.ТаможеннаяСтоимость = ЭлементБуфера.Сумма;
			Товар.СтатьяДекларацииНДСНалоговыйКредит = ПолучитьСтатьюНалоговойДекларации(Товар.СтавкаНДС);
		КонецЦикла;
		

		КоэффициентПропорциональногоОтнесенияНДСНаОбязательства = НДСОбщегоНазначенияПовтИсп.ПолучитьКоэффициентПропорциональногоНДСОбязательтсва(Объект.Организация, НачалоГода(Объект.Дата));

		Для каждого Строка Из Объект.Товары Цикл
			
			Строка.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
				Строка.ТаможеннаяСтоимость + Строка.СуммаПошлины,
				Строка.СтавкаНДС,
				Ложь);
				
			Если Строка.НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально") Тогда
				Строка.СуммаНДСПропорционально = Строка.СуммаНДС * КоэффициентПропорциональногоОтнесенияНДСНаОбязательства;
			Иначе
				Строка.СуммаНДСПропорционально = 0;
			КонецЕсли;
			
		КонецЦикла;
			
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРазделыТаможеннойДекларацииИмпорт") Тогда
			
			НомерРаздела = 1;
			СтруктураПоиска = Новый Структура("СтавкаНДС, КодУКТВЭД");
			
			ТаблицаТоваров = Объект.Товары.Выгрузить(, "СтавкаНДС, КодУКТВЭД, ТаможеннаяСтоимость, СуммаПошлины, СуммаНДС");
			ТаблицаТоваров.Свернуть("СтавкаНДС, КодУКТВЭД", "ТаможеннаяСтоимость, СуммаПошлины, СуммаНДС");
			Для каждого СтрокаТовары Из ТаблицаТоваров Цикл
				
				Раздел = Объект.Разделы.Добавить();
				
				ЗаполнитьЗначенияСвойств(Раздел, СтрокаТовары);
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТовары);
				НайденныеСтроки = Объект.Товары.НайтиСтроки(СтруктураПоиска);
				Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					НайденнаяСтрока.НомерРаздела = НомерРаздела;
				КонецЦикла;
				
				Раздел.НомерРаздела = НомерРаздела;
				НомерРаздела = НомерРаздела + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьСтатьюНалоговойДекларации(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат Справочники.СтатьиНалоговыхДеклараций.ПустаяСсылка();
	КонецЕсли;
	
	СтавкаНДСПеречисление = СтавкаНДС.ПеречислениеСтавкаНДС;
		
	Если СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС0
		  ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7
          ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС14
	      ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС20 Тогда
		// ст 12.1 Декларации
		Возврат Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРОблагНДСТамож;
	Иначе
		// ст 12.3 Декларации
		Возврат Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРОблагБезНДС;
	КонецЕсли; 

КонецФункции

#КонецОбласти

#Область Прочее

// Возвращаемое значение:
// 	Массив - массив операций, соглашения и договоры по которым доступны к использованию.
Функция ОперацииОтбораСоглашенийДоговоров() Экспорт
	Операции = Новый Массив;
	Операции.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	Возврат Операции;
КонецФункции

// Параметры:
// 	Ссылка - см. ОбщегоНазначения.ЗначениеРеквизитаОбъекта.Ссылка
// 	ИмяРеквизита - см. ОбщегоНазначения.ЗначениеРеквизитаОбъекта.ИмяРеквизита
// Возвращаемое значение:
// 	Булево, Произвольный - значение реквизита, прочитанного из информационной базы по ссылке на объект
//  		см. ОбщегоНазначения.ЗначениеРеквизитаОбъекта(). Если полученное значение не имеет тип булево, 
//  		возвращается значение Ложь.
//
Функция ЗначениеРеквизитаОбъектаТипаБулево(Ссылка, ИмяРеквизита) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	Если ТипЗнч(Результат) <> Тип("Булево") Тогда
		Результат = Ложь;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);	
	Возврат Результат
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеЗакупок

Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	ЕстьПравоНаЧтениеПоступленийТоваровУслуг = ПравоДоступа("Чтение", Метаданные.Документы.ПриобретениеТоваровУслуг);
	Если Не (ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКОформлениюТаможенныхДеклараций)
		И ЕстьПравоНаЧтениеПоступленийТоваровУслуг) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры.Вставить("МассивОрганизаций",?(ЗначениеЗаполнено(Параметры.Организация), 
											ОбщегоНазначенияУТКлиентСервер.Массив(Параметры.Организация),
											Неопределено));
	Параметры.Вставить("БезОграниченияПериода");
	
	ТекстГиперссылки = НСтр("ru='ТД импорт';uk='МД імпорт'");
	
	Если ЕстьНакладныеКОформлению(Параметры) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,,
			ИмяФормыРабочееМестоТаможеннаяДекларацияИмпорт());
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,,
			ИмяФормыРабочееМестоТаможеннаяДекларацияИмпорт());
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция ИмяФормыРабочееМестоТаможеннаяДекларацияИмпорт() Экспорт
	
	Возврат "Документ.ТаможеннаяДекларацияИмпорт.Форма.РабочееМесто";
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ТаможеннаяДекларацияИмпорт.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";  
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ec673d5c-3b26-4068-af11-d7eb39591ba1");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ТаможеннаяДекларацияИмпорт.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет в табличной части ""Товары"" реквизит ""Хозяйственная операция"".
                                   |Очищает ошибочно установленные серии и статусы указания серий.
                                   |Заполняет реквизит ""Назначение"" в табличной части Товары, перезаполняет реквизит ""Аналитика учета номенклатуры"".
                                   |Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС. 
                                   |Заполняет признак Оплата в иностранной валюте. 
                                   |Заполняет реквизит ОбъектРасчетов.'
                                   |;uk='Заповнює у табличній частині ""Товари"" реквізит ""Господарська операція"".
                                   |Очищає помилково встановлені серії та статуси серії.
                                   |Заповнює реквізит ""Призначення"" у табличній частині Товари, перезаповнює реквізит ""Аналітика обліку номенклатури"".
                                   |Заповнює реквізит СтавкаНДС з типом СправочникСсылка.СтавкиНДС.
                                   |Заповнює ознаку Оплата в іноземній валюті.
                                   |Заповнює реквізит ОбъектРасчетов.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.БанковскиеСчетаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.КлючиАналитикиУчетаНоменклатуры.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Назначения.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СоглашенияСПоставщиками.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаявкаНаРасходованиеДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РасходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ГруппыФинансовогоУчетаРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.КлючиАналитикиУчетаНоменклатуры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";           
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";                        
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию2";
	НоваяСтрока.Порядок = "После";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыКОформлениюТаможенныхДеклараций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Партнеры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";         
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.СгенерироватьДокументыДляПереброскиОстатковСПустогоНазначенияПоДавальческойСхеме";
	НоваяСтрока.Порядок = "Любой";
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);                       
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьКлючиАналитикиНоменклатуры(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);	                
	
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыОбработатьДанныеДляГенерацииНазначений(
		Обработчик.ПриоритетыВыполнения,
		"После"
	);

#КонецОбласти

#Область СгенерироватьКлючиАналитикиНоменклатуры

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.ТаможеннаяДекларацияИмпорт.СгенерироватьКлючиАналитикиНоменклатуры";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ТаможеннаяДекларацияИмпорт.ЗарегистрироватьДанныеКОбновлениюКлючейАналитики";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("900ac886-6148-4f90-baab-1e4ffbc457b4");
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ЧитаемыеОбъекты = "Документ.ТаможеннаяДекларацияИмпорт,"
		+ "Справочник.КлючиАналитикиУчетаНоменклатуры,"
		+ "Справочник.Назначения";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.АналитикаУчетаНоменклатуры,"
		+ "Справочник.КлючиАналитикиУчетаНоменклатуры";
	Обработчик.БлокируемыеОбъекты = "Справочник.КлючиАналитикиУчетаНоменклатуры,"
		+ "РегистрСведений.АналитикаУчетаНоменклатуры";
	Обработчик.Комментарий = НСтр("ru='Генерирует ключи аналитики учета номенклатуры по данным документа ""Таможенная декларация (импорт)"". До завершения обработчика работа с документами не возможна.';uk='Генерує ключі аналітики обліку номенклатури за даними документа ""Митна декларація (імпорт)"". До завершення обробника робота з документами неможлива.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.КорректировкаНазначенияТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.КлючиАналитикиУчетаНоменклатуры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеЗатратНаВыпуск.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПеремещениеТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаВыпускПродукции.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТКА
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаМатериаловВПроизводство.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	
	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаДавальцу.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТКА
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратСырьяОтПереработчика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СборкаТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатков.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииНезавершенногоПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииПроизводственныхЗатрат.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ВыручкаИСебестоимостьПродаж.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ТаможеннаяДекларацияИмпорт.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.МаршрутныйЛистПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТКА
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратМатериаловИзПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.СтоимостьТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеНедостачТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеОтПереработчика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	
	


	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияТоваровУслуг.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаСписаниеПоНормативам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриобретениеТоваровУслуг.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.СебестоимостьТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВнутреннееПотреблениеТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.МатериалыИРаботыВПроизводстве.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииЗатратНаВыпуск.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";


	


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровПоставщику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";



	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПересортицаТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПорчаТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаТоваровМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеСырьяОтДавальца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТКА
	
	//++ НЕ УТ  
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВыпускПродукции.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ



	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ВыпускПродукции.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизацийКПередаче.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТрудозатратыНезавершенногоПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииТоваровОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДвиженияНоменклатураНоменклатура.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";


	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратСырьяДавальцу.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТКА

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизацийКОформлению.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровОтКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОприходованиеИзлишковТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаСырьяПереработчику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ
	
	


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПрочееОприходованиеТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.Закупки.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	
	//++ НЕ УТ
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	//-- НЕ УТ


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Партнеры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";         
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Документы.ТаможеннаяДекларацияИмпорт";
	НоваяСтрока.Порядок    = "Удалить"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьКлючиАналитикиНоменклатуры(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения
	);	                        
	
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыОбработатьДанныеДляГенерацииНазначений(
		Обработчик.ПриоритетыВыполнения,
		"Любой"
	);

#КонецОбласти

#Область СгенерироватьОбъектыРасчетов

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ТаможеннаяДекларацияИмпорт.СгенерироватьОбъектыРасчетов";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d2df5ac3-04b7-4ae1-bd54-0e3d672269f6");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ТаможеннаяДекларацияИмпорт.ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Генерирует объекты справочника  Объекты расчетов на основании данных документа.';uk='Генерує об''єкти довідника Об''єкти розрахунків на підставі даних документа.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ТаможеннаяДекларацияИмпорт.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Документы.ТаможеннаяДекларацияИмпорт";
	НоваяСтрока.Порядок    = "Удалить"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения
	);	

#КонецОбласти

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ТаможеннаяДекларацияИмпорт";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОбработке.Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаможеннаяДекларацияИмпорт.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
	|	ГДЕ
	|		ТаможеннаяДекларацияИмпорт.УдалитьПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Товары.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Товары.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|	ГДЕ
	|		Ключи.Назначение <> Товары.ВидЗапасов.УстарелоНазначение
	|		И Товары.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Товары.Ссылка.Проведен
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Товары.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Товары.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|	ГДЕ
	|		Ключи.Назначение <> Товары.Назначение
	|		И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Товары.Ссылка.Проведен
	|
	|	) КАК КОбработке
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
	|ГДЕ
	|	Товары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|	ИЛИ Товары.АналитикаУчетаНоменклатуры.МестоХранения ССЫЛКА Справочник.Партнеры";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаможеннаяДекларацияИмпорт.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
	|ГДЕ 
	|	// Переход на ставку НДС справочником
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.ТаможеннаяДекларацияИмпорт.Разделы КАК ТаможеннаяДекларацияИмпортРазделы
	|		ГДЕ
	|			ТаможеннаяДекларацияИмпорт.Ссылка = ТаможеннаяДекларацияИмпортРазделы.Ссылка
	|			И ТаможеннаяДекларацияИмпортРазделы.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|			И ТаможеннаяДекларацияИмпортРазделы.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))
	|	
	|	
	|	// Необходимо заполнение реквизита ИспользоватьРазделы
	|	ИЛИ (НЕ Ссылка.ИспользоватьРазделы
	|			И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.ТаможеннаяДекларацияИмпорт.Разделы КАК ТаможеннаяДекларацияИмпортРазделы
	|		ГДЕ
	|			ТаможеннаяДекларацияИмпорт.Ссылка = ТаможеннаяДекларацияИмпортРазделы.Ссылка))
	|	
	|	// Необходимо заполнение новых реквизитов СтавкаНДС и СтавкаПошлины на основании соответствующих полей из разделов
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
	|		ГДЕ
	|			ТаможеннаяДекларацияИмпорт.Ссылка = ТаможеннаяДекларацияИмпортТовары.Ссылка
	|			И ТаможеннаяДекларацияИмпортТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))
	|	// Требуется заполнение реквизита ОбъектРасчетов
	|	ИЛИ (ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.ТаможеннаяДекларацияИмпорт.РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
	|			ГДЕ
	|				ТЧРасшифровкаПлатежа.Ссылка = ТаможеннаяДекларацияИмпорт.Ссылка
	|				И ТЧРасшифровкаПлатежа.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|				И ТЧРасшифровкаПлатежа.УдалитьЗаказ НЕ В (&ПустыеЗначенияОбъектРасчетов)))
	|	ИЛИ
	|		ТаможеннаяДекларацияИмпорт.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|";
	
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());

	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;  
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка
	|ПОМЕСТИТЬ ВТОбъектыДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;           
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка                      КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных         КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Ссылка.Проведен             КАК Проведен,
	|	ВЫБОР 
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.Договор.УдалитьПорядокОплаты
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСКлиентами.ПустаяСсылка)
	|			И НЕ ОбъектыДляОбработки.Ссылка.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.Соглашение.УдалитьПорядокОплаты
	|		ИНАЧЕ	
	|			ВЫБОР 
	|				КОГДА ОбъектыДляОбработки.Ссылка.ВалютаВзаиморасчетов = &ВалютаРеглУчета 
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах)
	|			КОНЕЦ
	|	КОНЕЦ                                           КАК УдалитьПорядокОплаты,
	|	ВЫБОР
	|		КОГДА НЕ ОбъектыДляОбработки.Ссылка.БанковскийСчетОрганизации = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|			ТОГДА ОбъектыДляОбработки.Ссылка.БанковскийСчетОрганизации.ВалютаДенежныхСредств
	|		ИНАЧЕ Значение(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ                                           КАК ВалютаОплаты,
	|	ОбъектыДляОбработки.Ссылка.Дата                 КАК Дата,
	|	ОбъектыДляОбработки.Ссылка.Валюта               КАК Валюта,
	|	ОбъектыДляОбработки.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ОбъектыДляОбработки.Ссылка.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|	ОбъектыДляОбработки.Ссылка.СуммаДокумента       КАК СуммаДокумента,
	|	ОбъектыДляОбработки.Ссылка.Статус               КАК Статус
	|
	|ПОМЕСТИТЬ ТаблицаСсылок
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка               КАК Ссылка,
	|	ДанныеДляОбработки.ВерсияДанных         КАК ВерсияДанных, 
	|	ДанныеДляОбработки.Проведен             КАК Проведен,
	|	ВЫБОР КОГДА ДанныеДляОбработки.ВалютаОплаты = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)  
	|   	ТОГДА
	|	    	ВЫБОР КОГДА ДанныеДляОбработки.УдалитьПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)  
	|           	ТОГДА   
	|	    			ВЫБОР КОГДА ДанныеДляОбработки.Валюта = &ВалютаРеглУчета 
	|           			ТОГДА   
	|               			ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|						ИНАЧЕ
	|							ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах) 
	|           		КОНЕЦ
	|				ИНАЧЕ
	|               	ДанныеДляОбработки.УдалитьПорядокОплаты
	|           КОНЕЦ
	|   	ИНАЧЕ
	|	    	ВЫБОР КОГДА ДанныеДляОбработки.Валюта = &ВалютаРеглУчета 
	|           	ТОГДА   
	|               	ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВГривнахОплатаВГривнах)
	|				ИНАЧЕ
	|               	ВЫБОР КОГДА ДанныеДляОбработки.ВалютаОплаты = &ВалютаРеглУчета 
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВГривнах) 
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте) 
	|					КОНЕЦ 
	|           КОНЕЦ
	|   КОНЕЦ КАК УдалитьПорядокОплаты,
	|	ДанныеДляОбработки.Валюта               КАК ВалютаДокумента,
	|	ДанныеДляОбработки.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДляОбработки.ВалютаОплаты         КАК ВалютаОплаты,
	|	ДанныеДляОбработки.СуммаДокумента       КАК СуммаДокумента,
	|	ДанныеДляОбработки.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|	(ДанныеДляОбработки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни))
	|		И ТЧТовары.АналитикаУчетаНоменклатуры.МестоХранения ССЫЛКА Справочник.Партнеры КАК ЗаполнитьАналитикуНоменклатуры,
	|	ТЧТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ЗаполнитьХозяйственнуюОперацию
	|ИЗ
	|	ТаблицаСсылок КАК ДанныеДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТЧТовары
	|		ПО ДанныеДляОбработки.Ссылка = ТЧТовары.Ссылка
	|";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	
	Документ = Запрос.Выполнить().Выбрать();
	
	Пока Документ.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект(); // ДокументОбъект
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
					Если ЗначениеЗаполнено(СтрокаТовары.ВидЗапасов.УстарелоНазначение) Тогда
						СтрокаТовары.Назначение = СтрокаТовары.ВидЗапасов.УстарелоНазначение;
						ОбъектИзменен = Истина;
					ИначеЕсли ЗначениеЗаполнено(СтрокаТовары.АналитикаУчетаНоменклатуры.Назначение) Тогда
						СтрокаТовары.Назначение = СтрокаТовары.АналитикаУчетаНоменклатуры.Назначение;
						ОбъектИзменен = Истина;
					КонецЕсли;
				КонецЦикла;
		
				ПараметрыЗаполненияКлючей = РегистрыСведений.АналитикаУчетаНоменклатуры.ПараметрыЗаполненияКлючейАналитики();
				МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(
					ДокументОбъект.ВариантОформления, 
					Неопределено, 
					ДокументОбъект.Подразделение, 
					Неопределено
				);
				ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
				ИменаПолей.Произвольный = "Склад";
				РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(
					ДокументОбъект.Товары, 
					МестаУчета, 
					ИменаПолей, 
					ПараметрыЗаполненияКлючей
				);
				
				ОбъектИзменен = ОбъектИзменен ИЛИ ПараметрыЗаполненияКлючей.ИзмененаАналитика;				
				
				Для Каждого Строка Из ДокументОбъект.Товары Цикл
					Если Документ.ЗаполнитьХозяйственнуюОперацию Тогда
						Строка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
						ОбъектИзменен = Истина;
					КонецЕсли;
				КонецЦикла;
				Если Документ.Проведен
					И Документ.ЗаполнитьАналитикуНоменклатуры Тогда
					
					ИтоговаяТЧ = ДокументОбъект.Товары.ВыгрузитьКолонки();
					
					ПоляОтбора = "ХозяйственнаяОперация, ДокументПоступления";
					ДанныеДляЗаполненияАналитик = ДокументОбъект.Товары.Выгрузить(, ПоляОтбора);
					ДанныеДляЗаполненияАналитик.Свернуть(ПоляОтбора);
					
					Для Каждого ПоляГруппТоваров Из ДанныеДляЗаполненияАналитик Цикл
						
						ПараметрыОтбора = Новый Структура(ПоляОтбора,
							ПоляГруппТоваров.ХозяйственнаяОперация,
							ПоляГруппТоваров.ДокументПоступления
						);
							
						НайденныеСтроки = ДокументОбъект.Товары.НайтиСтроки(ПараметрыОтбора);
						ТаблицаДляОбработки = ДокументОбъект.Товары.Выгрузить(НайденныеСтроки);
						
						ТаблицаДляОбработки.Колонки.Добавить("Партнер", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
						ТаблицаДляОбработки.ЗаполнитьЗначения(ДокументОбъект.Поставщик, "Партнер");
						
						ДоговорПоставщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоляГруппТоваров.ДокументПоступления, "Договор");
						ТаблицаДляОбработки.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
						ТаблицаДляОбработки.ЗаполнитьЗначения(ДоговорПоставщика, "Договор");
						
						МенеджерАналитики = РегистрыСведений.АналитикаУчетаНоменклатуры;
						МестаУчета = МенеджерАналитики.МестаУчета(
							ДокументОбъект.ВариантОформления,
							Неопределено,
							ДокументОбъект.Подразделение,
							Неопределено,
							Неопределено
						);
						
						РасчетИменПолей = МенеджерАналитики.МестаУчета(
							ПоляГруппТоваров.ХозяйственнаяОперация,
							"Склад",
							ДокументОбъект.Подразделение,
							"Партнер",
							"Договор"
						);
						
						ИменаПолей = МенеджерАналитики.ИменаПолейКоллекцииПоУмолчанию();
						ИменаПолей.Произвольный = РасчетИменПолей.Произвольный;
						
						// Метод ЗаполнитьВКоллекции использует номера строк как индексы, подготовка номеров строк.
						Для НомерСтроки = 1 По ТаблицаДляОбработки.Количество() Цикл
							ОбрабатываемаяСтрока = ТаблицаДляОбработки[НомерСтроки - 1];
							ОбрабатываемаяСтрока.НомерСтроки = НомерСтроки;
						КонецЦикла;
						
						МенеджерАналитики.ЗаполнитьВКоллекции(ТаблицаДляОбработки, МестаУчета, ИменаПолей);
						
						ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДляОбработки, ИтоговаяТЧ);
						
					КонецЦикла;
					
					ДокументОбъект.Товары.Загрузить(ИтоговаяТЧ);
					
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				
				// Переход на ставку НДС справочником.
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("Разделы");
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				// Заполнение реквизита ОплатаВВалюте.
				Если НЕ ЗначениеЗаполнено(ДокументОбъект.УдалитьПорядокОплаты) Тогда
					ДокументОбъект.ОплатаВВалюте = (Документ.УдалитьПорядокОплаты = Перечисления.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте);
					ДокументОбъект.УдалитьПорядокОплаты = Документ.УдалитьПорядокОплаты;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				// Заполнение реквизита ИспользоватьРазделы.
				Если ДокументОбъект.Разделы.Количество() > 0 Тогда
					ДокументОбъект.ИспользоватьРазделы = Истина;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				// Заполнение в тч Товары реквизитов СтавкаНДС, СтавкаПошлины, СтавкаАкциза, КодУКТВЭД на основании соответствующих полей из разделов.
				СтрокаТаблицыРазделов = Неопределено;
				Для каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
					Если Не ЗначениеЗаполнено(СтрокаТовары.СтавкаНДС) Тогда
						Если СтрокаТаблицыРазделов = Неопределено Или СтрокаТаблицыРазделов.НомерРаздела <> СтрокаТовары.НомерРаздела Тогда
							СтрокаТаблицыРазделов = ДокументОбъект.Разделы.Найти(СтрокаТовары.НомерРаздела, "НомерРаздела");
						КонецЕсли;
						Если СтрокаТаблицыРазделов <> Неопределено Тогда
							ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрокаТаблицыРазделов, "СтавкаНДС, СтавкаПошлины, СтавкаАкциза, КодУКТВЭД");
							ОбъектИзменен = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				// Заполнение объектов расчета
				ОбъектыРасчетовСервер.ЗаполнитьОбъектыРасчетов(ДокументОбъект, ОбъектИзменен);
				
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

// Регистрирует данные для обработчика обновления СгенерироватьКлючиАналитикиНоменклатуры
//
Процедура ЗарегистрироватьДанныеКОбновлениюКлючейАналитики(Параметры) Экспорт
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаНоменклатуры.ТекстЗапросаГенерацииКлючейКОбработке()
	+ "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтКлючиКГенерации КАК КлючиКГенерации
	|	ПО КлючиКГенерации.Ссылка = Товары.АналитикаУчетаНоменклатуры
	|		И КлючиКГенерации.Назначение = Товары.ВидЗапасов.УстарелоНазначение
	|ГДЕ
	|	Товары.АналитикаУчетаНоменклатуры.Назначение <> Товары.ВидЗапасов.УстарелоНазначение
	|	И Товары.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Товары.Ссылка.Проведен
	|	И КлючиКГенерации.Ссылка ЕСТЬ NULL
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоРегистратору", " ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПоместитьВТ", "ПОМЕСТИТЬ ВтКлючиКГенерации");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Справочники.КлючиАналитикиУчетаНоменклатуры.УстановитьПараметрыЗапросаКлючей(Запрос);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

// Обработчик обновления документа, по табличным частям генерирует недостающие ключи аналитики учета номенклатуры в ИБ.
Процедура СгенерироватьКлючиАналитикиНоменклатуры(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ТаможеннаяДекларацияИмпорт";
	МетаданныеДокумента = Метаданные.Документы.ТаможеннаяДекларацияИмпорт;
	
	ДополнительныеПараметрыВыборкиДанных = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыВыборкиДанных.ВыбиратьПорциями = Ложь;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяДокумента, ДополнительныеПараметрыВыборкиДанных);
	Пока Выборка.Следующий() Цикл
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Поля.Номенклатура,
		|	Поля.Характеристика,
		|	Поля.Серия,
		|	Поля.МестоХранения,
		|	Товары.Назначение КАК Назначение,
		|	Поля.СтатьяКалькуляции
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Товары
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Поля
		|	ПО Поля.Ссылка = Товары.АналитикаУчетаНоменклатуры
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|	ПО Поля.Номенклатура = Аналитика.Номенклатура
		|		И Поля.Характеристика = Аналитика.Характеристика
		|		И Поля.Серия = Аналитика.Серия
		|		И Поля.МестоХранения = Аналитика.МестоХранения
		|		И Товары.ВидЗапасов.УстарелоНазначение = Аналитика.Назначение
		|		И Поля.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|ГДЕ
		|	Товары.АналитикаУчетаНоменклатуры.Назначение <> Товары.ВидЗапасов.УстарелоНазначение
		|	И Товары.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И Товары.Ссылка.Проведен
		|	И Товары.Ссылка = &Ссылка
		|	И Аналитика.КлючАналитики ЕСТЬ NULL
		|");
		
		Запрос.УстановитьПараметр("Ссылка", Выборка.Ссылка);
		
		Попытка
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяДокумента);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось заблокировать документ: %Ссылка% по причине: %Причина%';uk='Не вдалося заблокувати документ: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеДокумента,
				Выборка.Ссылка,
				ТекстСообщения
			);
			Продолжить;
		КонецПопытки;
		
		Ключи = Запрос.Выполнить().Выбрать();
		Пока Ключи.Следующий() Цикл
			Попытка
				РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Ключи, Истина);
			Исключение
				ТекстСообщения = НСтр("ru='Не удалось инициализировать ключ аналитики учета номенклатуры: %Ключ% по причине: %Причина%';uk='Не вдалося ініціалізувати ключ аналітики обліку номенклатури: %Ключ% по причині: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ключ%", Выборка.Ссылка);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Предупреждение,
					МетаданныеДокумента, 
					ТекстСообщения
				);
				КонецПопытки;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
		ЗафиксироватьТранзакцию();
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяДокумента);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКГенерацииОбъектовРасчетов(Параметры) Экспорт

	ОбъектыРасчетовСервер.ЗарегистрироватьДанныеДляГенерацииОбъектовРасчета(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

Процедура СгенерироватьОбъектыРасчетов(Параметры) Экспорт
	
	ОбъектыРасчетовСервер.СгенерироватьОбъектыРасчетов(ПустаяСсылка(), Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
