&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ИнициализацияТаблицФормы();
	
	ЗаполнитьУсловноеОформление();
	
	// КодНаименованияОперации
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыНаименованияОпераций();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодНаименованияОперации.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодПредмета
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыТипПредмета();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодПредмета.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	//КодСтороныОперации
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыСтороныОпераций();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодСтороныОперации.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодСтраны
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокКодовСтран();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодСтраныПроисхождения.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодВалюты
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокКодовВалют();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодВалюты.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодОснования
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыОснования();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодОснования.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодМетода
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыМетода();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодМетода.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// КодИсточника
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыИсточника();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодИсточника.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	// ПоказательРентабельности
	Список = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораПоказательРентабельности();
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиПоказательРентабельности.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
		
	мПересчитыватьСуммуВсехОпераций = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияТаблицФормы()

	ОбычныеЦеныКонтролируемыхОпераций.Очистить();
	_ОбычныеЦеныКонтролируемыхОпераций = РегистрыСведений.ОбычныеЦеныКонтролируемыхОпераций.СоздатьНаборЗаписей();
	_ОбычныеЦеныКонтролируемыхОпераций.Прочитать();
	ЗначениеВРеквизитФормы(_ОбычныеЦеныКонтролируемыхОпераций.Выгрузить(), "ОбычныеЦеныКонтролируемыхОпераций");
	
	ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.Очистить();
	_ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций = РегистрыСведений.ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.СоздатьНаборЗаписей();
	_ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.Прочитать();
	ЗначениеВРеквизитФормы(_ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.Выгрузить(), "ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций");
	
	ПредметыКонтролируемыхОпераций.Очистить();
	_ПредметыКонтролируемыхОпераций = РегистрыСведений.ПредметыКонтролируемыхОпераций.СоздатьНаборЗаписей();
	_ПредметыКонтролируемыхОпераций.Прочитать();
	ЗначениеВРеквизитФормы(_ПредметыКонтролируемыхОпераций.Выгрузить(), "ПредметыКонтролируемыхОпераций");
	
	ДоговорыУчастниковКонтролируемыхОпераций.Очистить();
	_ДоговорыУчастниковКонтролируемыхОпераций  = РегистрыСведений.ДоговорыУчастниковКонтролируемыхОпераций	.СоздатьНаборЗаписей();
	_ДоговорыУчастниковКонтролируемыхОпераций.Прочитать();
	ЗначениеВРеквизитФормы(_ДоговорыУчастниковКонтролируемыхОпераций.Выгрузить(), "ДоговорыУчастниковКонтролируемыхОпераций");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("КонтролируемыеСделкиИсследуемаяСторона");
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.КонтролируемыеСделки.КодМетода");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "301";
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.КонтролируемыеСделки.КодМетода");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "305";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		ЗаполнитьПериодДокумента();
	
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодДокумента()

	Объект.НачПериода = НачалоГода(Объект.Дата);
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(СведенияОКонтролируемыхОперациях.КонПериода) КАК Период
	               |ИЗ
	               |	Документ.СведенияОКонтролируемыхОперациях КАК СведенияОКонтролируемыхОперациях
	               |ГДЕ
	               |	СведенияОКонтролируемыхОперациях.Организация = &Организация
	               |	И СведенияОКонтролируемыхОперациях.Дата МЕЖДУ &НачГода И &КонецГода";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("НачГода", 	 НачалоГода(Объект.Дата));
	Запрос.УстановитьПараметр("КонецГода", 	 КонецГода(Объект.Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если ЗначениеЗаполнено(Выборка.Период) Тогда
		Объект.НачПериода = НачалоДня(Выборка.Период) + 24*60*60;
	КонецЕсли;
	
	Объект.КонПериода = Объект.Дата;
	
	Если Объект.НачПериода > Объект.КонПериода Тогда
		Объект.онПериода = Объект.НачПериода;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
		
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры // НастроитьЭлементыФормы

&НаСервере
Процедура ЗаполнитьЗаПериодНаСервере()
	
	ИнициализацияТаблицФормы();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("НачПериода",  НачалоДня(Объект.НачПериода));
	Запрос.УстановитьПараметр("КонПериода",  КонецДня(Объект.КонПериода));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Контрагенты", ПолучитьСписокСвязанныхКонтрагентов(Объект.Организация, Объект.НачПериода));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование 		КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени 				КАК МоментВремени,
	|	Док.НомерСтроки 						КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) 	КАК Период,
	|	Док.Ссылка.Валюта 						КАК Валюта,
	|	Док.Ссылка.Валюта.Код 					КАК КодВалюты,
	|	Док.Ссылка 								КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент 					КАК Контрагент,
	|	Док.Ссылка.Договор 						КАК ДоговорКонтрагента,
	|	Док.Номенклатура 						КАК ПредметСделки,
	|	Док.Характеристика 						КАК ХарактеристикаНоменклатуры,
	|	"""" 									КАК Содержание,
    |	Док.КоличествоУпаковок                  КАК КоличествоУпаковок,
    |   Док.Упаковка                            КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Док.Ссылка.ЦенаВключаетНДС
	|			ТОГДА Док.Сумма - Док.СуммаНДС
	|		ИНАЧЕ Док.Сумма
	|	КОНЕЦ КАК СуммаВал
	|Поместить ВремТаблица
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени КАК МоментВремени,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) КАК Период,
	|	Док.Ссылка.Валюта КАК Валюта,
	|	Док.Ссылка.Валюта.Код КАК КодВалюты,
	|	Док.Ссылка КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.Ссылка.Договор КАК ДоговорКонтрагента,
	|	NULL КАК ПредметСделки,
	|	NULL КАК ХарактеристикаНоменклатуры,
	|	Док.Содержание КАК Содержание,
	|	Док.Количество КАК Количество,
    |	Док.ЕдиницаИзмерения КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Док.Ссылка.ЦенаВключаетНДС
	|			ТОГДА Док.Сумма - Док.СуммаНДС
	|		ИНАЧЕ Док.Сумма
	|	КОНЕЦ КАК СуммаВал
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)
    |
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени КАК МоментВремени,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) КАК Период,
	|	Док.Ссылка.Валюта КАК Валюта,
	|	Док.Ссылка.Валюта.Код КАК КодВалюты,
	|	Док.Ссылка КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.Ссылка.Договор КАК ДоговорКонтрагента,
	|	Док.Номенклатура КАК ПредметСделки,
	|	Док.Характеристика КАК ХарактеристикаНоменклатуры,
	|	"""" КАК Содержание,
    |	Док.КоличествоУпаковок КАК КоличествоУпаковок,
    |   Док.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Док.Ссылка.ЦенаВключаетНДС
	|			ТОГДА Док.Сумма - Док.СуммаНДС
	|		ИНАЧЕ Док.Сумма
	|	КОНЕЦ КАК СуммаВал
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени КАК МоментВремени,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) КАК Период,
	|	Док.Ссылка.Валюта КАК Валюта,
	|	Док.Ссылка.Валюта.Код КАК КодВалюты,
	|	Док.Ссылка КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.Ссылка.Договор КАК ДоговорКонтрагента,
	|	NULL КАК ПредметСделки,
	|	NULL КАК ХарактеристикаНоменклатуры,
	|	Док.Содержание КАК Содержание,
	|	Док.Количество КАК Количество, 
    |    ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Док.Ссылка.ЦенаВключаетНДС
	|			ТОГДА Док.Сумма - Док.СуммаНДС
	|		ИНАЧЕ Док.Сумма
	|	КОНЕЦ КАК СуммаВал
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)


	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени КАК МоментВремени,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) КАК Период,
	|	Док.Ссылка.Валюта КАК Валюта,
	|	Док.Ссылка.Валюта.Код КАК КодВалюты,
	|	Док.Ссылка КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.Ссылка.Договор КАК ДоговорКонтрагента,
	|	Номенклатура КАК ПредметСделки,
	|	Характеристика КАК ХарактеристикаНоменклатуры,
	|	"""" КАК Содержание,
	|	1 КАК Количество,       
    |    ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	Док.Сумма КАК СуммаВал
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)

	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Ссылка.МоментВремени КАК МоментВремени,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Док.Ссылка.Дата, ДЕНЬ) КАК Период,
	|	Док.Ссылка.Валюта КАК Валюта,
	|	Док.Ссылка.Валюта.Код КАК КодВалюты,
	|	Док.Ссылка КАК ДокументИнформационнйБазы,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.Ссылка.Договор КАК ДоговорКонтрагента,
	|	Номенклатура КАК ПредметСделки,
	|	Характеристика КАК ХарактеристикаНоменклатуры,
	|	"""" КАК Содержание,
    |	Док.КоличествоУпаковок КАК КоличествоУпаковок,
    |   Док.Упаковка КАК Упаковка,
	|	Док.Сумма КАК СуммаВал
	|ИЗ
	|	Документ.ПоступлениеОтПереработчика.Товары КАК Док
	|ГДЕ
	|	Док.Ссылка.Организация = &Организация И Док.Ссылка.Проведен
	|	И Док.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И Док.Ссылка.Контрагент В(&Контрагенты)


	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремТаблица.Период,
	|	ВремТаблица.Валюта,
	|	ВремТаблица.КодВалюты,
	|	ВремТаблица.ДокументИнформационнйБазы,
	|	ВремТаблица.Контрагент,
	|	ВремТаблица.ДоговорКонтрагента,
	|	ВремТаблица.ПредметСделки,
	|	ВремТаблица.ХарактеристикаНоменклатуры,
	|	ВремТаблица.Содержание,
	|	ВремТаблица.КоличествоУпаковок,
    |	ВремТаблица.Упаковка,
	|	ВремТаблица.СуммаВал
	|ИЗ
	|	ВремТаблица КАК ВремТаблица
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВремТаблица.КонтрагентНаименование,
	|	ВремТаблица.МоментВремени,
	|	ВремТаблица.НомерСтроки
	|";
				   
				   
				   
	ТипПереработка1 = Тип("ДокументСсылка.ОтчетПереработчика");
	ТипПереработка2 = Тип("ДокументСсылка.ПоступлениеОтПереработчика");
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Строка = Объект.КонтролируемыеСделки.Добавить();
		
		Строка.ДокументИнформационнйБазы = Выборка.ДокументИнформационнйБазы; 
		Строка.Контрагент 				 = Выборка.Контрагент; 
		Строка.ДоговорКонтрагента 		 = Выборка.ДоговорКонтрагента;

		Строка.СтатусКонтрагента		 = ПолучитьСтатусКонтрагентаНаСервере(Строка.ДоговорКонтрагента);
		
		ЗаполнитьДанныеПоДоговорамУчастников(Строка); //КодНаименованияОперации, ДатаКонтракта, НомерКонтракта, КодСтороныОперации12, УсловиеПоставкиПоИнкотермс
		
		Строка.ПредметСделки		 		= Выборка.ПредметСделки;
		Строка.ХарактеристикаНоменклатуры	= Выборка.ХарактеристикаНоменклатуры;
		Строка.ПредметСделкиСтрока		 	= Выборка.Содержание;
		
		Если НЕ ЗначениеЗаполнено(Строка.ПредметСделкиСтрока) Тогда
			
			Строка.ПредметСделкиСтрока 			= "" + Строка.ПредметСделки;
			Если ЗначениеЗаполнено(Строка.ХарактеристикаНоменклатуры) Тогда
				Строка.ПредметСделкиСтрока = "" + Строка.ПредметСделки.НаименованиеПолное + " (" + Строка.ХарактеристикаНоменклатуры + ")";	
			Иначе
				Строка.ПредметСделкиСтрока = "" + Строка.ПредметСделки.НаименованиеПолное;
			КонецЕсли;		
		КонецЕсли;
		
		ЗаполнитьДанныеПоПредметамКонтролируемыхОпераций(Строка); //КодПредмета, КодКлассификатораПродукцииИУслуг, КодУКТВЭД, КодУКВЭП, КодСтраныПроисхождения, ТорговаяМарка, Производитель, ЕдиницаИзмерения  
		
		Если    ТипЗнч(Строка.ДокументИнформационнйБазы) = ТипПереработка1
			ИЛИ ТипЗнч(Строка.ДокументИнформационнйБазы) = ТипПереработка2 Тогда
		
			
			Если НЕ ЗначениеЗаполнено(Выборка.Содержание) Тогда
				// продукция, которая перерабатывается
				Строка.КодПредмета = "204";	
				Строка.ПредметСделкиСтрока = Строка.ПредметСделкиСтрока + " (переробка)";	
			КонецЕсли;
			
		КонецЕсли;
			
		Строка.ДатаОперацииС	 = Выборка.Период;
		Строка.ДатаОперацииПо	 = Выборка.Период;
		
		Строка.КоличествоУпаковок= Выборка.КоличествоУпаковок;
        Строка.Упаковка		     = Выборка.Упаковка;
        Если ЗначениеЗаполнено(Строка.Упаковка) Тогда
            КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.ПредметСделки);
        Иначе
            КоэффициентУпаковки = 1;
        КонецЕсли;    
        Строка.Количество 		 = КоэффициентУпаковки * Строка.КоличествоУпаковок;
		Строка.ЦенаВал		 	 = ?(Выборка.КоличествоУпаковок = 0, 0, Выборка.СуммаВал / Выборка.КоличествоУпаковок);
		
		Строка.КодВалюты		 = Выборка.КодВалюты;
		Строка.КурсВалюты		 = ПолучитьКурсВалюты(Выборка.Валюта, Выборка.Период);
		
		Строка.СуммаРегл 		 = Строка.КоличествоУпаковок * Строка.ЦенаВал * Строка.КурсВалюты;
		Строка.ЦенаРегл  		 = ?(Строка.КоличествоУпаковок = 0, 0, Строка.СуммаРегл/Строка.КоличествоУпаковок);

		ЗаполнитьДанныеПоОбычнойЦене(Строка, Выборка.Период); //КодОснования, КодМетода123, КодИсточника, ЦенаОбычнаяРегл  
		
		РассчитатьСуммуКорректировкиДоходовЗатратПоСтроке(Строка);
		
		Если Объект.СуммыВсехОпераций.НайтиСтроки(Новый Структура("Контрагент", Строка.Контрагент)).Количество() = 0 Тогда
			СтрокаИтогов = Объект.СуммыВсехОпераций.Добавить();
			СтрокаИтогов.Контрагент = Строка.Контрагент;
		КонецЕсли;
		
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаПериод(Команда)
	
	Если    Не ЗначениеЗаполнено(ОБъект.НачПериода)
		ИЛИ Не ЗначениеЗаполнено(ОБъект.КонПериода) Тогда
			ПоказатьПредупреждение(,НСтр("ru='Укажите начало и конец периода!';uk='Вкажіть початок і кінець періоду!'"));
		Возврат;	
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьЗаПериодОкончание",ЭтаФорма);
	Если Объект.КонтролируемыеСделки.Количество() > 0 Тогда
		
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Табличная часть будет очищена. Продолжить?';uk='Таблична частина буде очищена. Продовжити?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаПериодОкончание(Результат, ДопПараметры) Экспорт

	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КонтролируемыеСделки.Очистить();		
	
	ЗаполнитьЗаПериодНаСервере();
	
	мПересчитыватьСуммуВсехОпераций = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСвязанностьНажатие(Элемент)
	
	ПараметрыОткрытия = Новый Структура("Организация", Объект.Организация);
	
	ОткрытьФорму("РегистрСведений.СвязанностьЛицКонтролируемыхОпераций.Форма.ФормаСписка", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
//Конец ИнтеграцияС1СДокументооборотом

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ГруппаСуммыВсехОпераций Тогда
		Если мПересчитыватьСуммуВсехОпераций = Истина Тогда
			
			РассчитатьСуммуВсехОпераций();
			Элементы.СуммыВсехОпераций.Обновить();
			мПересчитыватьСуммуВсехОпераций = Ложь;
			
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуВсехОпераций()

	Для каждого СтрокаИтогов Из Объект.СуммыВсехОпераций Цикл
		
		ДанныеПоКонтрагенту = Объект.КонтролируемыеСделки.НайтиСтроки(Новый Структура("Контрагент", СтрокаИтогов.Контрагент));
		
		СуммаРегл = 0;
		Для каждого СтрокаДанныеПоКонтрагенту Из ДанныеПоКонтрагенту Цикл
			СуммаРегл = СуммаРегл + СтрокаДанныеПоКонтрагенту.СуммаРегл;
		КонецЦикла;
		СтрокаИтогов.СуммаРегл = СуммаРегл;
		
		Если СтрокаИтогов.СуммаОперацийЗаПериод < СтрокаИтогов.СуммаРегл Тогда
			СтрокаИтогов.СуммаОперацийЗаПериод = СтрокаИтогов.СуммаРегл;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиСуммаРеглПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ЦенаРегл  = ?(ТекущиеДанные.КоличествоУпаковок = 0, 0, ТекущиеДанные.СуммаРегл/ТекущиеДанные.КоличествоУпаковок);
	РассчитатьСуммуКорректировкиДоходовЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммыВсехОперацийПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.СуммыВсехОпераций.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока и ОтменаРедактирования Тогда
		Возврат;	
	КонецЕсли;
	
	Если НоваяСтрока Тогда
	
		Если Объект.СуммыВсехОпераций.НайтиСтроки(Новый Структура("Контрагент", ТекущиеДанные.Контрагент)).Количество() > 1 Тогда
			Сообщить(СтрШаблон(НСтр("ru='Контрагент %1 уже есть в табличной части!';uk='Контрагент %1 вже є у табличній частині!'"), ТекущиеДанные.Контрагент));
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Контрагент) Тогда
		Сообщить(НСтр("ru='Укажите контрагента!';uk='Вкажіть контрагента!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуВсехОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура НачПериодаПриИзменении(Элемент)
	
	Если Объект.НачПериода > Объект.КонПериода Тогда
		Объект.КонПериода = Объект.НачПериода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонПериодаПриИзменении(Элемент)
	
	Если Объект.НачПериода > Объект.КонПериода Тогда
		Объект.КонПериода = Объект.НачПериода;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоДоговорамУчастников(Строка)

	ЕстьДанныеДляЗаполнения = Истина;
	СтруктураПоиска = Новый Структура("Организация, Контрагент, ДоговорКонтрагента", Объект.Организация, Строка.Контрагент, Строка.ДоговорКонтрагента);
	Если ДоговорыУчастниковКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		
		СтруктураПоиска.Вставить("ДоговорКонтрагента", ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
		Если ДоговорыУчастниковКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			
				ЕстьДанныеДляЗаполнения = Ложь;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Если ЕстьДанныеДляЗаполнения = Истина Тогда
	
		СтрокаДанных = ДоговорыУчастниковКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска)[0];
		
		Строка.КодНаименованияОперации 	= СтрокаДанных.КодНаименованияОперации;
		Строка.ДатаКонтракта   			= СтрокаДанных.ДатаКонтракта;
		Строка.НомерКонтракта   		= СтрокаДанных.НомерКонтракта;
		Строка.КодСтороныОперации   	= СтрокаДанных.КодСтороныОперации;
		Строка.УсловиеПоставкиПоИнкотермс = СтрокаДанных.УсловиеПоставкиПоИнкотермс;
		Строка.МестоПоставкиПоИнкотермс   = СтрокаДанных.МестоПоставкиПоИнкотермс;
	
	КонецЕсли;		

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоПредметамКонтролируемыхОпераций(Строка)

	ЕстьДанныеДляЗаполнения = Истина;
	СтруктураПоиска = Новый Структура("ПредметСделки, ХарактеристикаНоменклатуры", Строка.ПредметСделки, ?(ЗначениеЗаполнено(Строка.ХарактеристикаНоменклатуры), Строка.ХарактеристикаНоменклатуры, ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка")));
	Если ПредметыКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		
		СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
		Если ПредметыКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			ЕстьДанныеДляЗаполнения = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьДанныеДляЗаполнения = Истина Тогда
	
		СтрокаДанных = ПредметыКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска)[0];
		
		Строка.КодПредмета = СтрокаДанных.КодПредмета;
		Строка.КодКлассификатораПродукцииИУслуг   = СтрокаДанных.КодКлассификатораПродукцииИУслуг;
		Строка.КодУКТВЭД   		= СтрокаДанных.КодУКТВЭД;
		Строка.КодУКВЭП   		= СтрокаДанных.КодУКВЭП;
		Строка.КодСтраныПроисхождения = СтрокаДанных.КодСтраныПроисхождения;
		Строка.ТорговаяМарка 	= СтрокаДанных.ТорговаяМарка;
		Строка.Производитель 	= СтрокаДанных.Производитель;
		Строка.Упаковка = СтрокаДанных.Упаковка;
	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКурсВалютыНаКлиенте(Валюта, Период)

	Возврат ПолучитьКурсВалюты(Валюта, Период);	

КонецФункции 

&НаСервере
Функция ПолучитьКурсВалюты(Валюта, Период)

	СтруктураПоиска = Новый Структура("Валюта, Период", Валюта, НачалоДня(Период));
	
	Если ТаблицаКурсов.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
	
		СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Период);
		КурсВзаиморасчетов           = СтруктураКурсаВзаиморасчетов.Курс;
		КратностьВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Кратность;
		Курс = ?(КратностьВзаиморасчетов = 0, 0, КурсВзаиморасчетов/КратностьВзаиморасчетов);
		
		Строка = ТаблицаКурсов.Добавить();
		Строка.Валюта = Валюта;
		Строка.Период = НачалоДня(Период);
		Строка.Курс   = Курс;
		
	Иначе
		
		Курс = ТаблицаКурсов.НайтиСтроки(СтруктураПоиска)[0].Курс;
		
	КонецЕсли;
	
	Возврат Курс;	

КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСписокСвязанныхКонтрагентов(Организация, Дата)

	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", 	 Дата);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.Контрагент,
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.КодСвязанностиЛиц1,
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.КодСвязанностиЛиц2,
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.КодСвязанностиЛиц3,
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.КодСвязанностиЛиц4,
	               |	СвязанностьЛицКонтролируемыхОперацийСрезПоследних.КодСвязанностиЛиц5
	               |ИЗ
	               |	РегистрСведений.СвязанностьЛицКонтролируемыхОпераций.СрезПоследних(&Период, Организация = &Организация) КАК СвязанностьЛицКонтролируемыхОперацийСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
		
	МассивСвязанныхКонтрагентов = Новый Массив();
	Пока Выборка.Следующий() Цикл
		
		Если    ЗначениеЗаполнено(Выборка.КодСвязанностиЛиц1)
			ИЛИ ЗначениеЗаполнено(Выборка.КодСвязанностиЛиц2) 
			ИЛИ ЗначениеЗаполнено(Выборка.КодСвязанностиЛиц3) 
			ИЛИ ЗначениеЗаполнено(Выборка.КодСвязанностиЛиц4) 
			ИЛИ ЗначениеЗаполнено(Выборка.КодСвязанностиЛиц5) Тогда
			
			МассивСвязанныхКонтрагентов.Добавить(Выборка.Контрагент);
		
		КонецЕсли;
		
	КонецЦикла;
	
	ВОзврат МассивСвязанныхКонтрагентов;	

КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеПоОбычнойЦене(Строка, Период)

	ЕстьДанныеДляЗаполнения = Истина;
	СтруктураПоиска = Новый Структура("Организация, Контрагент, ДоговорКонтрагента", ОБъект.Организация, Строка.Контрагент, Строка.ДоговорКонтрагента);
	Если ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		
		СтруктураПоиска.Вставить("ДоговорКонтрагента", ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
		Если ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			
			СтруктураПоиска.Вставить("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
			Если ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
				
				СтруктураПоиска.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
				Если ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
					ЕстьДанныеДляЗаполнения = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Если ЕстьДанныеДляЗаполнения = Истина Тогда
	
		СтрокаДанных = ПараметрыОпределенияОбычныхЦенКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска)[0];
		
		Строка.КодОснования = СтрокаДанных.КодОснования;
		Строка.КодМетода   = СтрокаДанных.КодМетода;
		Строка.КодИсточника = СтрокаДанных.КодИсточника;
		Строка.ИсследуемаяСторона = СтрокаДанных.ИсследуемаяСторона;
		
	КонецЕсли;
	
	ЕстьДанныеДляЗаполнения = Истина;
	СтруктураПоиска = Новый Структура("Организация, Контрагент, ДоговорКонтрагента,  ПредметСделки, ХарактеристикаНоменклатуры", Объект.Организация, Строка.Контрагент, Строка.ДоговорКонтрагента, Строка.ПредметСделки, ?(ЗначениеЗаполнено(Строка.ХарактеристикаНоменклатуры), Строка.ХарактеристикаНоменклатуры, ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка")));
	Если ОбычныеЦеныКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		
		СтруктураПоиска.Вставить("ДоговорКонтрагента",  ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
		Если ОбычныеЦеныКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			
			СтруктураПоиска.Вставить("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
			Если ОбычныеЦеныКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
				
				СтруктураПоиска.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
				Если ОбычныеЦеныКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
					ЕстьДанныеДляЗаполнения = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	
    Если ЕстьДанныеДляЗаполнения = Истина Тогда
	
		СтрокаДанных 		   = ОбычныеЦеныКонтролируемыхОпераций.НайтиСтроки(СтруктураПоиска)[0];
		Строка.ЦенаОбычнаяРегл = СтрокаДанных.Цена * ПолучитьКурсВалюты(СтрокаДанных.ВалютаЦены, Период);	
		Строка.ПоказательРентабельности 		= СтрокаДанных.ПоказательРентабельности;	
		Строка.ЗначениеПоказателяРентабельности = СтрокаДанных.ЗначениеПоказателяРентабельности;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтатусКонтрагента(ДоговорКонтрагента)

	Возврат ПолучитьСтатусКонтрагентаНаСервере(ДоговорКонтрагента);
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСтатусКонтрагентаНаСервере(ДоговорКонтрагента)

	СтатусКонтрагента 	= Неопределено;
	ВидДоговора 		= ДоговорКонтрагента.ТипДоговора;
	
	Если ВидДоговора = Перечисления.ТипыДоговоров.СКомиссионером Тогда
		СтатусКонтрагента = Ложь;
	ИначеЕсли ВидДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда
		СтатусКонтрагента = Истина;
	ИначеЕсли ВидДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
		СтатусКонтрагента = Ложь;
	ИначеЕсли ВидДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
		СтатусКонтрагента = Истина;
	ИначеЕсли ВидДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
		СтатусКонтрагента = Истина;
	ИначеЕсли ВидДоговора = Перечисления.ТипыДоговоров.ИмпортКомиссия Тогда
		СтатусКонтрагента = Ложь;
	Иначе
		СтатусКонтрагента = Ложь;
	КонецЕсли;
	
	Возврат СтатусКонтрагента;

КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуКорректировкиДоходовЗатратПоСтроке(ТекущиеДанные)

	Если  ТекущиеДанные.СтатусКонтрагента = Ложь 
		И ТекущиеДанные.ЦенаОбычнаяРегл > ТекущиеДанные.ЦенаРегл Тогда
		
		ТекущиеДанные.СуммаКорректировкиДоходов = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаОбычнаяРегл - ТекущиеДанные.СуммаРегл;
		
	ИначеЕсли ТекущиеДанные.СтатусКонтрагента = Истина 
		    И ТекущиеДанные.ЦенаОбычнаяРегл < ТекущиеДанные.ЦенаРегл Тогда
			
		ТекущиеДанные.СуммаКорректировкиРасходов = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаОбычнаяРегл - ТекущиеДанные.СуммаРегл;
		
	Иначе
		
		ТекущиеДанные.СуммаКорректировкиРасходов = 0;
		ТекущиеДанные.СуммаКорректировкиДоходов = 0;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуКорректировкиДоходовЗатрат()
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСтроки = Новый Структура("ЦенаРегл, ЦенаОбычнаяРегл, СтатусКонтрагента, КоличествоУпаковок, СуммаРегл, СуммаКорректировкиРасходов, СуммаКорректировкиДоходов");
	ЗаполнитьЗначенияСвойств(СтруктураСтроки, ТекущиеДанные);
	
	РассчитатьСуммуКорректировкиДоходовЗатратПоСтроке(СтруктураСтроки);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтруктураСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиЦенаОбычнаяРеглПриИзменении(Элемент)
	
	РассчитатьСуммуКорректировкиДоходовЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКурсВалютыПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаРегл = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаВал * ТекущиеДанные.КурсВалюты;
	ТекущиеДанные.ЦенаРегл  = ?(ТекущиеДанные.КоличествоУпаковок = 0, 0, ТекущиеДанные.СуммаРегл/ТекущиеДанные.КоличествоУпаковок);
	РассчитатьСуммуКорректировкиДоходовЗатрат();

КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКоличествоПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    КоэффициентУпаковки = ПолучитьКоэффициентУпаковки(ТекущиеДанные.Упаковка, ТекущиеДанные.ПредметСделки);
    ТекущиеДанные.Количество = КоэффициентУпаковки * ТекущиеДанные.КоличествоУпаковок;
	
	ТекущиеДанные.СуммаРегл = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаВал * ТекущиеДанные.КурсВалюты;
	ТекущиеДанные.ЦенаРегл  = ?(ТекущиеДанные.КоличествоУпаковок = 0, 0, ТекущиеДанные.СуммаРегл/ТекущиеДанные.КоличествоУпаковок);
	РассчитатьСуммуКорректировкиДоходовЗатрат();

КонецПроцедуры

&НаСервере
Функция ПолучитьКоэффициентУпаковки(Упаковка, Номенклатура)
    Если ЗначениеЗаполнено(Упаковка) Тогда
        КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
    Иначе
        КоэффициентУпаковки = 1;
    КонецЕсли;    
    Возврат КоэффициентУпаковки;
КонецФункции 

&НаКлиенте
Процедура КонтролируемыеСделкиЦенаВалПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаРегл = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаВал * ТекущиеДанные.КурсВалюты;
	ТекущиеДанные.ЦенаРегл  = ?(ТекущиеДанные.КоличествоУпаковок = 0, 0, ТекущиеДанные.СуммаРегл/ТекущиеДанные.КоличествоУпаковок);
	РассчитатьСуммуКорректировкиДоходовЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКодВалютыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

    Валюта = НайтиВалюту(ТекущиеДанные.КодВалюты);
	Если ЗначениеЗаполнено(Валюта) Тогда
	
        ТекущиеДанные.КурсВалюты = ПолучитьКурсВалютыНаКлиенте(Валюта, ТекущиеДанные.ДатаОперацииПо);
		
		ТекущиеДанные.СуммаРегл = ТекущиеДанные.КоличествоУпаковок * ТекущиеДанные.ЦенаВал * ТекущиеДанные.КурсВалюты;
		ТекущиеДанные.ЦенаРегл  = ?(ТекущиеДанные.КоличествоУпаковок = 0, 0, ТекущиеДанные.СуммаРегл/ТекущиеДанные.КоличествоУпаковок);
		РассчитатьСуммуКорректировкиДоходовЗатрат();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиВалюту(КодВалюты)

	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Валюты.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.Валюты КАК Валюты
	                      |ГДЕ
	                      |	Валюты.Код = &Код");	
	Запрос.УстановитьПараметр("Код", КодВалюты);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
	
		Возврат Выборка.Ссылка;	
	
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции 

&НаСервереБезКонтекста
Процедура ПроверитьСоответствиеКодаСтороныОперации(КонНаимОперации, КодСторОперации)
	
	СписокВозможныхЗначений = КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыСтороныОпераций(КонНаимОперации);
	Если СписокВозможныхЗначений.НайтиПоЗначению(КодСторОперации) = Неопределено Тогда
		КодСторОперации = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКодСтороныОперацииПриИзменени(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьСоответствиеКодаСтороныОперации(ТекущиеДанные.КодНаименованияОперации, ТекущиеДанные.КодСтороныОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКодНаименованияОперацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьСоответствиеКодаСтороныОперации(ТекущиеДанные.КодНаименованияОперации, ТекущиеДанные.КодСтороныОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиСтатусКонтрагентаПриИзменении(Элемент)
	
	РассчитатьСуммуКорректировкиДоходовЗатрат();

КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиДоговорКонтрагентаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДоговора = ПолучитьРеквизитыДоговора(ТекущиеДанные.ДоговорКонтрагента);
	ТекущиеДанные.ДатаКонтракта  = РеквизитыДоговора.Дата;
	ТекущиеДанные.НомерКонтракта = РеквизитыДоговора.Номер;
	
	ТекущиеДанные.СтатусКонтрагента = ПолучитьСтатусКонтрагента(ТекущиеДанные.ДоговорКонтрагента);
	
	РассчитатьСуммуКорректировкиДоходовЗатрат();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыДоговора(ДоговорКонтрагента)

	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "Дата, Номер");

КонецФункции

&НаКлиенте
Процедура КонтролируемыеСделкиПредметСделкиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура("ПредметСделкиСтрока, ПредметСделки, ХарактеристикаНоменклатуры, КодПредмета, КодУКТВЭД");
	ЗаполнитьЗначенияСвойств(СтруктураДанных, ТекущиеДанные);
	
	КонтролируемыеСделкиПредметСделкиПриИзмененииНаСервере(СтруктураДанных);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтруктураДанных);
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура КонтролируемыеСделкиПредметСделкиПриИзмененииНаСервере(ТекущиеДанные)
	
	Если  ЗначениеЗаполнено(ТекущиеДанные.ХарактеристикаНоменклатуры)
		И ТекущиеДанные.ХарактеристикаНоменклатуры.Владелец = ТекущиеДанные.ПредметСделки Тогда
		
		ТекущиеДанные.ПредметСделкиСтрока = "" + ТекущиеДанные.ПредметСделки.НаименованиеПолное + " (" + ТекущиеДанные.ХарактеристикаНоменклатуры + ")";	
		
	Иначе
		
		ТекущиеДанные.ПредметСделкиСтрока = "" + ТекущиеДанные.ПредметСделки.НаименованиеПолное;
		
	КонецЕсли;
	
	Тип = ТекущиеДанные.ПредметСделки.ВидНоменклатуры.ТипНоменклатуры;
	Если Тип = Перечисления.ТипыНоменклатуры.Работа Тогда
		ТекущиеДанные.КодПредмета = "207";
	ИначеЕсли Тип = Перечисления.ТипыНоменклатуры.Услуга Тогда
		ТекущиеДанные.КодПредмета = "204";
	Иначе	
	    ТекущиеДанные.КодПредмета = "201";
	КонецЕсли;
	
	ТекущиеДанные.КодУКТВЭД = ТекущиеДанные.ПредметСделки.НоменклатураГТД.КодУКТВЭД;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура("ПредметСделкиСтрока, ПредметСделки, ХарактеристикаНоменклатуры");
	ЗаполнитьЗначенияСвойств(СтруктураДанных, ТекущиеДанные);
	
	КонтролируемыеСделкиХарактеристикаНоменклатурыПриИзмененииНаСервере(СтруктураДанных);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтруктураДанных);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КонтролируемыеСделкиХарактеристикаНоменклатурыПриИзмененииНаСервере(ТекущиеДанные)
	
	Если  ЗначениеЗаполнено(ТекущиеДанные.ХарактеристикаНоменклатуры) Тогда
		
		ТекущиеДанные.ПредметСделкиСтрока = "" + ТекущиеДанные.ПредметСделки.НаименованиеПолное + " (" + ТекущиеДанные.ХарактеристикаНоменклатуры + ")";	
		
	Иначе
		
		ТекущиеДанные.ПредметСделкиСтрока = "" + ТекущиеДанные.ПредметСделки.НаименованиеПолное;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОБъект.СуммыВсехОпераций.НайтиСтроки(Новый Структура("Контрагент", ТекущиеДанные.Контрагент)).Количество() = 0 Тогда
		СтрокаИтогов = ОБъект.СуммыВсехОпераций.Добавить();
		СтрокаИтогов.Контрагент = ТекущиеДанные.Контрагент;
	КонецЕсли;
	
	мПересчитыватьСуммуВсехОпераций = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиКодСтороныОперацииНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
		ТекущиеДанные = Элементы.КонтролируемыеСделки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элементы.КонтролируемыеСделкиКодСтороныОперации.СписокВыбора.Очистить();
	Список = КонтролируемыеСделкиКодСтороныОперацииНачалоВыбораНаСервере(ТекущиеДанные.КодНаименованияОперации);
	Для каждого ЭлементСписка Из Список Цикл
		Элементы.КонтролируемыеСделкиКодСтороныОперации.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;

КонецПроцедуры
&НаСервере
Функция КонтролируемыеСделкиКодСтороныОперацииНачалоВыбораНаСервере(КодНаимОперации)
	Возврат КонтролируемыеОперацииПовтИсп.ПолучитьСписокВыбораКодыСтороныОпераций(КодНаимОперации);
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
    
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(
		ЭтаФорма, 
		"КонтролируемыеСделкиПредметСделкиЕдиницаИзмерения", 
		"Объект.КонтролируемыеСделки.Упаковка"
	);
    
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеСделкиУпаковкаПриИзменении(Элемент)
    
    ТекущаяСтрока = Элементы.КонтролируемыеСделки.ТекущиеДанные;
    
    ПараметрыПересчетаКоличестваЕдиниц = Новый Структура;
    
    ПараметрыПересчетаКоличестваЕдиниц.Вставить("Номенклатура", ТекущаяСтрока.ПредметСделки); 
    
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПараметрыПересчетаКоличестваЕдиниц);
    
    КоличествоЕдиницДоПересчета = ТекущаяСтрока.Количество;
    
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
    
	Если КоличествоЕдиницДоПересчета <> 0 Тогда
		ТекущаяСтрока.ЦенаВал = ТекущаяСтрока.ЦенаВал
		   / КоличествоЕдиницДоПересчета
		   * ТекущаяСтрока.Количество;
    КонецЕсли;
       
    
	ТекущаяСтрока.СуммаРегл = ТекущаяСтрока.КоличествоУпаковок * ТекущаяСтрока.ЦенаВал * ТекущаяСтрока.КурсВалюты;
	ТекущаяСтрока.ЦенаРегл  = ?(ТекущаяСтрока.КоличествоУпаковок = 0, 0, ТекущаяСтрока.СуммаРегл/ТекущаяСтрока.КоличествоУпаковок);
    
	РассчитатьСуммуКорректировкиДоходовЗатрат();

КонецПроцедуры


&НаКлиенте
Процедура УстановитьИнтервал(Команда)
		
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "НачПериода", "КонПериода"));
	
	КонецПроцедуры
