#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстЗапроса = "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("КонтролируемыеОперации");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
	    ТекстЗапросаТаблицаКонтролируемыеОперации(Запрос, ТекстыЗапроса, Регистры);
	    ТекстЗапросаТаблицаОбщиеСведенияОКонтролируемыхОперациях(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка как Ссылка,
	|	ДанныеДокумента.Дата как Период,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.СведенияОКонтролируемыхОперациях КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
    |";
    
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
    
	Запрос.УстановитьПараметр("Период",         Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",    Реквизиты.Организация);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаКонтролируемыеОперации(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "КонтролируемыеОперации";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
    КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	КонтролируемыеСделки.Контрагент,
	|	КонтролируемыеСделки.ДокументИнформационнйБазы,
	|	КонтролируемыеСделки.ДоговорКонтрагента,
	|	КонтролируемыеСделки.КодНаименованияОперации,
	|	КонтролируемыеСделки.ПредметСделки,
	|	КонтролируемыеСделки.ХарактеристикаНоменклатуры,
	|	КонтролируемыеСделки.ПредметСделкиСтрока,
	|	КонтролируемыеСделки.КодПредмета,
	|	КонтролируемыеСделки.КодКлассификатораПродукцииИУслуг,
	|	КонтролируемыеСделки.КодУКТВЭД,
	|	КонтролируемыеСделки.КодУКВЭП,
	|	КонтролируемыеСделки.ДатаКонтракта,
	|	КонтролируемыеСделки.НомерКонтракта,
	|	КонтролируемыеСделки.КодСтороныОперации,
	|	КонтролируемыеСделки.КодСтраныПроисхождения,
	|	КонтролируемыеСделки.УсловиеПоставкиПоИнкотермс,
	|	КонтролируемыеСделки.МестоПоставкиПоИнкотермс,
	|	КонтролируемыеСделки.ТорговаяМарка,
	|	КонтролируемыеСделки.Производитель,
	|	КонтролируемыеСделки.ДатаОперацииС,
	|	КонтролируемыеСделки.ДатаОперацииПо,
	|	КонтролируемыеСделки.ЦенаВал,
	|	КонтролируемыеСделки.КоличествоУпаковок КАК Количество,
	|	КонтролируемыеСделки.КоличествоСогласноКонтракта,
	|	КонтролируемыеСделки.КодВалюты,
	|	КонтролируемыеСделки.КурсВалюты,
	|	КонтролируемыеСделки.СуммаРегл,
	|	КонтролируемыеСделки.ЦенаРегл,
	|	КонтролируемыеСделки.КодОснования,
	|	КонтролируемыеСделки.КодМетода,
	|	КонтролируемыеСделки.КодИсточника,
	|	КонтролируемыеСделки.ЦенаОбычнаяРегл,
	|	КонтролируемыеСделки.СуммаКорректировкиДоходов,
	|	КонтролируемыеСделки.СуммаКорректировкиРасходов,
	|	КонтролируемыеСделки.ПоказательРентабельности,
	|	КонтролируемыеСделки.ЗначениеПоказателяРентабельности,
	|	КонтролируемыеСделки.ПризнакГруппировкиВСовокупность,
	|	КонтролируемыеСделки.ИсследуемаяСторона,
	|	КонтролируемыеСделки.Упаковка,            
    |	ВЫБОР КОГДА КонтролируемыеСделки.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
    |		КонтролируемыеСделки.ПредметСделки.ЕдиницаИзмерения.Код
    |	КОГДА КонтролируемыеСделки.Упаковка.Код = """" ТОГДА
    |		КонтролируемыеСделки.Упаковка.ЕдиницаИзмерения.Код
    |	ИНАЧЕ
    |		КонтролируемыеСделки.Упаковка.Код
    |	КОНЕЦ КАК КодЕдиницыИзмерения,
    |	ВЫБОР КОГДА КонтролируемыеСделки.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
    |		КонтролируемыеСделки.ПредметСделки.ЕдиницаИзмерения
    |	ИНАЧЕ
    |		КонтролируемыеСделки.Упаковка
    |	КОНЕЦ КАК ЕдиницаИзмерения,
	|	КонтролируемыеСделки.СтатусКонтрагента
	|ИЗ
	|	Документ.СведенияОКонтролируемыхОперациях.КонтролируемыеСделки КАК КонтролируемыеСделки
	|ГДЕ
	|	КонтролируемыеСделки.Ссылка = &Ссылка
	|УПОРЯДОЧИТЬ ПО НомерСтроки
	|";
	
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаОбщиеСведенияОКонтролируемыхОперациях(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "ОбщиеСведенияОКонтролируемыхОперациях";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
    КонецЕсли;
	
	ТекстЗапроса = 
	"
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	КонтролируемыеСделки.Контрагент,
	|	СУММА(КонтролируемыеСделки.СуммаРегл) КАК СуммаКонтролируемыхОпераций,
	|	0 КАК СуммаОбщая
	|ИЗ
	|	Документ.СведенияОКонтролируемыхОперациях.КонтролируемыеСделки КАК КонтролируемыеСделки
	|ГДЕ
	|	КонтролируемыеСделки.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтролируемыеСделки.Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	СуммыВсехОпераций.Контрагент,
	|	0,
	|	СУММА(СуммыВсехОпераций.СуммаОперацийЗаПериод)
	|ИЗ
	|	Документ.СведенияОКонтролируемыхОперациях.СуммыВсехОпераций КАК СуммыВсехОпераций
	|ГДЕ
	|	СуммыВсехОпераций.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыВсехОпераций.Контрагент";
	
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область СозданиеНаОсновании

Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

Функция ЗаполнитьЗаПериод(Объект) Экспорт 
	
	Возврат Истина;
	
КонецФункции // ЗаполнитьПоОстаткам

#КонецОбласти


#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КонтролируемыеСделки.Ссылка
	|ИЗ
	|	Документ.СведенияОКонтролируемыхОперациях.КонтролируемыеСделки КАК КонтролируемыеСделки
	|ГДЕ
	|	КонтролируемыеСделки.Количество = 0 И КонтролируемыеСделки.КоличествоУпаковок <> 0
	|";
    
	Запрос = Новый Запрос(ТекстЗапроса);
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
        Параметры, 
        Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
    );
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию2_1_24(Параметры) Экспорт
	
	ПолноеИмя = "Документ.СведенияОКонтролируемыхОперациях";
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь,ПолноеИмя);
	Пока Выборка.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПолноеИмя);
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойБлокировке(Выборка.Ссылка);
			Продолжить;
		КонецПопытки;
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Если Объект = Неопределено Тогда
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			Продолжить;
		КонецЕсли;
		ЕстьИзменения = Ложь;
		Для Каждого СтрокаКонтролируемыеСделки Из Объект.КонтролируемыеСделки Цикл
            Если СтрокаКонтролируемыеСделки.Количество = 0 И СтрокаКонтролируемыеСделки.КоличествоУпаковок <> 0 Тогда
                Если ЗначениеЗаполнено(СтрокаКонтролируемыеСделки.Упаковка) Тогда
                    КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(СтрокаКонтролируемыеСделки.Упаковка, СтрокаКонтролируемыеСделки.ПредметСделки);
                Иначе
                    КоэффициентУпаковки = 1;
                КонецЕсли;    
                СтрокаКонтролируемыеСделки.Количество = КоэффициентУпаковки * СтрокаКонтролируемыеСделки.КоличествоУпаковок;
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЦикла;
		Если ЕстьИзменения Тогда
			Попытка
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(Выборка.Ссылка);
				ВызватьИсключение;
			КонецПопытки;
		Иначе
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
    
    Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмя);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли