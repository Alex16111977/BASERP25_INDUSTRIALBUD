#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьФормуПослеЗавершенияОперации = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Документы.ЧекККМ.Форма.МенюОперацийСККМ", "ЗакрыватьФормуПослеЗавершенияОперации", Ложь);
	
	Кассир = Параметры.Кассир;
	ПраваДоступа = НастройкиПродажДляПользователейСервер.ПраваДоступаРМК(Кассир);
	
	КассаККМ = Параметры.КассаККМ;
	ПриИзмененииКассыККМ();
	
	НастроитьРМК();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если СохранятьНастройкиПриЗакрытии И НЕ ЗавершениеРаботы Тогда
		СохранитьНастройки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗакрыватьФормуПослеЗавершенияОперацииПриИзменении(Элемент)
	СохранятьНастройкиПриЗакрытии = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьКассуККМ(Команда)
	
	Отбор = Новый Структура("Ссылка", ДоступныеКассыККМ);
	ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму(
		"Справочник.КассыККМ.ФормаВыбора",
		ПараметрыОткрытия,,,,,
		Новый ОписаниеОповещения("ПослеИзмененияКассыККМ", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКассовуюСмену(Команда)
	
	ОчиститьСообщения();
	
	РозничныеПродажиКлиент.ЗакрытьКассовуюСмену(
		ПараметрыКассыККМ,
		Новый ОписаниеОповещения("ПослеЗавершенияОперацииСКассовойСменой", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКассовуюСмену(Команда)
	
	ОчиститьСообщения();
	
	РозничныеПродажиКлиент.ОткрытьКассовуюСмену(
		ПараметрыКассыККМ,
		Новый ОписаниеОповещения("ПослеЗавершенияОперацииСКассовойСменой", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ВнесениеДенежныхСредств(Команда)
	
	ОчиститьСообщения();
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		ЭтотОбъект,
		Новый ОписаниеОповещения("ВнесениеДенежныхСредствПослеОбработкиСостоянияСмены", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыемкаДенежныхСредств(Команда)
	
	ОчиститьСообщения();
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		ЭтотОбъект,
		Новый ОписаниеОповещения("ВыемкаДенежныхСредствПослеОбработкиСостоянияСмены", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДенежныйЯщик(Команда)
	
	ОчиститьСообщения();
	
	МенеджерОборудованияКлиент.НачатьОткрытиеДенежногоЯщика(
		Новый ОписаниеОповещения("ПослеЗавершенияОперацииОткрытияДенежногоЯщика", ЭтотОбъект),
		УникальныйИдентификатор,
		ПараметрыКассыККМ.ИдентификаторУстройства);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьНулевойЧек(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОповещенияЗавершение = Новый ОписаниеОповещения("ПростаяОперацияЗавершение", ЭтотОбъект);
	
	ИдентификаторУстройства                = ПараметрыКассыККМ.ИдентификаторУстройства;
	ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	ОборудованиеПодключено = МенеджерОборудованияУТКлиент.ОборудованиеПодключено(ИдентификаторУстройства);
	
	Если ОборудованиеПодключено ИЛИ ИспользоватьБезПодключенияОборудования Тогда
		
		ОписаниеОшибки = "";
		
		Результат = Ложь;
		
		Если Не ИспользоватьБезПодключенияОборудования Тогда
			
			ВходныеПараметры  = Неопределено;
			ВыходныеПараметры = Неопределено;
			
			//Напечатать нулевой чек на фискальном регистраторе
			Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
			                                                        "PrintNullReceipt",
			                                                        ВходныеПараметры,
			                                                        ВыходныеПараметры);
			
		КонецЕсли;
		
		Если Не Результат И Не ИспользоватьБезПодключенияОборудования Тогда
			
			ТекстСообщения = НСтр("ru='При печати чека произошла ошибка.
                                  |Чек не напечатан на фискальном регистраторе.
                                  |Дополнительное описание:
                                  |%ДополнительноеОписание%'
                                  |;uk='При друку чека виникла помилка.
                                  |Чек не надрукований на фіскальному реєстраторі.
                                  |Додатковий опис:
                                  |%ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,
			                             "%ДополнительноеОписание%",
			                             ВыходныеПараметры[1]);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияЗавершение, Истина);
			
	Иначе
		
		ТекстСообщения = НСтр("ru='При подключении устройства произошла ошибка.
                                    |Чек не напечатан на фискальном регистраторе.
                                    |Дополнительное описание:
                                    |%ДополнительноеОписание%'
                                    |;uk='При підключенні пристрою сталася помилка.
                                    |Чек не надрукований на фіскальному реєстраторі.
                                    |Додатковий опис:
                                    |%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияЗавершение, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДополнительнеОтчеты(Команда)
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.УправлениеФискальнымУстройством", 
		Новый Структура("ИдентификаторУстройства", ПараметрыКассыККМ.ИдентификаторУстройства), 
		ЭтаФорма,,,,
		Новый ОписаниеОповещения("ПростаяОперацияЗавершение", ЭтотОбъект), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте                           

Процедура ПростаяОперацияЗавершение(Результат, ДополнительныеПараметры) Экспорт	

	ОбновитьЗаголовкиФормы(ЭтотОбъект);
	
	Если ЗакрыватьФормуПослеЗавершенияОперации Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовкиФормы(Форма)
	
	Форма.СтруктураСостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(Форма.КассаККМ);
	
	Форма.Элементы.ОткрытьКассовуюСмену.Доступность = Форма.ПраваДоступа.ОткрытьСмену И Не Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта;
	Форма.Элементы.ЗакрытьКассовуюСмену.Доступность = Форма.ПраваДоступа.ЗакрытьСмену И    Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта;
	Форма.Элементы.ПробитьНулевойЧек.Доступность = Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта;

	Форма.Элементы.ВнесениеДенежныхСредств.Доступность = Форма.ПраваДоступа.ВнесениеДенег;
	Форма.Элементы.ВыемкаДенежныхСредств.Доступность   = Форма.ПраваДоступа.ВыемкаДенег;
	
	Форма.Элементы.ГруппаКассаККМ.Заголовок = Строка(Форма.КассаККМ);
	
	Если ЗначениеЗаполнено(Форма.СтруктураСостояниеКассовойСмены.СтатусКассовойСмены) Тогда
		
		ЗаголовокГруппыОперацииСКассовойСменой = НСтр("ru='Статус смены: %СтатусСмены% %ВремяИзменения%';uk='Статус зміни: %СтатусСмены% %ВремяИзменения%'");
		
		ЗаголовокГруппыОперацииСКассовойСменой = СтрЗаменить(ЗаголовокГруппыОперацииСКассовойСменой, "%СтатусСмены%", Форма.СтруктураСостояниеКассовойСмены.СтатусКассовойСмены);
		ЗаголовокГруппыОперацииСКассовойСменой = СтрЗаменить(ЗаголовокГруппыОперацииСКассовойСменой, "%ВремяИзменения%", Формат(Форма.СтруктураСостояниеКассовойСмены.ДатаИзмененияСтатуса,"ДФ='dd.MM.yy ЧЧ:мм'"));
		
	Иначе
		
		ЗаголовокГруппыОперацииСКассовойСменой = НСтр("ru='Смена не открыта';uk='Зміна не відкрита'");
		
	КонецЕсли;
	Форма.Элементы.ГруппаОперацииСКассовойСменой.Заголовок = ЗаголовокГруппыОперацииСКассовойСменой;
	
	ЗаголовокГруппыДенежныеОперации = НСтр("ru='В кассе %НаличностьВКассе% %Валюта%';uk='В касі %НаличностьВКассе% %Валюта%'");
	
	перНаличностьВКассе = Формат(Форма.СтруктураСостояниеКассовойСмены.НаличностьВКассе, "ЧДЦ=2"); 	
	ЗаголовокГруппыДенежныеОперации = СтрЗаменить(ЗаголовокГруппыДенежныеОперации, "%НаличностьВКассе%", перНаличностьВКассе);
	ЗаголовокГруппыДенежныеОперации = СтрЗаменить(ЗаголовокГруппыДенежныеОперации, "%Валюта%", Форма.СтруктураСостояниеКассовойСмены.ВалютаПредставление);
	Форма.Элементы.ГруппаДенежныеОперации.Заголовок = ЗаголовокГруппыДенежныеОперации;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Документы.ЧекККМ.Форма.МенюОперацийСККМ", "ЗакрыватьФормуПослеЗавершенияОперации", ЗакрыватьФормуПослеЗавершенияОперации);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьРМК()
	
	ФормированиеФискальныхЧековСервер.ЗаполнитьДоступныеКассыККМ(ДоступныеКассыККМ, КассаККМ);
	
	Элементы.ИзменитьКассуККМ.Видимость = ДоступныеКассыККМ.Количество() > 1 И Параметры.ИзменитьКассуККМ;
	Элементы.ОткрытьДенежныйЯщик.Видимость = Не ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	РозничныеПродажи.ПодписатьГорячиеКлавишиНаКнопках(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКассыККМ()
	
	СтруктураСостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(КассаККМ);
	ПараметрыКассыККМ = Новый ФиксированнаяСтруктура(Справочники.КассыККМ.ПараметрыКассыККМ(КассаККМ));
	ОбновитьЗаголовкиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияОперацииОткрытияДенежногоЯщика(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда  
		ЗаголовокИнформации = НСтр("ru='При открытии денежного ящика возникла ошибка.';uk='При відкритті грошового ящика виникла помилка.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияКассыККМ(ВыбраннаяКассаККМ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ВыбраннаяКассаККМ) Тогда
		Возврат;
	КонецЕсли;
	
	КассаККМ = ВыбраннаяКассаККМ;
	
	ПриИзмененииКассыККМ();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьКассуККМЗавершение", ВладелецФормы);
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, КассаККМ);
	
	Если ЗакрыватьФормуПослеЗавершенияОперации Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияОперацииСКассовойСменой(Результат, ДополнительныеПараметры) Экспорт
	
  	ОбновитьЗаголовкиФормы(ЭтотОбъект);
	
	Если ЗакрыватьФормуПослеЗавершенияОперации Тогда
		Закрыть();
	КонецЕсли;
	
	Оповестить("ИзменениеКассовойСмены");
	
КонецПроцедуры

&НаКлиенте
Процедура ВнесениеДенежныхСредствПослеОбработкиСостоянияСмены(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	РозничныеПродажиКлиент.ВнесениеДенежныхСредств(
		ЭтотОбъект,
		ПараметрыКассыККМ,
		Новый ОписаниеОповещения("ПослеВнесенияВыемкиДенежныхСредств", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыемкаДенежныхСредствПослеОбработкиСостоянияСмены(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	РозничныеПродажиКлиент.ВыемкаДенежныхСредств(
		ЭтотОбъект,
		ПараметрыКассыККМ,
		Новый ОписаниеОповещения("ПослеВнесенияВыемкиДенежныхСредств", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВнесенияВыемкиДенежныхСредств(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьЗаголовкиФормы(ЭтотОбъект);
	
	Если ЗакрыватьФормуПослеЗавершенияОперации Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти