#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	РаспределениеРасходовБудущихПериодовЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		
		РаспределениеРасходовБудущихПериодовЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	РаспределениеРасходовБудущихПериодовЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Распределение РБП".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеРасходовБудущихПериодов) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РаспределениеРасходовБудущихПериодов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеРасходовБудущихПериодов);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУчетПрочихДоходовРасходов";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
	РаспределениеРасходовБудущихПериодовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

//++ НЕ УТ

// Возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеРасходовБудущихПериодовЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеРасходовБудущихПериодовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

//-- НЕ УТ

// Возвращает параметры выбора статей в документе.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	МассивПаметровВыбора = Новый Массив;
	
	#Область СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область РаспределениеРасходовСтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.РаспределениеРасходов";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РаспределениеРасходовСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("РаспределениеРасходовАналитикаРасходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ТекстЗапросаРасходыКРаспределению(Первые = Ложь) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Данные.Период КАК Период,
		|	Данные.Организация КАК Организация,
		|	Данные.НалоговоеНазначение,
		|	Данные.Подразделение КАК Подразделение,
		|	Данные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Данные.СтатьяРасходов КАК СтатьяРасходов,
		|	Данные.СтатьяРасходов.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	Данные.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	Данные.АналитикаРасходов КАК АналитикаРасходов,
		|	Данные.СуммаПриход - Данные.СуммаРасход КАК Сумма,
		|	Данные.СуммаУпрПриход - Данные.СуммаУпрРасход КАК СуммаУпр,
		|	Данные.СуммаРеглПриход - Данные.СуммаРеглРасход КАК СуммаРегл,
		|	Данные.СуммаРеглБезНДСПриход - Данные.СуммаРеглБезНДСРасход КАК СуммаРеглБезНДС,
		|	Данные.НДСРеглПриход - Данные.НДСРеглРасход КАК НДСРегл,
		|	Данные.СуммаРеглПриход - Данные.СуммаРеглРасход - (Данные.ПостояннаяРазницаПриход - Данные.ПостояннаяРазницаРасход) - (Данные.ВременнаяРазницаПриход - Данные.ВременнаяРазницаРасход) КАК СуммаНУ,
		|	Данные.ПостояннаяРазницаПриход - Данные.ПостояннаяРазницаРасход КАК ПостояннаяРазница,
		|	Данные.ВременнаяРазницаПриход - Данные.ВременнаяРазницаРасход КАК ВременнаяРазница
		|ИЗ
		|	(ВЫБРАТЬ
		|		Данные.Организация КАК Организация,
		|		Данные.НалоговоеНазначение КАК НалоговоеНазначение,
		|		Данные.Подразделение КАК Подразделение,
		|		Данные.СтатьяРасходов КАК СтатьяРасходов,
		|		Данные.АналитикаРасходов КАК АналитикаРасходов,
		|		Данные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		СУММА(Данные.СуммаПриход) КАК СуммаПриход,
		|		СУММА(Данные.СуммаУпрПриход) КАК СуммаУпрПриход,
		|		СУММА(Данные.СуммаРеглПриход) КАК СуммаРеглПриход,
		|		СУММА(Данные.СуммаРеглБезНДСПриход) КАК СуммаРеглБезНДСПриход,
		|		СУММА(Данные.НДСРеглПриход) КАК НДСРеглПриход,
		|		СУММА(Данные.ПостояннаяРазницаПриход) КАК ПостояннаяРазницаПриход,
		|		СУММА(Данные.ВременнаяРазницаПриход) КАК ВременнаяРазницаПриход,
		|		СУММА(Данные.СуммаРасход) КАК СуммаРасход,
		|		СУММА(Данные.СуммаУпрРасход) КАК СуммаУпрРасход,
		|		СУММА(Данные.СуммаРеглРасход) КАК СуммаРеглРасход,
		|		СУММА(Данные.СуммаРеглБезНДСРасход) КАК СуммаРеглБезНДСРасход,
		|		СУММА(Данные.НДСРеглРасход) КАК НДСРеглРасход,
		|		СУММА(Данные.ПостояннаяРазницаРасход) КАК ПостояннаяРазницаРасход,
		|		СУММА(Данные.ВременнаяРазницаРасход) КАК ВременнаяРазницаРасход,
		|		СУММА(Данные.СуммаОстаток) КАК СуммаОстаток,
		|		СУММА(Данные.СуммаУпрОстаток) КАК СуммаУпрОстаток,
		|		СУММА(Данные.СуммаРеглОстаток) КАК СуммаРеглОстаток,
		|		СУММА(Данные.СуммаРеглБезНДСОстаток) КАК СуммаРеглБезНДСОстаток,
		|		СУММА(Данные.НДСРеглОстаток) КАК НДСРеглОстаток,
		|		СУММА(Данные.ПостояннаяРазницаОстаток) КАК ПостояннаяРазницаОстаток,
		|		СУММА(Данные.ВременнаяРазницаОстаток) КАК ВременнаяРазницаОстаток,
		|		МАКСИМУМ(Данные.Период) КАК Период,
		|		МИНИМУМ(Данные.БезСумм) КАК БезСумм
		|	ИЗ
		|		(ВЫБРАТЬ &Первые,
		|			Приходы.Период КАК Период,
		|			Приходы.Организация КАК Организация,
		|			Приходы.НалоговоеНазначение КАК НалоговоеНазначение,
		|			Приходы.Подразделение КАК Подразделение,
		|			Приходы.СтатьяРасходов КАК СтатьяРасходов,
		|			Приходы.АналитикаРасходов КАК АналитикаРасходов,
		|			Приходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|			Приходы.Сумма КАК СуммаПриход,
		|			Приходы.СуммаУпр КАК СуммаУпрПриход,
		|			0 КАК СуммаРеглПриход,
		|			0 КАК СуммаРеглБезНДСПриход,
		|			0 КАК НДСРеглПриход,
		|			0 КАК ПостояннаяРазницаПриход,
		|			0 КАК ВременнаяРазницаПриход,
		|			0 КАК СуммаРасход,
		|			0 КАК СуммаУпрРасход,
		|			0 КАК СуммаРеглРасход,
		|			0 КАК СуммаРеглБезНДСРасход,
		|			0 КАК НДСРеглРасход,
		|			0 КАК ПостояннаяРазницаРасход,
		|			0 КАК ВременнаяРазницаРасход,
		|			0 КАК СуммаОстаток,
		|			0 КАК СуммаУпрОстаток,
		|			0 КАК СуммаРеглОстаток,
		|			0 КАК СуммаРеглБезНДСОстаток,
		|			0 КАК НДСРеглОстаток,
		|			0 КАК ПостояннаяРазницаОстаток,
		|			0 КАК ВременнаяРазницаОстаток,
		|			ЛОЖЬ КАК БезСумм
		|		ИЗ
		|			РегистрНакопления.ПрочиеРасходы КАК Приходы
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|				ПО (Статьи.Ссылка = Приходы.СтатьяРасходов)
		|					И (Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК Распределение
		|				ПО (НАЧАЛОПЕРИОДА(Распределение.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Приходы.Период, МЕСЯЦ))
		|					И (Распределение.Проведен)
		|					И (Распределение.ВариантУказанияСуммыУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыУказанияСуммыРБП.ОпределяетсяАвтоматически))
		|					И (Распределение.Организация = Приходы.Организация)
		|					И (Распределение.Подразделение = Приходы.Подразделение)
		|					И (Распределение.СтатьяРасходов = Приходы.СтатьяРасходов)
		|					И (Распределение.АналитикаРасходов = Приходы.АналитикаРасходов)
		|					И (Распределение.НаправлениеДеятельности = Приходы.НаправлениеДеятельности)
		|		ГДЕ
		|			НАЧАЛОПЕРИОДА(Приходы.Период, ДЕНЬ) <= &ДатаОкончания
		|			И Приходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Распределение.Ссылка ЕСТЬ NULL
		|			И (&ПоВсемОрганизациям ИЛИ Приходы.Организация = &Организация)
		|			И (&ПоВсемПодразделениям ИЛИ Приходы.Подразделение = &Подразделение)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ &Первые,
		|			Приходы.Период,
		|			Приходы.Организация,
		|			Приходы.НалоговоеНазначение,
		|			Приходы.Подразделение,
		|			Приходы.СтатьяРасходов,
		|			Приходы.АналитикаРасходов,
		|			Приходы.НаправлениеДеятельности,
		|			0,
		|			0,
		|			Приходы.СуммаРегл,
		|			Приходы.СуммаРеглБезНДС КАК СуммаРеглБезНДСПриход,
		|			Приходы.НДСРегл КАК НДСРеглПриход,
		|			Приходы.ПостояннаяРазница,
		|			Приходы.ВременнаяРазница,
		|			0,
		|			0,
		|			0, 
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			ЛОЖЬ
		|		ИЗ
		|			РегистрНакопления.ПрочиеРасходы КАК Приходы
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|				ПО (Статьи.Ссылка = Приходы.СтатьяРасходов)
		|					И (Статьи.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК Распределение
		|				ПО (НАЧАЛОПЕРИОДА(Распределение.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Приходы.Период, МЕСЯЦ))
		|					И (Распределение.Проведен)
		|					И (Распределение.ВариантУказанияСуммыРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыУказанияСуммыРБП.ОпределяетсяАвтоматически))
		|					И (Распределение.Организация = Приходы.Организация)
		|					И (Распределение.Подразделение = Приходы.Подразделение)
		|					И (Распределение.СтатьяРасходов = Приходы.СтатьяРасходов)
		|					И (Распределение.АналитикаРасходов = Приходы.АналитикаРасходов)
		|					И (Распределение.НаправлениеДеятельности = Приходы.НаправлениеДеятельности)
		|		ГДЕ
		|			НАЧАЛОПЕРИОДА(Приходы.Период, ДЕНЬ) <= &ДатаОкончания
		|			И Приходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Распределение.Ссылка ЕСТЬ NULL
		|			И (&ПоВсемОрганизациям ИЛИ Приходы.Организация = &Организация)
		|			И (&ПоВсемПодразделениям ИЛИ Приходы.Подразделение = &Подразделение)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ &Первые,
		|			Расходы.Период,
		|			Расходы.Организация,
		|			Расходы.НалоговоеНазначение,
		|			Расходы.Подразделение,
		|			Расходы.СтатьяРасходов,
		|			Расходы.АналитикаРасходов,
		|			Расходы.НаправлениеДеятельности,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			Расходы.Сумма,
		|			Расходы.СуммаУпр,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			ЛОЖЬ
		|		ИЗ
		|			РегистрНакопления.ПрочиеРасходы КАК Расходы
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|				ПО (Статьи.Ссылка = Расходы.СтатьяРасходов)
		|					И (Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК Распределение
		|				ПО (Распределение.Ссылка = Расходы.Регистратор)
		|		ГДЕ
		|			НАЧАЛОПЕРИОДА(ЕСТЬNULL(Распределение.Дата, Расходы.Период), ДЕНЬ) <= &ДатаОкончания
		|			И Расходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ Расходы.РасчетСебестоимости
		|			И (&ПоВсемОрганизациям ИЛИ Расходы.Организация = &Организация)
		|			И (&ПоВсемПодразделениям ИЛИ Расходы.Подразделение = &Подразделение)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ &Первые,
		|			Расходы.Период,
		|			Расходы.Организация,
		|			Расходы.НалоговоеНазначение,
		|			Расходы.Подразделение,
		|			Расходы.СтатьяРасходов,
		|			Расходы.АналитикаРасходов,
		|			Расходы.НаправлениеДеятельности,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			Расходы.СуммаРегл,
		|			Расходы.СуммаРеглБезНДС,
		|			Расходы.НДСРегл,
		|			Расходы.ПостояннаяРазница,
		|			Расходы.ВременнаяРазница,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			ЛОЖЬ
		|		ИЗ
		|			РегистрНакопления.ПрочиеРасходы КАК Расходы
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|				ПО (Статьи.Ссылка = Расходы.СтатьяРасходов)
		|					И (Статьи.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК Распределение
		|				ПО (Распределение.Ссылка = Расходы.Регистратор)
		|		ГДЕ
		|			НАЧАЛОПЕРИОДА(ЕСТЬNULL(Распределение.Дата, Расходы.Период), ДЕНЬ) <= &ДатаОкончания
		|			И Расходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ Расходы.РасчетСебестоимости
		|			И (&ПоВсемОрганизациям ИЛИ Расходы.Организация = &Организация)
		|			И (&ПоВсемПодразделениям ИЛИ Расходы.Подразделение = &Подразделение)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ &Первые,
		|			Себестоимость.Период,
		|			Себестоимость.Организация,
		|			Себестоимость.НалоговоеНазначение,
		|			Себестоимость.Подразделение,
		|			Себестоимость.СтатьяРасходовСписания,
		|			Себестоимость.АналитикаРасходов,
		|			ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			ИСТИНА
		|		ИЗ
		|			РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|				ПО (Статьи.Ссылка = Себестоимость.СтатьяРасходовСписания)
		|					И (Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)
		|						ИЛИ Статьи.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|				ПО (Назначения.Ссылка = Себестоимость.АналитикаУчетаНоменклатуры.Назначение)
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК Распределение
		|				ПО (НАЧАЛОПЕРИОДА(Распределение.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Себестоимость.Период, МЕСЯЦ))
		|					И (Распределение.Проведен)
		|					И (Распределение.Организация = Себестоимость.Организация)
		|					И (Распределение.Подразделение = Себестоимость.Подразделение)
		|					И (Распределение.СтатьяРасходов = Себестоимость.СтатьяРасходовСписания)
		|					И (Распределение.АналитикаРасходов = Себестоимость.АналитикаРасходов)
		|					И (Распределение.НаправлениеДеятельности = ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)))
		|		ГДЕ
		|			Себестоимость.Период <= &ДатаОкончания
		|			И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И Распределение.Ссылка ЕСТЬ NULL
		|			И (&ПоВсемОрганизациям ИЛИ Себестоимость.Организация = &Организация)
		|			И (&ПоВсемПодразделениям ИЛИ Себестоимость.Подразделение = &Подразделение)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ &Первые,
		|			ДАТАВРЕМЯ(1, 1, 1),
		|			Остатки.Организация,
		|			Остатки.НалоговоеНазначение,
		
		|			Остатки.Подразделение,
		|			Остатки.СтатьяРасходов,
		|			Остатки.АналитикаРасходов,
		|			Остатки.НаправлениеДеятельности,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			0,
		|			Остатки.СуммаОстаток,
		|			Остатки.СуммаУпрОстаток,
		|			Остатки.СуммаРеглОстаток,
		|			Остатки.СуммаРеглБезНДСОстаток,
		|			Остатки.НДСРеглОстаток,
		|			Остатки.ПостояннаяРазницаОстаток,
		|			Остатки.ВременнаяРазницаОстаток,
		|			ЛОЖЬ
		|		ИЗ
		|			РегистрНакопления.ПрочиеРасходы.Остатки(
		|					, (&ПоВсемОрганизациям ИЛИ Организация = &Организация)
		|						И (&ПоВсемПодразделениям ИЛИ Подразделение = &Подразделение)
		|					И СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)
		|						ИЛИ СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)) КАК Остатки) КАК Данные
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Данные.Организация,
		|		Данные.НалоговоеНазначение,
		|		Данные.Подразделение,
		|		Данные.СтатьяРасходов,
		|		Данные.АналитикаРасходов,
		|		Данные.НаправлениеДеятельности) КАК Данные
		|ГДЕ
		|	(Данные.СуммаПриход - Данные.СуммаРасход <> 0
		|				И Данные.СуммаОстаток <> 0
		|			ИЛИ Данные.СуммаУпрПриход - Данные.СуммаУпрРасход <> 0
		|				И Данные.СуммаУпрОстаток <> 0
		|			ИЛИ Данные.СуммаРеглПриход - Данные.СуммаРеглРасход <> 0
		|				И Данные.СуммаРеглОстаток <> 0
		|			ИЛИ Данные.СуммаРеглБезНДСПриход - Данные.СуммаРеглБезНДСРасход <> 0
		|				И Данные.СуммаРеглБезНДСОстаток <> 0
		|			ИЛИ Данные.НДСРеглПриход - Данные.НДСРеглРасход <> 0
		|				И Данные.НДСРеглОстаток <> 0
		|			ИЛИ Данные.ПостояннаяРазницаПриход - Данные.ПостояннаяРазницаРасход <> 0
		|				И Данные.ПостояннаяРазницаОстаток <> 0
		|			ИЛИ Данные.ВременнаяРазницаПриход - Данные.ВременнаяРазницаРасход <> 0
		|				И Данные.ВременнаяРазницаОстаток <> 0
		|			ИЛИ Данные.БезСумм)";
	
	Если Первые Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Первые,", "ПЕРВЫЕ 1");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Первые,", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	РаспределениеРасходовБудущихПериодовЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	Запрос.УстановитьПараметр("Валюта", Константы.ВалютаУправленческогоУчета.Получить());
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	Запрос.УстановитьПараметр("Организация",                                Реквизиты.Организация);

КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.Дата                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов          КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов       КАК АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
    |	ДанныеДокумента.НалоговоеНазначение 	КАК НалоговоеНазначение,
	|	ДанныеДокумента.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	Строки.Подразделение                    КАК КорПодразделение,
	|	Строки.СтатьяРасходов					КАК КорСтатьяРасходов,
	|	Строки.АналитикаРасходов				КАК КорАналитикаРасходов,
	|	СУММА(Строки.Сумма)                            КАК СуммаСНДС,
	|	СУММА(Строки.СуммаУпр)                         КАК СуммаБезНДС,
	|	СУММА(Строки.СуммаУпр)                         КАК СуммаБезНДСУпр,
	|	СУММА(Строки.СуммаРегл)                        КАК СуммаСНДСРегл,
	|	СУММА(Строки.СуммаРеглБезНДС)                  КАК СуммаБезНДСРегл,
    |	СУММА(Строки.НДСРегл) 					   	   КАК НДСРегл,
	|	СУММА(Строки.ПостояннаяРазница)                КАК ПостояннаяРазница,
	|	СУММА(Строки.ВременнаяРазница)                 КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаУпр <> 0
	|		ИЛИ Строки.СуммаРегл <> 0
	|		ИЛИ Строки.ПостояннаяРазница <> 0
	|		ИЛИ Строки.ВременнаяРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Дата,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.НалоговоеНазначение,
	|	Строки.Подразделение,
	|	Строки.СтатьяРасходов,
	|	Строки.АналитикаРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Строки.Дата								КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	ДанныеДокумента.Организация				КАК Организация,
	|	Строки.Подразделение                    КАК Подразделение,
	|	Строки.СтатьяРасходов					КАК СтатьяРасходов,
	|	Строки.АналитикаРасходов				КАК АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НалоговоеНазначение		КАК НалоговоеНазначение,
	|	ДанныеДокумента.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение           КАК КорПодразделение,
	|	ДанныеДокумента.СтатьяРасходов          КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов       КАК КорАналитикаРасходов,
	|	СУММА(Строки.Сумма)                     КАК СуммаСНДС,
	|	СУММА(Строки.СуммаУпр)                  КАК СуммаБезНДС,
	|	СУММА(Строки.СуммаУпр)                  КАК СуммаБезНДСУпр,
	|	СУММА(Строки.СуммаРегл)                 КАК СуммаСНДСРегл,
	|	СУММА(Строки.СуммаРеглБезНДС)           КАК СуммаБезНДСРегл,
    |	СУММА(Строки.НДСРегл) 					КАК НДСРегл,
	|	СУММА(Строки.ПостояннаяРазница)         КАК ПостояннаяРазница,
	|	СУММА(Строки.ВременнаяРазница)          КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаУпр <> 0
	|		ИЛИ Строки.СуммаРегл <> 0
	|		ИЛИ Строки.ПостояннаяРазница <> 0
	|		ИЛИ Строки.ВременнаяРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Дата,
	|	ДанныеДокумента.Организация,
	|	Строки.Подразделение,
	|	Строки.СтатьяРасходов,
	|	Строки.АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.НалоговоеНазначение,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ДополнительныеПоля = "КорНаправлениеДеятельности,КорПодразделение,КорСтатьяРасходов,КорАналитикаРасходов";
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ДополнительныеПоля = "КорНаправлениеДеятельности,КорПодразделение,КорСтатьяРасходов,КорАналитикаРасходов";
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.Дата                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	Строки.Ссылка.Организация               КАК Организация,
	|	ВЫБОР
	|		КОГДА Строки.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Строки.Подразделение
	|		ИНАЧЕ Строки.Ссылка.Подразделение
	|	КОНЕЦ                                   КАК Подразделение,
	|	&Ссылка                                 КАК ДокументПоступленияРасходов,
	|	Строки.СтатьяРасходов                   КАК СтатьяРасходов,
	|	Строки.АналитикаРасходов                КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаПартий,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаНоменклатуры,
    |	ДанныеДокумента.НалоговоеНазначение 	КАК НалоговоеНазначение,
	|	
	|	Строки.Сумма                            КАК Стоимость,
	|	Строки.СуммаУпр                         КАК СтоимостьБезНДС,
	|	0                                       КАК НДСУпр,
	|	Строки.СуммаРегл                        КАК СтоимостьРегл,
	|	Строки.ПостояннаяРазница                КАК ПостояннаяРазница,
	|	Строки.ВременнаяРазница                 КАК ВременнаяРазница,
	|	Строки.НДСРегл                          КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП) КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаУпр <> 0
	|		ИЛИ Строки.СуммаРегл <> 0
	|		ИЛИ Строки.ПостояннаяРазница <> 0
	|		ИЛИ Строки.ВременнаяРазница <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.НомерСтроки,
	|	Строки.Дата КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов КАК Статья,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|
	|	Строки.Подразделение КАК КорПодразделение,
	|	Строки.СтатьяРасходов КАК КорСтатья,
	|	Строки.АналитикаРасходов КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|
	|	Строки.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		 ИЛИ НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ
	|			Строки.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	Строки.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	Строки.Сумма КАК СуммаВВалюте
	|
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК Строки
	|		ПО Строки.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаУпр <> 0 ИЛИ Строки.СуммаРегл <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт
	
#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.РаспределениеРасходовБудущихПериодов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1d50840e-b291-4de8-8b9c-13d8b0fae95c");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РаспределениеРасходовБудущихПериодов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Документ.РаспределениеРасходовБудущихПериодов";
	Обработчик.ИзменяемыеОбъекты = "Документ.РаспределениеРасходовБудущихПериодов";
	Обработчик.БлокируемыеОбъекты = "Документ.РаспределениеРасходовБудущихПериодов";
	Обработчик.Комментарий = НСтр("ru='В документе ""Распределение расходов будущих периодов"" заполняются новые реквизиты ""Правило распределения"", ""Вариант распределения суммы (упр.)"" и ""Вариант распределения суммы (регл.)"".';uk='У документі ""Розподіл витрат майбутніх періодів"" заповнюються нові реквізити ""Правило розподілу"", ""Варіант розподілу суми (упр.)"" та ""Варіант розподілу суми (регл.)"".'");

#КонецОбласти
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК Операция
	|ГДЕ
	|	(Операция.ПравилоРаспределения = ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияРБП.ПустаяСсылка)
	|		ИЛИ Операция.ВариантУказанияСуммыУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыУказанияСуммыРБП.ПустаяСсылка)
	|		ИЛИ Операция.ВариантУказанияСуммыРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыУказанияСуммыРБП.ПустаяСсылка))
	|	И Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.РаспределениеРасходовБудущихПериодов";
	МетаданныеОбъекта = Метаданные.Документы.РаспределениеРасходовБудущихПериодов;
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц
	);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если Не ЗначениеЗаполнено(ДокументОбъект.ПравилоРаспределения) Тогда
				ДокументОбъект.ПравилоРаспределения = Перечисления.ПравилаРаспределенияРБП.ПоМесяцам;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДокументОбъект.ВариантУказанияСуммыУпр) Тогда
				ДокументОбъект.ВариантУказанияСуммыУпр = Перечисления.ВариантыУказанияСуммыРБП.УказываетсяВручную;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДокументОбъект.ВариантУказанияСуммыРегл) Тогда
				ДокументОбъект.ВариантУказанияСуммыРегл = Перечисления.ВариантыУказанияСуммыРБП.УказываетсяВручную;
			КонецЕсли;
			
			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Ложь, Ложь, РежимЗаписиДокумента.Запись);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();

		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось обработать документ: %1 по причине: %2';uk='Не вдалося обробити документ: %1 з причини: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения
			);
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеПроизводства

Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.РаспределениеРасходовБудущихПериодов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаРасходыКРаспределению(Истина);
	
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Параметры.Период));
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Не ЗначениеЗаполнено(Параметры.Организация));
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Не ЗначениеЗаполнено(Параметры.Подразделение));
	
	ТекстГиперссылки = НСтр("ru='Распределение расходов будущих периодов';uk='Розподіл витрат майбутніх періодів'");
	
	УстановитьПривилегированныйРежим(Истина);
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,,
			"Документ.РаспределениеРасходовБудущихПериодов.Форма.ФормаСпискаДокументов");
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,,
			"Документ.РаспределениеРасходовБудущихПериодов.Форма.ФормаСпискаДокументов");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
