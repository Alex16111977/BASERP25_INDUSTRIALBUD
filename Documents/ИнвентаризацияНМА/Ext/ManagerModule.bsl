
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти


// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПеремещениеНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ПринятиеКУчетуНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.СписаниеНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
		
КонецПроцедуры

// Добавляет команду создания документа на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИнвентаризацияНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ИнвентаризацияНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИнвентаризацияНМА);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
		
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИнвентаризацияНМА";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
    ПереопределениеРасчетаПараметров.Вставить("Подразделение", "ДанныеДокумента.Ссылка.ПодразделениеОрганизации");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодразделениеОрганизации КАК Подразделение,
	|	ДанныеДокумента.Ответственный
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИнвентаризацияНМА"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИнвентаризацияНМА);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период                                 КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТабличнаяЧасть.НомерСтроки-1, 0)  КАК НомерЗаписи,
	|	&Ссылка                               КАК Ссылка,
	|	&Период                               КАК Дата,
	|	&Организация                          КАК Организация,
	|	&Подразделение                        КАК Подразделение,
	|	&Проведен                             КАК Проведен,
	|	&ХозяйственнаяОперация                КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных              КАК ТипСсылки,
	|	ЛОЖЬ                                  КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                  КАК ОтражатьВУпрУчете,
	|	ТабличнаяЧасть.НематериальныйАктив    КАК НематериальныйАктив
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.НМА КАК ТабличнаяЧасть
	|		ПО ТабличнаяЧасть.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт



	// НА-4 (Инвентаризационная опись)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НА4";
	КомандаПечати.Представление = НСтр("ru='НА-4 (Инвентаризационная опись)';uk='НА-4 (Інвентаризаційний опис)'");

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НА4") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"НА4",
			НСтр("ru='НА-4 (Инвентаризационная опись)';uk='НА-4 (Інвентаризаційний опис)'"),
		ПечатьНА4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
		
КонецПроцедуры

Функция ПечатьНА4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Для каждого Ссылка Из МассивОбъектов Цикл
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияНМА.Дата КАК Дата,
		|	ИнвентаризацияНМА.Номер КАК Номер,
		|	ИнвентаризацияНМА.Организация КАК Организация,
		|	ИнвентаризацияНМА.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ИнвентаризацияНМА.ПодразделениеОрганизации.Представление КАК ПодразделениеПредставление,
		|	ИнвентаризацияНМА.ПодразделениеОрганизации.Код КАК ПодразделениеКод,
		|	ИнвентаризацияНМА.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
		|	ИнвентаризацияНМА.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
		|	ИнвентаризацияНМА.ДокументОснованиеВид КАК ДокументОснованиеВид,
		|	ИнвентаризацияНМА.ДокументОснованиеДата КАК ДокументОснованиеДата,
		|	ИнвентаризацияНМА.ДокументОснованиеНомер КАК ДокументОснованиеНомер
		|ИЗ
		|	Документ.ИнвентаризацияНМА КАК ИнвентаризацияНМА
		|ГДЕ
		|	ИнвентаризацияНМА.Ссылка = &Ссылка";
		
		Док = Запрос.Выполнить().Выбрать();
		Док.Следующий();
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка.ПолучитьОбъект());
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",      Ссылка);
		Запрос.УстановитьПараметр("Организация", Док.Организация);
		Запрос.УстановитьПараметр("Дата",        Док.Дата);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияНМАНМА.НомерСтроки КАК НомерСтроки,
		|	ИнвентаризацияНМАНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ИнвентаризацияНМАНМА.НематериальныйАктив.НаименованиеПолное КАК НематериальныйАктивНаименованиеПолное,
		|	ИнвентаризацияНМАНМА.НаличиеПоДаннымУчета КАК Количество,
		|	ИнвентаризацияНМАНМА.НаличиеФактическое КАК ФактКоличество,
		|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
		|	ИнвентаризацияНМАНМА.НакопленнаяАмортизация КАК НакопленнаяАмортизация,
		|	ИнвентаризацияНМАНМА.СтоимостьПоДаннымУчета КАК Стоимость,
		|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ   
		|ИЗ
		|	Документ.ИнвентаризацияНМА.НМА КАК ИнвентаризацияНМАНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И НематериальныйАктив В
		|						(ВЫБРАТЬ
		|							ИнвентаризацияНМАНМА.НематериальныйАктив
		|						ИЗ
		|							Документ.ИнвентаризацияНМА.НМА КАК ИнвентаризацияНМАНМА
		|						ГДЕ
		|							ИнвентаризацияНМАНМА.Ссылка = &Ссылка)) КАК ПервоначальныеСведенияНМА
		|		ПО ИнвентаризацияНМАНМА.НематериальныйАктив = ПервоначальныеСведенияНМА.НематериальныйАктив
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииНМАБУ.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И НематериальныйАктив В
		|						(ВЫБРАТЬ
		|							ИнвентаризацияНМАНМА.НематериальныйАктив
		|						ИЗ
		|							Документ.ИнвентаризацияНМА.НМА КАК ИнвентаризацияНМАНМА
		|						ГДЕ
		|							ИнвентаризацияНМАНМА.Ссылка = &Ссылка)) КАК ПараметрыАмортизацииНМАБУ
		|		ПО ИнвентаризацияНМАНМА.НематериальныйАктив = ПараметрыАмортизацииНМАБУ.НематериальныйАктив
		|ГДЕ
		|	ИнвентаризацияНМАНМА.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

		ТаблицаНМА = Запрос.Выполнить().Выгрузить();
		
		Макет = ПолучитьМакет("ПФ_MXL_UK_НА4");
		
		// печать производится на языке, указанном в настройках пользователя
		//КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормироваияПечатныхФорм(УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "РежимФормированияПечатныхФорм"));
		КодЯзыкаПечать = "uk";
		//Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		Макет.КодЯзыкаМакета = "ru";
		
		// Получаем области макета для вывода в табличный документ
		Шапка 				= Макет.ПолучитьОбласть("Шапка");
		ЗаголовокТаблицы 	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		СтрокаТаблицы 		= Макет.ПолучитьОбласть("СтрокаТаблицы");
		Подвал 				= Макет.ПолучитьОбласть("Подвал");
		Расписка			= Макет.ПолучитьОбласть("Расписка");
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка.ПолучитьОбъект());
		
		
		ТабДокумент = Новый ТабличныйДокумент;
		
		// Зададим параметры макета по умолчанию
		ТабДокумент.ПолеСверху              = 10;
		ТабДокумент.ПолеСлева               = 0;
		ТабДокумент.ПолеСнизу               = 0;
		ТабДокумент.ПолеСправа              = 0;
		ТабДокумент.РазмерКолонтитулаСверху = 10;
		ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
		//ТабДокумент.АвтоМасштаб             = Истина;
		
		// Загрузим настройки пользователя
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНМА_НА4";

		// Выведем шапку документа
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		//
		Шапка.Параметры.Заполнить(Док);
		Шапка.Параметры.НаименованиеПолноеОрганизации 	= Док.ОрганизацияНаименованиеПолное;
		Шапка.Параметры.ЕДРПОУ 	= ФормированиеПечатныхФорм.ПолучитьКодОрганизации(СведенияОбОрганизации);
		
		// краткий формат дат
		Шапка.Параметры.ДокументОснованиеДата 			= Формат(Док.ДокументОснованиеДата, 		"ДЛФ=DD;Л =" + МультиязычностьУкр.ОпределитьКодЯзыкаДляФормат(КодЯзыкаПечать));
		Шапка.Параметры.ДатаНачалаИнвентаризации 		= Формат(Док.ДатаНачалаИнвентаризации, 		"ДЛФ=DD;Л =" + МультиязычностьУкр.ОпределитьКодЯзыкаДляФормат(КодЯзыкаПечать));
		Шапка.Параметры.ДатаОкончанияИнвентаризации 	= Формат(Док.ДатаОкончанияИнвентаризации, 	"ДЛФ=DD;Л =" + МультиязычностьУкр.ОпределитьКодЯзыкаДляФормат(КодЯзыкаПечать));
		
		Шапка.Параметры.НомерДок = Док.Номер;
		//Шапка.Параметры.ДатаДокумента  = Док.Дата;
		
		ОсновныеСотрудникиФизическихЛицМОЛ = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(Ссылка.МОЛ, Истина, Ссылка.Организация, Ссылка.Дата);
		Если ЗначениеЗаполнено(ОсновныеСотрудникиФизическихЛицМОЛ) Тогда
			Для каждого Строка Из ОсновныеСотрудникиФизическихЛицМОЛ Цикл
				ДанныеМОЛВыборка = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Строка.Сотрудник, "Должность", Ссылка.Дата);			
			КонецЦикла;
			Для каждого СтрокаМОЛВыборка Из ДанныеМОЛВыборка Цикл			
				Шапка.Параметры.Должность 		= СтрокаМОЛВыборка.Должность;
				Шапка.Параметры.РасшифровкаПодписи 	= СтрокаМОЛВыборка.ФизическоеЛицо;			
			КонецЦикла;
		КонецЕсли;

		ТабДокумент.Вывести(Шапка);
		
		
		// Проверим, помещаются ли строка над таблицей, заголовок и первая строка.
		ШапкаТаблицы = Новый Массив;
		ШапкаТаблицы.Добавить(ЗаголовокТаблицы);
		ШапкаТаблицы.Добавить(СтрокаТаблицы);
		
		//Если НЕ ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, ШапкаТаблицы) Тогда			
		//	// Выведем разрыв страницы
		//	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		//КонецЕсли;
		
		// Сама таблица
		// Выведем заголовок таблицы	
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		// Выведем строки таблицы
		Для Каждого СтрокаНМА Из ТаблицаНМА Цикл
			
			СтрокаТаблицы.Параметры.Заполнить(СтрокаНМА);
			
			ТабДокумент.Вывести(СтрокаТаблицы);
			
		КонецЦикла;
		
		
		// Подвал - члены комиссии
		НаименованиеЧленовКомиссии = Новый Массив;
		НаименованиеЧленовКомиссии.Добавить("ПредседательКомиссии");
		НаименованиеЧленовКомиссии.Добавить("ПервыйЧленКомиссии");
		НаименованиеЧленовКомиссии.Добавить("ВторойЧленКомиссии");
		НаименованиеЧленовКомиссии.Добавить("ТретийЧленКомиссии");
		Подвал.Параметры.Заполнить(ВыборкаПоКомиссии);
		
		// Сначала выведем членов комиссии из выборки
		Для Каждого ЧленКомиссии Из НаименованиеЧленовКомиссии Цикл
			
			Подвал.Параметры[ЧленКомиссии + "Должность"] 	= ВыборкаПоКомиссии[ЧленКомиссии + "Должность"];
			Подвал.Параметры[ЧленКомиссии + "ФИО"] 			= ВыборкаПоКомиссии[ЧленКомиссии + "ФИО"];
			
		КонецЦикла;
		
		
		// Проверим, помещаются ли шапка подписей и одна подпись
		ПодвалРасписка = Новый Массив;
		ПодвалРасписка.Добавить(Подвал);
		ПодвалРасписка.Добавить(Расписка);
		

		
		ТабДокумент.Вывести(Подвал);
		
		Расписка.Параметры.НомерДок = Док.Номер;
		
		ТабДокумент.Вывести(Расписка);
	КонецЦикла; 
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьНА4()

#КонецОбласти

#КонецОбласти

#КонецЕсли
