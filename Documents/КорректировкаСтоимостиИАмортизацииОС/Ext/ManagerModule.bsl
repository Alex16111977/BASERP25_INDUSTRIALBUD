#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	КорректировкаСтоимостиИАмортизацииОСЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		КорректировкаСтоимостиИАмортизацииОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

КонецПроцедуры

// Добавляет команду создания документа "Корректировка стоимости и амортизации ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаСтоимостиИАмортизацииОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаСтоимостиИАмортизацииОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаСтоимостиИАмортизацииОС);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса);
	
	КорректировкаСтоимостиИАмортизацииОСЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос,
																							ТекстыЗапроса,
																							Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.КорректировкаСтоимостиИАмортизацииОС";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	КонецЕсли;
		
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.ВидИмущества КАК ВидИмущества
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаСтоимостиИАмортизацииОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Корректировка стоимости и амортизации ОС';uk='Коригування вартості і амортизації ОЗ'"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.КорректировкаСтоимостиИАмортизацииОС);
	
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ПрибыльУбыток", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки);
	
	ЗначенияПараметровПроведения.Вставить("УвеличениеСтоимости", Перечисления.ХозяйственныеОперации.УвеличениеСтоимостиОС);
	ЗначенияПараметровПроведения.Вставить("УменьшениеСтоимости", Перечисления.ХозяйственныеОперации.УменьшениеСтоимостиОС);
	ЗначенияПараметровПроведения.Вставить("УвеличениеАмортизации", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("УменьшениеАмортизации", Перечисления.ХозяйственныеОперации.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("ИзменениеДоходовЦФ", Неопределено);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период                                 КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");

	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&УвеличениеСтоимости                        КАК ХозяйственнаяОперация,
	|	&Организация                                КАК Организация,
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	&Подразделение                              КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                   КАК Арендатор,
	|
	|	КорректировкаСтоимости.Стоимость            КАК Стоимость,
	|	КорректировкаСтоимости.СтоимостьРегл        КАК СтоимостьРегл,
	|	КорректировкаСтоимости.СтоимостьНУ          КАК СтоимостьНУ,
	|	КорректировкаСтоимости.СтоимостьПР          КАК СтоимостьПР,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		- КорректировкаСтоимости.СтоимостьНУ
	|		- КорректировкаСтоимости.СтоимостьПР    КАК СтоимостьВР,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ          КАК СтоимостьЦФ,
	|	КорректировкаСтоимости.СтоимостьПРЦФ        КАК СтоимостьПРЦФ,
	|	КорректировкаСтоимости.ЗалоговаяСтоимость   КАК ЗалоговаяСтоимость,
	|
	|	&Организация                                КАК КорОрганизация,
	|	&Подразделение                              КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Арендованное)
	|			ТОГДА СведенияОбУчете.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорКонтрагент,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	&СтатьяРасходов                             КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                          КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПР <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&УменьшениеСтоимости                        КАК ХозяйственнаяОперация,
	|	&Организация                                КАК Организация,
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	&Подразделение                              КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                   КАК Арендатор,
	|
	|	КорректировкаСтоимости.Стоимость            КАК Стоимость,
	|	КорректировкаСтоимости.СтоимостьРегл        КАК СтоимостьРегл,
	|	КорректировкаСтоимости.СтоимостьНУ          КАК СтоимостьНУ,
	|	КорректировкаСтоимости.СтоимостьПР          КАК СтоимостьПР,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		- КорректировкаСтоимости.СтоимостьНУ
	|		- КорректировкаСтоимости.СтоимостьПР    КАК СтоимостьВР,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ          КАК СтоимостьЦФ,
	|	КорректировкаСтоимости.СтоимостьПРЦФ        КАК СтоимостьПРЦФ,
	|	КорректировкаСтоимости.ЗалоговаяСтоимость   КАК ЗалоговаяСтоимость,
	|
	|	&Организация                                КАК КорОрганизация,
	|	&Подразделение                              КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Арендованное)
	|			ТОГДА СведенияОбУчете.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорКонтрагент,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	&СтатьяРасходов                             КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                          КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПР <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&УвеличениеАмортизации                      КАК ХозяйственнаяОперация,
	|	&Организация                                КАК Организация,
	|	КорректировкаАмортизации.ОсновноеСредство   КАК ОсновноеСредство,
	|	&Подразделение                              КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	КорректировкаАмортизации.Амортизация        КАК Амортизация,
	|	КорректировкаАмортизации.АмортизацияРегл    КАК АмортизацияРегл,
	|	КорректировкаАмортизации.АмортизацияНУ      КАК АмортизацияНУ,
	|	КорректировкаАмортизации.АмортизацияПР      КАК АмортизацияПР,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		- КорректировкаАмортизации.АмортизацияНУ
	|		- КорректировкаАмортизации.АмортизацияПР КАК АмортизацияВР,
	|
	|	КорректировкаАмортизации.АмортизацияЦФ      КАК АмортизацияЦФ,
	|	КорректировкаАмортизации.АмортизацияПРЦФ    КАК АмортизацияПРЦФ,
	|
	|	&Организация                                КАК КорОрганизация,
	|	&Подразделение                              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	&СтатьяРасходов                             КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                          КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтУвеличениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПР <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&УменьшениеАмортизации                      КАК ХозяйственнаяОперация,
	|	&Организация                                КАК Организация,
	|	КорректировкаАмортизации.ОсновноеСредство   КАК ОсновноеСредство,
	|	&Подразделение                              КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	КорректировкаАмортизации.Амортизация        КАК Амортизация,
	|	КорректировкаАмортизации.АмортизацияРегл    КАК АмортизацияРегл,
	|	КорректировкаАмортизации.АмортизацияНУ      КАК АмортизацияНУ,
	|	КорректировкаАмортизации.АмортизацияПР      КАК АмортизацияПР,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		- КорректировкаАмортизации.АмортизацияНУ
	|		- КорректировкаАмортизации.АмортизацияПР КАК АмортизацияВР,
	|
	|	КорректировкаАмортизации.АмортизацияЦФ      КАК АмортизацияЦФ,
	|	КорректировкаАмортизации.АмортизацияПРЦФ    КАК АмортизацияПРЦФ,
	|
	|	&Организация                                КАК КорОрганизация,
	|	&Подразделение                              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтУменьшениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПР <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	&УменьшениеСтоимости                      КАК ХозяйственнаяОперация,
	|	&Организация                              КАК Организация,
	|	&Подразделение                            КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                           КАК Статья,
	|	&АналитикаАктивовПассивов                 КАК Аналитика,
	|	КорректировкаСтоимости.Стоимость          КАК Сумма
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &ОтражатьВУпрУчете
	|	И КорректировкаСтоимости.Стоимость <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	&УвеличениеАмортизации                    КАК ХозяйственнаяОперация,
	|	&Организация                              КАК Организация,
	|	&Подразделение                            КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                           КАК Статья,
	|	&АналитикаАктивовПассивов                 КАК Аналитика,
	|	КорректировкаАмортизации.Амортизация      КАК Сумма
	|ИЗ
	|	ВтУвеличениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &ОтражатьВУпрУчете
	|	И КорректировкаАмортизации.Амортизация <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УвеличениеСтоимости                       КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	КорректировкаСтоимости.Стоимость           КАК Сумма,
	|	КорректировкаСтоимости.Стоимость           КАК СуммаУпр,
	|	КорректировкаСтоимости.СтоимостьРегл       КАК СуммаРегл
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	(КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УменьшениеСтоимости                       КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	Неопределено                               КАК АналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяРасходов                            КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	&АналитикаРасходов                         КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	КорректировкаСтоимости.Стоимость           КАК Сумма,
	|	КорректировкаСтоимости.Стоимость           КАК СуммаУпр,
	|	КорректировкаСтоимости.СтоимостьРегл       КАК СуммаРегл
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	(КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УвеличениеСтоимости                       КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	Неопределено                               КАК АналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяРасходов                            КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	&АналитикаРасходов                         КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	0                                          КАК Сумма,
	|	0                                          КАК СуммаУпр,
	|	КорректировкаСтоимости.СтоимостьЦФ         КАК СуммаРегл
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	КорректировкаСтоимости.СтоимостьЦФ <> 0";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УменьшениеСтоимости                       КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	КорректировкаСтоимости.ОсновноеСредство    КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	0                                          КАК Сумма,
	|	0                                          КАК СуммаУпр,
	|	КорректировкаСтоимости.СтоимостьЦФ         КАК СуммаРегл
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	КорректировкаСтоимости.СтоимостьЦФ <> 0";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УменьшениеАмортизации                     КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                             КАК Статья,
	|	&АналитикаДоходов                          КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                               КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	КорректировкаАмортизации.ОсновноеСредство  КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	КорректировкаАмортизации.Амортизация       КАК Сумма,
	|	КорректировкаАмортизации.Амортизация       КАК СуммаУпр,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтУменьшениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство)
	|ГДЕ
	|	(КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&УвеличениеАмортизации                     КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|
	|	&Подразделение                             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	КорректировкаАмортизации.ОсновноеСредство  КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                             КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	КорректировкаАмортизации.ОсновноеСредство  КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	КорректировкаАмортизации.Амортизация       КАК Сумма,
	|	КорректировкаАмортизации.Амортизация       КАК СуммаУпр,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтУвеличениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство)
	|ГДЕ
	|	(КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	&Подразделение                                 КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&УвеличениеСтоимости                           КАК ХозяйственнаяОперация,
	|	&СтатьяДоходов                                 КАК СтатьяДоходов,
	|	&АналитикаДоходов                              КАК АналитикаДоходов,
	|
	|	КорректировкаСтоимости.Стоимость               КАК Сумма,
	|	КорректировкаСтоимости.Стоимость               КАК СуммаУпр,
	|	КорректировкаСтоимости.Стоимость               КАК СуммаУпрБезНДС,
	|	КорректировкаСтоимости.СтоимостьРегл
	|		+ КорректировкаСтоимости.СтоимостьЦФ       КАК СуммаРеглБезНДС,
	|	КорректировкаСтоимости.СтоимостьРегл
	|		+ КорректировкаСтоимости.СтоимостьЦФ       КАК СуммаРегл
	|
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	(КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	&Подразделение                                 КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&ИзменениеДоходовЦФ                            КАК ХозяйственнаяОперация,
	|	&СтатьяДоходов                                 КАК СтатьяДоходов,
	|	&АналитикаДоходов                              КАК АналитикаДоходов,
	|
	|	0                                              КАК Сумма,
	|	0                                              КАК СуммаУпр,
	|	0                                              КАК СуммаУпрБезНДС,
	|	КорректировкаСтоимости.СтоимостьЦФ             КАК СуммаРеглБезНДС,
	|	КорректировкаСтоимости.СтоимостьЦФ             КАК СуммаРегл
	|
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство)
	|ГДЕ
	|	КорректировкаСтоимости.СтоимостьЦФ <> 0";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	&Подразделение                                 КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&УменьшениеАмортизации                         КАК ХозяйственнаяОперация,
	|	&СтатьяДоходов                                 КАК СтатьяДоходов,
	|	&АналитикаДоходов                              КАК АналитикаДоходов,
	|
	|	КорректировкаАмортизации.Амортизация           КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ КорректировкаАмортизации.Амортизация
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ КорректировкаАмортизации.Амортизация
	|	КОНЕЦ КАК СуммаУпрБезНДС,
	|
	|	ВЫБОР 
	|		КОГДА НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА 0
	|		КОГДА КорректировкаАмортизации.НачислятьИзнос
	|			ТОГДА 0
	|		ИНАЧЕ КорректировкаАмортизации.АмортизацияРегл + КорректировкаАмортизации.АмортизацияЦФ
	|	КОНЕЦ КАК СуммаРеглБезНДС,
	|
	|	ВЫБОР 
	|		КОГДА НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА 0
	|		КОГДА КорректировкаАмортизации.НачислятьИзнос
	|			ТОГДА 0
	|		ИНАЧЕ КорректировкаАмортизации.АмортизацияРегл + КорректировкаАмортизации.АмортизацияЦФ
	|	КОНЕЦ КАК СуммаРегл
	|
	|ИЗ
	|	ВтУменьшениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО (СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство)
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ (КорректировкаАмортизации.АмортизацияРегл <> 0 ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0)
	|				И НЕ КорректировкаАмортизации.НачислятьИзнос";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = КорректировкаСтоимостиИАмортизацииОСЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС");
	
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	втПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
		|	втПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
		|	втПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
		|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ТаблицаОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ
		|ИЗ
		|	Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
		|		ПО (втПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка
		|	И (втПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ <> ТаблицаОС.ПервоначальнаяСтоимостьБУ
		|			ИЛИ втПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ <> ТаблицаОС.ПервоначальнаяСтоимостьУУ)";
		
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры) Экспорт

	ИмяРегистра = "ВтПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(
		ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	втПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользования,
	|	втПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	втПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	втПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	втПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	втПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизацииУУ КАК СтоимостьДляВычисленияАмортизации,
	|	ВЫБОР
	|		КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА ТаблицаОС.СтоимостьДляВычисленияАмортизацииБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизацииРегл
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСУУ КАК втПараметрыАмортизацииОСУУ
	|		ПО (втПараметрыАмортизацииОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И втПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации <> ТаблицаОС.СтоимостьДляВычисленияАмортизацииУУ";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                        КАК Период,
	|	&Организация                   КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)   КАК ДатаОтражения,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

Процедура ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "ВтУвеличениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость > 0
	|			ТОГДА КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ = 0
	|			ТОГДА 0
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьЦФ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьПРЦФ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПРЦФ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|			ТОГДА КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|ПОМЕСТИТЬ ВтУвеличениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ > 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ = 0
	|					ТОГДА 0
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|					ТОГДА КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ > 0
	|					ТОГДА КорректировкаСтоимости.СтоимостьПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "ВтУменьшениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость < 0
	|			ТОГДА -КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ = 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьЦФ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьПРЦФ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПРЦФ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|			ТОГДА -КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУменьшениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ < 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ < 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ = 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втУвеличениеАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаАмортизации.ОсновноеСредство   КАК ОсновноеСредство,
	|	КорректировкаАмортизации.НачислятьИзнос     КАК НачислятьИзнос,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.Амортизация > 0
	|			ТОГДА КорректировкаАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияНУ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ = 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияЦФ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияПРЦФ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПРЦФ
	|
	|ПОМЕСТИТЬ втУвеличениеАмортизации
	|ИЗ
	|	ВтКорректировкаАмортизации КАК КорректировкаАмортизации
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ > 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ > 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ = 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втУменьшениеАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаАмортизации.ОсновноеСредство   КАК ОсновноеСредство,
	|	КорректировкаАмортизации.НачислятьИзнос     КАК НачислятьИзнос,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.Амортизация < 0
	|			ТОГДА -КорректировкаАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияНУ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ = 0
	|			ТОГДА 0
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияЦФ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияПРЦФ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПРЦФ
	|
	|ПОМЕСТИТЬ втУменьшениеАмортизации
	|ИЗ
	|	ВтКорректировкаАмортизации КАК КорректировкаАмортизации
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ < 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ = 0
	|					ТОГДА 0
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|					ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ < 0
	|					ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> ТаблицаОС.СтоимостьУУ 
	|				И &ОтражатьВУпрУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьУУ - ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) <> ТаблицаОС.СтоимостьБУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) <> ТаблицаОС.СтоимостьНУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьНУ - ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) <> ТаблицаОС.СтоимостьПР 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьПР - ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> ТаблицаОС.СтоимостьЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьЦФ - ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) <> ТаблицаОС.СтоимостьЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.СтоимостьЦФ - ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПРЦФ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0) <> ТаблицаОС.ЗалоговаяСтоимость 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Арендованное)
	|			ТОГДА ТаблицаОС.ЗалоговаяСтоимость - ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ втКорректировкаСтоимости
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> ТаблицаОС.СтоимостьУУ 
	|				И &ОтражатьВУпрУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) <> ТаблицаОС.СтоимостьБУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) <> ТаблицаОС.СтоимостьНУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) <> ТаблицаОС.СтоимостьПР 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> ТаблицаОС.СтоимостьЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0) <> ТаблицаОС.ЗалоговаяСтоимость 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Арендованное))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	&НачислятьИзнос                             КАК НачислятьИзнос,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.Амортизация, 0) <> ТаблицаОС.АмортизацияУУ 
	|				И &ОтражатьВУпрУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияУУ - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) <> ТаблицаОС.АмортизацияБУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияБУ - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) <> ТаблицаОС.АмортизацияНУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияНУ - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) <> ТаблицаОС.АмортизацияПР 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияПР - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) <> ТаблицаОС.АмортизацияЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) <> ТаблицаОС.АмортизацияЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|			ТОГДА ТаблицаОС.АмортизацияЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПРЦФ
	|
	|ПОМЕСТИТЬ втКорректировкаАмортизации
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|	И (ЕСТЬNULL(АмортизацияОС.Амортизация, 0) <> ТаблицаОС.АмортизацияУУ 
	|				И &ОтражатьВУпрУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) <> ТаблицаОС.АмортизацияБУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) <> ТаблицаОС.АмортизацияНУ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) <> ТаблицаОС.АмортизацияПР 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное)
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) <> ТаблицаОС.АмортизацияЦФ 
	|				И &ОтражатьВРеглУчете 
	|				И &ВидИмущества = ЗНАЧЕНИЕ(Перечисление.ВидыИмущества.Собственное))";
	
	КорректировкаСтоимостиИАмортизацииОСЛокализация.ДополнитьТекстЗапросаВтКорректировкаАмортизации(ТекстЗапроса);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&СтатьяРасходов                                 КАК СтатьяРасходов,
	|	&АналитикаРасходов                              КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	КорректировкаСтоимости.Стоимость СуммаСНДС,
	|	КорректировкаСтоимости.Стоимость КАК СуммаБезНДС,
	|	КорректировкаСтоимости.Стоимость КАК СуммаБезНДСУпр,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		+ КорректировкаСтоимости.СтоимостьЦФ КАК СуммаСНДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		+ КорректировкаСтоимости.СтоимостьЦФ КАК СуммаБезНДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьПР
	|		+ КорректировкаСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		+ КорректировкаСтоимости.СтоимостьЦФ
	|		- КорректировкаСтоимости.СтоимостьНУ
	|		- КорректировкаСтоимости.СтоимостьПР
	|		- КорректировкаСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&УменьшениеСтоимости                            КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПР <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&СтатьяРасходов                                 КАК СтатьяРасходов,
	|	&АналитикаРасходов                              КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	0 СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ КАК СуммаСНДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ КАК СуммаБезНДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ
	|		- КорректировкаСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&ИзменениеДоходовЦФ                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (КорректировкаСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&СтатьяРасходов                                 КАК СтатьяРасходов,
	|	&АналитикаРасходов                              КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	КорректировкаАмортизации.Амортизация            КАК СуммаСНДС,
	|	КорректировкаАмортизации.Амортизация            КАК СуммаБезНДС,
	|	КорректировкаАмортизации.Амортизация            КАК СуммаБезНДСУпр,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ    КАК СуммаСНДСРегл,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ    КАК СуммаБезНДСРегл,
	|
	|	КорректировкаАмортизации.АмортизацияПР
	|		+ КорректировкаАмортизации.АмортизацияПРЦФ  КАК ПостояннаяРазница,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ
	|		- КорректировкаАмортизации.АмортизацияНУ
	|		- КорректировкаАмортизации.АмортизацияПР
	|		- КорректировкаАмортизации.АмортизацияПРЦФ  КАК ВременнаяРазница,
	|
	|	&УвеличениеАмортизации                          КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	ВтУвеличениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПР <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.КорректировкаСтоимостиИАмортизацииОС", "втСведенияОбУчете");
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());
	
	#Область УменьшениеСтоимости
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	КорректировкаСтоимости.Стоимость КАК Стоимость,
	|	КорректировкаСтоимости.Стоимость КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	КорректировкаСтоимости.СтоимостьРегл КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьПР КАК ПостояннаяРазница,
	|
	|	КорректировкаСтоимости.СтоимостьРегл
	|		- КорректировкаСтоимости.СтоимостьНУ
	|		- КорректировкаСтоимости.СтоимостьПР КАК ВременнаяРазница,
	|
	|	&УменьшениеСтоимости КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ВтУменьшениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	(КорректировкаСтоимости.Стоимость <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПР <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	КорректировкаСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	КорректировкаСтоимости.СтоимостьЦФ
	|		- КорректировкаСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&УвеличениеСтоимости КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ВтУвеличениеСтоимости КАК КорректировкаСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаСтоимости.ОсновноеСредство
	|ГДЕ
	|	(КорректировкаСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеАмортизации
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	КорректировкаАмортизации.Амортизация КАК Стоимость,
	|	КорректировкаАмортизации.Амортизация КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	КорректировкаАмортизации.АмортизацияПР
	|		+ КорректировкаАмортизации.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|
	|	КорректировкаАмортизации.АмортизацияРегл
	|		+ КорректировкаАмортизации.АмортизацияЦФ
	|		- КорректировкаАмортизации.АмортизацияНУ
	|		- КорректировкаАмортизации.АмортизацияПР
	|		- КорректировкаАмортизации.АмортизацияПРЦФ КАК ВременнаяРазница,
	|
	|	&УвеличениеАмортизации КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ВтУвеличениеАмортизации КАК КорректировкаАмортизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = КорректировкаАмортизации.ОсновноеСредство
	|ГДЕ
	|	(КорректировкаАмортизации.Амортизация <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПР <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат "";

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат КорректировкаСтоимостиИАмортизацииОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчета.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.СчетУчета     = "СчетУчета";
	ПараметрыНастройки.Субконто1     = "Субконто1";
	ПараметрыНастройки.Субконто2     = "Субконто2";
	ПараметрыНастройки.Субконто3     = "Субконто3";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьи";
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "Объект.АналитикаАктивовПассивов";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчете");
	
	Возврат ПараметрыНастройки;
	
КонецФункции

Процедура ЗаполнитьПоДаннымУчета(Объект, ВыбранныеСтроки = Неопределено) Экспорт

	Если ВыбранныеСтроки <> Неопределено
		И ВыбранныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = КорректировкаСтоимостиИАмортизацииОСЛокализация.ТекстЗапросаЗаполнитьПоДаннымУчета();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьИАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(СтоимостьИАмортизация.СтоимостьУУ) КАК СтоимостьУУ,
		|	СУММА(СтоимостьИАмортизация.СтоимостьБУ) КАК СтоимостьБУ,
		|	СУММА(СтоимостьИАмортизация.СтоимостьНУ) КАК СтоимостьНУ,
		|	СУММА(СтоимостьИАмортизация.СтоимостьПР) КАК СтоимостьПР,
		|	СУММА(СтоимостьИАмортизация.СтоимостьЦФ) КАК СтоимостьЦФ,
		|	СУММА(СтоимостьИАмортизация.ЗалоговаяСтоимость) КАК ЗалоговаяСтоимость,
		|	СУММА(СтоимостьИАмортизация.АмортизацияБУ) КАК АмортизацияБУ,
		|	СУММА(СтоимостьИАмортизация.АмортизацияНУ) КАК АмортизацияНУ,
		|	СУММА(СтоимостьИАмортизация.АмортизацияПР) КАК АмортизацияПР,
		|	СУММА(СтоимостьИАмортизация.АмортизацияУУ) КАК АмортизацияУУ,
		|	СУММА(СтоимостьИАмортизация.АмортизацияЦФ) КАК АмортизацияЦФ,
		|	МАКСИМУМ(ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ) КАК ПервоначальнаяСтоимостьБУ,
		|	МАКСИМУМ(ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ) КАК ПервоначальнаяСтоимостьУУ,
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации) КАК СтоимостьДляВычисленияАмортизацииУУ,
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизацииРегл) КАК СтоимостьДляВычисленияАмортизацииБУ
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтоимостьОСОстатки.ОсновноеСредство КАК ОсновноеСредство,
		|		СтоимостьОСОстатки.СтоимостьОстаток КАК СтоимостьУУ,
		|		СтоимостьОСОстатки.СтоимостьРеглОстаток КАК СтоимостьБУ,
		|		СтоимостьОСОстатки.СтоимостьНУОстаток КАК СтоимостьНУ,
		|		СтоимостьОСОстатки.СтоимостьПРОстаток КАК СтоимостьПР,
		|		СтоимостьОСОстатки.СтоимостьЦФОстаток КАК СтоимостьЦФ,
		|		СтоимостьОСОстатки.ЗалоговаяСтоимостьОстаток КАК ЗалоговаяСтоимость,
		|		0 КАК АмортизацияБУ,
		|		0 КАК АмортизацияНУ,
		|		0 КАК АмортизацияПР,
		|		0 КАК АмортизацияУУ,
		|		0 КАК АмортизацияЦФ
		|	ИЗ
		|		РегистрНакопления.СтоимостьОС.Остатки(
		|				&ПериодГраница,
		|				Организация = &Организация
		|					И ОсновноеСредство В (&СписокОС)) КАК СтоимостьОСОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СтоимостьОС.ОсновноеСредство,
		|		СтоимостьОС.Стоимость,
		|		ВЫБОР
		|			КОГДА СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -СтоимостьОС.СтоимостьРегл
		|			ИНАЧЕ СтоимостьОС.СтоимостьРегл
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -СтоимостьОС.СтоимостьНУ
		|			ИНАЧЕ СтоимостьОС.СтоимостьНУ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -СтоимостьОС.СтоимостьПР
		|			ИНАЧЕ СтоимостьОС.СтоимостьПР
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -СтоимостьОС.СтоимостьЦФ
		|			ИНАЧЕ СтоимостьОС.СтоимостьЦФ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -СтоимостьОС.ЗалоговаяСтоимость
		|			ИНАЧЕ СтоимостьОС.ЗалоговаяСтоимость
		|		КОНЕЦ,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|	ГДЕ
		|		СтоимостьОС.Регистратор = &Ссылка
		|		И СтоимостьОС.Организация = &Организация
		|		И СтоимостьОС.Период < &Период
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		АмортизацияОСОстатки.ОсновноеСредство,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		-АмортизацияОСОстатки.АмортизацияРеглОстаток,
		|		-АмортизацияОСОстатки.АмортизацияНУОстаток,
		|		-АмортизацияОСОстатки.АмортизацияПРОстаток,
		|		-АмортизацияОСОстатки.АмортизацияОстаток,
		|		-АмортизацияОСОстатки.АмортизацияЦФОстаток
		|	ИЗ
		|		РегистрНакопления.АмортизацияОС.Остатки(
		|				&ПериодГраница,
		|				Организация = &Организация
		|					И ОсновноеСредство В (&СписокОС)) КАК АмортизацияОСОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		АмортизацияОС.ОсновноеСредство,
		|		АмортизацияОС.Амортизация,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияРегл
		|			ИНАЧЕ АмортизацияОС.АмортизацияРегл
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияНУ
		|			ИНАЧЕ АмортизацияОС.АмортизацияНУ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияПР
		|			ИНАЧЕ АмортизацияОС.АмортизацияПР
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.Амортизация
		|			ИНАЧЕ АмортизацияОС.Амортизация
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияЦФ
		|			ИНАЧЕ АмортизацияОС.АмортизацияЦФ
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.АмортизацияОС КАК АмортизацияОС
		|	ГДЕ
		|		АмортизацияОС.Регистратор = &Ссылка
		|		И АмортизацияОС.Организация = &Организация
		|		И АмортизацияОС.Период < &Период
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		АмортизацияОС.ОсновноеСредство,
		|		АмортизацияОС.Амортизация,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияРегл
		|			ИНАЧЕ АмортизацияОС.АмортизацияРегл
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияНУ
		|			ИНАЧЕ АмортизацияОС.АмортизацияНУ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияПР
		|			ИНАЧЕ АмортизацияОС.АмортизацияПР
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.Амортизация
		|			ИНАЧЕ АмортизацияОС.Амортизация
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -АмортизацияОС.АмортизацияЦФ
		|			ИНАЧЕ АмортизацияОС.АмортизацияЦФ
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.АмортизацияОС КАК АмортизацияОС
		|	ГДЕ
		|		АмортизацияОС.Регистратор ССЫЛКА Документ.АмортизацияОС2_4
		|		И АмортизацияОС.Организация = &Организация
		|		И АмортизацияОС.Период < &Период
		|		И АмортизацияОС.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
		|
		|	) КАК СтоимостьИАмортизация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И Организация = &Организация
		|				И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = СтоимостьИАмортизация.ОсновноеСредство)
		|			И (ПервоначальныеСведенияОС.Организация = &Организация)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И Организация = &Организация
		|				И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСУУ
		|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СтоимостьИАмортизация.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУ.Организация = &Организация)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьИАмортизация.ОсновноеСредство";
		
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	Период = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Период", Период);
	
	СписокОС = Объект.ОС.Выгрузить(ВыбранныеСтроки).ВыгрузитьКолонку("ОсновноеСредство");
	Запрос.УстановитьПараметр("СписокОС", СписокОС);
	
	СтоимостьИАмортизация = Запрос.Выполнить().Выгрузить();
	СтоимостьИАмортизация.Индексы.Добавить("ОсновноеСредство");
	
	Если ВыбранныеСтроки <> Неопределено Тогда
		СписокСтрок = ВыбранныеСтроки;
	Иначе
		СписокСтрок = Объект.ОС;
	КонецЕсли; 
	
	Для каждого ДанныеСтроки Из СписокСтрок Цикл
		
		ДанныеУчета = СтоимостьИАмортизация.Найти(ДанныеСтроки.ОсновноеСредство, "ОсновноеСредство");
		Если ДанныеУчета <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, ДанныеУчета);
		Иначе
			ДанныеСтроки.СтоимостьБУ = 0;
			ДанныеСтроки.СтоимостьНУ = 0;
			ДанныеСтроки.СтоимостьПР = 0;
			ДанныеСтроки.СтоимостьУУ = 0;
			ДанныеСтроки.СтоимостьЦФ = 0;
			ДанныеСтроки.АмортизацияБУ = 0;
			ДанныеСтроки.АмортизацияНУ = 0;
			ДанныеСтроки.АмортизацияПР = 0;
			ДанныеСтроки.АмортизацияУУ = 0;
			ДанныеСтроки.АмортизацияЦФ = 0;
			ДанныеСтроки.ЗалоговаяСтоимость = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьБУ = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьНУ = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьУУ = 0;
			ДанныеСтроки.СтоимостьДляВычисленияАмортизацииБУ = 0;
			ДанныеСтроки.СтоимостьДляВычисленияАмортизацииУУ = 0;
			ДанныеСтроки.ПРДляВычисленияАмортизации = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

#КонецЕсли