
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Параметры.Ключ = Документы.РегистрацияПрочихДоходов.ПустаяСсылка() Тогда
		
		// создается новый документ
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный, Месяц", 
		"Объект.Организация", "Объект.Ответственный", "Объект.ПериодРегистрации");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Если Не ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
			Объект.ПериодРегистрации = ТекущаяДатаСеанса();
		КонецЕсли;
		
		Объект.ПланируемаяДатаВыплаты = НачалоДня(ТекущаяДатаСеанса()) + 86400;
		ЗаполнитьДанныеФормыПоОрганизации();
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;
	
	//++ НЕ БЗК
	Элементы.ЗаполнитьЧП.Видимость = Истина;
	//-- НЕ БЗК
		
	// Запомним дату документа, при смене месяца надо пересчитывать налоги и взносы.
	ДатаПрежняя = Объект.Дата;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ИзмененыРезультатыРасчетаНДФЛ" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ОбновитьДанныеНДФЛНаСервере(Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

    // СтандартныеПодсистемы.УправлениеДоступом
    УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	КодДоходаНДФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.ВидПрочегоДохода, "КодДоходаНДФЛ");
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	ПриПолученииДанныхНаСервере();
	
	СохраняемыеЗначения = Новый Структура;
	СохраняемыеЗначения.Вставить("Исполнитель", ТекущийОбъект.Исполнитель);
	СохраняемыеЗначения.Вставить("ДолжностьИсполнителя", ТекущийОбъект.ДолжностьИсполнителя);
	
	ЗарплатаКадры.СохранитьЗначенияЗаполненияОтветственныхРаботников(ТекущийОбъект.Организация, СохраняемыеЗначения);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбработатьИзменениеОрганизацииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ОбработатьИзменениеДатыДокументаНаСервере();
	ДатаПрежняя = Объект.Дата;
КонецПроцедуры

&НаКлиенте
Процедура ВидПрочегоДоходаПриИзменении(Элемент)
	
	ОбработатьИзменениеВидаПрочегоДоходаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланируемаяДатаВыплатыПриИзменении(Элемент)
	
	РассчитатьНДФЛИВзносыВсем();
	
КонецПроцедуры


#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыНачисленияУдержанияВзносы

&НаКлиенте
Процедура НачисленияФизическоеЛицоПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.НачисленияУдержанияВзносы.ТекущиеДанные;
	
	Если ДанныеСтроки.ФизическоеЛицо <> УдаляемоеФизическоеЛицо Тогда
		УдалитьДанныеУдаляемогоФизическогоЛица();
		УдаляемоеФизическоеЛицо = ДанныеСтроки.ФизическоеЛицо;
	КонецЕсли;
	
	
	РассчитатьНДФЛиВзносыПоТекущейСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачисленоПриИзменении(Элемент)
	
	РассчитатьНДФЛиВзносыПоТекущейСтроке();
	
КонецПроцедуры


&НаКлиенте
Процедура НачисленияУдержанияВзносыПередУдалением(Элемент, Отказ)
	
	Если Элементы.НачисленияУдержанияВзносы.ТекущиеДанные <> Неопределено Тогда
		УдаляемоеФизическоеЛицо = Элементы.НачисленияУдержанияВзносы.ТекущиеДанные.ФизическоеЛицо;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияУдержанияВзносыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Элементы.НачисленияУдержанияВзносы.ТекущиеДанные <> Неопределено Тогда
		
		Если НоваяСтрока И Копирование Тогда
			Элементы.НачисленияУдержанияВзносы.ТекущиеДанные.ФизическоеЛицо = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
		Иначе
			УдаляемоеФизическоеЛицо = Элементы.НачисленияУдержанияВзносы.ТекущиеДанные.ФизическоеЛицо;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияУдержанияВзносыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УдаляемоеФизическоеЛицо = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ДобавитьНовуюСтроку(ВыбранноеЗначение);
	Иначе
		Для Каждого ФизическоеЛицо Из ВыбранноеЗначение Цикл
			ДобавитьНовуюСтроку(ФизическоеЛицо);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияУдержанияВзносыПослеУдаления(Элемент)
	
	УдалитьДанныеУдаляемогоФизическогоЛица();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборПолучателей(Команда)
	
	РолиФизическихЛиц = Новый Массив;
	РолиФизическихЛиц.Добавить(ПредопределенноеЗначение("Перечисление.РолиФизическихЛиц.Сотрудник"));
	РолиФизическихЛиц.Добавить(ПредопределенноеЗначение("Перечисление.РолиФизическихЛиц.БывшийСотрудник"));
	РолиФизическихЛиц.Добавить(ПредопределенноеЗначение("Перечисление.РолиФизическихЛиц.ПрочийПолучательДоходов"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("АдресСпискаПодобранныхФизическихЛиц", АдресСпискаПодобранныхФизическихЛиц());
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	СтруктураОтбора.Вставить("Роль", РолиФизическихЛиц);
	
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыФормы, Элементы.НачисленияУдержанияВзносы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееОРасчетеНДФЛ(Команда)
	
	УчетНДФЛКлиентРасширенный.ОткрытьФормуПодробнееОРасчетеНДФЛ(
		Объект, "РегистрацияПрочихДоходов", ЭтаФорма, ОписаниеДокумента(ЭтаФорма), АдресСпискаПодобранныхФизическихЛиц());
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура УдалитьДанныеУдаляемогоФизическогоЛица()
	
	Если ЗначениеЗаполнено(УдаляемоеФизическоеЛицо) Тогда
		УчетНДФЛКлиентСерверРасширенный.УдалитьДанныеНДФЛФизическоголица(Объект, УдаляемоеФизическоеЛицо);
		УдаляемоеФизическоеЛицо = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьНачисленияСотрудника(Сотрудники, СохранятьИсправления = Истина) Экспорт
	РассчитатьНДФЛИВзносыНаСервере(Сотрудники);
КонецПроцедуры

&НаСервере
Процедура РассчитатьНДФЛИВзносыВсем()
	
	Если Не ЗначениеЗаполнено(Объект.ПланируемаяДатаВыплаты) Или Объект.НачисленияУдержанияВзносы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РасчетНДФЛНарастающимИтогомСНачалаГода Тогда
		РассчитатьНДФЛИВзносыНаСервере();
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНДФЛиВзносыПоТекущейСтроке()
	
	ДанныеСтроки = Элементы.НачисленияУдержанияВзносы.ТекущиеДанные;
	
	Если ДанныеСтроки.ФизическоеЛицо.Пустая() Или ДанныеСтроки.Начислено = 0 Или Объект.ВидПрочегоДохода.Пустая() Или Не ЗначениеЗаполнено(Объект.ПланируемаяДатаВыплаты) Тогда
		
		Если ДанныеСтроки.Начислено = 0 Тогда
			ДанныеСтроки.НДФЛ = 0;
			ДанныеСтроки.КВыплате = 0;
		КонецЕсли;
		
	Иначе
		
		Если РасчетНДФЛНарастающимИтогомСНачалаГода Тогда
			РассчитатьНДФЛИВзносыНаСервере(ДанныеСтроки.ФизическоеЛицо);
		Иначе
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтогиКВыплате()
	
	Для Каждого ДанныеСтроки Из Объект.НачисленияУдержанияВзносы Цикл
		ЗаполнитьКВыплатеПоСтроке(ЭтаФорма, ДанныеСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКВыплатеПоСтроке(Форма, ДанныеСтроки)
	
	Если Форма.РасчетНДФЛНарастающимИтогомСНачалаГода Тогда
		
		ДанныеСтроки.НДФЛ = 0;
		СтрокиНДФЛ = Форма.Объект.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо", ДанныеСтроки.ФизическоеЛицо));
		Для каждого СтрокаНДФЛ Из СтрокиНДФЛ Цикл
			ДанныеСтроки.НДФЛ = ДанныеСтроки.НДФЛ + СтрокаНДФЛ.Налог;
		КонецЦикла;
		
	КонецЕсли;
	ДанныеСтроки.КВыплате = ДанныеСтроки.Начислено - ДанныеСтроки.НДФЛ;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура("Организация, Период", Объект.Организация, НачалоДня(Объект.ПериодРегистрации));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОрганизацииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьДанныеФормыПоОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДатыДокументаНаСервере()

	УстановитьФункциональныеОпцииФормы();
	Если НачалоМесяца(ДатаПрежняя) <> НачалоМесяца(Объект.Дата) Тогда
		РассчитатьНДФЛИВзносыВсем();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеВидаПрочегоДоходаНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ВидПрочегоДохода) Тогда
		
		
	Иначе
		
		КодДоходаНДФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПрочегоДохода, "КодДоходаНДФЛ");
		
	КонецЕсли;
	
	УстановитьРежимРасчетаНДФЛИОтображениеЭлементовРедактированияНДФЛ();
	
	РассчитатьНДФЛИВзносыВсем();
	
КонецПроцедуры


&НаСервере
Процедура РассчитатьИтогиНаСервере()

	Для каждого Строка Из ЭтотОбъект.Объект.НачисленияУдержанияВзносы Цикл
		ЗаполнитьКВыплатеПоСтроке(ЭтаФорма, Строка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхФизическихЛиц()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.НачисленияУдержанияВзносы.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо"), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьНовуюСтроку(ФизическоеЛицо)
	
	СтрокиНачислений = Объект.НачисленияУдержанияВзносы.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
	
	Если СтрокиНачислений.Количество() = 0 Тогда
		НоваяСтрока = Объект.НачисленияУдержанияВзносы.Добавить();
		НоваяСтрока.ФизическоеЛицо = ФизическоеЛицо;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	
	ЗапрашиваемыеЗначения.Вставить("Исполнитель", "Объект.Исполнитель");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьИсполнителя", "Объект.ДолжностьИсполнителя");
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	УстановитьРежимРасчетаНДФЛИОтображениеЭлементовРедактированияНДФЛ();
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеДокумента(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеРасчетногоДокумента();
	Описание.МесяцНачисленияИмя		= "ПериодРегистрации";
	Описание.ПериодДействияВШапке	= Истина;
	Описание.НачалоБазовогоПериодаИмя = "";
	Описание.ОкончаниеБазовогоПериодаИмя = "";
	Описание.ИменаПолейНачисления	= "ВидПрочегоДохода";
	Описание.ВидНачисленияВШапке	= Истина;
	Описание.ВидНачисленияИмя		= "ВидПрочегоДохода";
	
	Описание.РассчитыватьУдержанияИмя = "РассчитыватьУдержания";
	
	Описание.НачисленияИмя			= "НачисленияУдержанияВзносы";
	Описание.НДФЛИмя				= "НДФЛ";
	
	Описание.РегистрацияНачисленийДоступна = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаСервере
Функция СведенияОбНДФЛ() Экспорт
	
	ТаблицаНачислений = Объект.НачисленияУдержанияВзносы.Выгрузить();
	
	ТаблицаНачислений.Колонки.Добавить("Начисление", Новый ОписаниеТипов());
	ТаблицаНачислений.ЗаполнитьЗначения(Объект.ВидПрочегоДохода, "Начисление");
	
	Возврат УчетНДФЛФормыРасширенный.СведенияОбНДФЛ(ЭтаФорма, , , ТаблицаНачислений);
	
КонецФункции

&НаСервере
Процедура РассчитатьНДФЛИВзносыНаСервере(ФизическоеЛицо = Неопределено)
	
	Если ФизическоеЛицо = Неопределено Тогда
		СписокФизическихЛиц = Неопределено;
	Иначе
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.РассчитатьНДФЛИВзносыДокумента(СписокФизическихЛиц);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	РассчитатьИтогиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимРасчетаНДФЛИОтображениеЭлементовРедактированияНДФЛ()
	
	РасчетНДФЛНарастающимИтогомСНачалаГода = УчетНДФЛФормыРасширенный.РасчетНДФЛНарастающимИтогомСНачалаГода(Объект.ВидПрочегоДохода);
	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПодробнееОРасчетеНДФЛ",
		"Видимость",
		РасчетНДФЛНарастающимИтогомСНачалаГода);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачисленияНДФЛ",
		"ТолькоПросмотр",
		РасчетНДФЛНарастающимИтогомСНачалаГода);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаполнитьПоВыплатам",
		"Видимость",
		Объект.ВидПрочегоДохода.КодДоходаНДФЛ.Код = "157" ИЛИ Объект.ВидПрочегоДохода.КодДоходаНДФЛ.Код = "140");	
		
КонецПроцедуры


&НаСервере
Процедура ОбновитьДанныеНДФЛНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Объект.НДФЛ.Загрузить(Параметр.НДФЛ.Выгрузить());
	
	
	Модифицированность = Истина;
	
	ЗаполнитьИтогиКВыплате();
	РассчитатьИтогиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЧПНаСервере()
	
	//++ НЕ БЗК
	Объект.НачисленияУдержанияВзносы.Очистить(); 
	Объект.НДФЛ.Очистить();

	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ОстакиОбороты.Контрагент КАК Контрагент,
	               |	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
	               |	СУММА(ОстакиОбороты.УдержНДФЛ) КАК УдержНДФЛ,
	               |	СУММА(ОстакиОбороты.УдержВС) КАК УдержВС,
	               |	СУММА(ВЫБОР
	               |			КОГДА ОстакиОбороты.ОбДт > ОстакиОбороты.ОбКт
	               |				ТОГДА ОстакиОбороты.ОбДт - ОстакиОбороты.ОбКт
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК НачисленоДохода
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		Хозрасчетный.Контрагент КАК Контрагент,
	               |		Хозрасчетный.Договор КАК Договор,
	               |		СУММА(Хозрасчетный.УдержНДФЛ) КАК УдержНДФЛ,
	               |		СУММА(Хозрасчетный.УдержВС) КАК УдержВС,
	               |		СУММА(Хозрасчетный.ОбКт) КАК ОбКт,
	               |		СУММА(Хозрасчетный.ОбДт) КАК ОбДт
	               |	ИЗ
	               |		(ВЫБРАТЬ
	               |			ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	               |			ХозрасчетныйОбороты.Субконто2 КАК Договор,
	               |			ВЫБОР
	               |				КОГДА ХозрасчетныйОбороты.КорСчет В (&Счет6411)
	               |					ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	               |				ИНАЧЕ 0
	               |			КОНЕЦ КАК УдержНДФЛ,
	               |			ВЫБОР
	               |				КОГДА ХозрасчетныйОбороты.КорСчет В (&Счет642)
	               |					ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
	               |				ИНАЧЕ 0
	               |			КОНЕЦ КАК УдержВС,
	               |			ХозрасчетныйОбороты.СуммаОборотДт КАК ОбДт,
	               |			ХозрасчетныйОбороты.СуммаОборотКт КАК ОбКт
	               |		ИЗ
	               |			РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетов), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
	               |		ГДЕ
	               |			ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	               |			И (ХозрасчетныйОбороты.КорСчет В ИЕРАРХИИ (&СписокКорСчетов)
	               |					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет6411)
	               |					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет642))) КАК Хозрасчетный
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		Хозрасчетный.Контрагент,
	               |		Хозрасчетный.Договор) КАК ОстакиОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ПО (ОстакиОбороты.Контрагент.КодПоЕДРПОУ <> """")
	               |			И ОстакиОбороты.Контрагент.КодПоЕДРПОУ = ФизическиеЛица.КодПоДРФО
	               |ГДЕ
	               |	(ОстакиОбороты.ОбДт > 0
	               |			ИЛИ ОстакиОбороты.ОбКт > 0)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстакиОбороты.Контрагент,
	               |	ФизическиеЛица.Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	                                
	Запрос.УстановитьПараметр("парамОрганизация", Объект.Организация);
	Если Объект.ПериодРегистрации < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Объект.ПериодРегистрации) );
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации) );
	КонецЕсли;	
	
	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками);				//631
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУчастникамиПФГ);				//633
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте);	//6851
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоВыданнымАвансамВНациональнойВалюте);	//3711
	
	СписокСчетовБанкКасса = Новый СписокЗначений;
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.Касса);			//301
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.СчетаВБанках);	//311
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВНациональнойВалюте);	//3721
	
	СписокСчетовАвансы = Новый СписокЗначений;
	СписокСчетовАвансы.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоВыданнымАвансамВНациональнойВалюте);	//3711
	
	СписокСчетовПоставщики = Новый СписокЗначений;
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками);				//631
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУчастникамиПФГ);				//633
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте);	//6851
	
	Запрос.УстановитьПараметр("СписокСчетов"	, СписокСчетов);
	Запрос.УстановитьПараметр("Счет6411"		, ПланыСчетов.Хозрасчетный.РасчетыПоНДФЛ);
	Запрос.УстановитьПараметр("Счет642"			, ПланыСчетов.Хозрасчетный.РасчетыПоОбязательнымПлатежам);
	Запрос.УстановитьПараметр("СписокКорСчетов"	, СписокСчетовБанкКасса);
	Запрос.УстановитьПараметр("СписокСчетовАвансы"	, СписокСчетовАвансы);
    Запрос.УстановитьПараметр("СписокСчетовПоставщики"	, СписокСчетовПоставщики);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НачисленоДохода = 0 И Выборка.УдержНДФЛ = 0 И Выборка.УдержВС = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(Выборка.ФизическоеЛицо) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Для контрагента - %1 не найдено соответствия физического лица по коду ДФРО. Информация о нем не заполнена в документе!';uk='Увага! Для контрагента - %1 не знайдено відповідності фізичної особи за кодом ДФРО. Інформація про нього не заповнена в документі!'"), Выборка.Контрагент);
			Сообщить (ТекстСообщения);
			Продолжить;
			
		КонецЕсли;
		
		ТекСтрока = Объект.НачисленияУдержанияВзносы.Добавить();
			
		ТекСтрока.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		ТекСтрока.Начислено = Выборка.НачисленоДохода;
		ТекСтрока.НДФЛ = Выборка.УдержНДФЛ + Выборка.УдержВС;
		ТекСтрока.КВыплате = ТекСтрока.Начислено - ТекСтрока.НДФЛ;
		
		ТекСтрокаНДФЛ = Объект.НДФЛ.Добавить();
		ТекСтрокаНДФЛ.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		ТекСтрокаНДФЛ.Доход = Выборка.НачисленоДохода;
		ТекСтрокаНДФЛ.Налог = Выборка.УдержНДФЛ;
		ТекСтрокаНДФЛ.КодДохода = Объект.ВидПрочегоДохода.КодДоходаНДФЛ;
		ТекСтрокаНДФЛ.НалоговыйПериод = Объект.ПериодРегистрации;
		
		Если ЗначениеЗаполнено(Выборка.УдержВС) Тогда
			ТекСтрокаВС = Объект.НДФЛ.Добавить();
			ТекСтрокаВС.ФизическоеЛицо = Выборка.ФизическоеЛицо;
			ТекСтрокаВС.Доход = Выборка.НачисленоДохода;
			ТекСтрокаВС.Налог = Выборка.УдержВС;
			Если  Объект.ПериодРегистрации < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда
				ТекСтрокаВС.КодДохода = Объект.ВидПрочегоДохода.КодДоходаНДФЛ.ОблагаетсяВоеннымСбором;
			Иначе
				ТекСтрокаВС.КодДохода = Объект.ВидПрочегоДохода.КодДоходаНДФЛ.ОблагаетсяВоеннымСбором2021;
			КонецЕсли;	
			ТекСтрокаВС.НалоговыйПериод = Объект.ПериодРегистрации;
		КонецЕсли;	

	КонецЦикла;
	
	//-- НЕ БЗК

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАлиментыНаСервере()
	
	//++ НЕ БЗК
	Объект.НачисленияУдержанияВзносы.Очистить(); 
	Объект.НДФЛ.Очистить();

	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	               |	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК НачисленоДохода
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетов), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ПО (ХозрасчетныйОбороты.Субконто1.КодПоЕДРПОУ <> """")
	               |			И ХозрасчетныйОбороты.Субконто1.КодПоЕДРПОУ = ФизическиеЛица.КодПоДРФО
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Субконто1,
	               |	ФизическиеЛица.Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("парамОрганизация", Объект.Организация);
	Если Объект.ПериодРегистрации < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Объект.ПериодРегистрации) );
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации) );
	КонецЕсли;

	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументам);				//6853
	
	Запрос.УстановитьПараметр("СписокСчетов"	, СписокСчетов);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Выборка.ФизическоеЛицо) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Внимание! Для контрагента - %1 не найдено соответствия физического лица по коду ДФРО. Информация о нем не заполнена в документе!';uk='Увага! Для контрагента - %1 не знайдено відповідності фізичної особи за кодом ДФРО. Інформація про нього не заповнена в документі!'"), Выборка.Контрагент);
			Сообщить (ТекстСообщения);
			Продолжить;
			
		КонецЕсли;
		
		ТекСтрока = Объект.НачисленияУдержанияВзносы.Добавить();
			
		ТекСтрока.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		ТекСтрока.Начислено = Выборка.НачисленоДохода;
		ТекСтрока.НДФЛ = 0;
		ТекСтрока.КВыплате = ТекСтрока.Начислено;
		
		ТекСтрокаНДФЛ = Объект.НДФЛ.Добавить();
		ТекСтрокаНДФЛ.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		ТекСтрокаНДФЛ.Доход = Выборка.НачисленоДохода;
		ТекСтрокаНДФЛ.Налог = 0;
		ТекСтрокаНДФЛ.КодДохода = Объект.ВидПрочегоДохода.КодДоходаНДФЛ;
		ТекСтрокаНДФЛ.НалоговыйПериод = Объект.ПериодРегистрации;
		
	КонецЦикла;
	//-- НЕ БЗК

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоВыплатам(Команда)
	Если Элементы.КодДоходаНДФЛ.ТекстРедактирования = "157" Тогда
		ЗаполнитьЧПНаСервере();
	ИначеЕсли Элементы.КодДоходаНДФЛ.ТекстРедактирования = "140" Тогда
		ЗаполнитьАлиментыНаСервере();	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства
