#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;

	Если ЗначениеЗаполнено(Организация) Тогда
		Шаблон = НСтр("ru='Указанная организация должна быть плательщиком НДС';uk='Зазначена організація повинна бути платником ПДВ'");
		ОрганизацияПлательщикНДС = НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Организация, Дата);
		ПроверитьИСообщитьОшибку(Не ОрганизацияПлательщикНДС,
			Отказ, Шаблон, "Организация", "Организация");		
	КонецЕсли; 
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		
		НепроверяемыеРеквизиты.Добавить("Партнер");
		НепроверяемыеРеквизиты.Добавить("Соглашение");
		
		Если ЗначениеЗаполнено(Контрагент) и ЗначениеЗаполнено(Организация) Тогда
			Шаблон = НСтр("ru='Организация %Организация% не может быть одновременно и поставщиком и получателем';uk='Організація %Организация% не може бути одночасно і постачальником і одержувачем'");
			Шаблон = СтрЗаменить(Шаблон, "%Организация%", Организация);
			ПроверитьИСообщитьОшибку(Контрагент = Организация,
				Отказ, Шаблон, "Конрагент", "Конрагент");		
		КонецЕсли;

	КонецЕсли;
	
	Если Не ВключаетсяВУточняющийРасчет Тогда
		НепроверяемыеРеквизиты.Добавить("ДатаОтгрузкиОплаты");
	КонецЕсли;

    СуммаНДСДокументаКПроверке = Поставка.Итог("СуммаНДС");
	СуммаДокументаКПроверке    = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(Поставка, Ложь);
	
	Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита
		И НЕ (СуммаДокументаКПроверке = 0 И СуммаНДСДокументаКПроверке = 0) Тогда				
		Сообщение = НСтр("ru='Для данного вида операции сумма документа и сумма НДС должна быть нулевой!';uk='Для даного виду операції сума документа та ПДВ повинна дорівнювати нулю!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, , "Поставка", "Объект", Отказ);
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита Тогда
		НепроверяемыеРеквизиты.Добавить("Поставка.СтатьяДекларацииНДСНалоговыйКредит");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	// Выбор статей и аналитик.
	ПараметрыВыбораСтатейИАналитик = Документы.РегистрацияВходящегоНалоговогоДокумента.ПараметрыВыбораСтатейИАналитик(ВидОперации);
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты,
		ПараметрыВыбораСтатейИАналитик);

	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки)
	 ИЛИ (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита) 
	 ИЛИ (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек)Тогда
		// Не очищаем реквизит
 	Иначе
		ВидДокумента = Неопределено;
	КонецЕсли;
	
	Если (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат) 
	 ИЛИ (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка) Тогда
		// Не очищаем реквизит
 	Иначе
		НомерКорректируемогоВходящегоДокумента = Неопределено;
		ДатаКорректируемогоВходящегоДокумента = Неопределено;
		ОснованиеКорректировки = Неопределено;
	КонецЕсли;
		
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		Партнер = Справочники.Партнеры.НашеПредприятие;
		Соглашение = Неопределено;
	КонецЕсли;

	Если Договор = Неопределено Тогда
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
			Договор = Справочники.ДоговорыМеждуОрганизациями.ПустаяСсылка();
		Иначе
			Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	ЕстьДвиженияПоРегиструСоставПоставки = 
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная ИЛИ
	 	ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат ИЛИ
	 	ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка ИЛИ
	 	ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек ИЛИ
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита;	 
		
	Для каждого Строка Из Поставка Цикл
		Если Не ЗначениеЗаполнено(Строка.ДокументПоставки) И Строка.ДокументПоставки <> Неопределено Тогда
			Строка.ДокументПоставки = Неопределено;
		КонецЕсли;
	КонецЦикла;

    Если Не ЕстьДвиженияПоРегиструСоставПоставки Тогда
		ПоВводуОстатков = Ложь;
	КонецЕсли;

	ЭтоВидОперацииДляОтраженияПереоценки = 
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка ИЛИ
 		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек;
	
	Если Не ЭтоВидОперацииДляОтраженияПереоценки Тогда 
		Переоценка = Ложь;	
	КонецЕсли;	
	
    СуммаНДСДокумента = Поставка.Итог("СуммаНДС");
	СуммаДокумента    = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(Поставка, Ложь);
	
	// Выбор статей и аналитик.
	ПараметрыВыбораСтатейИАналитик = Документы.РегистрацияВходящегоНалоговогоДокумента.ПараметрыВыбораСтатейИАналитик(ВидОперации);
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанных = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанных = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("ВидПоставки") Тогда
			
			КонтрагентПлательщикНДС = НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Контрагент);
			
			Если КонтрагентПлательщикНДС Тогда
				Если ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.ВводОстатков Тогда
					ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
					
				ИначеЕсли ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.Поставка Тогда
					ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
					
				ИначеЕсли ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
					ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат;				
					
				ИначеЕсли ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.Переоценка Тогда
					ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка;
					
				КонецЕсли;
			Иначе
				// Для неплательщиков НДС всегда "Прочее (товарный чек и т. п.)"
				ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек;
			КонецЕсли;
			
			Если ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.ВводОстатков Тогда
			    ПоВводуОстатков = Истина;
				
			ИначеЕсли ДанныеЗаполнения.ВидПоставки = Перечисления.ВидыПоставки.Переоценка Тогда
				Переоценка = Истина;			
				
			КонецЕсли;
			
			Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Партнер = Неопределено;
			КонецЕсли;
			
			Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоОстаткам(ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Выбор статей и аналитик.
	ПараметрыВыбораСтатейИАналитик = Документы.РегистрацияВходящегоНалоговогоДокумента.ПараметрыВыбораСтатейИАналитик(ВидОперации);
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	ИнициализироватьДокумент();

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()
		
	Ответственный          = Пользователи.ТекущийПользователь();
	Организация            = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	ДатаВходящегоДокумента = ?(ЗначениеЗаполнено(ДатаВходящегоДокумента), ДатаВходящегоДокумента, ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДата()));

	ОбособленноеПодразделение = НДСВходящийСервер.ОпределитьОбособленноеПодразделениеПоУмолчанию(
		Организация, 
		Договор
	);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ПроверитьИСообщитьОшибку(УсловиеОшибки, Отказ, Знач Шаблон, Знач ИмяПоля, Знач СинонимПоля, Знач ИмяТабчасти = Неопределено, Знач НомерСтроки = Неопределено)
	
	Если УсловиеОшибки Тогда
		Сообщение = СтрЗаменить(Шаблон, "%СинонимПоля%", СинонимПоля);
		Сообщение = СтрЗаменить(Сообщение, "%НомерСтроки%", НомерСтроки);
		Сообщение = СтрЗаменить(Сообщение, "%ИмяТабчасти%", ИмяТабчасти);
		Если ЗначениеЗаполнено(ИмяТабчасти) И НомерСтроки > 0 Тогда
			ИмяПоля = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТабчасти, НомерСтроки, ИмяПоля);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, , ИмяПоля, "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ИННКонтрагента(Контрагент)

	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат "";
	Иначе
		Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИННПлательщикаНДС"));	
	КонецЕсли;

КонецФункции // ()

Процедура ЗаполнитьИзXML(ЗаполняемыеДанные = "", ТекстXML = Неопределено) Экспорт
	
	ЗаполнятьШапку = Истина;
	ЗаполнятьТЧ = Истина;
	ЗаполнятьКорректируемыйДокумент = Истина;
	Если НЕ ПустаяСтрока(ЗаполняемыеДанные) Тогда
		
		ЗаполняемыеДанныеВРЕГ = ВРег(ЗаполняемыеДанные);
		ЗаполнятьШапку = СтрНайти(ЗаполняемыеДанныеВРЕГ, "ШАПКА") > 0;
		ЗаполнятьТЧ = СтрНайти(ЗаполняемыеДанныеВРЕГ, "ТЧ") > 0;
		ЗаполнятьКорректируемыйДокумент = СтрНайти(ЗаполняемыеДанныеВРЕГ, "КОРРЕКТИРУЕМЫЙДОКУМЕНТ") > 0;
		
	КонецЕсли; 
	
	Если НЕ ЗаполнятьШапку И НЕ ЗаполнятьТЧ И НЕ ЗаполнятьКорректируемыйДокумент Тогда
		Возврат;
	КонецЕсли;
	
	
	СтруктураПоказателейXML = Новый Структура();
	
	ТЧ = Новый ТаблицаЗначений;
	СтруктураПоказателейXML.Вставить("ТабличнаяЧасть", ТЧ);
	ТЧ.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	КоличествоСтрокТЧ = 0;
	
	Если ТекстXML = Неопределено Тогда
		
		ДДXML = ДанныеXML.Получить();// двоичные данные
		Если ЗначениеЗаполнено(ДДXML) Тогда
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
			ДДXML.Записать(ИмяВременногоФайла);
			Текст = Новый ТекстовыйДокумент;
			Текст.Прочитать(ИмяВременногоФайла, "windows-1251");
			ТекстXML = Текст.ПолучитьТекст();
		    УдалитьФайлы(ИмяВременногоФайла);
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ИгнорироватьПробелы = Истина;
	Попытка
		ЧтениеXML.УстановитьСтроку(ТекстXML);
	Исключение
		
		Сообщить(НСтр("ru='Данный файл содерижит информацию в закодированном виде и не является стандартым текстовым XML-файлом.
|Необходимо воспользоваться специализированным программным обеспечением для его расшифровки.'
|;uk='Зазначений файл містить інформацію в закодованому вигляді та не являє собою стандартний текстовий XML-файл.
|Необхідно скористатися спеціалізованим програмним забезбеченням для його розшифрування.'"));
							 
		Возврат;
		
	КонецПопытки;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ТекУзел = ВРЕГ(ЧтениеXML.Имя);
			НачалоХХХХ = Найти(ТекУзел, "XXXX");
			
			Если    ТекУзел = "DECLAR"
				ИЛИ ТекУзел = "DECLARHEAD"
				ИЛИ ТекУзел = "DECLARBODY"
				ИЛИ ТекУзел = "LINKED_DOCS" Тогда
				Продолжить;
			ИначеЕсли ВРЕГ(ЧтениеXML.ЗначениеАтрибута("xsi:nil")) = "TRUE" Тогда
					Продолжить;
			ИначеЕсли НачалоХХХХ > 0 И ЗаполнятьТЧ Тогда
				
				НомерСтрокиСтрокой = ЧтениеXML.ЗначениеАтрибута("ROWNUM");
				Если Не ЗначениеЗаполнено(НомерСтрокиСтрокой) Тогда
					Продолжить;
				КонецЕсли;        

				НомерСтроки = Число(НомерСтрокиСтрокой);
				Пока НомерСтроки > КоличествоСтрокТЧ Цикл
					
					КоличествоСтрокТЧ = КоличествоСтрокТЧ + 1;

					НоваяСтрока = ТЧ.Добавить();
					НоваяСтрока.НомерСтроки = КоличествоСтрокТЧ;

				КонецЦикла;
				
				ИмяКолонки = Сред(ТекУзел, НачалоХХХХ + 4);
				Если ТЧ.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
					ТЧ.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЕсли;
				
				ЧтениеXML.Прочитать();
				ТЧ[НомерСтроки - 1][ИмяКолонки] = ЧтениеXML.Значение;
				
			Иначе
				ЧтениеXML.Прочитать();
				СтруктураПоказателейXML.Вставить(ТекУзел, ЧтениеXML.Значение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;		

	ЧтениеXML.Закрыть();   
	
	// определим номер схемы документа
	C_DOC     = ""; СтруктураПоказателейXML.Свойство("C_DOC",	   C_DOC);
	C_DOC_SUB = ""; СтруктураПоказателейXML.Свойство("C_DOC_SUB", C_DOC_SUB);
	C_DOC_VER = ""; СтруктураПоказателейXML.Свойство("C_DOC_VER", C_DOC_VER);

	// найдем подходящую форму налоговой (регл. отчет)
	Если  НЕ (C_DOC     = "J12" ИЛИ C_DOC     = "F12")
		И НЕ (C_DOC_SUB = "010" ИЛИ C_DOC_SUB = "012") Тогда
		
		// не тот XML подсунули
		Сообщить(НСтр("ru='Загруженный файл не является XML образом Налоговой накладной или Приложения 2 к налоговой накладной!';uk='Завантажений файл не є XML образом Податкової накладної або Додатка 2 до податкової накладної!'"));
		Возврат;	
		
	КонецЕсли;
	
	ПоддерживаемаяВерсия = Ложь;
	
	Попытка
		ПоддерживаемаяВерсия =  Число(C_DOC_VER) >= 8;
	Исключение
	КонецПопытки;
	
	Если Не ПоддерживаемаяВерсия Тогда
		Сообщить(НСтр("ru='Загруженный файл XML имеет устаревшую версию, загрузка не поддерживается.';uk='Завантажений файл XML має застарілу версію, завантаження не підтримується.'"));
		Возврат;	
	КонецЕсли;
	
	Если ЗаполнятьШапку ИЛИ ЗаполнятьТЧ Тогда
		
		Если C_DOC_SUB = "010" Тогда
			ИсточникиСумм = 
			"R03G7, НДС20, НДС
			|R03G109, НДС7, НДС
			|R03G14, НДС14, НДС
			|R01G7, НДС20, База 
			|R01G109, НДС7, База
			|R01G14, НДС14, База
			|R01G9, НДС0, База
			|R01G8, НДС0, База
			|R01G10, БезНДС, База";
		Иначе
			ИсточникиСумм = 
			"R02G9, НДС20, НДС
			|R02G111, НДС7, НДС
			|R03G14, НДС14, НДС
			|R01G9, НДС20, База 
			|R01G111, НДС7, База
			|R01G14, НДС14, База
			|R006G03, НДС0, База
			|R007G03, НДС0, База
			|R01G11, БезНДС, База";
		КонецЕсли;
		
		ТаблицаСумм = Новый ТаблицаЗначений;
		ТаблицаСумм.Колонки.Добавить("Имя");
		ТаблицаСумм.Колонки.Добавить("СтавкаНДС");
		ТаблицаСумм.Колонки.Добавить("Показатель");
		ТаблицаСумм.Колонки.Добавить("База", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		ТаблицаСумм.Колонки.Добавить("НДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		ТаблицаСумм.Колонки.Добавить("Установлен", Новый ОписаниеТипов("Булево"));

		
		Для ж = 1 По СтрЧислоСтрок(ИсточникиСумм) Цикл
			
			НовыйИсточник = ТаблицаСумм.Добавить();
			ЧастиИсточника = СтрРазделить(СтрПолучитьСтроку(ИсточникиСумм, ж), ",", Истина);
			Для жж = 0 По 2 Цикл      
				ЧастьИсточника = СокрЛП(ЧастиИсточника[жж]);
				Если жж = 1 Тогда
					НовыйИсточник[жж] = НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС[ЧастьИсточника]);					
				Иначе
					НовыйИсточник[жж] = ЧастьИсточника;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ЗначениеПоказателя = "";
		Для каждого СтрокаТаблицы Из ТаблицаСумм Цикл
			
			Если СтруктураПоказателейXML.Свойство(СтрокаТаблицы.Имя, ЗначениеПоказателя) Тогда
				
				СтрокаТаблицы[СтрокаТаблицы.Показатель] = ЗначениеПоказателя;
				СтрокаТаблицы.Установлен = Истина;	
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаСумм = ТаблицаСумм.Скопировать(Новый Структура("Установлен", Истина), "СтавкаНДС, База, НДС");
		ТаблицаСумм.Свернуть("СтавкаНДС", "База, НДС");
		
	КонецЕсли;

	Если ЗаполнятьШапку Тогда
		
  		// заполним структуру показателй последней текущей версии
		// переопределим для старых версий
		
		Если C_DOC_SUB = "010" Тогда
			НовВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная");	
		Иначе
			НовВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат");	
		КонецЕсли;
		
		Если ВидОперации <> НовВидОперации Тогда
			
			Сообщить(НСтр("ru='При заполнении изменен вид операции документа!';uk='При заповненні змінено вид операції документа!'"), СтатусСообщения.Важное);
			
			ВидОперации = НовВидОперации;
			
			ВидДокумента = "-";
			ВключаетсяВУточняющийРасчет = Ложь;	
			
		КонецЕсли;
		
		// Организация
		ОрганизацияИНН = ""; СтруктураПоказателейXML.Свойство("HKBUY", ОрганизацияИНН);
		
		СписокПоказателей = Новый СписокЗначений; 
		СписокПоказателей.Добавить("","ИНН");
		Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Дата, СписокПоказателей);
		
		Если НЕ Сведения.Свойство("ИНН") ИЛИ НЕ (ОрганизацияИНН = Сведения.ИНН) Тогда
			
			Если ЗначениеЗаполнено(Организация) Тогда
				Сообщить(НСтр("ru='ИНН Организации в документе не совпадает с ИНН Покупателя из файла!';uk='ІПН Організації в документі, не збігається з ІПН Покупця з файлу!'"), СтатусСообщения.Важное);	                                   
			КонецЕсли;
			
			ЗаполнитьОрганизациюПоИНННаСервере(ОрганизацияИНН);
			
		КонецЕсли;
		
		// контрагент
		КонтрагентИНН = ""; 		СтруктураПоказателейXML.Свойство("HKSEL", КонтрагентИНН);

		Если НЕ ИННКонтрагента(Контрагент) = КонтрагентИНН Тогда
			
			Если ЗначениеЗаполнено(Контрагент) Тогда
				Сообщить(НСтр("ru='Данные контрагента (ИНН) не соответствуют данным из файла';uk='Дані контрагента (ІПН) не відповідають даним з файлу'"), СтатусСообщения.Важное);	
			КонецЕсли;
			
			ЗаполнитьКонтрагентаПоИНННаСервере(КонтрагентИНН);		
			
		КонецЕсли;

		// договор контрагента
		ДоговорКонтрагентаДата  = "";
		ДоговорКонтрагентаНомер = "";
		Если C_DOC_SUB = "010" Тогда
			СтруктураПоказателейXML.Свойство("H01G2D", ДоговорКонтрагентаДата);
			СтруктураПоказателейXML.Свойство("H01G3S", ДоговорКонтрагентаНомер);
		Иначе
			СтруктураПоказателейXML.Свойство("H01G1D", ДоговорКонтрагентаДата);
			СтруктураПоказателейXML.Свойство("H01G2S", ДоговорКонтрагентаНомер);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДоговорКонтрагентаДата) Тогда
			ДоговорКонтрагентаДата = СокрЛП(ДоговорКонтрагентаДата);
			ДоговорКонтрагентаДата = Дата(Сред(ДоговорКонтрагентаДата, 5, 4), Сред(ДоговорКонтрагентаДата, 3, 2), Сред(ДоговорКонтрагентаДата, 1, 2));
		КонецЕсли;
		
		Если  ЗначениеЗаполнено(Контрагент)
			И ЗначениеЗаполнено(ДоговорКонтрагентаДата)
			И ЗначениеЗаполнено(ДоговорКонтрагентаНомер) Тогда
			
			ЗаполнитьДоговорНаСервере(ДоговорКонтрагентаДата, ДоговорКонтрагентаНомер);
			
		КонецЕсли;
		
		HFILL = ""; СтруктураПоказателейXML.Свойство("HFILL", HFILL);
		ДатаВходящегоДокумента = '00010101';
		Если ЗначениеЗаполнено(HFILL) Тогда
			HFILL = СокрЛП(HFILL);
			ДатаВходящегоДокумента = Дата(Сред(HFILL, 5, 4), Сред(HFILL, 3, 2), Сред(HFILL, 1, 2));
		КонецЕсли;
		ДатаВходящегоДокумента = ДатаВходящегоДокумента;
		
		//номер вх. документа
		HNUM  = ""; СтруктураПоказателейXML.Свойство("HNUM",  HNUM);  HNUM  = СокрЛП(HNUM);
		HNUM1 = ""; СтруктураПоказателейXML.Свойство("HNUM1", HNUM1); HNUM1 = СокрЛП(HNUM1); HNUM1 = ?(HNUM1 = "0", "", HNUM1);
		HNUM2 = ""; СтруктураПоказателейXML.Свойство("HNUM2", HNUM2); HNUM2 = СокрЛП(HNUM2);
		НомерВходящегоДокумента = HNUM; 
		Если ЗначениеЗаполнено(HNUM1) Тогда
			НомерВходящегоДокумента = НомерВходящегоДокумента + "/" + HNUM1;
		КонецЕсли;                     
		Если ЗначениеЗаполнено(HNUM2)  И ДатаВходящегоДокумента < '2016-04-01' Тогда
			Если ЗначениеЗаполнено(HNUM1) Тогда
				Если C_DOC_VER > "04" Тогда
					// всего два слеша
					НомерВходящегоДокумента = НомерВходящегоДокумента + "/" + HNUM2;	
				Иначе	
				    // всего один слеш
					НомерВходящегоДокумента = НомерВходящегоДокумента + HNUM2;	
				КонецЕсли;
			Иначе
				Если C_DOC_VER > "04" Тогда
					// два слеша
					НомерВходящегоДокумента = НомерВходящегоДокумента + "//" + HNUM2;	
				Иначе	
				    // один слеш
					НомерВходящегоДокумента = НомерВходящегоДокумента + "/ " + HNUM2;	
				КонецЕсли;
			
			КонецЕсли;
		КонецЕсли;
		НомерВходящегоДокумента = НомерВходящегоДокумента;
				
		Поставка.Очистить();

 		Для каждого СтрокаСумм Из ТаблицаСумм Цикл
			
			Если СтрокаСумм.База = 0 И СтрокаСумм.НДС = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаДок = Поставка.Добавить();
			
			СтрокаДок.СтавкаНДС = СтрокаСумм.СтавкаНДС;
			СтрокаДок.Сумма     = СтрокаСумм.База; 
			СтрокаДок.СуммаНДС  = СтрокаСумм.НДС;
	
		КонецЦикла;
	
		// Тара
		Тара = 0;
		Если C_DOC_SUB = "010" Тогда
			СтруктураПоказателейXML.Свойство("R02G11",  Тара);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Тара) И Число(Тара) <> 0 Тогда
			
			СуммаВозвратнойТары = СуммаВозвратнойТары + Число(Тара);
		
		КонецЕсли;
		
	КонецЕсли;  

 	Если ЗаполнятьШапку ИЛИ ЗаполнятьКорректируемыйДокумент Тогда
		
		// для П2 дата и номер корректируемой налоговой накладной
		Если C_DOC_SUB = "012" Тогда
			HNUM  = ""; СтруктураПоказателейXML.Свойство("HPODNUM",  HNUM);  HNUM  = СокрЛП(HNUM);
			HNUM1 = ""; СтруктураПоказателейXML.Свойство("HPODNUM1", HNUM1); HNUM1 = СокрЛП(HNUM1); HNUM1 = ?(HNUM1 = "0", "", HNUM1);
			HNUM2 = ""; СтруктураПоказателейXML.Свойство("HPODNUM2", HNUM2); HNUM2 = СокрЛП(HNUM2);
			НомерКорректируемогоВходящегоДокумента = HNUM; 
			Если ЗначениеЗаполнено(HNUM1) Тогда
				НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/" + HNUM1;
			КонецЕсли;
			Если ЗначениеЗаполнено(HNUM2) Тогда
				Если ЗначениеЗаполнено(HNUM1) Тогда
					Если C_DOC_VER > "04" Тогда
						// всего два слеша
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/" + HNUM2;	
					Иначе	
					    // всего один слеш
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + HNUM2;	
					КонецЕсли;
				Иначе
					Если C_DOC_VER > "04" Тогда
						// два слеша
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "//" + HNUM2;	
					Иначе	
					    // один слеш
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/ " + HNUM2;	
					КонецЕсли;
				
				КонецЕсли;
			КонецЕсли;

			HFILL = ""; СтруктураПоказателейXML.Свойство("HPODFILL", HFILL);
			ДатаКорректируемогоВходящегоДокумента = '00010101';
			Если ЗначениеЗаполнено(HFILL) Тогда
				HFILL = СокрЛП(HFILL);
				ДатаКорректируемогоВходящегоДокумента = Дата(Сред(HFILL, 5, 4), Сред(HFILL, 3, 2), Сред(HFILL, 1, 2));
			КонецЕсли;
			НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента;
			ДатаКорректируемогоВходящегоДокумента  = ДатаКорректируемогоВходящегоДокумента;	
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаполнятьТЧ Тогда
		
		Поставка.Очистить();
		
		КэшПоискаУКТВЭД = Неопределено;
		КэшПоискаЕдиИзм = Неопределено;
		
		СоответствиеСтавокНДС = Новый Соответствие;
		СоответствиеСтавокНДС.Вставить("20", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС20));
		СоответствиеСтавокНДС.Вставить("7", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС7));
		СоответствиеСтавокНДС.Вставить("14", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС14));
		СоответствиеСтавокНДС.Вставить("901", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС0));
		СоответствиеСтавокНДС.Вставить("902", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС0));
		СоответствиеСтавокНДС.Вставить("903", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.БезНДС));
		
		Для каждого СтрокаФайла Из СтруктураПоказателейXML.ТабличнаяЧасть Цикл

			СтруктураИсходныхДанных = Новый Структура("G001,G21,G22,G3S,G5,G6,G7,G8,G008,G009,G010,G11_10");
			ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанных, СтрокаФайла);
			
			НоваяСтрока = Поставка.Добавить();
			
			НоваяСтрока.НаименованиеТовара = СтруктураИсходныхДанных.G3S;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G5) Тогда
				НоваяСтрока.Количество = Число(СтруктураИсходныхДанных.G5);
			ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанных.G8) Тогда
				НоваяСтрока.Количество = Число(СтруктураИсходныхДанных.G8);
			КонецЕсли;

			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G6) Тогда
				НоваяСтрока.Цена = Число(СтруктураИсходныхДанных.G6);
			ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанных.G7) Тогда
				НоваяСтрока.Цена = Число(СтруктураИсходныхДанных.G7);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G008) Тогда
				НоваяСтрока.СтавкаНДС = СоответствиеСтавокНДС[СтруктураИсходныхДанных.G008];
			КонецЕсли; 

			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G010) Тогда
				НоваяСтрока.Сумма = Число(СтруктураИсходныхДанных.G010);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G11_10) Тогда
				НоваяСтрока.СуммаНДС = Число(СтруктураИсходныхДанных.G11_10);
			КонецЕсли;
			
			
			НоваяСтрока.КодУКТВЭД = ПолучитьЭлементКлассификатораУКТВЭД(СтрокаФайла, КэшПоискаУКТВЭД);
			НоваяСтрока.ЕдиницаИзмерения = ПолучитьЭлементКлассификатораЕдиницИзмерения(СтрокаФайла, КэшПоискаЕдиИзм);

			
			
		КонецЦикла;
		
		//если итоги не совпадают с данными раздела А, распределим расхождение по строкам
		ТаблицаПогрешностей = Поставка.Выгрузить(,"НомерСтроки, СтавкаНДС, СуммаНДС");
		ТаблицаПогрешностей.Колонки.Добавить("СуммаНДСМинус", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		
		Для каждого СтрокаПогрешностей Из ТаблицаПогрешностей Цикл
			
			Если СтрокаПогрешностей.СуммаНДС < 0 Тогда
				СтрокаПогрешностей.СуммаНДСМинус = СтрокаПогрешностей.СуммаНДС;
				СтрокаПогрешностей.СуммаНДС = 0;
			КонецЕсли;
			
		КонецЦикла;
		
		ИтогиПогрешностей = ТаблицаПогрешностей.Скопировать();
		ИтогиПогрешностей.Свернуть("СтавкаНДС", "СуммаНДС, СуммаНДСМинус");
		Для каждого СтрокаИтогов Из ИтогиПогрешностей Цикл
			
			СтрокаРазделаА = ТаблицаСумм.Найти(СтрокаИтогов.СтавкаНДС, "СтавкаНДС");
			Если СтрокаРазделаА = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			РасхождениеПоСтавке = СтрокаРазделаА.НДС - СтрокаИтогов.СуммаНДС - СтрокаИтогов.СуммаНДСМинус;
			
			Если РасхождениеПоСтавке = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаИтогов.СуммаНДС = 0 ИЛИ РасхождениеПоСтавке < 0 И СтрокаИтогов.СуммаНДСМинус <> 0 Тогда
				КолонкаПогрешности = "СуммаНДСМинус";
			Иначе
				КолонкаПогрешности = "СуммаНДС";
			КонецЕсли;
			
			ТаблицаПогрешностейПоСтавке = ТаблицаПогрешностей.Скопировать(Новый Структура("СтавкаНДС", СтрокаИтогов.СтавкаНДС), "НомерСтроки, " + КолонкаПогрешности);
			МассивКоэф = ТаблицаПогрешностейПоСтавке.ВыгрузитьКолонку(КолонкаПогрешности);  
			МассивПогрешностей = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РасхождениеПоСтавке, МассивКоэф, 2);
			
			Для ж = 0 По МассивПогрешностей.ВГраница() Цикл
				
				СтрокаТоваров = Поставка[ТаблицаПогрешностейПоСтавке[ж].НомерСтроки -1];
				СтрокаТоваров.СуммаНДС = СтрокаТоваров.СуммаНДС + МассивПогрешностей[ж];
			
			КонецЦикла;
			
		КонецЦикла;
		
		Если КэшПоискаУКТВЭД <> Неопределено Тогда
			
			НовыеКодыУКТВЭД = КэшПоискаУКТВЭД.НайтиСтроки(Новый Структура("НовыйЭлемент", Истина));
			
			ПолныйКлассификаторТоваров = Неопределено;
			ПолныйКлассификаторУслуг = Неопределено;
			
			Для каждого СтрокаКэша ИЗ НовыеКодыУКТВЭД Цикл
				
				Если СтрокаКэша.Ссылка.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги Тогда
					
					Если ПолныйКлассификаторУслуг = Неопределено Тогда
						Макет = НСИССервер.ПолучитьМакет("КлассификаторДКПП", Справочники.КлассификаторУКТВЭД.ПолучитьМакет("КлассификаторДКПП"));
						КлассификаторXML = Макет.ПолучитьТекст();
						ПолныйКлассификаторУслуг = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
					КонецЕсли;
					
					СтрокаПолногоКлассификатора = ПолныйКлассификаторУслуг.Найти(СтрокаКэша.Ссылка.Код, "Code");
					Если СтрокаПолногоКлассификатора <> Неопределено Тогда 
						СправочникОбъект = СтрокаКэша.Ссылка.ПолучитьОбъект();
						СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код) + "/"+ СтрокаПолногоКлассификатора.Name;
						СправочникОбъект.НаименованиеПолное      = СтрокаПолногоКлассификатора.Name;
						СправочникОбъект.Записать();
					КонецЕсли;

				Иначе
					
					Если ПолныйКлассификаторТоваров = Неопределено Тогда
						Макет = НСИССервер.ПолучитьМакет("КлассификаторУКТВЭД", Справочники.КлассификаторУКТВЭД.ПолучитьМакет("КлассификаторУКТВЭД"));
						КлассификаторXML = Макет.ПолучитьТекст();
						ПолныйКлассификаторТоваров = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные; 
						
						ПолныйКлассификаторТоваров.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Число"));
						Для каждого СтрокаПолногоКлассификатора Из ПолныйКлассификаторТоваров Цикл
							
							Уровень = 1;    
							СтрокаНаименование = СтрокаПолногоКлассификатора.Name;
							ДлинаСтроки = СтрДлина(СтрокаНаименование);
							Для Сч = 1 По ДлинаСтроки Цикл
								
								ТекСимв = Сред(СтрокаНаименование, Сч, 1);
								
								Если ТекСимв = "-" Тогда
									Уровень = Уровень + 1;
								ИначеЕсли  ТекСимв  = " " Тогда
									Продолжить;	
								Иначе
									Прервать;
								КонецЕсли;
								
							КонецЦикла;
							СтрокаПолногоКлассификатора.Уровень = Уровень;
							
						КонецЦикла;

					КонецЕсли;
					
					СтрокаПолногоКлассификатора = ПолныйКлассификаторТоваров.Найти(СокрЛП(СтрокаКэша.Ссылка.Код), "Code");
					Если СтрокаПолногоКлассификатора <> Неопределено Тогда 
						
						ПолноеНаименование = СтрокаПолногоКлассификатора.Name;
						
						ИндексСтроки = ПолныйКлассификаторТоваров.Индекс(СтрокаПолногоКлассификатора);
						ТекУровень = СтрокаПолногоКлассификатора.Уровень;
						
						Пока Истина Цикл
							
							Если ТекУровень = 1 Тогда
								Прервать;
							КонецЕсли;
							
							ИндексСтроки = ИндексСтроки - 1;
							
							Если ИндексСтроки < 0 Тогда
								Прервать;
							КонецЕсли;
							
							ТекСтрокаИерархии = ПолныйКлассификаторТоваров.Получить(ИндексСтроки);
							
							Если ТекСтрокаИерархии.Уровень < ТекУровень Тогда
								
								ПолноеНаименование = ТекСтрокаИерархии.Name + Символы.ПС + ПолноеНаименование;
								ТекУровень = ТекСтрокаИерархии.Уровень;
								
							КонецЕсли;
							
						КонецЦикла;
						
						СправочникОбъект = СтрокаКэша.Ссылка.ПолучитьОбъект();
						СправочникОбъект.НаименованиеПолное      = ПолноеНаименование;
						СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код) + ?(СправочникОбъект.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного," X","") 
																	+ "/"+ СтрЗаменить(СправочникОбъект.НаименованиеПолное,Символы.ПС,"");
						СправочникОбъект.ВыводитьПриПечатиЧека   = СтрокаПолногоКлассификатора.Print = "1";
						СправочникОбъект.Записать(); 
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаТоваров ИЗ Поставка Цикл
		
		СтрокаТоваров.СуммаСНДС = СтрокаТоваров.Сумма + СтрокаТоваров.СуммаНДС;
		СтрокаТоваров.СтатьяДекларацииНДСНалоговыйКредит = Документы.РегистрацияВходящегоНалоговогоДокумента.ПолучитьСтатьюНалоговойДекларации(ВидОперации, СтрокаТоваров.СтавкаНДС);
		СтрокаТоваров.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;  		
		
	КонецЦикла;

	СуммаДокумента 	  = Поставка.Итог("СуммаСНДС");
	СуммаНДСДокумента = Поставка.Итог("СуммаНДС");

КонецПроцедуры

Функция ПолучитьЭлементКлассификатораУКТВЭД(ИсходныеДанные, Кэш)
	
	СтруктураПоиска = Новый Структура("Код, Вид");
	Если Кэш = Неопределено Тогда
		Кэш = Новый ТаблицаЗначений; 
		Для каждого ПолеПоиска ИЗ СтруктураПоиска Цикл
			Кэш.Колонки.Добавить(ПолеПоиска.Ключ);            
		КонецЦикла;
		Кэш.Колонки.Добавить("Ссылка");
		Кэш.Колонки.Добавить("НовыйЭлемент", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	СтруктураИсходныхДанныхПоиска = Новый Структура("G4,G32,G322,G33");
	ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанныхПоиска, ИсходныеДанные);

	Если ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G4) Тогда
		
		ИсходныйКодУКТВЭД = СокрЛП(СтруктураИсходныхДанныхПоиска.G4);
		КодУКТВЭД = "";
		Для ж = 1 По СтрДлина(ИсходныйКодУКТВЭД) Цикл
			
			КодУКТВЭД = КодУКТВЭД + Сред(ИсходныйКодУКТВЭД, ж, 1);
			Если ж = 4 ИЛИ ж = 6 ИЛИ ж = 8 Тогда
				КодУКТВЭД = КодУКТВЭД + " ";
			КонецЕсли;
			
		КонецЦикла;
		
		СтруктураПоиска.Код = КодУКТВЭД;
		
		Если ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G32) Тогда
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного;
		ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G322) Тогда
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодСХПродукции;
		Иначе
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовара;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G33) Тогда
		
		СтруктураПоиска.Код = СокрЛП(СтруктураИсходныхДанныхПоиска.G33);
		СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;  
	
	НайденныеСтроки = Кэш.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;

	НоваяЗаписьКэша = Кэш.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗаписьКэша, СтруктураПоиска);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторУКТВЭД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторУКТВЭД КАК КлассификаторУКТВЭД
	|ГДЕ
	|	КлассификаторУКТВЭД.Код = &Код
	|	И КлассификаторУКТВЭД.Вид = &Вид
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлассификаторУКТВЭД.ПометкаУдаления УБЫВ";
	Запрос.УстановитьПараметр("Код", СтруктураПоиска.Код);
	Запрос.УстановитьПараметр("Вид", СтруктураПоиска.Вид);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НоваяЗаписьКэша.Ссылка = Выборка.Ссылка;
		
	Иначе
		
		СправочникОбъект = Справочники.КлассификаторУКТВЭД.СоздатьЭлемент();
		
		СправочникОбъект.Код                     = СтруктураПоиска.Код;
		СправочникОбъект.Вид                	 = СтруктураПоиска.Вид;
		СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код);
				
		СправочникОбъект.Записать();
		НоваяЗаписьКэша.Ссылка = СправочникОбъект.Ссылка;
		НоваяЗаписьКэша.НовыйЭлемент = Истина;
		
	КонецЕсли;

	Возврат НоваяЗаписьКэша.Ссылка;
	
КонецФункции

Функция ПолучитьЭлементКлассификатораЕдиницИзмерения(ИсходныеДанные, Кэш)
	
	СтруктураПоиска = Новый Структура("Код, Наименование");
	Если Кэш = Неопределено Тогда
		Кэш = Новый ТаблицаЗначений; 
		Для каждого ПолеПоиска ИЗ СтруктураПоиска Цикл
			Кэш.Колонки.Добавить(ПолеПоиска.Ключ);            
		КонецЦикла;
		Кэш.Колонки.Добавить("Ссылка");
		Кэш.Колонки.Добавить("НовыйЭлемент", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	СтруктураИсходныхДанныхПоиска = Новый Структура("G105_2S, G4S");
	ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанныхПоиска, ИсходныеДанные);

	СтруктураПоиска.Код = СокрЛП(СтруктураИсходныхДанныхПоиска.G105_2S);
	СтруктураПоиска.Наименование = СокрЛП(СтруктураИсходныхДанныхПоиска.G4S);
	
	Если НЕ ЗначениеЗаполнено(СтруктураПоиска.Код) И НЕ ЗначениеЗаполнено(СтруктураПоиска.Наименование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НайденныеСтроки = Кэш.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;

	НоваяЗаписьКэша = Кэш.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗаписьКэша, СтруктураПоиска);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторЕдиницИзмерения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК КлассификаторЕдиницИзмерения
	|ГДЕ
	|	КлассификаторЕдиницИзмерения.Код = &Код
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторЕдиницИзмерения.Ссылка
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК КлассификаторЕдиницИзмерения
	|ГДЕ
	|	&Код = """"
	|	И КлассификаторЕдиницИзмерения.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Код", СтруктураПоиска.Код);
	Запрос.УстановитьПараметр("Наименование", СтруктураПоиска.Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НоваяЗаписьКэша.Ссылка = Выборка.Ссылка;
		
	Иначе
		
		СправочникОбъект = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
		СправочникОбъект.Владелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения;
		
		СправочникОбъект.Код                     = СтруктураПоиска.Код;
		СправочникОбъект.Наименование       	 = СтруктураПоиска.Наименование;
		СправочникОбъект.НаименованиеПолное      = СправочникОбъект.Наименование;
				
		СправочникОбъект.Записать();
		НоваяЗаписьКэша.Ссылка = СправочникОбъект.Ссылка;
		НоваяЗаписьКэша.НовыйЭлемент = Истина;
		
	КонецЕсли;

	Возврат НоваяЗаписьКэша.Ссылка;
	
КонецФункции

Процедура ЗаполнитьОрганизациюПоИНННаСервере(ОрганизацияИНН)
 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
               |	Рег.Организация КАК Ссылка
               |ИЗ                                  
               |	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Дата) КАК Рег
               |ГДЕ
               |	Рег.ИННПлательщикаНДС = &ОрганизацияИНН
               |";
	
	Запрос.УстановитьПараметр("ОрганизацияИНН", 			ОрганизацияИНН);
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Ссылка;
		Договор = Неопределено;
	КонецЕсли;	

КонецПроцедуры

Процедура ЗаполнитьКонтрагентаПоИНННаСервере(КонтрагентИНН)		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
               |	Спр.Ссылка
               |ИЗ
               |	Справочник.Контрагенты КАК Спр
               |ГДЕ
               |	Спр.ИННПлательщикаНДС = &КонтрагентИНН";
	
	Запрос.УстановитьПараметр("КонтрагентИНН", 			КонтрагентИНН);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Контрагент = Выборка.Ссылка;
		Партнер = Выборка.Ссылка.Партнер;
		Договор = Неопределено;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаполнитьДоговорНаСервере(ДоговорКонтрагентаДата, ДоговорКонтрагентаНомер)	
	
	Если  НЕ Договор.Дата  = ДоговорКонтрагентаДата
	  ИЛИ НЕ Договор.Номер = ДоговорКонтрагентаНомер Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
	               |	Спр.Ссылка
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК Спр
	               |ГДЕ
				   |	Спр.Контрагент = &Контрагент
				   |	И Спр.Дата 	 = &ДоговорКонтрагентаДата
				   |	И Спр.Номер  = &ДоговорКонтрагентаНомер
	               |";
				   
		Запрос.УстановитьПараметр("Контрагент", 			 Контрагент);				   
		Запрос.УстановитьПараметр("ДоговорКонтрагентаДата",  ДоговорКонтрагентаДата);
		Запрос.УстановитьПараметр("ДоговорКонтрагентаНомер", ДоговорКонтрагентаНомер);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Договор = Выборка.Ссылка;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти

#КонецЕсли
