#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДСРеестрПолученныхНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДСУсловныеПродажи(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		//++ НЕ УТ
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
		//-- НЕ УТ
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
    Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР(); 
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация) Экспорт
	
	МассивПаметровВыбора = Новый Массив;
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ДоступностьПоОперации = ХозяйственнаяОперация = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ОтборСтатейРасходов.ВариантРаспределенияРасходов = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
    
    Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка 						КАК Ссылка,
	|	ДанныеДокумента.Дата 						КАК Период,
	|	ДанныеДокумента.Организация 				КАК Организация,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.Контрагент ССЫЛКА Справочник.Организации
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	    ИНАЧЕ ДанныеДокумента.Партнер
	|	КОНЕЦ 										КАК Партнер,
	|	ДанныеДокумента.Контрагент 					КАК Контрагент,
	|	ДанныеДокумента.ВидОперации 				КАК ВидОперации,
	| 
	|	ВЫБОР 
	// ВводОстатков
	|       КОГДА ДанныеДокумента.ПоВводуОстатков
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.ВводОстатков)
	// ПоступлениеТоваровУслуг
	|       КОГДА ДанныеДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	// ВозвратТоваровПоставщику, ВозвратТоваровМеждуОрганизациями
	|       КОГДА ДанныеДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	// ОтчетКомитенту
	|       КОГДА ДанныеДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка)
	|			ТОГДА ВЫБОР 
	|				КОГДА ДанныеДокумента.Переоценка
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|        		КОНЕЦ
	// Все выше упомянутое, но для контрагента для неплательщика НДС
	|       КОГДА ДанныеДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек)
	|			ТОГДА ВЫБОР 
	|				КОГДА ДанныеДокумента.Переоценка
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|       	 	КОНЕЦ
	|	КОНЕЦ 										КАК ВидПоставки,
	| 
	|	ДанныеДокумента.Договор 					КАК Договор,
	|	ДанныеДокумента.НаправлениеДеятельности 	КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА КОНЕЦПЕРИОДА(ДанныеДокумента.ДатаОтгрузкиОплаты, ДЕНЬ)
	|		ИНАЧЕ 
	|			ДанныеДокумента.ДатаВходящегоДокумента
	|	КОНЕЦ 										КАК ДатаВозникновенияНК,
	|
	|	ДанныеДокумента.СтатьяРасходов 				КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов 			КАК АналитикаРасходов,
	|
	|	ДанныеДокумента.Переоценка 					КАК Переоценка,
	|	ДанныеДокумента.ПоВводуОстатков 			КАК ПоВводуОстатков,
	|
	|	ДанныеДокумента.Номер                  		КАК Номер,
	|	ДанныеДокумента.НомерВходящегоДокумента		КАК НомерВходящегоДокумента,
	|	ДанныеДокумента.ДатаВходящегоДокумента     	КАК ДатаВходящегоДокумента,
	|	ДанныеДокумента.Ответственный               КАК Ответственный,
	|	ДанныеДокумента.Комментарий                 КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления             КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен                    КАК Проведен
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
    |";
    
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
    
	Запрос.УстановитьПараметр("Период",                     	Реквизиты.Период);
	Запрос.УстановитьПараметр("ВидОперации",                	Реквизиты.ВидОперации);
	Запрос.УстановитьПараметр("Переоценка",                 	Реквизиты.Переоценка);
	Запрос.УстановитьПараметр("ВидПоставки",                	Реквизиты.ВидПоставки);
	Запрос.УстановитьПараметр("ПоВводуОстатков",            	Реквизиты.ПоВводуОстатков);
	Запрос.УстановитьПараметр("ДатаВозникновенияНК",        	Реквизиты.ДатаВозникновенияНК);
	Запрос.УстановитьПараметр("Организация",                	Реквизиты.Организация);
	Запрос.УстановитьПараметр("Партнер",    					Реквизиты.Партнер);
	Запрос.УстановитьПараметр("Контрагент",    					Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("НаправлениеДеятельности",    	Реквизиты.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("Договор",    					Реквизиты.Договор);
	Запрос.УстановитьПараметр("СтатьяРасходов",    				Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("АналитикаРасходов",    			Реквизиты.АналитикаРасходов);
	
	Запрос.УстановитьПараметр("Номер",                          Реквизиты.Номер);
	Запрос.УстановитьПараметр("НомерВходящегоДокумента",        Реквизиты.НомерВходящегоДокумента);
	Запрос.УстановитьПараметр("ДатаВходящегоДокумента",         Реквизиты.ДатаВходящегоДокумента);
	Запрос.УстановитьПараметр("Ответственный",                  Реквизиты.Ответственный);
	Запрос.УстановитьПараметр("Комментарий",                    Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("ПометкаУдаления",                Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                       Реквизиты.Проведен);
	
	ПараметрыПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для Каждого ТекПараметр Из ПараметрыПроведения Цикл
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных",	ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РегистрацияВходящегоНалоговогоДокумента"));
	ЗначенияПараметровПроведения.Вставить("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать",          ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	&ВидПоставки КАК ВидПоставки,
	|
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|   ВЫБОР
	|       КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|			ТОГДА -1	
	|       ИНАЧЕ	
	|       	1
	|   КОНЕЦ * ПоставкаДокумента.СуммаСНДС КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА (НЕ ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)) ТОГДА
	|   	ВЫБОР
	|       	КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|				ТОГДА -1	
	|       	ИНАЧЕ	
	|       		1
	|   	КОНЕЦ * (ПоставкаДокумента.СуммаНДС - ПоставкаДокумента.СуммаНДСПропорционально)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСКредит,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ПоставкаДокумента.ДокументПоставки КАК ДокументПоставки
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|   И &ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек)
	|   ) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	&ВидПоставки КАК ВидПоставки,
	|
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|   ПоставкаДокумента.СуммаСНДС КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА (НЕ ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)) ТОГДА
	|   	(ПоставкаДокумента.СуммаНДС - ПоставкаДокумента.СуммаНДСПропорционально)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСКредит,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ПоставкаДокумента.ДокументПоставки КАК ДокументПоставки
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|   И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки)
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	&ВидПоставки КАК ВидПоставки,
	|
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|   ВЫБОР
	|       КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|			ТОГДА -1	
	|       ИНАЧЕ	
	|       	1
	|   КОНЕЦ * ПоставкаДокумента.СуммаСНДС КАК СуммаВзаиморасчетов,
	|   ВЫБОР
	|       КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|			ТОГДА -1	
	|       ИНАЧЕ	
	|       	1
	|   КОНЕЦ * (ПоставкаДокумента.СуммаНДС - ПоставкаДокумента.СуммаНДСПропорционально) КАК СуммаНДСКредит,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ПоставкаДокумента.ДокументПоставки КАК ДокументПоставки
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|	И &ПоВводуОстатков
	|   И &ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек)
	|   )
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|   ВЫБОР КОГДА &ПоВводуОстатков
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.ВводОстатков)
	|	КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|	ИНАЧЕ
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК ВидПоставки,
	|
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ВЫБОР КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА - ПоставкаДокумента.СуммаСНДС ИНАЧЕ ПоставкаДокумента.СуммаСНДС КОНЕЦ КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА (НЕ ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)) ТОГДА
	|   	(ПоставкаДокумента.СуммаНДС - ПоставкаДокумента.СуммаНДСПропорционально) *
	|		ВЫБОР КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСКредит,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ПоставкаДокумента.ДокументПоставки КАК ДокументПоставки
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|   И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|   ВЫБОР КОГДА &ПоВводуОстатков
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.ВводОстатков)
	|	КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|	ИНАЧЕ
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК ВидПоставки,
	|
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ВЫБОР КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА - ПоставкаДокумента.СуммаСНДС ИНАЧЕ ПоставкаДокумента.СуммаСНДС КОНЕЦ КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА (НЕ ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)) ТОГДА
	|   	(ПоставкаДокумента.СуммаНДС - ПоставкаДокумента.СуммаНДСПропорционально) *
	|		ВЫБОР КОГДА ПоставкаДокумента.СуммаСНДС < 0 ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСКредит,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ПоставкаДокумента.ДокументПоставки КАК ДокументПоставки
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|   И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита)
	|	И ПоставкаДокумента.ДокументПоставки = НЕОПРЕДЕЛЕНО
	|";
	
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаНДСРеестрПолученныхНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "НДСРеестрПолученныхНалоговыхДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ПоставкаДокумента.СтатьяДекларацииНДСНалоговыйКредит КАК СтатьяДекларацииНДСНалоговыйКредит,
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.Сумма КАК СуммаБезНДС,
	|	ПоставкаДокумента.СуммаНДС КАК СуммаНДС,
// Информация для заполнения отчета "Анализ налогового кредита"	
	|	&ВидПоставки КАК ВидПоставки,
	|	ПоставкаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	&ДатаВозникновенияНК КАК ДатаВходящегоНалоговогоДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НалоговыйКредитПоставка) КАК ХозяйственнаяОперация
// ...
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО
	|			ISNULL(ПоставкаДокумента.ОбъектРасчетов.Организация, &Организация) = Аналитика.Организация
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Контрагент, &Контрагент) = Аналитика.Контрагент
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Партнер, &Партнер) = Аналитика.Партнер
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.Договор, &Договор) = Аналитика.Договор
	|			И ISNULL(ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности, &НаправлениеДеятельности) = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|	И НЕ (&ВидОперации = Значение(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита))
	|
	|";
	
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаНДСУсловныеПродажи(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "НДСУсловныеПродажи";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
    КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа)  КАК ВидУсловнойПродажи,
	|
	|	ПоставкаДокумента.Ссылка КАК Номенклатура,
	|	ПоставкаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ПоставкаДокумента.КодУКТВЭД КАК НомерГТД,
	|	СУММА(ВЫБОР КОГДА ПоставкаДокумента.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность) ТОГДА ПоставкаДокумента.Количество ИНАЧЕ 0 КОНЕЦ) КАК Количество,
	|	ВЫБОР КОГДА ПоставкаДокумента.Количество = 0 ТОГДА 0 КОГДА ПоставкаДокумента.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность) ТОГДА 0 ИНАЧЕ ПоставкаДокумента.НомерСтроки КОНЕЦ КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая) КАК НалоговоеНазначение,
	|	ПоставкаДокумента.НалоговоеНазначение КАК НалоговоеНазначениеПоФакту,
	|	
	|	СУММА(ВЫБОР
	|			КОГДА ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая)
	|				ТОГДА ПоставкаДокумента.СуммаНДСПропорционально * 100 / ВЫБОР
	|						КОГДА ПоставкаДокумента.СтавкаНДС.Ставка <> 0
	|							ТОГДА ПоставкаДокумента.СтавкаНДС.Ставка
	|						ИНАЧЕ 100
	|					КОНЕЦ
	|			ИНАЧЕ ПоставкаДокумента.Сумма
	|		КОНЕЦ) КАК СтоимостьРегл,
	|  	СУММА(ВЫБОР
	|      	КОГДА ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая) ТОГДА
	|      		ПоставкаДокумента.СуммаНДСПропорционально
	|      	ИНАЧЕ	
	|			ПоставкаДокумента.СуммаНДС
	|   	КОНЕЦ )	КАК НДСРегл
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|	И НЕ (ПоставкаДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Облагаемая))
	|   И &ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек)
	|   )
	|	И ПоставкаДокумента.СтавкаНДС.Ставка > 0	
	|СГРУППИРОВАТЬ ПО
	|	ПоставкаДокумента.Ссылка,
	|	ПоставкаДокумента.КодУКТВЭД,
	|	ВЫБОР КОГДА ПоставкаДокумента.Количество = 0 ТОГДА 0 КОГДА ПоставкаДокумента.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность) ТОГДА 0 ИНАЧЕ ПоставкаДокумента.НомерСтроки КОНЕЦ,
	|	ПоставкаДокумента.СтавкаНДС,
	|	ПоставкаДокумента.НалоговоеНазначение
	|";
	
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос)

	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПартнеровИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПоАналитикам = Новый Запрос;
	ЗапросПоАналитикам.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоставкаДокумента.ОбъектРасчетов.Организация КАК Организация,
	|	ПоставкаДокумента.ОбъектРасчетов.Контрагент КАК Контрагент,
	|	ПоставкаДокумента.ОбъектРасчетов.Партнер КАК Партнер,
	|	ПоставкаДокумента.ОбъектРасчетов.Договор КАК Договор,
	|	ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ПоставкаДокумента.ОбъектРасчетов.Организация = Аналитика.Организация
	|			И ПоставкаДокумента.ОбъектРасчетов.Контрагент = Аналитика.Контрагент
	|			И ПоставкаДокумента.ОбъектРасчетов.Партнер = Аналитика.Партнер
	|			И ПоставкаДокумента.ОбъектРасчетов.Договор = Аналитика.Договор
	|			И ПоставкаДокумента.ОбъектРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И ПоставкаДокумента.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация,
	|	&Контрагент,
	|	&Партнер,
	|	&Договор,
	|	&НаправлениеДеятельности
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ПоставкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО (&Организация = Аналитика.Организация)
	|			И (&Контрагент = Аналитика.Контрагент)
	|			И (&Партнер = Аналитика.Партнер)
	|			И (&Договор = Аналитика.Договор)
	|			И (&НаправлениеДеятельности = Аналитика.НаправлениеДеятельности)
	|ГДЕ
	|	ПоставкаДокумента.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И ПоставкаДокумента.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	
	ЗапросПоАналитикам.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросПоАналитикам.УстановитьПараметр("Организация", Запрос.Параметры.Организация);
	ЗапросПоАналитикам.УстановитьПараметр("Контрагент", Запрос.Параметры.Контрагент);
	ЗапросПоАналитикам.УстановитьПараметр("Партнер", Запрос.Параметры.Партнер);
	ЗапросПоАналитикам.УстановитьПараметр("Договор", Запрос.Параметры.Договор);
	ЗапросПоАналитикам.УстановитьПараметр("НаправлениеДеятельности", Запрос.Параметры.НаправлениеДеятельности);
	
	Выборка = ЗапросПоАналитикам.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПартнеровИнициализированы", Истина);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУПР") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.ВалютаРегламентированногоУчета,
																			 ,
																			 Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУПР",  Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
    
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	Неопределено КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ((ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
    |
	|	(ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) КАК СуммаСНДСРегл,
    |	(ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНДСНаРасходы) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|	И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита)
	|	И НЕ ДанныеДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|	И НЕ (ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально = 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;                    
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период 													КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНДСНаРасходы) КАК ХозяйственнаяОперация,
	|	&Организация 												КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО 												КАК Подразделение,
	|	&НаправлениеДеятельности 									КАК НаправлениеДеятельности,
	|	&СтатьяРасходов 											КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК АналитикаДоходов,
	|	&АналитикаРасходов            								КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                   				КАК АналитикаАктивовПассивов,
	|
	|	НЕОПРЕДЕЛЕНО                        						КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО              									КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                								КАК КорАналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НДС)                    	КАК КорАналитикаАктивовПассивов,
    |                          
	|	0 															КАК Сумма,
	|	ВЫРАЗИТЬ(
	|		(ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) 
	|		* &КоэффициентПересчетаВВалютуУпр 
	|			КАК ЧИСЛО(31,2)
	|	) 															КАК СуммаУпр,
	|	(ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) КАК СуммаРегл,
	|	&ВалютаРегламентированногоУчета 							КАК Валюта,
	|	(ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально) КАК СуммаВВалюте
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|	И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита)
	|	И НЕ ДанныеДокумента.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|	И НЕ (ДанныеДокумента.СуммаНДС - ДанныеДокумента.СуммаНДСПропорционально = 0)
	|
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                  		КАК Ссылка,
	|	&Период                    		КАК ДатаДокументаИБ,
	|	&Номер                   		КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных 		КАК ТипСсылки,
	|	&Организация             		КАК Организация,
	|	&Контрагент              		КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО           			КАК Подразделение,
	|	&Ответственный           		КАК Ответственный,
	|	&Комментарий                    КАК Комментарий,
	|	&ВалютаРегламентированногоУчета КАК Валюта,
	|	ДанныеДокумента.СуммаДокумента	КАК Сумма,
	|	НЕОПРЕДЕЛЕНО             		КАК Статус,
	|	&Проведен                		КАК Проведен,
	|	&ПометкаУдаления         		КАК ПометкаУдаления,
	|	&ВидОперации 					КАК Дополнительно,
	|	&ДатаВходящегоДокумента		    КАК ДатаПервичногоДокумента,
	|	&НомерВходящегоДокумента     	КАК НомерПервичногоДокумента,
	|	НЕОПРЕДЕЛЕНО                	КАК МестоХранения,
	|	&НаправлениеДеятельности 		КАК НаправлениеДеятельности,
	|	&Партнер                 		КАК Партнер,
	|	&Договор                 		КАК Договор,
	|	&Период         				КАК ДатаОтраженияВУчете,
	|	ЛОЖЬ 							КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО 					КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РегистрацияВходящегоНалоговогоДокумента";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать",           """""");
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров
		);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров
		);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

//++ НЕ УТ
#Область ПроведениеПоРеглУчетуУКР

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
	
	ТекстНалоговыйКредит = "
	|ВЫБРАТЬ // Налоговый кредит (Дт 6412 :: Кт 6442)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.СуммаНДС - Строки.СуммаНДСПропорционально) КАК Сумма,
	|	СУММА((Строки.СуммаНДС - Строки.СуммаНДСПропорционально) / КурсыУпрНК.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДС) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалоговыйКредитНеподтвержденный) КАК СчетКт,
	|	Операция.Контрагент КАК СубконтоКт1,
	|	Строки.ОбъектРасчетов.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
 	|	""%1"" +
	|	ВЫБОР
	|      	КОГДА Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная) ТОГДА
 	|			""%2""
	|      	КОГДА Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек) ТОГДА
 	|			""%3""
	|      	КОГДА Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка) ТОГДА
 	|			""%4""
	|      	КОГДА Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода) ТОГДА
  	|			""%5""
	|      	ИНАЧЕ	
	|			""""
	|   	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТКурсыУпрВалютыНК КАК КурсыУпрНК
	|	ПО
	|		ВЫБОР
	|			КОГДА Операция.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(Операция.ДатаОтгрузкиОплаты, ДЕНЬ)
	|			КОГДА Операция.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				Операция.ДатаВходящегоДокумента
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ)
	|		КОНЕЦ = КурсыУпрНК.Дата 
	|	
	|ГДЕ
	|   Операция.ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка), 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек),
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода)	
	|   ) 	
	|	И НЕ Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.НаправлениеДеятельности,
	|	Операция.Контрагент,
	|	Строки.ОбъектРасчетов.Договор
	|";
 
 	ТекстНалоговыйКредит = СтрШаблон(ТекстНалоговыйКредит, 
  		НСтр("ru='НДС: налоговый кредит: ';uk='ПДВ: податковий кредит: '",ЯзыкСодержания),
 		НСтр("ru='налоговая накладная';uk='податкова накладна'",ЯзыкСодержания),
 		НСтр("ru='подтверждение (товарный чек)';uk='підтвердження (товарний чек)'",ЯзыкСодержания),
		НСтр("ru='расчет корректировки (изменение суммы компенсации)';uk='розрахунок коригування (зміна суми компенсації)'",ЯзыкСодержания),
 		НСтр("ru='работы от нерезидента';uk='роботи від нерезидента'",ЯзыкСодержания));
    
	ТекстНалоговыйКредитСторно = "
	|ВЫБРАТЬ // Налоговый кредит сторно (Дт 6412 :: Кт 6442)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
    |	СУММА(Строки.СуммаНДС) КАК Сумма,
	|	СУММА(Строки.СуммаНДС / КурсыУпрНК.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДС) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалоговыйКредитНеподтвержденный) КАК СчетКт,
	|	Операция.Контрагент КАК СубконтоКт1,
	|	Строки.ОбъектРасчетов.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" +
	|	ВЫБОР
	|      	КОГДА Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат) ТОГДА
	|			""%2""
	|      	ИНАЧЕ	
	|			""""
	|   	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТКурсыУпрВалютыНК КАК КурсыУпрНК
	|	ПО
	|		ВЫБОР
	|			КОГДА Операция.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(Операция.ДатаОтгрузкиОплаты, ДЕНЬ)
	|			КОГДА Операция.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				Операция.ДатаВходящегоДокумента
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ)
	|		КОНЕЦ = КурсыУпрНК.Дата 
	|	
	|ГДЕ
	|   Операция.ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат)
	|   ) 	
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.НаправлениеДеятельности,
	|	Операция.Контрагент,
	|	Строки.ОбъектРасчетов.Договор
	|";

	ТекстНалоговыйКредитСторно = СтрШаблон(ТекстНалоговыйКредитСторно, 
		НСтр("ru='НДС: налоговый кредит сторно: ';uk='ПДВ: податковий кредит сторно: '",ЯзыкСодержания),
		НСтр("ru='расчет корректировки (возврат)';uk='розрахунок коригування (повернення)'",ЯзыкСодержания));
	
	ТекстКорректировкиНалоговогоКредита = "
	|ВЫБРАТЬ // Налоговый кредит (Дт 6412 :: Кт 6443)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.СуммаНДС - Строки.СуммаНДСПропорционально) КАК Сумма,
	|	СУММА((Строки.СуммаНДС - Строки.СуммаНДСПропорционально) / КурсыУпрНК.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДС) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговогоКредита) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТКурсыУпрВалютыНК КАК КурсыУпрНК
	|	ПО
	|		ВЫБОР
	|			КОГДА Операция.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(Операция.ДатаОтгрузкиОплаты, ДЕНЬ)
	|			КОГДА Операция.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				Операция.ДатаВходящегоДокумента
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ)
	|		КОНЕЦ = КурсыУпрНК.Дата 
	|	
	|ГДЕ
	|   Операция.ВидОперации В ( 
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки),
	|   	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита)
	|   ) 
	|	И НЕ (Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Строки.НалоговоеНазначение
	|";

	ТекстКорректировкиНалоговогоКредита = СтрШаблон(ТекстКорректировкиНалоговогоКредита, 
		НСтр("ru='НДС: налоговый кредит: исправление ошибки';uk='ПДВ: податковий кредит: виправлення помилки'",ЯзыкСодержания));
	
	
	ТекстУсловнаяПродажа = "
	|ВЫБРАТЬ // Условная продажа (Дт 6412 :: Кт 6435)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(ВЫБОР
	|      	КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая) ТОГДА
	|      		Строки.СуммаНДСПропорционально
	|      	ИНАЧЕ	
	|			Строки.СуммаНДС
	|   	КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|      	КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая) ТОГДА
	|      		Строки.СуммаНДСПропорционально
	|      	ИНАЧЕ	
	|			Строки.СуммаНДС
	|   	КОНЕЦ / КурсыУпрНК.Курс) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт, 
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт, 
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДС) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2, 
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТКурсыУпрВалютыНК КАК КурсыУпрНК
	|	ПО
	|		ВЫБОР
	|			КОГДА Операция.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(Операция.ДатаОтгрузкиОплаты, ДЕНЬ)
	|			КОГДА Операция.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				Операция.ДатаВходящегоДокумента
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ)
	|		КОНЕЦ = КурсыУпрНК.Дата 
	|	
	|ГДЕ
	|	(Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|		ИЛИ Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.ПропорциональноОблагаемая))
	|   И НЕ Операция.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат),
	|   							 ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация 
	|";

	ТекстУсловнаяПродажа = СтрШаблон(ТекстУсловнаяПродажа, 
		НСтр("ru='Налоговый кредит по операциям не обл. НДС';uk='Податковий кредит за операціями не опод. ПДВ'",ЯзыкСодержания));
	
	ТекстСписаниеНеполученногоНалоговогоКредита = "
	|ВЫБРАТЬ // Налоговый кредит (Дт 9X :: Кт 6442)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.СуммаНДС - Строки.СуммаНДСПропорционально) КАК Сумма,
	|	СУММА((Строки.СуммаНДС - Строки.СуммаНДСПропорционально) / КурсыУпрНК.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.АналитикаРасходов КАК СубконтоДт1, 
	|	Операция.СтатьяРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалоговыйКредитНеподтвержденный) КАК СчетКт,
	|	Операция.Контрагент КАК СубконтоКт1,
	|	Строки.ОбъектРасчетов.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТКурсыУпрВалютыНК КАК КурсыУпрНК
	|	ПО
	|		ВЫБОР
	|			КОГДА Операция.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(Операция.ДатаОтгрузкиОплаты, ДЕНЬ)
	|			КОГДА Операция.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				Операция.ДатаВходящегоДокумента
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ)
	|		КОНЕЦ = КурсыУпрНК.Дата 
	|	
	|ГДЕ
	|   Операция.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.СписаниеНалоговогоКредита) 
	|	И Строки.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|	И НЕ Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.НаправлениеДеятельности,
	|	Операция.Контрагент,
	|	Строки.ОбъектРасчетов.Договор
	|";

	ТекстСписаниеНеполученногоНалоговогоКредита = СтрШаблон(ТекстСписаниеНеполученногоНалоговогоКредита, 
		НСтр("ru='НДС: списание неполученного налогового кредита';uk='ПДВ: списання неотриманого податкового кредиту'",ЯзыкСодержания));
	
	Возврат
		ТекстНалоговыйКредит
        + " ОБЪЕДИНИТЬ ВСЕ " + ТекстНалоговыйКредитСторно
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстКорректировкиНалоговогоКредита
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстУсловнаяПродажа
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеНеполученногоНалоговогоКредита
		;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ДатаОтгрузкиОплаты <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтгрузкиОплаты, ДЕНЬ)
	|		КОГДА ДанныеДокумента.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|			ДанныеДокумента.ДатаВходящегоДокумента
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ДЕНЬ)
	|	КОНЕЦ КАК ДатаВозникновенияНК
	|ПОМЕСТИТЬ ВТДатыНК
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегистрацияВходящегоНалоговогоДокумента КАК ДанныеДокумента
	|		ПО ДокументыКОтражению.Ссылка = ДанныеДокумента.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыНК.ДатаВозникновенияНК КАК ДатаВозникновенияНК,
	|	МАКСИМУМ(КурсыВалют.Период) КАК ПериодКурса
	|ПОМЕСТИТЬ ВТПериодыКурсовВалютНК
	|ИЗ
	|	ВТДатыНК КАК ДатыНК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ДатыНК.ДатаВозникновенияНК >= КурсыВалют.Период
	|			И (КурсыВалют.Валюта = &ВалютаУпрУчета)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыНК.ДатаВозникновенияНК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалют.ДатаВозникновенияНК КАК Дата,
	|	ВЫБОР
	|		КОГДА КурсыВалют.Валюта ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА КурсыВалют.Кратность = 0
	|			ТОГДА КурсыВалют.Курс
	|		ИНАЧЕ КурсыВалют.Курс / КурсыВалют.Кратность
	|	КОНЕЦ КАК Курс
	|ПОМЕСТИТЬ ВТКурсыУпрВалютыНК
	|ИЗ
	|	ВТПериодыКурсовВалютНК КАК ПериодыКурсовВалют
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО (КурсыВалют.Валюта = &ВалютаУпрУчета)
	|			И ПериодыКурсовВалют.ПериодКурса = КурсыВалют.Период
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти
 
#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//     КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для   изменения.
//         См. описание 1 параметра процедуры   ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//     Параметры - Структура - Вспомогательные параметры. Для чтения.
//         См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

Функция ПолучитьСтатьюНалоговойДекларации(ВидОперации, СтавкаНДС) Экспорт
	
	СтатьиДеклараций = Справочники.СтатьиНалоговыхДеклараций;
	
	СтавкаНДСПеречисление = НДСКлиентСерверПовтИсп.ПеречислениеСтавкаНДСПоСтавкеНДС(СтавкаНДС);

	Если Не ЗначениеЗаполнено(ВидОперации) ИЛИ Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат СтатьиДеклараций.ПустаяСсылка();
		
	// Считаем, что есть право на налоговый кредит	
	ИначеЕсли СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС0 
		  ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7
          ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС14
	      ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС20 Тогда
	 
	 	// приобретение с уплатой НДС
		Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная
		 ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки
		 ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек Тогда
			
			Возврат СтатьиДеклараций.НДС_НКПокупкаСНДСвВРОблаг;
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита Тогда
			
			Возврат  СтатьиДеклараций.НДС_НККорректировкаВосстановлениеКредита;
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
			  ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка Тогда
							
			Возврат СтатьиДеклараций.НДС_НКИзменениеСтоимости	
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
							
			Возврат СтатьиДеклараций.НДС_НКИмпортВРОблагУслугиНерезидента;
			
		КонецЕсли;
		
	Иначе
		
		// приобретение без НДС
		Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная
			ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки
			ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита
			ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек Тогда
			
			// 11.2 Декларации,
			Возврат  СтатьиДеклараций.НДС_НКПокупкаБезНДСвВРНеОблаг;
				
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
			  ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка Тогда
				  
			Возврат СтатьиДеклараций.НДС_НККорректировкаБезНК;
			
		Иначе
			
			// Импорт			
			Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
				
				// странно, услуги от нерезидента успользуемые на территории Украины облагаются по ставке 20%
				Возврат СтатьиДеклараций.ПустаяСсылка();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтатьиДеклараций.ПустаяСсылка();
	
КонецФункции

Функция ЗаполнитьПоОстаткам(Объект) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
	// 0 - Временная таблица с данными по первому событию закрытому в рамках документов поставки (не обязательно открытому)
	"ВЫБРАТЬ
	|	СуммыПервогоСобытияКРегистрации.ОбъектРасчетов,
	|	СуммыПервогоСобытияКРегистрации.ДокументПоставки,
	|	СУММА(СуммыПервогоСобытияКРегистрации.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ СуммыПервогоСобытияКРегистрации
	|ИЗ
	|		(ВЫБРАТЬ
	// Берем сумму первого события на дату
	|			НДСРасчетНалоговогоКредита.ВидПоставки,
	|			НДСРасчетНалоговогоКредита.ОбъектРасчетов,
	|			ВЫБОР
	|           	КОГДА НДСРасчетНалоговогоКредита.ДокументПоставки.Ссылка ЕСТЬ NULL
	|               	ТОГДА НЕОПРЕДЕЛЕНО
	|               ИНАЧЕ
	|	              	НДСРасчетНалоговогоКредита.ДокументПоставки
	|			КОНЕЦ КАК ДокументПоставки,
	// Если по первому событию еще не определен документ поставки, то пустое значение обрабатывается вместе со всеми
	|			НДСРасчетНалоговогоКредита.СуммаПоставкиТребующаяРегистрацииННОборот КАК СуммаСНДС
	|		ИЗ
	|			РегистрНакопления.НДСРасчетНалоговогоКредита.Обороты(
	|					НАЧАЛОПЕРИОДА(&ДатаОстатков, ДЕНЬ),
	|					КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
	|					,
	|					АналитикаУчетаПоПартнерам.Партнер = &Партнер
	|						И АналитикаУчетаПоПартнерам.Организация = &Организация
	|						И АналитикаУчетаПоПартнерам.Договор = &Договор
	|						И АналитикаУчетаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|						И АналитикаУчетаПоПартнерам.Контрагент = &Контрагент) КАК НДСРасчетНалоговогоКредита
	|		ГДЕ
	|			НДСРасчетНалоговогоКредита.СуммаПоставкиТребующаяРегистрацииННОборот > 0
	|		    ИЛИ &ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка) 
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	// Отнимаем из нее сумму которая уже закрыта документами РегистрацияВходящегоНалоговогоДокумента или КорректировкаРегистров
	|		ВЫБРАТЬ
	|			НДССоставПоставки.ВидПоставки,
	|			НДССоставПоставки.ОбъектРасчетов,
	|			НДССоставПоставки.ДокументПоставки,
	|			-НДССоставПоставки.СуммаВзаиморасчетов
	|		ИЗ
	|			РегистрНакопления.НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов КАК НДССоставПоставки
	|		ГДЕ
	|			(НДССоставПоставки.Активность)
	|			И НДССоставПоставки.Регистратор <> &Ссылка
	|			И НДССоставПоставки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Партнер = &Партнер
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Организация = &Организация
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Договор = &Договор
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
	|			И НДССоставПоставки.ДатаВходящегоНалоговогоДокумента = &ДатаОстатков
	|
	|		    И НЕ НДССоставПоставки.ДокументПоставки.Ссылка ЕСТЬ NULL
	|		
	|
	|		) КАК СуммыПервогоСобытияКРегистрации
	|
	|ГДЕ 
	|	СуммыПервогоСобытияКРегистрации.ВидПоставки = &ВидПоставки
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыПервогоСобытияКРегистрации.ОбъектРасчетов,
	|	СуммыПервогоСобытияКРегистрации.ДокументПоставки
	|
	|ИМЕЮЩИЕ
	|	СУММА(СуммыПервогоСобытияКРегистрации.СуммаСНДС) > 0 
	|	ИЛИ (&ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка) И
	|	     СУММА(СуммыПервогоСобытияКРегистрации.СуммаСНДС) <> 0)
	|
	|;
	|
	// 1 - Выборка с данными по первому событию
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыПервогоСобытияКРегистрации.ОбъектРасчетов,
	|	СуммыПервогоСобытияКРегистрации.ДокументПоставки,
	|	СуммыПервогоСобытияКРегистрации.СуммаСНДС
	|ИЗ
	|	СуммыПервогоСобытияКРегистрации КАК СуммыПервогоСобытияКРегистрации
	|;
	|
	// 2 - Сумма закрытого первого события без указания документов поставки (распределяется пропорционально в рамках объекта расчетов)
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыЗакрытияПервогоСобытияБезДокументаПоставки.ОбъектРасчетов,
	|	СУММА(СуммыЗакрытияПервогоСобытияБезДокументаПоставки.СуммаСНДС) КАК СуммаСНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|			НДССоставПоставки.ВидПоставки,
	|			НДССоставПоставки.ОбъектРасчетов,
	|			НДССоставПоставки.СуммаВзаиморасчетов КАК СуммаСНДС
	|		ИЗ
	|			РегистрНакопления.НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов КАК НДССоставПоставки
	|		ГДЕ
	|			(НДССоставПоставки.Активность)
	|			И НДССоставПоставки.Регистратор <> &Ссылка
	|			И НДССоставПоставки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Партнер = &Партнер
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Организация = &Организация
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Договор = &Договор
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|			И НДССоставПоставки.АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
	|			И НДССоставПоставки.ДатаВходящегоНалоговогоДокумента = &ДатаОстатков
	|
	|		    И НДССоставПоставки.ДокументПоставки.Ссылка ЕСТЬ NULL
	|
	|		) КАК СуммыЗакрытияПервогоСобытияБезДокументаПоставки
	|
	|ГДЕ 
	|	СуммыЗакрытияПервогоСобытияБезДокументаПоставки.ВидПоставки = &ВидПоставки
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыЗакрытияПервогоСобытияБезДокументаПоставки.ОбъектРасчетов
	|
	|ИМЕЮЩИЕ
	|	СУММА(СуммыЗакрытияПервогоСобытияБезДокументаПоставки.СуммаСНДС) > 0 
	|   ИЛИ (&ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка) И
	|	     СУММА(СуммыЗакрытияПервогоСобытияБезДокументаПоставки.СуммаСНДС) <> 0)
	|
	|;
	// 3- База распределения
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставПоставкиСгруппированно.ОбъектРасчетов КАК ОбъектРасчетов,
	|	СоставПоставкиСгруппированно.ДокументПоставки КАК ДокументПоставки,
	|	СоставПоставкиСгруппированно.СтавкаНДС,
	|	СоставПоставкиСгруппированно.НалоговоеНазначение,
	|	СоставПоставкиСгруппированно.СуммаСНДС КАК СуммаСНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДССоставПоставки.ОбъектРасчетов КАК ОбъектРасчетов,
	|		НДССоставПоставки.ДокументПоставки КАК ДокументПоставки,
	|		НДССоставПоставки.СтавкаНДС,
	|		НДССоставПоставки.НалоговоеНазначение,
	|		СУММА(ВЫБОР
	|			КОГДА НДССоставПоставки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ * НДССоставПоставки.СуммаВзаиморасчетов) КАК СуммаСНДС
	|	ИЗ
	|		СуммыПервогоСобытияКРегистрации КАК СуммыПервогоСобытияКРегистрации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов КАК НДССоставПоставки
	|			ПО (НДССоставПоставки.Активность)
	|				И НДССоставПоставки.Регистратор <> &Ссылка
	|				И НДССоставПоставки.АналитикаУчетаПоПартнерам.Партнер = &Партнер
	|				И НДССоставПоставки.АналитикаУчетаПоПартнерам.Организация = &Организация
	|				И НДССоставПоставки.АналитикаУчетаПоПартнерам.Договор = &Договор
	|				И НДССоставПоставки.АналитикаУчетаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|				И НДССоставПоставки.АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
	|				И ((СуммыПервогоСобытияКРегистрации.ОбъектРасчетов = НДССоставПоставки.ОбъектРасчетов) ИЛИ
	|				   (СуммыПервогоСобытияКРегистрации.ОбъектРасчетов.Ссылка ЕСТЬ NULL И НДССоставПоставки.ОбъектРасчетов.Ссылка ЕСТЬ NULL))
	|				И СуммыПервогоСобытияКРегистрации.ДокументПоставки = НДССоставПоставки.ДокументПоставки
	|
	|	СГРУППИРОВАТЬ ПО
	|		НДССоставПоставки.ОбъектРасчетов,
	|		НДССоставПоставки.ДокументПоставки,
	|		НДССоставПоставки.НалоговоеНазначение,
	|		НДССоставПоставки.СтавкаНДС
	|	) КАК СоставПоставкиСгруппированно
	|ГДЕ
	|	СоставПоставкиСгруппированно.СуммаСНДС > 0
	|   ИЛИ (&ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка) И
	|	     СоставПоставкиСгруппированно.СуммаСНДС <> 0)";
	
	ДатаВозникновенияНК = ?(ЗначениеЗаполнено(Объект.ДатаОтгрузкиОплаты), Объект.ДатаОтгрузкиОплаты, Объект.ДатаВходящегоДокумента);
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаВозникновенияНК);
	Если ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		Запрос.УстановитьПараметр("Партнер", Справочники.Партнеры.НашеПредприятие);
	Иначе
		Запрос.УстановитьПараметр("Партнер", Объект.Партнер);
	КонецЕсли;
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор",                 Объект.Договор);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", Объект.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);	
	
	Если Объект.ПоВводуОстатков Тогда
		ВидПоставки = Перечисления.ВидыПоставки.ВводОстатков;
		
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат Тогда
		ВидПоставки = Перечисления.ВидыПоставки.Возврат;
		
	ИначеЕсли (Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка ИЛИ
	           Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек) 
		     И Объект.Переоценка Тогда
		ВидПоставки = Перечисления.ВидыПоставки.Переоценка;
		
	Иначе
		ВидПоставки = Перечисления.ВидыПоставки.Поставка;
		
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидПоставки", ВидПоставки);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если Результат[1].Пустой() Тогда		
		Возврат Ложь;
	КонецЕсли;
	
	//Результат[0].Выгрузить() - СуммыПервогоСобытияКРегистрации
    СуммыПервогоСобытияКРегистрации                 = Результат[1].Выгрузить();
	СуммыЗакрытияПервогоСобытияБезДокументаПоставки = Результат[2].Выгрузить();	
	СоставыПоставок                                 = Результат[3].Выгрузить();
	
	СуммыПервогоСобытияКРегистрации.Индексы.Добавить("ОбъектРасчетов, ДокументПоставки");	
	СоставыПоставок.Индексы.Добавить("ОбъектРасчетов, ДокументПоставки");	
	
	// Распределим суммы закрытия сформированные документами РегистрацияВходящегоНалоговогоДокумента
	// без указания документа поставки пропорционально суммам первого события в разрезе объектов расчета. 
	// Например такое возникает когда ПКО по которому еще не зарегистрированно ПТУ
	Отбор = Новый Структура("ОбъектРасчетов");
	Для каждого СтрокаКРаспределению Из СуммыЗакрытияПервогоСобытияБезДокументаПоставки Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаКРаспределению);
		
		// Определяем, на что будем распределять
		МассивСтрокБазыРаспределения = СуммыПервогоСобытияКРегистрации.НайтиСтроки(Отбор);
		
		СуммаРаспределения = СтрокаКРаспределению.СуммаСНДС;
		
		МассивСуммБазы = Новый Массив;
		Для каждого СтрокаБазы Из МассивСтрокБазыРаспределения Цикл
			МассивСуммБазы.Добавить(СтрокаБазы.СуммаСНДС);
		КонецЦикла;
		
		МассивСуммРаспределенныйНаБазу = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаРаспределения, МассивСуммБазы);
		
		Индекс = 0;
		Для каждого СтрокаБазы Из МассивСтрокБазыРаспределения Цикл
			СтрокаБазы.СуммаСНДС = СтрокаБазы.СуммаСНДС - МассивСуммРаспределенныйНаБазу[Индекс];
			
			// Возможно все таки оказалось что уже все закрыли (или по ошибке больше чем надо)
			Если СтрокаБазы.СуммаСНДС <= 0 Тогда
				СуммыПервогоСобытияКРегистрации.Удалить(СтрокаБазы);
			КонецЕсли;
			
			Индекс = Индекс + 1;
		КонецЦикла;

	КонецЦикла;
	
	Если СуммыПервогоСобытияКРегистрации.Количество() = 0 Тогда
		// После распределения данных снова может не оказаться
		Возврат Ложь;
	КонецЕсли;
		
	// Распределение сумм возникшего первого события по поставкам в разрезе ставок НДС, объекта расчетов и документа поставки 
	Отбор = Новый Структура("ОбъектРасчетов, ДокументПоставки");
	Для каждого СтрокаПервогоСобытия Из СуммыПервогоСобытияКРегистрации Цикл
		
		СуммаКРаспределению = СтрокаПервогоСобытия.СуммаСНДС;
		
		Если ЗначениеЗаполнено(СтрокаПервогоСобытия.ДокументПоставки) Тогда
			// Можем попробовать распределить
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаПервогоСобытия);
			СоставПоставкиПоДокументу = СоставыПоставок.НайтиСтроки(Отбор);
			
			СуммаПоставкиПоДокументу = 0;
			МассивСуммПоставки = Новый Массив;
			МассивСтрокПоставки = Новый Массив;
			
			
			Для каждого СтрокаСоставаПоставкиПоДокументу Из СоставПоставкиПоДокументу Цикл
				
				СуммаПоставкиПоДокументу = СуммаПоставкиПоДокументу + СтрокаСоставаПоставкиПоДокументу.СуммаСНДС;
				
				НоваяСтрока = Объект.Поставка.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСоставаПоставкиПоДокументу);
				МассивСтрокПоставки.Добавить(НоваяСтрока);
				МассивСуммПоставки.Добавить(НоваяСтрока.СуммаСНДС);
				
			КонецЦикла;
			
						
			ПревышениеСуммы = СуммаКРаспределению - СуммаПоставкиПоДокументу;
			
			Если ПревышениеСуммы >= 0 Тогда
				// Все, что нашли, уже распределено выше полностью, остаток - это аванс
				СуммаКРаспределению = ПревышениеСуммы;
			Иначе
				// Раcпределяем сумму первого события пропорционально суммам найденным в документе
				НовыйМассивСуммПоставки = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаКРаспределению, МассивСуммПоставки);
				Индекс = 0;
				Для каждого СтрокаПоставки Из МассивСтрокПоставки Цикл
					СтрокаПоставки.СуммаСНДС = НовыйМассивСуммПоставки[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;

				// Аванс не остается
				СуммаКРаспределению = 0;
			КонецЕсли;
			
		КонецЕсли;
		
		// Остаток - это аванс без документа поставки
		Если СуммаКРаспределению > 0 Тогда
			НоваяСтрока = Объект.Поставка.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПервогоСобытия);
			НоваяСтрока.СуммаСНДС = СуммаКРаспределению;
			
			Если НДСОбщегоНазначенияСервер.ОрганизацияКонтрагентПлательщикНДС(Объект.Контрагент, Объект.Дата) Тогда
				// Аванс от плательщиков, ставка вводится руками
				НоваяСтрока.СтавкаНДС = Неопределено; 
			Иначе
				// Аванс от не плательщиков, ставка всегда НеНДС
				НоваяСтрока.СтавкаНДС = НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НеНДС);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КоэффициентПропорциональногоОтнесенияНДСНаОбязательства = НДСОбщегоНазначенияПовтИсп.ПолучитьКоэффициентПропорциональногоНДСОбязательтсва(Объект.Организация, НачалоГода(Объект.Дата));
	
	// Дозаполняем оставшиеся колонки табличной части. Пересчитаем суммы от СуммыСНДС 
	Для каждого ТекущаяСтрока Из Объект.Поставка Цикл
		
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.СтавкаНДС) Тогда
			Продолжить; // Будет расчитана вручную, когда пользователь укажет ставку
		КонецЕсли;
		
		ТекущаяСтрока.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ТекущаяСтрока.СуммаСНДС, ТекущаяСтрока.СтавкаНДС, Истина);
		ТекущаяСтрока.Сумма = ТекущаяСтрока.СуммаСНДС - ТекущаяСтрока.СуммаНДС;
		Если ТекущаяСтрока.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально Тогда
			ТекущаяСтрока.СуммаНДСПропорционально = ТекущаяСтрока.СуммаНДС * КоэффициентПропорциональногоОтнесенияНДСНаОбязательства;
		Иначе
			ТекущаяСтрока.СуммаНДСПропорционально = 0;
		КонецЕсли;
		
		// Инвертируем суммы, если это возврат
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат Тогда
			ТекущаяСтрока.Сумма     = - ТекущаяСтрока.Сумма;
			ТекущаяСтрока.СуммаНДС  = - ТекущаяСтрока.СуммаНДС;
			ТекущаяСтрока.СуммаСНДС = - ТекущаяСтрока.СуммаСНДС;
			ТекущаяСтрока.СуммаНДСПропорционально = - ТекущаяСтрока.СуммаНДСПропорционально;
		КонецЕсли;
		
		ТекущаяСтрока.СтатьяДекларацииНДСНалоговыйКредит = ПолучитьСтатьюНалоговойДекларации(Объект.ВидОперации, ТекущаяСтрока.СтавкаНДС);

	КонецЦикла;
	
	
	Возврат Истина;
	
КонецФункции // ЗаполнитьПоОстаткам

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РегистрацияВходящегоНалоговогоДокумента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("45c74e9f-294e-4573-aa66-c7cb5f71c294");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РегистрацияВходящегоНалоговогоДокумента.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС.
                                   |Заполняет реквизит ОбъектРасчетов.'
                                   |;uk='Заповнює реквізит СтавкаПДВ. 
                                   |Заповнює реквізит Об''єкт розрахунків'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РегистрацияВходящегоНалоговогоДокумента.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РегистрацияВходящегоНалоговогоДокумента.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.РегистрацияВходящегоНалоговогоДокумента.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);             

#КонецОбласти

КонецПроцедуры

// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.РегистрацияВходящегоНалоговогоДокумента";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РегистрацияВходящегоНалоговогоДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента КАК РегистрацияВходящегоНалоговогоДокумента
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК РегистрацияВходящегоНалоговогоДокументаПоставка
	|			ГДЕ
	|				РегистрацияВходящегоНалоговогоДокумента.Ссылка = РегистрацияВходящегоНалоговогоДокументаПоставка.Ссылка
	|				И РегистрацияВходящегоНалоговогоДокументаПоставка.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|				И РегистрацияВходящегоНалоговогоДокументаПоставка.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|				И РегистрацияВходящегоНалоговогоДокумента.Ссылка.Проведен
	|			)
	|	ИЛИ 
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК РегистрацияВходящегоНалоговогоДокументаПоставка
	|			ГДЕ
	|				РегистрацияВходящегоНалоговогоДокумента.Ссылка = РегистрацияВходящегоНалоговогоДокументаПоставка.Ссылка
	|				И (РегистрацияВходящегоНалоговогоДокументаПоставка.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|	    			И РегистрацияВходящегоНалоговогоДокументаПоставка.УдалитьОбъектРасчетов НЕ В (&ПустыеЗначенияОбъектРасчетов)
	|				)
	|				И РегистрацияВходящегоНалоговогоДокумента.Ссылка.Проведен
	|			)
	|";
	
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда 
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;       
	
	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("Поставка");
				
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				ПараметрыПоискаОбъектаРасчетов = ОбъектыРасчетовСервер.ПолучитьПараметрыОбъектаРасчетов();
				
				ПараметрыПоискаОбъектаРасчетов.ТипРасчетов  = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ПараметрыПоискаОбъектаРасчетов.Организация  = ДокументОбъект.Организация;
				ПараметрыПоискаОбъектаРасчетов.Партнер      = ДокументОбъект.Партнер;
				ПараметрыПоискаОбъектаРасчетов.Контрагент   = ДокументОбъект.Контрагент;
				
				Для Каждого Поставка Из ДокументОбъект.Поставка Цикл  
					Если ТипЗнч(Поставка.УдалитьОбъектРасчетов) = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
						Поставка.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка(); 	
						ОбъектИзменен = Истина;
					ИначеЕсли НЕ ЗначениеЗаполнено(Поставка.ОбъектРасчетов) Тогда
						ПараметрыПоискаОбъектаРасчетов.Объект = Поставка.УдалитьОбъектРасчетов;
						НовыйОбъектРасчетов = ОбъектыРасчетовСервер.НайтиНовыйОбъектРасчетов(ПараметрыПоискаОбъектаРасчетов);
						Если НовыйОбъектРасчетов = Неопределено Тогда
							ВызватьИсключение(
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не найден объект расчетов для источника данных: %1';uk='Не знайдений об''єкт розрахунків для джерела даних: %1'"),
									Поставка.УдалитьОбъектРасчетов
								)
							);
						ИначеЕсли Поставка.ОбъектРасчетов <> НовыйОбъектРасчетов Тогда
							Поставка.ОбъектРасчетов = НовыйОбъектРасчетов;            
							ОбъектИзменен = Истина;
						КонецЕсли;
					КонецЕсли;	
				КонецЦикла;
				
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли