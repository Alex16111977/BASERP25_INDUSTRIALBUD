#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает таблицы расчетов по задолженностям указанного вида. Предназначена для запуска фоновым заданием.
//
// Параметры:
//  ПараметрыЗаполнения - Структура - см. ПараметрыЗаданияЗаполненияВзаиморасчетов().
//  АдресХранилища - Строка - место, куда фоновое задание поместит результат.
//
Процедура ЗаполнитьЗадолженностьПоОстаткам(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Новый Граница(КонецДня(ПараметрыЗаполнения.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", ПараметрыЗаполнения.Организация);
	Запрос.УстановитьПараметр("МассивСчетов", ПараметрыЗаполнения.СчетаРасчетов);
	Запрос.УстановитьПараметр("ТаблицаЗадолженности", ПараметрыЗаполнения.ТаблицаЗадолженности);
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризацияРасчетовЗадолженность.СчетРасчетов,
	|	ИнвентаризацияРасчетовЗадолженность.ДебиторКредитор,
	|	ИнвентаризацияРасчетовЗадолженность.Подтверждено,
	|	ИнвентаризацияРасчетовЗадолженность.НеПодтверждено,
	|	ИнвентаризацияРасчетовЗадолженность.ИстекСрокДавности,
	|	ИнвентаризацияРасчетовЗадолженность.ВидЗадолженности,
	|	ИнвентаризацияРасчетовЗадолженность.ВсегоДебиторская,
	|	ИнвентаризацияРасчетовЗадолженность.ВсегоКредиторская
	|ПОМЕСТИТЬ ЗадолженностьСтарая
	|ИЗ
	|	&ТаблицаЗадолженности КАК ИнвентаризацияРасчетовЗадолженность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.ФизическиеЛица), ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Организации), ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		ИНАЧЕ ХозрасчетныйОстатки.Счет.Наименование
	|	КОНЕЦ КАК ДебиторКредитор,
	|	ХозрасчетныйОстатки.Счет КАК СчетРасчетов,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|				ИЛИ ХозрасчетныйОстатки.СуммаОстатокДт > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.Дебиторская)
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|				ИЛИ ХозрасчетныйОстатки.СуммаОстатокКт > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.Кредиторская)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЗадолженности,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|					ИЛИ ХозрасчетныйОстатки.СуммаОстатокДт > 0
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстатокДт
	|			ИНАЧЕ ХозрасчетныйОстатки.СуммаОстатокКт
	|		КОНЕЦ) КАК Подтверждено,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|					ИЛИ ХозрасчетныйОстатки.СуммаОстатокДт > 0
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстатокДт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВсегоДебиторская,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|					ИЛИ ХозрасчетныйОстатки.СуммаОстатокКт > 0
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстатокКт
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВсегоКредиторская
	|ПОМЕСТИТЬ ЗадолженностьНовая
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет В (&МассивСчетов), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|				ИЛИ ХозрасчетныйОстатки.СуммаОстатокДт > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.Дебиторская)
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|				ИЛИ ХозрасчетныйОстатки.СуммаОстатокКт > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.Кредиторская)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗадолженности.ПустаяСсылка)
	|	КОНЕЦ,
	|	ХозрасчетныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.ФизическиеЛица), ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Организации), ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Контрагенты), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		ИНАЧЕ ХозрасчетныйОстатки.Счет.Наименование
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадолженностьНовая.ДебиторКредитор КАК ДебиторКредитор,
	|	ЗадолженностьНовая.СчетРасчетов КАК СчетРасчетов,
	|	ЗадолженностьНовая.ВидЗадолженности,
	|	ЕСТЬNULL(ЗадолженностьСтарая.Подтверждено, ЗадолженностьНовая.Подтверждено) КАК Подтверждено,
	|	ЕСТЬNULL(ЗадолженностьСтарая.НеПодтверждено, 0) КАК НеПодтверждено,
	|	ЕСТЬNULL(ЗадолженностьСтарая.ИстекСрокДавности, 0) КАК ИстекСрокДавности,
	|	ЕСТЬNULL(ЗадолженностьСтарая.ВсегоДебиторская, ЗадолженностьНовая.ВсегоДебиторская) КАК ВсегоДебиторская,
	|	ЕСТЬNULL(ЗадолженностьСтарая.ВсегоКредиторская, ЗадолженностьНовая.ВсегоКредиторская) КАК ВсегоКредиторская,
	|	ЕСТЬNULL(ЗадолженностьСтарая.ВсегоДебиторская, ЗадолженностьНовая.ВсегоДебиторская) + ЕСТЬNULL(ЗадолженностьСтарая.ВсегоКредиторская, ЗадолженностьНовая.ВсегоКредиторская) = ЕСТЬNULL(ЗадолженностьСтарая.Подтверждено, ЗадолженностьНовая.Подтверждено) КАК ЕстьПодтвержденнаяСумма,
	|	ЕСТЬNULL(ЗадолженностьСтарая.НеПодтверждено, 0) > 0 КАК ЕстьНеПодтвержденнаяСумма,
	|	ЕСТЬNULL(ЗадолженностьСтарая.ИстекСрокДавности, 0) > 0 КАК ЕстьИстекСрокДавности,
	|	ЕСТЬNULL(ЗадолженностьСтарая.Подтверждено, ЗадолженностьНовая.Подтверждено) > 0
	|		И ЕСТЬNULL(ЗадолженностьСтарая.НеПодтверждено, 0) > 0 КАК ЕстьЧастичноПодтвержденнаяСумма
	|ИЗ
	|	ЗадолженностьНовая КАК ЗадолженностьНовая
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗадолженностьСтарая КАК ЗадолженностьСтарая
	|		ПО ЗадолженностьНовая.СчетРасчетов = ЗадолженностьСтарая.СчетРасчетов
	|			И ЗадолженностьНовая.ДебиторКредитор = ЗадолженностьСтарая.ДебиторКредитор
	|			И ЗадолженностьНовая.ВидЗадолженности = ЗадолженностьСтарая.ВидЗадолженности
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадолженностьНовая.СчетРасчетов.Порядок,
	|	ДебиторКредитор
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Инициализирует параметры для выполнения задания заполнения задолженности по остаткам.
//
// Параметры:
//	Объект - ДокументОбъект.ИнвентаризацияРасчетов - документ инвентаризации, который подлежит заполнению.
//
// Возвращаемое значение:
//	Структура - содержит следующие элементы:
//		* Организация - СправочникСсылка.Организации - организация по данным которым будут заполняться остатки взаиморасчетов;
//		* Дата - Дата - дата, на конец которой будут заполняться остатки взаиморасчетов;
//		* СчетаРасчетов - Массив с элементами типа ПланСчетовСсылка.Хозрасчетный - при заполнении остатков, будут анализироваться только данные счета;
//		* ТаблицаЗадолженности - ТаблицаЗначений - таблица текущих данных задолженности. Если анализируемый счет присутствует в этой таблице, данные по задолженности будут взяты из таблицы.
//			** СчетРасчетов - ПланСчетовСсылка.Хозрасчетный - счет задолженности;
//			** ДебиторКредитор - СправочникСсылка.Организации, СправочникСсылка.Контрагенты, СправочникСсылка.ФизическиеЛица, СправочникСсылка.РегистрацииВНалоговомОргане, Строка - объект задолженности;
//			** Подтверждено - Число(15,2) - подтвержденная сумма задолженности;
//			** НеПодтверждено - Число(15,2) - неподтвержденная сумма задолженности;
//			** ИстекСрокДавности - Число(15,2) - сумма задолженности, срок давности по которой истек;
//			** ВидЗадолженности - ПеречислениеСсылка.ТипыЗадолженности - дебиторская/кредиторская задолженность;
//			** ВсегоДебиторская - Число(15,2) - сумма дебиторской задолженности (если вид задолженности - кредиторская, тогда = 0);
//			** ВсегоКредиторская - Число(15,2) - сумма кредиторской задолженности (если вид задолженности - дебиторская, тогда = 0).
//
Функция ПараметрыЗаданияЗаполненияВзаиморасчетов(Объект) Экспорт
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("Дата",        Объект.Дата);
	ПараметрыЗаполнения.Вставить("СчетаРасчетов");
	ПараметрыЗаполнения.Вставить("ТаблицаЗадолженности");
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт инвентаризации расчетов (ИНВ-17)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ17";
	КомандаПечати.Представление = НСтр("ru='Акт инвентаризации расчетов (ИНВ-17)';uk='Акт інвентаризації розрахунків (ІНВ-17)'");
	
	// Акт инвентаризации расчетов (ИНВ-17) Акт інвентаризації (нак. Мінфіна №572 від 17.06.2015)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "UK_Инвентаризация2015";
	КомандаПечати.Представление = НСтр("ru='Акт инвентаризации расчетов (нак. Мінфіна №572 від 17.06.2015)';uk='Акт інвентаризації розрахунків (нак. Мінфіна №572 від 17.06.2015)'");
	
	// Приказ о проведении инвентаризации (ИНВ-22)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ22";
	КомандаПечати.Представление = НСтр("ru='Приказ о проведении инвентаризации (ИНВ-22)';uk='Наказ про проведення інвентаризації (ІНВ-22)'");
		
КонецПроцедуры

// Функция формирует данные для печатной формы ИНВ22
Функция ДанныеДляПечатнойФормыИНВ22(ПараметрыПечати, МассивОбъектов, КодЯзыкаПечать) Экспорт	
	
	Возврат ПолучитьВыборкуШапок(МассивОбъектов, КодЯзыкаПечать);
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ17") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ17", НСтр("ru='ИНВ-17 (акт инвентаризации)';uk='ІНВ-17 (акт інвентаризації)'"), 
			ПечатьИНВ17(МассивОбъектов, ОбъектыПечати, ПараметрыВывода), , "Документ.ИнвентаризацияРасчетов.ПФ_MXL_Инв17",,Истина);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "UK_Инвентаризация2015") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "UK_Инвентаризация2015", НСтр("ru='ИНВ-17 (нак. Мінфіна №572 від 17.06.2015)';uk='ІНВ-17 (нак. Мінфіна №572 від 17.06.2015)'"), 
			ПечатьИНВ_2015(МассивОбъектов, ОбъектыПечати), , "Документ.ИнвентаризацияРасчетов.ПФ_MXL_UK_Инвентаризация2015");			
	КонецЕсли;
	
КонецПроцедуры
Функция ПолучитьВыборкуШапок(МассивОбъектов, КодЯзыкаПечать)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("НаименованиеИмуществаОбязательствШапка", 
		НСтр("ru='расчетов с покупателями, поставщиками и прочими дебиторами и кредиторами';uk='розрахунків з покупцями, постачальниками та іншими дебіторами і кредиторами'",КодЯзыкаПечать));
	Запрос.УстановитьПараметр("НаименованиеИмуществаОбязательствПодвал",
		НСтр("ru='расчеты с покупателями, поставщиками и прочими дебиторами и кредиторами';uk='розрахунки з покупцями, постачальниками та іншими дебіторами і кредиторами'",КодЯзыкаПечать));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Инвентаризация.Ссылка КАК Ссылка,
	|	Инвентаризация.Дата КАК Дата,
	|	Инвентаризация.Номер КАК Номер,
	|	&НаименованиеИмуществаОбязательствШапка КАК НаименованиеИмуществаОбязательствШапка,
	|	&НаименованиеИмуществаОбязательствПодвал КАК НаименованиеИмуществаОбязательствПодвал,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеОрганизации,
	|	Инвентаризация.Организация КАК Организация,
	|	Инвентаризация.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Инвентаризация.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Инвентаризация.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Инвентаризация.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Инвентаризация.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Инвентаризация.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Инвентаризация.ИнвентаризационнаяКомиссия.(
	|		ФИОПолные КАК ФИОПолные,
	|		Председатель КАК Председатель,
	|		Должность КАК Должность
	|	) КАК ИнвентаризационнаяКомиссия
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов КАК Инвентаризация
	|ГДЕ
	|	Инвентаризация.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	Председатель УБЫВ";
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ПечатьИНВ17(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб			= Истина;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияРасчетовСКонтрагентами_ИНВ17";

	ЗапросКонтрагенты = Новый Запрос;
	ЗапросКонтрагенты.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ЗапросКонтрагенты.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор КАК Ссылка,
	|	Контрагенты.Наименование,
	|	Контрагенты.НаименованиеПолное
	|ПОМЕСТИТЬ ДебиторыКредиторы
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДебиторыКредиторы.ДебиторКредитор = Контрагенты.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор,
	|	Организации.Наименование,
	|	Организации.НаименованиеПолное
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДебиторыКредиторы.ДебиторКредитор = Организации.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор,
	|	ФизическиеЛица.Наименование,
	|	ФизическиеЛица.УточнениеНаименования
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ДебиторыКредиторы.ДебиторКредитор = ФизическиеЛица.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Инвентаризация.ВидЗадолженности КАК ВидЗадолженности,
	|	Инвентаризация.ДебиторКредитор КАК Контрагент,
	|	ЕСТЬNULL(ДебиторыКредиторы.НаименованиеПолное, Инвентаризация.ДебиторКредитор) КАК КонтрагентНаименованиеПолное,
	|	ЕСТЬNULL(ДебиторыКредиторы.Наименование, Инвентаризация.ДебиторКредитор) КАК КонтрагентНаименование,
	|	Инвентаризация.СчетРасчетов КАК СчетРасчетов,
	|	ПРЕДСТАВЛЕНИЕ(Инвентаризация.СчетРасчетов) КАК СчетРасчетовПредставление,
	|	МИНИМУМ(Инвентаризация.НомерСтроки) КАК НомерСтроки,
	|	СУММА(Инвентаризация.Подтверждено + Инвентаризация.НеПодтверждено) КАК Всего,
	|	СУММА(Инвентаризация.Подтверждено) КАК Подтверждено,
	|	СУММА(Инвентаризация.НеПодтверждено) КАК НеПодтверждено,
	|	СУММА(Инвентаризация.ИстекСрокДавности) КАК ИстекСрокДавности,
	|	Инвентаризация.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК Инвентаризация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДебиторыКредиторы КАК ДебиторыКредиторы
	|		ПО (ДебиторыКредиторы.Ссылка = Инвентаризация.ДебиторКредитор)
	|ГДЕ
	|	Инвентаризация.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	Инвентаризация.Ссылка,
	|	Инвентаризация.ВидЗадолженности,
	|	Инвентаризация.ДебиторКредитор,
	|	Инвентаризация.СчетРасчетов,
	|	ЕСТЬNULL(ДебиторыКредиторы.НаименованиеПолное, Инвентаризация.ДебиторКредитор),
	|	ЕСТЬNULL(ДебиторыКредиторы.Наименование, Инвентаризация.ДебиторКредитор)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Инвентаризация.СчетРасчетов.Порядок,
	|	Инвентаризация.ДебиторКредитор
	|АВТОУПОРЯДОЧИВАНИЕ
	|ИТОГИ
	|	СУММА(Всего),
	|	СУММА(Подтверждено),
	|	СУММА(НеПодтверждено),
	|	СУММА(ИстекСрокДавности)
	|ПО
	|	Ссылка,
	|	ВидЗадолженности";
	
	РезультатКонтрагенты = ЗапросКонтрагенты.Выполнить();
	ВыборкаПоВидуДокумента = РезультатКонтрагенты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияРасчетов.ПФ_MXL_Инв17", КодЯзыкаПечать);
	ПервыйДокумент = Истина;

	ВыборкаШапок = ПолучитьВыборкуШапок(МассивОбъектов, КодЯзыкаПечать).Выбрать();
	
	Пока ВыборкаШапок.Следующий() Цикл
	
		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
				
		// Варианты заголовков разделов с подписями печатной формы	
		ЗаголовокРазделаПодписей = Новый Структура();
		ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru='Председатель комиссии';uk='Голова комісії'",КодЯзыкаПечать));
		ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru='Члены комиссии';uk='Члени комісії'",КодЯзыкаПечать));
		
		// Формирование шапки
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Шапка.Параметры.Заполнить(ВыборкаШапок);
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаШапок.Организация, ВыборкаШапок.Дата);
		Шапка.Параметры.Организация          = ?(ПустаяСтрока(СведенияОбОрганизации.ПолноеНаименование),СведенияОбОрганизации.НаименованиеДляПечатныхФорм,
												СведенияОбОрганизации.ПолноеНаименование)+", "+
												СведенияОбОрганизации.КодПоЕДРПОУ;
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаШапок.Номер,Истина,Истина);
		Шапка.Параметры.ДеньДокумента  = День(ВыборкаШапок.Дата);
		//Шапка.Параметры.МесяцДокумента = Сред(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД"), 
		//												 СтрНайти(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД"), " "));
		Шапка.Параметры.МесяцДокумента = Сред(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), 
											Найти(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), " "));
		
		Шапка.Параметры.ДеньОснования = День(ВыборкаШапок.ДокументОснованиеДата);
		Шапка.Параметры.МесяцОснования = Сред(Формат(ВыборкаШапок.ДокументОснованиеДата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), 
											Найти(Формат(ВыборкаШапок.ДокументОснованиеДата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), " "));
		
		ТабДокумент.Вывести(Шапка);

		// Формирование строк дебиторской задолженности
		ПодвалТаблицыДт = Макет.ПолучитьОбласть("ПодвалТаблицыДт");
		СтрокаТаблицыДт = Макет.ПолучитьОбласть("СтрокаТаблицыДт");
		
		СтруктураПоиска = Новый Структура("Ссылка", ВыборкаШапок.Ссылка);
		
		Если ВыборкаПоВидуДокумента.НайтиСледующий(СтруктураПоиска) Тогда
			ВыборкаПоВидуЗадолженности = ВыборкаПоВидуДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Иначе
			Продолжить;
		КонецЕсли;
					
		СтруктураПоиска = Новый Структура("ВидЗадолженности", Перечисления.ТипыЗадолженности.Дебиторская);
				
		Если ВыборкаПоВидуЗадолженности.НайтиСледующий(СтруктураПоиска) Тогда

			ПодвалТаблицыДт.Параметры.Заполнить(ВыборкаПоВидуЗадолженности);
			
			ВыборкаПоКонтрагенту = ВыборкаПоВидуЗадолженности.Выбрать();
			Пока ВыборкаПоКонтрагенту.Следующий() Цикл
				СтрокаТаблицыДт.Параметры.Заполнить(ВыборкаПоКонтрагенту);
				СтрокаТаблицыДт.Параметры.КонтрагентПредставление = ?(ПустаяСтрока(ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное),
																	ВыборкаПоКонтрагенту.КонтрагентНаименование, 
																	ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное);
				ТабДокумент.Вывести(СтрокаТаблицыДт);
			КонецЦикла;
			
		КонецЕсли;
	
		ТабДокумент.Вывести(ПодвалТаблицыДт);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Шапка оборотной стороны Акта
		ШапкаОборот = Макет.ПолучитьОбласть("ШапкаОборот");
		ТабДокумент.Вывести(ШапкаОборот);
		
		// Формирование строк кредиторской задолженности
		ПодвалТаблицыКт = Макет.ПолучитьОбласть("ПодвалТаблицыКт");
		СтрокаТаблицыКт = Макет.ПолучитьОбласть("СтрокаТаблицыКт");
				
		ВыборкаПоВидуЗадолженности.Сбросить();
		
		СтруктураПоиска = Новый Структура("ВидЗадолженности", Перечисления.ТипыЗадолженности.Кредиторская);
		
		Если ВыборкаПоВидуЗадолженности.НайтиСледующий(СтруктураПоиска) Тогда
								
			ПодвалТаблицыКт.Параметры.Заполнить(ВыборкаПоВидуЗадолженности);
			
			ВыборкаПоКонтрагенту = ВыборкаПоВидуЗадолженности.Выбрать();
			Пока ВыборкаПоКонтрагенту.Следующий() Цикл
				СтрокаТаблицыКт.Параметры.Заполнить(ВыборкаПоКонтрагенту);
				СтрокаТаблицыКт.Параметры.КонтрагентПредставление = ?(ПустаяСтрока(ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное),
																		ВыборкаПоКонтрагенту.КонтрагентНаименование, 
																		ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное);
				ТабДокумент.Вывести(СтрокаТаблицыКт);
			КонецЦикла;
				
		КонецЕсли;
				
		ТабДокумент.Вывести(ПодвалТаблицыКт);
		
		// Подвал печатной формы
		Подвал = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести(Подвал);
		
		Подпись = Макет.ПолучитьОбласть("Подпись");
		ТаблицаИнвентаризационнаяКомиссия = ВыборкаШапок.ИнвентаризационнаяКомиссия.Выгрузить();
		
		// Выведем подпись председателя инвентаризационной комиссии
		ПредседательКомиссии = ТаблицаИнвентаризационнаяКомиссия.Найти(Истина, "Председатель");
		
		Если ПредседательКомиссии <> Неопределено Тогда
							
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = ПредседательКомиссии.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ПредседательКомиссии.ФИОПолные;
			
		Иначе
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = "";
			Подпись.Параметры.РасшифровкаПодписи       = "";
			
		КонецЕсли;
			
		ТабДокумент.Вывести(Подпись);
		
		// Выведем подписи членов комиссии
		ВыводитьЗаголовок = Истина;
		
		// Сначала выведем членов комиссии из выборки
		ЧислоЧленовКомиссии = 0;
		Для Каждого ЧленКомиссии Из ТаблицаИнвентаризационнаяКомиссия Цикл
			
			Если ЧленКомиссии.Председатель Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, 
			                                               ЗаголовокРазделаПодписей.ЧленыКомиссии,
														   "");
			Подпись.Параметры.Должность                = ЧленКомиссии.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ЧленКомиссии.ФИОПолные;
			
			ТабДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			ЧислоЧленовКомиссии = ЧислоЧленовКомиссии + 1;
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ЧислоЧленовКомиссии < 3 Тогда
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Для Итератор = (ЧислоЧленовКомиссии + 1) По 3 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, 
				                                               ЗаголовокРазделаПодписей.ЧленыКомиссии,
															   "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
		
		КонецЕсли;
	
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаШапок.Ссылка);

	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПечатьИНВ_2015(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать="uk";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб			= Истина;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияРасчетовСКонтрагентами_ИНВ_2015";

	ЗапросКонтрагенты = Новый Запрос;
	ЗапросКонтрагенты.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ЗапросКонтрагенты.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор КАК Ссылка,
	|	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ, 
	|	Контрагенты.Наименование,
	|	Контрагенты.НаименованиеПолное
	|ПОМЕСТИТЬ ДебиторыКредиторы
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДебиторыКредиторы.ДебиторКредитор = Контрагенты.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор,
	|	Организации.КодПоЕДРПОУ, 
	|	Организации.Наименование,
	|	Организации.НаименованиеПолное
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДебиторыКредиторы.ДебиторКредитор = Организации.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДебиторыКредиторы.ДебиторКредитор,
	|	ФизическиеЛица.КодПоДРФО, 
	|	ФизическиеЛица.Наименование,
	|	ФизическиеЛица.УточнениеНаименования
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК ДебиторыКредиторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ДебиторыКредиторы.ДебиторКредитор = ФизическиеЛица.Ссылка
	|ГДЕ
	|	ДебиторыКредиторы.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Инвентаризация.ВидЗадолженности КАК ВидЗадолженности,
	|	Инвентаризация.ДебиторКредитор КАК Контрагент,
	|	ЕСТЬNULL(ДебиторыКредиторы.КодПоЕДРПОУ, """") КАК КонтрагентКодПоЕДРПОУ,
	|	ЕСТЬNULL(ДебиторыКредиторы.НаименованиеПолное, Инвентаризация.ДебиторКредитор) КАК КонтрагентНаименованиеПолное,
	|	ЕСТЬNULL(ДебиторыКредиторы.Наименование, Инвентаризация.ДебиторКредитор) КАК КонтрагентНаименование,
	|	Инвентаризация.СчетРасчетов КАК СчетРасчетов,
	|	ПРЕДСТАВЛЕНИЕ(Инвентаризация.СчетРасчетов) КАК СчетРасчетовПредставление,
	|	МИНИМУМ(Инвентаризация.НомерСтроки) КАК НомерСтроки,
	|	СУММА(Инвентаризация.Подтверждено + Инвентаризация.НеПодтверждено) КАК Всего,
	|	СУММА(Инвентаризация.Подтверждено) КАК Подтверждено,
	|	СУММА(Инвентаризация.НеПодтверждено) КАК НеПодтверждено,
	|	СУММА(Инвентаризация.ИстекСрокДавности) КАК ИстекСрокДавности,
	|	Инвентаризация.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ИнвентаризацияРасчетов.Задолженность КАК Инвентаризация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДебиторыКредиторы КАК ДебиторыКредиторы
	|		ПО (ДебиторыКредиторы.Ссылка = Инвентаризация.ДебиторКредитор)
	|ГДЕ
	|	Инвентаризация.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	Инвентаризация.Ссылка,
	|	Инвентаризация.ВидЗадолженности,
	|	Инвентаризация.ДебиторКредитор,
	|	Инвентаризация.СчетРасчетов,
	|	ЕСТЬNULL(ДебиторыКредиторы.КодПоЕДРПОУ, """"),
	|	ЕСТЬNULL(ДебиторыКредиторы.НаименованиеПолное, Инвентаризация.ДебиторКредитор),
	|	ЕСТЬNULL(ДебиторыКредиторы.Наименование, Инвентаризация.ДебиторКредитор)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Инвентаризация.СчетРасчетов.Порядок,
	|	Инвентаризация.ДебиторКредитор
	|АВТОУПОРЯДОЧИВАНИЕ
	|ИТОГИ
	|	СУММА(Всего),
	|	СУММА(Подтверждено),
	|	СУММА(НеПодтверждено),
	|	СУММА(ИстекСрокДавности)
	|ПО
	|	Ссылка,
	|	ВидЗадолженности";
	
	РезультатКонтрагенты = ЗапросКонтрагенты.Выполнить();
	ВыборкаПоВидуДокумента = РезультатКонтрагенты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияРасчетов.ПФ_MXL_UK_Инвентаризация2015", КодЯзыкаПечать);
	ПервыйДокумент = Истина;

	ВыборкаШапок = ПолучитьВыборкуШапок(МассивОбъектов, КодЯзыкаПечать).Выбрать();
	
	Пока ВыборкаШапок.Следующий() Цикл
	
		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
				
		// Варианты заголовков разделов с подписями печатной формы	
		ЗаголовокРазделаПодписей = Новый Структура();
		ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru='Председатель комиссии';uk='Голова комісії'", КодЯзыкаПечать));
		ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru='Члены комиссии';uk='Члени комісії'", КодЯзыкаПечать));
		
		// Формирование шапки
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Шапка.Параметры.Заполнить(ВыборкаШапок);
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаШапок.Организация, ВыборкаШапок.Дата);
		
		Шапка.Параметры.Организация          = ?(ПустаяСтрока(СведенияОбОрганизации.ПолноеНаименование),СведенияОбОрганизации.НаименованиеДляПечатныхФорм,
												СведенияОбОрганизации.ПолноеНаименование);
												
		КодЕДРПОУ = СведенияОбОрганизации.КодПоЕДРПОУ;
		
		Если Не ПустаяСтрока(КодЕДРПОУ) И СтрДлина(КодЕДРПОУ) = 8 Тогда
			Шапка.Параметры.КодЕДРПОУ1 = Сред(КодЕДРПОУ, 1, 1);
			Шапка.Параметры.КодЕДРПОУ2 = Сред(КодЕДРПОУ, 2, 1);
			Шапка.Параметры.КодЕДРПОУ3 = Сред(КодЕДРПОУ, 3, 1);
			Шапка.Параметры.КодЕДРПОУ4 = Сред(КодЕДРПОУ, 4, 1);
			Шапка.Параметры.КодЕДРПОУ5 = Сред(КодЕДРПОУ, 5, 1);
			Шапка.Параметры.КодЕДРПОУ6 = Сред(КодЕДРПОУ, 6, 1);
			Шапка.Параметры.КодЕДРПОУ7 = Сред(КодЕДРПОУ, 7, 1);
			Шапка.Параметры.КодЕДРПОУ8 = Сред(КодЕДРПОУ, 8, 1);
		КонецЕсли;
								
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаШапок.Номер,Истина,Истина);
		Шапка.Параметры.ДеньДокумента  = День(ВыборкаШапок.Дата);
		
		Шапка.Параметры.МесяцДокумента = Сред(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД"), 
														 СтрНайти(Формат(ВыборкаШапок.Дата, "ДЛФ=ДД"), " "));
														 
		Шапка.Параметры.ДеньОснования = День(ВыборкаШапок.ДокументОснованиеДата);
		Шапка.Параметры.МесяцОснования = Сред(Формат(ВыборкаШапок.ДокументОснованиеДата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), 
		Найти(Формат(ВыборкаШапок.ДокументОснованиеДата, "ДЛФ=ДД;"+НСтр("ru='Л=ru';uk='Л=uk'", КодЯзыкаПечать)), " "));
		Шапка.Параметры.ДокументОснованиеНомер = ВыборкаШапок.ДокументОснованиеНомер;												 
		
		ТабДокумент.Вывести(Шапка);
		
		//позиционирование на конкретном документе
		СтруктураПоиска = Новый Структура("Ссылка", ВыборкаШапок.Ссылка);
	    ВыборкаПоВидуДокумента.НайтиСледующий(СтруктураПоиска);
		ВыборкаПоВидуЗадолженности = ВыборкаПоВидуДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		//макеты выводимых областей
		ШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
		Нумерация	 	= Макет.ПолучитьОбласть("НумерацияКолонок");	
		СтрокаТаблицыДт	= Макет.ПолучитьОбласть("СтрокаТаблицыДт");
		ПодвалТаблицыДт	= Макет.ПолучитьОбласть("ПодвалТаблицыДт");
		СтрокаТаблицыКт	= Макет.ПолучитьОбласть("СтрокаТаблицыКт");
		ПодвалТаблицыКт	= Макет.ПолучитьОбласть("ПодвалТаблицыКт");
		
		//ПО ДЕБИТОРАМ
		// выведем шапку таблицы дебиторы
		ШапкаТаблицы.Параметры.ПараметрКолонки = "дебіторами";
		ШапкаТаблицы.Параметры.ЗаголовокШапки  = "1.  За  дебіторською  заборгованістю";
		ШапкаТаблицы.Параметры.ВидКонтрагента  = "Дебітор";
		ШапкаТаблицы.Параметры.ДтКт  			= "дебіторської";
		ШапкаТаблицы.Параметры.РасходДоход	= "витрати";
		ТабДокумент.Вывести(ШапкаТаблицы);
		ТабДокумент.Вывести(Нумерация);
				
		СтруктураПоиска = Новый Структура("ВидЗадолженности", Перечисления.ТипыЗадолженности.Дебиторская);
		// Формирование строк дебиторской задолженности
		Если ВыборкаПоВидуЗадолженности.НайтиСледующий(СтруктураПоиска) Тогда
			
			ПодвалТаблицыДт.Параметры.Заполнить(ВыборкаПоВидуЗадолженности);
			
			ВыборкаПоКонтрагенту = ВыборкаПоВидуЗадолженности.Выбрать();
			Пока ВыборкаПоКонтрагенту.Следующий() Цикл
				СтрокаТаблицыДт.Параметры.Заполнить(ВыборкаПоКонтрагенту);
				СтрокаТаблицыДт.Параметры.КонтрагентПредставление = ?(ПустаяСтрока(ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное),
				ВыборкаПоКонтрагенту.КонтрагентНаименование, ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное);
				СтрокаТаблицыДт.Параметры.КодПоЕДРПОУ = ВыборкаПоКонтрагенту.КонтрагентКодПоЕДРПОУ;
				СтрокаТаблицыДт.Параметры.СчетРасчетовПредставление = ВыборкаПоКонтрагенту.СчетРасчетовПредставление;
				Если НЕ ТабДокумент.ПроверитьВывод(СтрокаТаблицыДт) Тогда
					// Выведем разрыв страницы
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабДокумент.Вывести(Нумерация);
					ТабДокумент.Вывести(СтрокаТаблицыДт);
				Иначе
					ТабДокумент.Вывести(СтрокаТаблицыДт);
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;
		
		Если НЕ ТабДокумент.ПроверитьВывод(ПодвалТаблицыДт) Тогда
			// Выведем разрыв страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(Нумерация);
			ТабДокумент.Вывести(ПодвалТаблицыДт);
		Иначе
			ТабДокумент.Вывести(ПодвалТаблицыДт);
		КонецЕсли;
		
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		//ПО КРЕДИТОРАМ
		// выведем шапку таблицы кредиторы
		ШапкаТаблицы.Параметры.ПараметрКолонки = "кредиторами";
		ШапкаТаблицы.параметры.ЗаголовокШапки  = "2.  За  кредиторською  заборгованістю";
		ШапкаТаблицы.Параметры.ВидКонтрагента  = "Кредитор";
		ШапкаТаблицы.Параметры.ДтКт  			= "кредіторської";
		ШапкаТаблицы.Параметры.РасходДоход	= "доходи";
		ТабДокумент.Вывести(ШапкаТаблицы);
		ТабДокумент.Вывести(Нумерация);
		
		ВыборкаПоВидуЗадолженности.Сбросить();
		
		СтруктураПоиска = Новый Структура("ВидЗадолженности", Перечисления.ТипыЗадолженности.Кредиторская);
		// Формирование строк кредиторской задолженности
		Если ВыборкаПоВидуЗадолженности.НайтиСледующий(СтруктураПоиска) Тогда
			
			ПодвалТаблицыКт.Параметры.Заполнить(ВыборкаПоВидуЗадолженности);
			
			ВыборкаПоКонтрагенту = ВыборкаПоВидуЗадолженности.Выбрать();
			Пока ВыборкаПоКонтрагенту.Следующий() Цикл
				СтрокаТаблицыКт.Параметры.Заполнить(ВыборкаПоКонтрагенту);
				СтрокаТаблицыКт.Параметры.КонтрагентПредставление = ?(ПустаяСтрока(ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное),
				ВыборкаПоКонтрагенту.КонтрагентНаименование, ВыборкаПоКонтрагенту.КонтрагентНаименованиеПолное);
				СтрокаТаблицыКт.Параметры.КодПоЕДРПОУ = ВыборкаПоКонтрагенту.КонтрагентКодПоЕДРПОУ;
				СтрокаТаблицыКт.Параметры.СчетРасчетовПредставление = ВыборкаПоКонтрагенту.СчетРасчетовПредставление;
				Если НЕ ТабДокумент.ПроверитьВывод(СтрокаТаблицыКт) Тогда
				// Выведем разрыв страницы
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабДокумент.Вывести(Нумерация);
					ТабДокумент.Вывести(СтрокаТаблицыКт);
				Иначе
					ТабДокумент.Вывести(СтрокаТаблицыКт);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		Если НЕ ТабДокумент.ПроверитьВывод(ПодвалТаблицыКт) Тогда
			// Выведем разрыв страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(Нумерация);
			ТабДокумент.Вывести(ПодвалТаблицыКт);
		Иначе
			ТабДокумент.Вывести(ПодвалТаблицыКт);
		КонецЕсли;

		// Подвал печатной формы
		Подвал = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести(Подвал);
		
		Подпись = Макет.ПолучитьОбласть("Подпись");
		ТаблицаИнвентаризационнаяКомиссия = ВыборкаШапок.ИнвентаризационнаяКомиссия.Выгрузить();
		
		// Выведем подпись председателя инвентаризационной комиссии
		ПредседательКомиссии = ТаблицаИнвентаризационнаяКомиссия.Найти(Истина, "Председатель");
		
		Если ПредседательКомиссии <> Неопределено Тогда
							
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = ПредседательКомиссии.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ПредседательКомиссии.ФИОПолные;
			
		Иначе
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = "";
			Подпись.Параметры.РасшифровкаПодписи       = "";
			
		КонецЕсли;
			
		ТабДокумент.Вывести(Подпись);
		
		// Выведем подписи членов комиссии
		ВыводитьЗаголовок = Истина;
		
		// Сначала выведем членов комиссии из выборки
		ЧислоЧленовКомиссии = 0;
		Для Каждого ЧленКомиссии Из ТаблицаИнвентаризационнаяКомиссия Цикл
			
			Если ЧленКомиссии.Председатель Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, 
			                                               ЗаголовокРазделаПодписей.ЧленыКомиссии,
														   "");
			Подпись.Параметры.Должность                = ЧленКомиссии.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ЧленКомиссии.ФИОПолные;
			
			ТабДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			ЧислоЧленовКомиссии = ЧислоЧленовКомиссии + 1;
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ЧислоЧленовКомиссии < 3 Тогда
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Для Итератор = (ЧислоЧленовКомиссии + 1) По 3 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, 
				                                               ЗаголовокРазделаПодписей.ЧленыКомиссии,
															   "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
		
		КонецЕсли;
	
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаШапок.Ссылка);

	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции
#КонецОбласти

#КонецОбласти

#КонецЕсли