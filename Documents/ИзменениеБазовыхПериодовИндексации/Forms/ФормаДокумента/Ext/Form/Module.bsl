#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", 
		"Объект.Организация",
		"Объект.Ответственный");
		
		Если ПолучитьФункциональнуюОпцию("ВыполнятьРасчетЗарплатыПоПодразделениям") Тогда
			ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
		КонецЕсли;
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ЗаполнитьДанныеФормыПоОрганизации();
		ПриПолученииДанныхНаСервере();
		
		Объект.Дата = ТекущаяДатаСеанса();
		
		Если Не ЗначениеЗаполнено(Объект.ДатаИзменения) Тогда
			Объект.ДатаИзменения = НачалоМесяца(ТекущаяДатаСеанса());
		КонецЕсли;
	
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойств
		
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	ПрочитатьВремяРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьИЗакрытьНаКлиенте", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ЗаполнитьДокументНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ЗаполнитьДокументНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранных()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Позиции.Выгрузить(, "Позиция").ВыгрузитьКолонку("Позиция"), УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы


&НаСервере
Функция ЕстьПодразделение() 
	
	Возврат ПолучитьФункциональнуюОпцию("ВыполнятьРасчетЗарплатыПоПодразделениям");
	
КонецФункции

&НаКлиенте
Процедура ВыбратьПозициюИзСпискаИБПИ(Форма, АдресСпискаПодобранных, ЕстьПодразделение=Ложь) Экспорт 
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	СтруктураОтбор = Новый Структура;
	СтруктураОтбор.Вставить("Владелец", Форма.Объект.Организация);
	СтруктураОтбор.Вставить("ДатаПримененияОтбора", Форма.Объект.ДатаИзменения);
	
	Если ЕстьПодразделение И ЗначениеЗаполнено(Форма.Объект.Подразделение) Тогда
			СтруктураОтбор.Вставить("Подразделение", Форма.Объект.Подразделение);
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбор);
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Истина);
	ПараметрыОткрытия.Вставить("СкрытьПанельВводаДокументов", Истина);
	ПараметрыОткрытия.Вставить("АдресСпискаПодобранных", АдресСпискаПодобранных);
	
	ОткрытьФорму("Справочник.ШтатноеРасписание.ФормаВыбора", ПараметрыОткрытия, Форма.Элементы.Позиции);
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПодборУправленческойПозиции()
	
	ВыбратьПозициюИзСпискаИБПИ(ЭтотОбъект, АдресСпискаПодобранных(),ЕстьПодразделение());
	
КонецПроцедуры

&НаКлиенте
Процедура ПозицииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ОбработатьВыбранныеПозиции(ВыбранноеЗначение) Тогда
		Если Объект.Позиции.Количество() > 0 Тогда
			Элементы.Позиции.ТекущаяСтрока = Объект.Позиции[Объект.Позиции.Количество() - 1].ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДокументНаСервере()
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнийБПИ(Позиция, ДатаВступленияВСилу)
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("Организация",Объект.Организация);
	Отбор.Вставить("Подразделение",Позиция.Подразделение);
	Отбор.Вставить("Должность",Позиция.Должность);
	Отбор.Вставить("Сотрудник",Справочники.Сотрудники.ПустаяСсылка());
	
	ДанныеБПИ=РегистрыСведений.БазовыеПериодыИндексации.СрезПоследних(ДатаВступленияВСилу, Отбор);
	
	Если ДанныеБПИ.Количество()=1 Тогда
		Возврат ДанныеБПИ.Получить(0).Период;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ДобавитьДанныеПоПозицииБПИ(Форма, Позиция, ДатаВступленияВСилу) Экспорт
	
	НоваяСтрокаПозиции = Форма.Объект.Позиции.Добавить();
	
	НоваяСтрокаПозиции.Позиция = Позиция;
	
	ДанныеБПИ = ПолучитьПоследнийБПИ(Позиция,ДатаВступленияВСилу);
	
	Если ЗначениеЗаполнено(ДанныеБПИ) Тогда
		
		НоваяСтрокаПозиции.ПредыдущаяДатаБПИ=ДанныеБПИ;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьВыбранныеПозиции(ВыбранноеЗначение)
	
	ПозицияДобавлена = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ВыбранныеПозиции = ВыбранноеЗначение;
		
	Иначе
		
		ВыбранныеПозиции = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
		
	КонецЕсли;
	
	Для каждого Позиция Из ВыбранныеПозиции Цикл
		
		СтруктураПоиска = Новый Структура("Позиция", Позиция);
		
		Если Объект.Позиции.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			
			ДобавитьДанныеПоПозицииБПИ(ЭтаФорма, Позиция, Объект.ДатаИзменения);
			ДобавленнаяСтрока = Объект.Позиции[Объект.Позиции.Количество() - 1];
			
			ПозицияДобавлена = Истина;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ПозицияДобавлена;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьФормуДокументаИзмененияБПИ(Форма)
	
	ИмяКоманды = "ПодборУправленческойПозиции";
	ЭлементыФормы = Форма.Элементы;
	
	Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Заголовок = НСтр("ru='Подбор';uk='Підбір'");
		КомандаФормы.Действие = "Подключаемый_" + ИмяКоманды;
		КомандаФормы.ИзменяетСохраняемыеДанные = Истина;
	КонецЕсли;
	
	Если ЭлементыФормы.Найти(ИмяКоманды) = Неопределено Тогда
		Элемент = ЭлементыФормы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементыФормы.Позиции.КоманднаяПанель);
		Элемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Элемент.ИмяКоманды = ИмяКоманды;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ПрочитатьВремяРегистрации();
	
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
	ДополнитьФормуДокументаИзмененияБПИ(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Серверные_обработчики_событий_формы
&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаИзменения) Тогда
		Объект.ДатаИзменения = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ЗаполнитьДокументНаСервере();
	КонецЕсли;

	
	ЗаполнитьДанныеФормыПоОрганизации();
	
КонецПроцедуры
#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	
	ОчищаемыеТаблицы = Новый Массив;
	ОчищаемыеТаблицы.Добавить("Объект.Позиции");
	
	Возврат ОчищаемыеТаблицы;
	
КонецФункции

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",		НСтр("ru='организации';uk='організації'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Подразделение",     НСтр("ru='подразделения';uk='підрозділу'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "ДатаИзменения",		НСтр("ru='даты изменения';uk='дати зміни'")));
	
	Возврат Массив
КонецФункции

#КонецОбласти


&НаСервере
Процедура ПрочитатьВремяРегистрации()
	
	ВремяРегистрации = ЗарплатаКадрыРасширенный.ВремяРегистрацииДокумента(Объект.Ссылка, Объект.ДатаИзменения);
	
КонецПроцедуры

#Область ЗаписьДокумента

&НаКлиенте
Процедура ЗаписатьИЗакрытьНаКлиенте(Результат, ДополнительныеПараметры) Экспорт 
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	
КонецПроцедуры

&НаКлиенте
Процедура ПозицииПозицияШтатногоРасписанияПриИзменении(Элемент)
	
	ТекДанные=Элементы.Позиции.ТекущиеДанные;
	
	Если ТекДанные.Позиция = Неопределено 
		Или НЕ ЗначениеЗаполнено(ТекДанные.Позиция) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеБПИ=ПолучитьПоследнийБПИ(ТекДанные.Позиция, НачалоМесяца(Объект.ДатаИзменения));
	
	Если ЗначениеЗаполнено(ДанныеБПИ) Тогда
		
		ТекДанные.ПредыдущаяДатаБПИ =ДанныеБПИ;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодходящиеПозиции(Команда)
	
	Если Объект.Позиции.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПодходящиеПозицииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru='Табличная часть будет очищена, продолжить?';uk='Таблична частина буде очищена, продовжити?'"), РежимДиалогаВопрос.ДаНет);
		
	Иначе 
		
		ЗаполнитьПодходящиеПозицииЗавершение(КодВозвратаДиалога.Да, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПодходящиеПозицииЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПодходящиеПозицииНаСервере();
	
КонецПроцедуры

// Процедура заполняет таблицы документа сотрудниками, у которых среди плановых показателей есть выбранные в документе.
// Также выполняются все сопутствующие действия: индексация, округление, расчет ФОТ и т.п.
&НаСервере
Процедура ЗаполнитьПодходящиеПозицииНаСервере(СохранятьИсправления = Ложь, ВыводитьСообщения = Ложь)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		ПолучитьСообщенияПользователю(?(ВыводитьСообщения, Ложь, Истина));
		Возврат;
	КонецЕсли;
	
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьДокумент(ВремяРегистрации, СохранятьИсправления);
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры
// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуПослеПодтвержденияЗаписи", ЭтотОбъект, Команда);
	ЗарплатаКадрыРасширенныйКлиент.ПоказатьДиалогЗаписиОбъектаДляВыполненияПодключаемойКоманды(ЭтотОбъект, Команда, ОписаниеОповещения);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключаемуюКомандуПослеПодтвержденияЗаписи(РезультатВопроса, Команда) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", ?(Объект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		//ЗаписатьНаКлиенте(Ложь, ПараметрыЗаписи);
		Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
			Возврат; // Запись не удалась, сообщения о причинах выводит платформа.
		КонецЕсли;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

// Конец СтандартныеПодсистемы.Свойства

