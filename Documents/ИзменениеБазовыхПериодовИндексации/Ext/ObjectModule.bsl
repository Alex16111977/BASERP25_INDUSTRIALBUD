#Если Не ТолстыйКлиентУправляемоеПриложение Или Сервер Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическиеЛица.ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПолучитьДанныеДокумента(МенеджерВременныхТаблиц);
	
	ПроверитьЗаполнениеПредыдущихБПИ(МенеджерВременныхТаблиц, Отказ);
	
	Если НЕ Отказ Тогда
		
		ДанныеПроведения = ПолучитьДанныеДляПроведения(МенеджерВременныхТаблиц);

		РасчетЗарплаты.СформироватьДвиженияБазовыеПериодыИндексации(ЭтотОбъект, Движения, ДанныеПроведения.БазовыеПериодыИндексации);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
			
КонецПроцедуры

Функция ОпределитьДатуИзменения(ДанныеЗаполнения) Экспорт
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИзменениеШтатногоРасписания") Тогда
		ДатаИзм=ДанныеЗаполнения.ДатаВступленияВСилу;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИндексацияШтатногоРасписания") Тогда
		ДатаИзм=ДанныеЗаполнения.МесяцИндексации;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УтверждениеШтатногоРасписания") Тогда
		ДатаИзм=ДанныеЗаполнения.МесяцВступленияВСилу;
	КонецЕсли;
	Возврат ДатаИзм;
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИзменениеШтатногоРасписания")  Или
		 ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УтверждениеШтатногоРасписания") Или
		 ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИндексацияШтатногоРасписания") Тогда
		
		ЭтотОбъект.Дата = ТекущаяДатаСеанса();
		ЭтотОбъект.Организация = ДанныеЗаполнения.Организация;
		ЭтотОбъект.Подразделение = ДанныеЗаполнения.Подразделение;
		ЭтотОбъект.ДатаИзменения = ОпределитьДатуИзменения(ДанныеЗаполнения);
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИндексацияШтатногоРасписания") Тогда
			СписокПозиций=ДанныеЗаполнения.Позиции.ВыгрузитьКолонку("ПозицияШтатногоРасписания");
		Иначе			
			СписокПозиций=ДанныеЗаполнения.Позиции.ВыгрузитьКолонку("Позиция");
		КонецЕсли;
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ТекущиеДанныеПозиций = ТекущиеДанныеПозиций(ЭтотОбъект, МенеджерВременныхТаблиц, СписокПозиций);
		
		Позиции.Загрузить(ТекущиеДанныеПозиций);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТПозицийШтатногоРасписания(Объект, МенеджерВременныхТаблиц, СписокПозиций = Неопределено, ПолноеОписание = Ложь) Экспорт
	
	Параметры = УправлениеШтатнымРасписанием.ПараметрыПолученияПозицийШтатногоРасписания();
	Параметры.Организация 		= Объект.Организация;
	Параметры.Подразделение 	= Объект.Подразделение;
	Параметры.ДатаАктуальности 	= НачалоМесяца(Объект.ДатаИзменения);
	
	Если НЕ СписокПозиций = Неопределено Тогда
		Параметры.ПозицииШтатногоРасписания 	= СписокПозиций;
	КонецЕсли;
	
	УправлениеШтатнымРасписанием.СоздатьВТПозицииШтатногоРасписания(Истина, МенеджерВременныхТаблиц, "ВТПозицииШтатногоРасписания", Параметры);
	
	ОписательВременныхТаблиц = УправлениеШтатнымРасписанием.ОписательВременныхТаблицДляПолученияДанныхПозицийШтатногоРасписания(
	МенеджерВременныхТаблиц,
	"ВТПозицииШтатногоРасписания");
	
КонецПроцедуры

Функция ТекущиеДанныеПозиций(Объект, МенеджерВременныхТаблиц = Неопределено, СписокПозиций = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Иначе	
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли;
	
	СоздатьВТПозицийШтатногоРасписания(Объект, Запрос.МенеджерВременныхТаблиц, СписокПозиций, Ложь);
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Сотрудник", Справочники.Сотрудники.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаСреза", НачалоМесяца(Объект.ДатаИзменения));

	Запрос.Текст =
		"ВЫБРАТЬ
		|	БазовыеПериодыИндексацииСрезПоследних.Период КАК ПредыдущаяДатаБПИ,
		|	БазовыеПериодыИндексацииСрезПоследних.Подразделение КАК Подразделение,
		|	БазовыеПериодыИндексацииСрезПоследних.Должность КАК Должность
		|ПОМЕСТИТЬ ВТДанныеБПИ
		|ИЗ
		|	РегистрСведений.БазовыеПериодыИндексации.СрезПоследних(&ДатаСреза, Организация=&Организация И Сотрудник = &Сотрудник) КАК БазовыеПериодыИндексацииСрезПоследних
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания Как Позиция,
		|	ВТДанныеБПИ.ПредыдущаяДатаБПИ
		|ИЗ
		|	ВТПозицииШтатногоРасписания КАК ПозицииШтатногоРасписания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеБПИ КАК ВТДанныеБПИ
		|       ПО ПозицииШтатногоРасписания.Подразделение = ВТДанныеБПИ.Подразделение
		|		И ПозицииШтатногоРасписания.Должность      = ВТДанныеБПИ.Должность";
	
	Результат=Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьДокумент(ДатаИндексации, СохранятьИсправления = Ложь) Экспорт
	
	Позиции.Очистить();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекущиеДанныеПозиций = ТекущиеДанныеПозиций(ЭтотОбъект, МенеджерВременныхТаблиц);
	
	Позиции.Загрузить(ТекущиеДанныеПозиций);

КонецПроцедуры

Функция ПолучитьДанныеДокумента(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Если ДатаИзменения >= Дата(2021,04,01) Тогда
		Запрос.УстановитьПараметр("Период", ДатаИзменения)
	Иначе	
		Запрос.УстановитьПараметр("Период", НачалоМесяца(ДатаИзменения))
	КонецЕсли;	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ИзменениеБПИ.Позиция.Подразделение КАК Подразделение,
	|	ИзменениеБПИ.Позиция.Должность КАК Должность,
	|	ИзменениеБПИ.ПредыдущаяДатаБПИ КАК ПредыдущаяДатаБПИ,
	|	ИзменениеБПИ.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	Документ.ИзменениеБазовыхПериодовИндексации.Позиции КАК ИзменениеБПИ
	|ГДЕ
	|	ИзменениеБПИ.Ссылка = &Ссылка";

	Запрос.Выполнить();
	
КонецФункции	

// Необходимо получить данные для формирования движений
//		кадровой истории - см. КадровыйУчетРасширенный.СформироватьКадровыеДвижения
//		плановых начислений - см. РасчетЗарплатыРасширенный.СформироватьДвиженияПлановыхНачислений
//		плановых выплат (авансы) - см. РасчетЗарплаты.СформироватьДвиженияПлановыхВыплат.
// 
Функция ПолучитьДанныеДляПроведения(МенеджерВременныхТаблиц)
	Запрос = Новый Запрос;
	ДанныеДляПроведения = Новый Структура; 

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзменениеБПИ.Период КАК Период,
		|	ИзменениеБПИ.Организация КАК Организация,
		|	ИзменениеБПИ.Подразделение КАК Подразделение,
		|	ИзменениеБПИ.Должность КАК Должность
		|ИЗ
		|	ВТДанныеДокумента КАК ИзменениеБПИ
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзменениеБПИ.Период,
		|	ИзменениеБПИ.Организация,
		|	ИзменениеБПИ.Подразделение,
		|	ИзменениеБПИ.Должность
		|";

	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатыЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения.Вставить("БазовыеПериодыИндексации", 			РезультатыЗапроса.Выгрузить());
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура ПроверитьЗаполнениеПредыдущихБПИ(МенеджерВременныхТаблиц, Отказ)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПроверки.Подразделение КАК Подразделение,
	|	ДанныеПроверки.Должность КАК Должность,
	|	ДанныеПроверки.НомерСтроки НомерСтроки
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеПроверки
	|ГДЕ
	|	ДанныеПроверки.ПредыдущаяДатаБПИ = ДанныеПроверки.Период";
		
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатыЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого Строка ИЗ РезультатыЗапроса Цикл
		
		СтрокаОшибки = НСтр("ru='Для позиции в строке ';uk='Для позиції в рядку'")+ Строка.НомерСтроки+ НСтр("ru=' в текущем периоде базовый период уже установлен';uk=' в поточному періоді базовий період вже встановлено'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаОшибки,,Строка.НомерСтроки,,Отказ);
		
		Отказ = Истина;
	
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

#КонецЕсли
