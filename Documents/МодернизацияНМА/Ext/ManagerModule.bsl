#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	Документы.ИзменениеПараметровНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
КонецПроцедуры

// Добавляет команду создания документа "Модернизация НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.МодернизацияНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.МодернизацияНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.МодернизацияНМА);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.МодернизацияНМА";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.НематериальныйАктив КАК НематериальныйАктив
	|ИЗ
	|	Документ.МодернизацияНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";

	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДопПараметры);
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаМесяцОкончание", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.МодернизацияНМА"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.МодернизацияНМА);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтНематериальныеАктивы(ТекстыЗапроса)
	
	ИмяТаблицы = "ВтНематериальныеАктивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса, "Документ.МодернизацияНМА");
	
	Текст =
	"ВЫБРАТЬ
	|	втПорядокУчетаНМА.НематериальныйАктив КАК ОбъектУчета,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтНематериальныеАктивы
	|ИЗ
	|	втПорядокУчетаНМА КАК втПорядокУчетаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	Организация,
	|	Подразделение";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаПоДокументу";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втТаблицаПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка КАК Ссылка,
	|	втТаблицаПоДокументу.НематериальныйАктив КАК ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ втТаблицаПоДокументу
	|ИЗ
	|	Документ.МодернизацияНМА КАК втТаблицаПоДокументу
	|ГДЕ
	|	втТаблицаПоДокументу.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	втТаблицаПоДокументу.НематериальныйАктив,
	|	Организация,
	|	Подразделение";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втАмортизацияПоДокументу";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втАмортизацияПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(втНачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втНачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ
	|ПОМЕСТИТЬ втАмортизацияПоДокументу
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчета
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	СчетаБухгалтерскогоУчета.СчетУчета КАК СчетУчета,
	|	СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|				&Период,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК СчетаБухгалтерскогоУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = СчетаБухгалтерскогоУчета.НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаНалоговоеНазначение(ТекстыЗапроса)
	
	ИмяТаблицы = "втНалоговоеНазначение";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втНалоговоеНазначение
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	ПорядокУчетаНМАБУ.НалоговоеНазначение КАК НалоговоеНазначение
	|ПОМЕСТИТЬ втНалоговоеНазначение
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(
	|				&Период,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК ПорядокУчетаНМАБУ
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ПорядокУчетаНМАБУ.НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчетаСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втСчетаУчетаСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаАмортизацииСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаАмортизацииСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетАмортизации КАК Счет
	|ПОМЕСТИТЬ втСчетаАмортизацииСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|";
		
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаСгруппированные";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчетаСгруппированные.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаСгруппированные
	|ИЗ
	|	втСчетаУчетаСгруппированные КАК втСчетаУчетаСгруппированные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаАмортизацииСгруппированные.Счет
	|ИЗ
	|	втСчетаАмортизацииСгруппированные КАК втСчетаАмортизацииСгруппированные";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиПоСчетам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНематериальныеАктивы(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиПоСчетам
	|"+
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОбъектУчета,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаБУ,
	|	СУММА(ХозрасчетныйОстатки.СуммаНУОстаток) КАК СуммаНУ
	|ПОМЕСТИТЬ втОстаткиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					втСчетаСгруппированные КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			(Организация, Подразделение, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.ОбъектУчета
	|				ИЗ
	|					ВтНематериальныеАктивы КАК Т)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Счет";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиСтоимости
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ВосстановительнаяСтоимость.СуммаБУ, 0)) КАК СуммаБУ,
	|	СУММА(ЕСТЬNULL(ВосстановительнаяСтоимость.СуммаНУ, 0)) КАК СуммаНУ,
	|	СУММА(ЕСТЬNULL(-НакопленнаяАмортизация.СуммаБУ, 0) ) КАК НакопленнаяАмортизацияБУ,
	|	СУММА(ЕСТЬNULL(-НакопленнаяАмортизация.СуммаНУ, 0) ) КАК НакопленнаяАмортизацияНУ
	|ПОМЕСТИТЬ втОстаткиСтоимости
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втСчетаУчета.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК ВосстановительнаяСтоимость
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ВосстановительнаяСтоимость.ОбъектУчета
	|			И (втСчетаУчета.СчетУчета = ВосстановительнаяСтоимость.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК НакопленнаяАмортизация
	|		ПО втТаблицаПоДокументу.ОбъектУчета = НакопленнаяАмортизация.ОбъектУчета
	|			И (втСчетаУчета.СчетАмортизации = НакопленнаяАмортизация.Счет)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаПоДокументу.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаДанныеСчетаНакопленияСтоимости(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеСчетаНакопленияСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНематериальныеАктивы(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ДанныеСчетаНакопленияСтоимости.Субконто1 КАК ОбъектУчета,
	|	СУММА(ДанныеСчетаНакопленияСтоимости.СуммаОборот) КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаНакопленияСтоимости
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&КонецМесяца,
	|			Регистратор,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеИзготовлениеНематериальныхАктивов)),
	|			,
	|			(Организация, Подразделение, Субконто1, НаправлениеДеятельности) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.ОбъектУчета,
	|					Т.НаправлениеДеятельности
	|				ИЗ
	|					ВтНематериальныеАктивы КАК Т),
	|			,
	|			) КАК ДанныеСчетаНакопленияСтоимости
	|ГДЕ
	|	ДанныеСчетаНакопленияСтоимости.Регистратор <> &Ссылка
	|	И ДанныеСчетаНакопленияСтоимости.СуммаОборот <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеСчетаНакопленияСтоимости.Субконто1";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДляПроведения";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНематериальныеАктивы(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса);
	ВременнаяТаблицаДанныеСчетаНакопленияСтоимости(ТекстыЗапроса);
	ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаНалоговоеНазначение(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка,
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение,
	|	ВтНематериальныеАктивы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДанныеСчетаНакопленияСтоимости.СуммаБУ, 0) КАК СтоимостьМодернизацииБУ,
	|	ВЫБОР
	|		КОГДА втНалоговоеНазначение.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ДанныеСчетаНакопленияСтоимости.СуммаБУ, 0)
	|	КОНЕЦ КАК СтоимостьМодернизацииНУ,
	|
	|		ЕСТЬNULL(втОстаткиСтоимости.СуммаБУ, 0) 
	|		- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизацияБУ, 0) - ЕСТЬNULL(втАмортизацияПоДокументу.СуммаБУ, 0)
	|		+ ЕСТЬNULL(ДанныеСчетаНакопленияСтоимости.СуммаБУ, 0) КАК СтоимостьБУ,
	|
	|	ВЫБОР
	|		КОГДА втНалоговоеНазначение.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 
	|				ЕСТЬNULL(втОстаткиСтоимости.СуммаНУ, 0) 
	|				- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизацияНУ, 0) - ЕСТЬNULL(втАмортизацияПоДокументу.СуммаНУ, 0)
	|		ИНАЧЕ 
	|				ЕСТЬNULL(втОстаткиСтоимости.СуммаНУ, 0) 
	|				- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизацияНУ, 0) - ЕСТЬNULL(втАмортизацияПоДокументу.СуммаНУ, 0)
	|       		+ ЕСТЬNULL(ДанныеСчетаНакопленияСтоимости.СуммаБУ, 0)
	|	КОНЕЦ  КАК СтоимостьНУ
	|ПОМЕСТИТЬ втДанныеДляПроведения
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиСтоимости КАК втОстаткиСтоимости
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втОстаткиСтоимости.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеСчетаНакопленияСтоимости КАК ДанныеСчетаНакопленияСтоимости
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ДанныеСчетаНакопленияСтоимости.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияПоДокументу КАК втАмортизацияПоДокументу
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втАмортизацияПоДокументу.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалоговоеНазначение КАК втНалоговоеНазначение
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втНалоговоеНазначение.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНематериальныеАктивы КАК ВтНематериальныеАктивы
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ВтНематериальныеАктивы.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияНМА(ТекстыЗапроса, "Документ.МодернизацияНМА");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|
	|	втДанныеДляПроведения.СтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	втДанныеДляПроведения.СтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	втПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	втПервоначальныеСведенияНМА.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
	|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
	|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
	|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
	|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
	|	ПО втПервоначальныеСведенияНМА.НематериальныйАктив = втДанныеДляПроведения.ОбъектУчета";
	

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАБУ(ТекстыЗапроса, "Документ.МодернизацияНМА");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|
	| втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ
	|	- РАЗНОСТЬДАТ(ЕСТЬNULL(втПараметрыАмортизацииНМАБУ.Период, &Период), &Период, МЕСЯЦ) КАК СрокПолезногоИспользованияБУ,
	| втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияНУ
	|	- РАЗНОСТЬДАТ(ЕСТЬNULL(втПараметрыАмортизацииНМАБУ.Период, &Период), &Период, МЕСЯЦ) КАК СрокПолезногоИспользованияНУ,
	|	втПараметрыАмортизацииНМАБУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАБУ КАК втПараметрыАмортизацииНМАБУ
	|	ПО втПараметрыАмортизацииНМАБУ.НематериальныйАктив = втДанныеДляПроведения.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&Проведен                               КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	&НематериальныйАктив                    КАК НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолноеИмяОбъекта()

	Возврат "Документ.МодернизацияНМА";

КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ВременнаяТаблицаДанныеДокумента()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.МодернизацияНМА КАК ТаблицаДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|			И (НематериальныйАктив, Организация) В
	|				(ВЫБРАТЬ
	|					Т.ОбъектУчета,
	|					Т.Организация
	|				ИЗ
	|					втОбъектыДокумента КАК Т)) КАК ПорядокУчетаНМА
	|		ПО ТаблицаДокумента.Организация = ПорядокУчетаНМА.Организация
	|			И ТаблицаДокумента.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
КонецФункции

Функция ВременнаяТаблицаОстаткиСчетаКапитализации()
	
	Возврат
	"ВЫБРАТЬ
	|	ДанныеУчета.Счет КАК Счет,
	|	ДанныеУчета.Субконто1 КАК НематериальныйАктив,
	|	СУММА(ДанныеУчета.СуммаОборот) КАК СуммаБУ
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&ГраницаМесяцОкончание,
	|			Регистратор,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеИзготовлениеНематериальныхАктивов)),
	|			,
	|			(Организация, Подразделение, НаправлениеДеятельности, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.НаправлениеДеятельности,
	|					Т.НематериальныйАктив
	|				ИЗ
	|					втДанныеДокумента КАК Т),
	|			,
	|			) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Субконто1,
	|	ДанныеУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(втОстатки.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втОстаткиСгруппированные
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.НематериальныйАктив КАК НематериальныйАктив,
	|	втОстатки.СуммаБУ КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализации
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.НематериальныйАктив = втДанныеДокумента.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализацииСгруппированные
	|ИЗ
	|	втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.НематериальныйАктив";
	
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт

	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизации("МодернизацияНМА"));
	ТекстыВременныхТаблиц.Добавить(УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам());
	ТекстыВременныхТаблиц.Добавить(ВременнаяТаблицаДанныеДокумента());
	ТекстыВременныхТаблиц.Добавить(ВременнаяТаблицаОстаткиСчетаКапитализации());

	ТекстыВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	Возврат ТекстОтраженияВРеглУчетеУКР();
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(УчетНМА.ТекстОтраженияВРеглУчетеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(УвеличениеСтоимостиНМА());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция УвеличениеСтоимостиНМА()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Увеличение стоимости НМА (Дт СчетУчета:: Кт 154)
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втДанныеСчетаКапитализации.СуммаБУ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|   втПорядокУчетаНМАБУ.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	СчетаОтражения.СчетУчета КАК СчетДт,
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА втПорядокУчетаНМАБУ.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(втДанныеСчетаКапитализации.СуммаБУ, 0)
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|   втПорядокУчетаНМАБУ.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО втДанныеДокумента.НематериальныйАктив = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втДанныеДокумента.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО втДанныеДокумента.НематериальныйАктив = НематериальныеАктивы.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
	|	ПО втДанныеДокумента.НематериальныйАктив = втПорядокУчетаНМА.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
	|	ПО втДанныеДокумента.НематериальныйАктив = втПорядокУчетаНМАБУ.ОбъектУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Увеличение стоимости НМА';uk='Збільшення вартості НМА'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции

//Функция ПеремещениеНМА()
//	
//	Возврат "
//	|////////////////////////////////////////////////////////////////////////////////////////////////////
//	|// Перемещение НМА (Дт СчетУчета:: Кт СчетУчета)
//	|ВЫБРАТЬ
//	|	
//	|	Операция.Ссылка КАК Ссылка,
//	|	Операция.Дата КАК Период,
//	|	Операция.Организация КАК Организация,
//	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
//	|	
//	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК Сумма,
//	|	0 КАК СуммаУУ,
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
//	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
//	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
//	|	
//	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
//	|	Операция.ПодразделениеПолучатель КАК ПодразделениеДт,
//	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
//	
//	|	втПорядокУчетаНМАБУ.НалоговоеНазначение 			 КАК НалоговоеНазначениеДт,
//	
//	|	
//	|	СчетаОтражения.СчетУчета КАК СчетДт,
//	|	Операция.НематериальныйАктив КАК СубконтоДт1,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
//	|	
//	|	0 КАК ВалютнаяСуммаДт,
//	|	0 КАК КоличествоДт,
//	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК СуммаНУДт,
//	
//	|	0 КАК СуммаПРДт,
//	|	0 КАК СуммаВРДт,
//	
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
//	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
//	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
//	|	Операция.Подразделение КАК ПодразделениеКт,
//	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
//	
//	|	втПорядокУчетаНМАБУ.НалоговоеНазначение   КАК НалоговоеНазначениеКт,
//	
//	|	
//	|	СчетаОтражения.СчетУчета КАК СчетКт,
//	|	Операция.НематериальныйАктив КАК СубконтоКт1,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
//	|	
//	|	0 КАК ВалютнаяСуммаКт,
//	|	0 КАК КоличествоКт,
//	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК СуммаНУКт,
//	
//	|	0 КАК СуммаПРКт,
//	|	0 КАК СуммаВРКт,
//	|
//	
//	|	""Перемещение НМА"" КАК Содержание
//	|ИЗ
//	|	Документ.ПеремещениеНМА КАК Операция
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
//	|		ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
//	|		ПО Операция.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
//	|		ПО Операция.НематериальныйАктив = НематериальныеАктивы.Ссылка
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
//	|	ПО Операция.НематериальныйАктив = втПорядокУчетаНМА.ОбъектУчета
//	
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
//	|	ПО Операция.НематериальныйАктив = втПорядокУчетаНМАБУ.ОбъектУчета
//	
//	|ГДЕ
//	|	Операция.Ссылка = &Ссылка
//	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперацииРеглУчет.ПеремещениеНМА)
//	|";
//	
//КонецФункции

//Функция ПеремещениеАмортизации()
//	
//	Возврат "
//	|////////////////////////////////////////////////////////////////////////////////////////////////////
//	|// Перемещение амортизации НМА (Дт СчетНачисленияАмортизации:: Кт СчетНачисленияАмортизации)
//	|ВЫБРАТЬ
//	|
//	|	Операция.Ссылка КАК Ссылка,
//	|	Операция.Дата КАК Период,
//	|	Операция.Организация КАК Организация,
//	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
//	|	
//	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК Сумма,
//	|	0 КАК СуммаУУ,
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
//	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
//	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
//	|	
//	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
//	|	Операция.Подразделение КАК ПодразделениеДт,
//	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
//	
//	|	втПорядокУчетаНМАБУ.НалоговоеНазначение 			 КАК НалоговоеНазначениеДт,
//	
//	|	
//	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
//	|	Операция.НематериальныйАктив КАК СубконтоДт1,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
//	|	
//	|	0 КАК ВалютнаяСуммаДт,
//	|	0 КАК КоличествоДт,
//	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУДт,
//	
//	|	0 КАК СуммаПРДт,
//	|	0 КАК СуммаВРДт,
//	
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
//	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
//	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
//	|	
//	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
//	|	Операция.ПодразделениеПолучатель КАК ПодразделениеКт,
//	|	втПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
//	
//	|	втПорядокУчетаНМАБУ.НалоговоеНазначение 			 КАК НалоговоеНазначениеКт,
//	
//	|	
//	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетКт,
//	|	Операция.НематериальныйАктив КАК СубконтоКт1,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
//	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
//	|	
//	|	0 КАК ВалютнаяСуммаКт,
//	|	0 КАК КоличествоКт,
//	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУКт,
//	
//	|	0 КАК СуммаПРКт,
//	|	0 КАК СуммаВРКт,
//	
//	|	""Перемещение амортизации НМА"" КАК Содержание
//	|ИЗ
//	|	Документ.ПеремещениеНМА КАК Операция
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
//	|		ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
//	|		ПО Операция.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
//	|		ПО Операция.НематериальныйАктив = НематериальныеАктивы.Ссылка
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
//	|	ПО Операция.НематериальныйАктив = втПорядокУчетаНМА.ОбъектУчета
//	
//	|	
//	|	ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
//	|	ПО Операция.НематериальныйАктив = втПорядокУчетаНМАБУ.ОбъектУчета
//	
//	|ГДЕ
//	|	Операция.Ссылка = &Ссылка
//	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперацииРеглУчет.ПеремещениеНМА)
//	|";
//	
//КонецФункции


#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетНМА.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
		
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

#КонецОбласти
#КонецОбласти

#КонецЕсли
