#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("ТМЦВЭксплуатации");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТМЦВЭксплуатации(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);

	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Перемещение в эксплуатации".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПеремещениеВЭксплуатации) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПеремещениеВЭксплуатации.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПеремещениеВЭксплуатации);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнутреннееПотребление";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
	Отчеты.МатериалыВЭксплуатации.ДобавитьКомандуОтчета(КомандыОтчетов);
	
КонецПроцедуры

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР()
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции

#КонецОбласти

#Область ПроводкиРеглУчетаУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
	
#Область ПеремещениеВЭксплуатации // (Дт 111, 1122 :: Кт 111, 1122)
	ПеремещениеВЭксплуатации =  "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Перемещение в эксплуатации товаров (Дт 111, 1122 :: Кт 111, 1122)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	втСтоимостиТМЦВЭксплуатации.Стоимость КАК Сумма, 
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьУУ КАК СуммаУУ, 
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаДт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втСтоимостиТМЦВЭксплуатации.ПодразделениеПолучатель КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоДт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	втСтоимостиТМЦВЭксплуатации.Количество КАК КоличествоДт,
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	втСтоимостиТМЦВЭксплуатации.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоКт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	втСтоимостиТМЦВЭксплуатации.Количество КАК КоличествоКт,
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПеремещениеВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		втСтоимостиТМЦВЭксплуатации КАК втСтоимостиТМЦВЭксплуатации
	|	ПО
	|		Операция.Ссылка = втСтоимостиТМЦВЭксплуатации.Ссылка
	|/////////////////////////////////////////////////////////////	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Перемещение амортизации эксплуатации товаров (Дт 1322 :: Кт 1322)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьАмортизации КАК Сумма, 
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьАмортизацииУУ КАК СуммаУУ, 
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииАмортизация) КАК ВидСчетаДт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втСтоимостиТМЦВЭксплуатации.Подразделение	КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоДт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КоличествоДт,
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьАмортизацииНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииАмортизация) КАК ВидСчетаКт,
	|	втСтоимостиТМЦВЭксплуатации.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	втСтоимостиТМЦВЭксплуатации.ПодразделениеПолучатель КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	втСтоимостиТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	втСтоимостиТМЦВЭксплуатации.Номенклатура КАК СубконтоКт1,
	|	втСтоимостиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСтоимостиТМЦВЭксплуатации.СтоимостьАмортизацииНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПеремещениеВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		втСтоимостиТМЦВЭксплуатации КАК втСтоимостиТМЦВЭксплуатации
	|	ПО
	|		Операция.Ссылка = втСтоимостиТМЦВЭксплуатации.Ссылка
	|"; 

	ПеремещениеВЭксплуатации = СтрШаблон(ПеремещениеВЭксплуатации, 
		НСтр("ru='Перемещение ТМЦ в эксплуатации';uk='Переміщення ТМЦ в експлуатації'",ЯзыкСодержания));

#КонецОбласти 	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ПеремещениеВЭксплуатации);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаТЦМВЭксплуатации
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)
	|	И ПланСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|	И НЕ ПланСчетов.Забалансовый
	|	И ПланСчетов.Количественный
	|	И НЕ ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаАмортизацииТЦМВЭксплуатации
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)
	|	И ПланСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|	И НЕ ПланСчетов.Забалансовый
	|	И НЕ ПланСчетов.Количественный
	|	И НЕ ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеВЭксплуатацииТовары.Ссылка,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация КАК Организация,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение КАК Подразделение,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.ПодразделениеПолучатель,
	|	ПеремещениеВЭксплуатацииТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	СУММА(ПеремещениеВЭксплуатацииТовары.Количество) КАК Количество,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации
	|ПОМЕСТИТЬ втПеремещениеТЦМ
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|ГДЕ
	|	ПеремещениеВЭксплуатацииТовары.Ссылка = &Ссылка
	|	И ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В (ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив), ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.БиблиотечныеФонды))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеВЭксплуатацииТовары.Ссылка,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.ПодразделениеПолучатель,
	|	ПеремещениеВЭксплуатацииТовары.Номенклатура,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ПартииТМЦВЭксплуатации) КАК ПартияТМЦВЭксплуатации,
	|	ХозрасчетныйОстатки.НалоговоеНазначение,
	|	ХозрасчетныйОстатки.КоличествоОстатокДт КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстаток,
	|	ХозрасчетныйОстатки.СуммаУУОстатокДт КАК СуммаУУОстаток,
	|	ХозрасчетныйОстатки.СуммаНУОстатокДт КАК СуммаНУОстаток
	|ПОМЕСТИТЬ втОстаткиТМЦВЭксплуатации
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Ссылка
	|				ИЗ
	|					втСчетаТЦМВЭксплуатации КАК Т),
	|			,
	|			(Организация, Подразделение, Субконто1, Субконто2) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.Номенклатура,
	|					Т.ПартияТМЦВЭксплуатации
	|				ИЗ
	|					втПеремещениеТЦМ КАК Т)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ПартииТМЦВЭксплуатации) КАК ПартияТМЦВЭксплуатации,
	|	ХозрасчетныйОстатки.НалоговоеНазначение,
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаОстаток,
	|	ХозрасчетныйОстатки.СуммаУУОстатокКт КАК СуммаУУОстаток,
	|	ХозрасчетныйОстатки.СуммаНУОстатокКт КАК СуммаНУОстаток
	|ПОМЕСТИТЬ втОстаткиАмортизацииТМЦВЭксплуатации
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Ссылка
	|				ИЗ
	|					втСчетаАмортизацииТЦМВЭксплуатации КАК Т),
	|			,
	|			(Организация, Подразделение, Субконто1, Субконто2) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.Номенклатура,
	|					Т.ПартияТМЦВЭксплуатации
	|				ИЗ
	|					втПеремещениеТЦМ КАК Т)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.Номенклатура, втОстаткиАмортизацииТМЦВЭксплуатации.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации, втОстаткиАмортизацииТМЦВЭксплуатации.ПартияТМЦВЭксплуатации) КАК ПартияТМЦВЭксплуатации,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.НалоговоеНазначение, втОстаткиАмортизацииТМЦВЭксплуатации.НалоговоеНазначение) КАК НалоговоеНазначение,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.СуммаУУОстаток, 0) КАК СуммаУУОстаток,
	|	ЕСТЬNULL(втОстаткиТМЦВЭксплуатации.СуммаНУОстаток, 0) КАК СуммаНУОстаток,
	|	ЕСТЬNULL(втОстаткиАмортизацииТМЦВЭксплуатации.СуммаОстаток, 0) КАК СуммаАмортизацииОстаток,
	|	ЕСТЬNULL(втОстаткиАмортизацииТМЦВЭксплуатации.СуммаУУОстаток, 0) КАК СуммаАмортизацииУУОстаток,
	|	ЕСТЬNULL(втОстаткиАмортизацииТМЦВЭксплуатации.СуммаНУОстаток, 0) КАК СуммаАмортизацииНУОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	втОстаткиТМЦВЭксплуатации КАК втОстаткиТМЦВЭксплуатации
	|		ПОЛНОЕ СОЕДИНЕНИЕ втОстаткиАмортизацииТМЦВЭксплуатации КАК втОстаткиАмортизацииТМЦВЭксплуатации
	|		ПО втОстаткиТМЦВЭксплуатации.Номенклатура = втОстаткиАмортизацииТМЦВЭксплуатации.Номенклатура
	|			И втОстаткиТМЦВЭксплуатации.ПартияТМЦВЭксплуатации = втОстаткиАмортизацииТМЦВЭксплуатации.ПартияТМЦВЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеремещениеТЦМ.Ссылка,
	|	втПеремещениеТЦМ.Подразделение,
	|	втПеремещениеТЦМ.ПодразделениеПолучатель,
	|	втПеремещениеТЦМ.Номенклатура,
	|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатации,
	|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатацииКатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	втОстатки.НалоговоеНазначение,
	|	втПеремещениеТЦМ.Количество,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаУУОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаУУОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК СтоимостьУУ,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаНУОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаНУОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК СтоимостьНУ,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаАмортизацииОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаАмортизацииОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК СтоимостьАмортизации,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаАмортизацииУУОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаАмортизацииУУОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК СтоимостьАмортизацииУУ,
	|	ВЫБОР
	|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|			ТОГДА втОстатки.СуммаАмортизацииНУОстаток
	|		КОГДА втОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстатки.СуммаАмортизацииНУОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|	КОНЕЦ КАК СтоимостьАмортизацииНУ
	|ПОМЕСТИТЬ втСтоимостиТМЦВЭксплуатации
	|ИЗ
	|	втПеремещениеТЦМ КАК втПеремещениеТЦМ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втПеремещениеТЦМ.Номенклатура = втОстатки.Номенклатура
	|			И втПеремещениеТЦМ.ПартияТМЦВЭксплуатации = втОстатки.ПартияТМЦВЭксплуатации;";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПеремещениеВЭксплуатации";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата КАК Период,
	|	ДанныеШапки.Номер КАК Номер,
	|	ДанныеШапки.Организация КАК Организация,
	|	ДанныеШапки.Подразделение КАК Подразделение,
	|	ДанныеШапки.ПодразделениеПолучатель КАК ПодразделениеПолучатель
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("ПодразделениеПолучатель", Реквизиты.ПодразделениеПолучатель);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПеремещениеВЭксплуатации"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Перемещение в эксплуатации';uk='Переміщення в експлуатації'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеремещениеВЭксплуатации);
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ТМЦВЭксплуатации(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТМЦВЭксплуатации";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации КАК Партия,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначение,
	|	-ТабличнаяЧасть.Количество КАК Количество
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	&ПодразделениеПолучатель,
	|	ТабличнаяЧасть.ФизическоеЛицоПолучатель,
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации,
	|	ТабличнаяЧасть.ПартияТМЦВЭксплуатации.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТабличнаяЧасть.Количество
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.Дата КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	// Ведомость МШ-7 (сдача)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МШ7с";
	КомандаПечати.Представление = НСтр("ru='Ведомость МШ-7 (сдача)';uk='Відомість МШ-7 (здача)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Ведомость МШ-7 (выдача)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МШ7в";
	КомандаПечати.Представление = НСтр("ru='Ведомость МШ-7 (выдача)';uk='Відомість МШ-7 (видача)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	// Требование-накладная (М-11)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьМ11";
	КомандаПечати.Идентификатор = "М11";
	КомандаПечати.Представление = НСтр("ru='Требование-накладная (М-11)';uk='Вимога-накладна (М-11)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Форма ОЗ-1
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОЗ1";
	КомандаПечати.Идентификатор = "ОЗ1";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-1';uk='Форма ОЗ-1'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьФормОСБюджет";
	КомандаПечати.Идентификатор = "АктПеремещениеОСМАБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт внутреннего перемещения основных средств';uk='Акт внутрішнього переміщення основних засобів'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

// Функция формирует табличный документ с типовой печатной формой ОЗ-1
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПолучитьДанныеДляПечатнойФормыОЗ1(ПараметрыПечати, Ссылка) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПланСчетов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ втСчетаТЦМВЭксплуатации
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК ПланСчетов
		|ГДЕ
		|	ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
		|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)
		|	И ПланСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|	И НЕ ПланСчетов.Забалансовый
		|	И ПланСчетов.Количественный
		|	И НЕ ПланСчетов.ЗапретитьИспользоватьВПроводках
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПеремещениеВЭксплуатацииТовары.Ссылка,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация КАК Организация,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение КАК Подразделение,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.ПодразделениеПолучатель,
		|	ПеремещениеВЭксплуатацииТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеВЭксплуатацииТовары.Характеристика,
		|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
		|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации,
		|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицо КАК Сдал,
		|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицоПолучатель КАК Принял,
		|	СУММА(ПеремещениеВЭксплуатацииТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втПеремещениеТЦМ
		|ИЗ
		|	Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
		|ГДЕ
		|	ПеремещениеВЭксплуатацииТовары.Ссылка = &Ссылка
		|	И ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В (ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив), ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.БиблиотечныеФонды))
		|
		|СГРУППИРОВАТЬ ПО
		|	ПеремещениеВЭксплуатацииТовары.Ссылка,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение,
		|	ПеремещениеВЭксплуатацииТовары.Ссылка.ПодразделениеПолучатель,
		|	ПеремещениеВЭксплуатацииТовары.Номенклатура,
		|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации,
		|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации,
		|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицо,
		|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицоПолучатель,
		|	ПеремещениеВЭксплуатацииТовары.Характеристика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Подразделение,
		|	Номенклатура,
		|	ПартияТМЦВЭксплуатации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ПартииТМЦВЭксплуатации) КАК ПартияТМЦВЭксплуатации,
		|	ХозрасчетныйОстатки.НалоговоеНазначение,
		|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) КАК СуммаОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет В
		|				(ВЫБРАТЬ
		|					Т.Ссылка
		|				ИЗ
		|					втСчетаТЦМВЭксплуатации КАК Т),
		|			,
		|			(Организация, Подразделение, Субконто1, Субконто2) В
		|				(ВЫБРАТЬ
		|					Т.Организация,
		|					Т.Подразделение,
		|					Т.Номенклатура,
		|					Т.ПартияТМЦВЭксплуатации
		|				ИЗ
		|					втПеремещениеТЦМ КАК Т)) КАК ХозрасчетныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПартияТМЦВЭксплуатации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПеремещениеТЦМ.Ссылка КАК Ссылка,
		|	втПеремещениеТЦМ.Ссылка.Номер КАК НомерАкта,
		|	втПеремещениеТЦМ.Ссылка.Дата КАК ДатаАкта,
		|	втПеремещениеТЦМ.Ссылка.Организация КАК Организация,
		|	втПеремещениеТЦМ.Ссылка.Организация.Префикс КАК Префикс,
		|	втПеремещениеТЦМ.Подразделение КАК СдалоПодразделение,
		|	втПеремещениеТЦМ.ПодразделениеПолучатель КАК ПринялоПодразделение,
		|	""Переміщення"" КАК ВидОперации,
		|	втПеремещениеТЦМ.Номенклатура КАК Номенклатура,
		|	втПеремещениеТЦМ.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	втПеремещениеТЦМ.Характеристика,
		|	втПеремещениеТЦМ.Характеристика.Наименование КАК ХарактеристикаНаименование,
		|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатации,
		|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатацииКатегорияЭксплуатации КАК КатегорияЭксплуатации,
		|	втПеремещениеТЦМ.Количество,
		|	втПеремещениеТЦМ.Сдал,
		|	втПеремещениеТЦМ.Принял,
		|	ВЫБОР
		|		КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
		|			ТОГДА втОстатки.СуммаОстаток
		|		КОГДА втОстатки.КоличествоОстаток = 0
		|			ТОГДА 0
		|		ИНАЧЕ втОстатки.СуммаОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
		|	КОНЕЦ КАК ПервоначальнаяСтоимость
		|ИЗ
		|	втПеремещениеТЦМ КАК втПеремещениеТЦМ
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
		|		ПО втПеремещениеТЦМ.Номенклатура = втОстатки.Номенклатура
		|			И втПеремещениеТЦМ.ПартияТМЦВЭксплуатации = втОстатки.ПартияТМЦВЭксплуатации";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Период", Ссылка.МоментВремени());
		
	ВыборкаПоОС = Запрос.Выполнить();
	
	Возврат ВыборкаПоОС;

КонецФункции // ПечатьОЗ1()

Функция ПолучитьДанныеДляПечатнойФормыАктПеремещениеОСМА_2016(ПараметрыПечати, Ссылка) Экспорт
	

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Ссылка.МоментВремени());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеВЭксплуатации.Дата КАК ДатаАкта,
	|	ПеремещениеВЭксплуатации.Номер КАК НомерАкта,
	|	ПеремещениеВЭксплуатации.ПодразделениеПолучатель.Представление КАК ПринялоПодразделение,
	|	ПеремещениеВЭксплуатации.Организация КАК Организация,
	|	ПеремещениеВЭксплуатации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|ГДЕ
	|	ПеремещениеВЭксплуатации.Ссылка = &Ссылка";
	
	ВыборкаПоШапке = Запрос.Выполнить();
	
	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втСчетаТЦМВЭксплуатации
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК ПланСчетов
	|ГДЕ
	|	ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура)
	|	И ПланСчетов.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)
	|	И ПланСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|	И НЕ ПланСчетов.Забалансовый
	|	И ПланСчетов.Количественный
	|	И НЕ ПланСчетов.ЗапретитьИспользоватьВПроводках
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация КАК Организация,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение КАК Подразделение,
	|	ПеремещениеВЭксплуатацииТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеВЭксплуатацииТовары.Характеристика КАК Характеристика,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации КАК ПартияТМЦВЭксплуатацииКатегорияЭксплуатации,
	|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицо КАК Сдал,
	|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицоПолучатель КАК Принял,
	|	СУММА(ПеремещениеВЭксплуатацииТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ втПеремещениеТЦМ
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|ГДЕ
	|	ПеремещениеВЭксплуатацииТовары.Ссылка = &Ссылка
	|	И ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ В (ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.МалоценныйНеоборотныйАктив), ЗНАЧЕНИЕ(Перечисление.ВидыТМЦВЭксплуатации.БиблиотечныеФонды))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Организация,
	|	ПеремещениеВЭксплуатацииТовары.Ссылка.Подразделение,
	|	ПеремещениеВЭксплуатацииТовары.Номенклатура,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации,
	|	ПеремещениеВЭксплуатацииТовары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации,
	|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицо,
	|	ПеремещениеВЭксплуатацииТовары.ФизическоеЛицоПолучатель,
	|	ПеремещениеВЭксплуатацииТовары.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ПартииТМЦВЭксплуатации) КАК ПартияТМЦВЭксплуатации,
	|	ХозрасчетныйОстатки.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) КАК СуммаОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Ссылка
	|				ИЗ
	|					втСчетаТЦМВЭксплуатации КАК Т),
	|			,
	|			(Организация, Подразделение, Субконто1, Субконто2) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.Номенклатура,
	|					Т.ПартияТМЦВЭксплуатации
	|				ИЗ
	|					втПеремещениеТЦМ КАК Т)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ПартияТМЦВЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеремещениеТЦМ.Номенклатура КАК Номенклатура,
	|	втПеремещениеТЦМ.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	втПеремещениеТЦМ.Характеристика КАК Характеристика,
	|	втПеремещениеТЦМ.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	втПеремещениеТЦМ.ПартияТМЦВЭксплуатацииКатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	втПеремещениеТЦМ.Количество КАК Количество,
	|	втПеремещениеТЦМ.Сдал КАК Сдал,
	|	втПеремещениеТЦМ.Принял КАК Принял,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА втПеремещениеТЦМ.Количество = втОстатки.КоличествоОстаток
	|				ТОГДА втОстатки.СуммаОстаток
	|			КОГДА втОстатки.КоличествоОстаток = 0
	|				ТОГДА 0
	|			ИНАЧЕ втОстатки.СуммаОстаток * втПеремещениеТЦМ.Количество / втОстатки.КоличествоОстаток
	|		КОНЕЦ, 0) КАК ПервоначальнаяСтоимость,
	|	втПеремещениеТЦМ.Номенклатура.Код КАК ИнвентарныйНомер
	|ИЗ
	|	втПеремещениеТЦМ КАК втПеремещениеТЦМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втПеремещениеТЦМ.Номенклатура = втОстатки.Номенклатура
	|			И втПеремещениеТЦМ.ПартияТМЦВЭксплуатации = втОстатки.ПартияТМЦВЭксплуатации
	|ИТОГИ ПО
	|	Сдал,
	|	Принял";
	
	ВыборкаПоОС = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС", ВыборкаПоШапке, ВыборкаПоОС);

КонецФункции // ПолучитьДанныеДляПечатнойФормыАктПеремещениеОСМА_2016()

// Функция формирует печатную форму документа
//
Функция ПечатьМШ7(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета, Сдача)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Подразделение) КАК Подразделение,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.Префикс КАК Префикс,
	|	ВЫБОР
	|		КОГДА &Сдача
	|			ТОГДА ""Переміщення (здача)""
	|		ИНАЧЕ ""Переміщення (видача)""
	|	КОНЕЦ КАК КодОперации,
	|	Документ.Товары.(
	|		НомерСтроки КАК НомерПП,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|		Номенклатура.Код КАК НоменклатурныйНомерКод,
	|		Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|		Характеристика.НаименованиеПолное КАК Характеристика,
	|		Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|		Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА &Сдача
	|				ТОГДА Документ.Товары.ФизическоеЛицо
	|			ИНАЧЕ Документ.Товары.ФизическоеЛицоПолучатель
	|		КОНЕЦ КАК Сотрудник,
	|		ПартияТМЦВЭксплуатации.Дата КАК ДатаПоступленияВЭксплуатацию,
	|		ПартияТМЦВЭксплуатации.СрокЭксплуатации КАК СрокСлужбы
	|	) КАК ТаблицаТовары
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.Товары.ПартияТМЦВЭксплуатации.КатегорияЭксплуатации.ВидТМЦ = &ВидТМЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерПП";
	
	ЗапросПоДокументам.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросПоДокументам.УстановитьПараметр("Сдача",Сдача);
	ЗапросПоДокументам.УстановитьПараметр("ВидТМЦ",Перечисления.ВидыТМЦВЭксплуатации.МалоценныйБыстроизнашивающийсяПредмет);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеМалоценныхАктивовВЭксплуатации_МШ7";
	
	ТоварКод = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(ТоварКод) Тогда
		ТоварКод = "Код";
	КонецЕсли;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_МШ7");
	
	
	Если ЗапросПоДокументам.Выполнить().Пустой() Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабДокумент.Вывести(ОбластьМакета);
			
			Возврат ТабДокумент;
			
		КонецЕсли;

	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодписи   = Макет.ПолучитьОбласть("Подвал");
	ОбластьСтрока	 = Макет.ПолучитьОбласть("Строка");
	
	ПервыйДокумент = Истина;
	
	ВыборкаДокументы = ЗапросПоДокументам.Выполнить().Выбрать();
	
	Пока ВыборкаДокументы.Следующий() Цикл
		ВыборкаСтроки = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		СписокФизическихЛиц = Новый Массив;
		Пока ВыборкаСтроки.Следующий() Цикл
			СписокФизическихЛиц.Добавить(ВыборкаСтроки.Сотрудник);
		КонецЦикла;
	КонецЦикла;
		
	ОсновныеСотрудникиФизическихЛиц = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(СписокФизическихЛиц, Истина, ВыборкаДокументы.Организация, ВыборкаДокументы.Дата);
	СписокСотрудников = ОсновныеСотрудникиФизическихЛиц.ВыгрузитьКолонку("Сотрудник");
	Если ЗначениеЗаполнено(СписокСотрудников) Тогда
		ДанныеФизЛица = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, "ФизическоеЛицо, ТабельныйНомер", ВыборкаДокументы.Ссылка.Дата);
	КонецЕсли;
	
	ВыборкаДокументы.Сбросить();
	ВыборкаСтроки.Сбросить();
	
	Пока ВыборкаДокументы.Следующий() Цикл
	
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		ОбластьЗаголовок.Параметры.Заполнить(ВыборкаДокументы);
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация, ВыборкаДокументы.Дата);	
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации  = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,",,КодЯзыкаПечать);
		ОбластьЗаголовок.Параметры.КодПоЕДРПОУ  = СведенияОбОрганизации.КодПоЕДРПОУ;
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументы.Номер, Ложь, Истина);
		ОбластьЗаголовок.Параметры.ДатаДокумента =  ВыборкаДокументы.Дата;
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабДокумент, Макет, ОбластьЗаголовок, ВыборкаДокументы.Ссылка);
		ТабДокумент.Вывести(ОбластьЗаголовок);
		
		// Вывод строк документа.
		ВыборкаСтроки = ВыборкаДокументы.ТаблицаТовары.Выбрать();
		Пока ВыборкаСтроки.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			ОбластьСтрока.Параметры.ТабельныйНомер	= "";
			Если ДанныеФизЛица <> Неопределено Тогда
				СтрокаТЗ = ДанныеФизЛица.Найти(ВыборкаСтроки.Сотрудник, "ФизическоеЛицо");
			КонецЕсли;
			
			Если СтрокаТЗ <> Неопределено Тогда
				ОбластьСтрока.Параметры.ТабельныйНомер	= СтрокаТЗ.ТабельныйНомер;
			КонецЕсли;
			
			Если ТоварКод = "Артикул" Тогда
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерАртикул;
			Иначе
				ОбластьСтрока.Параметры.НоменклатурныйНомер = ВыборкаСтроки.НоменклатурныйНомерКод;
			КонецЕсли;
			
			ОбластьСтрока.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаСтроки.НоменклатураНаименование,
				ВыборкаСтроки.Характеристика);
				
			ТабДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		// Вывод подвала.
		ТабДокумент.Вывести(ОбластьПодписи);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);

	КонецЦикла;
		
	Возврат ТабДокумент;

КонецФункции // ПечатьМШ7()

// Функция получает данные для формирования печатной формы М-11
//
Функция ПолучитьДанныеДляПечатнойФормыМ11(ПараметрыПечати, МассивДокументов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивДокументов, 
		МенеджерВременныхТаблиц,
		,
		Новый Структура("Руководитель, ГлавныйБухгалтер", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	   |	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)КАК Упаковка,
	|	Товары.Количество КАК Количество,
	|	Товары.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации,
	|	Товары.ФизическоеЛицо,
	|	Товары.ФизическоеЛицоПолучатель
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 		[1] - Выборка по шапкам документов
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.Дата КАК ДатаСоставления,
	|	Документы.Организация КАК Организация,
	|	Документы.Организация.Префикс КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	Документы.Подразделение КАК Подразделение,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК РуководительОтправителя,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителяОтправителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтерОтправителя,
	|	""Переміщення"" КАК КодВидаОперации,
	|	НЕОПРЕДЕЛЕНО КАК Заказ,
	|	НЕОПРЕДЕЛЕНО КАК УчетныйВидЦены,
	|	НЕОПРЕДЕЛЕНО КАК УчетныйВидЦеныВалютаЦены
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК Документы
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО Документы.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	Документы.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК КладовщикОтправитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщикаОтправителя,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	Товары.Номенклатура.Код КАК НоменклатурныйНомерКод,
	|	Товары.Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|	Товары.Характеристика.НаименованиеПолное КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО КАК Упаковка,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	Товары.Количество КАК Количество,
	|	0 КАК Сумма,
	|	0 КАК Цена,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка,
	|	Склад
	|";
	
		
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакет();
	// Пакет запросов:
	// 		[0] - Временная таблица по табличной части документа
   	// 		[1] - Выборка по шапкам документов
	// 		[2] - Выборка по табличным частям документов
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПакетаЗапросов[1],
		РезультатПакетаЗапросов[2]);
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МШ7с") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МШ7с", НСтр("ru='Ведомость МШ-7 (сдача)';uk='Відомість МШ-7 (здача)'"),
			ПечатьМШ7(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета, Истина), , ИмяМакета);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МШ7в") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МШ7в", НСтр("ru='Ведомость МШ-7 (выдача)';uk='Відомість МШ-7 (видача)'"),
			ПечатьМШ7(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ИмяМакета, Ложь), , ИмяМакета);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
