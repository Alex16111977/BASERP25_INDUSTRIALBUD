
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	КадровыйУчетФормы.ФормаКадровогоДокументаПриСозданииНаСервере(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		Если Не ЗначениеЗаполнено(Объект.Дата) Тогда
			Объект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		
		Объект.ДатаОстатков = Объект.Дата;
		
		// Очистим реквизит Сотрудник, если он уже принят.
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) И Параметры.ЗначениеКопирования.Проведен Тогда
			Объект.Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
			Объект.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли; 
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Организация") 
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда  
			Объект.Организация = Параметры.ЗначенияЗаполнения.Организация;
		КонецЕсли;
		Если Параметры.ЗначенияЗаполнения.Свойство("Сотрудник") 
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Сотрудник) Тогда  
			Объект.Сотрудник = Параметры.ЗначенияЗаполнения.Сотрудник;
		КонецЕсли;
		Если Параметры.ЗначенияЗаполнения.Свойство("ДатаОстатков")
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ДатаОстатков) Тогда  
			Объект.ДатаОстатков = Параметры.ЗначенияЗаполнения.ДатаОстатков;
		КонецЕсли;
		
		Если Параметры.ЗначенияЗаполнения.Свойство("АдресОстатковОтпускаВХранилище") 
			И Параметры.ЗначенияЗаполнения.АдресОстатковОтпускаВХранилище <> Неопределено Тогда  
			ЗагрузитьОстаткиОтпусков(Параметры.ЗначенияЗаполнения.АдресОстатковОтпускаВХранилище);
		КонецЕсли;
		
		Если Параметры.Свойство("ЕжегодныеОтпуска") 
			И Параметры.ЕжегодныеОтпуска <> Неопределено 
			И Параметры.ЕжегодныеОтпуска.Количество() > 0 Тогда
			Объект.ЕжегодныеОтпуска.Загрузить(Параметры.ЕжегодныеОтпуска.Выгрузить());
		КонецЕсли;
		
	КонецЕсли;

	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект) 
	
    // СтандартныеПодсистемы.УправлениеДоступом
    УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Проведен", ЭтаФорма.Объект.Проведен);
	ПараметрыОповещения.Вставить("ПомеченНаУдаление", ЭтаФорма.Объект.ПометкаУдаления);
	ПараметрыОповещения.Вставить("Результат", ЭтаФорма.Объект.Ссылка);
	ПараметрыОповещения.Вставить("Ответственный", ЭтаФорма.Объект.Ответственный);
	ПараметрыОповещения.Вставить("ДатаДокумента", ЭтаФорма.Объект.Дата);
	ПараметрыОповещения.Вставить("НомерДокумента", ЭтаФорма.Объект.Номер);
	ПараметрыОповещения.Вставить("Сотрудник", ЭтаФорма.Объект.Сотрудник);
	Оповестить("ПослеЗаписиОбъектаСвязанногоСПереводомКДругомуРаботодателю", ПараметрыОповещения, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененСтажФизическогоЛица" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ОбновитьСтажиСотрудника();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

   // СтандартныеПодсистемы.УправлениеДоступом
   УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
   // Конец СтандартныеПодсистемы.УправлениеДоступом
   
   ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОстатковПриИзменении(Элемент)
	ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыЕжегодныеОтпуска

&НаКлиенте
Процедура ЕжегодныеОтпускаПриАктивизацииСтроки(Элемент)
	ОстаткиОтпусковКлиент.ПриАктивизацииСтрокиЕжегодногоОтпуска(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ЕжегодныеОтпускаОписаниеСтажевыхПоказателей" Тогда
		КадровыйУчетРасширенныйКлиент.ОткрытьФормуРедактированияСтажейСотрудника(
			ЭтаФорма, Объект.Сотрудник, Объект.ДатаОстатков, Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидСтажаОтпуска(ТекущиеДанные.ВидЕжегодногоОтпуска))));
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Если Объект.ЕжегодныеОтпуска.Количество() = 1 Тогда
			Элемент.ТекущиеДанные.ВидЕжегодногоОтпуска = ПредопределенноеЗначение("Справочник.ВидыОтпусков.Основной");
			ОстаткиОтпусковКлиент.ПриИзмененииВидаЕжегодногоОтпуска(ЭтаФорма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	ОстаткиОтпусковКлиент.ПриОкончанииРедактированияЕжегодногоОтпуска(Элемент.ТекущиеДанные, НоваяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаПередУдалением(Элемент, Отказ)
	ОстаткиОтпусковКлиент.ПередУдалениемЕжегодногоОтпуска(ЭтаФорма, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаВидЕжегодногоОтпускаПриИзменении(Элемент)
	
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);
	
	ТекущиеДанные = Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДнейВГод = КоличествоДнейЕжегодногоОтпускаПоУмолчанию(ТекущиеДанные.ВидЕжегодногоОтпуска);
	Если КоличествоДнейВГод <> Неопределено Тогда
		ТекущиеДанные.КоличествоДнейВГод = КоличествоДнейВГод;
	КонецЕсли;
	
	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(Элементы.ЕжегодныеОтпуска.ТекущиеДанные);
	ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(Объект.Сотрудник, ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаКоличествоДнейВГодПриИзменении(Элемент)
	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(Элементы.ЕжегодныеОтпуска.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыОстаткиОтпусковПоРабочимГодам

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Если Объект.ОстаткиОтпусковПоРабочимГодам.Количество() = 1 Тогда
			Элемент.ТекущиеДанные.ВидЕжегодногоОтпуска = ПредопределенноеЗначение("Справочник.ВидыОтпусков.Основной");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамВидЕжегодногоОтпускаПриИзменении(Элемент)
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамРабочийГодНачалоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОстаткиОтпусковПоРабочимГодам.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.РабочийГодНачало) И НЕ ЗначениеЗаполнено(ТекущиеДанные.РабочийГодОкончание) Тогда
			ТекущиеДанные.РабочийГодОкончание = ДобавитьМесяц(ТекущиеДанные.РабочийГодНачало, 12) - 1;
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамОстатокПриИзменении(Элемент)
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиОтпусковПоРабочимГодамПослеУдаления(Элемент, Отказ)
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваЭлементовФормы(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ИнфоНадписьПредупреждение",
		"Видимость",
		Форма.ЕстьРанееВведенныеДанные);

КонецПроцедуры


&НаСервере
Процедура ПриПолученииДанныхНаСервере()

	ПараметрыСтажевыхЭлементов = ОстаткиОтпусков.ПараметрыДляДополнитьТабличнуюЧастьСтажевымиЭлементами();
	ПараметрыСтажевыхЭлементов.Форма = ЭтаФорма;
	ПараметрыСтажевыхЭлементов.ИмяТаблицы = "ЕжегодныеОтпуска";
	ПараметрыСтажевыхЭлементов.ТабличнаяЧастьВОбъекте = Истина;
	ПараметрыСтажевыхЭлементов.ЗаполнятьРеквизитыПоСотруднику = Истина;
	ПараметрыСтажевыхЭлементов.Сотрудник = Объект.Сотрудник;
	ПараметрыСтажевыхЭлементов.ДатаСреза = Объект.ДатаОстатков;
	ОстаткиОтпусков.ДополнитьТабличнуюЧастьСтажевымиЭлементами(ПараметрыСтажевыхЭлементов);
	
	ЗаполнитьВторичныеДанныеЕжегодныхОтпусков();
	СформироватьИнфоНадпись();
	УстановитьСвойстваЭлементовФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаКлиенте()
	ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаСервере();
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);
	СформироватьИнфоНадпись();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуПраваЗавершение(Результат, УдаляемаяСтрока) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ОстаткиОтпусковКлиент.ОчиститьОстаткиОтпусковПоВидуОтпуска(Объект.ОстаткиОтпусковПоРабочимГодам, УдаляемаяСтрока.ВидЕжегодногоОтпуска);
		Объект.ЕжегодныеОтпуска.Удалить(УдаляемаяСтрока);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаСервере()
	
	ГигКонтракт = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		
		ВремяРегистрации = ЗарплатаКадрыРасширенный.ВремяРегистрацииДокумента(Объект, Объект.ДатаОстатков);
		
		СтруктураОтбораКадровыхДанных = Новый Структура("ЛевоеЗначение, ВидСравнения, ПравоеЗначение", "Регистратор", "<>", Объект.Ссылка);
		Отбор = Новый Массив;
		Отбор.Добавить(СтруктураОтбораКадровыхДанных);
		
		ПоляОтбораПериодическихДанных = Новый Структура;
		ПоляОтбораПериодическихДанных.Вставить("КадроваяИсторияСотрудников", Отбор);
		
		Поля = "ВидДоговора";
		
		УстановитьПривилегированныйРежим(Истина);
		
		ДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Объект.Сотрудник, Поля, ВремяРегистрации, ПоляОтбораПериодическихДанных, Ложь);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ДанныеСотрудников.Количество() > 0 Тогда
			
			ТекущиеКадровыеДанныеСотрудника = ДанныеСотрудников[0];
			
			ГигКонтракт = ТекущиеКадровыеДанныеСотрудника.ВидДоговора= Перечисления.ВидыДоговоровССотрудниками.ГигКонтракт;
			
		КонецЕсли;	
	КонецЕсли;
	
	ТЧЕжегодныеОтпускаПредыдущие = ОстаткиОтпусков.ЕжегодныеОтпускаСотрудника(Объект.Сотрудник, Объект.ДатаОстатков, Объект.Ссылка, ГигКонтракт);
	
	Объект.ЕжегодныеОтпуска.Очистить();
	Для каждого ПравоНаОтпуск Из ТЧЕжегодныеОтпускаПредыдущие Цикл
		ЗаполнитьЗначенияСвойств(Объект.ЕжегодныеОтпуска.Добавить(), ПравоНаОтпуск);
	КонецЦикла; 
	
	ЗаполнитьВторичныеДанныеЕжегодныхОтпусков();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьОстаткиОтпусков(АдресВоВременномХранилище)

	ЗаполнитьЕжегоднымиОтпускамиСотрудникаНаСервере();
	Объект.ОстаткиОтпусковПоРабочимГодам.Загрузить(ПолучитьИзВременногоХранилища(АдресВоВременномХранилище));
	ОстаткиОтпусковКлиентСервер.ВнестиСводныйОстатокВТаблицуПрав(Объект);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеДанныеЕжегодныхОтпусков()

	ОстаткиОтпусков.ДополнитьСведениямиОПредыдущихЕжегодныхОтпускахСотрудника(Объект, "ДатаОстатков");
	ОстаткиОтпусков.УстановитьУсловноеОформлениеЕжегодныхОтпусков(ЭтаФорма);

КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоДнейЕжегодногоОтпускаПоУмолчанию(ВидЕжегодногоОтпуска)
	Возврат ОстаткиОтпусков.КоличествоДнейОтпускаВГодПоУмолчанию(ВидЕжегодногоОтпуска);
КонецФункции

&НаСервере
Процедура СформироватьИнфоНадпись()

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФактическиеОтпуска.Сотрудник,
		|	ИСТИНА КАК ЕстьФактическиеОтпуска
		|ПОМЕСТИТЬ ВТОтпуска
		|ИЗ
		|	РегистрНакопления.ФактическиеОтпуска КАК ФактическиеОтпуска
		|ГДЕ
		|	ФактическиеОтпуска.Период < &ДатаДокумента
		|	И ФактическиеОтпуска.Сотрудник = &Сотрудник
		|	И ФактическиеОтпуска.Регистратор <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НачальныеОстаткиОтпусков.Сотрудник,
		|	ИСТИНА КАК ЕстьОстатки
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрСведений.НачальныеОстаткиОтпусков КАК НачальныеОстаткиОтпусков
		|ГДЕ
		|	НачальныеОстаткиОтпусков.ДатаОстатка < &ДатаДокумента
		|	И НачальныеОстаткиОтпусков.Сотрудник = &Сотрудник
		|	И НачальныеОстаткиОтпусков.Регистратор <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоложенныеВидыЕжегодныхОтпусков.Сотрудник,
		|	ИСТИНА КАК ЕстьПрава
		|ПОМЕСТИТЬ ВТПрава
		|ИЗ
		|	РегистрСведений.ПоложенныеВидыЕжегодныхОтпусков КАК ПоложенныеВидыЕжегодныхОтпусков
		|ГДЕ
		|	ПоложенныеВидыЕжегодныхОтпусков.Период < &ДатаДокумента
		|	И ПоложенныеВидыЕжегодныхОтпусков.Сотрудник = &Сотрудник
		|	И ПоложенныеВидыЕжегодныхОтпусков.Регистратор <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОтпуска.Сотрудник,
		|	ВТОтпуска.ЕстьФактическиеОтпуска,
		|	ВТОстатки.ЕстьОстатки
		|ПОМЕСТИТЬ ВТОстаткиОтпуска
		|ИЗ
		|	ВТОстатки КАК ВТОстатки
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТОтпуска КАК ВТОтпуска
		|		ПО ВТОстатки.Сотрудник = ВТОтпуска.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОстаткиОтпуска.Сотрудник,
		|	ЕСТЬNULL(ВТОстаткиОтпуска.ЕстьФактическиеОтпуска, ЛОЖЬ) КАК ЕстьФактическиеОтпуска,
		|	ЕСТЬNULL(ВТОстаткиОтпуска.ЕстьОстатки, ЛОЖЬ) КАК ЕстьОстатки,
		|	ЕСТЬNULL(ВТПрава.ЕстьПрава, ЛОЖЬ) КАК ЕстьПрава
		|ИЗ
		|	ВТОстаткиОтпуска КАК ВТОстаткиОтпуска
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТПрава КАК ВТПрава
		|		ПО ВТОстаткиОтпуска.Сотрудник = ВТПрава.Сотрудник";
	
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.ДатаОстатков);
	Запрос.УстановитьПараметр("Сотрудник", Объект.Сотрудник);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТекстПредупреждения = "";
	ЕстьРанееВведенныеДанные = НЕ РезультатЗапроса.Пустой();
	
	Если ЕстьРанееВведенныеДанные Тогда
		ТекстПредупреждения = НСтр("ru='По сотруднику существуют ранее введенные';uk='По співробітнику існують раніше введені'") + " ";
		СборныйТекстПредупреждения = "";
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Если Выборка.ЕстьФактическиеОтпуска Тогда
			СборныйТекстПредупреждения = СборныйТекстПредупреждения + НСтр("ru='отпуска';uk='відпустки'");
		КонецЕсли;
		Если Выборка.ЕстьОстатки Тогда
			Если СборныйТекстПредупреждения = "" Тогда
				Суффикс = "";
			ИначеЕсли Выборка.ЕстьПрава Тогда
				 Суффикс = ", ";
			Иначе
				Суффикс = " и ";
			КонецЕсли;
			СборныйТекстПредупреждения = СборныйТекстПредупреждения + Суффикс + НСтр("ru='остатки отпусков';uk='залишки відпусток'");
		КонецЕсли;
		Если Выборка.ЕстьПрава Тогда
			СборныйТекстПредупреждения = СборныйТекстПредупреждения + ?(СборныйТекстПредупреждения = "", "", " и ") + НСтр("ru='права на ежегодные отпуска';uk='права на щорічні відпустки'");
		КонецЕсли;
		ТекстПредупреждения = ТекстПредупреждения + СборныйТекстПредупреждения + ". ";
		ТекстПредупреждения = ТекстПредупреждения + НСтр("ru='Эти данные не станут учитываться.';uk='Ці дані не будуть враховуватися.'");
	КонецЕсли;
	
	ИнфоНадписьПредупреждение = ТекстПредупреждения;
	УстановитьСвойстваЭлементовФормы(ЭтаФорма);
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаСервере
Процедура ОбновитьСтажиСотрудника()
	ОстаткиОтпусков.ЗаполнитьВторичныеРеквизитыСтажевыхОтпусков(Объект.ЕжегодныеОтпуска, Объект.Сотрудник, Объект.ДатаОстатков);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(Сотрудник, ИдентификаторСтроки)
	ТекущиеДанные = Объект.ЕжегодныеОтпуска.НайтиПоИдентификатору(ИдентификаторСтроки);
	ОстаткиОтпусков.ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(ТекущиеДанные, Сотрудник, Объект.ДатаОстатков);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидСтажаОтпуска(ВидОтпуска)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидОтпуска, "ВидСтажа");
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
