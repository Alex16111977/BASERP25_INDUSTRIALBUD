#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ВзаимозачетЗадолженностиЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовОперации;
	
	ДебиторскаяБезРазбиения=Ложь;
	Если ДополнительныеСвойства.Свойство("ДебиторскаяБезРазбиения")
		И ДополнительныеСвойства.ДебиторскаяБезРазбиения Тогда
		ДебиторскаяБезРазбиения = Истина;
	КонецЕсли;
	
	КредиторскаяБезРазбиения=Ложь;
	Если ДополнительныеСвойства.Свойство("КредиторскаяБезРазбиения")
		И ДополнительныеСвойства.КредиторскаяБезРазбиения Тогда
		КредиторскаяБезРазбиения = Истина;
	КонецЕсли;
	
	Документы.ВзаимозачетЗадолженности.ЗаполнитьИменаРеквизитовПоВидуОперации(
		ВидОперации,
		МассивВсехРеквизитов, 
		МассивРеквизитовОперации);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ОбщегоНазначенияУТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
		МассивВсехРеквизитов,
		МассивРеквизитовОперации,
		МассивНепроверяемыхРеквизитов);
	
	// Проверим соответствие сумм документа и табличной части.
	Если ДебиторскаяЗадолженность.Итог("СуммаРегл") <> КредиторскаяЗадолженность.Итог("СуммаРегл") Тогда
		ТекстСообщения = НСтр("ru='Сумма регламентированного учета по строкам в табличной части ""Задолженность дебитора"" должна равняться сумме регламентированного учета по строкам в табличной части ""Задолженность перед кредитором""';uk='Сума регламентованого обліку по рядках в табличній частині ""Заборгованість дебітора"" повинна дорівнювати сумі регламентованого обліку по рядках в табличній частині ""Заборгованість перед кредитором""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			, // Поле
			,
			Отказ);
	КонецЕсли;
	
	// Проверим соответствие сумм документа и табличной части.
	Если ДебиторскаяЗадолженность.Итог("СуммаУпр") <> КредиторскаяЗадолженность.Итог("СуммаУпр") Тогда
		ТекстСообщения = НСтр("ru='Сумма управленческого учета по строкам в табличной части ""Задолженность дебитора"" должна равняться сумме управленческого учета по строкам в табличной части ""Задолженность перед кредитором""';uk='Сума управлінського обліку по рядках в табличній частині ""Заборгованість дебітора"" повинна дорівнювати сумі управлінського обліку по рядках в табличній частині ""Заборгованість перед кредитором""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			, // Поле
			,
			Отказ);
	КонецЕсли;
	
	Если ДебиторскаяБезРазбиения И ДебиторскаяЗадолженность.Количество() > 0 И НЕ ЗначениеЗаполнено(ДебиторскаяЗадолженность[0].ВалютаВзаиморасчетов) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена Валюта взаиморасчетов Дебиторской задолженности';uk='Не заповнена Валюта взаєморозрахунків Дебіторської заборгованості'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"Элементы.ДебиторскаяЗадолженность.ТекущиеДанные.ВалютаВзаиморасчетов",
			,
			Отказ);
		МассивНепроверяемыхРеквизитов.Добавить("ДебиторскаяЗадолженность.ВалютаВзаиморасчетов");
	КонецЕсли;
	
	Если КредиторскаяБезРазбиения И КредиторскаяЗадолженность.Количество() > 0 И НЕ ЗначениеЗаполнено(КредиторскаяЗадолженность[0].ВалютаВзаиморасчетов) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена Валюта взаиморасчетов Кредиторской задолженности';uk='Не заповнена Валюта взаєморозрахунків Кредиторської заборгованості'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"Элементы.КредиторскаяЗадолженность.ТекущиеДанные.ВалютаВзаиморасчетов",
			,
			Отказ);
		МассивНепроверяемыхРеквизитов.Добавить("КредиторскаяЗадолженность.ВалютаВзаиморасчетов");
	КонецЕсли;
	
	Если НЕ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями 
		И НЕ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ОбъектРасчетовИнтеркампани");
	КонецЕсли;
	
	Если (ТипДебитора <> Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
		И ТипДебитора <> Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик
		И ТипКредитора <> Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
		И ТипКредитора <> Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
		ИЛИ КонтрагентДебитор = КонтрагентКредитор Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектРасчетовДебиторКредитор");
	КонецЕсли;
	
	Если ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
		Или ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДебиторскаяЗадолженность.Партнер");
	КонецЕсли;
	Если ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
		Или ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КредиторскаяЗадолженность.Партнер");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	СтруктураПредставлений = Документы.ВзаимозачетЗадолженности.ПредставлениеРекизитовПоВидуОперации(ВидОперации);
	ПроверитьСовпадениеЮрЛиц("КонтрагентДебитор", СтруктураПредставлений.КонтрагентДебитор, Отказ);
	Если МассивНепроверяемыхРеквизитов.Найти("КонтрагентКредитор") = Неопределено Тогда
		ПроверитьСовпадениеЮрЛиц("КонтрагентКредитор", СтруктураПредставлений.КонтрагентКредитор, Отказ);
	КонецЕсли;
	Если МассивНепроверяемыхРеквизитов.Найти("ОрганизацияКредитор") = Неопределено Тогда
		ПроверитьСовпадениеЮрЛиц("ОрганизацияКредитор", СтруктураПредставлений.ОрганизацияКредитор, Отказ);
	КонецЕсли;
	
	ВзаимозачетЗадолженностиЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если ЗначениеЗаполнено(ВидОперации)
		И ВидОперации <> Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами
		И ВидОперации <> Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками
		И ВидОперации <> Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер
		И ВидОперации <> Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Произвольный Тогда
		
		КонтрагентКредитор = КонтрагентДебитор;
		ТипКредитора = ТипДебитора;
		
	ИначеЕсли ЗначениеЗаполнено(ВидОперации)
		И ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер Тогда
		
		КонтрагентКредитор = КонтрагентДебитор;
		
	КонецЕсли;
	
	СформироватьСписокЗависимыхЗаказов();
	
	// Заполнение реквизитов в табличных частях.
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ЗаполнитьСуммыРеглУпр();
		
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(ДебиторскаяЗадолженность);
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(КредиторскаяЗадолженность);
		
		// В дебиторской задолженности могут быть задолженности клиентов и авансы поставщикам.
		ЭтоОрганизацияПоставщик = (ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
			ИЛИ ТипЗнч(КонтрагентДебитор) = Тип("СправочникСсылка.Организации");
		
		ЗаполнитьВладельцаОбъектаРасчета(ЭтотОбъект, ЭтоОрганизацияПоставщик, "ДебиторскаяЗадолженность");
		
		// В кредиторской задолженности могут быть задолженности поставщиков и авансы клиентов.
		ЭтоОрганизацияПоставщик = (ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
			ИЛИ ТипЗнч(КонтрагентКредитор) = Тип("СправочникСсылка.Организации");
		
		Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями
			Или ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
			ЗаполнитьВладельцаОбъектаРасчета(ЭтотОбъект, ЭтоОрганизацияПоставщик, "КредиторскаяЗадолженность", ОрганизацияКредитор);
		Иначе
			ЗаполнитьВладельцаОбъектаРасчета(ЭтотОбъект, ЭтоОрганизацияПоставщик, "КредиторскаяЗадолженность");
		КонецЕсли;
		
		ЗаполнитьТипРасчетовВТабличнойЧасти(ДебиторскаяЗадолженность, ТипДебитора);
		ЗаполнитьТипРасчетовВТабличнойЧасти(КредиторскаяЗадолженность, ТипКредитора);
		
		РасчетыМеждуОрганизациямиДебитор =
			ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
			Или ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик;
		
		РасчетыМеждуОрганизациямиКредитор =
			ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент
			Или ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик;
		
		Если Не РасчетыМеждуОрганизациямиДебитор И ЗначениеЗаполнено(КонтрагентДебитор) Тогда
			ПартнерДебитор = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(КонтрагентДебитор);
			ЗаполнитьПартнераВТабличнойЧасти(ДебиторскаяЗадолженность, ПартнерДебитор, РасчетыМеждуОрганизациямиДебитор);
			Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента
				ИЛИ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями
				ИЛИ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер
				ИЛИ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика
				ИЛИ ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
				ЗаполнитьПартнераВТабличнойЧасти(КредиторскаяЗадолженность, ПартнерДебитор, РасчетыМеждуОрганизациямиКредитор);
			КонецЕсли;
		КонецЕсли;
		
		Если Не РасчетыМеждуОрганизациямиКредитор И ЗначениеЗаполнено(КонтрагентКредитор) Тогда
			ПартнерКредитор = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(КонтрагентКредитор);
			ЗаполнитьПартнераВТабличнойЧасти(КредиторскаяЗадолженность, ПартнерКредитор, РасчетыМеждуОрганизациямиКредитор);
		КонецЕсли;
		
	КонецЕсли;
	
	ВзаимозачетЗадолженностиЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ВзаимозачетЗадолженностиЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ВзаимозачетЗадолженностиЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	РегистрыСведений.СостоянияЗаказовПоставщикам.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ВзаимозачетЗадолженностиЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	РегистрыСведений.СостоянияЗаказовПоставщикам.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или НЕ ДанныеЗаполнения.Свойство("Организация") Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь()
		И Пользователи.РолиДоступны("ДобавлениеИзменениеДокументовКорректировкиЗадолженностиЗачетОплаты") Тогда
		ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура СформироватьСписокЗависимыхЗаказов()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыРасчетов.Объект КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаСсылок
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|ГДЕ
		|	ОбъектыРасчетов.Ссылка В (&МассивОбъектовРасчетовДеб)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыРасчетов.Объект КАК Ссылка
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|ГДЕ
		|	ОбъектыРасчетов.Ссылка В (&МассивОбъектовРасчетовКред)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Ссылка
		|ИЗ
		|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Ссылка
		|ИЗ
		|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК ЗаказКлиента
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка В (ВЫБРАТЬ Ссылка ИЗ ТаблицаСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиента.Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка
		|ИЗ
		|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(ВЫБРАТЬ Ссылка ИЗ ТаблицаСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиента.Ссылка" 
		//++ НЕ УТКА
		+ "
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК ЗаказКлиента
		|ИЗ
		|	Документ.ЗаказДавальца КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка В (ВЫБРАТЬ Ссылка ИЗ ТаблицаСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиента.Ссылка"
		//-- НЕ УТКА
		+ "
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказПоставщику.Ссылка КАК ЗаказПоставщику
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.Ссылка В(ВЫБРАТЬ Ссылка ИЗ ТаблицаСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПоставщику.Ссылка
		|";
	
	Запрос.УстановитьПараметр("МассивОбъектовРасчетовДеб", ДебиторскаяЗадолженность.ВыгрузитьКолонку("ОбъектРасчетов"));
	Запрос.УстановитьПараметр("МассивОбъектовРасчетовКред", КредиторскаяЗадолженность.ВыгрузитьКолонку("ОбъектРасчетов"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	
	МассивЗависимыхЗаказов = Результат[1].Выгрузить().ВыгрузитьКолонку("ЗаказКлиента");
	ДополнительныеСвойства.Вставить("МассивЗависимыхЗаказовКлиентов", Новый ФиксированныйМассив(МассивЗависимыхЗаказов));
	
	МассивЗависимыхЗаказов = Результат[2].Выгрузить().ВыгрузитьКолонку("ЗаказПоставщику");
	ДополнительныеСвойства.Вставить("МассивЗависимыхЗаказовПоставщикам", Новый ФиксированныйМассив(МассивЗависимыхЗаказов));
	
КонецПроцедуры

Процедура ПроверитьСовпадениеЮрЛиц(Реквизит, Представление, Отказ)
	
	ЮрЛицо = ЭтотОбъект[Реквизит];
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ЮрЛицо) И (Организация = ЮрЛицо) Тогда
		Текст = НСтр("ru='Организация и %Контрагент% должны различаться.';uk='Організація і %Контрагент% повинні розрізнятися.'");
		Текст = СтрЗаменить(Текст,"%Контрагент%", Представление);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, ЭтотОбъект, Реквизит,, Отказ);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьТипРасчетовВТабличнойЧасти(ТабличнаяЧасть, ТипКонтрагента)
	
	Если ЗначениеЗаполнено(ТипКонтрагента) Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер Тогда
			
			Если ТабличнаяЧасть = ДебиторскаяЗадолженность Тогда
				ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
			Иначе
				ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
			КонецЕсли;
			
		Иначе
			
			Если ТипКонтрагента = Перечисления.ТипыУчастниковВзаимозачета.Клиент
				Или ТипКонтрагента = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент Тогда
				ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
			Иначе
				ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
			СтрокаТаблицы.ТипРасчетов = ТипРасчетов;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСуммыРеглУпр()
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	
	Для Каждого СтрокаЗадолженности Из ДебиторскаяЗадолженность Цикл
		Если СтрокаЗадолженности.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
			СтрокаЗадолженности.СуммаРегл = СтрокаЗадолженности.СуммаВзаиморасчетов;
		КонецЕсли;
		
		Если СтрокаЗадолженности.ВалютаВзаиморасчетов = ВалютаУправленческогоУчета Тогда
			СтрокаЗадолженности.СуммаУпр = СтрокаЗадолженности.СуммаВзаиморасчетов;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаЗадолженности Из КредиторскаяЗадолженность Цикл
		Если СтрокаЗадолженности.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
			СтрокаЗадолженности.СуммаРегл = СтрокаЗадолженности.СуммаВзаиморасчетов;
		КонецЕсли;
		
		Если СтрокаЗадолженности.ВалютаВзаиморасчетов = ВалютаУправленческогоУчета Тогда
			СтрокаЗадолженности.СуммаУпр = СтрокаЗадолженности.СуммаВзаиморасчетов;
		КонецЕсли;
	КонецЦикла;
	
	СуммаРегл = ДебиторскаяЗадолженность.Итог("СуммаРегл");
	СуммаУпр = ДебиторскаяЗадолженность.Итог("СуммаУпр");
	
КонецПроцедуры

// Процедура заполняет организацию владельца объекта расчета в табличной части.
//
// Параметры:
//  Объект - ДокументОбъект - платежный документ/заявка или взаимозачет задолженности
//  ЭтоОрганизацияПоставщик - Булево - Это расчеты с собственной организацией-поставщиком,
//                                     в этом случае берем организацию-получатель из объекта расчетов
//  ИмяТаблицы - Строка - Имя табличной части как в метаданных
//                        В табличной части объект расчетов должен содержаться в поле "Заказ"
//                        Необходимо наличие поля "Организация".
//  Организация - СправочникСсылка.Организации - Организация по умолчанию, если не найдена в объекте расчетов
//
Процедура ЗаполнитьВладельцаОбъектаРасчета(Объект, ЭтоОрганизацияПоставщик = Ложь, ИмяТаблицы = "РасшифровкаПлатежа", Организация = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки-1 КАК ИндексСтроки,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ИндексСтроки КАК ИндексСтроки,
	|	Таблица.ОбъектРасчетов.Организация КАК Организация
	|ИЗ
	|	вт КАК Таблица";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Таблица", Объект[ИмяТаблицы].Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект[ИмяТаблицы][Выборка.ИндексСтроки].Организация = Выборка.Организация;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПартнераВТабличнойЧасти(ОбъектТабличнаяЧасть, ПартнерСсылка, РасчетыМеждуОрганизациями)
	Для Каждого СтрокаТаблицы Из ОбъектТабличнаяЧасть Цикл
		Если РасчетыМеждуОрганизациями Тогда
			СтрокаТаблицы.Партнер = Неопределено;
		ИначеЕсли Не ЗначениеЗаполнено(СтрокаТаблицы.Партнер) Тогда
			СтрокаТаблицы.Партнер = ПартнерСсылка;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
