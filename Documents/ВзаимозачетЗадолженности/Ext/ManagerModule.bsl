#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	
	ВзаимозачетЗадолженностиЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, Регистры);
		
		ВзаимозачетЗадолженностиЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ВзаимозачетЗадолженностиЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Взаимозачет задолженности".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ВзаимозачетЗадолженности) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ВзаимозачетЗадолженности.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ВзаимозачетЗадолженности);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	ВзаимозачетЗадолженностиЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Процедура заполняет массивы реквизитов, зависимых от вида операции документа.
//
// Параметры:
//	ВидОперации - ПеречислениеСсылка.ВидыОперацийВзаимозачетаЗадолженности - Выбранный вид операции.
//	МассивВсехРеквизитов - Массив - Массив всех имен реквизитов, зависимых от вида операции.
//	МассивРеквизитовОперации - Массив - Массив имен реквизитов, используемых в выбранного вида операции.
//
Процедура ЗаполнитьИменаРеквизитовПоВидуОперации(ВидОперации, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ОрганизацияКредитор");
	МассивВсехРеквизитов.Добавить("КонтрагентКредитор");
	МассивВсехРеквизитов.Добавить("ТипДебитора");
	МассивВсехРеквизитов.Добавить("ТипКредитора");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента Тогда
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами Тогда
		МассивРеквизитовОперации.Добавить("КонтрагентКредитор");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями Тогда
		МассивРеквизитовОперации.Добавить("ОрганизацияКредитор");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика Тогда
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками Тогда
		МассивРеквизитовОперации.Добавить("КонтрагентКредитор");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
		МассивРеквизитовОперации.Добавить("ОрганизацияКредитор");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер Тогда
		
	Иначе //произвольный
		МассивРеквизитовОперации.Добавить("КонтрагентКредитор");
		МассивРеквизитовОперации.Добавить("ТипДебитора");
		МассивРеквизитовОперации.Добавить("ТипКредитора");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает представление реквизитов в заивисимости от вида операции взаимозачета.
//
// Параметры:
//	ВидОперации - ПеречислениеСсылка.ВидыОперацийВзаимозачетаЗадолженности - Выбранный вид операции.
//
// Возвращаемое значение:
// Структура - содержит структуру с ключами:
//     * Организация         - Строка - Представление организации.
//     * ОрганизацияКредитор - Строка - Представление организации-кредитора.
//     * КонтрагентДебитор   - Строка - Представление контрагента-дебитора.
//     * КонтрагентКредитор  - Строка - Представление контрагента-кредитора.
Функция ПредставлениеРекизитовПоВидуОперации(ВидОперации) Экспорт
	
	СтруктураПредставлений = Новый Структура;
	
	Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями Тогда
		
		СтруктураПредставлений.Вставить("Организация",         НСтр("ru='Организация, выполнившая отгрузку';uk='Організація, що виконала відвантаження'"));
		СтруктураПредставлений.Вставить("ОрганизацияКредитор", НСтр("ru='Организация, получившая аванс';uk='Організація, яка отримала аванс'"));
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
		
		СтруктураПредставлений.Вставить("Организация",         НСтр("ru='Организация, уплатившая аванс';uk='Організація, яка сплатила аванс'"));
		СтруктураПредставлений.Вставить("ОрганизацияКредитор", НСтр("ru='Организация, оформившая поступление';uk='Організація, яка оформила надходження'"));
		
	Иначе
		
		СтруктураПредставлений.Вставить("Организация", НСтр("ru='Организация';uk='Організація'"));
		СтруктураПредставлений.Вставить("ОрганизацияКредитор", НСтр("ru='Организация';uk='Організація'"));
		
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента
		Или ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями Тогда
		
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Клиент';uk='Клієнт'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Клиент';uk='Клієнт'"));
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами Тогда
		
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Клиент-дебитор';uk='Клієнт-дебітор'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Клиент-кредитор';uk='Клієнт-кредитор'"));
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика
		Или ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
		
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Поставщик';uk='Постачальник'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Поставщик';uk='Постачальник'"));
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками Тогда
		
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Поставщик-дебитор';uk='Постачальник-дебітор'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Поставщик-кредитор';uk='Постачальник-кредитор'"));
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер Тогда
		
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Контрагент';uk='Контрагент'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Контрагент';uk='Контрагент'"));
		
	Иначе
	
		СтруктураПредставлений.Вставить("КонтрагентДебитор", НСтр("ru='Дебитор';uk='Дебітор'"));
		СтруктураПредставлений.Вставить("КонтрагентКредитор", НСтр("ru='Кредитор';uk='Кредитор'"));
		
	КонецЕсли;
	
	Возврат СтруктураПредставлений;
	
КонецФункции

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ВзаимозачетЗадолженностиЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
// 		Строка - Текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ВзаимозачетЗадолженностиЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

//-- НЕ УТ

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	( ЗначениеРазрешено(Организация)
	|	ИЛИ ЗначениеРазрешено(ДебиторскаяЗадолженность.Организация)
	|	ИЛИ ЗначениеРазрешено(КредиторскаяЗадолженность.Организация)
	|	)И( ЗначениеРазрешено(ДебиторскаяЗадолженность.Партнер)
	|	ИЛИ ЗначениеРазрешено(КредиторскаяЗадолженность.Партнер)
	|	) 
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	( ЗначениеРазрешено(Организация)
	|	ИЛИ ЗначениеРазрешено(ДебиторскаяЗадолженность.Организация)
	|	ИЛИ ЗначениеРазрешено(КредиторскаяЗадолженность.Организация)
	|	)И( ЗначениеРазрешено(ДебиторскаяЗадолженность.Партнер)
	|	ИЛИ ЗначениеРазрешено(КредиторскаяЗадолженность.Партнер)
	|	)И ЗначениеРазрешено(ВидОперации)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	МассивТиповОрганизаций = Новый Массив;
	МассивТиповОрганизаций.Добавить(Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент);
	МассивТиповОрганизаций.Добавить(Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик);
	Запрос.УстановитьПараметр("ТипыОрганизаций", МассивТиповОрганизаций);
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями);
	Запрос.УстановитьПараметр("ОперацииВзаимозачетаМеждуОрганизациями", МассивОпераций);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата               КАК Период,
	|	ДанныеДокумента.Ссылка             КАК Ссылка,
	|	ДанныеДокумента.Номер              КАК Номер,
	|	ДанныеДокумента.Организация        КАК Организация,
	|	ДанныеДокумента.Подразделение      КАК Подразделение
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности  КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",                           Реквизиты.Период);
	Запрос.УстановитьПараметр("Номер",                            Реквизиты.Номер);
	Запрос.УстановитьПараметр("Организация",                      Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",       Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("Подразделение",                    Реквизиты.Подразделение);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос) Экспорт
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.ВалютаРегламентированногоУчета,
	                                                                         Неопределено, // ВалютаВзаиморасчетов
	                                                                         Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",           Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",          Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура ИнициализироватьВтДебиторскаяКредиторскаяЗадолженность(Запрос) Экспорт
	
	Если Запрос.Параметры.Свойство("ВтДебиторскаяКредиторскаяЗадолженностьИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросВременныхТаблиц = Новый Запрос;
	ЗапросВременныхТаблиц.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросВременныхТаблиц.Текст = ТекстЗапросаВтДебиторскаяЗадолженность() + ОбщегоНазначения.РазделительПакетаЗапросов()
								+ ТекстЗапросаВтКредиторскаяЗадолженность() + ОбщегоНазначения.РазделительПакетаЗапросов()
								+ ТекстЗапросаВтДанныеДокумента()  + ОбщегоНазначения.РазделительПакетаЗапросов()
								+ ТекстЗапросаВтРасчетыСКлиентамиПредварительная() + ОбщегоНазначения.РазделительПакетаЗапросов()
								+ ТекстЗапросаВтРасчетовПоПоставщикамиПредварительная();
	
	ЗапросВременныхТаблиц.УстановитьПараметр("Ссылка",                            Запрос.Параметры.Ссылка);
	ЗапросВременныхТаблиц.УстановитьПараметр("Организация",                       Запрос.Параметры.Организация);
	ЗапросВременныхТаблиц.УстановитьПараметр("Подразделение",                     Запрос.Параметры.Подразделение);
	ЗапросВременныхТаблиц.УстановитьПараметр("ВалютаРегламентированногоУчета",    Запрос.Параметры.ВалютаРегламентированногоУчета);
	
	ЗапросВременныхТаблиц.ВыполнитьПакет();
	
	Запрос.УстановитьПараметр("ВтДебиторскаяКредиторскаяЗадолженностьИнициализированы", Истина);

КонецПроцедуры

Процедура ИнициализироватьВременныеТаблицыПоРасчетам(Запрос, ВидЗадолженности = Неопределено)
	
	ЗапросВременнойТаблицы = Новый Запрос;
	ЗапросВременнойТаблицы.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	ЗапросВременнойТаблицы.УстановитьПараметр("Ссылка",                            Запрос.Параметры.Ссылка);
	ЗапросВременнойТаблицы.УстановитьПараметр("Организация",                       Запрос.Параметры.Организация);
	ЗапросВременнойТаблицы.УстановитьПараметр("Подразделение",                     Запрос.Параметры.Подразделение);
	ЗапросВременнойТаблицы.УстановитьПараметр("ВалютаРегламентированногоУчета",    Запрос.Параметры.ВалютаРегламентированногоУчета);
	
	ТекстЗапроса = "";
	Если (ВидЗадолженности = "Клиенты" ИЛИ ВидЗадолженности = Неопределено)
		И НЕ Запрос.Параметры.Свойство("ВтРасчетовПоКлиентамИнициализирована") Тогда
		Запрос.УстановитьПараметр("ВтРасчетовПоКлиентамИнициализирована", Истина);
		ТекстЗапроса = ТекстЗапросаВтРасчетыСКлиентами();
	КонецЕсли;
	
	Если (ВидЗадолженности = "Поставщики" ИЛИ ВидЗадолженности = Неопределено)
		И НЕ Запрос.Параметры.Свойство("ВтРасчетовПоПоставщикамИнициализирована") Тогда
		Запрос.УстановитьПараметр("ВтРасчетовПоПоставщикамИнициализирована", Истина);
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаВтРасчетовПоПоставщиками();
	КонецЕсли;
	
	Если ВидЗадолженности = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаВтРасчетыПоЗадолженности();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		ЗапросВременнойТаблицы.Текст = ТекстЗапроса;
		ЗапросВременнойТаблицы.ВыполнитьПакет();
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ТекстЗапросаДанных =
	"ВЫБРАТЬ
	|	""ДебиторскаяЗадолженность"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.ВалютаВзаиморасчетов КАК ВалютаДокумента,
	|	ТаблицаДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|	""КредиторскаяЗадолженность "" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.ВалютаВзаиморасчетов КАК ВалютаДокумента,
	|	ТаблицаДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|
	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность  КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);

КонецПроцедуры

Функция ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСКлиентами";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьВтДебиторскаяКредиторскаяЗадолженность(Запрос);
	ИнициализироватьВременныеТаблицыПоРасчетам(Запрос, "Клиенты");
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = ТекстЗапросаРасчетыСКлиентами();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьВтДебиторскаяКредиторскаяЗадолженность(Запрос);
	ИнициализироватьВременныеТаблицыПоРасчетам(Запрос, "Поставщики");
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = ТекстЗапросаРасчетыСПоставщиками();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует движения по регистру ДвиженияКонтрагентКонтрагент на временных таблиц дебиторской и кредиторской задолженности.
//
Процедура ПодготовитьТаблицуДвиженияКонтрагентКонтрагент(Запрос)
	
	Если Запрос.Параметры.Свойство("ДвиженияКонтрагентКонтрагентПодготовлены") Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьВтДебиторскаяКредиторскаяЗадолженность(Запрос);
	ИнициализироватьВременныеТаблицыПоРасчетам(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ЗапросЗадолженостей = Новый Запрос;
	ЗапросЗадолженостей.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросЗадолженостей.УстановитьПараметр("Ссылка",                            Запрос.Параметры.Ссылка);
	ЗапросЗадолженостей.УстановитьПараметр("Период",                            Запрос.Параметры.Период);
	ЗапросЗадолженостей.УстановитьПараметр("Подразделение",                     Запрос.Параметры.Подразделение);
	ЗапросЗадолженостей.УстановитьПараметр("ВалютаУправленческогоУчета",        Запрос.Параметры.ВалютаУправленческогоУчета);
	ЗапросЗадолженостей.УстановитьПараметр("ВалютаРегламентированногоУчета",    Запрос.Параметры.ВалютаРегламентированногоУчета);
	ЗапросЗадолженостей.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",    Запрос.Параметры.КоэффициентПересчетаВВалютуУпр);
	ЗапросЗадолженостей.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",   Запрос.Параметры.КоэффициентПересчетаВВалютуРегл);
	
	ЗапросЗадолженостей.Текст = 
	"ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
	|	Задолженность.Организация                              КАК Организация,
	|	&Подразделение                                         КАК Подразделение,
	|	
	|	Задолженность.Партнер                                  КАК Партнер,
	|	Задолженность.Контрагент                               КАК Контрагент,
	|	Задолженность.Договор                                  КАК Договор,
	|	Задолженность.НаправлениеДеятельности                  КАК НаправлениеДеятельности,
	|	Задолженность.ОбъектРасчетов                           КАК ОбъектРасчетов,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК КорПартнер,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорКонтрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК КорДоговор,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорОбъектРасчетов,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорТипСуммы,
	|	
	|	ВЫБОР КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА Задолженность.СуммаВзаиморасчетов
	|		КОГДА Задолженность.Сумма <> 0 
	|			ТОГДА ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ Задолженность.СуммаУпр
	|	КОНЕЦ                                                  КАК Сумма,
	|	0                                                      КАК СуммаБезНДС,
	|	ВЫБОР КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА Задолженность.СуммаВзаиморасчетов
	|		КОГДА Задолженность.Сумма <> 0
	|			ТОГДА ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ Задолженность.СуммаРегл
	|	КОНЕЦ                                                  КАК СуммаРегл,
	|	0                                                      КАК СуммаРеглБезНДС,
	|	
	|	Задолженность.ВалютаВзаиморасчетов                     КАК Валюта,
	|	Задолженность.СуммаВзаиморасчетов                      КАК СуммаВВалюте,
	|	0                                                      КАК СуммаБезНДСВВалюте,
	|	
	|	Задолженность.ВалютаВзаиморасчетов                     КАК ВалютаВзаиморасчетов,
	|	ВЫБОР КОГДА Задолженность.СуммаВзаиморасчетов = 0 
	|		ТОГДА 1
	|		ИНАЧЕ (ВЫБОР КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|					ТОГДА Задолженность.СуммаВзаиморасчетов 
	|					КОГДА Задолженность.Сумма <> 0
	|						ТОГДА ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|					ИНАЧЕ Задолженность.СуммаРегл
	|				КОНЕЦ) / Задолженность.СуммаВзаиморасчетов
	|	КОНЕЦ                                                  КАК КурсВалютыВзаиморасчетов,
	|	Задолженность.СуммаВзаиморасчетов                      КАК СуммаВВалютеВзаиморасчетов,
	|	0                                                      КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК КорВалютаВзаиморасчетов,
	|	0                                                      КАК КорСуммаВВалютеВзаиморасчетов,
	|	0                                                      КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	Задолженность.ОбъектРасчетов                           КАК ИсточникГФУРасчетов,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорИсточникГФУРасчетов
	|ИЗ
	|	ВтРасчетыПоЗадолженности КАК Задолженность
	|ГДЕ
	|	Задолженность.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Задолженность.Организация,
	|	Задолженность.Сумма УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
	|	Задолженность.Организация                              КАК Организация,
	|	&Подразделение                                         КАК Подразделение,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК Партнер,
	|	НЕОПРЕДЕЛЕНО                                           КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	НЕОПРЕДЕЛЕНО                                           КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                           КАК ОбъектРасчетов,
	|	
	|	Задолженность.Партнер                                  КАК КорПартнер,
	|	Задолженность.Контрагент                               КАК КорКонтрагент,
	|	Задолженность.Договор                                  КАК КорДоговор,
	|	Задолженность.НаправлениеДеятельности                  КАК КорНаправлениеДеятельности,
	|	Задолженность.ОбъектРасчетов                           КАК КорОбъектРасчетов,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                           КАК КорТипСуммы,
	|	
	|	ВЫБОР 
	|		КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета ТОГДА
	|			Задолженность.СуммаВзаиморасчетов
	|		КОГДА Задолженность.Сумма <> 0 ТОГДА
	|			ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			Задолженность.СуммаУпр
	|	КОНЕЦ                                                  КАК Сумма,
	|	0                                                      КАК СуммаБезНДС,
	|	ВЫБОР 
	|		КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета ТОГДА
	|			Задолженность.СуммаВзаиморасчетов
	|		КОГДА Задолженность.Сумма <> 0 ТОГДА
	|			ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			Задолженность.СуммаРегл
	|	КОНЕЦ                                                  КАК СуммаРегл,
	|	0                                                      КАК СуммаРеглБезНДС,
	|	
	|	Задолженность.ВалютаВзаиморасчетов                     КАК Валюта,
	|	Задолженность.СуммаВзаиморасчетов                      КАК СуммаВВалюте,
	|	0                                                      КАК СуммаБезНДСВВалюте,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК ВалютаВзаиморасчетов,
	|	0                                                      КАК СуммаВВалютеВзаиморасчетов,
	|	0                                                      КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|	Задолженность.ВалютаВзаиморасчетов                     КАК КорВалютаВзаиморасчетов,
	|	ВЫБОР КОГДА Задолженность.СуммаВзаиморасчетов = 0 
	|		ТОГДА 1
	|		ИНАЧЕ (ВЫБОР КОГДА Задолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|					ТОГДА Задолженность.СуммаВзаиморасчетов
	|					КОГДА Задолженность.Сумма <> 0 ТОГДА
	|						ВЫРАЗИТЬ(Задолженность.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|					ИНАЧЕ Задолженность.СуммаРегл
	|				КОНЕЦ) / Задолженность.СуммаВзаиморасчетов
	|	КОНЕЦ                                                  КАК КурсВалютыВзаиморасчетов,
	|	Задолженность.СуммаВзаиморасчетов                      КАК КорСуммаВВалютеВзаиморасчетов,
	|	0                                                      КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	НЕОПРЕДЕЛЕНО                                           КАК ИсточникГФУРасчетов,
	|	Задолженность.ОбъектРасчетов                           КАК КорИсточникГФУРасчетов
	|ИЗ
	|	ВтРасчетыПоЗадолженности КАК Задолженность
	|ГДЕ
	|	Задолженность.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Задолженность.Организация,
	|	Задолженность.Сумма УБЫВ";
	
	Результат = ЗапросЗадолженостей.ВыполнитьПакет();
	
	ТаблицаДвиженияКонтрагентКонтрагент = Неопределено;
	ВзаиморасчетыСервер.ПровестиВзаимозачет(
		Результат[0].Выгрузить(),
		Результат[1].Выгрузить(),
		ТаблицаДвиженияКонтрагентКонтрагент);
	
	ЗапросЗадолженостей.УстановитьПараметр("ТаблицаДвиженияКонтрагентКонтрагент", ТаблицаДвиженияКонтрагентКонтрагент);
	ЗапросЗадолженостей.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвиженияКонтрагентКонтрагент.Период,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Регистратор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.НомерСтроки,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Активность,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ХозяйственнаяОперация,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Организация,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Подразделение,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Партнер,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Контрагент,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Договор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.НаправлениеДеятельности,
	|	ТаблицаДвиженияКонтрагентКонтрагент.ОбъектРасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорПартнер,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорКонтрагент,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорДоговор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорНаправлениеДеятельности,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорОбъектРасчетов,
	
	|	ТаблицаДвиженияКонтрагентКонтрагент.ТипСуммы,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорТипСуммы,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Сумма,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДС,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаРегл,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаРеглБезНДС,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Валюта,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаВВалюте,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДСВВалюте,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ВалютаВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаВВалютеВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорВалютаВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорСуммаВВалютеВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорСуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ИсточникГФУРасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорИсточникГФУРасчетов
	|ПОМЕСТИТЬ ВтДвиженияКонтрагентКонтрагент
	|ИЗ
	|	&ТаблицаДвиженияКонтрагентКонтрагент КАК ТаблицаДвиженияКонтрагентКонтрагент";
	
	ЗапросЗадолженостей.ВыполнитьПакет();
	
	Запрос.УстановитьПараметр("ДвиженияКонтрагентКонтрагентПодготовлены", Истина);

КонецПроцедуры

Функция ТекстЗапросаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияКонтрагентКонтрагент";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ПодготовитьТаблицуДвиженияКонтрагентКонтрагент(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДвиженияКонтрагентКонтрагент.Период,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Регистратор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.НомерСтроки,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Активность,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ХозяйственнаяОперация,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Организация,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Подразделение,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Партнер,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Контрагент,
	|	ТаблицаДвиженияКонтрагентКонтрагент.Договор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.НаправлениеДеятельности,
	|	ТаблицаДвиженияКонтрагентКонтрагент.ОбъектРасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорПартнер,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорКонтрагент,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорДоговор,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорНаправлениеДеятельности,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорОбъектРасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ТипСуммы,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорТипСуммы,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Сумма,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДС,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаРегл,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаРеглБезНДС,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.Валюта,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаВВалюте,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДСВВалюте,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ВалютаВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаВВалютеВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорВалютаВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорСуммаВВалютеВзаиморасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорСуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаДвиженияКонтрагентКонтрагент.ИсточникГФУРасчетов,
	|	ТаблицаДвиженияКонтрагентКонтрагент.КорИсточникГФУРасчетов
	|ИЗ
	|	ВтДвиженияКонтрагентКонтрагент КАК ТаблицаДвиженияКонтрагентКонтрагент";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДебиторскаяЗадолженность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ТипРасчетов                     КАК ТипРасчетов,
	|	ДанныеДокумента.ОбъектРасчетов                  КАК ОбъектРасчетов,
	|
	|	ОбъектыРасчетов.Партнер                         КАК Партнер,
	|	ОбъектыРасчетов.Организация                     КАК Организация,
	|	ОбъектыРасчетов.Договор                         КАК Договор,
	|	ОбъектыРасчетов.Контрагент                      КАК Контрагент,
	|	ОбъектыРасчетов.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|
	|	ДанныеДокумента.Сумма                           КАК Сумма,
	|	ДанныеДокумента.СуммаВзаиморасчетов             КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.СуммаРегл                       КАК СуммаРегл,
	|	ДанныеДокумента.СуммаУпр                        КАК СуммаУпр,
	|	ОбъектыРасчетов.Подразделение                   КАК Подразделение
	|
	|ПОМЕСТИТЬ ВтДебиторскаяЗадолженность
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.Ссылка = ДанныеДокумента.ОбъектРасчетов
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтКредиторскаяЗадолженность()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ТипРасчетов                     КАК ТипРасчетов,
	|	ДанныеДокумента.ОбъектРасчетов                  КАК ОбъектРасчетов,
	|
	|	ОбъектыРасчетов.Партнер                         КАК Партнер,
	|	ОбъектыРасчетов.Организация                     КАК Организация,
	|	ОбъектыРасчетов.Договор                         КАК Договор,
	|	ОбъектыРасчетов.Контрагент                      КАК Контрагент,
	|	ОбъектыРасчетов.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|
	|	ДанныеДокумента.Сумма                           КАК Сумма,
	|	ДанныеДокумента.СуммаВзаиморасчетов             КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.СуммаРегл                       КАК СуммаРегл,
	|	ДанныеДокумента.СуммаУпр                        КАК СуммаУпр,
	|
	|	ОбъектыРасчетов.Подразделение                   КАК Подразделение
	|
	|ПОМЕСТИТЬ ВтКредиторскаяЗадолженность
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.Ссылка = ДанныеДокумента.ОбъектРасчетов
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеДокумента()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                 КАК Ссылка,
	|	ДанныеДокумента.Проведен                                               КАК Проведен,
	|	ДанныеДокумента.Дата                                                   КАК Дата,
	|	ДанныеДокумента.Номер                                                  КАК Номер,
	|	ДанныеДокумента.ВидОперации                                            КАК ВидОперации,
	|	ДанныеДокумента.СуммаРегл                                              КАК СуммаРегл,
	|	ДанныеДокумента.СуммаУпр                                               КАК СуммаУпр,
	|
	|	ДанныеДокумента.Организация                                            КАК Организация,
	|	ДанныеДокумента.ОрганизацияКредитор                                    КАК ОрганизацияКредитор,
	|	ДанныеДокумента.ОбъектРасчетовИнтеркампани                             КАК ОбъектРасчетовИнтеркампани,
	|
	|	ДанныеДокумента.ТипДебитора                                            КАК ТипДебитора,
	|	ДанныеДокумента.ТипКредитора                                           КАК ТипКредитора,
	|	ДанныеДокумента.КонтрагентДебитор                                      КАК КонтрагентДебитор,
	|	ДанныеДокумента.КонтрагентКредитор                                     КАК КонтрагентКредитор,
	|	ДанныеДокумента.ОбъектРасчетовДебиторКредитор                          КАК ОбъектРасчетовДтКт,
	|
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.КонтрагентДебитор) = ТИП(Справочник.Организации)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                  КАК ДебиторОрганизация,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.КонтрагентКредитор) = ТИП(Справочник.Организации)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                  КАК КредиторОрганизация
	|
	|ПОМЕСТИТЬ ВтДанныеДокумента
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//1. Организация - КонтрагентДт - ОбъектРасчетовДт - Расход - СуммаДт - РасчетыСКлиентом
//2. Организация - КонтрагентКт - ОбъектРасчетовКт - Расход - -СуммаКт - РасчетыСКлиентом
//3. КонтрагентДт(Орг) - КонтрагентКт - ОбъектРасчетовДтКт - Расход - СуммаКт - РасчетыСКлиентом
//4. КонтрагентКт(Орг) - КонтрагентДт(Не орг) - ОбъектРасчетовДтКт - Приход - СуммаДт - РасчетыСПоставщиком
//
Функция ТекстЗапросаВтРасчетыСКлиентамиПредварительная()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.ВидДвижения КАК ВидДвижения,
	|	Задолженность.Организация КАК Организация,
	|	Задолженность.Партнер КАК Партнер,
	|	Задолженность.Контрагент КАК Контрагент,
	|	Задолженность.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Задолженность.Договор КАК Договор,
	|	Задолженность.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Задолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	СУММА(Задолженность.Сумма) КАК Сумма,
	|	СУММА(Задолженность.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(Задолженность.СуммаРегл) КАК СуммаРегл,
	|	СУММА(Задолженность.СуммаУпр) КАК СуммаУпр
	|
	|ПОМЕСТИТЬ ВтРасчетовПоКлиентамПредварительная
	|ИЗ
	// Расчеты по основной организации
	|	(ВЫБРАТЬ // дебиторская задолженность дебитора-клиента перед организацией считается оплаченной
	|		ДебиторскаяЗадолженность.Ссылка                   КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)            КАК ВидДвижения,
	|		ДебиторскаяЗадолженность.Организация              КАК Организация,
	|		ДебиторскаяЗадолженность.Партнер                  КАК Партнер,
	|		ДебиторскаяЗадолженность.Контрагент               КАК Контрагент,
	|		ДебиторскаяЗадолженность.Договор                  КАК Договор,
	|		ДебиторскаяЗадолженность.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|		ДебиторскаяЗадолженность.ОбъектРасчетов           КАК ОбъектРасчетов,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов     КАК ВалютаВзаиморасчетов,
	|
	|		ДебиторскаяЗадолженность.Сумма                    КАК Сумма,
	|		ДебиторскаяЗадолженность.СуммаВзаиморасчетов      КАК СуммаВзаиморасчетов,
	|		ДебиторскаяЗадолженность.СуммаРегл                КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.СуммаУпр                 КАК СуммаУпр
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // кредиторская задолженность организации перед кредитором-клиентом сторнируется
	|		КредиторскаяЗадолженность.Ссылка                    КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|		КредиторскаяЗадолженность.Организация               КАК Организация,
	|		КредиторскаяЗадолженность.Партнер                   КАК Партнер,
	|		КредиторскаяЗадолженность.Контрагент                КАК Контрагент,
	|		КредиторскаяЗадолженность.Договор                   КАК Договор,
	|		КредиторскаяЗадолженность.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|
	|		КредиторскаяЗадолженность.ОбъектРасчетов            КАК ОбъектРасчетов,
	|		КредиторскаяЗадолженность.ВалютаВзаиморасчетов      КАК ВалютаВзаиморасчетов,
	|
	|		-КредиторскаяЗадолженность.Сумма                    КАК Сумма,
	|		-КредиторскаяЗадолженность.СуммаВзаиморасчетов      КАК СуммаВзаиморасчетов,
	|		-КредиторскаяЗадолженность.СуммаРегл                КАК СуммаРегл,
	|		-КредиторскаяЗадолженность.СуммаУпр                 КАК СуммаУпр
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// 3.
	|	ВЫБРАТЬ // перенос кредиторки по клиенту у дебитора-организации на кредиторку поставщику перед кредитором-клиентом
	|		ДанныеДокумента.Ссылка                                               КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                               КАК ВидДвижения,
	|		ДанныеДокумента.КонтрагентДебитор                                    КАК Организация,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Партнер                           КАК Партнер,
	|		ДанныеДокумента.КонтрагентКредитор                                   КАК Контрагент,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Договор                           КАК Договор,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|
	|		ДанныеДокумента.ОбъектРасчетовДтКт                                   КАК ОбъектРасчетов,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов              КАК ВалютаВзаиморасчетов,
	|
	|		0                                                                    КАК Сумма,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|				ТОГДА КредиторскаяЗадолженность.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ КредиторскаяЗадолженность.СуммаВзаиморасчетов
	|		КОНЕЦ                                                                КАК СуммаВзаиморасчетов,
	|		КредиторскаяЗадолженность.СуммаРегл                                  КАК СуммаРегл,
	|		КредиторскаяЗадолженность.СуммаУпр                                   КАК СуммаУпр
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	ГДЕ
	|		ДанныеДокумента.ДебиторОрганизация И ДанныеДокумента.КонтрагентКредитор <> ДанныеДокумента.КонтрагентДебитор
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	//4.
	|	ВЫБРАТЬ // добавление дебиторской задолженности по клиентам у организации-кредитора
	|		ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                     КАК ВидДвижения,
	|		ДанныеДокумента.КонтрагентКредитор                         КАК Организация,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Партнер                 КАК Партнер,
	|		ДанныеДокумента.КонтрагентДебитор                          КАК Контрагент,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Договор                 КАК Договор,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ДанныеДокумента.ОбъектРасчетовДтКт                         КАК ОбъектРасчетов,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|		0                                                          КАК Сумма,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|				ТОГДА ДебиторскаяЗадолженность.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ ДебиторскаяЗадолженность.СуммаВзаиморасчетов
	|		КОНЕЦ                                                      КАК СуммаВзаиморасчетов,
	|		ДебиторскаяЗадолженность.СуммаРегл                         КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.СуммаУпр                          КАК СуммаУпр
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	ГДЕ
	|		ДанныеДокумента.КредиторОрганизация
	|		И НЕ ДанныеДокумента.ДебиторОрганизация 
	|		И ДанныеДокумента.КонтрагентКредитор <> ДанныеДокумента.КонтрагентДебитор
	// клиент контр клиент орг
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|) КАК Задолженность
	|СГРУППИРОВАТЬ ПО
	|	Задолженность.Ссылка,
	|	Задолженность.ВидДвижения,
	|	Задолженность.Организация,
	|	Задолженность.Партнер,
	|	Задолженность.Контрагент,
	|	Задолженность.ОбъектРасчетов,
	|	Задолженность.ВалютаВзаиморасчетов,
	|	Задолженность.Договор,
	|	Задолженность.НаправлениеДеятельности
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 1. Организация - КонтрагентДт - ОбъектРасчетовДт - Приход - -СуммаДт - РасчетыСПоставщиком 
// 2. Организация - КонтрагентКт - ОбъектРасчетовКт - Приход - СуммаКт - РасчетыСПоставщиком
// 3. КонтрагентДт(Орг) - КонтрагентКт - ОбъектРасчетовДтКт - Приход - -СуммаКт - РасчетыСПоставщиком
// 4. КонтрагентКт(Орг) - КонтрагентДт(Не Орг) - ОбъектРасчетовДтКт - Приход - СуммаДт - РасчетыСПоставщиком
// 5. Организация - ОрганизацияКт - ОбъектРасчетовИнтеркампани - Приход - Сумма
//
Функция ТекстЗапросаВтРасчетовПоПоставщикамиПредварительная()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Задолженность.Ссылка                     КАК Ссылка,
	|	Задолженность.ВидДвижения                КАК ВидДвижения,
	|	Задолженность.Организация                КАК Организация,
	|	Задолженность.Партнер                    КАК Партнер,
	|	Задолженность.Контрагент                 КАК Контрагент,
	|	Задолженность.Договор                    КАК Договор,
	|	Задолженность.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|
	|	Задолженность.ОбъектРасчетов             КАК ОбъектРасчетов,
	|	Задолженность.ВалютаВзаиморасчетов       КАК ВалютаВзаиморасчетов,
	|
	|	СУММА(Задолженность.Сумма)               КАК Сумма,
	|	СУММА(Задолженность.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(Задолженность.СуммаРегл)           КАК СуммаРегл,
	|	СУММА(Задолженность.СуммаУпр)            КАК СуммаУпр
	|
	|ПОМЕСТИТЬ ВтРасчетовПоПоставщикамПредварительная
	|ИЗ
	// 1. Расчеты по основной организации
	|	(ВЫБРАТЬ // дебиторская задолженность дебитора-поставщика перед организацией сторнируется
	|		ДебиторскаяЗадолженность.Ссылка                  КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|		ДебиторскаяЗадолженность.Организация             КАК Организация,
	|		ДебиторскаяЗадолженность.Партнер                 КАК Партнер,
	|		ДебиторскаяЗадолженность.Контрагент              КАК Контрагент,
	|		ДебиторскаяЗадолженность.Договор                 КАК Договор,
	|		ДебиторскаяЗадолженность.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ДебиторскаяЗадолженность.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|		-ДебиторскаяЗадолженность.Сумма                  КАК Сумма,
	|		-ДебиторскаяЗадолженность.СуммаВзаиморасчетов    КАК СуммаВзаиморасчетов,
	|		-ДебиторскаяЗадолженность.СуммаРегл              КАК СуммаРегл,
	|		-ДебиторскаяЗадолженность.СуммаУпр               КАК СуммаУпр
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|	ГДЕ
	|		ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// 2.
	|	ВЫБРАТЬ // кредиторская задолженность организации перед кредитором-поставщиком считается оплаченной
	|		КредиторскаяЗадолженность.Ссылка                  КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|		КредиторскаяЗадолженность.Организация             КАК Организация,
	|		КредиторскаяЗадолженность.Партнер                 КАК Партнер,
	|		КредиторскаяЗадолженность.Контрагент              КАК Контрагент,
	|		КредиторскаяЗадолженность.Договор                 КАК Договор,
	|		КредиторскаяЗадолженность.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		КредиторскаяЗадолженность.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		КредиторскаяЗадолженность.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|		КредиторскаяЗадолженность.Сумма                   КАК Сумма,
	|		КредиторскаяЗадолженность.СуммаВзаиморасчетов     КАК СуммаВзаиморасчетов,
	|		КредиторскаяЗадолженность.СуммаРегл               КАК СуммаРегл,
	|		КредиторскаяЗадолженность.СуммаУпр                КАК СуммаУпр
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|	ГДЕ
	|		КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// 3.(РасчетыСПоставщиком, Дебитор-Организация)
	|	ВЫБРАТЬ // перенос кредиторки по поставщику у дебитора-организации на кредиторку по поставщику перед кредитором-поставщиком
	|		ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                     КАК ВидДвижения,
	|		ДанныеДокумента.КонтрагентДебитор                          КАК Организация,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Партнер                 КАК Партнер,
	|		ДанныеДокумента.КонтрагентКредитор                         КАК Контрагент,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Договор                 КАК Договор,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ДанныеДокумента.ОбъектРасчетовДтКт                         КАК ОбъектРасчетов,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|		0                                                          КАК Сумма,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|				ТОГДА -КредиторскаяЗадолженность.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ -КредиторскаяЗадолженность.СуммаВзаиморасчетов
	|		КОНЕЦ                                                      КАК СуммаВзаиморасчетов,
	|		-КредиторскаяЗадолженность.СуммаРегл                       КАК СуммаРегл,
	|		-КредиторскаяЗадолженность.СуммаУпр                        КАК СуммаУпр
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК КредиторскаяЗадолженность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	ГДЕ
	|		ДанныеДокумента.ДебиторОрганизация И ДанныеДокумента.КонтрагентКредитор <> ДанныеДокумента.КонтрагентДебитор
	|		И КредиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// 4.(РасчетыСПоставщиком, Кредитор-Организация)
	|	ВЫБРАТЬ // перенос кредиторки по поставщику у дебитора-организации на кредиторку по поставщику перед кредитором-поставщиком
	|		ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                     КАК ВидДвижения,
	|		ДанныеДокумента.КонтрагентКредитор                         КАК Организация,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Партнер                 КАК Партнер,
	|		ДанныеДокумента.КонтрагентДебитор                          КАК Контрагент,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.Договор                 КАК Договор,
	|		ДанныеДокумента.ОбъектРасчетовДтКт.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ДанныеДокумента.ОбъектРасчетовДтКт                         КАК ОбъектРасчетов,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов              КАК ВалютаВзаиморасчетов,
	|
	|		ДебиторскаяЗадолженность.Сумма                             КАК Сумма,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.ОбъектРасчетовДтКт.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|				ТОГДА ДебиторскаяЗадолженность.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ ДебиторскаяЗадолженность.СуммаВзаиморасчетов
	|		КОНЕЦ                                                      КАК СуммаВзаиморасчетов,
	|		ДебиторскаяЗадолженность.СуммаРегл                         КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.СуммаУпр                          КАК СуммаУпр
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	ГДЕ
	|		ДанныеДокумента.КредиторОрганизация
	|		И НЕ ДанныеДокумента.ДебиторОрганизация
	|		И ДанныеДокумента.КонтрагентКредитор <> ДанныеДокумента.КонтрагентДебитор
	|		И ДебиторскаяЗадолженность.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// 5. (МеждуОрганизациями)
	|	ВЫБРАТЬ // перенос дебиторки по клиентам у организации на организацию-кредитора
	|		ДанныеДокумента.Ссылка                                             КАК Ссылка,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                             КАК ВидДвижения,
	|		ДанныеДокумента.Организация                                        КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)                      КАК Партнер,
	|		ДанныеДокумента.ОрганизацияКредитор                                КАК Контрагент,
	|		ДанныеДокумента.ОбъектРасчетовИнтеркампани.Договор                 КАК Договор,
	|		ДанныеДокумента.ОбъектРасчетовИнтеркампани.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ДанныеДокумента.ОбъектРасчетовИнтеркампани                         КАК ОбъектРасчетов,
	|		ДебиторскаяЗадолженность.ВалютаВзаиморасчетов                      КАК ВалютаВзаиморасчетов,
	|
	|		0                                                                  КАК Сумма,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.ОбъектРасчетовИнтеркампани.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|				ТОГДА ДебиторскаяЗадолженность.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ ДебиторскаяЗадолженность.СуммаВзаиморасчетов
	|		КОНЕЦ                                                              КАК СуммаВзаиморасчетов,
	|		ДебиторскаяЗадолженность.СуммаРегл                                 КАК СуммаРегл,
	|		ДебиторскаяЗадолженность.СуммаУпр                                  КАК СуммаУпр
	|	ИЗ 
	|		ВтДебиторскаяЗадолженность КАК ДебиторскаяЗадолженность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	ГДЕ
	|		ДанныеДокумента.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями),
	|										ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями))
	|
	|) КАК Задолженность
	|СГРУППИРОВАТЬ ПО
	|	Задолженность.Ссылка,
	|	Задолженность.ВидДвижения,
	|	Задолженность.Организация,
	|	Задолженность.Партнер,
	|	Задолженность.Контрагент,
	|	Задолженность.ОбъектРасчетов,
	|	Задолженность.Договор,
	|	Задолженность.ВалютаВзаиморасчетов,
	|	Задолженность.НаправлениеДеятельности
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Объединяем предварительные расчеты с зеркальными по интеркампани
Функция ТекстЗапросаВтРасчетыСКлиентами()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗадолженностьПредварительная.Ссылка                  КАК Ссылка,
	|	ЗадолженностьПредварительная.ВидДвижения             КАК ВидДвижения,
	|	ЗадолженностьПредварительная.Организация             КАК Организация,
	|	ЗадолженностьПредварительная.Партнер                 КАК Партнер,
	|	ЗадолженностьПредварительная.Контрагент              КАК Контрагент,
	|	ЗадолженностьПредварительная.ОбъектРасчетов          КАК ОбъектРасчетов,
	|	ЗадолженностьПредварительная.Договор                 КАК Договор,
	|	ЗадолженностьПредварительная.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗадолженностьПредварительная.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|	ЗадолженностьПредварительная.Сумма                   КАК Сумма,
	|	ЗадолженностьПредварительная.СуммаВзаиморасчетов     КАК СуммаВзаиморасчетов,
	|	ЗадолженностьПредварительная.СуммаРегл               КАК СуммаРегл,
	|	ЗадолженностьПредварительная.СуммаУпр                КАК СуммаУпр
	|
	|ПОМЕСТИТЬ ВтРасчетовПоКлиентам
	|ИЗ
	|	ВтРасчетовПоКлиентамПредварительная КАК ЗадолженностьПредварительная
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадолженностьПоставщикам.Ссылка                                          КАК Ссылка,
	|	ВЫБОР КОГДА ЗадолженностьПоставщикам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ                                                                    КАК ВидДвижения,
	|	ЗадолженностьПоставщикам.Контрагент                                      КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)                            КАК Партнер,
	|	ЗадолженностьПоставщикам.Организация                                     КАК Контрагент,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.Ссылка, Неопределено)                  КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.Договор, Неопределено)                 КАК Договор,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.НаправлениеДеятельности, Неопределено) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.ВалютаВзаиморасчетов, Неопределено)    КАК ВалютаВзаиморасчетов,
	|
	|	ЗадолженностьПоставщикам.Сумма                                           КАК Сумма,
	|	ВЫБОР
	|			КОГДА ЕСТЬNULL(ОбъектРасчетовЗеркальный.ВалютаВзаиморасчетов, Неопределено) = &ВалютаРегламентированногоУчета
	|				ТОГДА ЗадолженностьПоставщикам.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ ЗадолженностьПоставщикам.СуммаВзаиморасчетов
	|	КОНЕЦ                                                                    КАК СуммаВзаиморасчетов,
	|	ЗадолженностьПоставщикам.СуммаРегл                                       КАК СуммаРегл,
	|	ЗадолженностьПоставщикам.СуммаУпр                                        КАК СуммаУпр
	|
	|ИЗ
	|	ВтРасчетовПоПоставщикамПредварительная КАК ЗадолженностьПоставщикам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.Ссылка = ЗадолженностьПоставщикам.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектРасчетовЗеркальный
	|			ПО ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств)
	|						ТОГДА ТИПЗНАЧЕНИЯ(ОбъектРасчетовЗеркальный.Объект) = ТИП(Документ.СписаниеБезналичныхДенежныхСредств)
	|							И ОбъектРасчетовЗеркальный.Сумма = ОбъектыРасчетов.Сумма
	|							И ОбъектРасчетовЗеркальный.Дата = ОбъектыРасчетов.ДатаВходящегоДокумента
	|							И ОбъектРасчетовЗеркальный.Номер = ОбъектыРасчетов.НомерВходящегоДокумента
	|							И ОбъектРасчетовЗеркальный.Валюта = ОбъектыРасчетов.Валюта
	|					КОГДА ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.СписаниеБезналичныхДенежныхСредств)
	|						ТОГДА ТИПЗНАЧЕНИЯ(ОбъектРасчетовЗеркальный.Объект) = ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств)
	|							И ОбъектРасчетовЗеркальный.Сумма = ОбъектыРасчетов.Сумма
	|							И ОбъектРасчетовЗеркальный.ДатаВходящегоДокумента = ОбъектыРасчетов.Дата
	|							И ОбъектРасчетовЗеркальный.НомерВходящегоДокумента = ОбъектыРасчетов.Номер
	|							И ОбъектРасчетовЗеркальный.Валюта = ОбъектыРасчетов.Валюта
	|					КОГДА ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ПриходныйКассовыйОрдер)
	|						ТОГДА ТИПЗНАЧЕНИЯ(ОбъектРасчетовЗеркальный.Объект) = ТИП(Документ.РасходныйКассовыйОрдер)
	|							И ОбъектРасчетовЗеркальный.Сумма = ОбъектыРасчетов.Сумма
	|							И ОбъектРасчетовЗеркальный.Дата = ОбъектыРасчетов.ДатаВходящегоДокумента
	|							И ОбъектРасчетовЗеркальный.Номер = ОбъектыРасчетов.НомерВходящегоДокумента
	|							И ОбъектРасчетовЗеркальный.Валюта = ОбъектыРасчетов.Валюта
	|					КОГДА ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.РасходныйКассовыйОрдер)
	|						ТОГДА ТИПЗНАЧЕНИЯ(ОбъектРасчетовЗеркальный.Объект) = ТИП(Документ.ПриходныйКассовыйОрдер)
	|							И ОбъектРасчетовЗеркальный.Сумма = ОбъектыРасчетов.Сумма
	|							И ОбъектРасчетовЗеркальный.ДатаВходящегоДокумента = ОбъектыРасчетов.Дата
	|							И ОбъектРасчетовЗеркальный.НомерВходящегоДокумента = ОбъектыРасчетов.Номер
	|							И ОбъектРасчетовЗеркальный.Валюта = ОбъектыРасчетов.Валюта
	|					ИНАЧЕ ОбъектРасчетовЗеркальный.Объект = ОбъектыРасчетов.Объект
	|				КОНЕЦ
	|				И ОбъектРасчетовЗеркальный.ТипРасчетов = 
	|					ВЫБОР КОГДА ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					КОНЕЦ
	|				И  ОбъектРасчетовЗеркальный.Организация = ОбъектыРасчетов.Контрагент
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗадолженностьПоставщикам.Контрагент) = ТИП(Справочник.Организации)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетовПоПоставщиками()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗадолженностьПредварительная.Ссылка                  КАК Ссылка,
	|	ЗадолженностьПредварительная.ВидДвижения             КАК ВидДвижения,
	|	ЗадолженностьПредварительная.Организация             КАК Организация,
	|	ЗадолженностьПредварительная.Партнер                 КАК Партнер,
	|	ЗадолженностьПредварительная.Контрагент              КАК Контрагент,
	|	ЗадолженностьПредварительная.ОбъектРасчетов          КАК ОбъектРасчетов,
	|	ЗадолженностьПредварительная.Договор                 КАК Договор,
	|	ЗадолженностьПредварительная.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗадолженностьПредварительная.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|
	|	ЗадолженностьПредварительная.Сумма                   КАК Сумма,
	|	ЗадолженностьПредварительная.СуммаВзаиморасчетов     КАК СуммаВзаиморасчетов,
	|	ЗадолженностьПредварительная.СуммаРегл               КАК СуммаРегл,
	|	ЗадолженностьПредварительная.СуммаУпр                КАК СуммаУпр
	|
	|ПОМЕСТИТЬ ВтРасчетовПоПоставщикам
	|ИЗ
	|	ВтРасчетовПоПоставщикамПредварительная КАК ЗадолженностьПредварительная
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадолженностьКлиентов.Ссылка                                             КАК Ссылка,
	|	ВЫБОР КОГДА ЗадолженностьКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ                                                                    КАК ВидДвижения,
	|	ЗадолженностьКлиентов.Контрагент                                         КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)                            КАК Партнер,
	|	ЗадолженностьКлиентов.Организация                                        КАК Контрагент,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.Ссылка, Неопределено)                  КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.Договор, Неопределено)                 КАК Договор,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.НаправлениеДеятельности, Неопределено) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ОбъектРасчетовЗеркальный.ВалютаВзаиморасчетов, Неопределено)    КАК ВалютаВзаиморасчетов,
	|
	|	ЗадолженностьКлиентов.Сумма                                              КАК Сумма,
	|	ВЫБОР
	|			КОГДА ЕСТЬNULL(ОбъектРасчетовЗеркальный.ВалютаВзаиморасчетов, Неопределено) = &ВалютаРегламентированногоУчета
	|				ТОГДА ЗадолженностьКлиентов.СуммаРегл
	|			//Для поддержки старых документов, в которых мог быть пустой объект расчетов с разными валютами.
	|			ИНАЧЕ ЗадолженностьКлиентов.СуммаВзаиморасчетов
	|	КОНЕЦ                                                                    КАК СуммаВзаиморасчетов,
	|	ЗадолженностьКлиентов.СуммаРегл                                          КАК СуммаРегл,
	|	ЗадолженностьКлиентов.СуммаУпр                                           КАК СуммаУпр
	|
	|ИЗ
	|	ВтРасчетовПоКлиентамПредварительная КАК ЗадолженностьКлиентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.Ссылка = ЗадолженностьКлиентов.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектРасчетовЗеркальный
	|			ПО ОбъектРасчетовЗеркальный.Объект = ОбъектыРасчетов.Объект
	|				И ОбъектРасчетовЗеркальный.ТипРасчетов = 
	|					ВЫБОР КОГДА ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					КОНЕЦ
	|				И  ОбъектРасчетовЗеркальный.Организация = ОбъектыРасчетов.Контрагент
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗадолженностьКлиентов.Контрагент) = ТИП(Справочник.Организации)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСКлиентами()

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Дата   КАК Период,
	|	ДанныеДокумента.Дата   КАК ДатаПлатежа,
	|	ДанныеДокумента.Дата   КАК ДатаРегистратора,
	|	ДанныеДокумента.Номер  КАК НомерРегистратора,
	|	ТабличнаяЧасть.ВидДвижения                     КАК ВидДвижения,
	|	
	|	// Измерения
	|	ЕСТЬNULL(РегистрАналитикаУчетаПоПартнерам.КлючАналитики, Неопределено) КАК АналитикаУчетаПоПартнерам,
	|
	|	ТабличнаяЧасть.Организация                                            КАК Организация,
	|	ТабличнаяЧасть.Контрагент                                             КАК Контрагент,
	|	ТабличнаяЧасть.Партнер                                                КАК Партнер,
	|	ТабличнаяЧасть.Договор                                                КАК Договор,
	|	ТабличнаяЧасть.НаправлениеДеятельности                                КАК НаправлениеДеятельности,
	|
	|	ТабличнаяЧасть.ОбъектРасчетов                                         КАК ОбъектРасчетов,
	|	ТабличнаяЧасть.ВалютаВзаиморасчетов                                   КАК Валюта,
	|	
	|	// Сумма в валюте взаиморассчетов.
	|	ТабличнаяЧасть.СуммаВзаиморасчетов                                    КАК Сумма,
	|	ТабличнаяЧасть.СуммаВзаиморасчетов                                    КАК КОплате,
	|	ВЫБОР 
	|		КОГДА ТабличнаяЧасть.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета ТОГДА
	|			ТабличнаяЧасть.СуммаВзаиморасчетов
	|		КОГДА ТабличнаяЧасть.Сумма <> 0 ТОГДА 
	|			ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			ТабличнаяЧасть.СуммаРегл
	|	КОНЕЦ                                                                 КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета ТОГДА
	|			ТабличнаяЧасть.СуммаВзаиморасчетов
	|		КОГДА ТабличнаяЧасть.Сумма <> 0 ТОГДА 
	|			ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			ТабличнаяЧасть.СуммаУпр
	|	КОНЕЦ                                                                 КАК СуммаУпр,
	|	
	|	// Реквизиты
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет)                        КАК ФормаОплаты,
	|	&ВалютаРегламентированногоУчета                                       КАК ВалютаДокумента
	|	
	|ИЗ
	|	ВтРасчетовПоКлиентам КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО
	|			ТабличнаяЧасть.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|			И ТабличнаяЧасть.Партнер     = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И ТабличнаяЧасть.Контрагент  = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И ТабличнаяЧасть.Договор     = РегистрАналитикаУчетаПоПартнерам.Договор
	|			И ТабличнаяЧасть.НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                 КАК Регистратор,
	|	ДанныеДокумента.Дата                                                   КАК Период,
	|	ДанныеДокумента.Дата                                                   КАК ДатаПлатежа,
	|	ДанныеДокумента.Дата                                                   КАК ДатаРегистратора,
	|	ДанныеДокумента.Номер                                                  КАК НомерРегистратора,
	|	ТабличнаяЧасть.ВидДвижения                                             КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(РегистрАналитикаУчетаПоПартнерам.КлючАналитики, Неопределено) КАК АналитикаУчетаПоПартнерам,
	|
	|	ТабличнаяЧасть.Организация                                             КАК Организация,
	|	ТабличнаяЧасть.Контрагент                                              КАК Контрагент,
	|	ТабличнаяЧасть.Партнер                                                 КАК Партнер,
	|	ТабличнаяЧасть.Договор                                                 КАК Договор,
	|	ТабличнаяЧасть.НаправлениеДеятельности                                 КАК НаправлениеДеятельности,
	|
	|	ТабличнаяЧасть.ОбъектРасчетов                                          КАК ОбъектРасчетов,
	|	ТабличнаяЧасть.ВалютаВзаиморасчетов                                    КАК Валюта,
	|	
	|	// Ресурсы
	|	// Сумма в валюте взаиморассчетов
	|	ТабличнаяЧасть.СуммаВзаиморасчетов                                     КАК Сумма,
	|	ТабличнаяЧасть.СуммаВзаиморасчетов                                     КАК КОплате,
	|	ВЫБОР 
	|		КОГДА ТабличнаяЧасть.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета ТОГДА
	|			ТабличнаяЧасть.СуммаВзаиморасчетов
	|		КОГДА ТабличнаяЧасть.Сумма <> 0 ТОГДА 
	|			ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			ТабличнаяЧасть.СуммаРегл
	|	КОНЕЦ                                                                  КАК СуммаРегл,
	|	ВЫБОР 
	|		КОГДА ТабличнаяЧасть.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета ТОГДА
	|			ТабличнаяЧасть.СуммаВзаиморасчетов
	|		КОГДА ТабличнаяЧасть.Сумма <> 0 ТОГДА 
	|			ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			ТабличнаяЧасть.СуммаУпр
	|	КОНЕЦ                                                                  КАК СуммаУпр,
	|	
	|	// Реквизиты
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)   КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет)                          КАК ФормаОплаты,
	|	&ВалютаРегламентированногоУчета                                         КАК ВалютаДокумента
	|	
	|ИЗ
	|	ВтРасчетовПоПоставщикам КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДокумента КАК ДанныеДокумента ПО ИСТИНА
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО
	|			ТабличнаяЧасть.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|			И ТабличнаяЧасть.Партнер     = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И ТабличнаяЧасть.Контрагент  = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И ТабличнаяЧасть.Договор     = РегистрАналитикаУчетаПоПартнерам.Договор
	|			И ТабличнаяЧасть.НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыПоЗадолженности()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Задолженность.ВидДвижения КАК ВидДвижения,
	|	Задолженность.Организация КАК Организация,
	|	Задолженность.Партнер КАК Партнер,
	|	Задолженность.Контрагент КАК Контрагент,
	|	Задолженность.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Задолженность.Договор КАК Договор,
	|	Задолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Задолженность.Сумма КАК Сумма,
	|	Задолженность.СуммаРегл КАК СуммаРегл,
	|	Задолженность.СуммаУпр КАК СуммаУпр,
	|	Задолженность.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Задолженность.НаправлениеДеятельности КАК НаправлениеДеятельности
	|
	|ПОМЕСТИТЬ ВтРасчетыПоЗадолженности
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаВзаиморасчетов < 0
	|			ТОГДА ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				КОНЕЦ
	|			ИНАЧЕ РасчетыСКлиентами.ВидДвижения
	|		КОНЕЦ                                     КАК ВидДвижения,
	|		РасчетыСКлиентами.Организация             КАК Организация,
	|		РасчетыСКлиентами.Партнер                 КАК Партнер,
	|		РасчетыСКлиентами.Контрагент              КАК Контрагент,
	|		РасчетыСКлиентами.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		РасчетыСКлиентами.Договор                 КАК Договор,
	|		РасчетыСКлиентами.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасчетыСКлиентами.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|		ВЫБОР КОГДА РасчетыСКлиентами.Сумма < 0
	|			ТОГДА -РасчетыСКлиентами.Сумма
	|			ИНАЧЕ РасчетыСКлиентами.Сумма
	|		КОНЕЦ                                     КАК Сумма,
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаРегл < 0
	|			ТОГДА -РасчетыСКлиентами.СуммаРегл
	|			ИНАЧЕ РасчетыСКлиентами.СуммаРегл
	|		КОНЕЦ                                     КАК СуммаРегл,
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаУпр < 0
	|			ТОГДА -РасчетыСКлиентами.СуммаУпр
	|			ИНАЧЕ РасчетыСКлиентами.СуммаУпр
	|		КОНЕЦ                                     КАК СуммаУпр,
	|		ВЫБОР КОГДА РасчетыСКлиентами.СуммаВзаиморасчетов < 0
	|			ТОГДА -РасчетыСКлиентами.СуммаВзаиморасчетов
	|			ИНАЧЕ РасчетыСКлиентами.СуммаВзаиморасчетов
	|		КОНЕЦ                                     КАК СуммаВзаиморасчетов
	|	ИЗ
	|		ВтРасчетовПоКлиентам КАК РасчетыСКлиентами
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСПоставщиками.СуммаВзаиморасчетов < 0
	|			ТОГДА ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				КОНЕЦ
	|			ИНАЧЕ РасчетыСПоставщиками.ВидДвижения
	|		КОНЕЦ                                        КАК ВидДвижения,
	|		РасчетыСПоставщиками.Организация             КАК Организация,
	|		РасчетыСПоставщиками.Партнер                 КАК Партнер,
	|		РасчетыСПоставщиками.Контрагент              КАК Контрагент,
	|		РасчетыСПоставщиками.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		РасчетыСПоставщиками.Договор                 КАК Договор,
	|		РасчетыСПоставщиками.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасчетыСПоставщиками.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.Сумма < 0
	|			ТОГДА -РасчетыСПоставщиками.Сумма
	|			ИНАЧЕ РасчетыСПоставщиками.Сумма
	|		КОНЕЦ                                        КАК Сумма,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.СуммаРегл < 0
	|			ТОГДА -РасчетыСПоставщиками.СуммаРегл
	|			ИНАЧЕ РасчетыСПоставщиками.СуммаРегл
	|		КОНЕЦ                                        КАК СуммаРегл,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.СуммаУпр < 0
	|			ТОГДА -РасчетыСПоставщиками.СуммаУпр
	|			ИНАЧЕ РасчетыСПоставщиками.СуммаУпр
	|		КОНЕЦ                                        КАК СуммаУпр,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.СуммаВзаиморасчетов < 0
	|			ТОГДА -РасчетыСПоставщиками.СуммаВзаиморасчетов
	|			ИНАЧЕ РасчетыСПоставщиками.СуммаВзаиморасчетов
	|		КОНЕЦ                                        КАК СуммаВзаиморасчетов
	|	ИЗ
	|		ВтРасчетовПоПоставщикам КАК РасчетыСПоставщиками
	|
	|	) КАК Задолженность
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	Если ПравоДоступа("Изменение", Метаданные.Документы.ВзаимозачетЗадолженности) Тогда
		// Акт взаимозачета (Microsoft Word)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьАктаВзаимозачетаЗадолженностиMicrosoftWord";
		КомандаПечати.МенеджерПечати = "";
		КомандаПечати.Идентификатор = "АктВзаимозачетаMicrosoftWord";
		КомандаПечати.Представление = НСтр("ru='Акт взаимозачета (Microsoft Word)';uk='Акт взаємозаліку (Microsoft Word)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;

		// Акт переуступки долга (Microsoft Word)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьАктаПереуступкиДолгаMicrosoftWord";
		КомандаПечати.МенеджерПечати = "";
		КомандаПечати.Идентификатор = "АктПереуступкиДолгаMicrosoftWord";
		КомандаПечати.Представление = НСтр("ru='Акт переуступки долга (Microsoft Word)';uk='Акт переуступки боргу (Microsoft Word)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;
		
		// Акт переуступки долга между организациями (Microsoft Word)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьАктаПереуступкиДолгаМеждуОрганизациямиMicrosoftWord";
		КомандаПечати.МенеджерПечати = "";
		КомандаПечати.Идентификатор = "АктПереуступкиДолгаМеждуОрганизациямиMicrosoftWord";
		КомандаПечати.Представление = НСтр("ru='Акт переуступки долга между организациями (Microsoft Word)';uk='Акт переуступки боргу між організаціями (Microsoft Word)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоОрганизаций";
	КонецЕсли;

	ВзаимозачетЗадолженностиЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

Функция ПолучитьДанныеПечати(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Структура = Новый Структура("Данные, Макеты",
		ПолучитьДанныеОбъектаПоМакетам(МассивДокументов, МассивИменМакетов),
		ПолучитьМакетыИОписанияСекций(МассивИменМакетов));
				
	Возврат Структура;
	
КонецФункции

Функция ПолучитьДанныеОбъектаПоМакетам(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	КодЯзыкаПечать = "uk";

	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	МассивРезультатов = ПолучитьДанныеВзаимозачетЗадолженности(МассивДокументов);
	
	Выборка = МассивРезультатов[0].Выбрать();
	ВыборкаДебиторскаяЗадолженность = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаКредиторскаяЗадолженность = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
	
		ДанныеОбъекта = Новый Структура;
		ДанныеОбъекта.Вставить("Организация",                               Выборка.Организация);
		ДанныеОбъекта.Вставить("ДолжностьРуководителя",                     Выборка.ДолжностьРуководителя);
		ДанныеОбъекта.Вставить("Руководитель",                              Выборка.Руководитель);
		ДанныеОбъекта.Вставить("ОрганизацияКредитор",                       Выборка.ОрганизацияКредитор);
		ДанныеОбъекта.Вставить("ДолжностьРуководителяОрганизацииКредитора", Выборка.ДолжностьРуководителяОрганизацииКредитора);
		ДанныеОбъекта.Вставить("РуководительОрганизацииКредитора",          Выборка.РуководительОрганизацииКредитора);
		ДанныеОбъекта.Вставить("Дата",                                      Формат(Выборка.Дата, "ДЛФ=ДД;Л="+МультиязычностьУкр.ОпределитьКодЯзыкаДляФормат(КодЯзыкаПечать)));
		ДанныеОбъекта.Вставить("АдресОрганизации",                          ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Выборка.ОрганизацияСсылка, "Юридический", Выборка.Дата));
		ДанныеОбъекта.Вставить("АдресОрганизацииКредитора",                 ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Выборка.ОрганизацияКредиторСсылка, "Юридический", Выборка.Дата));
		ДанныеОбъекта.Вставить("Взаимозачет",                               Выборка.Взаимозачет);
		ДанныеОбъекта.Вставить("ВзаимозачетМеждуОрганизациями",             Выборка.ВзаимозачетМеждуОрганизациями);
		ДанныеОбъекта.Вставить("Сумма", ФормированиеПечатныхФорм.ФорматСумм(Выборка.СуммаРегл, ВалютаРегламентированногоУчета));
		
		СуммаПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(Выборка.СуммаРегл, ВалютаРегламентированногоУчета,, КодЯзыкаПечать);
		ДанныеОбъекта.Вставить("СуммаПрописью", СуммаПрописью);

		ДанныеОбъекта.Вставить("ДебиторскаяЗадолженность", Новый Массив);
		ДанныеОбъекта.Вставить("КредиторскаяЗадолженность", Новый Массив);
		
		Если ТипЗнч(Выборка.КонтрагентДебитор) = Тип("СправочникСсылка.Контрагенты") Тогда
			ДанныеОбъекта.Вставить("КонтрагентДебитор",  Справочники.Контрагенты.СокрЮрНаименованиеНаДату(Выборка.КонтрагентДебитор, Выборка.Дата));
		Иначе 
			ДанныеОбъекта.Вставить("КонтрагентДебитор",  Выборка.КонтрагентДебитор.Наименование);
		КонецЕсли;
		Если ТипЗнч(Выборка.КонтрагентКредитор) = Тип("СправочникСсылка.Контрагенты") Тогда
			ДанныеОбъекта.Вставить("КонтрагентКредитор",  Справочники.Контрагенты.СокрЮрНаименованиеНаДату(Выборка.КонтрагентКредитор, Выборка.Дата));
		Иначе 
			ДанныеОбъекта.Вставить("КонтрагентКредитор",  Выборка.КонтрагентКредитор.Наименование);
		КонецЕсли;

		АдресКонтрагентаДебитора = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.КонтрагентДебитор, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,, Выборка.Дата);
		ДанныеОбъекта.Вставить("АдресКонтрагентаДебитора", АдресКонтрагентаДебитора);
		
		АдресКонтрагентаКредитора = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.КонтрагентКредитор, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,, Выборка.Дата);
		ДанныеОбъекта.Вставить("АдресКонтрагентаКредитора", АдресКонтрагентаКредитора);
		
		АдресОрганизацииКредитора = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.КонтрагентКредитор, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,, Выборка.Дата);
		ДанныеОбъекта.Вставить("АдресОрганизацииКредитора", АдресОрганизацииКредитора);
		
		Если ВыборкаДебиторскаяЗадолженность.НайтиСледующий(Выборка.Ссылка, "Ссылка") Тогда 
			СформироватьТаблицуЗадолженности(ВыборкаДебиторскаяЗадолженность, ДанныеОбъекта.ДебиторскаяЗадолженность);
		КонецЕсли;
		Если ВыборкаКредиторскаяЗадолженность.НайтиСледующий(Выборка.Ссылка, "Ссылка") Тогда 
			СформироватьТаблицуЗадолженности(ВыборкаКредиторскаяЗадолженность, ДанныеОбъекта.КредиторскаяЗадолженность);
		КонецЕсли;

		ДанныеОбъектаПоМакетам = Новый Структура;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ДанныеОбъекта);
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(Выборка.Ссылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	Возврат ДанныеПоВсемОбъектам;
	
КонецФункции

Функция ПолучитьМакетыИОписанияСекций(знач МассивИменМакетов) Экспорт
	
	ОписаниеСекций = Новый Структура;
	ДвоичныеДанныеМакетов = Новый Структура;
	
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		
		Макет = Неопределено;
		ОписаниеСекцийМакета = Неопределено;
		Если ИмяМакета = "ПФ_DOC_АктВзаимозачета_uk" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейВзаимозачетЗадолженности();
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВзаимозачетЗадолженности.ПФ_DOC_АктВзаимозачета_uk");
		ИначеЕсли ИмяМакета = "ПФ_DOC_АктПереуступкиДолга_uk" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейВзаимозачетЗадолженности();
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВзаимозачетЗадолженности.ПФ_DOC_АктПереуступкиДолга_uk");
		ИначеЕсли ИмяМакета = "ПФ_DOC_АктПереуступкиДолгаМеждуОрганизациями_uk" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейВзаимозачетЗадолженности();
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВзаимозачетЗадолженности.ПФ_DOC_АктПереуступкиДолгаМеждуОрганизациями_uk");
		КонецЕсли;		

		Если ОписаниеСекцийМакета <> Неопределено И Макет <> Неопределено Тогда
			
			ОписаниеСекций.Вставить(ИмяМакета, ОписаниеСекцийМакета);
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Структура = Новый Структура("ОписаниеСекций, ДвоичныеДанныеМакетов",
		ОписаниеСекций,
	    ДвоичныеДанныеМакетов);
	
	Возврат Структура;
	
КонецФункции

// Формирует таблицу задолженности.
// 
// Параметры:
// 	РезультатЗапроса - РезультатЗапроса - результат запроса.
// 	МассивСтрок - Массив - Изменяемый массив строк
Процедура СформироватьТаблицуЗадолженности(РезультатЗапроса, МассивСтрок)
	
	КодЯзыкаПечать = "uk";

	НомерСтроки = 0;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НомерСтроки = НомерСтроки + 1;
		СтрокаТаблицы = Новый Структура;
		СтрокаТаблицы.Вставить("НомерСтроки", НомерСтроки);
		СтрокаТаблицы.Вставить("Сумма", Формат(Выборка.СуммаВзаиморасчетов, "ЧДЦ=2"));
		СтрокаТаблицы.Вставить("Валюта", Выборка.ВалютаВзаиморасчетов);
		
		ТекстДокумент = "";
		Если ЗначениеЗаполнено(Выборка.НаименованиеДляПечати) Тогда
			ТекстДокумент = Выборка.НаименованиеДляПечати;
		ИначеЕсли ЗначениеЗаполнено(Выборка.ТипДокумента) Тогда
			ТекстДокумент = ТекстДокумент
				+ ?(Не ПустаяСтрока(ТекстДокумент), ", " + Символы.ПС, "")
				+ ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Выборка, МультиязычностьУкр.ПолучитьЛокализованныйСинонимОбъекта(Выборка.ТипДокумента,КодЯзыкаПечать), КодЯзыкаПечать);
		КонецЕсли;
		Если ПустаяСтрока(ТекстДокумент) Тогда
			ТекстДокумент = НСтр("ru='Без документа';uk='Без документа'",КодЯзыкаПечать);
		КонецЕсли;
	
		СтрокаТаблицы.Вставить("Документ", ТекстДокумент);
		
		МассивСтрок.Добавить(СтрокаТаблицы);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
// 	МассивДокументов - Массив из ДокументСсылка - массив документов.
// Возвращаемое значение:
// 	Массив из РезультатЗапроса - 
Функция ПолучитьДанныеВзаимозачетЗадолженности(МассивДокументов)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивДокументов,
		МенеджерВременныхТаблиц);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивДокументов,
		МенеджерВременныхТаблиц,
		"ОрганизацияКредитор", 
		Новый Структура("Руководитель", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель),
		"ТаблицаОтветственныеЛицаОрганизацииКредитора");
		
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК Организация,
	|	ДанныеДокумента.Организация КАК ОрганизацияСсылка,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ДанныеДокумента.ОрганизацияКредитор.НаименованиеСокращенное КАК ОрганизацияКредитор,
	|	ДанныеДокумента.ОрганизацияКредитор КАК ОрганизацияКредиторСсылка,
	|	ТаблицаОтветственныеЛицаОрганизацииКредитора.РуководительНаименование КАК РуководительОрганизацииКредитора,
	|	ТаблицаОтветственныеЛицаОрганизацииКредитора.РуководительДолжность КАК ДолжностьРуководителяОрганизацииКредитора,
	|	ДанныеДокумента.КонтрагентДебитор КАК КонтрагентДебитор,
	|	ДанныеДокумента.КонтрагентКредитор КАК КонтрагентКредитор,
	|	ВЫБОР КОГДА (НЕ (ДанныеДокумента.ВидОперации В (&ВидыОперацийМеждуОрганизациями)) И ДанныеДокумента.КонтрагентДебитор = ДанныеДокумента.КонтрагентКредитор)
	|			ИЛИ (ДанныеДокумента.ВидОперации В (&ВидыОперацийМеждуОрганизациями) И ДанныеДокумента.Организация = ДанныеДокумента.ОрганизацияКредитор) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Взаимозачет,
	|	ВЫБОР КОГДА ДанныеДокумента.ВидОперации В (&ВидыОперацийМеждуОрганизациями) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ВзаимозачетМеждуОрганизациями,
	|	ДанныеДокумента.СуммаРегл КАК СуммаРегл
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаОтветственныеЛицаОрганизацииКредитора КАК ТаблицаОтветственныеЛицаОрганизацииКредитора
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛицаОрганизацииКредитора.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.ТипРасчетов КАК ТипРасчетов,
	|	ВЫБОР КОГДА Задолженность.ОбъектРасчетов.Объект = НЕОПРЕДЕЛЕНО ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Задолженность.ОбъектРасчетов.Объект
	|	КОНЕЦ КАК ТипДокумента,
	|	Задолженность.ОбъектРасчетов.Дата КАК Дата,
	|	Задолженность.ОбъектРасчетов.Номер КАК Номер,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Задолженность.ОбъектРасчетов.Объект) = ТИП(Справочник.ДоговорыКонтрагентов) ТОГДА
	|		ВЫРАЗИТЬ(Задолженность.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).НаименованиеДляПечати
	|			КОГДА ТИПЗНАЧЕНИЯ(Задолженность.ОбъектРасчетов.Объект) = ТИП(Справочник.ДоговорыМеждуОрганизациями) ТОГДА
	|		ВЫРАЗИТЬ(Задолженность.ОбъектРасчетов.Объект КАК Справочник.ДоговорыМеждуОрганизациями).НаименованиеДляПечати
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаименованиеДляПечати,
	|	Задолженность.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Задолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК Задолженность
	|ГДЕ
	|	Задолженность.Ссылка В (&МассивДокументов)
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Задолженность.Ссылка КАК Ссылка,
	|	Задолженность.ТипРасчетов КАК ТипРасчетов,
	|	ВЫБОР КОГДА Задолженность.ОбъектРасчетов.Объект = НЕОПРЕДЕЛЕНО ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Задолженность.ОбъектРасчетов.Объект
	|	КОНЕЦ КАК ТипДокумента,
	|	Задолженность.ОбъектРасчетов.Дата КАК Дата,
	|	Задолженность.ОбъектРасчетов.Номер КАК Номер,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Задолженность.ОбъектРасчетов.Объект) = ТИП(Справочник.ДоговорыКонтрагентов) ТОГДА
	|		ВЫРАЗИТЬ(Задолженность.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).НаименованиеДляПечати
	|			КОГДА ТИПЗНАЧЕНИЯ(Задолженность.ОбъектРасчетов.Объект) = ТИП(Справочник.ДоговорыМеждуОрганизациями) ТОГДА
	|		ВЫРАЗИТЬ(Задолженность.ОбъектРасчетов.Объект КАК Справочник.ДоговорыМеждуОрганизациями).НаименованиеДляПечати
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаименованиеДляПечати,
	|	Задолженность.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Задолженность.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК Задолженность
	|ГДЕ
	|	Задолженность.Ссылка В (&МассивДокументов)
	|
	|ИТОГИ ПО
	|	Ссылка
	|");
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("ВидыОперацийМеждуОрганизациями", МассивОпераций);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
		
	Возврат МассивРезультатов;

КонецФункции

Функция ПолучитьОписаниеОбластейВзаимозачетЗадолженности()

	Секции = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "ВерхнийКолонтитул","ВерхнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "НижнийКолонтитул","НижнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Заголовок", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "ДебиторскаяЗадолженность", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "КредиторскаяЗадолженность", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "ШапкаТаблицы", "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "СтрокаТаблицы", "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Итог", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Подвал", "Общая");
	
	Возврат Секции;

КонецФункции

// Параметры:
// 	СтруктураДанныхОбъектаПечати - Структура - состоит из:
// * ОсновнойПолучатель - Строка -
// * МассивРеквизитовПолучателей - Массив из Строка -
// * МассивДанных - Массив из ДокументСсылка -
// 
Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "КонтрагентДебитор";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("КонтрагентДебитор");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("КонтрагентКредитор");
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS.
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область СформироватьНедостающиеКлючиАналитикиПоПартнерам	

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.304";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4c47b129-4014-43bc-8125-be399962cbc5");
	Обработчик.Процедура = "Документы.ВзаимозачетЗадолженности.СформироватьНедостающиеКлючиАналитикиПоПартнерам";
	Обработчик.ЧитаемыеОбъекты = "Документ.ВзаимозачетЗадолженности,"
		+ "РегистрСведений.АналитикаУчетаПоПартнерам";
	Обработчик.ИзменяемыеОбъекты = "Справочник.КлючиАналитикиУчетаПоПартнерам,"
		+ "РегистрСведений.АналитикаУчетаПоПартнерам";
	Обработчик.Комментарий = НСтр("ru='Создает ключи аналитики по партнерам для документов ""Взаимозачет задолженности"".';uk='Створює ключі аналітики по партнерам для документів ""Взаємозалік заборгованості"".'");
	
#КонецОбласти

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию24

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Документы.ВзаимозачетЗадолженности.ОбработатьДанныеДляПереходаНаНовуюВерсию24";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("74a3833b-9838-4ba3-a0c1-dc1371b7f39f");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ВзаимозачетЗадолженности.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию24";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрНакопления.ДвиженияКонтрагентКонтрагент,"
		+ "Документ.ВзаимозачетЗадолженности";
	Обработчик.ИзменяемыеОбъекты = "Документ.ВзаимозачетЗадолженности";
	Обработчик.БлокируемыеОбъекты = "Документ.ВзаимозачетЗадолженности";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ""Организация"" в табличной части ""Кредиторская задолженность"".
                                   |Заполняет сумму управленческого и регламентированного учета в табличных частях документа.'
                                   |;uk='Заповнює реквізит ""Організація"" у табличній частині ""Кредиторська заборгованість"".
                                   |Заповнює суму управлінського та регламентованого обліку у табличних частинах документа.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.СуммыДокументовВВалютахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";   
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВзаимозачетЗадолженности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";   
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДвиженияКонтрагентКонтрагент.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";   

#КонецОбласти

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ВзаимозачетЗадолженности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("6c702e29-6b3a-4868-94ab-572afd5ef21a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ВзаимозачетЗадолженности.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ОбъектыРасчетов.';uk='Заповнює реквізит Об''екти розрахунків.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ВзаимозачетЗадолженности.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ВзаимозачетЗадолженности.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ВзаимозачетЗадолженности.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);
	
#КонецОбласти

КонецПроцедуры


// Обработчик обновления
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию24(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка.ВидОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями))
	|	И ДанныеДокумента.УдалитьЗаказ = НЕОПРЕДЕЛЕНО
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.Ссылка.ОрганизацияКредитор
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТЧДебиторскаяЗадолженность
	|			ПО ТЧДебиторскаяЗадолженность.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ТЧДебиторскаяЗадолженность.СуммаУпр = 0
	|	И ТЧДебиторскаяЗадолженность.СуммаРегл = 0
	|	И ТЧДебиторскаяЗадолженность.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТЧКредиторскаяЗадолженность
	|			ПО ТЧКредиторскаяЗадолженность.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ТЧКредиторскаяЗадолженность.СуммаУпр = 0
	|	И ТЧКредиторскаяЗадолженность.СуммаРегл = 0
	|	И ТЧКредиторскаяЗадолженность.Сумма <> 0
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию24(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.Ссылка                     КАК Ссылка,
	|	СсылкиДляОбработки.Ссылка.ВерсияДанных        КАК ВерсияДанных,
	|	СсылкиДляОбработки.Ссылка.Проведен            КАК Проведен,
	|	СсылкиДляОбработки.Ссылка.ВидОперации         КАК ВидОперации,
	|	СсылкиДляОбработки.Ссылка.Организация         КАК Организация,
	|	СсылкиДляОбработки.Ссылка.ОрганизацияКредитор КАК ОрганизацияКредитор,
	|	СсылкиДляОбработки.Ссылка.КонтрагентДебитор   КАК КонтрагентДебитор,
	|	СсылкиДляОбработки.Ссылка.КонтрагентКредитор  КАК КонтрагентКредитор
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.Ссылка                                                                        КАК Ссылка,
	|	ЕСТЬNULL(КонтрагентКонтрагент.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))        КАК Организация,
	|
	|	ЕСТЬNULL(КонтрагентКонтрагент.Партнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))               КАК Партнер,
	|	ЕСТЬNULL(КонтрагентКонтрагент.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))         КАК Контрагент,
	|	ЕСТЬNULL(КонтрагентКонтрагент.УдалитьОбъектРасчетов, Неопределено)                               КАК УдалитьОбъектРасчетов,
	|	ЕСТЬNULL(КонтрагентКонтрагент.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))    КАК ВалютаВзаиморасчетов,
	|
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорПартнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))            КАК КорПартнер,
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорКонтрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))      КАК КорКонтрагент,
	|	ЕСТЬNULL(КонтрагентКонтрагент.УдалитьКорОбъектРасчетов, Неопределено)                            КАК УдалитьКорОбъектРасчетов,
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК КорВалютаВзаиморасчетов,
	|
	|	СУММА(ЕСТЬNULL(КонтрагентКонтрагент.Сумма,0))                                                    КАК СуммаУпр,
	|	СУММА(ЕСТЬNULL(КонтрагентКонтрагент.СуммаВВалютеВзаиморасчетов,0))                               КАК СуммаВВалютеВзаиморасчетов,
	|	СУММА(ЕСТЬNULL(КонтрагентКонтрагент.КорСуммаВВалютеВзаиморасчетов,0))                            КАК КорСуммаВВалютеВзаиморасчетов,
	|	СУММА(ЕСТЬNULL(КонтрагентКонтрагент.СуммаРегл,0))                                                КАК СуммаРегл
	|
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияКонтрагентКонтрагент КАК КонтрагентКонтрагент
	|	ПО
	|		СсылкиДляОбработки.Ссылка = КонтрагентКонтрагент.Регистратор
	|СГРУППИРОВАТЬ ПО
	|	СсылкиДляОбработки.Ссылка,
	|	ЕСТЬNULL(КонтрагентКонтрагент.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.Партнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.УдалитьОбъектРасчетов, Неопределено),
	|	ЕСТЬNULL(КонтрагентКонтрагент.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорПартнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорКонтрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(КонтрагентКонтрагент.УдалитьКорОбъектРасчетов, Неопределено),
	|	ЕСТЬNULL(КонтрагентКонтрагент.КорВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	ТЗКонтрагентКонтрагент = Результат[2].Выгрузить();
	
	ТЗКонтрагентКонтрагент.Индексы.Добавить("Ссылка, Организация, Контрагент");
	СтруктураПоискаСтрок       = Новый Структура("Ссылка, Организация, Контрагент");
	СтруктураПоискаСтрокКор    = Новый Структура("Ссылка, Организация, КорКонтрагент");
	СтруктураПоискаСтрок       = Новый Структура("Ссылка, Контрагент");
	СтруктураПоискаСтрокКор    = Новый Структура("Ссылка, КорКонтрагент");
	СтруктуруПоискаПоДокументу = Новый Структура("Партнер, УдалитьЗаказ, ВалютаВзаиморасчетов");
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ВзаимозачетЗадолженности");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			Если НЕ Выборка.Проведен Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				Выборка.Ссылка, 
				Выборка.ВерсияДанных, 
				Параметры.Очередь
			);
			
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрокаДокумента Из ДокументОбъект.КредиторскаяЗадолженность Цикл
				Если Не ЗначениеЗаполнено(СтрокаДокумента.УдалитьЗаказ) Тогда
					СтрокаДокумента.Организация = ДокументОбъект.ОрганизацияКредитор;
				КонецЕсли;
			КонецЦикла;
			
			Если Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями 
				ИЛИ Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
				
				// Дебиторская задолженность
				СтруктураПоискаСтрок.Ссылка      = Выборка.Ссылка;
				СтруктураПоискаСтрок.Контрагент  = Выборка.КонтрагентДебитор;
				
				СтрокиККДебиторка = ТЗКонтрагентКонтрагент.НайтиСтроки(СтруктураПоискаСтрок);
				
				Для Каждого СтрокаККДт Из СтрокиККДебиторка Цикл
					СтруктуруПоискаПоДокументу.Партнер              = СтрокаККДт.Партнер;
					СтруктуруПоискаПоДокументу.УдалитьЗаказ         = СтрокаККДт.УдалитьОбъектРасчетов;
					СтруктуруПоискаПоДокументу.ВалютаВзаиморасчетов = СтрокаККДт.ВалютаВзаиморасчетов;
					
					СтрокиДт = ДокументОбъект.ДебиторскаяЗадолженность.НайтиСтроки(СтруктуруПоискаПоДокументу);
					
					РаспределитьСтрокуККНаСтрокиДокумента(СтрокаККДт, СтрокиДт, Ложь);
					
				КонецЦикла;
				
				// Кредиторская задолженность
				СтруктураПоискаСтрок.Ссылка      = Выборка.Ссылка;
				СтруктураПоискаСтрок.Контрагент  = Выборка.Организация;
				
				СтрокиКККредиторка = ТЗКонтрагентКонтрагент.НайтиСтроки(СтруктураПоискаСтрок);
				
				Для Каждого СтрокаКККт Из СтрокиКККредиторка Цикл
					СтруктуруПоискаПоДокументу.Партнер              = СтрокаКККт.КорПартнер;
					СтруктуруПоискаПоДокументу.УдалитьЗаказ         = СтрокаКККт.УдалитьКорОбъектРасчетов;
					СтруктуруПоискаПоДокументу.ВалютаВзаиморасчетов = СтрокаКККт.КорВалютаВзаиморасчетов;
					
					СтрокиКт = ДокументОбъект.КредиторскаяЗадолженность.НайтиСтроки(СтруктуруПоискаПоДокументу);
					
					РаспределитьСтрокуККНаСтрокиДокумента(СтрокаКККт, СтрокиКт, Истина);
					
				КонецЦикла;
				
			Иначе
				
				// Дебиторская задолженность
				СтруктураПоискаСтрок.Ссылка      = Выборка.Ссылка;
				СтруктураПоискаСтрок.Контрагент  = Выборка.КонтрагентДебитор;
				
				СтрокиККДебиторка = ТЗКонтрагентКонтрагент.НайтиСтроки(СтруктураПоискаСтрок);
				
				Для Каждого СтрокаККДт Из СтрокиККДебиторка Цикл
					
					СтруктуруПоискаПоДокументу.Партнер              = СтрокаККДт.Партнер;
					СтруктуруПоискаПоДокументу.УдалитьЗаказ         = СтрокаККДт.УдалитьОбъектРасчетов;
					СтруктуруПоискаПоДокументу.ВалютаВзаиморасчетов = СтрокаККДт.ВалютаВзаиморасчетов;
					
					СтрокиДт = ДокументОбъект.ДебиторскаяЗадолженность.НайтиСтроки(СтруктуруПоискаПоДокументу);
					
					РаспределитьСтрокуККНаСтрокиДокумента(СтрокаККДт, СтрокиДт, Ложь);
					
				КонецЦикла;
				
				// Кредиторская задолженность
				Если Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента
					ИЛИ Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями
					ИЛИ Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер
					ИЛИ Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика
					ИЛИ Выборка.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями Тогда
					Контрагент  = Выборка.КонтрагентДебитор;
				Иначе;
					Контрагент  = Выборка.КонтрагентКредитор;
				КонецЕсли;
				
				СтруктураПоискаСтрокКор.КорКонтрагент = Контрагент;
				СтруктураПоискаСтрокКор.Ссылка        = Выборка.Ссылка;
				СтрокиКККредиторка = ТЗКонтрагентКонтрагент.НайтиСтроки(СтруктураПоискаСтрокКор);
				
				Для Каждого СтрокаКККт Из СтрокиКККредиторка Цикл
					СтруктуруПоискаПоДокументу.Партнер              = СтрокаКККт.КорПартнер;
					СтруктуруПоискаПоДокументу.УдалитьЗаказ         = СтрокаКККт.УдалитьКорОбъектРасчетов;
					СтруктуруПоискаПоДокументу.ВалютаВзаиморасчетов = СтрокаКККт.КорВалютаВзаиморасчетов;
					
					СтрокиКт = ДокументОбъект.КредиторскаяЗадолженность.НайтиСтроки(СтруктуруПоискаПоДокументу);
					
					РаспределитьСтрокуККНаСтрокиДокумента(СтрокаКККт, СтрокиКт, Истина);
					
				КонецЦикла;
				
			КонецЕсли;
			
			ДокументОбъект.СуммаРегл = ДокументОбъект.ДебиторскаяЗадолженность.Итог("СуммаРегл");
			ДокументОбъект.СуммаУпр = ДокументОбъект.ДебиторскаяЗадолженность.Итог("СуммаУпр");
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, , ,РежимЗаписиДокумента.Запись);
			ЗафиксироватьТранзакцию();     
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось обработать: %ВзаимозачетЗадолженности% по причине: %Причина%';uk='Не вдалося обробити: %ВзаимозачетЗадолженности% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВзаимозачетЗадолженности%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ВзаимозачетЗадолженности, 
				Выборка.Ссылка, 
				ТекстСообщения
			);
			
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь, 
		"Документ.ВзаимозачетЗадолженности"
	);

КонецПроцедуры

Процедура РаспределитьСтрокуККНаСтрокиДокумента(СтрокаКК, СтрокиДокумента, ЭтоКредит)
	
	Если ЭтоКредит Тогда
		СуммаРасчетовПервоначальная = СтрокаКК.КорСуммаВВалютеВзаиморасчетов;
	Иначе
		СуммаРасчетовПервоначальная = СтрокаКК.СуммаВВалютеВзаиморасчетов;
	КонецЕсли;
	
	СуммаРегл = СтрокаКК.СуммаРегл;
	СуммаУпр  = СтрокаКК.СуммаУпр;
	СуммаВВалютеВзаиморасчетов = СуммаРасчетовПервоначальная;
	
	Для Каждого СтрокаДокумента Из СтрокиДокумента Цикл
		
		Если СуммаВВалютеВзаиморасчетов = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаДокумента.СуммаВзаиморасчетов < СуммаВВалютеВзаиморасчетов Тогда
			СтрокаДокумента.СуммаРегл = СтрокаКК.СуммаРегл * (СтрокаДокумента.СуммаВзаиморасчетов / СуммаРасчетовПервоначальная);
			СтрокаДокумента.СуммаУпр  = СтрокаКК.СуммаУпр  * (СтрокаДокумента.СуммаВзаиморасчетов / СуммаРасчетовПервоначальная);
		Иначе
			СтрокаДокумента.СуммаРегл = СтрокаДокумента.СуммаРегл + СуммаРегл;
			СтрокаДокумента.СуммаУпр = СтрокаДокумента.СуммаУпр + СуммаУпр;
		КонецЕсли;
		
		СуммаВВалютеВзаиморасчетов = СуммаВВалютеВзаиморасчетов - СтрокаДокумента.СуммаВзаиморасчетов;
		СуммаРегл = СуммаРегл - СтрокаДокумента.СуммаРегл;
		СуммаУпр  = СуммаУпр  - СтрокаДокумента.СуммаУпр;
		
	КонецЦикла;
	
КонецПроцедуры


// Обработчик обновления
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВзаимозачетЗадолженности.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|ГДЕ
	|	ВзаимозачетЗадолженности.Проведен
	|	И ((ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА
	|			ИЗ
	|				Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТЧКредиторскаяЗадолженность
	|			ГДЕ
	|				ТЧКредиторскаяЗадолженность.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|				И ТЧКредиторскаяЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)))
	|	ИЛИ (ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТЧДебиторскаяЗадолженность
	|			ГДЕ
	|				ТЧДебиторскаяЗадолженность.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|				И ТЧДебиторскаяЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)))
	|	ИЛИ (ВзаимозачетЗадолженности.ОбъектРасчетовИнтеркампани = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		И (ВзаимозачетЗадолженности.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями)
	|			ИЛИ ВзаимозачетЗадолженности.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями))))
	|";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка
	|ПОМЕСТИТЬ ОбновляемыеДанные
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка               КАК Ссылка,
	|	ВзаимозачетЗадолженности.ВерсияДанных  КАК ВерсияДанных
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|//Заполнение объектов расчета в ТЧ ДебиторскаяЗадолженность для не пустого поля УдалитьЗаказ
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка               КАК Ссылка,
	|	""ДебиторскаяЗадолженность""           КАК ИмяТЧ,
	|	ТЧЗадолженность.НомерСтроки 		   КАК НомерСтроки,
	|	&ОпределениеОрганизации 			   КАК Организация,
	|	ОбъектыРасчетов.Ссылка                 КАК ОбъектРасчетов
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТЧЗадолженность
	|			ПО ОбновляемыеДанные.Ссылка = ТЧЗадолженность.Ссылка
	|				И ТЧЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ТЧЗадолженность.ТипРасчетов = ОбъектыРасчетов.ТипРасчетов
	|				И &ОпределениеОрганизации = ОбъектыРасчетов.Организация
	|				И ТЧЗадолженность.УдалитьЗаказ = ОбъектыРасчетов.Объект
	|				И ТЧЗадолженность.Партнер = ОбъектыРасчетов.Партнер
	|				И ТЧЗадолженность.ВалютаВзаиморасчетов = ОбъектыРасчетов.ВалютаВзаиморасчетов
	|				И ВзаимозачетЗадолженности.КонтрагентДебитор = ОбъектыРасчетов.Контрагент
	|ГДЕ 
	|	НЕ ТЧЗадолженность.УдалитьЗаказ В(&ПустыеЗначенияОбъектовРасчетов) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Заполнение объектов расчета в ТЧ ДебиторскаяЗадолженность для пустого поля УдалитьЗаказ
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка               КАК Ссылка,
	|	""ДебиторскаяЗадолженность""           КАК ИмяТЧ,
	|	ТЧЗадолженность.НомерСтроки 		   КАК НомерСтроки,
	|	ТЧЗадолженность.Организация 		   КАК Организация,
	|	ОбъектыРасчетов.Ссылка                 КАК ОбъектРасчетов
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТЧЗадолженность
	|			ПО ОбновляемыеДанные.Ссылка = ТЧЗадолженность.Ссылка
	|				И ТЧЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО НЕОПРЕДЕЛЕНО = ОбъектыРасчетов.Объект
	|				И ТЧЗадолженность.ТипРасчетов = ОбъектыРасчетов.ТипРасчетов
	|				И ТЧЗадолженность.Организация = ОбъектыРасчетов.Организация
	|				И ТЧЗадолженность.Партнер = ОбъектыРасчетов.Партнер
	|				И ВзаимозачетЗадолженности.КонтрагентДебитор = ОбъектыРасчетов.Контрагент
	|				И ТЧЗадолженность.ВалютаВзаиморасчетов = ОбъектыРасчетов.ВалютаВзаиморасчетов
	|				И ВЫБОР КОГДА ВзаимозачетЗадолженности.ТипДебитора В (ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик))
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|					КОНЕЦ = ОбъектыРасчетов.Договор
	|				И ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) = ОбъектыРасчетов.НаправлениеДеятельности
	|ГДЕ
	|	ТЧЗадолженность.УдалитьЗаказ В(&ПустыеЗначенияОбъектовРасчетов) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Заполнение объектов расчета в ТЧ КредиторскаяЗадолженность для не пустого поля УдалитьЗаказ
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка                КАК Ссылка,
	|	""КредиторскаяЗадолженность""           КАК ИмяТЧ,
	|	ТЧЗадолженность.НомерСтроки 			КАК НомерСтроки,
	|	&ОпределениеОрганизации 				КАК Организация,
	|	ОбъектыРасчетов.Ссылка                  КАК ОбъектРасчетов
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТЧЗадолженность
	|			ПО ОбновляемыеДанные.Ссылка = ТЧЗадолженность.Ссылка
	|				И ТЧЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ТЧЗадолженность.ТипРасчетов = ОбъектыРасчетов.ТипРасчетов
	|				И &ОпределениеОрганизации = ОбъектыРасчетов.Организация
	|				И ТЧЗадолженность.УдалитьЗаказ = ОбъектыРасчетов.Объект
	|				И ТЧЗадолженность.Партнер = ОбъектыРасчетов.Партнер
	|				И ТЧЗадолженность.ВалютаВзаиморасчетов = ОбъектыРасчетов.ВалютаВзаиморасчетов
	|				И ВзаимозачетЗадолженности.КонтрагентКредитор = ОбъектыРасчетов.Контрагент
	|ГДЕ 
	|	НЕ ТЧЗадолженность.УдалитьЗаказ В(&ПустыеЗначенияОбъектовРасчетов) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Заполнение объектов расчета в ТЧ КредиторскаяЗадолженность для пустого поля УдалитьЗаказ
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка                КАК Ссылка,
	|	""КредиторскаяЗадолженность""           КАК ИмяТЧ,
	|	ТЧЗадолженность.НомерСтроки 			КАК НомерСтроки,
	|	ТЧЗадолженность.Организация 			КАК Организация,
	|	ОбъектыРасчетов.Ссылка                  КАК ОбъектРасчетов
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТЧЗадолженность
	|			ПО ОбновляемыеДанные.Ссылка = ТЧЗадолженность.Ссылка
	|				И ТЧЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО НЕОПРЕДЕЛЕНО = ОбъектыРасчетов.Объект
	|				И ТЧЗадолженность.ТипРасчетов = ОбъектыРасчетов.ТипРасчетов
	|				И ТЧЗадолженность.Организация = ОбъектыРасчетов.Организация
	|				И ТЧЗадолженность.Партнер = ОбъектыРасчетов.Партнер
	|				И ВзаимозачетЗадолженности.КонтрагентКредитор = ОбъектыРасчетов.Контрагент
	|				И ТЧЗадолженность.ВалютаВзаиморасчетов = ОбъектыРасчетов.ВалютаВзаиморасчетов
	|				И ВЫБОР КОГДА ВзаимозачетЗадолженности.ТипКредитора В (ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик))
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|					КОНЕЦ = ОбъектыРасчетов.Договор
	|				И ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) = ОбъектыРасчетов.НаправлениеДеятельности
	|ГДЕ
	|	ТЧЗадолженность.УдалитьЗаказ В (&ПустыеЗначенияОбъектовРасчетов)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|//Заполнение поля ОбъектРасчетовИнтеркампани
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбновляемыеДанные.Ссылка               КАК Взаимозачет,
	|	ОбъектыРасчетов.Ссылка                 КАК ОбъектРасчетовИнтеркампани,
	|	ОбъектыРасчетов.ВалютаВзаиморасчетов   КАК ВалютаВзаиморасчетов
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
	|			ПО ОбновляемыеДанные.Ссылка = ВзаимозачетЗадолженности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|				И ВзаимозачетЗадолженности.Организация = ОбъектыРасчетов.Организация
	|				И ВзаимозачетЗадолженности.ОрганизацияКредитор = ОбъектыРасчетов.Контрагент
	|				И ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) = ОбъектыРасчетов.Партнер
	|				И (ВзаимозачетЗадолженности.УдалитьОбъектРасчетовИнтеркампани = ОбъектыРасчетов.Объект
	|					ИЛИ (ВзаимозачетЗадолженности.УдалитьОбъектРасчетовИнтеркампани В (&ПустыеЗначенияОбъектовРасчетов)
	|							И ОбъектыРасчетов.Объект = Неопределено))
	|ГДЕ
	|	ВзаимозачетЗадолженности.ОбъектРасчетовИнтеркампани = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|
	|";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектовРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	Запрос.УстановитьПараметр("ТипРасчетовСПоставщиком", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	ТекстЗапросаОпределениеОрганизации = "
	|ЕСТЬNULL(
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ТЧЗадолженность.УдалитьЗаказ) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями) 
	|			И ТЧЗадолженность.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|				ВЫРАЗИТЬ(ТЧЗадолженность.УдалитьЗаказ КАК Документ.ПередачаТоваровМеждуОрганизациями).ОрганизацияПолучатель
	|	КОГДА ТИПЗНАЧЕНИЯ(ТЧЗадолженность.УдалитьЗаказ) = ТИП(Справочник.ДоговорыМеждуОрганизациями) 
	|			И ТЧЗадолженность.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|				ВЫРАЗИТЬ(ТЧЗадолженность.УдалитьЗаказ КАК Справочник.ДоговорыМеждуОрганизациями).ОрганизацияПолучатель
	|	КОГДА ТИПЗНАЧЕНИЯ(ТЧЗадолженность.УдалитьЗаказ) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями) 
	|			И ТЧЗадолженность.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|				ВЫРАЗИТЬ(ТЧЗадолженность.УдалитьЗаказ КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).Комиссионер
	|	ИНАЧЕ
	|		ТЧЗадолженность.УдалитьЗаказ.Организация
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТЧЗадолженность.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|			ВЫБОР
	|				КОГДА ВзаимозачетЗадолженности.ВидОперации В (
	|					ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями),
	|					ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями)
	|				)
	|					ТОГДА ВзаимозачетЗадолженности.ОрганизацияКредитор
	|				ИНАЧЕ ВзаимозачетЗадолженности.Организация
	|			КОНЕЦ                              
	|	ИНАЧЕ
	|		ТЧЗадолженность.Организация
	|	КОНЕЦ
	|)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОпределениеОрганизации", ТекстЗапросаОпределениеОрганизации);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаДокумент = РезультатЗапроса[1].Выбрать();
	ВыборкаТЧ = РезультатЗапроса[2].Выгрузить();
	ВыборкаТЧ.Индексы.Добавить("Ссылка, ИмяТЧ, НомерСтроки");
	ВыборкаОбъектРасчетовИнтеркампани = РезультатЗапроса[3].Выгрузить();
	ВыборкаОбъектРасчетовИнтеркампани.Индексы.Добавить("Взаимозачет");
	
	Пока ВыборкаДокумент.Следующий() Цикл
		
		Ссылка = ВыборкаДокумент.Ссылка;
		
		НачатьТранзакцию();
		
		Попытка
		
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				Ссылка,
				ВыборкаДокумент.ВерсияДанных,
				Параметры.Очередь
			);
				
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ТабЧастиЗадолженности = Новый Массив();
			ТабЧастиЗадолженности.Добавить("ДебиторскаяЗадолженность");
			ТабЧастиЗадолженности.Добавить("КредиторскаяЗадолженность");
			
			Для Каждого ТабЧастьЗадолженность Из ТабЧастиЗадолженности Цикл
				ТЧ = ДокументОбъект[ТабЧастьЗадолженность]; // ТабличнаяЧасть 
				Для Каждого СтрокаЗадолженность Из ТЧ Цикл
					Если Не ЗначениеЗаполнено(СтрокаЗадолженность.ОбъектРасчетов) Тогда
						Отбор = Новый Структура("Ссылка, ИмяТЧ, НомерСтроки", Ссылка, ТабЧастьЗадолженность, СтрокаЗадолженность.НомерСтроки);
						НайденныйОР = ВыборкаТЧ.НайтиСтроки(Отбор);
						
						Если НайденныйОР.Количество() = 1 Тогда
							ОбъектРасчетов = НайденныйОР[0].ОбъектРасчетов;
							Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
								СтрокаЗадолженность.ОбъектРасчетов = ОбъектРасчетов;
							ИначеЕсли Не ЗначениеЗаполнено(ОбъектРасчетов) И ЗначениеЗаполнено(СтрокаЗадолженность.УдалитьЗаказ) Тогда
								ДополнительныеКритерииПоиска = ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска();
								ДополнительныеКритерииПоиска.Партнер = СтрокаЗадолженность.Партнер;
								ДополнительныеКритерииПоиска.Контрагент = ?(ТабЧастьЗадолженность = "КредиторскаяЗадолженность",
									ДокументОбъект.КонтрагентКредитор,
									ДокументОбъект.КонтрагентДебитор);
									
								ОбъектРасчетов = ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(
									СтрокаЗадолженность.УдалитьЗаказ,
									НайденныйОР[0].Организация,
									СтрокаЗадолженность.ТипРасчетов,
									ДополнительныеКритерииПоиска);
									Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
										СтрокаЗадолженность.ОбъектРасчетов = ОбъектРасчетов;
									ИначеЕсли ДокументОбъект.Проведен Тогда
										ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru='Не удалось заполнить объект расчетов в документе: %1, строка № %2, табличная часть %3';uk='Не вдалося заповнити об''єкт розрахунків у документі: %1, рядок № %2, таблична частина %3'"),
											Ссылка,
											СтрокаЗадолженность.НомерСтроки,
											ТабЧастьЗадолженность));
									КонецЕсли;
										
							КонецЕсли;
						ИначеЕсли ДокументОбъект.Проведен Тогда
							ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Не удалось заполнить объект расчетов в документе: %1, строка № %2, табличная часть %3';uk='Не вдалося заповнити об''єкт розрахунків у документі: %1, рядок № %2, таблична частина %3'"),
								Ссылка,
								СтрокаЗадолженность.НомерСтроки,
								ТабЧастьЗадолженность));
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Если Не ЗначениеЗаполнено(ДокументОбъект.ОбъектРасчетовИнтеркампани) 
				И (ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями
					ИЛИ ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями)
				Тогда
					Отбор = Новый Структура("Взаимозачет", Ссылка);
					НайденныеСтроки = ВыборкаОбъектРасчетовИнтеркампани.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество() = 1 И ЗначениеЗаполнено(НайденныеСтроки[0].ОбъектРасчетовИнтеркампани) Тогда
						ДокументОбъект.ОбъектРасчетовИнтеркампани = НайденныеСтроки[0].ОбъектРасчетовИнтеркампани;
					ИначеЕсли НайденныеСтроки.Количество() > 1 Тогда
						УникальынеВалютыВзаиморасчета = ДокументОбъект.ДебиторскаяЗадолженность.ВыгрузитьКолонку("ВалютаВзаиморасчетов");
						ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(УникальынеВалютыВзаиморасчета);
						Если УникальынеВалютыВзаиморасчета.Количество() = 1 Тогда
							ВалютаВзаиморасчетов = УникальынеВалютыВзаиморасчета[0];
							ОтборПоВалюте = Новый Структура("Взаимозачет, ВалютаВзаиморасчетов", Ссылка, ВалютаВзаиморасчетов);
							НайденныеСтрокиПоВалюте = ВыборкаОбъектРасчетовИнтеркампани.НайтиСтроки(ОтборПоВалюте);
							
							Если НайденныеСтрокиПоВалюте.Количество() = 1 И ЗначениеЗаполнено(НайденныеСтрокиПоВалюте[0].ОбъектРасчетовИнтеркампани) Тогда
								ДокументОбъект.ОбъектРасчетовИнтеркампани = НайденныеСтрокиПоВалюте[0].ОбъектРасчетовИнтеркампани;
							Иначе
								ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Не удалось заполнить поле Объект расчетов интеркампани в документе: %1';uk='Не вдалося заповнити поле Об''єкт розрахунків інтеркампані в документі: %1'"),
								Ссылка));
							КонецЕсли;
						КонецЕсли;
						
					ИначеЕсли ДокументОбъект.Проведен Тогда
						ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Не удалось заполнить поле Объект расчетов интеркампани в документе: %1';uk='Не вдалося заповнити поле Об''єкт розрахунків інтеркампані в документі: %1'"),
							Ссылка));
					КонецЕсли;
			КонецЕсли;

			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДокументОбъект.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
	
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Ссылка);
		КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);

КонецПроцедуры

Функция ТекстЗапросаРегистрацияДанныхДляГенерацииПустогоОбъектаРасчетов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВзаимозачетЗадолженностиДебиторскаяЗадолженность.Ссылка.КонтрагентДебитор КАК Контрагент
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ВзаимозачетЗадолженностиДебиторскаяЗадолженность
	|ГДЕ
	|	ВзаимозачетЗадолженностиДебиторскаяЗадолженность.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И ВзаимозачетЗадолженностиДебиторскаяЗадолженность.Ссылка.Проведен
	|	И НЕ ВзаимозачетЗадолженностиДебиторскаяЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВзаимозачетЗадолженностиКредиторскаяЗадолженность.Ссылка.КонтрагентКредитор КАК Контрагент
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ВзаимозачетЗадолженностиКредиторскаяЗадолженность
	|ГДЕ
	|	ВзаимозачетЗадолженностиКредиторскаяЗадолженность.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И ВзаимозачетЗадолженностиКредиторскаяЗадолженность.Ссылка.Проведен
	|	И НЕ ВзаимозачетЗадолженностиКредиторскаяЗадолженность.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПолучениеДанныхДляГенерацииПустогоОбъектаРасчетов() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТЧДебиторскаяЗадолженность.ТипРасчетов КАК ТипРасчетов,
	|	ТЧДебиторскаяЗадолженность.Организация,
	|	ТЧДебиторскаяЗадолженность.Партнер,
	|	ИсточникДанных.КонтрагентДебитор,
	|	ВЫБОР КОГДА ИсточникДанных.ТипДебитора В (ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
	|											ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик))
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	ТЧДебиторскаяЗадолженность.ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ТЧДебиторскаяЗадолженность
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ИсточникДанных
	|		ПО ТЧДебиторскаяЗадолженность.Ссылка = ИсточникДанных.Ссылка
	|ГДЕ
	|	ТЧДебиторскаяЗадолженность.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И ИсточникДанных.Проведен
	|	И ИсточникДанных.КонтрагентДебитор В (&ОбрабатываемыеДанные)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТЧКредиторскаяЗадолженность.ТипРасчетов КАК ТипРасчетов,
	|	ТЧКредиторскаяЗадолженность.Организация,
	|	ТЧКредиторскаяЗадолженность.Партнер,
	|	ИсточникДанных.КонтрагентКредитор,
	|	ВЫБОР КОГДА ИсточникДанных.ТипКредитора В (ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
	|											ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик))
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	ТЧКредиторскаяЗадолженность.ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТЧКредиторскаяЗадолженность
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ИсточникДанных
	|		ПО ТЧКредиторскаяЗадолженность.Ссылка = ИсточникДанных.Ссылка
	|ГДЕ
	|	ТЧКредиторскаяЗадолженность.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
	|	И ИсточникДанных.Проведен
	|	И ИсточникДанных.КонтрагентКредитор В (&ОбрабатываемыеДанные)";
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаВтДебиторскаяЗадолженность22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ТипРасчетов                     КАК ТипРасчетов,
	|	ДанныеДокумента.УдалитьЗаказ                    КАК Заказ,
	|	ДанныеДокумента.Партнер                         КАК Партнер,
	|	ДанныеДокумента.Сумма                           КАК Сумма,
	|	ДанныеДокумента.СуммаВзаиморасчетов             КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.НомерСтроки                     КАК НомерСтроки,
	
	|	ЕСТЬNULL(
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Документ.ПередачаТоваровМеждуОрганизациями).ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Справочник.ДоговорыМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Справочник.ДоговорыМеждуОрганизациями).ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).Комиссионер
	|		ИНАЧЕ
	|			ДанныеДокумента.УдалитьЗаказ.Организация
	|		КОНЕЦ,
	|		ВЫБОР КОГДА ДанныеДокумента.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|			&Организация
	|		ИНАЧЕ
	|			ДанныеДокумента.Организация
	|		КОНЕЦ
	|	) КАК Организация,
	
	|	ЕСТЬNULL(
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) В (ТИП(Справочник.ДоговорыКонтрагентов), ТИП(Справочник.ДоговорыМеждуОрганизациями)) ТОГДА
	|			ДанныеДокумента.УдалитьЗаказ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА ДанныеДокумента.УдалитьЗаказ.Договор = Неопределено 
	|					ТОГДА
	|						ВЫБОР 
	|							КОГДА &РасчетыМеждуОрганизациямиКредитор
	|								ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР 
	|						КОГДА ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.РасчетыЧерезОтдельногоКонтрагента, ЛОЖЬ) = ИСТИНА
	|							ТОГДА
	|								ВЫБОР 
	|									КОГДА ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком 
	|										ТОГДА ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.ДоговорПокупки,ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|									ИНАЧЕ ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.ДоговорПродажи,ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|								КОНЕЦ
	|						ИНАЧЕ ДанныеДокумента.УдалитьЗаказ.Договор
	|					КОНЕЦ
	|			КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		КОНЕЦ
	|	) КАК Договор,
	
	|	ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.Подразделение, &Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности
	|
	|ПОМЕСТИТЬ ВтДебиторскаяЗадолженность
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.ДебиторскаяЗадолженность КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтКредиторскаяЗадолженность22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ТипРасчетов                     КАК ТипРасчетов,
	|	ДанныеДокумента.УдалитьЗаказ                    КАК Заказ,
	|	ДанныеДокумента.Партнер                         КАК Партнер,
	|	ДанныеДокумента.Сумма                           КАК Сумма,
	|	ДанныеДокумента.СуммаВзаиморасчетов             КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.НомерСтроки                     КАК НомерСтроки,
	
	|	ЕСТЬNULL(
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Документ.ПередачаТоваровМеждуОрганизациями).ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Справочник.ДоговорыМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Справочник.ДоговорыМеждуОрганизациями).ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями) И ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком ТОГДА
	|			ВЫРАЗИТЬ(ДанныеДокумента.УдалитьЗаказ КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).Комиссионер
	|		ИНАЧЕ
	|			ДанныеДокумента.УдалитьЗаказ.Организация
	|		КОНЕЦ,
	|		ВЫБОР КОГДА ДанныеДокумента.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА
	|			&ПараметрОрганизацияКредитор
	|		ИНАЧЕ
	|			ДанныеДокумента.Организация
	|		КОНЕЦ
	|	) КАК Организация,
	
	|	ЕСТЬNULL(
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.УдалитьЗаказ) В (ТИП(Справочник.ДоговорыКонтрагентов), ТИП(Справочник.ДоговорыМеждуОрганизациями)) ТОГДА
	|			ДанныеДокумента.УдалитьЗаказ
	|		ИНАЧЕ
	|			ВЫБОР 
	|				КОГДА ДанныеДокумента.УдалитьЗаказ.Договор = Неопределено 
	|					ТОГДА
	|						ВЫБОР 
	|							КОГДА &РасчетыМеждуОрганизациямиКредитор
	|								ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|						КОНЕЦ
	|				ИНАЧЕ 
	|					ВЫБОР 
	|						КОГДА ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.РасчетыЧерезОтдельногоКонтрагента, ЛОЖЬ) = ИСТИНА
	|							ТОГДА
	|								ВЫБОР 
	|									КОГДА ДанныеДокумента.ТипРасчетов = &ТипРасчетовСПоставщиком 
	|										ТОГДА ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.ДоговорПокупки,ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|									ИНАЧЕ ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.ДоговорПродажи,ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|								КОНЕЦ
	|						ИНАЧЕ ДанныеДокумента.УдалитьЗаказ.Договор
	|					КОНЕЦ
	|			КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		КОНЕЦ
	|	) КАК Договор,
	
	|	ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.Подразделение, &Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(ДанныеДокумента.УдалитьЗаказ.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности
	|
	|ПОМЕСТИТЬ ВтКредиторскаяЗадолженность
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДополнитьТекстЗапросаСоединениемСКлючамиАналитики22(ТекстЗапроса)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ЗапросТабличныеЧасти = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ТекстЗапросаТабличныеЧасти = ЗапросТабличныеЧасти.ПолучитьТекстЗапроса();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличныеЧасти.Организация КАК Организация,
	|	ТабличныеЧасти.Партнер     КАК Партнер,
	|	ТабличныеЧасти.Контрагент  КАК Контрагент,
	|	ТабличныеЧасти.Договор     КАК Договор,
	|	ТабличныеЧасти.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.Организация КАК Организация,
	|		ВложенныйЗапрос.Партнер КАК Партнер,
	|		ВложенныйЗапрос.Контрагент КАК Контрагент,
	|		ВложенныйЗапрос.Договор КАК Договор,
	|		ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности
	|	ИЗ
	|		ВложенныйЗапрос
	|	) КАК ТабличныеЧасти
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ТабличныеЧасти.Организация = Аналитика.Организация
	|		И ТабличныеЧасти.Партнер = Аналитика.Партнер
	|		И ТабличныеЧасти.Контрагент = Аналитика.Контрагент
	|		И ТабличныеЧасти.Договор = Аналитика.Договор
	|		И ТабличныеЧасти.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК НеобработаннаяАналитика
	|	ПО
	|		ТабличныеЧасти.Организация = Аналитика.Организация
	|		И ТабличныеЧасти.Партнер = Аналитика.Партнер
	|		И ТабличныеЧасти.Контрагент = Аналитика.Контрагент
	|		И ТабличныеЧасти.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		И Аналитика.Договор = НЕОПРЕДЕЛЕНО
	|		И ТабличныеЧасти.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК НеобработаннаяАналитикаИнтеркампани
	|	ПО
	|		ТабличныеЧасти.Организация = Аналитика.Организация
	|		И ТабличныеЧасти.Партнер = Аналитика.Партнер
	|		И ТабличныеЧасти.Контрагент = Аналитика.Контрагент
	|		И ТабличныеЧасти.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|		И Аналитика.Договор = НЕОПРЕДЕЛЕНО
	|		И ТабличныеЧасти.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	Аналитика.КлючАналитики ЕСТЬ NULL
	|	И НеобработаннаяАналитика.КлючАналитики ЕСТЬ NULL
	|	И НеобработаннаяАналитикаИнтеркампани.КлючАналитики ЕСТЬ NULL
	|";
	
	СхемаЗапросаАналитик = Новый СхемаЗапроса;
	СхемаЗапросаАналитик.УстановитьТекстЗапроса(ТекстЗапроса);
	СхемаЗапросаАналитик.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Запрос.УстановитьТекстЗапроса(ТекстЗапросаТабличныеЧасти);
	
	ЗапросТабличныеЧасти.УстановитьТекстЗапроса(СхемаЗапросаАналитик.ПолучитьТекстЗапроса());
	
	Возврат СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецФункции

Процедура ДополнитьТекстЗапросаПроведенияДляПроверки22(РезультирующийЗапрос, ПолноеИмяРегистра, ПолноеИмяДокумента) 
	
	ТекстЗапросаФормированияДвижений = РезультирующийЗапрос.Текст;
	ЗначенияПараметров = Новый Структура;
	
	ТекстРезультирующегоЗапроса = "";
	
	ЧастиИмениРегистра = СтрРазделить(ПолноеИмяРегистра, ".", Ложь);
	ТипРегистра = ЧастиИмениРегистра[0];
	ИмяРегистра = ЧастиИмениРегистра[1];
	МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(ПолноеИмяРегистра);
	
	ТекстРегистра = 
	"ВЫБРАТЬ";
	
	ТекстВыборкиСуммирующегоЗапроса =
	"ВЫБРАТЬ";
	ТекстГруппировкиСуммирующиегоЗапроса = "
	|СГРУППИРОВАТЬ ПО";
	ТекстУсловияСуммирующиегоЗапроса = "
	|ИМЕЮЩИЕ
	|	ЛОЖЬ";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаФормированияДвижений);
	Запрос = СхемаЗапроса.ПакетЗапросов[0];
	
	ВсеКолонки = Новый Массив;
	КолонкиКУдалению = Новый Массив;
	
	Для Каждого Колонка Из Запрос.Колонки Цикл
		Если МетаданныеРегистра.Реквизиты.Найти(Колонка.Псевдоним) <> Неопределено Тогда
			КолонкиКУдалению.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;
	Для каждого КолонкаКУдалению Из КолонкиКУдалению Цикл
		Запрос.Колонки.Удалить(Запрос.Колонки.Индекс(КолонкаКУдалению));
	КонецЦикла;
	
	ТекстЗапросаФормированияДвижений = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Для Каждого Колонка Из Запрос.Колонки Цикл
		
		ВсеКолонки.Добавить(Колонка.Псевдоним);
		
		Если ТипРегистра = "РегистрНакопления"
			И МетаданныеРегистра.Ресурсы.Найти(Колонка.Псевдоним) <> Неопределено Тогда
			
			ТекстРегистра = ТекстРегистра + "
			|	-ТаблицаРегистра." + Колонка.Псевдоним + ",";
			ТекстВыборкиСуммирующегоЗапроса = ТекстВыборкиСуммирующегоЗапроса + "
			|	СУММА(ВложенныйЗапрос." + Колонка.Псевдоним + ") КАК " + Колонка.Псевдоним + ",";
			ТекстУсловияСуммирующиегоЗапроса = ТекстУсловияСуммирующиегоЗапроса + "
			|	ИЛИ СУММА(ВложенныйЗапрос." + Колонка.Псевдоним + ") <> 0";
			
		ИначеЕсли Не Колонка.Псевдоним = "КонтрольноеПолеОбновлениеИБ" Тогда
			ТекстРегистра = ТекстРегистра + "
			|	ТаблицаРегистра." + Колонка.Псевдоним + ",";
			
			ТекстВыборкиСуммирующегоЗапроса = ТекстВыборкиСуммирующегоЗапроса + "
			|	ВложенныйЗапрос." + Колонка.Псевдоним + " КАК " + Колонка.Псевдоним + ",";
			
			ТекстГруппировкиСуммирующиегоЗапроса = ТекстГруппировкиСуммирующиегоЗапроса + " 
			|	ВложенныйЗапрос." + Колонка.Псевдоним + ",";
		КонецЕсли;
	КонецЦикла;
	
	ТекстВставкиЗапросФормирующийДвижения = "";
	
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		
		Если ВсеКолонки.Найти(Измерение.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстРегистра = ТекстРегистра + "
		|	ТаблицаРегистра." + Измерение.Имя + ",";
		ТекстВыборкиСуммирующегоЗапроса = ТекстВыборкиСуммирующегоЗапроса + "
		|	ВложенныйЗапрос." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
		ТекстГруппировкиСуммирующиегоЗапроса = ТекстГруппировкиСуммирующиегоЗапроса + " 
		|	ВложенныйЗапрос." + Измерение.Имя + ",";
		ТекстВставкиЗапросФормирующийДвижения = ТекстВставкиЗапросФормирующийДвижения + "
		|	&ПустоеЗначение" + Измерение.Имя + " КАК " + Измерение.Имя + ",";
		ЗначенияПараметров.Вставить("ПустоеЗначение" + Измерение.Имя, Измерение.Тип.ПривестиЗначение());
		
	КонецЦикла;
	
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		
		Если ВсеКолонки.Найти(Ресурс.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстРегистра = ТекстРегистра + "
		|	-ТаблицаРегистра." + Ресурс.Имя + ",";
		ТекстВыборкиСуммирующегоЗапроса = ТекстВыборкиСуммирующегоЗапроса + "
		|	СУММА(ВложенныйЗапрос." + Ресурс.Имя + ") КАК " + Ресурс.Имя + ",";
		ТекстУсловияСуммирующиегоЗапроса = ТекстУсловияСуммирующиегоЗапроса + "
		|	ИЛИ СУММА(ВложенныйЗапрос." + Ресурс.Имя + ") <> 0";
		ТекстВставкиЗапросФормирующийДвижения = ТекстВставкиЗапросФормирующийДвижения + "
		|	0 КАК " + Ресурс.Имя + ",";
	КонецЦикла;
	
	ТекстРегистра = Лев(ТекстРегистра, СтрДлина(ТекстРегистра) - 1);
	ТекстВыборкиСуммирующегоЗапроса = Лев(ТекстВыборкиСуммирующегоЗапроса, СтрДлина(ТекстВыборкиСуммирующегоЗапроса) - 1);
	ТекстГруппировкиСуммирующиегоЗапроса = Лев(ТекстГруппировкиСуммирующиегоЗапроса, СтрДлина(ТекстГруппировкиСуммирующиегоЗапроса) - 1);
	
	Если Не ПустаяСтрока(ТекстВставкиЗапросФормирующийДвижения) Тогда
		ТекстВставкиЗапросФормирующийДвижения = Лев(ТекстВставкиЗапросФормирующийДвижения, СтрДлина(ТекстВставкиЗапросФормирующийДвижения) - 1);
		ТекстЗапросаФормированияДвижений = СтрЗаменить(ТекстЗапросаФормированияДвижений,
											"ИЗ",
											",
											|" +ТекстВставкиЗапросФормирующийДвижения + "
											|ИЗ");
	КонецЕсли;
			
	ТекстРегистра = ТекстРегистра + "
	|ИЗ
	|	" + ПолноеИмяРегистра + " КАК ТаблицаРегистра";
	
	ТекстРегистра = ТекстРегистра + "
	|ГДЕ
	|	ТаблицаРегистра.Регистратор ССЫЛКА " + ПолноеИмяДокумента;
	
	ТекстРезультирующегоЗапроса = ТекстРезультирующегоЗапроса + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НеправильныеДвижения.Регистратор КАК Регистратор
	|ИЗ
	|(" + ТекстВыборкиСуммирующегоЗапроса + "
	|ИЗ
	|	("
	+ ТекстЗапросаФормированияДвижений 
	+ "
	|ОБЪЕДИНИТЬ ВСЕ
	|" 
	+ ТекстРегистра
	+ ") КАК ВложенныйЗапрос "
	+ ТекстГруппировкиСуммирующиегоЗапроса
	+ ТекстУсловияСуммирующиегоЗапроса + ") КАК НеправильныеДвижения";
	
	Для Каждого Параметр Из ЗначенияПараметров Цикл
		РезультирующийЗапрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	РезультирующийЗапрос.Текст = ТекстРезультирующегоЗапроса;
	
КонецПроцедуры

Функция ПереопределенныеПараметрыЗапроса22()
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("ПараметрОрганизацияКредитор", "
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ВидОперации В (
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями)
		|			)
		|			ТОГДА ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ОрганизацияКредитор
		|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).Организация
		|	КОНЕЦ");
	ПараметрыЗапроса.Вставить("РасчетыМежду2Организациями", "
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ВидОперации В (
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями)
		|			)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ");
	ПараметрыЗапроса.Вставить("РасчетыМеждуОрганизациямиДебитор", "
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ТипДебитора В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
		|			)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ");
	ПараметрыЗапроса.Вставить("РасчетыМеждуОрганизациямиКредитор", "
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ТипКредитора В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияКлиент),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
		|			)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ");
	ПараметрыЗапроса.Вставить("ДоговорИнтеркампани", "
		|	ЕСТЬNULL(
		|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ОбъектРасчетовИнтеркампани) 
		|				= ТИП(Справочник.ДоговорыМеждуОрганизациями) ТОГДА
		|			ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ОбъектРасчетовИнтеркампани
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ОбъектРасчетовИнтеркампани.Договор
		|		КОНЕЦ,
		|		ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))");
	ПараметрыЗапроса.Вставить("ОбъектРасчетовИнтеркампани", "
		|	ВЫРАЗИТЬ(ДанныеДокумента.Ссылка КАК Документ.ВзаимозачетЗадолженности).ОбъектРасчетовИнтеркампани");
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

Функция ТекстЗапросаКлючиАналитикПоПартнерам22()
	
	ТекстЗапроса = "
	|	ВЫБРАТЬ // аналитика {Организация, Дебитор-партнер, Дебитор-контрагент}
	|		ДанныеДокумента.Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДанныеДокумента.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ДанныеДокумента.Договор КАК Договор,
	|		ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Организация, Кредитор-партнер, Кредитор-контрагент}
	|		ДанныеДокумента.Организация КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДанныеДокумента.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		ДанныеДокумента.Договор КАК Договор,
	|		ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Дебитор-организация, Кредитор-партнер, Кредитор-контрагент}
	|		&КонтрагентДебитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДанныеДокумента.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентКредитор КАК Контрагент,
	|		ВЫБОР КОГДА &РасчетыМеждуОрганизациямиКредитор 
	|			ТОГДА &ДоговорИнтеркампани
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		КОНЕЦ КАК Договор,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиДебитор И &КонтрагентКредитор <> &КонтрагентДебитор
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Кредитор-организация, Дебитор-партнер, Дебитор-контрагент}
	|		&КонтрагентКредитор КАК Организация,
	|		(ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ИНАЧЕ ДанныеДокумента.Партнер КОНЕЦ) КАК Партнер,
	|		&КонтрагентДебитор КАК Контрагент,
	|		ВЫБОР КОГДА &РасчетыМеждуОрганизациямиДебитор 
	|			ТОГДА &ДоговорИнтеркампани
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		КОНЕЦ КАК Договор,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка И &РасчетыМеждуОрганизациямиКредитор И &КонтрагентКредитор <> &КонтрагентДебитор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Дебитор-организация, Предприятие-партнер, Организация}
	|		&КонтрагентДебитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		ДанныеДокумента.Организация КАК Контрагент,
	|		ДанныеДокумента.Договор КАК Договор,
	|		ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		&РасчетыМеждуОрганизациямиДебитор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Кредитор-организация, Предприятие-партнер, Организация}
	|		&КонтрагентКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		ДанныеДокумента.Организация КАК Контрагент,
	|		ДанныеДокумента.Договор КАК Договор,
	|		ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтКредиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		&РасчетыМеждуОрганизациямиКредитор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Организация, Предприятие-партнер, Организация-кредитор}
	|		ДанныеДокумента.Организация КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		&ПараметрОрганизацияКредитор КАК Контрагент,
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&ПараметрОрганизацияКредитор) = ТИП(Справочник.Организации)
	|			ТОГДА &ДоговорИнтеркампани
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		КОНЕЦ КАК Договор,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		&РасчетыМежду2Организациями
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ // аналитика {Организация-кредитор, Предприятие-партнер, Организация}
	|		&ПараметрОрганизацияКредитор КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|		ДанныеДокумента.Организация КАК Контрагент,
	|		&ДоговорИнтеркампани КАК Договор,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
	|	ИЗ
	|		ВтДебиторскаяЗадолженность КАК ДанныеДокумента
	|	ГДЕ
	|		&РасчетыМежду2Организациями
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаФормированияКлючейАналитикПоПартнерам22()
	
	ПолноеИмяДокумента = "Документ.ВзаимозачетЗадолженности";
	СинонимТаблицыДокумента = "ДанныеДокумента";
	
	ТекстЗапроса = 
		ТекстЗапросаВтДебиторскаяЗадолженность22() + "
		|;" +
		ТекстЗапросаВтКредиторскаяЗадолженность22() + "
		|;" +
		ТекстЗапросаКлючиАналитикПоПартнерам22();
	ПараметрыЗапроса = ПереопределенныеПараметрыЗапроса22();
	ПараметрыЗапроса.Вставить("ТипРасчетовСПоставщиком", "ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)");
	
	АдаптированныйТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
		ТекстЗапроса,
		ПолноеИмяДокумента,
		СинонимТаблицыДокумента,
		ПараметрыЗапроса);
	
	Возврат ДополнитьТекстЗапросаСоединениемСКлючамиАналитики22(АдаптированныйТекстЗапроса);
	
КонецФункции

// Формирует в ИБ все недостающие ключи аналитики по партнерам для последующего группового проведения документов.
//
Процедура СформироватьНедостающиеКлючиАналитикиПоПартнерам() Экспорт // Запускается монопольно
	
	Запрос = Новый Запрос(АдаптированныйТекстЗапросаФормированияКлючейАналитикПоПартнерам22());
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Формирует перечень документов с отличающимися от эталонных движениями
//
// Параметры
//    ИмяРегистра - Строка - Проверяемый регистр.
//
// Возвращаемое значение
//    Массив - Документы для перепроведения по регистру.
//
Функция РегистраторыДляПерепроведенияПриОбновлении(ИмяРегистра) Экспорт
	
	Регистраторы = Новый Массив;
	
	Если ИмяРегистра = "РасчетыСКлиентами"
		Или ИмяРегистра = "РасчетыСПоставщиками" Тогда
	
		ПолноеИмяДокумента = "Документ.ВзаимозачетЗадолженности";
		МетаданныеДокумента = Метаданные.НайтиПоПолномуИмени(ПолноеИмяДокумента);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
        ЗапросАналитик = Новый Запрос(АдаптированныйТекстЗапросаФормированияКлючейАналитикПоПартнерам22());
		ЗапросАналитик.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
		РезультатЗапроса = ЗапросАналитик.ВыполнитьПакет();
		
		Если ИмяРегистра = "РасчетыСКлиентами" Тогда
            ТекстЗапросаВТ = ТекстЗапросаВтРасчетыСКлиентами();
			ТекстЗапроса = ТекстЗапросаРасчетыСКлиентами();
		ИначеЕсли ИмяРегистра = "РасчетыСПоставщиками" Тогда
			ТекстЗапросаВТ = ТекстЗапросаВтРасчетовПоПоставщиками();
			ТекстЗапроса = ТекстЗапросаРасчетыСПоставщиками();
		КонецЕсли;
		
        ПереопределениеРасчетаПараметров = ПереопределенныеПараметрыЗапроса22();
		
		Запрос.Текст = ТекстЗапросаВТ;
		ПараметрыЗапроса = Запрос.НайтиПараметры();
		Для Каждого Параметр Из ПараметрыЗапроса Цикл
			ТекстЗамены = Неопределено;
			Если ПереопределениеРасчетаПараметров.Свойство(Параметр.Имя) Тогда
				ТекстЗамены = ПереопределениеРасчетаПараметров[Параметр.Имя];
			ИначеЕсли Параметр.Имя = "Период" Тогда
				ТекстЗамены = "";
			ИначеЕсли МетаданныеДокумента.Реквизиты.Найти(Параметр.Имя) <> Неопределено Тогда
				ТекстЗамены = "ВЫРАЗИТЬ(" + СинонимТаблицыДокумента + ".Ссылка КАК " + ПолноеИмяДокумента + ")." + Параметр.Имя;
			КонецЕсли;
		
			Если ТекстЗамены <> Неопределено Тогда
				ТекстЗапросаВТ = СтрЗаменить(ТекстЗапросаВТ, "&" + Параметр.Имя, ТекстЗамены);
			КонецЕсли;
		КонецЦикла;
		
		ТекстЗапросаВТ = ТекстЗапросаВТ + ";
		|УНИЧТОЖИТЬ ВтДебиторскаяЗадолженность;
		|УНИЧТОЖИТЬ ВтКредиторскаяЗадолженность;";
		
		ЗапросВТ = Новый Запрос(ТекстЗапросаВТ);
		ЗапросВТ.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
		РезультатЗапроса = ЗапросВТ.ВыполнитьПакет();
		
		Запрос.Текст = ТекстЗапроса;
        ДополнитьТекстЗапросаПроведенияДляПроверки22(Запрос, "РегистрНакопления." + ИмяРегистра, ПолноеИмяДокумента);
		Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Возврат Регистраторы;
	
КонецФункции


#КонецОбласти

#КонецОбласти

#КонецЕсли
