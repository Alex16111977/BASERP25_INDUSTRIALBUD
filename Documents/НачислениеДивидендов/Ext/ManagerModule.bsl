#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ИнтеграцияБЗК");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДивиденды(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Начисление дивидендов".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.НачислениеДивидендов) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.НачислениеДивидендов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.НачислениеДивидендов);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРеглУчет";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Возвращает текст запроса для отражения в регл. учете.
//
// Возвращаемое значение:
//   Строка - текст отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР(); 
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - текст построения временной таблицы для отражения в регл. учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	
КонецФункции

#Область ПроводкиРегУчетаУКР

// Возвращает текст запроса для отражения в регл. учете.
//
// Возвращаемое значение:
//   Строка - текст отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
	
	ТекстДивидендыПрочимФизическимЛицам = "
	// Начисление дивидендов физическим лицам
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Начислено КАК Сумма,
	|	Строки.Начислено / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльИспользованнаяВОтчетномПериоде) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов.ФизическиеЛица КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|";

	ТекстДивидендыПрочимФизическимЛицам = СтрШаблон(ТекстДивидендыПрочимФизическимЛицам, 
		НСтр("ru='Начисление дивидендов физическим лицам';uk='Нарахування дивідендів фізичним особам'",ЯзыкСодержания));
	
	ТекстНДФЛДивидендыПрочимФизическимЛицам = "
	// Начисление НДФЛ с дивидендов физическим лицам
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.НДФЛ КАК Сумма,
	|	Строки.НДФЛ / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДФЛ) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов.ФизическиеЛица КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|";

	ТекстНДФЛДивидендыПрочимФизическимЛицам = СтрШаблон(ТекстНДФЛДивидендыПрочимФизическимЛицам, 
		НСтр("ru = Начисление НДФЛ с дивидендов физическим лицам''", ЯзыкСодержания));
	
	ТекстВСДивидендыПрочимФизическимЛицам = "
	// Начисление ВС с дивидендов физическим лицам
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.ВС КАК Сумма,
	|	Строки.ВС / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоОбязательнымПлатежам) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Налоги.ВоенныйСбор) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов.ФизическиеЛица КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|";

	ТекстВСДивидендыПрочимФизическимЛицам = СтрШаблон(ТекстВСДивидендыПрочимФизическимЛицам, 
		НСтр("ru='Начисление военного сбора с дивидендов физическим лицам';uk='Нарахування військового збору з дивідендів фізичним особам'",ЯзыкСодержания));

	ТекстДивидендыЮридическимЛицам = "
	// Начисление дивидендов юридическим лицам
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(Строки.Начислено) КАК Сумма,
	|	СУММА(Строки.Начислено / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльИспользованнаяВОтчетномПериоде) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеДивидендов.ЮридическиеЛица КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Строки.Контрагент";

	ТекстДивидендыЮридическимЛицам = СтрШаблон(ТекстДивидендыЮридическимЛицам, 
		НСтр("ru='Начисление дивидендов юридическим лицам';uk='Нарахування дивідендів юридичним особам'",ЯзыкСодержания));
	
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстДивидендыПрочимФизическимЛицам);
	ТекстыОтражения.Добавить(ТекстНДФЛДивидендыПрочимФизическимЛицам);
	ТекстыОтражения.Добавить(ТекстВСДивидендыПрочимФизическимЛицам);
	ТекстыОтражения.Добавить(ТекстДивидендыЮридическимЛицам);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - текст построения временной таблицы для отражения в регл. учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	//Возврат "
	//|ВЫБРАТЬ
	//|	ВложенныйЗапрос.Организация КАК Организация,
	//|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.ДатаОтражения, МЕСЯЦ) КАК ДатаОтражения
	//|ПОМЕСТИТЬ ПериодыИОрганизации
	//|ИЗ
	//|	(ВЫБРАТЬ
	//|		РазрезыКОтражению.Организация КАК Организация,
	//|		РазрезыКОтражению.ДатаОтражения КАК ДатаОтражения
	//|	ИЗ
	//|		РазрезыКОтражению КАК РазрезыКОтражению
	//|	
	//|	ОБЪЕДИНИТЬ ВСЕ
	//|	
	//|	ВЫБРАТЬ
	//|		ВЫРАЗИТЬ(РазрезыКОтражению.Организация КАК Справочник.Организации).ГоловнаяОрганизация,
	//|		РазрезыКОтражению.ДатаОтражения
	//|	ИЗ
	//|		РазрезыКОтражению КАК РазрезыКОтражению) КАК ВложенныйЗапрос
	//|ГДЕ
	//|	НЕ ВложенныйЗапрос.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	ВложенныйЗапрос.Организация,
	//|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.ДатаОтражения, МЕСЯЦ)
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ПериодыИОрганизации.Организация КАК Организация,
	//|	МАКСИМУМ(ЕСТЬNULL(ИсторияРегистрацийВНалоговомОргане.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Период,
	//|	НАЧАЛОПЕРИОДА(ПериодыИОрганизации.ДатаОтражения, МЕСЯЦ) КАК ДатаОтражения
	//|ПОМЕСТИТЬ ПоследниеРегистрацииВНО
	//|ИЗ
	//|	ПериодыИОрганизации КАК ПериодыИОрганизации
	//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	//|		ПО ПериодыИОрганизации.Организация = ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
	//|			И ПериодыИОрганизации.ДатаОтражения >= ИсторияРегистрацийВНалоговомОргане.Период
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	ПериодыИОрганизации.Организация,
	//|	НАЧАЛОПЕРИОДА(ПериодыИОрганизации.ДатаОтражения, МЕСЯЦ)
	//|;
	//|";
	Возврат "";
	
КонецФункции

#КонецОбласти


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.Дата КАК Дата,
	|	Документ.ДатаВыплаты КАК ДатаВыплаты,
	|	Документ.Валюта КАК Валюта,
	|	Документ.Организация КАК Организация
	|ИЗ
	|	Документ.НачислениеДивидендов КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";

	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		Реквизиты.Валюта,
		Неопределено,
		Реквизиты.Дата);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",	Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("ДатаДокумента",			Реквизиты.Дата);
	Запрос.УстановитьПараметр("ДатаВыплаты",			Реквизиты.ДатаВыплаты);
КонецПроцедуры

Функция ТекстЗапросаВТТаблицаФизическихЛиц(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТТаблицаФизическихЛиц"; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Ссылка КАК Ссылка,
	|	ДивидендыФизическимЛицам.ФизическоеЛицо,
	|	ДивидендыФизическимЛицам.Начислено,
	|	ДивидендыФизическимЛицам.КодДохода,
	|	ДивидендыФизическимЛицам.ВидСтавки,
	|	ДивидендыФизическимЛицам.НДФЛ,
	|	ДивидендыФизическимЛицам.КодДоходаВС,
	|	ДивидендыФизическимЛицам.ВидСтавкиВС,
	|	ДивидендыФизическимЛицам.ВС,
	|	ДивидендыФизическимЛицам.КВыплате,
	|	ДивидендыФизическимЛицам.ОбособленноеПодразделение,
	|	НЕ ДивидендыФизическимЛицам.ПрочийАкционер КАК ЭтоСотрудник
	|ПОМЕСТИТЬ ВТТаблицаФизическихЛиц
	|ИЗ
	|	Документ.НачислениеДивидендов.ФизическиеЛица КАК ДивидендыФизическимЛицам
	|ГДЕ
	|	ДивидендыФизическимЛицам.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТТаблицаФизическихЛиц", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТаблицаФизическихЛиц(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
	|	&ДатаДокумента КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивидендыСотрудникам)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивиденды)
	|	КОНЕЦ КАК Аналитика,
	|	СУММА(ВЫРАЗИТЬ((ТаблицаФизическихЛиц.НДФЛ+ТаблицаФизическихЛиц.ВС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма
	|ИЗ
	|	ВТТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивидендыСотрудникам)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивиденды)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаДокумента КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК Статья,
	|	Неопределено КАК Аналитика,
	|	СУММА(ВЫРАЗИТЬ((ТаблицаФизическихЛиц.НДФЛ+ТаблицаФизическихЛиц.ВС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма
	|ИЗ
	|	ВТТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц
	|";

	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТТаблицаФизическихЛиц", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТаблицаФизическихЛиц(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ДатаДокумента                                                КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНалоговВзносовСЗаработнойПлаты) КАК ХозяйственнаяОперация,
	|	&Организация                                                  КАК Организация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивидендыСотрудникам)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивиденды)
	|	КОНЕЦ                                                         КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                                  КАК КорАналитикаАктивовПассивов,
	|	СУММА(ВЫРАЗИТЬ((ТаблицаФизическихЛиц.НДФЛ + ТаблицаФизическихЛиц.ВС) * &КоэффициентПересчетаВВалютуУпр  КАК ЧИСЛО(31,2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ((ТаблицаФизическихЛиц.НДФЛ + ТаблицаФизическихЛиц.ВС) * &КоэффициентПересчетаВВалютуУпр  КАК ЧИСЛО(31,2))) КАК СуммаУпр,
	|	СУММА(ТаблицаФизическихЛиц.НДФЛ + ТаблицаФизическихЛиц.ВС) КАК СуммаРегл
	|ИЗ
	|	ВТТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивидендыСотрудникам)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НФДЛДивиденды)
	|	КОНЕЦ
	|	
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДивиденды(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СведенияОДоходахНДФЛ";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТТаблицаФизическихЛиц", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТаблицаФизическихЛиц(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НФДЛДивидендыСотрудникам)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НФДЛДивиденды)
	|	КОНЕЦ КАК НачислениеУдержание,
	|	ТаблицаФизическихЛиц.ФизическоеЛицо КАК Акционер,
	|	ТаблицаФизическихЛиц.ФизическоеЛицо,
	|	ТаблицаФизическихЛиц.Начислено КАК Начислено,
	|	ТаблицаФизическихЛиц.НДФЛ КАК НДФЛ,
	|	ТаблицаФизическихЛиц.ВС КАК ВС,
	|	ТаблицаФизическихЛиц.КодДохода,
	|	ТаблицаФизическихЛиц.КодДоходаВС,
	|	ТаблицаФизическихЛиц.НДФЛ + ТаблицаФизическихЛиц.ВС КАК Сумма,
	|	ТаблицаФизическихЛиц.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций) КАК Подразделение,
	|	ТаблицаФизическихЛиц.КВыплате КАК КВыплате
	|ИЗ
	|	ВТТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаДивиденды");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаФизическихЛиц.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ТаблицаФизическихЛиц.ЭтоСотрудник
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДивидендыСотрудников)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.Дивиденды)
	|	КОНЕЦ КАК Начисление,
	|	ТаблицаФизическихЛиц.Ссылка КАК ДокументОснование,
	|	ТаблицаФизическихЛиц.Начислено КАК Сумма
	|ИЗ
	|	ВТТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаНачисления");
	
	// Используется для получения настроек начисления дивидендов при отражении движений в регистр
	ТекстЗапросаРеквизитов = 
	"ВЫБРАТЬ
	|	&Ссылка,
	|	&Организация,
	|	&ДатаВыплаты";
	ТекстыЗапроса.Добавить(ТекстЗапросаРеквизитов, "ТаблицаПараметрыНачисленияДивидендов");
	
	Возврат "";
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ДатаДокумента 				 		КАК Период,
	|	&Организация 				 		КАК Организация,
	|	НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаДокумента 				 		КАК Период,
	|	&Организация 				 		КАК Организация,
	|	НАЧАЛОПЕРИОДА(&ДатаВыплаты, ДЕНЬ) 	КАК ДатаОтражения
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ) <> НАЧАЛОПЕРИОДА(&ДатаВыплаты, ДЕНЬ)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Справка о списании депонированной зарплаты.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.НачислениеДивидендов";
	КомандаПечати.Идентификатор = "ПФ_MXL_СправкаОДивидендах";
	КомандаПечати.Представление = НСтр("ru='Справка о дивидендах';uk='Довідка про дивіденди'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;

КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НужноПечататьСправку = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОДивидендах");
	
	Если НужноПечататьСправку Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаОДивидендах",
			НСтр("ru='Справка о дивидендах';uk='Довідка про дивіденди'"), ПечатьСправки(МассивОбъектов, ОбъектыПечати,ПараметрыВывода), ,
			"Документ.ДивидендыФизическимЛицам.ПФ_MXL_СправкаОРегистрацииПрочихДоходов",,Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСправки(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)

	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДивидендах";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	ДанныеПечатиОбъектов = ДанныеПечатиДокументов(МассивОбъектов);
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.НачислениеДивидендов.ПФ_MXL_СправкаОДивидендах", КодЯзыкаПечать);
	
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакетаШапкаФЛ = Макет.ПолучитьОбласть("ШапкаФЛ");
	ОбластьМакетаСтрокаФЛ = Макет.ПолучитьОбласть("СтрокаФЛ");
	ОбластьМакетаИтогФЛ = Макет.ПолучитьОбласть("ИтогФЛ");
	
	ОбластьМакетаШапкаЮЛ = Макет.ПолучитьОбласть("ШапкаЮЛ");
	ОбластьМакетаСтрокаЮЛ = Макет.ПолучитьОбласть("СтрокаЮЛ");
	ОбластьМакетаИтогЮЛ = Макет.ПолучитьОбласть("ИтогЮЛ");
	
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	Для Каждого ДанныеПечати Из ДанныеПечатиОбъектов Цикл
		
		ТекущаяТаблицаФЛ = Истина;
		
		ДанныеДокумента = ДанныеПечати.Значение;
		
		// Документы нужно выводить на разных страницах.
		Если Не ПервыйДокумент Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
		
		ОбластьМакетаШапка.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаШапка);
		
		Если ДанныеДокумента.ТабличнаяЧастьФЛ.Количество() > 0 Тогда
			
			ДокументРезультат.Вывести(ОбластьМакетаШапкаФЛ);
			
			СтруктураИтогаФЛ = Новый Структура();
			СтруктураИтогаФЛ.Вставить("НДФЛ",          0);
			СтруктураИтогаФЛ.Вставить("ВС",          0);
			СтруктураИтогаФЛ.Вставить("Начислено",     0);
			СтруктураИтогаФЛ.Вставить("СуммаВычета",   0);
			СтруктураИтогаФЛ.Вставить("СуммаКВыплате", 0);
			
			Для каждого ДанныеСотрудника Из ДанныеДокумента.ТабличнаяЧастьФЛ Цикл
				
				МассивВыводимыхОбластей = Новый Массив;
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаСтрокаФЛ);
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаИтогФЛ);
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаПодвал);
				
				Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, МассивВыводимыхОбластей) Тогда
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ДокументРезультат.Вывести(ОбластьМакетаШапкаФЛ);
				КонецЕсли;
				
				ОбластьМакетаСтрокаФЛ.Параметры.Заполнить(ДанныеСотрудника);
				
				ДокументРезультат.Вывести(ОбластьМакетаСтрокаФЛ);
				
				СтруктураИтогаФЛ.Начислено     = СтруктураИтогаФЛ.Начислено + ДанныеСотрудника.Начислено;
				СтруктураИтогаФЛ.НДФЛ          = СтруктураИтогаФЛ.НДФЛ + ДанныеСотрудника.НДФЛ;
				СтруктураИтогаФЛ.ВС            = СтруктураИтогаФЛ.ВС + ДанныеСотрудника.ВС;
				СтруктураИтогаФЛ.СуммаКВыплате = СтруктураИтогаФЛ.СуммаКВыплате + ДанныеСотрудника.СуммаКВыплате;
				
			КонецЦикла;
			
			ОбластьМакетаИтогФЛ.Параметры.Заполнить(СтруктураИтогаФЛ);
			ДокументРезультат.Вывести(ОбластьМакетаИтогФЛ);
			
		КонецЕсли;
		
		Если ДанныеДокумента.ТабличнаяЧастьЮЛ.Количество() > 0 Тогда
			
			ДокументРезультат.Вывести(ОбластьМакетаШапкаЮЛ);
			
			СтруктураИтогаЮЛ = Новый Структура("Начислено, СуммаКВыплате", 0, 0);
			Для каждого ДанныеСотрудника Из ДанныеДокумента.ТабличнаяЧастьЮЛ Цикл
				
				МассивВыводимыхОбластей = Новый Массив;
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаСтрокаЮЛ);
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаИтогЮЛ);
				МассивВыводимыхОбластей.Добавить(ОбластьМакетаПодвал);
				
				Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, МассивВыводимыхОбластей) Тогда
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ДокументРезультат.Вывести(ОбластьМакетаШапкаЮЛ);
				КонецЕсли;
				
				ОбластьМакетаСтрокаЮЛ.Параметры.Заполнить(ДанныеСотрудника);
				
				ДокументРезультат.Вывести(ОбластьМакетаСтрокаЮЛ);
				
				СтруктураИтогаЮЛ.Начислено     = СтруктураИтогаЮЛ.Начислено + ДанныеСотрудника.Начислено;
				СтруктураИтогаЮЛ.СуммаКВыплате = СтруктураИтогаЮЛ.СуммаКВыплате + ДанныеСотрудника.СуммаКВыплате;
				
			КонецЦикла;
			
			ОбластьМакетаИтогЮЛ.Параметры.Заполнить(СтруктураИтогаЮЛ);
			ДокументРезультат.Вывести(ОбластьМакетаИтогЮЛ);
			
		КонецЕсли;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДокумента);
		ДокументРезультат.Вывести(ОбластьМакетаПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.Ссылка);
				
	КонецЦикла;
	
	Возврат ДокументРезультат;
		
КонецФункции

Функция ДанныеПечатиДокументов(МассивОбъектов)
	
	ДанныеПечатиОбъектов = Новый Соответствие;
	
	ВалютаУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоФизическоеЛицо,
		|	ДивидендыФизическимЛицам.Ссылка.Организация.НаименованиеПолное КАК Организация,
		|	ДивидендыФизическимЛицам.Ссылка КАК Ссылка,
		|	ДивидендыФизическимЛицам.Ссылка.Номер,
		|	НАЧАЛОПЕРИОДА(ДивидендыФизическимЛицам.Ссылка.Дата, ДЕНЬ) КАК Период,
		|	ДивидендыФизическимЛицам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДивидендыФизическимЛицам.НДФЛ КАК НДФЛ,
		|	ДивидендыФизическимЛицам.ВС КАК ВС,
		|	ДивидендыФизическимЛицам.Начислено КАК Начислено,
		|	ДивидендыФизическимЛицам.КВыплате КАК СуммаКВыплате,
		|	ДивидендыФизическимЛицам.Ссылка.Ответственный КАК Исполнитель,
		|	"""" КАК ДолжностьИсполнителя
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.НачислениеДивидендов.ФизическиеЛица КАК ДивидендыФизическимЛицам
		|ГДЕ
		|	ДивидендыФизическимЛицам.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК ЭтоФизическоеЛицо,
		|	ДивидендыЮридическимЛицам.Ссылка.Организация.НаименованиеПолное КАК Организация,
		|	ДивидендыЮридическимЛицам.Ссылка КАК Ссылка,
		|	ДивидендыЮридическимЛицам.Ссылка.Номер,
		|	НАЧАЛОПЕРИОДА(ДивидендыЮридическимЛицам.Ссылка.Дата, ДЕНЬ) КАК Период,
		|	ДивидендыЮридическимЛицам.Контрагент КАК ФизическоеЛицо,
		|	0 КАК НДФЛ,
		|	0 КАК ВС,
		|	ДивидендыЮридическимЛицам.Начислено КАК Начислено,
		|	ДивидендыЮридическимЛицам.КВыплате КАК СуммаКВыплате,
		|	ДивидендыЮридическимЛицам.Ссылка.Ответственный КАК Исполнитель,
		|	"""" КАК ДолжностьИсполнителя
		|ИЗ
		|	Документ.НачислениеДивидендов.ЮридическиеЛица КАК ДивидендыЮридическимЛицам
		|ГДЕ
		|	ДивидендыЮридическимЛицам.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц, "ВТДанныеДокументов");
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВТ, Истина, "ФИОПолные");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокументов.ЭтоФизическоеЛицо КАК ЭтоФизическоеЛицо,
		|	ДанныеДокументов.Организация КАК Организация,
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Номер КАК Номер,
		|	ДанныеДокументов.Период КАК Дата,
		|	ЕСТЬNULL(КадровыеДанныеФизическихЛиц.ФИОПолные, ДанныеДокументов.ФизическоеЛицо) КАК Акционер,
		|	ДанныеДокументов.НДФЛ КАК НДФЛ,
		|	ДанныеДокументов.ВС КАК ВС,	
		|	ДанныеДокументов.Начислено КАК Начислено,
		|	ДанныеДокументов.СуммаКВыплате КАК СуммаКВыплате,
		|	ЕСТЬNULL(ДанныеДокументов.Исполнитель, """") КАК Исполнитель,
		|	ДанныеДокументов.ДолжностьИсполнителя КАК ДолжностьИсполнителя
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
		|		ПО ДанныеДокументов.Период = КадровыеДанныеФизическихЛиц.Период
		|			И ДанныеДокументов.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
		|ИТОГИ
		|	МАКСИМУМ(Организация),
		|	МАКСИМУМ(Номер),
		|	МАКСИМУМ(Дата),
		|	СУММА(НДФЛ),
		|	СУММА(ВС),
		|	СУММА(Начислено),
		|	СУММА(СуммаКВыплате),
		|	МАКСИМУМ(Исполнитель),
		|	МАКСИМУМ(ДолжностьИсполнителя)
		|ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	ГруппировкаПоДокументу = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ГруппировкаПоДокументу.Следующий() Цикл
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Ссылка", ГруппировкаПоДокументу.Ссылка);
		ДанныеПечати.Вставить("НазваниеОрганизации", ГруппировкаПоДокументу.Организация);
		ДанныеПечати.Вставить("ЕдиницаИзмерения", ВалютаУчета.НаименованиеПолное);
		ДанныеПечати.Вставить("ДатаДокумента", Формат(ГруппировкаПоДокументу.Дата, "ДЛФ=D"));
		ДанныеПечати.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ГруппировкаПоДокументу.Номер, Истина, Истина));
		ДанныеПечати.Вставить("Начислено", ГруппировкаПоДокументу.Начислено);
		ДанныеПечати.Вставить("НДФЛ", ГруппировкаПоДокументу.НДФЛ);
		ДанныеПечати.Вставить("ВС", ГруппировкаПоДокументу.ВС);
		ДанныеПечати.Вставить("СуммаКВыплате", ГруппировкаПоДокументу.СуммаКВыплате);
		ДанныеПечати.Вставить("ДолжностьИсполнителя", ГруппировкаПоДокументу.ДолжностьИсполнителя);
		ДанныеПечати.Вставить("ИсполнительРасшифровкаПодписи", ФизическиеЛицаЗарплатаКадры.РасшифровкаПодписи(ГруппировкаПоДокументу.Исполнитель));
		ДанныеПечати.Вставить("ТабличнаяЧастьФЛ", Новый Массив);
		ДанныеПечати.Вставить("ТабличнаяЧастьЮЛ", Новый Массив);
		
		ГруппировкаПоСотрудникам = ГруппировкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ГруппировкаПоСотрудникам.Следующий() Цикл
			
			Если ГруппировкаПоСотрудникам.ЭтоФизическоеЛицо Тогда
				
				СтрокаДанныхПечати = Новый Структура;
				СтрокаДанныхПечати.Вставить("Акционер", ГруппировкаПоСотрудникам.Акционер);
				СтрокаДанныхПечати.Вставить("Начислено", ГруппировкаПоСотрудникам.Начислено);
				СтрокаДанныхПечати.Вставить("НДФЛ", ГруппировкаПоСотрудникам.НДФЛ);
				СтрокаДанныхПечати.Вставить("ВС", ГруппировкаПоСотрудникам.ВС);	
				СтрокаДанныхПечати.Вставить("СуммаКВыплате", ГруппировкаПоСотрудникам.СуммаКВыплате);
				ДанныеПечати.ТабличнаяЧастьФЛ.Добавить(СтрокаДанныхПечати);
				
			Иначе
				
				СтрокаДанныхПечати = Новый Структура;
				СтрокаДанныхПечати.Вставить("Акционер", ГруппировкаПоСотрудникам.Акционер);
				СтрокаДанныхПечати.Вставить("Начислено", ГруппировкаПоСотрудникам.Начислено);
				СтрокаДанныхПечати.Вставить("СуммаКВыплате", ГруппировкаПоСотрудникам.СуммаКВыплате);
				ДанныеПечати.ТабличнаяЧастьЮЛ.Добавить(СтрокаДанныхПечати);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеПечатиОбъектов.Вставить(ГруппировкаПоДокументу.Ссылка, ДанныеПечати);
		
	КонецЦикла;
	
	Возврат ДанныеПечатиОбъектов;
		
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
