
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	УстановитьФункциональныеОпцииФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();	
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	СобытияФормКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФизическиеЛица

&НаКлиенте
Процедура ФизическиеЛицаФизическоеЛицоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	
	Если ИспользоватьНачислениеЗарплаты И ЗначениеЗаполнено(ТекущиеДанные.ФизическоеЛицо) Тогда
		ФизическоеЛицоПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	Иначе
		
		ТекущиеДанные.ПрочийАкционер = Ложь;
		
	КонецЕсли;
	
	ТекущиеДанные.Контрагент = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаНачисленоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("НДФЛ, ВС, КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаСтавкаНДФЛПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("НДФЛ, КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаНДФЛПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры


&НаКлиенте
Процедура ФизическиеЛицаПриИзменении(Элемент)
	ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
		УстановитьКодДоходаНДФЛ(ТекущиеДанные.ПолучитьИдентификатор());
		УстановитьКодДоходаВС(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЮридическиеЛица

&НаКлиенте
Процедура ЮридическиеЛицаПриИзменении(Элемент)
	ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ЮридическиеЛица.ТекущиеДанные;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаНачисленоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЮридическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("КВыплатеЮридическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаСтавкаНалогаНаПрибыльПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЮридическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("НалогНаПрибыль, КВыплатеЮридическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаНалогНаПрибыльПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЮридическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("КВыплатеЮридическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаКВыплатеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЮридическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("НачисленоЮридическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаВвестиЗаявкуНаПеречисление(Команда)
	СоздатьЗаявкуНаПеречислениеДенежныхСредств("ФизическиеЛица");
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВвестиЗаявкуНаПеречисление(Команда)
	СоздатьЗаявкуНаПеречислениеДенежныхСредств("ЮридическиеЛица");
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаВвестиРасходныйОрдер(Команда)
	ВвестиПлатежныйДокумент("ФизическиеЛица", "РасходныйКассовыйОрдер");
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВвестиРасходныйОрдер(Команда)
	ВвестиПлатежныйДокумент("ЮридическиеЛица", "РасходныйКассовыйОрдер");
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаВвестиСписание(Команда)
	ВвестиПлатежныйДокумент("ФизическиеЛица", "СписаниеБезналичныхДенежныхСредств");
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВвестиСписание(Команда)
	ВвестиПлатежныйДокумент("ЮридическиеЛица", "СписаниеБезналичныхДенежныхСредств");
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Не облагается НДФЛ
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ФизическиеЛицаКодДохода.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ФизическиеЛица.КодДохода");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Не облагается';uk='Не оподатковується'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Не облагается ВС
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ФизическиеЛицаКодДоходаВС.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ФизическиеЛица.КодДоходаВС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Не облагается';uk='Не оподатковується'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Не облагается налог на прибыль
	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Ставки = УчетНДФЛ.ПолучитьСтавкиНДФЛНаДату(Объект.Дата);
	ЗначенияСтавок = Новый ФиксированноеСоответствие(Ставки);
	
	ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоги()
	
	ВсегоПоДокументу = Объект.ФизическиеЛица.Итог("Начислено") + Объект.ЮридическиеЛица.Итог("Начислено");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Действия, ДанныеСтроки)
	
	Если Действия.Свойство("НачисленоФизическомуЛицу") Тогда
	КонецЕсли;
	
	Если Действия.Свойство("НачисленоЮридическомуЛицу") Тогда
		ДанныеСтроки.Начислено = ДанныеСтроки.КВыплате;
	КонецЕсли;
	
	Если Действия.Свойство("НДФЛ") Тогда
		ДанныеСтроки.НДФЛ = (ДанныеСтроки.Начислено) * ЗначенияСтавок[ДанныеСтроки.ВидСтавки];
	КонецЕсли;
	
	Если Действия.Свойство("ВС") Тогда
		ДанныеСтроки.ВС = (ДанныеСтроки.Начислено) * ЗначенияСтавок[ДанныеСтроки.ВидСтавкиВС];
	КонецЕсли;
	
	
	Если Действия.Свойство("КВыплатеФизическомуЛицу") Тогда
		ДанныеСтроки.КВыплате = ДанныеСтроки.Начислено - ДанныеСтроки.НДФЛ - ДанныеСтроки.ВС;
	КонецЕсли;
	
	Если Действия.Свойство("КВыплатеЮридическомуЛицу") Тогда
		ДанныеСтроки.КВыплате = ДанныеСтроки.Начислено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ФизическоеЛицоПриИзмененииНаСервере(Идентификатор)
	
	ДанныеСтроки = Объект.ФизическиеЛица.НайтиПоИдентификатору(Идентификатор);
	ДатаПолученияДанных = ?(ЗначениеЗаполнено(Объект.ДатаВыплаты), Объект.ДатаВыплаты, ТекущаяДатаСеанса());
	
	Сотрудники = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСтроки.ФизическоеЛицо),
		Истина,
		Объект.Организация,
		ДатаПолученияДанных);
	
	Если Сотрудники.Количество() > 0 И ЗначениеЗаполнено(Сотрудники[0].Сотрудник) Тогда
		ДанныеСтроки.ПрочийАкционер = Ложь;
	Иначе
		ДанныеСтроки.ПрочийАкционер = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СоздатьЗаявкуНаПеречислениеДенежныхСредств(ИмяТаблицы)
	
	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	
	Если Не ПроверитьВозможностьОформленияВыплат(ТекущиеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	Основание = Новый Структура();
	Основание.Вставить("ДокументОснование",         Объект.Ссылка);
	Основание.Вставить("Контрагент",                ТекущиеДанные.Контрагент);
	Основание.Вставить("СуммаДокумента",            ТекущиеДанные.КВыплате);
	Основание.Вставить("Сумма",                     ТекущиеДанные.КВыплате);
	Основание.Вставить("Организация",               Объект.Организация);
	Основание.Вставить("ХозяйственнаяОперация",     ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств"));
	Основание.Вставить("ФормаОплатыНаличная",       Истина);
	Основание.Вставить("ФормаОплатыБезналичная",    Истина);
	Основание.Вставить("СтатьяРасходов",            ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки"));
	Основание.Вставить("ЖелательнаяДатаПлатежа",    Объект.ДатаВыплаты);
	
	Основание.Вставить("СчетУчета",             ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам"));
	Если ИмяТаблицы = "ФизическиеЛица" И Не ТекущиеДанные.ПрочийАкционер Тогда
		Основание.Вставить("Субконто1",             ТекущиеДанные.ФизическоеЛицо);
	Иначе
		Основание.Вставить("Субконто1",             ТекущиеДанные.Контрагент);
	КонецЕсли;

	ОткрытьФорму("Документ.ЗаявкаНаРасходованиеДенежныхСредств.ФормаОбъекта", Новый Структура("Основание", Основание));
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПлатежныйДокумент(ИмяТаблицы, ИмяДокумента)
	
	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	
	Если Не ПроверитьВозможностьОформленияВыплат(ТекущиеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	Основание = Новый Структура();
	Основание.Вставить("ДокументОснование",         Объект.Ссылка);
	Основание.Вставить("Контрагент",                ТекущиеДанные.Контрагент);
	Основание.Вставить("Валюта",                    Объект.Валюта);
	Основание.Вставить("СуммаДокумента",            ТекущиеДанные.КВыплате);
	Основание.Вставить("Сумма",                     ТекущиеДанные.КВыплате);
	Основание.Вставить("Организация",               Объект.Организация);
	Основание.Вставить("ХозяйственнаяОперация",     ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств"));
	Основание.Вставить("СтатьяРасходов",            ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки"));
	
	Основание.Вставить("СчетУчета",             ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетыПоНачисленнымДивидендам"));
	Если ИмяТаблицы = "ФизическиеЛица" И Не ТекущиеДанные.ПрочийАкционер Тогда
		Основание.Вставить("Субконто1",             ТекущиеДанные.ФизическоеЛицо);
	Иначе
		Основание.Вставить("Субконто1",             ТекущиеДанные.Контрагент);
	КонецЕсли;
	
	ОткрытьФорму("Документ."+ ИмяДокумента + ".ФормаОбъекта", Новый Структура("Основание", Основание));
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьВозможностьОформленияВыплат(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru='Команда не может быть выполнена для указанного объекта';uk='Команда не може бути виконана для зазначеного об''єкта'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
	КонецЕсли;
	
	НепроведенныеДокументы = ОбщегоНазначенияВызовСервера.ПроверитьПроведенностьДокументов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	
	Если НепроведенныеДокументы.Количество() > 0 Тогда
		ТекстПредупреждения = НСтр("ru='Оформление выплат возможно только на основании проведенного документа';uk='Оформлення виплат можливе лише на підставі проведеного документа'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКодДоходаНДФЛ(Идентификатор)
	
	ТекущиеДанные = Объект.ФизическиеЛица.НайтиПоИдентификатору(Идентификатор);
	ТекущиеДанные.КодДохода = Справочники.ВидыДоходовНДФЛ.ВидДоходовДивиденды();
	ТекущиеДанные.ВидСтавки = ТекущиеДанные.КодДохода.ВидСтавкиРезидента;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКодДоходаВС(Идентификатор)
	
	ТекущиеДанные = Объект.ФизическиеЛица.НайтиПоИдентификатору(Идентификатор);
	Если Объект.ДатаВыплаты < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда
		ТекущиеДанные.КодДоходаВС = ТекущиеДанные.КодДохода.ОблагаетсяВоеннымСбором;
	Иначе
		ТекущиеДанные.КодДоходаВС = ТекущиеДанные.КодДохода.ОблагаетсяВоеннымСбором2021;
	КонецЕсли;	
	ТекущиеДанные.ВидСтавкиВС = ТекущиеДанные.КодДоходаВС.ВидСтавкиРезидента;
	
КонецПроцедуры
// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаВСПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	СтруктураДействий = Новый Структура("КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные)
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаКодДоходаВСПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	ФизическиеЛицаКодДоходаВСПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	СтруктураДействий = Новый Структура("ВС, КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаСервере
Процедура ФизическиеЛицаКодДоходаВСПриИзмененииНаСервере(Идентификатор)
	ТекущиеДанные = Объект.ФизическиеЛица.НайтиПоИдентификатору(Идентификатор);
	ТекущиеДанные.ВидСтавкиВС = ТекущиеДанные.КодДоходаВС.ВидСтавкиРезидента;
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаКодДоходаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	ФизическиеЛицаКодДоходаПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	СтруктураДействий = Новый Структура("НДФЛ, ВС, КВыплатеФизическомуЛицу");
	ПересчитатьСтроку(СтруктураДействий, ТекущиеДанные);
КонецПроцедуры

&НаСервере
Процедура ФизическиеЛицаКодДоходаПриИзмененииНаСервере(Идентификатор)
	
	ТекущиеДанные = Объект.ФизическиеЛица.НайтиПоИдентификатору(Идентификатор);
	ТекущиеДанные.ВидСтавки = ТекущиеДанные.КодДохода.ВидСтавкиРезидента;
	УстановитьКодДоходаВС(Идентификатор);

КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ПараметрыФО = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	УстановитьФункциональныеОпцииФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	Ставки = УчетНДФЛ.ПолучитьСтавкиНДФЛНаДату(Объект.Дата);
	ЗначенияСтавок = Новый ФиксированноеСоответствие(Ставки);

КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти
