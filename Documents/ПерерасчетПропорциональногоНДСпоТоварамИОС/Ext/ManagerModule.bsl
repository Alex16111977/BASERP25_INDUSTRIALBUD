#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР()
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчетуУКР

// Возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();
    
#Область КорректировкаНаЗатраты // (Дт 9X :: Кт 6435)
	КорректировкаНаЗатраты = "
	|ВЫБРАТЬ //// Корректировка на затраты (Дт 9X :: Кт 6435)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Операция.СуммаКорректировкиБУ КАК Сумма,
	|	Операция.СуммаКорректировкиБУ / ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриПерерасчетеПропорциональногоНДС) КАК АналитикаУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Организация КАК СубконтоДт1, 
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриПерерасчетеПропорциональногоНДС) КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	Операция.СуммаКорректировкиБУ <> 0
	|";   

	КорректировкаНаЗатраты = СтрШаблон(КорректировкаНаЗатраты, 
		НСтр("ru='НДС: корректировка пропорц. НДС по товарам/услугам и ОС';uk='ПДВ: коригування пропорц. ПДВ за товарами/послугами та ОЗ'",ЯзыкСодержания));

#КонецОбласти 

#Область КорректировкаАвансовПоставщикам // (Дт 6435 :: Кт 6442)
	КорректировкаАвансовПоставщикам = "
	|ВЫБРАТЬ //// Корректировка авансов постащикам (Дт 6435 :: Кт 6442)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаКорректировкиНДС КАК Сумма,
	|	Строки.СуммаКорректировкиНДС / ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|                                    
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалоговыйКредитНеподтвержденный) КАК СчетКт,
    |   Аналитика.Контрагент КАК СубконтоКт1,
    |	Аналитика.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
    |
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.АвансыПоставщикам КАК Строки
	|	ПО 
	|		Операция.Ссылка = Строки.Ссылка
	|	
    |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
    |       РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
    |	ПО 
    |       Строки.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	Строки.СуммаКорректировкиНДС <> 0
	|"; 

	КорректировкаАвансовПоставщикам = СтрШаблон(КорректировкаАвансовПоставщикам, 
		НСтр("ru='НДС: корректировка пропорц. НДС по предоплатам за товары/услуги';uk='ПДВ: коригування пропорц. ПДВ за передоплатою за товари/послуги'",ЯзыкСодержания));

#КонецОбласти 

#Область КорректировкаНалоговогоКредита // (Дт 6412 :: Кт 6443)
	КорректировкаНалоговогоКредита = "
	|ВЫБРАТЬ //// Корректировка налогового кредита (Дт 6412 :: Кт 6443)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаКорректировкиНДС КАК Сумма,
	|	Строки.СуммаКорректировкиНДС / ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоНДС) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|                                    
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговогоКредита) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
    |
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПараметрыПерерасчетаОС КАК Строки
	|	ПО 
	|		Операция.Ссылка = Строки.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	Строки.СуммаКорректировкиНДС <> 0
	|";     

	КорректировкаНалоговогоКредита = СтрШаблон(КорректировкаНалоговогоКредита, 
		НСтр("ru='НДС: корректировка налогового кредита по пропорц. НДС';uk='ПДВ: коригування податкового кредиту за пропорц. ПДВ'",ЯзыкСодержания));

#КонецОбласти 

#Область КорректировкаПоСписаннымНеоборотнымАктивам // (Дт 9X :: Кт 6443 сторно)
	КорректировкаПоСписаннымНеоборотнымАктивам = "
	|ВЫБРАТЬ //// Корректировка по списанным ОС/НМА/малоценке (Дт 9X :: Кт 6443 сторно)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	-Строки.СуммаКорректировкиНДС КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриПерерасчетеПропорциональногоНДС) КАК АналитикаУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Организация КАК СубконтоДт1, 
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриПерерасчетеПропорциональногоНДС) КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговогоКредита) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
    |
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПараметрыПерерасчетаОС КАК Строки
	|	ПО 
	|		Операция.Ссылка = Строки.Ссылка
    |
	|ГДЕ
	|	Операция.СуммаКорректировкиНДС <> 0
	|";

	КорректировкаПоСписаннымНеоборотнымАктивам = СтрШаблон(КорректировкаПоСписаннымНеоборотнымАктивам, 
		НСтр("ru='НДС: корректировка пропорц. НДС по списанным необоротным активам';uk='ПДВ: коригування пропорц. ПДВ по списаних необоротних активах'",ЯзыкСодержания));
    
#КонецОбласти 

	Возврат КорректировкаНаЗатраты
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаАвансовПоставщикам
        + " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаНалоговогоКредита
        + " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаПоСписаннымНеоборотнымАктивам
		;

КонецФункции

#КонецОбласти 	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("УчетНДС");  
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСуммыКорректировокПропорциональногоНДС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
    
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
    
    Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                   КАК Ссылка,
	|	ДанныеДокумента.Дата                     КАК Период,
    |
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ДанныеДокумента.СпецРежимНалогообложения КАК СпецРежимНалогообложения,
	|	ДанныеДокумента.СуммаКорректировкиНДС 	 КАК СуммаКорректировкиНДС
	|
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
    
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
    
	Запрос.УстановитьПараметр("Период",                             Реквизиты.Период);
    Запрос.УстановитьПараметр("Граница",                            Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",                        Реквизиты.Организация);
	Запрос.УстановитьПараметр("СпецРежимНалогообложения",           Реквизиты.СпецРежимНалогообложения);
    
    Запрос.УстановитьПараметр("СтатьяРасходовПриПерерасчетеПропорциональногоНДС", ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриПерерасчетеПропорциональногоНДС);
    Запрос.УстановитьПараметр("НалоговоеНазначениеОблагаемое",       Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая);
    Запрос.УстановитьПараметр("НалоговоеНазначениеПропорциональное", Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально);
    Запрос.УстановитьПараметр("ФормироватьФинансовыйРезультат",      ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат"));
    Запрос.УстановитьПараметр("ВидПоставки",                         Перечисления.ВидыПоставки.Поставка);
    
    Запрос.УстановитьПараметр("ГраничнаяДатаНачалаИспользованияОС",  НачалоГода(ДобавитьМесяц(Реквизиты.Период, - 36)));
	Запрос.УстановитьПараметр("СуммаКорректировкиНДС",  			 Реквизиты.СуммаКорректировкиНДС);	
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",	     Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     	 Константы.ВалютаУправленческогоУчета.Получить());
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры


Процедура УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр") Тогда
		Возврат;
	КонецЕсли;
	
	КоэффициентПересчетаВВалютуУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
		Запрос.Параметры.ВалютаРегламентированногоУчета, 
	    Запрос.Параметры.ВалютаУправленческогоУчета, 
	    Запрос.Параметры.Период
	);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", КоэффициентПересчетаВВалютуУпр);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
    
	ИмяРегистра = "ВтИсходныеПрочиеРасходы"; 
	
	УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
    // Сумма корректировки затрат (БУ) - приход 
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС КАК СтатьяРасходов,
    |	&Организация КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
    |   &НалоговоеНазначениеОблагаемое КАК НалоговоеНазначение,
    |
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,              
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКорректировкиБУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|	ДанныеДокумента.СуммаКорректировкиБУ КАК СуммаСНДСРегл,
    |	ДанныеДокумента.СуммаКорректировкиБУ КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница, 
    |
    |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ ДанныеДокумента.СуммаКорректировкиБУ = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
    // Сумма корректировки затрат (БУ) - расход, если не формируем финансовый результат
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС КАК СтатьяРасходов,
    |	&Организация КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
    |   &НалоговоеНазначениеОблагаемое КАК НалоговоеНазначение,
    |
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКорректировкиБУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|	ДанныеДокумента.СуммаКорректировкиБУ КАК СуммаСНДСРегл,
    |	ДанныеДокумента.СуммаКорректировкиБУ КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница, 
    |
    |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ ДанныеДокумента.СуммаКорректировкиБУ = 0
	|	И НЕ &ФормироватьФинансовыйРезультат    
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// По ТЧ «Перерасчет ОС» для всех ОС/НМА/малоценки - приход
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС КАК СтатьяРасходов,
    |	&Организация КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
    |   &НалоговоеНазначениеОблагаемое КАК НалоговоеНазначение,
    |
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС КАК СуммаСНДСРегл,
    |	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница, 
    |
    |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПараметрыПерерасчетаОС КАК ПараметрыПерерасчетаОС
	|ГДЕ
	|	ПараметрыПерерасчетаОС.Ссылка = &Ссылка
	|	И НЕ ПараметрыПерерасчетаОС.СуммаКорректировкиНДС = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// По ТЧ «Перерасчет ОС» для всех ОС/НМА/малоценки - расход, если не формируем финансовый результат
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС КАК СтатьяРасходов,
    |	&Организация КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
    |   &НалоговоеНазначениеОблагаемое КАК НалоговоеНазначение,
    |
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
	|	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС КАК СуммаСНДСРегл,
    |	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС КАК СуммаБезНДСРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница, 
    |
    |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПараметрыПерерасчетаОС КАК ПараметрыПерерасчетаОС
	|ГДЕ
	|	ПараметрыПерерасчетаОС.Ссылка = &Ссылка
	|	И НЕ ПараметрыПерерасчетаОС.СуммаКорректировкиНДС = 0
    |	И НЕ &ФормироватьФинансовыйРезультат   
    |";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;           
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции           

Функция ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;                    
	
	УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос);
	
	ТекстЗапроса = "
    // Сумма корректировки затрат (БУ) - приход 
	|ВЫБРАТЬ
	|	&Период 													КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	&Организация 												КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО 												КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО 												КАК НаправлениеДеятельности,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС 			КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК АналитикаДоходов,
	|	&Организация                                      			КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                   				КАК АналитикаАктивовПассивов,
	|
	|	НЕОПРЕДЕЛЕНО                        						КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО              									КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                								КАК КорАналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НДС)                    	КАК КорАналитикаАктивовПассивов,
    |                          
	|	0 															КАК Сумма,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКорректировкиБУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ДанныеДокумента.СуммаКорректировкиБУ 						КАК СуммаРегл,
	|	&ВалютаРегламентированногоУчета 							КАК Валюта,
	|	ДанныеДокумента.СуммаКорректировкиБУ 						КАК СуммаВВалюте
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ ДанныеДокумента.СуммаКорректировкиБУ = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// По ТЧ «Перерасчет ОС» для всех ОС/НМА/малоценки
	|ВЫБРАТЬ
	|	&Период 													КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерерасчетПропорциональногоНДС) КАК ХозяйственнаяОперация,
	|	&Организация 												КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО 												КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО 												КАК НаправлениеДеятельности,
	|	&СтатьяРасходовПриПерерасчетеПропорциональногоНДС 			КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК АналитикаДоходов,
	|	&Организация                                      			КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                   				КАК АналитикаАктивовПассивов,
	|
	|	НЕОПРЕДЕЛЕНО                        						КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО              									КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      			КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                								КАК КорАналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НДС)                    	КАК КорАналитикаАктивовПассивов,
    |                          
	|	0 															КАК Сумма,
	|	ВЫРАЗИТЬ(-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС 				КАК СуммаРегл,
	|	&ВалютаРегламентированногоУчета 							КАК Валюта,
	|	-ПараметрыПерерасчетаОС.СуммаКорректировкиНДС 				КАК СуммаВВалюте
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПараметрыПерерасчетаОС КАК ПараметрыПерерасчетаОС
	|ГДЕ
	|	ПараметрыПерерасчетаОС.Ссылка = &Ссылка
	|	И НЕ ПараметрыПерерасчетаОС.СуммаКорректировкиНДС = 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов(Запрос, ТекстыЗапроса, Регистры)
    
	ИмяРегистра = "НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
    КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
    |	&Период КАК Период,
    |
	|	АвансыПоставщикам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	АвансыПоставщикам.Сделка КАК ОбъектРасчетов,	
	|	&ВидПоставки КАК ВидПоставки,
	|	АвансыПоставщикам.СтавкаНДС КАК СтавкаНДС,
	|	&НалоговоеНазначениеПропорциональное КАК НалоговоеНазначение,
    |
	|	0 КАК СуммаВзаиморасчетов,
	|	АвансыПоставщикам.СуммаКорректировкиНДС КАК СуммаНДСКредит,
	|
	|	НЕОПРЕДЕЛЕНО КАК ДатаВходящегоНалоговогоДокумента,
    |	НЕОПРЕДЕЛЕНО КАК ДокументПоставки
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.АвансыПоставщикам КАК АвансыПоставщикам
	|ГДЕ
	|	АвансыПоставщикам.Ссылка = &Ссылка
	|	И НЕ АвансыПоставщикам.СуммаКорректировкиНДС = 0
    |
    |";
    
    ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыКорректировокПропорциональногоНДС(Запрос, ТекстыЗапроса, Регистры)
    
    ИмяРегистра = "СуммыКорректировокПропорциональногоНДС";
    
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
    КонецЕсли; 
    
    ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяКорректировкаУсловнойПродажи) КАК ВидУсловнойПродажи,
	|	&СпецРежимНалогообложения КАК СпецРежимНалогообложения,
	|	-&СуммаКорректировкиНДС КАК СуммаКорректировкиНДС
	|ГДЕ
    |	&СуммаКорректировкиНДС <> 0
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//     КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для   изменения.
//         См. описание 1 параметра процедуры   ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//     Параметры - Структура - Вспомогательные параметры. Для чтения.
//         См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

#КонецОбласти 	


#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ПерерасчетПропорциональногоНДСпоТоварамИОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("263d6820-757a-4903-9531-7f0321ee73ee");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ПерерасчетПропорциональногоНДСпоТоварамИОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС';uk='Заповнює реквізит Ставка ПДВ'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ПерерасчетПропорциональногоНДСпоТоварамИОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПерерасчетПропорциональногоНДСпоТоварамИОС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ПерерасчетПропорциональногоНДСпоТоварамИОС
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта        = "Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект(); // ДокументОбъект
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				ДокументОбъект.Архивный = Истина;
				ОбъектИзменен = Истина;
				
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("АвансыПоставщикам");
				
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				ПараметрыПоискаОбъектаРасчетов = ОбъектыРасчетовСервер.ПолучитьПараметрыОбъектаРасчетов();
				
				ПараметрыПоискаОбъектаРасчетов.ТипРасчетов  = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ПараметрыПоискаОбъектаРасчетов.Организация  = ДокументОбъект.Организация;
				
				Для Каждого СтрокаАвансов Из ДокументОбъект.АвансыПоставщикам Цикл  
					
					Если НЕ ЗначениеЗаполнено(СтрокаАвансов.Сделка) И ЗначениеЗаполнено(СтрокаАвансов.УдалитьСделка) Тогда
						
						ПараметрыПоискаОбъектаРасчетов.Партнер      = СтрокаАвансов.АналитикаУчетаПоПартнерам.Партнер;
						ПараметрыПоискаОбъектаРасчетов.Контрагент   = СтрокаАвансов.АналитикаУчетаПоПартнерам.Контрагент;
						ПараметрыПоискаОбъектаРасчетов.Объект = СтрокаАвансов.УдалитьСделка;
						
						НовыйОбъектРасчетов = ОбъектыРасчетовСервер.НайтиНовыйОбъектРасчетов(ПараметрыПоискаОбъектаРасчетов);
						Если НовыйОбъектРасчетов = Неопределено Тогда
							ВызватьИсключение(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Не найден объект расчетов для источника данных: %1';uk='Не знайдений об''єкт розрахунків для джерела даних: %1'"),
							СтрокаАвансов.УдалитьСделка
							)
							);
						ИначеЕсли СтрокаАвансов.Сделка <> НовыйОбъектРасчетов Тогда
							СтрокаАвансов.Сделка = НовыйОбъектРасчетов;            
							ОбъектИзменен = Истина;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
			
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#КонецЕсли

