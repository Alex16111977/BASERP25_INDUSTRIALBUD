
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОпцияИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДанныеЗаполнения);
	
	ДатаУчетнойПолитики = КонецМесяца(Дата) + 1;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
		ЭтаФорма, Организация, ДатаУчетнойПолитики);
		
	ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, ДатаУчетнойПолитики);
	
	Если Параметры.ЭтоНовый И НЕ Параметры.Копирование Тогда
		
		
		
		СчетУчетаДооценокОС = ПланыСчетов.Хозрасчетный.ДооценкаОсновныхСредств;
		МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.Линейный;
		НачислятьАмортизациюБУ = Истина;
		Если ПлательщикНалогаНаПрибыль Тогда
			НачислятьАмортизациюНУ       = Истина;  
			СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииОС.Линейный;
			НалоговоеНазначение          = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
		Иначе
			НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность;
		КонецЕсли;
		
		
		
	КонецЕсли;
	
	ЗаголовокНакопленнаяАмортизация = НСтр("ru='Собственные средства, остаток на счете %1:';uk='Власні кошти, залишок на рахунку %1:'");
	ЗаголовокТекущаяСтоимость = НСтр("ru='Собственные средства, остаток на счете %1:';uk='Власні кошти, залишок на рахунку %1:'");
	
	Заголовок = НСтр("ru='Основные средства: %1';uk='Основні засоби: %1'");
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Заголовок, ?(Параметры.ЭтоНовый, НСтр("ru='Новая строка';uk='Новий рядок'"), ОсновноеСредство));
	
	СрокИспользованияБУСохраненный = СрокИспользованияБУ;
	Элементы.СрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияНУ);
	
	
	ОстаточнаяСтоимостьБУ = ТекущаяСтоимостьБУ - НакопленнаяАмортизацияБУ;
	ОстаточнаяСтоимостьНУ = ТекущаяСтоимостьНУ - НакопленнаяАмортизацияНУ;
	
	
	
	Элементы.ДекорацияЗаголовокНУ.Видимость = ПлательщикНалогаНаПрибыль;
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ОбновитьСвойстваЭлементовФормы();
	
	АдресМестонахожденияЗначенияПолей = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
		АдресМестонахождения,
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	
	ПараметрыВыбораСтатейИАналитик = ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Модифицированность Тогда
		
		Отказ = Истина;
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ЗавершитьРедактирование", ЭтаФорма),
			НСтр("ru='Данные были изменены. Сохранить изменения?';uk='Дані були змінені. Зберегти зміни?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Результат=Неопределено, ДополнительныеСведения=Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Результат = КодВозвратаДиалога.Да;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если ПроверитьЗаполнение() Тогда
			Модифицированность = Ложь;
			Закрыть(РеквизитыРедактируемыеВОтдельнойФорме());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ОчиститьНеИспользуемыеРеквизиты();
	
	НепроверяемыеРеквизиты = Новый Массив;
	НепроверяемыеРеквизиты.Добавить("АналитикаРасходовАмортизации");
	
	
	Если НЕ НачислятьАмортизациюБУ Тогда
		НепроверяемыеРеквизиты.Добавить("СтатьяРасходовАмортизации");
		НепроверяемыеРеквизиты.Добавить("МетодНачисленияАмортизацииБУ");
		НепроверяемыеРеквизиты.Добавить("СрокИспользованияБУ");
		НепроверяемыеРеквизиты.Добавить("ОбъемНаработкиБУ");
		НепроверяемыеРеквизиты.Добавить("ПоказательНаработки");
	Иначе
		Если МетодНачисленияАмортизацииБУ =  Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции Тогда
			
			НепроверяемыеРеквизиты.Добавить("СрокИспользованияБУ");
			
		ИначеЕсли (МетодНачисленияАмортизацииБУ =  Перечисления.СпособыНачисленияАмортизацииОС._100)
					ИЛИ (МетодНачисленияАмортизацииБУ =  Перечисления.СпособыНачисленияАмортизацииОС._50_50) Тогда
					
			НепроверяемыеРеквизиты.Добавить("СрокИспользованияБУ");
			НепроверяемыеРеквизиты.Добавить("ОбъемНаработкиБУ");
			НепроверяемыеРеквизиты.Добавить("ПоказательНаработки");
			
		Иначе
			
			НепроверяемыеРеквизиты.Добавить("ОбъемНаработкиБУ");
			НепроверяемыеРеквизиты.Добавить("ПоказательНаработки");
			
		КонецЕсли;
	КонецЕсли;
		
	Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата) Тогда
		
		НепроверяемыеРеквизиты.Добавить("СрокИспользованияНУ");
		НепроверяемыеРеквизиты.Добавить("СпособНачисленияАмортизацииНУ");
		
	ИначеЕсли НЕ НачислятьАмортизациюНУ
		ИЛИ (СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
		ИЛИ (СпособНачисленияАмортизацииНУ =  Перечисления.СпособыНачисленияАмортизацииОС._100)
		ИЛИ (СпособНачисленияАмортизацииНУ =  Перечисления.СпособыНачисленияАмортизацииОС._50_50) Тогда
		
		НепроверяемыеРеквизиты.Добавить("СрокИспользованияНУ");   
		НепроверяемыеРеквизиты.Добавить("СпособНачисленияАмортизацииНУ");
		
	КонецЕсли;
	
	
	
	Если Не ЕстьСобытияМодернизации Тогда
		
		НепроверяемыеРеквизиты.Добавить("ДатаМодернизации");
		
	КонецЕсли;
	
    Если  Не ЕстьСобытияМодернизации Тогда
			
		НепроверяемыеРеквизиты.Добавить("СтоимостьДляВычисленияАмортизации");
		
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновноеСредствоПриИзменении(Элемент)
	
	ОсновноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОсновноеСредствоПриИзмененииНаСервере()
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ИнвентарныйНомер", "Код");
	РеквизитыОбъектаУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОсновноеСредство, СтруктураРеквизитов);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыОбъектаУчета);
	
	ОбновитьСвойстваЭлементовФормы("ОсновноеСредство");
	
КонецПроцедуры

#Область ОбработчикиСобытийСтраницыУчет


&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("СчетУчета");
	
КонецПроцедуры

&НаСервере
Процедура СчетУчетаПриИзмененииНаСервере()
		
	Если СчетУчета = ПланыСчетов.Хозрасчетный.ЗемельныеУчастки Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа1;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.КапитальныеЗатратыПоУлучшениюЗемель Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа2;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДомаИСооружения Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа3;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МашиныИОборудование Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа4;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ТранспортныеСредства Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа5;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ИнструментыПриборыИИнвентарь Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа6;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.Животные Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа7;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МноголетниеНасаждения Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа8;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДругиеОсновныеСредства Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа9;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.БиблиотечныеФондыИндивидуально Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа10;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыИндивидуально Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа11;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ВременныеНетитульныеСооружения Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа12;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПриродныеРесурсы Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа13;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ИнвентарнаяТара Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа14;
	КонецЕсли; 

КонецПроцедуры


&НаКлиенте
Процедура СчетУчетаЦФПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("СчетУчетаЦФ");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетАмортизацииПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("СчетАмортизации");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетАмортизацииЦФПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("СчетАмортизацииЦФ");
	
КонецПроцедуры


&НаКлиенте
Процедура ДатаПринятияКУчетуПриИзменении(Элемент)
	
	Возврат;
	
		
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияПриИзменении(Элемент)
	
	АдресМестонахожденияЗначенияПолей = ЗначенияПолейКонтактнойИнформацииСервер(АдресМестонахождения,
		ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияОчистка(Элемент, СтандартнаяОбработка)
	
	АдресМестонахожденияЗначенияПолей = "";
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(
		УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
			ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПартнера"),
			АдресМестонахожденияЗначенияПолей),
		ЭтотОбъект,
		Новый ОписаниеОповещения("АдресМестонахожденияЗавершениеВыбора", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияЗавершениеВыбора(РезультатОткрытияФормы, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(РезультатОткрытияФормы)<>Тип("Структура") Тогда
		// не было изменений в данных
		Возврат;
	КонецЕсли;
	
	АдресМестонахожденияЗначенияПолей = РезультатОткрытияФормы.КонтактнаяИнформация;
	АдресМестонахождения = РезультатОткрытияФормы.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаОСПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("НалоговаяГруппаОС");	
КонецПроцедуры

&НаКлиенте
Процедура НалоговоеНазначениеПриИзменении(Элемент)
	Если НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность") Тогда
		ТекущаяСтоимостьНУ = 0;
		НакопленнаяАмортизацияНУ  = 0;
		ПервоначальнаяСтоимостьНУ  = 0;
		СтоимостьДляВычисленияАмортизацииНУ  = 0;
		ОстаточнаяСтоимостьНУ  = 0;
		
		СрокИспользованияНУ = 0; 
		НачислятьАмортизациюНУ = Ложь;
		СпособНачисленияАмортизацииНУ = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС.ПустаяСсылка");
		Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияНУ)
	КонецЕсли;
	ОбновитьСвойстваЭлементовФормы("НалоговоеНазначение");
КонецПроцедуры   


#КонецОбласти

#Область ОбработчикиСобытийСтраницыСтоимость

&НаКлиенте
Процедура ПервоначальнаяСтоимостьОтличаетсяПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("ПервоначальнаяСтоимостьОтличается");
	
	Если Не ПервоначальнаяСтоимостьОтличается Тогда
		
		ПервоначальнаяСтоимостьБУ = ТекущаяСтоимостьБУ;
		ПервоначальнаяСтоимостьНУ = ТекущаяСтоимостьНУ;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьДляВычисленияАмортизацииОтличаетсяПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("СтоимостьДляВычисленияАмортизацииОтличается");
	
	Если Не СтоимостьДляВычисленияАмортизацииОтличается Тогда
		СтоимостьДляВычисленияАмортизации = ТекущаяСтоимостьБУ;
		СтоимостьДляВычисленияАмортизацииНУ = ТекущаяСтоимостьНУ;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьПервоначальнуюСтоимость()
	
	Если Не ПервоначальнаяСтоимостьОтличается Тогда
		
		ПервоначальнаяСтоимостьБУ = ТекущаяСтоимостьБУ;
		ПервоначальнаяСтоимостьНУ = ТекущаяСтоимостьНУ;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьСтоимостьДляВычисленияАмортизации()
	
	Если Не СтоимостьДляВычисленияАмортизацииОтличается Тогда 
		СтоимостьДляВычисленияАмортизации   = ТекущаяСтоимостьБУ;
		СтоимостьДляВычисленияАмортизацииНУ = ТекущаяСтоимостьНУ;
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ПересчитатьЗависимыеСуммы()
	
	ПересчитатьПервоначальнуюСтоимость();
	ПересчитатьСтоимостьДляВычисленияАмортизации();
	
	ОстаточнаяСтоимостьБУ = ТекущаяСтоимостьБУ - НакопленнаяАмортизацияБУ;
	ОстаточнаяСтоимостьНУ = ТекущаяСтоимостьНУ - НакопленнаяАмортизацияНУ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПервоначальнаяСтоимостьБУПриИзменении(Элемент)
	
	ПервоначальнаяСтоимостьНУ = ПересчитатьСуммуНУ(ПервоначальнаяСтоимостьБУ); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПервоначальнаяСтоимостьНУПриИзменении(Элемент)
	
	Возврат;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяСтоимостьБУПриИзменении(Элемент)
	
	ТекущаяСтоимостьНУ = ПересчитатьСуммуНУ(ТекущаяСтоимостьБУ);
		
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяСтоимостьНУПриИзменении(Элемент)
	
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры



&НаКлиенте
Процедура НакопленнаяАмортизацияБУПриИзменении(Элемент)
	
	НакопленнаяАмортизацияНУ = ПересчитатьСуммуНУ(НакопленнаяАмортизацияБУ);
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Функция ПересчитатьСуммуНУ(ПеремСуммаБУ)
	
	ПеремСуммаНУ = 0;
	
	Если НалоговоеНазначение <> 
			ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность") Тогда
		ПеремСуммаНУ = ПеремСуммаБУ;
	КонецЕсли; 
	
	Возврат ПеремСуммаНУ;

КонецФункции // ПересчитатьСуммуНУ()

&НаКлиенте
Процедура НакопленнаяАмортизацияНУПриИзменении(Элемент)
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьДляВычисленияАмортизацииПриИзменении(Элемент)

	СтоимостьДляВычисленияАмортизацииНУ = ПересчитатьСуммуНУ(СтоимостьДляВычисленияАмортизации);

КонецПроцедуры


&НаКлиенте
Процедура ПревышениеСуммДооценокНадСуммамиУценокБУПриИзменении(Элемент)
	Если СуммаДопКапиталаНачисленногоПриДооценкахОСБУ = 0 Тогда
		СуммаДопКапиталаНачисленногоПриДооценкахОСБУ = ПревышениеСуммДооценокНадСуммамиУценокБУ;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСтраницыСобытия

&НаКлиенте
Процедура ЕстьСобытияМодернизацииПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("ЕстьСобытияМодернизации");
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьРезервПереоценкиПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("ЕстьРезервПереоценки");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСтраницыАмортизация


&НаКлиенте
Процедура МетодНачисленияАмортизацииБУПриИзменении(Элемент)
	
	Если НачислятьАмортизациюНУ Тогда    
		
		СпособНачисленияАмортизацииНУ = МетодНачисленияАмортизацииБУ;
		
	КонецЕсли; 
	ОбновитьСвойстваЭлементовФормы("МетодНачисленияАмортизацииБУ");
	РассчитатьНакопленнуюАмортизациюДляУпрощенныхМетодов();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособНачисленияАмортизацииНУПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("МетодНачисленияАмортизацииБУ");
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНакопленнуюАмортизациюДляУпрощенныхМетодов()
	
	УпрощенныйМетодНачисленияАмортизацииБУ = (
		МетодНачисленияАмортизацииБУ  = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС._50_50")) 
		ИЛИ (МетодНачисленияАмортизацииБУ  = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС._100"));
	
	Если УпрощенныйМетодНачисленияАмортизацииБУ Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ЗавершитьПересчитатьНакопленнуюАмортизациюДляУпрощенныхМетодов", ЭтаФорма),
			НСтр("ru='Изменен метод начисления амортизации. Перезаполнить накопленную амортизацию ?';uk='Змінено метод нарахування амортизації. Перезаповнити накопичену амортизацію?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПересчитатьНакопленнуюАмортизациюДляУпрощенныхМетодов(Результат=Неопределено, ДополнительныеСведения=Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Результат = КодВозвратаДиалога.Да;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПересчитатьНакопленнуяАмортизациюДляУпрощенныхМетодов();
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНакопленнуяАмортизациюДляУпрощенныхМетодов()
	
	Если МетодНачисленияАмортизацииБУ  = 
				ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС._50_50") Тогда
				
			НакопленнаяАмортизацияБУ = ТекущаяСтоимостьБУ/2;
			НакопленнаяАмортизацияНУ = ТекущаяСтоимостьНУ/2;
			
	ИначеЕсли МетодНачисленияАмортизацииБУ  =
					ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС._100") Тогда
					
			 НакопленнаяАмортизацияБУ = ТекущаяСтоимостьБУ;
			 НакопленнаяАмортизацияНУ = ТекущаяСтоимостьНУ;
			 
	КонецЕсли;
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияБУПриИзменении(Элемент)
	
	Если (СрокИспользованияНУ = 0 Или СрокИспользованияБУСохраненный = СрокИспользованияНУ) Тогда
		
		СрокИспользованияНУ = СрокИспользованияБУ;
		Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияНУ);
		
	КонецЕсли;
	
	СрокИспользованияБУСохраненный = СрокИспользованияБУ;
	Элементы.СрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияБУОстаточныйПриИзменении(Элемент)
	
	СрокИспользованияБУСохраненный = СрокИспользованияБУ;
	Элементы.СрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	Элементы.ИзносСрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияНУПриИзменении(Элемент)
	
	Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияНУ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательНаработкиПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПоказательНаработки) Тогда
		ОбновитьСвойстваЭлементовФормы("ПоказательНаработки");
	Иначе
		Элементы.ОбъемНаработкиБУ.Подсказка = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСпособовАмортизацииБУ(СчетУчетаБУ) Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Линейный);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	
	Если ЗначениеЗаполнено(СчетУчетаБУ) 
	   И СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа) Тогда		  
	   
	   	Если СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.БиблиотечныеФонды 
		 ИЛИ СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивы Тогда			
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._50_50);
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._100);			
		КонецЕсли;
		
	Иначе	
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УскоренногоУменьшенияОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Кумулятивный);		
	КонецЕсли;
	
	Возврат СписокПеречисления;
	
КонецФункции

Функция ПолучитьСписокСпособовАмортизацииНУ(СчетУчетаБУ, СпособНачисленияАмортизацииБУ) Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Линейный);
	
	Если СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции Тогда
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	КонецЕсли; 		
	
	Если ЗначениеЗаполнено(СчетУчетаБУ) 
	   И СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа) Тогда		  
	   
	   	Если СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.БиблиотечныеФонды 
		 ИЛИ СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивы Тогда			
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._50_50);
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._100);			
		КонецЕсли;
		
	Иначе	
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УскоренногоУменьшенияОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Кумулятивный);		
	КонецЕсли;
	
	Возврат СписокПеречисления;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийСтраницыИзнос

&НаКлиенте
Процедура ИзносСрокИспользованияБУПриИзменении(Элемент)
	
	Если СрокИспользованияНУ = 0 Или СрокИспользованияБУСохраненный = СрокИспользованияНУ Тогда
		
		СрокИспользованияНУ = СрокИспользованияБУ;
		Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияНУ);
		
	КонецЕсли;
	
	СрокИспользованияБУСохраненный = СрокИспользованияБУ;
	Элементы.СрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	Элементы.ИзносСрокИспользованияБУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользованияБУ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСтраницыОтражениеРасходов


&НаКлиенте
Процедура СтатьяРасходовАмортизацииПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовАмортизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗавершитьРедактирование();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция СчетУчетаПредставление(Счет)
	Возврат ?(ЗначениеЗаполнено(Счет), Счет, НСтр("ru='учета ОС';uk='обліку ОЗ'"));
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СчетАмортизацииПредставление(Счет)
	Возврат ?(ЗначениеЗаполнено(Счет), Счет, НСтр("ru='амортизации';uk='амортизації'"));
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СчетИзносаПредставление(Счет)
	Возврат ?(ЗначениеЗаполнено(Счет), Счет, НСтр("ru='износа';uk='зносу'"));
КонецФункции

&НаСервере
Процедура ОбновитьСвойстваЭлементовФормы(ИзмененныеРеквизиты=Неопределено)
	
	ОбновитьВсе = (ИзмененныеРеквизиты = Неопределено);
	Реквизиты = Новый Структура(ИзмененныеРеквизиты);
	
	
	Если ОбновитьВсе Тогда
	
		ЗаголовокТекущаяСтоимость = НСтр("ru='Остаток на счете %1:';uk='Залишок на рахунку %1:'");
		ЗаголовокНакопленнаяАмортизация = НСтр("ru='Остаток на счете %1:';uk='Залишок на рахунку %1:'");
		Элементы.ГруппаОстаточнаяСтоимостьЗаголовок.Заголовок = НСтр("ru='Сумма:';uk='Сума:'");
		
		Элементы.ГруппаСчетаУчета.Видимость = Истина;
		Элементы.СтраницаОтражениеРасходов.Заголовок = НСтр("ru='Отражение расходов';uk='Відображення витрат'");
		
		Реквизиты.Вставить("СчетУчета");
 		Реквизиты.Вставить("СчетАмортизации");
		
		БезАмортизацииБУ = Не НачислятьАмортизациюБУ;
		БезАмортизацииНУ = Не НачислятьАмортизациюНУ;
		БезАмортизации = БезАмортизацииБУ И БезАмортизацииНУ;
		
		Элементы.СчетАмортизацииОсновной.Заголовок = НСтр("ru='Накопленной амортизации';uk='Накопиченої амортизації'");
		
		Элементы.ГруппаОстаточнаяСтоимость.Видимость = Не БезАмортизации;
		Элементы.ГруппаНакопленнаяАмортизация.Заголовок = НСтр("ru='Накопленная амортизация';uk='Накопичена амортизація'");
		
	КонецЕсли; 
		
	
	Если ОбновитьВсе Или Реквизиты.Свойство("ПервоначальнаяСтоимостьОтличается") Тогда
		
		Элементы.ГруппаПервоначальнаяСтоимостьСуммы.ТолькоПросмотр = Не ПервоначальнаяСтоимостьОтличается;
		
		Элементы.ПервоначальнаяСтоимостьБУ.АвтоОтметкаНезаполненного = ПервоначальнаяСтоимостьОтличается;
		Элементы.ПервоначальнаяСтоимостьБУ.ОтметкаНезаполненного = ПервоначальнаяСтоимостьОтличается И Не ЗначениеЗаполнено(ПервоначальнаяСтоимостьБУ);
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СтоимостьДляВычисленияАмортизацииОтличается") Тогда
		
		Элементы.ГруппаСтоимостьДляВычисленияАмортизацииСуммы.ТолькоПросмотр = Не СтоимостьДляВычисленияАмортизацииОтличается;
		
		Элементы.СтоимостьДляВычисленияАмортизации.АвтоОтметкаНезаполненного = СтоимостьДляВычисленияАмортизацииОтличается;
		Элементы.СтоимостьДляВычисленияАмортизации.ОтметкаНезаполненного = СтоимостьДляВычисленияАмортизацииОтличается И Не ЗначениеЗаполнено(СтоимостьДляВычисленияАмортизации);
		
	КонецЕсли;
	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СчетУчета") Тогда
		
		СчетАмортизации = ОпределитьСчетАмортизации(СчетУчета);
		СчетУчетаПриИзмененииНаСервере();
		
		Реквизиты.Вставить("СчетАмортизации");
		
		Элементы.МетодНачисленияАмортизацииБУ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииБУ(СчетУчета));
		
		Реквизиты.Вставить("МетодНачисленияАмортизацииБУ");
		
		Элементы.ГруппаТекущаяСтоимостьЗаголовок.Заголовок = СтрШаблон(ЗаголовокТекущаяСтоимость, СчетУчетаПредставление(СчетУчета));
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СчетАмортизации") Тогда
		
		
		Элементы.ГруппаНакопленнаяАмортизацияЗаголовок.Заголовок = СтрШаблон(
			ЗаголовокНакопленнаяАмортизация,
			СчетАмортизацииПредставление(СчетАмортизации));
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("ЕстьСобытияМодернизации") Тогда
		
		Элементы.ДатаМодернизации.ТолькоПросмотр = Не ЕстьСобытияМодернизации;
		Элементы.НазваниеДокументаМодернизации.ТолькоПросмотр = Не ЕстьСобытияМодернизации;
		Элементы.НомерДокументаМодернизации.ТолькоПросмотр = Не ЕстьСобытияМодернизации;
		Элементы.СобытиеМодернизации.ТолькоПросмотр = Не ЕстьСобытияМодернизации;
		
		Элементы.ДатаМодернизации.АвтоОтметкаНезаполненного = ЕстьСобытияМодернизации;
		Элементы.ДатаМодернизации.ОтметкаНезаполненного = ЕстьСобытияМодернизации И Не ЗначениеЗаполнено(ДатаМодернизации);
		
		Элементы.СтоимостьДляВычисленияАмортизации.АвтоОтметкаНезаполненного = ЕстьСобытияМодернизации;
		Элементы.СтоимостьДляВычисленияАмортизации.ОтметкаНезаполненного = ЕстьСобытияМодернизации И Не ЗначениеЗаполнено(СтоимостьДляВычисленияАмортизации);
		
		Реквизиты.Вставить("МетодНачисленияАмортизацииБУ");
		
	КонецЕсли;
	
	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("МетодНачисленияАмортизацииБУ") Тогда
		
		ПоОстатку = (МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		ПоНаработке = (МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
		
		УпрощенныйМетодНачисленияАмортизацииБУ = (МетодНачисленияАмортизацииБУ  = Перечисления.СпособыНачисленияАмортизацииОС._50_50) 
												 		ИЛИ (МетодНачисленияАмортизацииБУ  = Перечисления.СпособыНачисленияАмортизацииОС._100);
		
		Элементы.ОбъемНаработкиБУ.Видимость = ПоНаработке;
		Элементы.ПоказательНаработки.Видимость = ПоНаработке;
		
		Элементы.ГрафикАмортизацииБУ.Видимость = НЕ ПоНаработке И НЕ УпрощенныйМетодНачисленияАмортизацииБУ;
		
		Элементы.СрокИспользованияБУ.Видимость =  НЕ ПоНаработке И НЕ УпрощенныйМетодНачисленияАмортизацииБУ;
				
		Элементы.СрокИспользованияБУ.АвтоОтметкаНезаполненного = Не ПоНаработке;
		Если Не Элементы.СрокИспользованияБУ.АвтоОтметкаНезаполненного Тогда
			Элементы.СрокИспользованияБУ.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		Если УпрощенныйМетодНачисленияАмортизацииБУ Тогда
		    НачислятьАмортизациюНУ = НачислятьАмортизациюБУ;
			Элементы.НачислятьАмортизациюНУ.Доступность = Ложь;
		Иначе 
			Элементы.НачислятьАмортизациюНУ.Доступность = Истина;
		КонецЕсли;

		
		Если НачислятьАмортизациюНУ Тогда 
			Элементы.СпособНачисленияАмортизацииНУ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииНУ(СчетУчета,МетодНачисленияАмортизацииБУ));
			
			Реквизиты.Вставить("МетодНачисленияАмортизацииНУ");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("МетодНачисленияАмортизацииНУ") Тогда
		
		ПоНаработкеНУ = (СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
		
		УпрощенныйМетодНачисленияАмортизацииНУ = (СпособНачисленияАмортизацииНУ  = Перечисления.СпособыНачисленияАмортизацииОС._50_50) 
												 		ИЛИ (СпособНачисленияАмортизацииНУ  = Перечисления.СпособыНачисленияАмортизацииОС._100);
		
		Элементы.СрокИспользованияНУ.Видимость =  НЕ ПоНаработкеНУ И НЕ УпрощенныйМетодНачисленияАмортизацииНУ;
		Элементы.СрокИспользованияНУ.АвтоОтметкаНезаполненного = НЕ ПоНаработкеНУ;
		
		Если НЕ Элементы.СрокИспользованияНУ.АвтоОтметкаНезаполненного Тогда
			Элементы.СрокИспользованияНУ.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("ПоказательНаработки") Тогда
		
		Если ЗначениеЗаполнено(ПоказательНаработки) Тогда
			Элементы.ОбъемНаработкиБУ.Подсказка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоказательНаработки, "ЕдиницаИзмерения");
		Иначе
			Элементы.ОбъемНаработкиБУ.Подсказка = "";
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НалоговоеНазначение") Тогда  
		
		ДоступностьЭлемента = НЕ НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность");
		
		Элементы.ТекущаяСтоимостьНУ.Доступность = ДоступностьЭлемента;
		Элементы.ПервоначальнаяСтоимостьНУ.Доступность = ДоступностьЭлемента;
		Элементы.НакопленнаяАмортизацияНУ.Доступность	= ДоступностьЭлемента;
		Элементы.ОстаточнаяСтоимостьНУ.Доступность = ДоступностьЭлемента;
		Элементы.СтоимостьДляВычисленияАмортизацииНУ.Доступность = ДоступностьЭлемента;
		Элементы.ГруппаАмортизацияНУ.Доступность = ДоступностьЭлемента;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПараметрыВыбораЭлемента(Элемент, ИмяЗаменяемогоПараметра, ЗначениеЗаменяемогоПараметра)
	
	Массив = Новый Массив;
	
	Для Каждого ПараметрВыбора Из Элемент.ПараметрыВыбора Цикл
		Если ПараметрВыбора.Имя = ИмяЗаменяемогоПараметра Тогда
			Массив.Добавить(ЗначениеЗаменяемогоПараметра);
		Иначе
			Массив.Добавить(ПараметрВыбора);
		КонецЕсли;
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНеИспользуемыеРеквизиты()
	
	ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности = ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности");
	
	Если НЕ ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности Тогда
		НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОпределитьСчетАмортизации(СчетУчетаОС)
	
	СчетАмортизацииОС = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	
	Если СчетУчетаОС = СчетАмортизацииОС Тогда 
		Возврат СчетАмортизацииОС;
	ИначеЕсли СчетУчетаОС.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОсновныеСредства) Тогда
	    СчетАмортизацииОС = ПланыСчетов.Хозрасчетный.ИзносОсновныхСредств;
	ИначеЕсли СчетУчетаОС.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа) Тогда
	    СчетАмортизацииОС = ПланыСчетов.Хозрасчетный.ИзносДругихНеоборотныхМатериальныхАктивовИндивидуально;
	КонецЕсли;
	
	Возврат  СчетАмортизацииОС;
	
КонецФункции

&НаКлиенте
Функция РеквизитыРедактируемыеВОтдельнойФорме()
	
	СтруктураДанных = Новый Структура(
	"ОсновноеСредство,
	|ИнвентарныйНомер,
	|СчетУчета,
	|СчетАмортизации,
	|СчетУчетаДооценокОС,
	|НалоговаяГруппаОС,
	|МОЛ,
	|АдресМестонахождения,
	|ПервоначальнаяСтоимостьБУ,
	|ПервоначальнаяСтоимостьНУ,
	|ТекущаяСтоимостьБУ,
	|ТекущаяСтоимостьНУ,
	|НакопленнаяАмортизацияБУ,
	|НакопленнаяАмортизацияНУ,
	|НачислятьАмортизациюБУ,
	|МетодНачисленияАмортизацииБУ,
	|СрокИспользованияБУ,
	|ПоказательНаработки,
	|ОбъемНаработкиБУ,
	|ГрафикАмортизацииБУ,
	|НачислятьАмортизациюНУ,
	|СрокИспользованияНУ,
	|СтатьяРасходовАмортизации,
	|АналитикаРасходовАмортизации,
	|НомерДокументаПринятияКУчету,
	|НомерДокументаМодернизации,
	|СобытиеПринятияКУчету,
	|СобытиеМодернизации,
	|НазваниеДокументаПринятияКУчету,
	|НазваниеДокументаМодернизации,
	|ДатаПринятияКУчету,
	|ДатаМодернизации,
	|НалоговоеНазначение,
	|ЛиквидационнаяСтоимостьБУ,
	|ПревышениеСуммДооценокНадСуммамиУценокБУ,
	|СпособНачисленияАмортизацииНУ,
	|СуммаДопКапиталаНачисленногоПриДооценкахОСБУ,
	|ЕстьСобытияМодернизации,
	|СтоимостьДляВычисленияАмортизации,
	|СтоимостьДляВычисленияАмортизацииНУ,
	|ПервоначальнаяСтоимостьОтличается,
	|СтоимостьДляВычисленияАмортизацииОтличается,
	|НаправлениеДеятельности");
	
	ЗаполнитьЗначенияСвойств(СтруктураДанных, ЭтаФорма);
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначенияПолейКонтактнойИнформацииСервер(Знач Представление, Знач ВидКонтактнойИнформации)
	
	Результат = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
	Возврат Результат;
	
КонецФункции


&НаСервереБезКонтекста
Функция ПараметрыВыбораСтатейИАналитик()
	
	ПараметрыВыбораСтатейИАналитик = Новый Массив;
	
	// СтатьяРасходовАмортизации
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "";
	ПараметрыВыбора.Статья = "СтатьяРасходовАмортизации";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовАмортизации";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовАмортизации");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовАмортизации");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатейИАналитик;
	
КонецФункции

#КонецОбласти
