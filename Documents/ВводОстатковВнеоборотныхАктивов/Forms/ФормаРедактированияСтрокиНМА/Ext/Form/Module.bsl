#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДанныеЗаполнения);
	
	ДатаУчетнойПолитики = Дата + 86400;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
		ЭтаФорма,
		Организация,
		ДатаУчетнойПолитики);
	
	ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, ДатаУчетнойПолитики);
	
	Элементы.ДекорацияЗаголовокНУ.Видимость = ПлательщикНалогаНаПрибыль;
	
	// Установка значений по умолчанию.
	Если Параметры.ЭтоНовый И НЕ Параметры.Копирование Тогда
		
 		СчетУчета 			 = ПланыСчетов.Хозрасчетный.ДругиеНематериальныеАктивы;
		СчетАмортизации 	 = ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияНематериальныхАктивов;
		СчетУчетаДооценокНМА = ПланыСчетов.Хозрасчетный.ДооценкаНематериальныхАктивов;
		СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный;
		НачислятьАмортизациюБУ = Истина;
		Если ПлательщикНалогаНаПрибыль Тогда
			НачислятьАмортизациюНУ 		  = Истина;
			СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный;
			НалоговоеНазначение           = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
		Иначе
			НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность;
		КонецЕсли;

		
	КонецЕсли;
	
	ЗаголовокНакопленнаяАмортизация = НСтр("ru='Собственные средства, остаток на счете %1:';uk='Власні кошти, залишок на рахунку %1:'");
	ЗаголовокТекущаяСтоимость = НСтр("ru='Собственные средства, остаток на счете %1:';uk='Власні кошти, залишок на рахунку %1:'");
	ЗаголовокПервоначальнаяСтоимость = НСтр("ru='Собственные средства, остаток на счете %1:';uk='Власні кошти, залишок на рахунку %1:'");
	
	Заголовок = НСтр("ru='Нематериальные активы: %1';uk='Нематеріальні активи: %1'");
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Заголовок, ?(Параметры.ЭтоНовый, НСтр("ru='Новая строка';uk='Новий рядок'"), НематериальныйАктив));
	
		
	ОстаточнаяСтоимостьБУ = ТекущаяСтоимостьБУ - НакопленнаяАмортизацияБУ;
	ОстаточнаяСтоимостьНУ = ТекущаяСтоимостьНУ - НакопленнаяАмортизацияНУ;
	
	Элементы.ГруппаАмортизацияНУ.Видимость = ПлательщикНалогаНаПрибыль;
	
		
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	СрокИспользованияБУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияБУ);
	СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияНУ);
	
	ОбновитьСвойстваЭлементовФормы();
	
	ПараметрыВыбораСтатейИАналитик = ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Модифицированность Тогда
		
		Отказ = Истина;
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?';uk='Дані були змінені. Зберегти зміни?'");
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтаФорма),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат=Неопределено, ДополнительныеПараметры=Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если ПроверитьЗаполнение() Тогда
			Модифицированность = Ложь;
			Закрыть(РеквизитыРедактируемыеВОтдельнойФорме());
		КонецЕсли;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ОчиститьНеИспользуемыеРеквизиты();
	
	ДатаУчетнойПолитики = Дата + 86400;
	
	ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, ДатаУчетнойПолитики);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовАмортизации");
	
	Если НЕ НачислятьАмортизациюБУ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СрокПолезногоИспользованияБУ");
		МассивНепроверяемыхРеквизитов.Добавить("СпособНачисленияАмортизацииБУ");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовАмортизации");
		МассивНепроверяемыхРеквизитов.Добавить("ОбъемПродукцииРаботДляВычисленияАмортизации");
	Иначе
		Если СпособНачисленияАмортизацииБУ =  Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда 
			МассивНепроверяемыхРеквизитов.Добавить("СрокПолезногоИспользованияБУ");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("ОбъемПродукцииРаботДляВычисленияАмортизации");
		КонецЕсли;
	КонецЕсли;
	
	Если ПлательщикНалогаНаПрибыль Тогда
		
		Если НЕ НачислятьАмортизациюНУ Тогда
			МассивНепроверяемыхРеквизитов.Добавить("СрокПолезногоИспользованияНУ");
			МассивНепроверяемыхРеквизитов.Добавить("СпособНачисленияАмортизацииНУ");
		Иначе
			Если СпособНачисленияАмортизацииНУ =  Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда 
				МассивНепроверяемыхРеквизитов.Добавить("СрокПолезногоИспользованияНУ");
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		 МассивНепроверяемыхРеквизитов.Добавить("СрокПолезногоИспользованияНУ");
		 МассивНепроверяемыхРеквизитов.Добавить("СпособНачисленияАмортизацииНУ");
				
	КонецЕсли;
		
	ПараметрыВыбораСтатейИАналитик = ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНеИспользуемыеРеквизиты()
	
	ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности = ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности");
	
	Если НЕ ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности Тогда
		НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НематериальныйАктивПриИзменении(Элемент)
	
	НематериальныйАктивПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура НематериальныйАктивПриИзмененииСервер()
	
	СчетУчета 			 = ПланыСчетов.Хозрасчетный.ДругиеНематериальныеАктивы;
	СчетАмортизации 	 = ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияНематериальныхАктивов;
	СчетУчетаДооценокНМА = ПланыСчетов.Хозрасчетный.ДооценкаНематериальныхАктивов;
	
	ОбновитьСвойстваЭлементовФормы();
	
	
КонецПроцедуры

#Область ОбработчикиСобытийСтраницыУчет

&НаКлиенте
Процедура СчетУчетаОсновнойПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетУчета");
КонецПроцедуры

&НаСервере
Процедура СчетУчетаПриИзмененииНаСервере()
		
	Если СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеПриродныхРесурсов Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа1;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаИспользованиеИмущества Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа2;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаЗнакиДляТоваровИУслуг Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа3;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПраваНаОбъектыПромышленнойСобственности Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа4;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.АвторскиеИСмежныеСНимиПрава Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа5;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДругиеНематериальныеАктивы Тогда
		НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.НМАГруппа6;
	КонецЕсли; 

КонецПроцедуры


&НаКлиенте
Процедура СчетАмортизацииОсновнойПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетАмортизации");
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетУчета");
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаЦФПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетУчетаЦФ");
КонецПроцедуры

&НаКлиенте
Процедура СчетАмортизацииПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетАмортизации");
КонецПроцедуры

&НаКлиенте
Процедура СчетАмортизацииЦФПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("СчетАмортизацииЦФ");
КонецПроцедуры

&НаКлиенте
Процедура ЕстьРезервПереоценкиПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("ЕстьРезервПереоценки");
КонецПроцедуры


&НаКлиенте
Процедура СтатьяДоходовПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяГруппаОСПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы("НалоговаяГруппаОС");
КонецПроцедуры 

&НаКлиенте
Процедура НалоговоеНазначениеПриИзменении(Элемент)
	
	Если НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность") Тогда
		ПервоначальнаяСтоимостьНУ = 0;
		НакопленнаяАмортизацияНУ  = 0;
		ПересчитатьЗависимыеСуммы();
		
		НачислятьАмортизациюНУ = Ложь;
		СрокПолезногоИспользованияНУ = 0;
		СпособНачисленияАмортизацииНУ = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС.ПустаяСсылка");
		Элементы.СрокИспользованияНУ.Подсказка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияНУ)
	КонецЕсли;
	
	ОбновитьСвойстваЭлементовФормы("НалоговоеНазначение");
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийСтраницыСтоимость

&НаКлиенте
Процедура ПервоначальнаяСтоимостьОтличаетсяПриИзменении(Элемент)
	
	Если Не ПервоначальнаяСтоимостьОтличается Тогда
		
		ПервоначальнаяСтоимостьБУ = ТекущаяСтоимостьБУ;
		ПервоначальнаяСтоимостьНУ = ТекущаяСтоимостьНУ;
		
	КонецЕсли;
	
	ОбновитьСвойстваЭлементовФормы("ПервоначальнаяСтоимостьОтличается");
	
КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьЗависимыеСуммы()
	
	
	ОстаточнаяСтоимостьБУ = ПервоначальнаяСтоимостьБУ - НакопленнаяАмортизацияБУ;
	ОстаточнаяСтоимостьНУ = ПервоначальнаяСтоимостьНУ - НакопленнаяАмортизацияНУ
	
КонецПроцедуры

&НаКлиенте
Функция ПересчитатьСуммуНУ(ПеремСуммаБУ)
	
	ПеремСуммаНУ = 0;
	
	Если НалоговоеНазначение <> 
			ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность") Тогда
		ПеремСуммаНУ = ПеремСуммаБУ;
	КонецЕсли; 
	
	Возврат ПеремСуммаНУ;

КонецФункции // ПересчитатьСуммуНУ()

&НаКлиенте
Процедура ПервоначальнаяСтоимостьБУПриИзменении(Элемент)
	
	Если ПервоначальнаяСтоимостьНУ = 0 Тогда
		ПервоначальнаяСтоимостьНУ = ПересчитатьСуммуНУ(ПервоначальнаяСтоимостьБУ);
	КонецЕсли;
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры


&НаКлиенте
Процедура НакопленнаяАмортизацияБУПриИзменении(Элемент)
	
	НакопленнаяАмортизацияНУ = ПересчитатьСуммуНУ(НакопленнаяАмортизацияБУ);
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура НакопленнаяАмортизацияНУПриИзменении(Элемент)
	
	ПересчитатьЗависимыеСуммы();
	
КонецПроцедуры


&НаКлиенте
Процедура ПревышениеСуммДооценокНадСуммамиУценокБУПриИзменении(Элемент)
	Если СуммаДопКапиталаНачисленногоПриДооценкахНМАБУ = 0 Тогда
		СуммаДопКапиталаНачисленногоПриДооценкахНМАБУ = ПревышениеСуммДооценокНадСуммамиУценокБУ;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСтраницыАмортизация

&НаКлиенте
Процедура НачислятьАмортизациюБУПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("НачислятьАмортизациюБУ");
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияБУПриИзменении(Элемент)
	
	Если ПлательщикНалогаНаПрибыль И СрокПолезногоИспользованияНУ = 0 Тогда
		
		СрокПолезногоИспользованияНУ = СрокПолезногоИспользованияБУ;
		СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияНУ);
		
	КонецЕсли;
	
	СрокИспользованияБУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияБУ);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияНУПриИзменении(Элемент)
	
	СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользованияНУ);
	
КонецПроцедуры


&НаКлиенте
Процедура НачислятьАмортизациюНУ_НМАПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("НачислятьАмортизациюНУ");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособНачисленияАмортизацииБУПриИзменении(Элемент)
	
	Если НачислятьАмортизациюНУ Тогда    
		
		СпособНачисленияАмортизацииНУ = СпособНачисленияАмортизацииБУ; 
		
	КонецЕсли; 
	
	ОбновитьСвойстваЭлементовФормы("СпособНачисленияАмортизацииБУ");
	
КонецПроцедуры

&НаКлиенте
Процедура МетодНачисленияАмортизацииНУПриИзменении(Элемент)
	
	ОбновитьСвойстваЭлементовФормы("МетодНачисленияАмортизацииНУ");
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовАмортизацииПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовАмортизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАмортизацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередаватьРасходыВДругуюОрганизациюПриИзменении(Элемент)
	
	Если Не ПередаватьРасходыВДругуюОрганизацию Тогда
		
		ОрганизацияПолучательРасходов = Неопределено;
		СчетПередачиРасходов = Неопределено;
		
	КонецЕсли;
	
	ОбновитьСвойстваЭлементовФормы("ПередаватьРасходыВДругуюОрганизацию");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		Закрыть(РеквизитыРедактируемыеВОтдельнойФорме());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция СчетУчетаПредставление(Счет)
	Возврат ?(ЗначениеЗаполнено(Счет), Счет, НСтр("ru='актива';uk='активу'"));
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СчетАмортизацииПредставление(Счет)
	Возврат ?(ЗначениеЗаполнено(Счет), Счет, НСтр("ru='амортизации';uk='амортизації'"));
КонецФункции

&НаСервере
Процедура ОбновитьСвойстваЭлементовФормы(ИзмененныеРеквизиты=Неопределено)
	
	ОбновитьВсе = (ИзмененныеРеквизиты = Неопределено);
	Реквизиты = Новый Структура(ИзмененныеРеквизиты);
	
	Если ОбновитьВсе  Тогда
	
		
		СписокСпособовНачисленияАмортизацииБУ = Элементы.СпособНачисленияАмортизацииБУ.СписокВыбора;
		СписокСпособовНачисленияАмортизацииБУ.Очистить();
		
		
		Элементы.СчетУчетаОсновной.Заголовок = НСтр("ru='Нематериального актива';uk='Нематеріального активу'");
		Элементы.СчетАмортизацииОсновной.Доступность = Истина;
		
		Элементы.ГруппаПереоценка.Видимость = Истина;
		
		Элементы.ГруппаНакопленнаяАмортизация.Видимость = Истина;
		Элементы.ГруппаОстаточнаяСтоимость.Видимость = Истина;
		
		Элементы.ГруппаОтражениеАмортизации.Заголовок = НСтр("ru='Отражение амортизационных расходов';uk='Відображення амортизаційних витрат'");
		Элементы.НачислятьАмортизациюБУ.Заголовок = НСтр("ru='Начисление амортизации';uk='Нарахування амортизації'");
		Элементы.НачислятьАмортизациюНУ.Заголовок = НСтр("ru='Начисление амортизации';uk='Нарахування амортизації'");
		Элементы.СпособНачисленияАмортизацииБУ.Заголовок = НСтр("ru='Способ начисления амортизации';uk='Спосіб нарахування амортизації'");
		Элементы.СрокИспользованияБУ.Заголовок = НСтр("ru='Срок использования, мес';uk='Строк використання, міс'");
		
		
		СписокСпособовНачисленияАмортизацииБУ.Добавить(ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.Линейный"));
		СписокСпособовНачисленияАмортизацииБУ.Добавить(ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка"));
		СписокСпособовНачисленияАмортизацииБУ.Добавить(ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции"));
		СписокСпособовНачисленияАмортизацииБУ.Добавить(ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.УскоренногоУменьшенияОстатка"));
		СписокСпособовНачисленияАмортизацииБУ.Добавить(ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.Кумулятивный"));
		
		Элементы.СрокИспользованияНУ.Доступность = Истина;
		
		Реквизиты.Вставить("СчетУчета");
		Реквизиты.Вставить("СчетАмортизации");
		
	КонецЕсли;
	
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СчетУчета") Тогда
		Элементы.ГруппаПервоначальнаяСтоимостьЗаголовок.Заголовок = СтрШаблон(ЗаголовокПервоначальнаяСтоимость, СчетУчетаПредставление(СчетУчета));
		СчетУчетаПриИзмененииНаСервере();
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СчетАмортизации") Тогда
		Элементы.ГруппаНакопленнаяАмортизацияЗаголовок.Заголовок = СтрШаблон(ЗаголовокНакопленнаяАмортизация, СчетАмортизацииПредставление(СчетАмортизации));
	КонецЕсли;
	
		
	Если ОбновитьВсе Или Реквизиты.Свойство("НачислятьАмортизациюБУ") Тогда
		
		Элементы.СпособНачисленияАмортизацииБУ.Доступность = НачислятьАмортизациюБУ;
		Элементы.ГруппаСрокИспользованияБУ.Доступность = НачислятьАмортизациюБУ;
		Элементы.ОбъемВыработки.Доступность = НачислятьАмортизациюБУ;
		
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НачислятьАмортизациюНУ") Тогда
		
		Элементы.ГруппаСрокИспользованияНУ.Доступность = НачислятьАмортизациюНУ;
		
	КонецЕсли;
	
		
	Если ОбновитьВсе Или Реквизиты.Свойство("СпособНачисленияАмортизацииБУ") Тогда
		
		ПропорциональноОбъемуПродукции = (СпособНачисленияАмортизацииБУ=Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		Элементы.ОбъемВыработки.Видимость = ПропорциональноОбъемуПродукции;
		Элементы.СрокИспользованияБУ.Видимость = Не ПропорциональноОбъемуПродукции;
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("СпособНачисленияАмортизацииНУ") Тогда
		ПропорциональноОбъемуПродукцииНУ = (СпособНачисленияАмортизацииНУ=Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		Элементы.СрокИспользованияНУ.Видимость = Не ПропорциональноОбъемуПродукцииНУ;
	КонецЕсли;
	
	Если ОбновитьВсе Или Реквизиты.Свойство("НалоговоеНазначение") Тогда  
		
		ДоступностьЭлемента = НЕ НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность");
		
		Элементы.ПервоначальнаяСтоимостьНУ.Доступность = ДоступностьЭлемента;
		Элементы.НакопленнаяАмортизацияНУ.Доступность	= ДоступностьЭлемента;
		Элементы.ОстаточнаяСтоимостьНУ.Доступность = ДоступностьЭлемента;
		Элементы.ГруппаАмортизацияНУ.Доступность = ДоступностьЭлемента;
		
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Функция РеквизитыРедактируемыеВОтдельнойФорме()
	
	СтруктураДанных = Новый Структура(
	"НематериальныйАктив,
	|СчетУчета,
	|СчетАмортизации,
	|СчетУчетаДооценокНМА,
	|НалоговаяГруппаОС,
	|НалоговоеНазначение,
	|ПервоначальнаяСтоимостьБУ,
	|ПервоначальнаяСтоимостьНУ,
	|НакопленнаяАмортизацияБУ,
	|НакопленнаяАмортизацияНУ,
	|ЛиквидационнаяСтоимостьБУ,
	|ПревышениеСуммДооценокНадСуммамиУценокБУ,
	|СуммаДопКапиталаНачисленногоПриДооценкахНМАБУ,
	|ДатаПринятияКУчетуРегл,
	|НачислятьАмортизациюБУ,
	|НачислятьАмортизациюНУ,
	|ОбъемПродукцииРаботДляВычисленияАмортизации,
	|СпособНачисленияАмортизацииБУ,
	|СпособНачисленияАмортизацииНУ,
	//|СпособПоступления,
	|СрокПолезногоИспользованияБУ,
	|СрокПолезногоИспользованияНУ,
	|СтатьяРасходовАмортизации,
	|АналитикаРасходовАмортизации,
	|МОЛ,
	|Подразделение,
	|НаправлениеДеятельности");
	
	ЗаполнитьЗначенияСвойств(СтруктураДанных, ЭтаФорма);
	
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыВыбораСтатейИАналитик()
	
	ПараметрыВыбораСтатейИАналитик = Новый Массив;
	
	// СтатьяРасходовАмортизации
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "";
	ПараметрыВыбора.Статья = "СтатьяРасходовАмортизации";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовАмортизации";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовАмортизации");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовАмортизации");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
		
	Возврат ПараметрыВыбораСтатейИАналитик;
	
КонецФункции

#КонецОбласти
