#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке();
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		Если НЕ Отказ Тогда
			ВыборкаПоТабличнойЧастиДокумента = СформироватьЗапросПоСтраховойСтаж().Выбрать();
			ПроверитьЗаполнениеТабличнойЧасти(ВыборкаПоТабличнойЧастиДокумента, Отказ);
		КонецЕсли;	
		Если НЕ Отказ Тогда
			ВыборкаПоТабличнойЧастиДокумента.Сбросить();
			ДобавитьСтрокуВДвиженияПоРегиструСведений(ВыборкаПоШапкеДокумента, ВыборкаПоТабличнойЧастиДокумента); 
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСтрокуВДвиженияПоРегиструСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСтраховойСтаж)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	Пока ВыборкаПоСтраховойСтаж.Следующий() Цикл
		Для НомерМесяца = 1 По 12 Цикл
			МесяцЗаписи = Дата(ВыборкаПоСтраховойСтаж.Год, НомерМесяца, 1);
			Если МесяцЗаписи >= ВыборкаПоШапкеДокумента.ПериодС И МесяцЗаписи <= ВыборкаПоШапкеДокумента.ПериодПо Тогда
				Движение = Движения.СтраховойСтажПоДаннымПФУ.Добавить();
				// Свойства
				Движение.Период	= Дата;
				// Измерения
				Движение.ФизическоеЛицо	= ВыборкаПоШапкеДокумента.Сотрудник.ФизическоеЛицо;
				Движение.Организация	= ВыборкаПоШапкеДокумента.Организация;
				Движение.Год			= ВыборкаПоСтраховойСтаж.Год;
				Движение.Месяц			= МесяцЗаписи;
				// Ресурсы
				Движение.ДниСтажа		= ВыборкаПоСтраховойСтаж["Месяц"+НомерМесяца];
				Если ВыборкаПоСтраховойСтаж["ПолныйМесяц"+НомерМесяца] = 1 Тогда
					Движение.МесяцыСтажа = 1
				Иначе
					Движение.МесяцыСтажа = 0
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;	
		Если ВыборкаПоСтраховойСтаж.УчитыватьИтогиГода Тогда
			Движение = Движения.СтраховойСтажПоДаннымПФУ.Добавить();
			Движение.Период	= Дата;
			// Измерения
			Движение.ФизическоеЛицо = ВыборкаПоШапкеДокумента.Сотрудник.ФизическоеЛицо;
			Движение.Организация	= ВыборкаПоШапкеДокумента.Организация;
			// Ресурсы
			Движение.Год			= ВыборкаПоСтраховойСтаж.Год;
			Движение.ДниСтажа		= ВыборкаПоСтраховойСтаж.ВсегоДней;
			Движение.МесяцыСтажа	= ВыборкаПоСтраховойСтаж.ВсегоМесяцев;
		КонецЕсли;
		
	КонецЦикла;	
	Если ВыборкаПоШапкеДокумента.ЛетНачальногоСтажа > 0 Или ВыборкаПоШапкеДокумента.МесяцевНачальногоСтажа > 0 Или ВыборкаПоШапкеДокумента.ДнейНачальногоСтажа > 0  Тогда
		Движение = Движения.СтраховойСтажПоДаннымПФУ.Добавить();
		Движение.Период	= Дата;
		// Измерения
		Движение.ФизическоеЛицо = ВыборкаПоШапкеДокумента.Сотрудник.ФизическоеЛицо;
		Движение.Организация	= ВыборкаПоШапкеДокумента.Организация;
		Движение.Год			= 0;
		// Ресурсы
		Движение.ДниСтажа		= ВыборкаПоШапкеДокумента.ДнейНачальногоСтажа;
		Движение.МесяцыСтажа	= ВыборкаПоШапкеДокумента.МесяцевНачальногоСтажа + ВыборкаПоШапкеДокумента.ЛетНачальногоСтажа*12;
	КонецЕсли;

	Движения.СтраховойСтажПоДаннымПФУ.Записывать = Истина;
	Движения.СтраховойСтажПоДаннымПФУ.Записать();

КонецПроцедуры 

Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВводСведенийДляРасчетаСтраховогоСтажа.Дата КАК Дата,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.Организация КАК Организация,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.Сотрудник КАК Сотрудник,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.Ссылка КАК Ссылка,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.ПериодС КАК ПериодС,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.ПериодПо КАК ПериодПо,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.ДнейНачальногоСтажа,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.МесяцевНачальногоСтажа,
	|	ВводСведенийДляРасчетаСтраховогоСтажа.ЛетНачальногоСтажа
	|ИЗ
	|	Документ.ВводСведенийДляРасчетаСтраховогоСтажа КАК ВводСведенийДляРасчетаСтраховогоСтажа
	|ГДЕ
	|	ВводСведенийДляРасчетаСтраховогоСтажа.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции

Функция СформироватьЗапросПоСтраховойСтаж()
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("ПериодС",				ПериодС);
	Запрос.УстановитьПараметр("ПериодПо",				ПериодПо);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Ссылка,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.НомерСтроки,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц1,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц2,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц3,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц4,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц5,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц6,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц7,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц8,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц9,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц10,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц11,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц12,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ВсегоМесяцев,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ВсегоДней,
	|	ВЫБОР
	|		КОГДА ГОД(&ПериодС) = ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год
	|				И МЕСЯЦ(&ПериодС) > 1
	|			ТОГДА ЛОЖЬ
	|		КОГДА ГОД(&ПериодПо) = ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год
	|				И МЕСЯЦ(&ПериодПо) < 12
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УчитыватьИтогиГода,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц1,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц2,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц3,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц4,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц5,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц6,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц7,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц8,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц9,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц10,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц11,
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц12
	|ИЗ
	|	Документ.ВводСведенийДляРасчетаСтраховогоСтажа.СтраховойСтаж КАК ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж
	|ГДЕ
	|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Ссылка = &ДокументСсылка";
		
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();
	
КонецФункции


Процедура ПроверитьЗаполнениеТабличнойЧасти(ВыборкаПоТабличнойЧастиДокумента, Отказ)
	
	Пока ВыборкаПоТабличнойЧастиДокумента.Следующий() Цикл
		Если Не ВыборкаПоТабличнойЧастиДокумента.УчитыватьИтогиГода Тогда
			ЧислоДнейПомесячно = 0;
			ЧислоДнейПомесячно = ВыборкаПоТабличнойЧастиДокумента.Месяц1 + ВыборкаПоТабличнойЧастиДокумента.Месяц2 + ВыборкаПоТабличнойЧастиДокумента.Месяц3 +
			ВыборкаПоТабличнойЧастиДокумента.Месяц4 + ВыборкаПоТабличнойЧастиДокумента.Месяц5 + ВыборкаПоТабличнойЧастиДокумента.Месяц6 +
			ВыборкаПоТабличнойЧастиДокумента.Месяц7 + ВыборкаПоТабличнойЧастиДокумента.Месяц8 + ВыборкаПоТабличнойЧастиДокумента.Месяц9 +
			ВыборкаПоТабличнойЧастиДокумента.Месяц10 + ВыборкаПоТабличнойЧастиДокумента.Месяц11 + ВыборкаПоТабличнойЧастиДокумента.Месяц12;
			Если ЧислоДнейПомесячно = 0 и (ВыборкаПоТабличнойЧастиДокумента.ВсегоМесяцев > 0 Или ВыборкаПоТабличнойЧастиДокумента.ВсегоДней > 0) Тогда
				ТекстСообщения = НСтр("ru='В строке %1 год не входит в период справки полностью, заполните стаж помесячно.';uk='У рядку %1 рік не входить у період довідки повністю, заповніть стаж помісячно.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыборкаПоТабличнойЧастиДокумента.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,"Объект", Отказ);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
