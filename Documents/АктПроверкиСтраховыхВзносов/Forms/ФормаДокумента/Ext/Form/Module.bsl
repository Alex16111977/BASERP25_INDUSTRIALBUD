
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Ключ.Пустая() Тогда
		// создается новый документ
		ЗначенияДляЗаполнения = Новый Структура;
		ЗначенияДляЗаполнения.Вставить("Месяц", "Объект.ПериодРегистрации");
		ЗначенияДляЗаполнения.Вставить("Организация", "Объект.Организация");
		ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");
		
		ПриПолученииДанныхНаСервере(ТекущийОбъект);
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.УправлениеДоступом
    УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	ПриПолученииДанныхНаСервере(ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Ошибки = ПолучитьСообщенияПользователю(Истина);
	
	ЗарплатаКадрыОтображениеОшибок.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
		ТекущийОбъект,
		ЭтотОбъект,
		Ошибки,
		СоответствиеДанныхОбъектаДанныхФормы());
		
	Для Каждого Сообщение Из Ошибки Цикл
		Если ВРег(Лев(Сообщение.Поле, 7)) = "ОБЪЕКТ." Тогда
			Сообщение.Поле = Сред(Сообщение.Поле, 8);
			Сообщение.ПутьКДанным = "Объект";
		ИначеЕсли  Не ЗначениеЗаполнено(Сообщение.ПутьКДанным) Тогда
			Сообщение.ПутьКДанным = Сообщение.Поле; 
			Сообщение.Поле = "";	
		КонецЕсли;	
		
		Сообщение.Сообщить();
	КонецЦикла;	
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)     
   // СтандартныеПодсистемы.УправлениеДоступом
   УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
   // Конец СтандартныеПодсистемы.УправлениеДоступом
	ЗаполнитьПредставлениеМесяцаПолученияДоходаВСтрокахТаблиц();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	УстановитьФункциональныеОпцииФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииСтрокойПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");
КонецПроцедуры
	
&НаКлиенте
Процедура ПериодРегистрацииСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеДляДоначисленияПриИзменении(Элемент)
	ЗаполнитьСписокВыбора();
КонецПроцедуры


#Область ОбработчикиСобытийТаблицыВзносы

&НаКлиенте
Процедура ВзносыМесяцРасчетногоПериодаСтрокойПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ДанныеТекущейСтроки, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыМесяцРасчетногоПериодаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ДанныеТекущейСтроки, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ВзносыМесяцРасчетногоПериодаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыМесяцРасчетногоПериодаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыМесяцРасчетногоПериодаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(элементы.Взносы.ТекущиеДанные, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ДобавитьПодобранныхФизическихЛицВТаблицу("Взносы", ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ПриИзмененииСоставаОчищаемойТаблицы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПослеУдаления(Элемент)
	ПриИзмененииСоставаОчищаемойТаблицы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыЭлементПриИзменении(Элемент)
	ЗаполнениеСтрокиВзносыФОТ(СтрЗаменить(Элемент.Имя,"Взносы",""), Ложь);
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийТаблицыВзносыФОТ

&НаКлиенте
Процедура ВзносыФОТМесяцРасчетногоПериодаСтрокойПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.ВзносыФОТ.ТекущиеДанные;
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ДанныеТекущейСтроки, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТМесяцРасчетногоПериодаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеТекущейСтроки = Элементы.ВзносыФОТ.ТекущиеДанные;
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ДанныеТекущейСтроки, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТМесяцРасчетногоПериодаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТМесяцРасчетногоПериодаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТМесяцРасчетногоПериодаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(элементы.ВзносыФОТ.ТекущиеДанные, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ДобавитьПодобранныхФизическихЛицВТаблицу("ВзносыФОТ", ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ПриИзмененииСоставаОчищаемойТаблицы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТПослеУдаления(Элемент)
	ПриИзмененииСоставаОчищаемойТаблицы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТЭлементПриИзменении(Элемент)
	ЗаполнениеСтрокиВзносыФОТ(СтрЗаменить(Элемент.Имя,"ВзносыФОТ",""));
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФОТРезультатПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.ВзносыФОТ.ТекущиеДанные;	
	Если ДанныеТекущейСтроки.Результат < 0 Тогда
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#Область ОбработчикиКомандФормы
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
// СтандартныеПодсистемы.Печать
//	
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура ПодборВзносы(Команда)
	КадровыйУчетКлиент.ПодобратьФизическихЛицОрганизации(
		Элементы.Взносы, 
		Объект.Организация, 
		АдресСпискаПодобранныхСотрудников("Взносы"), 
		Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПодборВзносыФОТ(Команда)
	КадровыйУчетКлиент.ПодобратьФизическихЛицОрганизации(
		Элементы.ВзносыФОТ, 
		Объект.Организация, 
		АдресСпискаПодобранныхСотрудников("ВзносыФОТ"), 
		Истина);
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	УстановитьФункциональныеОпцииФормы();
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");	
	
	ЗаполнитьПредставлениеМесяцаПолученияДоходаВСтрокахТаблиц();
	
	УстановитьВидимостьКолонокТаблиц();
	
	ОписаниеКлючевыхРеквизитов = КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов();
	ТаблицыОчищаемыеПриИзменении = КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении();
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма, , ОписаниеКлючевыхРеквизитов, ТаблицыОчищаемыеПриИзменении);
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма, ОписаниеКлючевыхРеквизитов);
	
	ЗаполнитьСписокВыбора();
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("Организация", Объект.Организация);
		ПараметрыФО.Вставить("Период", ТекущаяДатаСеанса());
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонокТаблиц()
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПредставлениеМесяцаПолученияДоходаВСтрокахТаблиц()
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.ВзносыФОТ, "МесяцРасчетногоПериода", "МесяцРасчетногоПериодаСтрокой");
КонецПроцедуры	

&НаКлиенте
Процедура ДобавитьПодобранныхФизическихЛицВТаблицу(ИмяТаблицы, СписокФизическихЛиц)
	Для Каждого ФизическоеЛицо Из СписокФизическихЛиц Цикл
		СтрокаТаблицы = Объект[ИмяТаблицы].Добавить();
		СтрокаТаблицы.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;	
	ПриИзмененииСоставаОчищаемойТаблицы(ЭтотОбъект);
КонецПроцедуры	

&НаСервере
Функция СоответствиеДанныхОбъектаДанныхФормы()
	СоответствиеДанных = ЗарплатаКадрыОтображениеОшибок.ОписаниеПодчиненностиДанныхФормы();
	
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиРеквизитов(СоответствиеДанных, "ПериодРегистрации", "ПериодРегистрацииСтрокой");
	
		
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		СоответствиеДанных,
		"Взносы",
		"МесяцРасчетногоПериода",
		"Объект.Взносы",
		"МесяцРасчетногоПериодаСтрокой");
		
	ЗарплатаКадрыОтображениеОшибок.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		СоответствиеДанных,
		"ВзносыФОТ",
		"МесяцРасчетногоПериода",
		"Объект.ВзносыФОТ",
		"МесяцРасчетногоПериодаСтрокой");
		
	Возврат СоответствиеДанных;		
	
КонецФункции	

&НаСервере
Функция АдресСпискаПодобранныхСотрудников(ИмяТаблицы)
	
	Возврат ПоместитьВоВременноеХранилище(Новый Массив, УникальныйИдентификатор);
	
КонецФункции

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Взносы");
	Массив.Добавить("Объект.ВзносыФОТ");
	
	Возврат Массив
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация", НСтр("ru='организации';uk='організації'")));
	
	Возврат Массив
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииСоставаОчищаемойТаблицы(Форма)
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(Форма);	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеСтрокиВзносыФОТСервер(ДанныеСтроки, ЭтоВзносыФОТ)
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.Организация) ИЛИ НЕ ЗначениеЗаполнено(ДанныеСтроки.ФизическоеЛицо) ИЛИ НЕ ЗначениеЗаполнено(ДанныеСтроки.МесяцРасчетногоПериода) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ВидЕСВ) Тогда
		Возврат;
	КонецЕсли;
	
	УчетСтраховыхВзносовРасширенный.ЗаполнитьДанныеСтрокиИсправленияВзносов(ДанныеСтроки, ЭтоВзносыФОТ);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеСтрокиВзносыФОТ(Элемент, ЭтоВзносыФОТ = Истина)
	
	Если ЭтоВзносыФОТ Тогда
		ДанныеТекущейСтроки = Элементы.ВзносыФОТ.ТекущиеДанные;
	Иначе
		ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	КонецЕсли;	
	ДанныеСтроки = Новый Структура();
	ДанныеСтроки.Вставить("Организация", Объект.Организация);
	ДанныеСтроки.Вставить("ФизическоеЛицо", ДанныеТекущейСтроки.ФизическоеЛицо);
	ДанныеСтроки.Вставить("МесяцРасчетногоПериода", ДанныеТекущейСтроки.МесяцРасчетногоПериода);
	Если Элемент = "ВидЕСВ" Тогда
		ДанныеСтроки.Вставить("ВидЕСВ", ДанныеТекущейСтроки.ВидЕСВ);
		ДанныеСтроки.Вставить("КатегорияЕСВ", Неопределено);
		ДанныеСтроки.Вставить("Налог", Неопределено);
		ДанныеСтроки.Вставить("СтатьяНалоговойДекларации", Неопределено);
	ИначеЕсли Элемент = "КатегорияЕСВ" Тогда
		ДанныеСтроки.Вставить("ВидЕСВ", ДанныеТекущейСтроки.ВидЕСВ);
		ДанныеСтроки.Вставить("КатегорияЕСВ", ДанныеТекущейСтроки.КатегорияЕСВ);
		ДанныеСтроки.Вставить("Налог", Неопределено);
		ДанныеСтроки.Вставить("СтатьяНалоговойДекларации", Неопределено);
	ИначеЕсли Элемент = "Налог" Тогда
		ДанныеСтроки.Вставить("ВидЕСВ", ДанныеТекущейСтроки.ВидЕСВ);
		ДанныеСтроки.Вставить("КатегорияЕСВ", ДанныеТекущейСтроки.КатегорияЕСВ);
		ДанныеСтроки.Вставить("Налог", ДанныеТекущейСтроки.Налог);
		ДанныеСтроки.Вставить("СтатьяНалоговойДекларации", Неопределено);
	КонецЕсли;	
	ЗаполнениеСтрокиВзносыФОТСервер(ДанныеСтроки, ЭтоВзносыФОТ);
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтроки, ДанныеСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбора()
	
	Этаформа.Элементы.ВзносыФОТТипНачисления.СписокВыбора.Очистить();
	Этаформа.Элементы.ВзносыТипНачисления.СписокВыбора.Очистить();
	
	Если Объект.ОснованиеДляДоначисления = Перечисления.ОснованияДляДоначисленияСтраховыхВзносов.ДляИсправленияОшибок Тогда
		Этаформа.Элементы.ВзносыФОТТипНачисления.СписокВыбора.Добавить(2,НСтр("ru='2 - Доначислено';uk='2 - Донараховано'"));
		Этаформа.Элементы.ВзносыФОТТипНачисления.СписокВыбора.Добавить(3,НСтр("ru='3 - Уменьшено';uk='3 - Зменшено'"));
		Этаформа.Элементы.ВзносыТипНачисления.СписокВыбора.Добавить(2,НСтр("ru='2 - Доначислено';uk='2 - Донараховано'"));
		Этаформа.Элементы.ВзносыТипНачисления.СписокВыбора.Добавить(3,НСтр("ru='3 - Уменьшено';uk='3 - Зменшено'"));

	Иначе
		Этаформа.Элементы.ВзносыФОТТипНачисления.СписокВыбора.Добавить(11,НСтр("ru='11 - Доначислено';uk='11 - Донараховано'"));
		Этаформа.Элементы.ВзносыФОТТипНачисления.СписокВыбора.Добавить(12,НСтр("ru='12 - Уменьшено';uk='12 - Зменшено'"));
		Этаформа.Элементы.ВзносыТипНачисления.СписокВыбора.Добавить(11,НСтр("ru='11 - Доначислено';uk='11 - Донараховано'"));
		Этаформа.Элементы.ВзносыТипНачисления.СписокВыбора.Добавить(12,НСтр("ru='12 - Уменьшено';uk='12 - Зменшено'"));
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти


