#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Процедура обновляет движения документа принятия к учету после отражения в регл. учете
// Вызывается при отражения в регл. учете из функции "РеглУчетПроведениеСервер.ДополнительнаяОбработкаПриОтраженииДокумента".
//
// Параметры:
// 		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджера временных таблиц проводок в регл. учете.
//
Процедура ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПринятиеКУчетуОС.Ссылка КАК Ссылка,
	|	ПринятиеКУчетуОС.Дата КАК Дата
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ПринятиеКУчетуОС.Ссылка = ДокументыКОтражению.Ссылка";
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	
	Если Не РеквизитыДокумента.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, РеквизитыДокумента.Ссылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Неопределено);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, Неопределено);
	
	НачатьТранзакцию();
	
	Попытка
		
		// БлокировкаДанных
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрочиеРасходы.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыАмортизацииОСБУ.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияОС.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		Блокировка.Заблокировать();
		
		// Запись движений
		
		Документ = ПроведениеДокументов.ЭмуляцияДокумента(РеквизитыДокумента.Ссылка, РеквизитыДокумента.Дата);
		МенеджерВременныхТаблицЗаписи = Новый МенеджерВременныхТаблиц;
				
		НаборЗаписей = РегистрыСведений.ПараметрыАмортизацииОСБУ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПараметрыАмортизацииОСБУ);
		ПроведениеДокументов.УстановитьДопСвойстваРегистра(НаборЗаписей, Документ, МенеджерВременныхТаблицЗаписи);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияОС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияОС);
		ПроведениеДокументов.УстановитьДопСвойстваРегистра(НаборЗаписей, Документ, МенеджерВременныхТаблицЗаписи);
		НаборЗаписей.Записать();
		
		ДанныеТаблиц = ПроведениеДокументов.ДанныеТаблицСтаройСтруктуры(Документ, МенеджерВременныхТаблицЗаписи);
		ВнеоборотныеАктивы.СформироватьЗаписиРегистровЗаданий(РеквизитыДокумента.Ссылка, ДанныеТаблиц);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Заполнение регистров ОС после отражения документа в регламентированном учете';uk='Заповнення регістрів ОЗ після відображення документа в регламентованому обліку'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//++ НЕ УТКА
	Документы.ПринятиеКУчетуОСМеждународныйУчет.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//-- НЕ УТКА
	
КонецПроцедуры

// Добавляет команду создания документа "Принятие к учету ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании	 - ТаблицаЗначений	 - Таблица с командами создания на основании. Для изменения.
//  	См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//  Порядок						 - Число			 - Порядок команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПринятиеКУчетуОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПринятиеКУчетуОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПринятиеКУчетуОС";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	ДанныеДокумента.СчетУчета КАК СчетУчета,
	|	ДанныеДокумента.СчетАмортизации КАК СчетАмортизации,
	|	ДанныеДокумента.СчетУчетаДооценокОС КАК СчетДооценки,
	|	ДанныеДокумента.Местонахождение КАК Местонахождение,
	|	ДанныеДокумента.МОЛ КАК МОЛ,
	|	ДанныеДокумента.АдресМестонахождения КАК АдресМестонахождения,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.НачислятьАмортизациюБУ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ,
	|	ДанныеДокумента.СрокИспользованияБУ,
	|	ДанныеДокумента.ГрафикАмортизации,
	|	ДанныеДокумента.НачислятьАмортизациюНУ,
	|	ДанныеДокумента.СпособНачисленияАмортизацииНУ,
	|	ДанныеДокумента.СрокИспользованияНУ,
	|	ДанныеДокумента.НалоговаяГруппаОС,
	|	ДанныеДокумента.НалоговоеНазначение,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.ДокументНаОснованииИнвентаризации,
	|	ВЫБОР
 	|		КОГДА ДанныеДокумента.ДокументНаОснованииИнвентаризации
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтоимостьЗаданаДокументом
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ГраницаКонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонецДня", КонецДня(Реквизиты.Период));
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик("НастройкиУчетаНалогаНаПрибыль",
		Реквизиты.Организация,
		Реквизиты.Период);
		
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПринятиеКУчетуОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Принятие к учету ОС';uk='Прийняття до обліку ОЗ'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса)
	
	ИмяТаблицы = "ТабличнаяЧасть";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции

Функция ДанныеДокументаИУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса);
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтСчетаВложенияВоВнеоборотныеАктивы(Ложь, ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	СУММА(ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.СуммаОстаток, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ВЫБОР
	|			КОГДА  &НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьНУ + ЕСТЬNULL(ДанныеУчета.СуммаОстаток, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	ТабличнаяЧасть КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ГраницаКонецМесяца,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ
	|						втСчетаВложенияВоВнеоборотныеАктивы КАК Т),
	|				,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И НаправлениеДеятельности = &НаправлениеДеятельности
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ТабличнаяЧасть КАК Т)) КАК ДанныеУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ДанныеУчета.Субконто1
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки";
	 
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции

Процедура ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса);
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	СУММА(ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.Сумма, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ВЫБОР
	|			КОГДА &НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьНУ + ЕСТЬNULL(ДанныеУчета.Сумма, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	ТабличнаяЧасть КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				,
	|				,
	|				Регистратор = &Ссылка
	|					И СчетДт = &СчетУчета, 
	|				,
	|				) КАК ДанныеУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ДанныеУчета.СубконтоДт1
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеДокументаИУчета(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	втДанныеДокументаИУчета.Ссылка,
	|	втДанныеДокументаИУчета.НомерСтроки,
	|	втДанныеДокументаИУчета.ОсновноеСредство,
	|	втДанныеДокументаИУчета.ИнвентарныйНомер,
	|	втДанныеДокументаИУчета.ПоказательНаработки,
	|	втДанныеДокументаИУчета.ОбъемНаработки,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	втДанныеДокументаИУчета КАК втДанныеДокументаИУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции


Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&МОЛ КАК МОЛ,
	|	&Местонахождение КАК Местонахождение,
	|	&АдресМестонахождения КАК АдресМестонахождения
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&СрокИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	&СрокИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	&СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	&СрокИспользованияНУ КАК СрокИспользованияДляВычисленияАмортизацииНУ,
	|	ТаблицаОС.ОбъемНаработки КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК СтоимостьДляВычисленияАмортизацииНУ,
	|	&ГрафикАмортизации КАК ГрафикАмортизации,
	|	&ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	&Период КАК ДатаПоследнегоИзменения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "Документ.ПринятиеКУчетуОС");
	
	Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	&СпособНачисленияАмортизацииНУ		КАК МетодНачисленияАмортизацииНУ,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	&Период КАК ДатаВводаВЭксплуатациюБУ,
	|	&Ссылка КАК ДокументВводаВЭксплуатациюБУ
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента,
	|	&СобытиеОС КАК Событие
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|	&НачислятьАмортизациюБУ                 КАК НачислятьАмортизациюБУ,
	|	&НачислятьАмортизациюНУ                 КАК НачислятьАмортизациюНУ,
	|	&СтатьяРасходов                         КАК СтатьяРасходовБУ,
	|	&АналитикаРасходов                      КАК АналитикаРасходовБУ,
	|	&НалоговаяГруппаОС						КАК НалоговаяГруппаОС,
	|	&НалоговоеНазначение					КАК НалоговоеНазначение
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&СчетУчета                              КАК СчетУчета,
	|	&СчетАмортизации                        КАК СчетНачисленияАмортизации,
	|	&СчетДооценки                      		КАК СчетДооценки,
	|	ТаблицаОС.ПоказательНаработки           КАК ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки                КАК ОбъемНаработки
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	
	|	&Организация КАК Организация,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры
Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Местонахождение                        КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Местонахождение                        КАК Подразделение,
	|	&Организация                            КАК Организация,
	|	&Проведен                               КАК Проведен,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
		
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ВременнаяТаблицаДанныеДокумента());
	ТекстыОтражения.Добавить(ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтСчетаВложенияВоВнеоборотныеАктивы(Ложь, Неопределено));
	ТекстыОтражения.Добавить(ВременнаяТаблицаОстаткиСчетаКапитализации());
	ТекстыОтражения.Добавить(Символы.ПС);
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
		
КонецФункции

Функция ВременнаяТаблицаДанныеДокумента()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.Местонахождение КАК Местонахождение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.СчетАмортизации,
	|	ТаблицаДокумента.СчетУчетаДооценокОС КАК СчетДооценки,
	|	ТаблицаДокумента.НачислятьАмортизациюБУ,
	|	ТаблицаДокумента.НалоговоеНазначение,
	|	ТаблицаДокумента.СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов,
	|	ТабличнаяЧасть.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаДокумента.ДокументНаОснованииИнвентаризации
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧасть
	|		ПО ТаблицаДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
КонецФункции

Функция ВременнаяТаблицаОстаткиСчетаКапитализации()
	
	Возврат
	"ВЫБРАТЬ
	|	ДанныеУчета.Счет                 КАК Счет,
	|	ДанныеУчета.Субконто1            КАК ОсновноеСредство,
	|	СУММА(ДанныеУчета.СуммаОборот)   КАК СуммаБУ
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&ГраницаМесяцОкончание,
	|			Регистратор,
	|			Счет В (ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ втСчетаВложенияВоВнеоборотныеАктивы КАК Т),
	|			,
	|			(Организация, Подразделение, НаправлениеДеятельности, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.НаправлениеДеятельности,
	|					Т.ОсновноеСредство
	|				ИЗ
	|					втДанныеДокумента КАК Т),
	|			,
	|			) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Субконто1,
	|	ДанныеУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.ОсновноеСредство,
	|	СУММА(втОстатки.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втОстаткиСгруппированные
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.ОсновноеСредство
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.ОсновноеСредство,
	|	втОстатки.СуммаБУ КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализации
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.ОсновноеСредство = втДанныеДокумента.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.ОсновноеСредство,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализацииСгруппированные
	|ИЗ
	|	втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.ОсновноеСредство";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ТекстОтраженияВРеглУчетеУКР();
		
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ПринятиеКУчетуОС());
		
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ПринятиеКУчетуОС()

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 15)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДокумента.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА втДанныеДокумента.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ втДанныеСчетаКапитализации.СуммаБУ
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|   ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	 |		0 КАК СуммаНУКт, 
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		втДанныеДокумента.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И втДанныеДокумента.Организация = УчетнаяПолитикаОрганизаций.Организация
	|
	|ГДЕ
	|	втДанныеСчетаКапитализации.СуммаБУ <> 0
	|";

	ТекстЗапроса = СтрШаблон(ТекстЗапроса, 
		НСтр("ru='Принятие к учету ОС';uk='Прийняття до обліку ОЗ'",ЯзыкСодержания)); 
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать


// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОЗ1";
	КомандаПечати.Идентификатор = "ОЗ1";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-1';uk='Форма ОЗ-1'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьФормОСБюджет";
	КомандаПечати.Идентификатор = "АктВводаОСБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт ввода в эксплуатацию основных средств';uk='Акт введення в експлуатацію основних засобів'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОЗ1(ПараметрыПечати, Ссылка) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если Ссылка.Проведен Тогда
		
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	Док.Дата КАК ДатаАкта,
		|	Док.Дата КАК ДатаВвода,
		|	Док.Номер КАК НомерАкта,
		|	Док.Подразделение.Представление КАК ПринялоПодразделение,
		|	Док.Подразделение КАК ПодразделениеПриняло,
		|	Док.Организация КАК Организация,
		|	Док.МОЛ.Код КАК КодМОЛа,
		|	Док.СчетУчета КАК СчетДт,
		|	Хозрасчетный.СчетКт КАК СчетКт,
		|	Док.Принял КАК Принял,
		|	Док.Сдал КАК Сдал,
		|	Док.Ссылка КАК Ссылка
		|	Поместить ШапкаДокумента
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|		ПО (Хозрасчетный.Регистратор = Док.Ссылка)
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|	И НЕ Хозрасчетный.СчетДт.Забалансовый
		|;
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.ДатаАкта КАК ДатаАкта,
		|	Док.ДатаВвода КАК ДатаВвода,
		|	Док.НомерАкта КАК НомерАкта,
		|	Док.ПринялоПодразделение КАК ПринялоПодразделение,
		|	Док.ПодразделениеПриняло КАК ПодразделениеПриняло,
		|	Док.Организация КАК Организация,
		|	Док.КодМОЛа КАК КодМОЛа,
		|	Док.СчетДт КАК СчетДт,
		|	Док.СчетКт КАК СчетКт,
		|	Док.Принял КАК Принял,
		|	Док.Сдал КАК Сдал,
		|	""Прийняття до обліку ОЗ"" КАК ВидОперации,
		|	ПервоначальныеСведения.ОсновноеСредство.ИнвентарныйНомер   КАК ИнвентарныйНомер,
		|	ПервоначальныеСведения.ПервоначальнаяСтоимостьБУ           КАК ПервоначальнаяСтоимость,
		|	ПервоначальныеСведения.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	ПервоначальныеСведения.ОсновноеСредство.ЗаводскойНомер     КАК ЗаводскойНомер,
		|	ПервоначальныеСведения.ОсновноеСредство.ДатаВыпуска        КАК ГодВыпуска,
		|	ПервоначальныеСведения.ОсновноеСредство.НомерПаспорта      КАК НомерПаспорта
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведения
		|    	ЛЕВОЕ СОЕДИНЕНИЕ
		|    		ШапкаДокумента Как Док
		|    	По Док.Ссылка = ПервоначальныеСведения.Регистратор	
		|ГДЕ
		|	ПервоначальныеСведения.Регистратор = &Ссылка";
			
	Иначе
			
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	Док.Дата КАК ДатаАкта,
		|	Док.Дата КАК ДатаВвода,
		|	Док.Номер КАК НомерАкта,
		|	Док.Подразделение.Представление КАК ПринялоПодразделение,
		|	Док.Подразделение КАК ПодразделениеПриняло,
		|	Док.Организация КАК Организация,
		|	Док.МОЛ.Код КАК КодМОЛа,
		|	Док.СчетУчета КАК СчетДт,
		|	Хозрасчетный.СчетКт КАК СчетКт,
		|	Док.Принял КАК Принял,
		|	Док.Сдал КАК Сдал,
		|	Док.Ссылка КАК Ссылка
		|	Поместить ШапкаДокумента
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|		ПО (Хозрасчетный.Регистратор = Док.Ссылка)
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|			И НЕ Хозрасчетный.СчетДт.Забалансовый
		|;
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.ДатаАкта КАК ДатаАкта,
		|	Док.ДатаВвода КАК ДатаВвода,
		|	Док.НомерАкта КАК НомерАкта,
		|	Док.ПринялоПодразделение КАК ПринялоПодразделение,
		|	Док.ПодразделениеПриняло КАК ПодразделениеПриняло,
		|	Док.Организация КАК Организация,
		|	Док.КодМОЛа КАК КодМОЛа,
		|	Док.СчетДт КАК СчетДт,
		|	Док.СчетКт КАК СчетКт,
		|	Док.Принял КАК Принял,
		|	Док.Сдал КАК Сдал,
		|	""Прийняття до обліку ОЗ"" КАК ВидОперации,
		|	ПервоначальныеСведения.ИнвентарныйНомер   КАК ИнвентарныйНомер,
		|	ПервоначальныеСведения.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	ПервоначальныеСведения.ОсновноеСредство.ЗаводскойНомер     КАК ЗаводскойНомер,
		|	ПервоначальныеСведения.ОсновноеСредство.ДатаВыпуска        КАК ГодВыпуска,
		|	ПервоначальныеСведения.ОсновноеСредство.НомерПаспорта      КАК НомерПаспорта
		|ИЗ
		|	Документ.ПринятиеКУчетуОС.ОС КАК ПервоначальныеСведения
		|   	ЛЕВОЕ СОЕДИНЕНИЕ
		|   		ШапкаДокумента Как Док
		|    	По Док.Ссылка = ПервоначальныеСведения.Ссылка	
		|ГДЕ
		|	ПервоначальныеСведения.Ссылка = &Ссылка";
		
	КонецЕсли;
	ВыборкаПоОС = Запрос.Выполнить();
	
	Возврат ВыборкаПоОС;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыАктВводаОС_2016(ПараметрыПечати, Ссылка) Экспорт
		
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	Док.Дата КАК ДатаАкта,
	|	Док.Дата КАК ДатаВвода,
	|	Док.Номер КАК НомерАкта,
	|	Док.Подразделение.Представление КАК МестоХранения,
	|	Док.Подразделение КАК ПодразделениеПриняло,
	|	Док.Организация КАК Организация,
	|	Док.МОЛ КАК Принял,
	|	Док.СчетУчета КАК СчетДт,
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка";	
	
	ВыборкаПоШапке = Запрос.Выполнить();

	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	ПервоначальныеСведения.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведения.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
	|	ПервоначальныеСведения.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ПервоначальныеСведения.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	ПервоначальныеСведения.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
	|	0 КАК ЦенаИзноса,
	|	0 КАК СуммаИзноса,
	|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокПолезногоИспользования
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО ПервоначальныеСведения.ОсновноеСредство = ПараметрыАмортизацииОСБУ.ОсновноеСредство
	|ГДЕ
	|	ПараметрыАмортизацииОСБУ.Регистратор = &Ссылка
	|	И ПервоначальныеСведения.Регистратор = &Ссылка"; 
	
	ВыборкаПоОС = Запрос.Выполнить();

	Запрос.Текст =      
	
	"ВЫБРАТЬ
	|	Хозрасчетный.СчетДт КАК СчетДт,
	|	Хозрасчетный.СчетКт КАК СчетКт,
	|	Хозрасчетный.Сумма КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.НомерСтроки";
	
	ВыборкаБУ = Запрос.Выполнить();
	
	Возврат Новый Структура("ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ", ВыборкаПоШапке, ВыборкаПоОС, ВыборкаБУ);
	
КонецФункции //ПолучитьДанныеДляПечатнойФормыАктВводаОС_2016()


#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	


	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
