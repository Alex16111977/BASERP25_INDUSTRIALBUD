
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуОС.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриСозданииЧтенииНаСервере();
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуОС.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.ОбъектыЭксплуатации.Форма.ФормаВыбора" Тогда
		
		Если ВыбранноеЗначение.Количество() > 0 Тогда
			
			ДополнительныеДанныеПодбора = ДополнительныеДанныеПодбора(
				ВыбранноеЗначение, Объект.Организация, Объект.Дата, ИспользуетсяСтоимостьНУ);
				
			Для Каждого ОсновноеСредство Из ВыбранноеЗначение Цикл
				ДанныеСтроки = Объект.ОС.Добавить();
				ДанныеСтроки.ОсновноеСредство = ОсновноеСредство;
				ДопДанные = ДополнительныеДанныеПодбора.Получить(ОсновноеСредство);
				ДанныеСтроки.ИнвентарныйНомер = ?(ЗначениеЗаполнено(ДопДанные.ИнвентарныйНомер), 
													ДопДанные.ИнвентарныйНомер, 
													ДопДанные.Код);
			КонецЦикла;
			
			ОбновитьСвойстваЭлементов("ОсновноеСредство");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборОсновныхСредств.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "РегистрСведений.СоставКомиссий.Форма.ФормаВыбора" Тогда
		ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(Знач ВыбранноеЗначение)
	ДобавленныеСтроки = УчетОСВызовСервера.ОбработатьПодборОсновныхСредств(Объект.ОС, ВыбранноеЗначение);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	ДоходыИРасходыСервер.ПослеЗаписиНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ПринятиеКУчетуОС", ПараметрыЗаписи, Объект.Ссылка);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбъектыЭксплуатации"));
	
	Если Объект.ДокументНаОснованииИнвентаризации Тогда
		Оповестить("ЗаписьДокументаНаОснованииИнвентаризации",, Объект.Ссылка);
	КонецЕсли;

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

	ОбщегоНазначенияУТКлиент.ВыполнитьДействияПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументНаОснованииПриИзменении(Элемент)
	
	Если Объект.ДокументНаОсновании Тогда
		Если СписокТиповДокументаОснования.Количество() > 0 Тогда
			СписокТиповДокументаОснования.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ВыборТипаДокументаОснования", ЭтаФорма), НСтр("ru='Документ основание';uk='Документ підстава'"));
		Иначе
			ОткрытьФорму("Документ.ИнвентаризацияОС.ФормаВыбора", , ЭтаФорма, ЭтаФорма,,, Новый ОписаниеОповещения("ВыборДокументаОснования", ЭтаФорма));
		КонецЕсли;
	Иначе
		Объект.ДокументОснование = Неопределено;
		Объект.ДокументНаОснованииИнвентаризации = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТипаДокументаОснования(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Объект.ДокументНаОсновании = Ложь;
	Иначе
		ОткрытьФорму(Результат.Значение, , ЭтаФорма, ЭтаФорма,,, Новый ОписаниеОповещения("ВыборДокументаОснования", ЭтаФорма));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДокументаОснования(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Объект.ДокументОснование = Результат;
		Объект.ДокументНаОснованииИнвентаризации = (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияОС"));
	Иначе
		Объект.ДокументНаОсновании = Ложь;
		Объект.ДокументНаОснованииИнвентаризации = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура БУНачислятьАмортизациюПриИзменении(Элемент)
	
	Если Объект.НачислятьАмортизациюБУ <> Объект.НачислятьАмортизациюНУ Тогда
		Объект.НачислятьАмортизациюНУ = Объект.НачислятьАмортизациюБУ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БУМетодНачисленияАмортизацииПриИзменении(Элемент)
	
	БУМетодНачисленияАмортизацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура БУМетодНачисленияАмортизацииПриИзмененииНаСервере()
	
	ЗаполнитьСписокСпособовАмортизацииНУ();
	Объект.СпособНачисленияАмортизацииНУ = Объект.МетодНачисленияАмортизацииБУ;
	ОбновитьСвойстваЭлементов("ПараметрыАмортизации");
		
КонецПроцедуры

&НаСервере
Процедура СпособНачисленияАмортизацииНУПриИзмененииНаСервере()
	ОбновитьСвойстваЭлементов("ПараметрыАмортизации");
КонецПроцедуры

&НаКлиенте
Процедура СпособНачисленияАмортизацииНУПриИзменении(Элемент)
	СпособНачисленияАмортизацииНУПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура БУСрокИспользованияПриИзменении(Элемент)
	
	Если Объект.СрокИспользованияНУ = 0
		Или СрокИспользованияБУ = Объект.СрокИспользованияНУ Тогда
		
		Объект.СрокИспользованияНУ = Объект.СрокИспользованияБУ;
		НУСрокИспользованияРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
			Объект.СрокИспользованияНУ);
		
	КонецЕсли;
	
	БУСрокИспользованияРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияБУ);
	
	СрокИспользованияБУ = Объект.СрокИспользованияБУ;
	
КонецПроцедуры

&НаКлиенте
Процедура НУСрокИспользованияПриИзменении(Элемент)
	
	НУСрокИспользованияРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияНУ);
	
КонецПроцедуры


&НаКлиенте
Процедура АдресМестонахожденияПриИзменении(Элемент)
	
	АдресМестонахожденияЗначенияПолей = ЗначенияПолейКонтактнойИнформацииСервер(Объект.АдресМестонахождения,
		ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияОчистка(Элемент, СтандартнаяОбработка)
	
	АдресМестонахожденияЗначенияПолей = "";
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(
		УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
			ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПартнера"),
			АдресМестонахожденияЗначенияПолей),
		ЭтотОбъект,
		Новый ОписаниеОповещения("АдресМестонахожденияЗавершениеВыбора", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресМестонахожденияЗавершениеВыбора(РезультатОткрытияФормы, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(РезультатОткрытияФормы)<>Тип("Структура") Тогда
		// не было изменений в данных
		Возврат;
	КонецЕсли;
	
	АдресМестонахожденияЗначенияПолей = РезультатОткрытияФормы.КонтактнаяИнформация;
	Объект.АдресМестонахождения = РезультатОткрытияФормы.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
	
	СтатьяРасходовДоИзменения = Объект.СтатьяРасходов;
	АналитикаРасходовДоИзменения = Объект.АналитикаРасходов;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры
 
&НаКлиенте
Процедура АналитикаРасходовПриИзменении(Элемент)
	
	АналитикаРасходовДоИзменения = Объект.АналитикаРасходов;
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры



&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура СтатьяДоходовПоИнвентаризацииПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСпискиСпособовАмортизации()
	ЗаполнитьСписокСпособовАмортизацииБУ();
	ЗаполнитьСписокСпособовАмортизацииНУ();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСпособовАмортизацииБУ()
	Элементы.БУСпособНачисленияАмортизации.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииБУ());
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСпособовАмортизацииНУ()
	Элементы.СпособНачисленияАмортизацииНУ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСпособовАмортизацииНУ());
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСпособовАмортизацииБУ() Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Линейный);
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	
	СчетУчетаБУ = Объект.СчетУчета;
	
	Если ЗначениеЗаполнено(СчетУчетаБУ) 
	   И СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа) Тогда		  
	   
	   	Если СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.БиблиотечныеФонды 
		 ИЛИ СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивы Тогда			
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._50_50);
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._100);			
		КонецЕсли;
		
	Иначе	
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УскоренногоУменьшенияОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Кумулятивный);		
	КонецЕсли;
	
	Возврат СписокПеречисления;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокСпособовАмортизацииНУ() Экспорт
	
	СписокПеречисления = Новый Массив;
	
	СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Линейный);
	
	Если Объект.МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции Тогда
			
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
		
	КонецЕсли;
	
	СчетУчетаБУ = Объект.СчетУчета;
	
	Если ЗначениеЗаполнено(СчетУчетаБУ) 
	   И СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа) Тогда		  
	   
	   	Если СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.БиблиотечныеФонды 
		 ИЛИ СчетУчетаБУ.Родитель = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивы Тогда			
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._50_50);
			СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС._100);			
		КонецЕсли;
		
	Иначе	
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.УскоренногоУменьшенияОстатка);
		СписокПеречисления.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.Кумулятивный);		
	КонецЕсли;
	
	Возврат СписокПеречисления;
	
КонецФункции

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	ОбновитьСвойстваЭлементов("СчетУчета");
	СчетУчетаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СчетУчетаПриИзмененииНаСервере()
	
	СчетУчета = Объект.СчетУчета;
	
	Если СчетУчета = ПланыСчетов.Хозрасчетный.ЗемельныеУчастки Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа1;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.КапитальныеЗатратыПоУлучшениюЗемель Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа2;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДомаИСооружения Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа3;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МашиныИОборудование Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа4;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ТранспортныеСредства Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа5;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ИнструментыПриборыИИнвентарь Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа6;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.Животные Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа7;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МноголетниеНасаждения Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа8;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ДругиеОсновныеСредства Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа9;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.БиблиотечныеФондыИндивидуально Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа10;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыИндивидуально Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа11;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ВременныеНетитульныеСооружения Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа12;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ПриродныеРесурсы Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа13;
	ИначеЕсли СчетУчета = ПланыСчетов.Хозрасчетный.ИнвентарнаяТара Тогда
		Объект.НалоговаяГруппаОС = Справочники.НалоговыеГруппыОсновныхСредств.ОСГруппа14;
	КонецЕсли; 

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

&НаКлиенте
Процедура ОСПослеУдаления(Элемент)
	
	ОбновитьСвойстваЭлементов("ОсновноеСредство");
	
КонецПроцедуры

&НаКлиенте
Процедура ОСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ОС.ТекущиеДанные;
	ОССтоимостьБУ = ТекущиеДанные.СтоимостьБУ;
	
КонецПроцедуры

&НаКлиенте
Процедура ОСОсновноеСредствоПриИзменении(Элемент)
	
	ОСОсновноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОССтоимостьБУПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОС.ТекущиеДанные;
	
	Если ТекущиеДанные.СтоимостьНУ = ОССтоимостьБУ Тогда
		ТекущиеДанные.СтоимостьНУ = ТекущиеДанные.СтоимостьБУ;
		ОССтоимостьБУ = ТекущиеДанные.СтоимостьБУ;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоНаименованию(Команда)
	
	ОсновноеСредство = ВнеоборотныеАктивыКлиентЛокализация.ПолучитьОСДляЗаполнениеПоНаименованию(
	ПараметрыЗаполненияПоНаименованию(ЭтаФорма));
	
	Если ЗначениеЗаполнено(ОсновноеСредство) Тогда
		ЗаполнитьПоНаименованиюСервер(ОсновноеСредство);
	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Процедура Подбор(Команда)
	
	МассивСостояний = Новый Массив;
	МассивСостояний.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияОС.НеПринятоКУчету"));
	МассивСостояний.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияОС.СнятоСУчета"));
	МассивСостояний.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету"));
	
	ПараметрыОтбор = Новый Структура;
	ПараметрыОтбор.Вставить("БУСостояние", МассивСостояний);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контекст", "БУ, МФУ");
	ПараметрыФормы.Вставить("ДатаСведений", Объект.Дата);
	ПараметрыФормы.Вставить("ТекущийРегистратор", Объект.Ссылка);
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.ОбъектыЭксплуатации.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#Область СтандартныеПодсистемы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ПрочитатьПараметрыУчетнойПолитики();
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
	АдресМестонахожденияЗначенияПолей = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
		Объект.АдресМестонахождения,
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	БУСрокИспользованияРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияБУ);
	
	НУСрокИспользованияРасшифровка = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
		Объект.СрокИспользованияНУ);
	
	ОбновитьСвойстваЭлементов();
	
	СрокИспользованияБУ = Объект.СрокИспользованияБУ;
	
	СтатьяРасходовДоИзменения = Объект.СтатьяРасходов;
	АналитикаРасходовДоИзменения = Объект.АналитикаРасходов;
	
	ЗаполнитьСписокТиповДокументаОснования();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТиповДокументаОснования()
	
	СписокТиповДокументаОснования = Новый СписокЗначений;
	СписокТиповДокументаОснования.Добавить("Документ.ИнвентаризацияОС.ФормаВыбора", НСтр("ru='Инвентаризация ОС';uk='Інвентаризація ОЗ'"));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвойстваЭлементов(Знач Параметры = Неопределено)
	
	ОбновитьВсе = (Параметры = Неопределено);
	Действия = ?(ТипЗнч(Параметры)=Тип("Структура"), Параметры, Новый Структура(Параметры));
	
	Если ОбновитьВсе Или Действия.Свойство("Организация") Тогда
		ВнеоборотныеАктивы.УстановитьПараметрыФункциональныхОпцийФормыОбъекта(ЭтаФорма, Объект.Организация, Объект.Дата);
		
		ПлательщикНалогаНаПрибыль = ПолучитьФункциональнуюОпцию("ПлательщикНалогаНаПрибыль", ПолучитьПараметрыФункциональныхОпцийФормы());
		Элементы.НалоговаяГруппаОС.Видимость   = ПлательщикНалогаНаПрибыль;
		Элементы.ГруппаНУАмортизация.Видимость = ПлательщикНалогаНаПрибыль;
		Элементы.ГруппаНУСрокИспользования.Видимость = ПлательщикНалогаНаПрибыль;
					
	КонецЕсли;
	
	Если ОбновитьВсе Или Действия.Свойство("СчетУчета") Тогда
		
		ЗаполнитьСпискиСпособовАмортизации();
		
		Если  Объект.СчетУчета.Родитель = ПланыСчетов.Хозрасчетный.ОсновныеСредства Тогда
			Объект.СчетАмортизации = ПланыСчетов.Хозрасчетный.ИзносОсновныхСредств;	
		ИначеЕсли Объект.СчетУчета = ПланыСчетов.Хозрасчетный.БиблиотечныеФондыИндивидуально 
			 ИЛИ Объект.СчетУчета = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыИндивидуально Тогда
			Объект.СчетАмортизации = ПланыСчетов.Хозрасчетный.ИзносДругихНеоборотныхМатериальныхАктивовИндивидуально;
		ИначеЕсли Объект.СчетУчета = ПланыСчетов.Хозрасчетный.БиблиотечныеФондыКоличественно 
			 ИЛИ Объект.СчетУчета = ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыКоличественно Тогда
			Объект.СчетАмортизации = ПланыСчетов.Хозрасчетный.ИзносДругихНеоборотныхМатериальныхАктивовКоличественно;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбновитьВсе Или Действия.Свойство("ПараметрыАмортизации") Тогда
		
		Элементы.БУСрокИспользования.АвтоОтметкаНезаполненного = (
			Объект.МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.Линейный
			Или Объект.МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка);
		
		Если Не Элементы.БУСрокИспользования.АвтоОтметкаНезаполненного Тогда
			Элементы.БУСрокИспользования.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
		ПропорциональноОбъемуПродукцииБУ = Объект.МетодНачисленияАмортизацииБУ  = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции;
		ПропорциональноОбъемуПродукцииНУ = Объект.СпособНачисленияАмортизацииНУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции;
		УпрощенныйМетодНачисленияАмортизацииБУ = ( 
							Объект.МетодНачисленияАмортизацииБУ  = Перечисления.СпособыНачисленияАмортизацииОС._50_50) 
							ИЛИ (Объект.МетодНачисленияАмортизацииБУ  = Перечисления.СпособыНачисленияАмортизацииОС._100);
							
		УпрощенныйМетодНачисленияАмортизацииНУ = ( 
							Объект.СпособНачисленияАмортизацииНУ  = Перечисления.СпособыНачисленияАмортизацииОС._50_50) 
							ИЛИ (Объект.СпособНачисленияАмортизацииНУ  = Перечисления.СпособыНачисленияАмортизацииОС._100);
		
		БУГрафикАмортизацииСрокИспользованияВидимость = НЕ (
							ПропорциональноОбъемуПродукцииБУ 
							ИЛИ УпрощенныйМетодНачисленияАмортизацииБУ);
							
		НУСрокИспользованияВидимость = НЕ (
							ПропорциональноОбъемуПродукцииНУ 
							ИЛИ УпрощенныйМетодНачисленияАмортизацииНУ);
							
		Элементы.БУГрафикАмортизации.Видимость 			  = БУГрафикАмортизацииСрокИспользованияВидимость;
		Элементы.БУСрокИспользования.Видимость 			  = БУГрафикАмортизацииСрокИспользованияВидимость;
		Элементы.БУСрокИспользованияРасшифровка.Видимость = БУГрафикАмортизацииСрокИспользованияВидимость; 
		
		Элементы.ЛиквидационнаяСтоимостьБУ.Видимость 	  = НЕ УпрощенныйМетодНачисленияАмортизацииБУ;

		Элементы.НУСрокИспользования.Видимость 			  = НУСрокИспользованияВидимость;
		Элементы.НУСрокИспользованияРасшифровка.Видимость = НУСрокИспользованияВидимость;

		Элементы.ОСПоказательНаработки.Видимость = ПропорциональноОбъемуПродукцииБУ;
				
		Элементы.ОСПоказательНаработкиЕдиницаИзмерения.Видимость = (
			Элементы.ОСПоказательНаработки.Видимость);
		
		Элементы.ОСОбъемНаработки.Видимость = (
			Элементы.ОСПоказательНаработки.Видимость);
			
	КонецЕсли;
		
	Если ОбновитьВсе Или Действия.Свойство("НалоговоеНазначение") Тогда
		Элементы.ГруппаНУАмортизация.Видимость = 
				НЕ (Объект.НалоговоеНазначение = 
					Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность);
	КонецЕсли;
	
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыЗаполненияПоНаименованию(Форма)
	
	Результат = Новый Структура;
	Результат.Вставить("Форма", Форма);
	Результат.Вставить("Объект", Форма.Объект);
	Результат.Вставить("ИмяТабличнойЧасти", "ОС");
	Результат.Вставить("ПолучатьИнвентарныйНомерИзКода", Истина);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоНаименованиюСервер(Знач ОсновноеСредство)
	
	УчетОСВызовСервера.ДозаполнитьТабличнуюЧастьОсновнымиСредствамиПоНаименованию(
		ПараметрыЗаполненияПоНаименованию(ЭтаФорма),
		ОсновноеСредство);
		
	ОбновитьСвойстваЭлементов("ОсновноеСредство");
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеДанныеПодбора(Знач МассивОбъектов, Знач Организация, Знач Дата, Знач ИспользуетсяСтоимостьНУ)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОбъектов, "Код,ИнвентарныйНомер");
		
	ДополнительныеДанныеПодбора = Новый Соответствие;
	Для каждого ОсновноеСредство Из МассивОбъектов Цикл
		
		ЗначенияРеквизита = ЗначенияРеквизитов.Получить(ОсновноеСредство);
		
		ДопДанные = Новый Структура();
		ДопДанные.Вставить("Код", ЗначенияРеквизита.Код);
		ДопДанные.Вставить("ИнвентарныйНомер", ЗначенияРеквизита.ИнвентарныйНомер);
		
		ДополнительныеДанныеПодбора.Вставить(ОсновноеСредство, ДопДанные);
		
	КонецЦикла; 
			
	Возврат ДополнительныеДанныеПодбора;
	
КонецФункции


&НаСервереБезКонтекста
Функция ЗначенияПолейКонтактнойИнформацииСервер(Знач Представление, Знач ВидКонтактнойИнформации)
	
	Результат = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
	Возврат Результат;
	
КонецФункции


&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ПрочитатьПараметрыУчетнойПолитики();
	ОбновитьСвойстваЭлементов("Организация");

КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ПрочитатьПараметрыУчетнойПолитики();
	ОбновитьСвойстваЭлементов("Дата");

КонецПроцедуры
 
&НаСервере
Процедура ПрочитатьПараметрыУчетнойПолитики()

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик("НастройкиУчетаНалогаНаПрибыль",
		Объект.Организация, 
		?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()));

КонецПроцедуры


&НаСервере
Процедура ОСОсновноеСредствоПриИзмененииНаСервере()

	ТекущаяСтрока = Элементы.ОС.ТекущаяСтрока;
	ДанныеСтроки = Объект.ОС.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
		РеквизитыОС= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеСтроки.ОсновноеСредство, "Код,ИнвентарныйНомер");
		ДанныеСтроки.ИнвентарныйНомер = ?(ЗначениеЗаполнено(РеквизитыОС.ИнвентарныйНомер), 
												РеквизитыОС.ИнвентарныйНомер, 
												РеквизитыОС.Код);
	КонецЕсли;

	ОбновитьСвойстваЭлементов("ОсновноеСредство");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСоставКомиссии(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);

	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	ОткрытьФорму("РегистрСведений.СоставКомиссий.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура НалоговоеНазначениеПриИзменении(Элемент)
	ОбновитьСвойстваЭлементов("НалоговоеНазначение");
КонецПроцедуры


#КонецОбласти