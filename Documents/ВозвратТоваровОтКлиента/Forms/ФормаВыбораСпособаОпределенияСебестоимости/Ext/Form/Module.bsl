#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ИспользоватьНесколькоВидовЦен = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен");
	НастроитьФормуПоПараметрам(Параметры);
	НастроитьПараметрыВыбораДокументаПродажи(Параметры);
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособОпределенияСебестоимости = Перечисления.СпособыОпределенияСебестоимости.ИзДокументаПродажи
		И НЕ ЗначениеЗаполнено(ДокументПродажи) Тогда
		ПроверяемыеРеквизиты.Добавить("ДокументПродажи");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособОпределенияСебестоимостиВручнуюПриИзменении(Элемент)
	
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОпределенияСебестоимостиТекущийДокументПриИзменении(Элемент)
	
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОпределенияСебестоимостиДокументПродажиПриИзменении(Элемент)
	
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦеныПриИзменении(Элемент)
	
	ЗаполнитьСуммыПоВидуЦен();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗаполненияПриИзменении(Элемент)
	
	ЗаполнитьСуммыПоВидуЦен();

КонецПроцедуры

&НаКлиенте
Процедура СуммаУпрПриИзменении(Элемент)
	
	Если ТипНалогообложения <> ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС") Тогда
		Товары[0].СуммаБезНДС = Товары[0].СуммаСНДС;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОКЗакрыть(Команда)

	ОчиститьСообщения();
	Если ПроверитьЗаполнение() Тогда
		ПеренестиДанныеВДокумент();
	КонецЕсли;
		
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьПараметрыВыбораДокументаПродажи(Параметры)
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Организация", Параметры.Организация));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Партнер", Параметры.Партнер));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Контрагент", Параметры.Контрагент));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Договор", Параметры.Договор));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Соглашение", Параметры.Соглашение));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено));
	Элементы.ДокументПродажи.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Если Форма.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимости.Вручную") Тогда
		Форма.Элементы.ГруппаСуммы.ТекущаяСтраница = Форма.Элементы.ГруппаВручнуюДанные;
		ПоказыватьСуммы = (Форма.Товары.Количество() = 1);
		Форма.Элементы.ГруппаУпр.Видимость = ПоказыватьСуммы;
		Форма.Элементы.ГруппаРегл.Видимость = ПоказыватьСуммы;
	Иначе
		Форма.Элементы.ГруппаСуммы.ТекущаяСтраница = Форма.Элементы.ГруппаВручнуюПустая;
	КонецЕсли;
	Если Форма.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПродажи") Тогда
		Форма.Элементы.ГруппаСтраницыДокументПродажи.ТекущаяСтраница = Форма.Элементы.СтраницаДокументПродажи;
	Иначе
		Форма.Элементы.ГруппаСтраницыДокументПродажи.ТекущаяСтраница = Форма.Элементы.СтраницаДокументПродажиПустая;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСуммыПоВидуЦен()
	
	Если НЕ ЗначениеЗаполнено(ВидЦены) ИЛИ НЕ ЗначениеЗаполнено(ДатаЗаполнения) Тогда
		Элементы.НадписьНетЦеныНаДату.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	СтруктураОтбораПоВидуЦен = Новый Структура;
	СтруктураОтбораПоВидуЦен.Вставить("Ссылка", ВидЦены);
	ЦенаВключаетНДС = Справочники.ВидыЦен.ВидЦеныИПризнакЦенаВключаетНДСПоУмолчанию(СтруктураОтбораПоВидуЦен).ЦенаВключаетНДС;
	
	СтруктураПересчетаСуммы = Новый Структура;
	СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", ЦенаВключаетНДС);
	СтруктураПересчетаСуммы.Вставить("НалогообложениеНДС", ТипНалогообложения);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Дата", ДатаЗаполнения);
	ПараметрыЗаполнения.Вставить("Валюта", ВалютаДокумента);
	ПараметрыЗаполнения.Вставить("ВидЦены", ВидЦены);
	ПараметрыЗаполнения.Вставить("НалогообложениеНДС", ТипНалогообложения);
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, ВидЦены");
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	
	ПродажиСервер.ЗаполнитьЦены(
		Товары,
		, // Массив строк или структура отбора
		ПараметрыЗаполнения,
		СтруктураДействий);
		
		
	КоэффициентПересчетаДокУпр = ?(ВалютаДокумента = ВалютаУправленческогоУчета,
			1,
			РаботаСКурсамивалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(ВалютаДокумента,
				ВалютаУправленческогоУчета,
				ДатаЗаполнения)
			);		
		
	//++ НЕ УТ
	КоэффициентПересчетаДокРегл = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			1,
			РаботаСКурсамивалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(ВалютаДокумента,
				ВалютаРегламентированногоУчета,
				ДатаЗаполнения)
			);
	//-- НЕ УТ
			
	НетЦены = Ложь;
	Для Каждого ТекущаяСтрока Из Товары Цикл
		
		СуммаБезНДС = ТекущаяСтрока.СуммаСНДС-ТекущаяСтрока.СуммаНДС;
		ТекущаяСтрока.СуммаСНДС = ТекущаяСтрока.СуммаСНДС * КоэффициентПересчетаДокУпр;
		ТекущаяСтрока.СуммаБезНДС = СуммаБезНДС * КоэффициентПересчетаДокУпр;
		
		//++ НЕ УТ
		ТекущаяСтрока.СуммаРегл = СуммаБезНДС * КоэффициентПересчетаДокРегл;
		ТекущаяСтрока.СуммаНДСРегл = (ТекущаяСтрока.СуммаСНДС - СуммаБезНДС) * КоэффициентПересчетаДокРегл;
		ТекущаяСтрока.СуммаНДСУпр = (ТекущаяСтрока.СуммаСНДС - СуммаБезНДС) * КоэффициентПересчетаДокУпр;
		//-- НЕ УТ
		
		Если ТекущаяСтрока.Цена = 0 Тогда
			НетЦены = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.НадписьНетЦеныНаДату.Видимость = НетЦены;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоПараметрам(Параметры)
	
	Организация = Параметры.Организация;
	Партнер = Параметры.Партнер;
	Договор = Параметры.Договор;
	Соглашение = Параметры.Соглашение;
	ПоказыватьРеализации = Параметры.ПоказыватьРеализации;
	ВалютаДокумента = Параметры.ВалютаДокумента;
	ТипНалогообложения = Параметры.ТипНалогообложения;
	
	Если ТипЗнч(Параметры.СтрокиТаблицыТовары) = Тип("Массив") Тогда
		
		Если Параметры.СтрокиТаблицыТовары.Количество() Тогда
			ПерваяСтрока = Параметры.СтрокиТаблицыТовары[0];
			ВидЦены = ПерваяСтрока.ВидЦены;
			ДатаЗаполнения = ПерваяСтрока.ДатаЗаполненияСебестоимостиПоВидуЦены;
			СпособОпределенияСебестоимости = ПерваяСтрока.СпособОпределенияСебестоимости;
			ДокументПродажи = ПерваяСтрока.ДокументПродажи;
		КонецЕсли;
		
		НомерСтроки = 0;
		Для Каждого ТекущаяСтрока Из Параметры.СтрокиТаблицыТовары Цикл
			
			НомерСтроки = НомерСтроки + 1;
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
			НоваяСтрока.НомерСтроки = НомерСтроки;
			
			Если ВидЦены <> ТекущаяСтрока.ВидЦены Тогда
				ВидЦены = Неопределено;
			КонецЕсли;
			
			Если ДатаЗаполнения <> ТекущаяСтрока.ДатаЗаполненияСебестоимостиПоВидуЦены Тогда
				ДатаЗаполнения = Неопределено;
			КонецЕсли;
			
			Если СпособОпределенияСебестоимости <> ТекущаяСтрока.СпособОпределенияСебестоимости Тогда
				СпособОпределенияСебестоимости = Перечисления.СпособыОпределенияСебестоимости.ИзТекущегоДокумента;
			КонецЕсли;
			
			Если ДокументПродажи <> ТекущаяСтрока.ДокументПродажи Тогда
				ДокументПродажи = Неопределено;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	МассивТипов = Новый Массив;
	Если ПоказыватьРеализации Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	Иначе
		МассивТипов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	КонецЕсли;
	Элементы.ДокументПродажи.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,
		"ГруппаВидЦен",
		"Видимость",
		ИспользоватьНесколькоВидовЦен);
		
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,
		"УпрБезНДС",
		"Видимость",
		ТипНалогообложения = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
		
	
	Если ТипНалогообложения = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда
		ТекстЗаголовкаУпр = НСтр("ru='Упр. учет с НДС (%1):';uk='Упр. облік з ПДВ (%1):'");
		ТекстЗаголовкаУпрБезНДС = НСтр("ru='Упр. учет без НДС (%1):';uk='Упр. облік без ПДВ (%1):'");
		Элементы.НадписьУпрБезНДС.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовкаУпрБезНДС, ВалютаУправленческогоУчета);
	Иначе
		ТекстЗаголовкаУпр = НСтр("ru='Упр. учет (%1):';uk='Упр. облік (%1):'");
	КонецЕсли;
	Элементы.НадписьУпр.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовкаУпр, ВалютаУправленческогоУчета);
	
	//++ НЕ УТ
	ТекстЗаголовкаРегл = НСтр("ru='Регл. учет %1(%2):';uk='Регл. облік %1(%2):'");
	Элементы.НадписьРегл.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗаголовкаРегл,
		?(ТипНалогообложения = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru='без НДС';uk='без ПДВ'") + " ", ""),
		ВалютаРегламентированногоУчета);	
		
	ТекстЗаголовкаНДСРегл = НСтр("ru='НДС регл. учет (%1):';uk='ПДВ регл. облік (%1):'");
	Элементы.НадписьНДСРегл.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗаголовкаНДСРегл,
		ВалютаРегламентированногоУчета
	);	

	ТекстЗаголовкаНДСУпр = НСтр("ru='НДС упр. учет (%1):';uk='ПДВ упр. облік (%1):'");
	Элементы.НадписьНДСУпр.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗаголовкаНДСУпр,
		ВалютаУправленческогоУчета
	);	
	//-- НЕ УТ		
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			НСтр("ru='Данные были изменены. Перенести изменения в документ?';uk='Дані були змінені. Перенести зміни в документ?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		Если ПроверитьЗаполнение() Тогда
			ВыполняетсяЗакрытие = Истина;
			ПеренестиДанныеВДокумент();
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ВыполняетсяЗакрытие = Истина;
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиДанныеВДокумент()

	Модифицированность = Ложь;
	
	СтрокиТаблицыТовары = ОбщегоНазначенияУТКлиент.НовыйМассивТовары();
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		
		ПараметрыСтроки = ОбщегоНазначенияУТКлиент.НовыйЭлементМассивТовары();
		
		ПараметрыСтроки.СпособОпределенияСебестоимости = СпособОпределенияСебестоимости;
		ПараметрыСтроки.ДокументРеализации = ДокументПродажи;
		ПараметрыСтроки.Идентификатор = ТекущаяСтрока.Идентификатор;
		
		Если СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимости.Вручную") Тогда
			ПараметрыСтроки.ВидЦеныСебестоимости = ВидЦены;
			ПараметрыСтроки.ДатаЗаполненияСебестоимостиПоВидуЦены = ДатаЗаполнения;
			ПараметрыСтроки.Себестоимость = ТекущаяСтрока.СуммаСНДС;
			ПараметрыСтроки.СебестоимостьБезНДС = ТекущаяСтрока.СуммаБезНДС;
			
			//++ НЕ УТ
			ПараметрыСтроки.СебестоимостьРегл = ТекущаяСтрока.СуммаРегл;
			ПараметрыСтроки.СебестоимостьНДСРегл = ТекущаяСтрока.СуммаНДСРегл;
			ПараметрыСтроки.СебестоимостьНДСУпр = ТекущаяСтрока.СуммаНДСУпр;
			//-- НЕ УТ
			
		Иначе
			ПараметрыСтроки.ВидЦеныСебестоимости = Неопределено;
			ПараметрыСтроки.ДатаЗаполненияСебестоимостиПоВидуЦены = Дата(1, 1, 1);
			ПараметрыСтроки.Себестоимость = 0;
			ПараметрыСтроки.СебестоимостьБезНДС = 0;
			
			//++ НЕ УТ
			ПараметрыСтроки.СебестоимостьРегл = 0;
			ПараметрыСтроки.СебестоимостьНДСРегл = 0;
			ПараметрыСтроки.СебестоимостьНДСУпр = 0;
			//-- НЕ УТ
			
		КонецЕсли;
		
		СтрокиТаблицыТовары.Добавить(ПараметрыСтроки);
		
	КонецЦикла;
	
	ПараметрыЗакрытия = ОбщегоНазначенияУТКлиент.НоваяСтруктураТовары();
	ПараметрыЗакрытия.СтрокиТаблицыТовары = СтрокиТаблицыТовары;
	Закрыть(ПараметрыЗакрытия);
						
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти



