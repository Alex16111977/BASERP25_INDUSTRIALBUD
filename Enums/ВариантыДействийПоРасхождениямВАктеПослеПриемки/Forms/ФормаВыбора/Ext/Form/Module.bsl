
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РасхожденияСервер.УправлениеСтраницамиВыборДействия(ЭтотОбъект, Параметры);
	
	ИспользоватьЗаказыПоставщикам = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам");
	КоличествоУпаковокРасхождения = Параметры.КоличествоУпаковокРасхождения;
	ГрупповоеИзменение = Параметры.ГрупповоеИзменение;
	
	Если Параметры.ВыбранноеДействие = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы Тогда
		ЗаЧейСчет = ?(Параметры.ПоВинеСтороннейКомпании, "ПоВинеСтороннейКомпании", "ЗаНашСчет");
	КонецЕсли;
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			
			Элементы.Страницы.ТекущаяСтраница = Элементы.Излишки;
			ДействиеИзлишки                   = Параметры.ВыбранноеДействие;
			
			Если Параметры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа
				Или Параметры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
				Если ДействиеИзлишки = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть Тогда
					ДействиеИзлишки = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ПустаяСсылка();
				КонецЕсли;
				Элементы.ОформитьПерепоставленноеИВернуть.Доступность = Ложь;
			КонецЕсли;
			
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.Недостачи;
			ДействиеНедостачи = Параметры.ВыбранноеДействие;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			Элементы.Недостачи.Видимость = Ложь;
			Заголовок = НСтр("ru='Как отработать излишек';uk='Як відпрацювати надлишок'");
		Иначе
			Элементы.Излишки.Видимость = Ложь;
			Заголовок = НСтр("ru='Как отработать недостачу';uk='Як відпрацювати нестачу'");
		КонецЕсли;
	Иначе
		Заголовок = НСтр("ru='Как отработать расхождения';uk='Як відпрацювати розбіжності'");
	КонецЕсли;
	
	АктПриемкиНаХранение = Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение
							Или Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя;
	
	Элементы.ПоВинеСтороннейКомпании.Видимость          = Не АктПриемкиНаХранение;
	Элементы.ПоВинеСтороннейКомпанииДекорация.Видимость = Не АктПриемкиНаХранение;
	
	Элементы.КартинкаПояснения.Видимость = Параметры.ПоказыватьПояснение;
	Элементы.Пояснение.Видимость         = Параметры.ПоказыватьПояснение;
	
	СформироватьЗаголовки(Параметры.ТипАкта,
						Параметры.КоличествоУпаковокРасхождения,
						Параметры.СпособОтраженияРасхождений,
						Параметры.ГрупповоеИзменение,
						Параметры.СтрокаПоЗаказу);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И Модифицированность Тогда
		
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
		               НСтр("ru='Выполненные изменения будут утеряны. Все равно закрыть?';uk='Виконані зміни будуть втрачені. Все одно закрити?'"),
		               РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаНашСчетПриИзменении(Элемент)
	
	ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоВинеСтороннейКомпанииПриИзменении(Элемент)
	
	ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьНедостачуПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьНедостачуИОжидатьДопоставкуПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьДопоставкуБезОформленияПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	
	ПараметрыЗакрытия = Новый Структура();
	ПараметрыЗакрытия.Вставить("ДействиеИзлишки", Неопределено);
	ПараметрыЗакрытия.Вставить("ДействиеНедостачи", Неопределено);
	ПараметрыЗакрытия.Вставить("ПоВинеСтороннейКомпании", Истина);
	
	Если ГрупповоеИзменение Тогда
		
		ПараметрыЗакрытия.ДействиеИзлишки   = ДействиеИзлишки;
		ПараметрыЗакрытия.ДействиеНедостачи = ДействиеНедостачи;
		ПараметрыЗакрытия.ПоВинеСтороннейКомпании = ПоВинеСтороннейКомпании();
		
	ИначеЕсли КоличествоУпаковокРасхождения > 0 Тогда
		
		ПараметрыЗакрытия.ДействиеИзлишки = ДействиеИзлишки;

	ИначеЕсли КоличествоУпаковокРасхождения < 0 Тогда
		
		ПараметрыЗакрытия.ДействиеНедостачи = ДействиеНедостачи;
		ПараметрыЗакрытия.ПоВинеСтороннейКомпании = ПоВинеСтороннейКомпании();
		
	КонецЕсли;
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	Закрыть(Неопределено);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьЗаголовки(ТипАкта, КоличествоУпаковокРасхождения, СпособОтраженияРасхождений, ГрупповоеИзменение, СтрокаПоЗаказу)
	
	Если ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриобретениеТоваровУслуг
		Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при поступлении согласованы с поставщиком';uk='Нестачі при надходженні погоджені з постачальником'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при поступлении не согласованы с поставщиком';uk='Нестачі при надходженні не погоджені з постачальником'");
		
		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов Тогда
			Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыПоставщикам) Тогда
				Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';uk='Надходження зменшується на недопоставлений товар з подальшою відміною недопоставлених рядків замовлення'");
			Иначе
				Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар';uk='Надходження зменшується на недопоставлений товар'");
			КонецЕсли;
			Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';uk='Надходження зменшується на недопоставлений товар з подальшим оформленням нового надходження недопоставленого товару'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления Тогда
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка поступления';uk='На недопоставлений товар оформлюється коригування надходження'");
			Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка поступления с последующим оформлением нового поступления недопоставленного товара';uk='На недопоставлений товар оформлюється коригування надходження з подальшим оформленням нового надходження недопоставленого товару'");
		КонецЕсли;
		
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок = НСтр("ru='Недопоставленный товар принимается на склад, документы не переоформляются';uk='Недопоставлений товар приймається на склад, документи не переоформлюються'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок   = НСтр("ru='Излишки при поступлении согласованы с поставщиком';uk='Надлишки при надходженні погоджені з постачальником'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок = НСтр("ru='Излишки при поступлении не согласованы с поставщиком';uk='Надлишки при надходженні не погоджені з постачальником'");

		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов Тогда
			Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru='Поступление увеличивается на перепоставленный товар';uk='Надходження збільшується на перепоставлений товар'");
			Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru='Поступление увеличивается на перепоставленный товар с последующим оформлением возврата перепоставленного товара';uk='Надходження збільшується на перепоставлений товар з подальшим оформленням повернення перепоставленого товару'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления Тогда
			Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка поступления';uk='На перепоставлений товар оформлюється коригування надходження'");
			Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка поступления с последующим оформлением возврата перепоставленного товара';uk='На перепоставлений товар оформлюється коригування надходження з подальшим оформленням повернення перепоставленого товару'");
		КонецЕсли;
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок = НСтр("ru='Перепоставленный товар оприходуется, относится на прочие доходы, документы не переоформляются';uk='Перепоставлений товар оприбутковується, відноситься на інші доходи, документи не переоформлюються'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок  = НСтр("ru='Перепоставленный товар отгружается со склада, документы не переоформляются';uk='Перепоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтКлиента Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при возврате согласованы с клиентом';uk='Нестачі при поверненні погоджені з клієнтом'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при возврате не согласованы с клиентом';uk='Нестачі при поверненні не погоджені з клієнтом'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru='Возврат товаров от клиента уменьшается на недопоставленный товар';uk='Повернення товарів від клієнта зменшується на недопоставлений товар'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru='Возврат товаров от клиента уменьшается на недопоставленный товар с последующим оформлением нового возврата недопоставленного товара';uk='Повернення товарів від клієнта зменшується на недопоставлений товар з подальшим оформленням нового повернення недопоставленого товару'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru='Недопоставленный товар принимается на склад, документы не переоформляются';uk='Недопоставлений товар приймається на склад, документи не переоформлюються'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru='Излишки при возврате согласованы с поставщиком';uk='Надлишки при поверненні погоджені з постачальником'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru='Излишки при возврате не согласованы с поставщиком';uk='Надлишки при поверненні не погоджені з постачальником'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru='Излишки возврата оприходуются, относятся на прочие доходы, документы не переоформляются';uk='Надлишки повернення оприбутковуються, відносяться на інші доходи, документи не переоформлюються'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru='Возврат товаров от клиента увеличивается на перепоставленный товар с последующим оформлением поступления перепоставленного товара';uk='Повернення товарів від клієнта збільшується на перепоставлений товар з подальшим оформленням надходження перепоставленого товару'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru='Возврат товаров от клиента увеличивается на перепоставленный товар';uk='Повернення товарів від клієнта збільшується на перепоставлений товар'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru='Перепоставленный товар отгружается со склада, документы не переоформляются';uk='Перепоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при поступлении согласованы с поставщиком';uk='Нестачі при надходженні погоджені з постачальником'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при поступлении не согласованы с поставщиком';uk='Нестачі при надходженні не погоджені з постачальником'");
		
		Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыПоставщикам) Тогда
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';uk='Надходження зменшується на недопоставлений товар з подальшою відміною недопоставлених рядків замовлення'");
		Иначе
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар';uk='Надходження зменшується на недопоставлений товар'");
		КонецЕсли;
		
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru='Поступление уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';uk='Надходження зменшується на недопоставлений товар з подальшим оформленням нового надходження недопоставленого товару'");
		
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок = НСтр("ru='Недопоставленный товар принимается на склад, документы не переоформляются';uk='Недопоставлений товар приймається на склад, документи не переоформлюються'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок   = НСтр("ru='Излишки при поступлении согласованы с поставщиком';uk='Надлишки при надходженні погоджені з постачальником'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок = НСтр("ru='Излишки при поступлении не согласованы с поставщиком';uk='Надлишки при надходженні не погоджені з постачальником'");
		
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru='Поступление увеличивается на перепоставленный товар';uk='Надходження збільшується на перепоставлений товар'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru='Поступление увеличивается на перепоставленный товар с последующим оформлением отгрузки перепоставленного товара';uk='Надходження збільшується на перепоставленний товар з подальшим оформленням відвантаження перепоставленного товару'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок = НСтр("ru='Перепоставленный товар оприходуется, документы не переоформляются';uk='Перепоставленний товар оприбутковується, документи не переоформляються'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок  = НСтр("ru='Перепоставленный товар отгружается со склада, документы не переоформляются';uk='Перепоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при поступлении согласованы с хранителем';uk='Нестачі при надходженні погоджені зі зберігачем'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при поступлении не согласованы с хранителем';uk='Нестачі при вступі не погоджені зі зберігачем'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru='Поступление товаров от хранителя уменьшается на недопоставленный товар';uk='Надходження товарів від зберігача зменшується на недопоставлений товар'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru='Поступление товаров от хранителя уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';uk='Надходження товарів від зберігача зменшується на недопоставлений товар з подальшим оформленням нового надходження недопоставленого товару'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru='Недопоставленный товар принимается на склад, документы не переоформляются';uk='Недопоставлений товар приймається на склад, документи не переоформлюються'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru='Излишки при поступлении согласованы с хранителем';uk='Надлишки при надходженні погоджені зі зберігачем'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru='Излишки при возврате не согласованы с хранителем';uk='Надлишки при поверненні не погоджені зі зберігачем'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru='Излишки поступления товаров от хранителя оприходуются, относятся на прочие доходы, документы не переоформляются';uk='Надлишки надходження товарів від зберігача оприбуткауються, відносяться на інші доходи, документи не переоформляються'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru='Поступление товаров от хранителя увеличивается на перепоставленный товар с последующим оформлением поступления перепоставленного товара';uk='Надходження товарів від зберігача збільшується на перепоставленний товар з подальшим оформленням надходження перепоставленного товару'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru='Поступление товаров от хранителя увеличивается на перепоставленный товар';uk='Надходження товарів від зберігача збільшується на перепоставленний товар'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru='Перепоставленный товар отгружается со склада, документы не переоформляются';uk='Перепоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
	КонецЕсли;
	
	АктПриемкиНаХранение = ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя;
	
	Если АктПриемкиНаХранение Тогда
		ЗначениеСписания = Элементы.ЗаНашСчет.СписокВыбора.Получить(0);
		ЗначениеСписания.Представление = НСтр("ru='Списать недостачи';uk='Списати нестачі'");
	КонецЕсли;
	
	ЗаголовокОформленияНедостачЗаНашСчет = ?(АктПриемкиНаХранение,
											НСтр("ru='Недостающий товар будет оприходован на склад и списан на недостачи';uk='Відсутній товар буде оприбутковано на склад і списано на нестачі'"),
											НСтр("ru='Недостающий товар будет оприходован на склад и списан на прочие расходы';uk='Відсутній товар буде оприбутковано на склад і списано на інші витрати'"));
	
	Элементы.ЗаНашСчетДекорация.Заголовок               = ЗаголовокОформленияНедостачЗаНашСчет;
	Элементы.ПоВинеСтороннейКомпанииДекорация.Заголовок = НСтр("ru='Недостающий товар будет оприходован на склад и списан на прочие расходы. Ответственность за недостачи будет возложена на стороннюю компанию';uk='Відсутній товар буде оприбутковано на склад і списано на інші витрати. Відповідальність за нестачі буде покладено на сторонню компанію'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПоВинеСтороннейКомпании()

	Возврат ?(ДействиеНедостачи = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы"),
	                                                      ?(ЗаЧейСчет = "ПоВинеСтороннейКомпании", Истина, Ложь),
	                                                      Ложь);

КонецФункции

&НаКлиенте
Процедура ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении()
	
	ДействиеНедостачи = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы");
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеНедостачиПриИзменении()
	
	ЗаЧейСчет = "";
	
КонецПроцедуры

#КонецОбласти

