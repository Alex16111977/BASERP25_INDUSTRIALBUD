#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаВидовЕСВ.Ссылка,
	|	ТаблицаВидовЕСВ.Синоним
	|ПОМЕСТИТЬ ВТТаблицаВидовЕСВ
	|ИЗ
	|	&ТаблицаВидовЕСВ КАК ТаблицаВидовЕСВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидовЕСВ.Ссылка
	|ИЗ
	|	ВТТаблицаВидовЕСВ КАК ТаблицаВидовЕСВ
	|ГДЕ
	|	ТаблицаВидовЕСВ.Синоним ПОДОБНО &СтрокаПоиска";
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаВидовЕСВ.Синоним ПОДОБНО &СтрокаПоиска", "ИСТИНА");
	КонецЕсли;
	
	ТаблицаВидовЕСВ = Новый ТаблицаЗначений;
	ТаблицаВидовЕСВ.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЕСВ"));
	ТаблицаВидовЕСВ.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(256)));
	
	ДоступныеВидыЕСВ = ДоступныеВидыЕСВ();
	
	Для Каждого ЗначениеПеречисления Из ДоступныеВидыЕСВ Цикл
		НоваяСтрока = ТаблицаВидовЕСВ.Добавить();
		НоваяСтрока.Ссылка = Перечисления.ВидыЕСВ[ЗначениеПеречисления.Имя];
		НоваяСтрока.Синоним = ЗначениеПеречисления.Синоним;
	КонецЦикла;
	
	// Отбор только по действующим категориям с учетом введенной строки.
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаВидовЕСВ", ТаблицаВидовЕСВ);
	Запрос.УстановитьПараметр("СтрокаПоиска", Строка(Параметры.СтрокаПоиска) + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеВидыЕСВ()
	
	ДоступныеВидыЕСВ = Новый Массив;
		
	ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.ОсновнаяЗарплата);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам") Тогда
		ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.ПоДоговорамГПХ);
	КонецЕсли;	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГигКонтракты") Тогда
		ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.ПоГигКонтрактам);
	КонецЕсли;	
    ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.Больничные);
	ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.Декретные);
	ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.НачисленияМобилизованным); 
	ДоступныеВидыЕСВ.Добавить(Метаданные.Перечисления.ВидыЕСВ.ЗначенияПеречисления.НеУчитывается);
	
	Возврат ДоступныеВидыЕСВ;
	
КонецФункции

#КонецОбласти

#КонецЕсли