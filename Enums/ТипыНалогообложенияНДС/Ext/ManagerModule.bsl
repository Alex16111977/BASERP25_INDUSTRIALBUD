#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Приводит переданное значение перечисления к двум: ПродажаОблагаетсяНДС или ПродажаНеОблагаетсяНДС
//
// Параметры:
//  НалогообложениеНДС	 - ПеречислениеСсылка.ТипыНалогообложенияНДС 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыНалогообложенияНДС 
//
Функция СНдсБезНдс(НалогообложениеНДС) Экспорт
	
	Если ТипыСНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		
	ИначеЕсли ТипыБезНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
		
	Иначе
		
		ТекстИсключения = НСтр("ru='Неподдерживаемый тип налогообложения НДС %НалогообложенияНДС%';uk='Непідтримуваний тип оподаткування ПДВ %НалогообложенияНДС%'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%НалогообложенияНДС%", НалогообложениеНДС);
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыСНДС() Экспорт
	
	Массив = Новый Массив;
    Массив.Добавить(ПродажаНаЭкспорт);
    Массив.Добавить(ПродажаОблагаетсяНДС);
	
	Возврат Массив;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, не предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыБезНДС() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(ПродажаНеОблагаетсяНДС);
    Массив.Добавить(ПродажаОсвобожденаОтНДС);
	
	Возврат Массив;
	
КонецФункции


// Возвращает налоговое назначение, которое соответствует налоогобложению
// для облагаемых налоогобложений - НДС_Облагаемая, для необлагаемых - НДС_НеоблагаемаяХозДеятельность
//
// Параметры:
//  НалогообложениеНДС	 - ПеречислениеСсылка.ТипыНалогообложенияНДС 
// 
// Возвращаемое значение:
//  СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат 
//
Функция НалоговоеНазначение(НалогообложениеНДС) Экспорт
	
	Если ТипыСНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
	ИначеЕсли ТипыБезНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
    Иначе
        Возврат Неопределено;
	КонецЕсли;
	
КонецФункции


#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений();
	ДоступныеТипыНалогообложения = УчетНДСУПКлиентСервер.ПрименяемыеТипыНалогообложенияНДС();
	Для каждого Элемент Из ДоступныеТипыНалогообложения Цикл
		ДанныеВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
