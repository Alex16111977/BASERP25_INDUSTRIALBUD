#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст подсказки по типу номенклатуры
//
// Параметры:
//  ТипНоменклатуры	 - ПеречислениеСсылка.ТипыНоменклатуры	 - тип номенклатуры.
// 
// Возвращаемое значение:
//  Строка - подсказка по типу номенклатуры.
//
Функция ПодсказкаПоТипуНоменклатуры(ТипНоменклатуры) Экспорт
	
	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		Возврат НСтр("ru='Материальные ценности, которые закупаются, производятся, реализуются предприятием и учитываются на складах. Возможен контроль остатков на складах, учет себестоимости, обеспечение потребностей и др.';uk='Матеріальні цінності, що закуповуються, виробляються, реалізуються підприємством і обліковуються на складах. Можливий контроль залишків на складах, облік собівартості, забезпечення потреб та ін.'");
	ИначеЕсли ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Набор Тогда
		ТекстПодсказки = НСтр("ru='Комплекты, которые не хранятся на складе, а собираются динамически. Используются для удобного выбора связанных позиций в документах продажи, ценообразовании и печати.';uk='Комплекти, що не зберігаються на складі, а збираються динамічно. Використовуються для зручного вибору пов''язаних позицій в документах продажу, ціноутворення і друку.'");
		//++ НЕ УТ
		ТекстПодсказки = НСтр("ru='Комплекты, которые не хранятся на складе, а собираются динамически. Используются для удобного выбора связанных позиций в ресурсных спецификациях, документах продажи, ценообразовании и печати.';uk='Комплекти, що не зберігаються на складі, а збираються динамічно. Використовуються для зручного вибору пов''язаних позицій в ресурсних специфікаціях, документах продажу, ціноутворення і друку.'");
		//-- НЕ УТ
		Возврат ТекстПодсказки;
	ИначеЕсли ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
		Возврат НСтр("ru='Нематериальные ценности, которые закупаются предприятием или реализуются клиентам. Для услуг не ведется учет себестоимости. В момент приобретения услуги указывается статья расходов, определяющая дальнейший учет расходов.';uk='Нематеріальні цінності, які закуповуються підприємством або реалізуються клієнтам. Для послуг не ведеться облік собівартості. У момент придбання послуги вказується стаття витрат, яка визначає подальший облік витрат.'");
	ИначеЕсли ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
		Возврат НСтр("ru='Нематериальные ценности, которые закупаются или производятся, реализуются клиентам и учитываются в подразделении-получателе. Ведется количественный учет и учет себестоимости.';uk='Нематеріальні цінності, які закуповуються або виробляються, реалізуються клієнтам і обліковуються в підрозділі-одержувачі. Ведеться кількісний облік та облік собівартості.'");
	ИначеЕсли ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара Тогда
		Возврат НСтр("ru='Тара, которая учитывается отдельно на складе и может возвращаться поставщику товаров или передаваться на условиях возврата клиенту.';uk='Тара, яка обліковується окремо на складі і може повертатися постачальнику товарів або передаватися на умовах повернення клієнту.'");
	Иначе
		Возврат НСтр("ru='Определяет возможности по учету номенклатуры в различных механизмах';uk='Визначає можливості по обліку номенклатури в різних механізмах'");
	КонецЕсли;	
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ИспользоватьМногооборотнуюТару = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	НесколькоВидовНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовНоменклатуры");
	ЭтоБазовая = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений();
	Для Каждого ТекЭлемент Из Перечисления.ТипыНоменклатуры Цикл
		Если ТекЭлемент = Перечисления.ТипыНоменклатуры.МногооборотнаяТара
			И (Не ИспользоватьМногооборотнуюТару ИЛИ Не НесколькоВидовНоменклатуры) Тогда
			Продолжить;
		ИначеЕсли ТекЭлемент = Перечисления.ТипыНоменклатуры.Набор
			И (Не ИспользоватьНаборы ИЛИ Не НесколькоВидовНоменклатуры) Тогда
			Продолжить;
		ИначеЕсли ТекЭлемент = Перечисления.ТипыНоменклатуры.Работа
			И (Не НесколькоВидовНоменклатуры ИЛИ ЭтоБазовая) Тогда
			Продолжить;
		Иначе
			ДанныеВыбора.Добавить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
