#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЭтоГруппа Тогда
		
		ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
		ДоступноОписаниеТипаПроизводственногоПроцесса = УправлениеДаннымиОбИзделиях.ДоступноОписаниеТипаПроизводственногоПроцесса();
		
		Если ДоступноОписаниеТипаПроизводственногоПроцесса Тогда
			ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.Номенклатура;
			ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка;
		Иначе
			ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры;
			ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка;
		КонецЕсли;
		
		Если ТипДанныхЗаполнения = Тип("СправочникСсылка.Номенклатура") Тогда
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "ЭтоГруппа");
			Если СтруктураРеквизитов.ЭтоГруппа = Ложь Тогда
				
				НоваяСтрока = ВыходныеИзделия.Добавить();
				НоваяСтрока.КлючСвязи          = Новый УникальныйИдентификатор;
				НоваяСтрока.Номенклатура       = ДанныеЗаполнения;
				НоваяСтрока.КоличествоУпаковок = 1;
				УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиСАвтовыбором(НоваяСтрока);
			
			КонецЕсли;
			
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ТипПроизводственногоПроцесса") Тогда
				Если ТипЗнч(ДанныеЗаполнения.ТипПроизводственногоПроцесса) = Тип("Массив") 
					ИЛИ ТипЗнч(ДанныеЗаполнения.ТипПроизводственногоПроцесса) = Тип("ФиксированныйМассив") Тогда
					ТипПроизводственногоПроцесса = ДанныеЗаполнения.ТипПроизводственногоПроцесса[0];
				Иначе
					ТипПроизводственногоПроцесса = ДанныеЗаполнения.ТипПроизводственногоПроцесса;
				КонецЕсли;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("Номенклатура") Тогда
				
				Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
					ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
					ИЛИ Не ЗначениеЗаполнено(ТипПроизводственногоПроцесса) Тогда
				
					НоваяСтрока = ВыходныеИзделия.Добавить();
					НоваяСтрока.КлючСвязи          = Новый УникальныйИдентификатор;
					НоваяСтрока.Номенклатура       = ДанныеЗаполнения.Номенклатура;
					НоваяСтрока.Характеристика     = ДанныеЗаполнения.Характеристика;
					НоваяСтрока.КоличествоУпаковок = 1;
					УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиСАвтовыбором(НоваяСтрока);
					
				КонецЕсли;
				
				ОсновноеИзделиеНоменклатура       = ДанныеЗаполнения.Номенклатура;
				ОсновноеИзделиеХарактеристика     = ДанныеЗаполнения.Характеристика;
				ОсновноеИзделиеКоличествоУпаковок = 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверитьЗаполнитьВидНоменклатуры();
		
		Ответственный = Пользователи.ТекущийПользователь();
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеПроизводственногоПроцесса = Справочники.РесурсныеСпецификации.ОписаниеПроизводственногоПроцесса(Ссылка);
	
	// Дата окончания действия должна быть не меньше даты начала.
	Если ЗначениеЗаполнено(НачалоДействия) 
		И ЗначениеЗаполнено(КонецДействия) 
		И НачалоДействия > КонецДействия Тогда
		
		ТекстОшибки = НСтр("ru='Дата окончания действия должна быть не меньше даты начала действия.';uk='Дата закінчення дії повинна бути не менше дати початку дії.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "КонецДействия",, Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
#Область ВыходныеИзделия
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ВидНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Характеристика");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ДоляСтоимости");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Спецификация");
#КонецОбласти
	
#Область ВозвратныеОтходы
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.СтатьяКалькуляции");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Спецификация");
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Характеристика");
	КонецЕсли;
#КонецОбласти
	
#Область ВходящееИзделие
//++ НЕ УТКА
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ Статус <> Перечисления.СтатусыСпецификаций.Действует Тогда
//-- НЕ УТКА
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеВидНоменклатуры");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеНоменклатура");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеКоличествоУпаковок");
//++ НЕ УТКА
	ИначеЕсли ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеНоменклатура");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеВидНоменклатуры");
	КонецЕсли;
	ПроверитьЗаполнениеВходящихИзделий(Отказ);
//-- НЕ УТКА
#КонецОбласти
	
#Область Материалы
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.СпособПолученияМатериала");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ИсточникПолученияПолуфабриката");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.СтатьяКалькуляции");
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Характеристика");
	КонецЕсли;
	ПроверитьЗаполнениеМатериалов(Отказ);
#КонецОбласти

#Область Трудозатраты
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.СтатьяКалькуляции");
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.Количество");
#КонецОбласти
	
#Область Общее
	// при разборке возможно несколько последних этапов
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует
		ИЛИ ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Разборка
		ИЛИ НЕ ОписаниеПроизводственногоПроцесса.НесколькоПоследнихЭтапов
		Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ЭтапРедактирование");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.ЭтапРедактирование");
	КонецЕсли;
	
	// при сборке возможно несколько первых этапов
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует
		ИЛИ ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ НЕ ОписаниеПроизводственногоПроцесса.НесколькоПервыхЭтапов
		Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ЭтапРедактирование");
		МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.ЭтапРедактирование");
	КонецЕсли;
#КонецОбласти

	ПроверитьДействующуюСпецификацию(МассивНепроверяемыхРеквизитов, Отказ);
	
	ПроверитьВыборЭтапов("ВыходныеИзделия",  "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("ВозвратныеОтходы", "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("МатериалыИУслуги", "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("Трудозатраты",     "ЭтапРедактирование", Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоГруппа Тогда
		
		Если Не ЭтоНовый() Тогда
			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить("Справочник.ЭтапыПроизводства").УстановитьЗначение("Владелец", Ссылка);
			Блокировка.Заблокировать();
		КонецЕсли;
		
		Если ПометкаУдаления Тогда
			Статус = Перечисления.СтатусыСпецификаций.Закрыта;
			ОчиститьВыборЭтапов(ВыходныеИзделия);
			ОчиститьВыборЭтапов(ВозвратныеОтходы);
			ОчиститьВыборЭтапов(МатериалыИУслуги);
			ОчиститьВыборЭтапов(Трудозатраты);
		КонецЕсли;
		
		ОчиститьНеиспользуемыеДанные();
		
		Если Не ЭтоНовый() Тогда
			ПроверитьСкорректироватьЭтапыПроизводства(Отказ);
		КонецЕсли;
		
		ЗаполнитьСлужебныеРеквизиты();
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ОбновитьПривязкуЭтапов();
		
		Если Не ЭтоНовый() Тогда
			ЗарегистрироватьРасчетДлительностиПоСпецификации();
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ПодготовитьДанныеДляСинхронизацииКлючей(ЭтотОбъект, ПараметрыСинхронизацииКлючей());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
	
		РегистрыСведений.СпецификацииИзделий.ЗаписатьИзделияПоСпецификации(Ссылка, Статус = Перечисления.СтатусыСпецификаций.Закрыта);
		
		//++ НЕ УТКА
		Если Статус = Перечисления.СтатусыСпецификаций.Действует Тогда
			ОбновитьТребуетсяСправочноеУказаниеСерий();
		КонецЕсли;
		//-- НЕ УТКА
		
		ПолныйПересчет = Ложь;
		
		Если ДополнительныеСвойства.Свойство("РассчитатьДлительностьПроизводства", ПолныйПересчет) Тогда
			РегистрыСведений.ЗаданияКРасчетуДлительностиПроизводства.ДобавитьЗадание(Ссылка, ПолныйПересчет, НЕ ДополнительныеСвойства.Свойство("ЗапретитьРасчетДлительностиПроизводства"));
		КонецЕсли;
	
	КонецЕсли;
	
	ОбщегоНазначенияУТ.СинхронизироватьКлючи(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЭтоГруппа Тогда
		Возврат
	КонецЕсли; 
	
	Ответственный = Пользователи.ТекущийПользователь();
	Статус = Перечисления.СтатусыСпецификаций.ВРазработке;
	
	НачалоДействия = ТекущаяДатаСеанса();
	КонецДействия  = '00010101';
	
	// Очистим связь с этапами
	СписокТЧ = Новый Массив;
	СписокТЧ.Добавить("ВыходныеИзделия");
	СписокТЧ.Добавить("ВозвратныеОтходы");
	СписокТЧ.Добавить("МатериалыИУслуги");
	СписокТЧ.Добавить("Трудозатраты");
	Для каждого ИмяТЧ Из СписокТЧ Цикл
		Для каждого ДанныеСтроки Из ЭтотОбъект[ИмяТЧ] Цикл
			ДанныеСтроки.Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
			ДанныеСтроки.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		КонецЦикла;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаЗаполнения

#Область СтатусДействует

Процедура ПроверитьДействующуюСпецификацию(МассивНепроверяемыхРеквизитов, Отказ)

	Если Статус <> Перечисления.СтатусыСпецификаций.Действует ИЛИ ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеВыходныхИзделийВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеВозвратныхОтходовВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеМатериаловВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеТрудозатратВСтатусеДействует(Отказ);
	
	ПроверитьПроизводственныйПроцесс(Отказ);
	
	ПроверитьАвтовыборРасчетПоФормуламОтборПоСвойствам(Отказ);
	
	ПроверитьМаршрутныеКарты(Отказ);
	
//++ НЕ УТКА
	Справочники.РесурсныеСпецификации.ПроверитьВложенныеСпецификации(ЭтотОбъект, Отказ);
//-- НЕ УТКА
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеВыходныхИзделийВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	ИмяТЧ           = "ВыходныеИзделия";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	ИспользоватьПараметризациюРесурсныхСпецификаций = ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций");
	
	// Проверка заполнения основного изделия
	Если Не ЗначениеЗаполнено(ВыходныеИзделия) Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru='Не введено ни одной строки в список ""%1""';uk='Не введено жодного рядка в список ""%1""'"), ПредставлениеТЧ);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			ИмяТЧ,
			,
			Отказ);
			
		Возврат;
	КонецЕсли;
	
	ЭтоСборка = (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	
	ВариантНазначенияВидНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры);
	ВариантНазначенияСписокНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры);
	
	// Проверка заполнения табличной части
	Для Индекс = 0 По ВыходныеИзделия.Количество() - 1 Цикл
		
		Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры И Индекс > 0 Тогда
			Прервать;
		КонецЕсли;
		
		Строка = ВыходныеИзделия[Индекс];
		
		Если Строка.Номенклатура.Пустая()
			И НЕ ВариантНазначенияВидНоменклатуры
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
				ИЛИ НЕ ИспользоватьПараметризациюРесурсныхСпецификаций) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Номенклатура';uk='Номенклатура'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.ВидНоменклатуры.Пустая() И ВариантНазначенияВидНоменклатуры Тогда
				
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Вид номенклатуры';uk='Вид номенклатури'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "ВидНоменклатуры");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
				
		КонецЕсли;
		
		Если НЕ ВариантНазначенияВидНоменклатуры Тогда
			ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ);
		КонецЕсли;
		
		Если Строка.ОбработатьПоСпецификации И Строка.Спецификация.Пустая() Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Спецификация';uk='Специфікація'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Спецификация");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ (ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры) Тогда
		ПроверитьПовторВыходныхИзделий(Отказ);
		ПроверитьЗаполнениеДолейСтоимостиВыходныхИзделия(Отказ);
	КонецЕсли;
	
	// Проверка характеристик выходных изделий
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка 
		ИЛИ (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка 
			И ВыходныеИзделия.Количество() > 1) Тогда
			
		ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
		ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
		ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
		
		СписокСтрок = Неопределено;
		Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
			
			СписокСтрок = Новый Массив;
			Для Ит = 1 По ВыходныеИзделия.Количество()-1 Цикл
				Строка = ВыходныеИзделия[Ит];
				Если Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ
					ИЛИ НЕ ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
					СписокСтрок.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
			И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
			
			СписокСтрок = ВыходныеИзделия.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", 
					Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
			
		КонецЕсли;
		
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
		
	КонецЕсли;
	
//++ НЕ УТКА
	// В версии 2.2 выпуск продукции по рассчитываемой стоимости возможен только на последних этапах
	Если ПроизводствоСервер.ИспользуетсяПроизводство22()
		И Не ЭтоНовый() Тогда 
		
		ВыпускающиеЭтапы = ВыпускающиеЭтапы();
		
		Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
			И ВыпускающиеЭтапы.Количество() > 1 Тогда
			
			Шаблон = НСтр("ru='Не введено ни одной строки в список ""%1"" по этапу ""%2""';uk='Не введено жодного рядка в список ""%1"" по етапу ""%2""'");
			Для каждого ВыпускающийЭтап Из ВыпускающиеЭтапы Цикл
				
				СтруктураПоиска = Новый Структура("ЭтапРедактирование", ВыпускающийЭтап);
				Если ВыходныеИзделия.НайтиСтроки(СтруктураПоиска).ВГраница() = -1 Тогда
					
					ТекстСообщения = СтрШаблон(Шаблон, ПредставлениеТЧ, ВыпускающийЭтап);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения,
						ЭтотОбъект,
						ИмяТЧ,
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Шаблон = НСтр("ru='Выпуск по рассчитываемой стоимости возможен только на последнем этапе (см. список ""%1"", строка %2)';uk='Випуск за розраховуваною вартістю можливий тільки на останньому етапі (див. список ""%1"", рядок %2)'");
		Для каждого Строка Из ВыходныеИзделия Цикл
			
			Если Строка.ЭтапРедактирование.Пустая()
				ИЛИ ВыпускающиеЭтапы.Найти(Строка.ЭтапРедактирование) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстСообщения = СтрШаблон(Шаблон, ПредставлениеТЧ, Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТЧ, Строка.НомерСтроки, "ЭтапРедактирование");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
//-- НЕ УТКА

КонецПроцедуры

Процедура ПроверитьЗаполнениеВозвратныхОтходовВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	ИмяТЧ           = "ВозвратныеОтходы";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	Для каждого Строка Из ВозвратныеОтходы Цикл
		
		Если Строка.Номенклатура.Пустая()
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
				ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Номенклатура';uk='Номенклатура'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ);
		
		Если Строка.ОбработатьПоСпецификации И Строка.Спецификация.Пустая() Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Спецификация';uk='Специфікація'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Спецификация");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.СтатьяКалькуляции) Тогда
		
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Статья калькуляции';uk='Стаття калькуляції'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		СписокСтрок = ВозвратныеОтходы.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
	КонецЕсли;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеМатериаловВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	ИмяТЧ           = "МатериалыИУслуги";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	ИспользуетсяПроизводство22 = ПроизводствоСервер.ИспользуетсяПроизводство22();
	ТипыНоменклатуры           = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МатериалыИУслуги.ВыгрузитьКолонку("Номенклатура"), "ТипНоменклатуры");
	
	Для каждого Строка Из МатериалыИУслуги Цикл
		
		Если Строка.Номенклатура.Пустая()
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
			
			ИЛИ Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции
				И Строка.ПроизводитсяВПроцессе
				И Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
			
			ИЛИ Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве
				И Строка.ПроизводитсяВПроцессе
				
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")
			) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Номенклатура';uk='Номенклатура'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.КоличествоУпаковок = 0
			И (ПустаяСтрока(Строка.АлгоритмРасчетаКоличества)
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Количество';uk='Кількість'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "КоличествоУпаковок");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
		
		КонецЕсли;
		
		Если Не Строка.ПроизводитсяВПроцессе
			И Не ЗначениеЗаполнено(Строка.СтатьяКалькуляции) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Статья калькуляции';uk='Стаття калькуляції'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.ПроизводитсяВПроцессе И Не ЗначениеЗаполнено(Строка.ИсточникПолученияПолуфабриката) Тогда
			
			Если Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации 
				И ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством") Тогда // только 2.1
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Спецификация';uk='Специфікація'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
			Если Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Этап выпуска полуфабриката';uk='Етап випуску напівфабрикату'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.Обеспечивать
			И (ИспользуетсяПроизводство22 И ТипыНоменклатуры[Строка.Номенклатура] = Перечисления.ТипыНоменклатуры.МногооборотнаяТара
				ИЛИ ТипыНоменклатуры[Строка.Номенклатура] = Перечисления.ТипыНоменклатуры.Набор) Тогда
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru='Для материала с типом ""%1"" доступно только указание способа получения ""Обеспечивать""';uk='Для матеріалу з типом ""%1"" доступно тільки зазначеня способу отримання ""Забезпечувати""'"),
					ТипыНоменклатуры[Строка.Номенклатура]);
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Корректность", НСтр("ru='Способ получения материала';uk='Спосіб отримання матеріалу'"), Строка.НомерСтроки, ПредставлениеТЧ, ТекстСообщения);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Характеристики
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
	КонецЕсли;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	// Дополнительные проверки: промежуточные полуфабрикаты, производство 2.1
	ПараметрыПроверкиПромежуточныхПолуфабрикатов = УправлениеДаннымиОбИзделиях.ПолучитьПараметрыПроверкиВнутреннихПолуфабрикатов(ЭтотОбъект);
	СтруктураПроверок = Новый Структура("СоответствиеСпецификации, КратностьПроизводимогоКоличества, ЗаполнениеВнутреннихПолуфабрикатов");
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ИмяРеквизита", "СпособПолученияМатериалаРедактирование");
	ПараметрыПроверки.Вставить("СтруктураПроверок", СтруктураПроверок);
	ПараметрыПроверки.Вставить("ПараметрыПроверкиВнутреннихПолуфабрикатов", ПараметрыПроверкиПромежуточныхПолуфабрикатов);
	
	Если УправлениеДаннымиОбИзделиях.ДоступноОписаниеВероятностиПримененияМатериалов() Тогда
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("ПроизводитсяВПроцессе,Альтернативный",Истина,Ложь));
	Иначе
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("ПроизводитсяВПроцессе",Истина));
	КонецЕсли;
	
	УправлениеДаннымиОбИзделиях.ПроверитьСпецификацииПолуфабрикатов(
		СписокСтрок,
		ПараметрыПроверки,
		Отказ,
		ЭтотОбъект);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТрудозатратВСтатусеДействует(Отказ)
	
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСтатьяКалькуляции = НСтр("ru='Не заполнена колонка ""Статья калькуляции"" в строке %1 списка ""Трудозатраты""';uk='Не заповнена колонка ""Стаття калькуляції"" в рядку %1 списку ""Трудовитрати""'");
	ШаблонКоличество        = НСтр("ru='Не заполнена колонка ""Количество"" в строке %1 списка ""Трудозатраты""';uk='Не заповнена колонка ""Кількість"" в рядку %1 списку ""Трудовитрати""'");
	
	Для каждого Строка Из Трудозатраты Цикл
		
		Если Строка.СтатьяКалькуляции.Пустая() Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтатьяКалькуляции, 
				Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
		Если Строка.Количество = 0
			И (ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) 
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонКоличество, 
				Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "Количество");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьАвтовыборРасчетПоФормуламОтборПоСвойствам(Отказ)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		Возврат;
	КонецЕсли;
	
//++ НЕ УТКА
#Область Автовыбор

	// Проверим, что свойства указанные в автовыборе и условиях использования есть в продукции
	ОсновноеИзделие = УправлениеДаннымиОбИзделияхКлиентСервер.ОсновнойРеквизитОсновногоИзделияСпецификации(ЭтотОбъект);
	Если ОсновноеИзделие.Значение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОсновноеИзделие.Имя = "ВидНоменклатуры" Тогда
		ВидИзделий = ОсновноеИзделие.Значение;
	Иначе
		ВидИзделий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеИзделие.Значение, "ВидНоменклатуры");
	КонецЕсли;
	
	СписокВсехДоступныхСвойств = УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляАвтовыбора(ВидИзделий);
	
	МассивПроверок = Новый Массив;
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "ВыходныеИзделия", ПредставлениеТаблицыВФорме("ВыходныеИзделия"), "Номенклатура"));
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "ВозвратныеОтходы", ПредставлениеТаблицыВФорме("ВозвратныеОтходы"), "Номенклатура"));
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "МатериалыИУслуги", ПредставлениеТаблицыВФорме("МатериалыИУслуги"), "Номенклатура"));
	
	// Настройки автоподбора материалов
	ШаблонСообщения = НСтр("ru='В настройке автовыбора определено, что номенклатура указывается в свойстве ""%1"", но это свойство не входит в состав свойств основного изделия (список ""%2"", строка %3).';uk='Настройці автовибору визначено, що номенклатура вказується у властивості ""%1"", але ця властивість не входить до складу властивостей основного виробу (список ""%2"", рядок %3).'");
	ТекстНеЗаданоСвойствоАвтовыбора = НСтр("ru='В настройке автовыбора определено, что номенклатура указывается в свойстве основного изделия, но свойство не задано.';uk='В настройці автовибору визначено, що номенклатура вказується у властивості основного виробу, але властивість не задано.'");
	ТекстНеЗаданАлгоритмАвтовыбора = НСтр("ru='В настройке автовыбора определено, что характеристика определяется по алгоритму, но алгоритм не задан.';uk='В настройці автовибору визначено, що характеристика визначається за алгоритмом, але алгоритм не заданий.'");
	
	Для каждого Проверка Из МассивПроверок Цикл
		
		ТабличнаяЧасть = ЭтотОбъект[Проверка.ИмяТЧ]; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия
		
		Для каждого ТекущаяСтрока Из ТабличнаяЧасть Цикл
			
			ТекстСообщения = "";
			Если ТекущаяСтрока.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции Тогда
				Если ЗначениеЗаполнено(ТекущаяСтрока.СвойствоСодержащееНоменклатуру) Тогда
					Если СписокВсехДоступныхСвойств.Найти(ТекущаяСтрока.СвойствоСодержащееНоменклатуру, "Свойство") = Неопределено Тогда
						
						ЗаголовокСвойства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.СвойствоСодержащееНоменклатуру, "Заголовок");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСообщения, 
							ЗаголовокСвойства,
							Проверка.Представление,
							Формат(ТекущаяСтрока.НомерСтроки, "ЧГ="));
							
					КонецЕсли;
				Иначе
					ТекстСообщения = ТекстНеЗаданоСвойствоАвтовыбора;
				КонецЕсли;
			ИначеЕсли ТекущаяСтрока.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоАлгоритму 
				И НЕ ЗначениеЗаполнено(ТекущаяСтрока.АлгоритмАвтовыбораХарактеристики) Тогда
				ТекстСообщения = ТекстНеЗаданАлгоритмАвтовыбора;
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Проверка.ИмяТЧ, ТекущаяСтрока.НомерСтроки, Проверка.Реквизит);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
#КонецОбласти

#Область ОтборПоСвойствам

	СписокВсехДоступныхСвойств = УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляОтбораПоСвойствам(ВидИзделий,,Ложь);

	// Настройки использования и расчета количества
	ШаблонСообщения = НСтр("ru='В настройках использования указаны неверные свойства (список ""%1"", строка %2).';uk='В настройках використання вказані невірні властивості (список ""%1"", рядок %2).'");
	
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "Трудозатраты", ПредставлениеТаблицыВФорме("Трудозатраты"), "ВидРабот"));
	
	Для каждого Проверка Из МассивПроверок Цикл
		
		ТабличнаяЧасть = ЭтотОбъект[Проверка.ИмяТЧ]; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия
		
		Для каждого ТекущаяСтрока Из ТабличнаяЧасть Цикл
			
			СтрокиОтбор = ОтборПоСвойствам.НайтиСтроки(Новый Структура("КлючСвязи", ТекущаяСтрока.КлючСвязи));
			
			Для каждого СтрокаОтбор Из СтрокиОтбор Цикл
				
				Если СписокВсехДоступныхСвойств.Найти(СтрокаОтбор.Свойство, "Свойство") = Неопределено Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонСообщения, Проверка.Представление, Формат(ТекущаяСтрока.НомерСтроки, "ЧГ="));
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Проверка.ИмяТЧ, ТекущаяСтрока.НомерСтроки, Проверка.Реквизит);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения, 
						ЭтотОбъект, 
						Поле,
						, 
						Отказ);
						
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Справочники.ЭтапыПроизводства.ПроверитьСоответствиеОтбораПоСвойствам(Ссылка, СписокВсехДоступныхСвойств, МногоэтапныйПроизводственныйПроцесс, Отказ);
	
#КонецОбласти

#Область РасчетПоФормулам

	ТабличныеЧастиОбъекта = Метаданные().ТабличныеЧасти;
	
	Отбор = Новый Структура("АлгоритмРасчетаКоличества", "");
	
	Для Ит = - МассивПроверок.ВГраница() По 0 Цикл
		Проверка = МассивПроверок[-Ит];
		Если ЭтотОбъект[Проверка.ИмяТЧ].НайтиСтроки(Отбор).Количество() = ЭтотОбъект[Проверка.ИмяТЧ].Количество() Тогда
			МассивПроверок.Удалить(-Ит);
		Иначе
			ИмяРеквизита = "КоличествоУпаковок";
			Если ТабличныеЧастиОбъекта[Проверка.ИмяТЧ].Реквизиты.Найти(ИмяРеквизита) = Неопределено Тогда
				ИмяРеквизита = "Количество";
			КонецЕсли;
			Проверка.Реквизит = ИмяРеквизита;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеИсточников = Справочники.РесурсныеСпецификации.ВыгрузитьДанныеДляКонструктораФормул(ЭтотОбъект, "");
	
	Если МассивПроверок.Количество() > 0 Тогда
		
		ПараметрыПроверки = Новый Структура("Состав, ВыводитьСообщения, ОчищатьНеНайденные", МассивПроверок, Истина, Ложь);
		
		УправлениеДаннымиОбИзделиях.ПроверитьОчиститьАлгоритмРасчетаКоличества(ЭтотОбъект, ОписаниеИсточников, ПараметрыПроверки, Отказ);
		
	КонецЕсли;
	
	Справочники.ЭтапыПроизводства.ПроверитьАлгоритмРасчетаКоличества(Ссылка, ОписаниеИсточников, МногоэтапныйПроизводственныйПроцесс, Отказ);
	
#КонецОбласти

//-- НЕ УТКА
	Возврат;
	
КонецПроцедуры

Процедура ПроверитьПроизводственныйПроцесс(Отказ)
	
	Если ОптимальнаяПартияВыпуска <> 0
		И МинимальнаяПартияВыпуска > ОптимальнаяПартияВыпуска Тогда
	
		ТекстСообщения = НСтр("ru='Минимальная партия выпуска не может превышать оптимальную партию';uk='Мінімальна партія випуску не може перевищувати оптимальну партію'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"МинимальнаяПартияВыпуска",
			,
			Отказ);
		
	КонецЕсли;
	
	Если МногоэтапныйПроизводственныйПроцесс Тогда
		ПроверитьПорядокЭтапов(Отказ);
	Иначе
		ПервыйЭтап = Справочники.РесурсныеСпецификации.ПолучитьЭтапОдногоЭтапногоПроцесса(Ссылка);
		Если Не ЗначениеЗаполнено(ПервыйЭтап) Тогда
			ТекстСообщения = НСтр("ru='Необходимо создать этап производства.';uk='Необхідно створити етап виробництва.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ПроверитьРеквизитыЭтапов(Отказ);
	
КонецПроцедуры

Процедура ПроверитьМаршрутныеКарты(Отказ)

//++ НЕ УТКА
	ШаблонСообщения = НСтр("ru='В этапе ""%1"" необходимо указать действующую маршрутную карту.';uk='У етапі ""%1"" необхідно зазначити діючу маршрутну карту.'");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка                            КАК Ссылка,
	|	ЭтапыПроизводства.Наименование                      КАК Наименование,
	|	ЭтапыПроизводства.МаршрутнаяКарта                   КАК МаршрутнаяКарта,
	|	ЭтапыПроизводства.МаршрутнаяКарта.Статус            КАК МаршрутнаяКартаСтатус,
	|	ЭтапыПроизводства.МаршрутнаяКарта.НачалоДействия    КАК МаршрутнаяКартаНачалоДействия,
	|	ЭтапыПроизводства.МаршрутнаяКарта.КонецДействия     КАК МаршрутнаяКартаКонецДействия
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ПО (ЭтапыПроизводства.Владелец = РесурсныеСпецификации.Ссылка)
	|			И (НЕ ЭтапыПроизводства.ПометкаУдаления)
	|			И (ЭтапыПроизводства.МаршрутнаяКарта <> ЗНАЧЕНИЕ(Справочник.МаршрутныеКарты.ПустаяСсылка))
	|ГДЕ
	|	РесурсныеСпецификации.Ссылка = &Спецификация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа";
	
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.МаршрутнаяКартаСтатус <> Перечисления.СтатусыМаршрутныхКарт.Действует
			
			// Начало действия маршрутной карты должно быть 
			// не позже чем конец действия спецификации
			ИЛИ Выборка.МаршрутнаяКартаНачалоДействия <> '000101010000'
				И КонецДействия <> '000101010000'
				И Выборка.МаршрутнаяКартаНачалоДействия > КонецДействия
				
			// Конец действия маршрутной карты должен быть 
			// не раньше чем начало действия спецификации
			ИЛИ Выборка.МаршрутнаяКартаКонецДействия <> '000101010000'
				И НачалоДействия <> '000101010000'
				И Выборка.МаршрутнаяКартаКонецДействия <= НачалоДействия Тогда 
				
			КлючДанных = Выборка.Ссылка;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СокрЛП(Выборка.Наименование));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, КлючДанных, "МаршрутнаяКарта",, Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
//-- НЕ УТКА
	
	Возврат; // обработчик пустой
	
КонецПроцедуры

Процедура ПроверитьРеквизитыЭтапов(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Спецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЭтапОбъект.ПроверитьЗаполнениеРеквизитов(Истина,,Отказ);
	КонецЦикла;
	
	ПроверитьЭтапыПроизводстваНаСтороне(Отказ);
	
КонецПроцедуры

Процедура ПроверитьПорядокЭтапов(Отказ)
	
	// Порядок производственного процесса
	
	СтруктураПроверок = Новый Структура;
	СтруктураПроверок.Вставить("НетПервойОперации",    НСтр("ru='Отсутствует первый этап.';uk='Відсутній перший етап.'"));
	СтруктураПроверок.Вставить("НетПоследнейОперации", НСтр("ru='Отсутствует последний этап.';uk='Відсутній останній етап.'"));
	СтруктураПроверок.Вставить("НетСледующейОперации", НСтр("ru='Этап ""%1"" ссылается на несуществующий этап.';uk='Етап ""%1"" посилається на неіснуючий етап.'"));
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
		//++ Устарело_Производство21
		ИЛИ ПроизводствоСервер.ИспользуетсяПроизводство21()
		//-- Устарело_Производство21
		Тогда
		СтруктураПроверок.Вставить("НесколькоПоследнихОпераций", НСтр("ru='Не может быть несколько последних этапов.';uk='Не може бути кілька останніх етапів.'"));
	КонецЕсли;
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
		//++ Устарело_Производство21
		ИЛИ ПроизводствоСервер.ИспользуетсяПроизводство21()
		//-- Устарело_Производство21
		Тогда
		СтруктураПроверок.Вставить("НесколькоПервыхОпераций", НСтр("ru='Не может быть несколько первых этапов.';uk='Не може бути кілька перших етапів.'"));
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяСправочникаОпераций",     "ЭтапыПроизводства");
	СтруктураПараметров.Вставить("ПолеНомерОперации",          "НомерЭтапа");
	СтруктураПараметров.Вставить("ПолеНомерСледующейОперации", "НомерСледующегоЭтапа");
	
	УправлениеДаннымиОбИзделиях.ПоследовательностьОперацийПравильная(
			Ссылка,
			СтруктураПараметров, 
			СтруктураПроверок, 
			Отказ);
	
	// Порядок полуфабрикатов производимых на этапах и настройка "Планировать не ранее"
	
	Отбор = Новый Структура("ПроизводитсяВПроцессе", Истина);
	МассивСтрок = МатериалыИУслуги.НайтиСтроки(Отбор);
	Для Индекс = -МассивСтрок.ВГраница() По 0 Цикл;
		СтрокаТаблицы = МассивСтрок[-Индекс];
		Если (СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации
					И ЗначениеЗаполнено(СтрокаТаблицы.ПланироватьНеРанее)
				ИЛИ СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
			) Тогда
			Продолжить;
		КонецЕсли;
		МассивСтрок.Удалить(-Индекс);
	КонецЦикла;
	
	Если МассивСтрок.ВГраница() <> -1 Тогда
		
		МассивСсылок = Новый Массив;
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			МассивСсылок.Добавить(СтрокаТаблицы.ЭтапРедактирование);
		КонецЦикла;
		
		ШаблонСообщения = НСтр("ru='Обнаружена неправильная последовательность этапов (список ""Материалы и работы"", строка %1).';uk='Виявлена неправильна послідовність етапів (список ""Матеріали і роботи"", рядок %1).'");
		
		Предшественники = Справочники.ЭтапыПроизводства.Предшественники(МассивСсылок);
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			Если (СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации
					И Предшественники[СтрокаТаблицы.ЭтапРедактирование].Найти(СтрокаТаблицы.ПланироватьНеРанее) <> Неопределено
				ИЛИ СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
					И Предшественники[СтрокаТаблицы.ЭтапРедактирование].Найти(СтрокаТаблицы.ИсточникПолученияПолуфабриката) <> Неопределено
				) Тогда
				Продолжить;
			КонецЕсли;
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", СтрокаТаблицы.НомерСтроки, "СпособПолученияМатериалаРедактирование"),, Отказ); 
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПовторВыходныхИзделий(Отказ)
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме("ВыходныеИзделия");
	
	Если ВыходныеИзделия.Количество() > 1 Тогда
		
		ШаблонПовторИзделия = НСтр("ru='Выходное изделие ""%1"" уже описано в спецификации (см. список ""%2"", строка %3). Дублирование выходных изделий в рамках одной спецификации не допускается.';uk='Вихідна виріб ""%1"" вже описано в специфікації (див. список ""%2"", рядок %3). Дублювання вихідних виробів в рамках однієї специфікації не допускається.'");

		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура");
		
		Для Каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
			
			СтруктураПоиска.Номенклатура = СтрокаТаблицы.Номенклатура;
			
			НайденныеИзделия = ВыходныеИзделия.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаНайдено Из НайденныеИзделия Цикл
				
				Если СтрокаТаблицы.НомерСтроки > СтрокаНайдено.НомерСтроки
					И (СтрокаТаблицы.Характеристика = СтрокаНайдено.Характеристика
						ИЛИ СтрокаТаблицы.Характеристика.Пустая()
						ИЛИ СтрокаНайдено.Характеристика.Пустая()) Тогда
						
						ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
							СтрокаТаблицы.Номенклатура,
							?(СтрокаНайдено.Характеристика.Пустая(),Неопределено,СтрокаТаблицы.Характеристика));
							
						ТекстСообщения = СтрШаблон(ШаблонПовторИзделия, ПредставлениеНоменклатуры, ПредставлениеТЧ, Формат(СтрокаНайдено.НомерСтроки, "ЧГ="));
						
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"ВыходныеИзделия",
							СтрокаТаблицы.НомерСтроки,
							"Номенклатура");
							
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщения,
							ЭтотОбъект,
							Поле,,
							Отказ);
							
						Прервать;
						
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьЗаполнениеДолейСтоимостиВыходныхИзделия(Отказ)
	
	// Для способов расчета "По плановой стоимости", "По объему", "По весу" проверка не выполняется
	Если СпособРаспределенияЗатратНаВыходныеИзделия <> Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоДолямСтоимости Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаспределенияЗатрат = Справочники.РесурсныеСпецификации.ПараметрыРаспределенияЗатрат(ЭтотОбъект);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВыходныеИзделия.ЭтапРедактирование КАК Ссылка,
	|	ВыходныеИзделия.НомерСтроки        КАК НомерСтроки,
	|
	|	ВыходныеИзделия.Номенклатура       КАК Номенклатура,
	|	ВыходныеИзделия.Характеристика     КАК Характеристика,
	|	ВыходныеИзделия.ЭтапРедактирование КАК ЭтапРедактирование,
	|
	|	ВыходныеИзделия.Упаковка           КАК Упаковка,
	|	ВыходныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
	|
	|	ВыходныеИзделия.ДоляСтоимости      КАК ДоляСтоимости
	|
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	&ВыходныеИзделия КАК ВыходныеИзделия
	|;
	|" + ПроизводствоСервер.ТекстЗапросаПроверитьДолиСтоимостиВыходныхИзделий(ПараметрыРаспределенияЗатрат.ПоляСвязи);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВыходныеИзделия", ВыходныеИзделия.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ПредставлениеТЧ = ПредставлениеТаблицыВФорме("ВыходныеИзделия");
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл;
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Доля стоимости';uk='Частка вартості'"), Выборка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", Выборка.НомерСтроки, "ДоляСтоимости");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
		
			КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВсеСтатусы

//++ НЕ УТКА

Процедура ПроверитьЗаполнениеВходящихИзделий(Отказ)
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка Тогда
		
		РеквизитПроверки = ?(ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры,
			"ОсновноеИзделиеВидНоменклатуры",
			"ОсновноеИзделиеНоменклатура");
		
		ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект[РеквизитПроверки], "ТипНоменклатуры");
		
		Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru='Не допускается указание работы в поле ""%1""';uk='Не допускається зазначення роботи в полі ""%1""'"), РеквизитПроверки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,,
				РеквизитПроверки,,
				Отказ);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

//-- НЕ УТКА

Процедура ПроверитьЗаполнениеМатериалов(Отказ)
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме("МатериалыИУслуги");

	Для каждого Строка Из МатериалыИУслуги Цикл
		
		Если ЗначениеЗаполнено(Строка.СпособПолученияМатериала) Тогда
			
			Если УправлениеДаннымиОбИзделияхКлиентСервер.ПолуфабрикатПроизводимыйВПроцессе(Строка) И НЕ Строка.ПроизводитсяВПроцессе Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Производится в процессе';uk='Виробляється в процесі'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Строка.НомерСтроки, "ПроизводитсяВПроцессе");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
		Иначе
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Способ получения материала';uk='Спосіб отримання матеріалу'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Строка.НомерСтроки, "СпособПолученияМатериала");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьВыборЭтапов(ИмяТаблицы, РеквизитЭтап, Отказ)
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТаблицы);
	ШаблонСообщения = НСтр("ru='Не допускается выбор этапов помеченных на удаление (список ""%1"", строка %2).';uk='Не допускається вибір етапів позначених на вилучення (список ""%1"", рядок %2).'");
	Для каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		Если СтрокаТаблицы[РеквизитЭтап].Пустая() ИЛИ НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы[РеквизитЭтап], "ПометкаУдаления") Тогда
			Продолжить;
		КонецЕсли;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ПредставлениеТЧ, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, СтрокаТаблицы.НомерСтроки, РеквизитЭтап);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ВыгрузитьМатериалыИУслуги()
	
	Результат = МатериалыИУслуги.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование, 
		|ПроизводитсяВПроцессе,
		|ИсточникПолученияПолуфабриката");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	Результат.Индексы.Добавить("ЭтапРедактирование, Номенклатура");
	Результат.Индексы.Добавить("ЭтапРедактирование, Номенклатура, Характеристика");
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьВыходныеИзделия()
	
	Результат = ВыходныеИзделия.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьВозвратныеОтходы()
	
	Результат = ВозвратныеОтходы.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьЭтапыПроизводстваНаСтороне(Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Таблица
	|ГДЕ
	|	Таблица.Владелец = &Владелец И Таблица.ПроизводствоНаСтороне И НЕ Таблица.ПометкаУдаления");
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Производство на стороне доступно только для типа производственного процесса "Изготовление, сборка"
	Если ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru='В спецификации типа ""%1"" не допускается использовать этапы, выполняемые переработчиком';uk='У специфікації типу ""%1"" не допускається використовувати етапи, що виконуються переробником'"), ТипПроизводственногоПроцесса);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		Возврат;
	КонецЕсли;
	
	// В КА не допускается в многоэтапной спецификации использовать этапы, выполняемые переработчиком
	Если МногоэтапныйПроизводственныйПроцесс И ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
		ТекстСообщения = НСтр("ru='В многоэтапной спецификации не допускается использовать этапы, выполняемые переработчиком';uk='У багатоетапної специфікації не допускається використовувати етапи, що виконуються переробником'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МатериалыИУслуги.НомерСтроки                                                   КАК НомерСтроки,
	|	ВЫРАЗИТЬ(МатериалыИУслуги.Номенклатура КАК Справочник.Номенклатура)            КАК Номенклатура,
	|	ВЫРАЗИТЬ(МатериалыИУслуги.ЭтапРедактирование КАК Справочник.ЭтапыПроизводства) КАК ЭтапРедактирование,
	|	МатериалыИУслуги.ПроизводитсяВПроцессе                                         КАК ПроизводитсяВПроцессе,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.ИсточникПолученияПолуфабриката ССЫЛКА Справочник.ЭтапыПроизводства
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                          КАК ПроизводитсяНаЭтапе,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.ИсточникПолученияПолуфабриката ССЫЛКА Справочник.ЭтапыПроизводства
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                                                          КАК ПроизводитсяПоСпецификации
	|ПОМЕСТИТЬ ВТТаблица
	|ИЗ
	|	&МатериалыИУслуги КАК МатериалыИУслуги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокЭтапов.Ссылка                КАК Ссылка,
	|	СписокЭтапов.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне
	|ПОМЕСТИТЬ ВтСписокЭтапов
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК СписокЭтапов
	|ГДЕ
	|	СписокЭтапов.Владелец = &Спецификация
	|	И НЕ СписокЭтапов.ПометкаУдаления
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка),
	|	МАКСИМУМ(СписокЭтапов.ПроизводствоНаСтороне)
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК СписокЭтапов
	|ГДЕ
	|	СписокЭтапов.Владелец = &Спецификация
	|	И СписокЭтапов.НомерЭтапа = 1
	|	И НЕ СписокЭтапов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СписокЭтапов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыИУслуги.НомерСтроки                       КАК НомерСтроки,
	|	МатериалыИУслуги.Номенклатура                      КАК Номенклатура,
	|	ЕСТЬNULL(СписокЭтапов.ПроизводствоНаСтороне, ЛОЖЬ) КАК ПроизводствоНаСтороне,
	|	МатериалыИУслуги.ПроизводитсяВПроцессе             КАК ПроизводитсяВПроцессе,
	|	МатериалыИУслуги.ПроизводитсяНаЭтапе               КАК ПроизводитсяНаЭтапе,
	|	МатериалыИУслуги.ПроизводитсяПоСпецификации        КАК ПроизводитсяПоСпецификации
	|ПОМЕСТИТЬ ВТМатериалыИУслуги
	|ИЗ
	|	ВТТаблица КАК МатериалыИУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСписокЭтапов КАК СписокЭтапов
	|		ПО МатериалыИУслуги.ЭтапРедактирование = СписокЭтапов.Ссылка
	|;
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Этап,
	|	Таблица.Наименование         КАК НаименованиеЭтапа,
	|	Таблица.НомерЭтапа           КАК НомерЭтапа,
	|	Таблица.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Таблица
	|ГДЕ 
	|	Таблица.Владелец = &Спецификация
	|		И Таблица.ПроизводствоНаСтороне
	|		И НЕ Таблица.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТМатериалыИУслуги КАК Т
	|ГДЕ
	|	ВЫБОР
	|			КОГДА Т.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|					И Т.ПроизводствоНаСтороне
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	//++ Устарело_Производство21
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТМатериалыИУслуги КАК Т
	|ГДЕ
	|	&ИспользуетсяПроизводство21
	|	И ВЫБОР
	|			КОГДА Т.ПроизводитсяВПроцессе
	|					И Т.ПроизводствоНаСтороне
	|					И (НЕ &ИспользуетсяПроизводство22
	|						ИЛИ Т.ПроизводитсяПоСпецификации
	|							И &ИспользуетсяПроизводство22)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	//-- Устарело_Производство21
	|");
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	НастройкиПодсистемыПроизводство = ПроизводствоСерверПовтИсп.НастройкиПодсистемыПроизводство();
	//++ Устарело_Производство21
	Запрос.УстановитьПараметр("ИспользуетсяПроизводство21", НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21);
	//-- Устарело_Производство21
	Запрос.УстановитьПараметр("ИспользуетсяПроизводство22", НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство22);
	
	ТаблицаВыходныеИзделия  = ВыгрузитьВыходныеИзделия();
	ТаблицаВозвратныеОтходы = ВыгрузитьВозвратныеОтходы();
	ТаблицаМатериалыИУслуги = ВыгрузитьМатериалыИУслуги();
	Запрос.УстановитьПараметр("МатериалыИУслуги", ТаблицаМатериалыИУслуги);
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоПакетов = Результат.Количество();
	
	// Для этапов выполняемых на стороне должны быть описаны материалы и выходные изделия
	Если Не Результат[КоличествоПакетов - 3].Пустой() Тогда
		
		ШаблонТекстаПовторПродукции = НСтр("ru='Номенклатура ""%1"" не может присутствовать одновременно в продукции и в материалах (услугах).';uk='Номенклатура ""%1"" не може бути присутньою одночасно в продукції і в матеріалах (послугах).'");
		ШаблонТекстаПовторПобочногоВыпуска = НСтр("ru='Номенклатура ""%1"" не может присутствовать одновременно в побочном выпуске и в материалах (услугах).';uk='Номенклатура ""%1"" не може бути присутньою одночасно в побічному випуску і в матеріалах (послугах).'");
		
		Если МногоэтапныйПроизводственныйПроцесс Тогда
			
			//++ НЕ УТКА
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ЭтапРедактирование");
			
			ПараметрыОтбораПоПустомуЭтапу = Новый Структура;
			ПараметрыОтбораПоПустомуЭтапу.Вставить("ЭтапРедактирование", Справочники.ЭтапыПроизводства.ПустаяСсылка());
			
			ШаблонТекстаНетСтрокВТабличнойЧасти = НСтр("ru='Для этапа %1, выполняемого переработчиком, не введено ни одной строки в список ""%2""';uk='Для етапу %1, виконуваного переробником, не введено жодного рядка в список ""%2""'");
			
			Выборка = Результат[КоличествоПакетов - 3].Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ПараметрыОтбора.ЭтапРедактирование = Выборка.Этап;
				
				МассивСтрокВыходныеИзделия = ТаблицаВыходныеИзделия.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерСледующегоЭтапа = 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВыходныеИзделия,
						ТаблицаВыходныеИзделия.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				МассивСтрокВозвратныеОтходы = ТаблицаВозвратныеОтходы.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерСледующегоЭтапа = 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВозвратныеОтходы,
						ТаблицаВозвратныеОтходы.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				// В этапе на стороне должно быть хотя бы одно выходное изделие
				// * в старой концепции - по рассчитываемой стоимости
				// * в новой концепции: 
				// ** в последнем этапе - по рассчитываемой стоимости
				// ** в остальных этапах - по фиксируемой стоимости
				Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство22 Тогда
					
					// проверяем все этапы за исключением последнего, последний проверит платформа
					Если Выборка.НомерСледующегоЭтапа <> 0 И МассивСтрокВозвратныеОтходы.ВГраница() = -1 Тогда
						
						ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("ВозвратныеОтходы"));
						
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщения, 
							ЭтотОбъект, 
							"ВозвратныеОтходы",, 
							Отказ); 
						
					КонецЕсли;
					
				Иначе
					
					Если МассивСтрокВыходныеИзделия.ВГраница() = -1 Тогда
						
						ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("ВыходныеИзделия"));
						
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщения,
							ЭтотОбъект,
							"ВыходныеИзделия",,
							Отказ);
						
					КонецЕсли;
					
				КонецЕсли;
				
				МассивСтрокМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерЭтапа = 1 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокМатериалыИУслуги,
						ТаблицаМатериалыИУслуги.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				// Для этапа, выполняемого переработчиком, должен быть описан хотя бы один материал
				Если МассивСтрокМатериалыИУслуги.ВГраница() = -1 Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("МатериалыИУслуги"));
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения, 
						ЭтотОбъект, 
						"МатериалыИУслуги",, 
						Отказ);
						
				КонецЕсли;
				
				//++ Устарело_Производство21
				// В старой концепции передача одной и той же номенклатуры между этапами запрещена
				Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21 Тогда
					
					МассивПроверок = Новый Массив;
					
					ПараметрыПроверки = Новый Структура;
					ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВыходныеИзделия");
					ПараметрыПроверки.Вставить("Коллекция",         МассивСтрокВыходныеИзделия);
					ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПродукции);
					
					МассивПроверок.Добавить(ПараметрыПроверки);
					
					ПараметрыПроверки = Новый Структура;
					ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВозвратныеОтходы");
					ПараметрыПроверки.Вставить("Коллекция",         МассивСтрокВозвратныеОтходы);
					ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПобочногоВыпуска);
					
					МассивПроверок.Добавить(ПараметрыПроверки);
					
					Для Каждого ПараметрыПроверки Из МассивПроверок Цикл
						
						Коллекция = ПараметрыПроверки.Коллекция; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия,СправочникТабличнаяЧасть.РесурсныеСпецификации.ВозвратныеОтходы
						
						Для Каждого Строка Из Коллекция Цикл
							
							СтруктураПоиска = Новый Структура;
							СтруктураПоиска.Вставить("ЭтапРедактирование", Выборка.Этап);
							СтруктураПоиска.Вставить("Номенклатура", Строка.Номенклатура);
							
							Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
								СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
							КонецЕсли; 
							
							ЕстьДублиМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1;
							
							Если НЕ ЕстьДублиМатериалыИУслуги И Выборка.НомерЭтапа = 1 Тогда
								
								СтруктураПоиска.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
								
								ЕстьДублиМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1;
								
							КонецЕсли;
							
							Если ЕстьДублиМатериалыИУслуги Тогда
							
								ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									ПараметрыПроверки.ШаблонТекста, 
									НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
										Строка.Номенклатура, 
										Строка.Характеристика));
								
								ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
									ТекстСообщения, 
									ЭтотОбъект, 
									ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
										ПараметрыПроверки.ИмяТабличнойЧасти, 
										Строка.НомерСтроки, 
										"Номенклатура"),,
									Отказ);
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЕсли;
				//-- Устарело_Производство21
				
			КонецЦикла;
			//-- НЕ УТКА
			
		Иначе
			
			//++ Устарело_Производство21
			// В старой концепции передача одной и той же номенклатуры между этапами запрещена
			Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21 Тогда
					
				МассивПроверок = Новый Массив;
				
				ПараметрыПроверки = Новый Структура;
				ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВыходныеИзделия");
				ПараметрыПроверки.Вставить("Коллекция",         ВыходныеИзделия);
				ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПродукции);
				
				МассивПроверок.Добавить(ПараметрыПроверки);
				
				ПараметрыПроверки = Новый Структура;
				ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВозвратныеОтходы");
				ПараметрыПроверки.Вставить("Коллекция",         ВозвратныеОтходы);
				ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПобочногоВыпуска);
				
				МассивПроверок.Добавить(ПараметрыПроверки);
				
				Для Каждого ПараметрыПроверки Из МассивПроверок Цикл
					
					Коллекция = ПараметрыПроверки.Коллекция; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия,СправочникТабличнаяЧасть.РесурсныеСпецификации.ВозвратныеОтходы
					
					Для Каждого Строка Из Коллекция Цикл
					
						СтруктураПоиска = Новый Структура("Номенклатура", Строка.Номенклатура);
						Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
							СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
						КонецЕсли; 
						
						Если МатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1 Тогда
							
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ПараметрыПроверки.ШаблонТекста, 
								НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
									Строка.Номенклатура, 
									Строка.Характеристика));
								
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
								ТекстСообщения, 
								ЭтотОбъект, 
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									ПараметрыПроверки.ИмяТабличнойЧасти, 
									Строка.НомерСтроки, 
									"Номенклатура"),,
								Отказ);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			//-- Устарело_Производство21
			
			// Для этапа, выполняемого переработчиком, должен быть описан хотя бы один материал
			Если ТаблицаМатериалыИУслуги.Количество() = 0 Тогда
				
				ШаблонТекста = НСтр("ru='Не введено ни одной строки в список ""Материалы и услуги""';uk='Не введено жодного рядка в список ""Матеріали і послуги""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ШаблонТекста, 
					ЭтотОбъект, 
					"МатериалыИУслуги",, 
					Отказ); 
					
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// В списке ""Материалы и работы"" не допускается указание работ для этапов, выполняемых переработчиком
	Если Не Результат[КоличествоПакетов - 2].Пустой() Тогда
		
		ШаблонТекста = НСтр("ru='В списке ""Материалы и работы"" не допускается указание работ для этапов, выполняемых переработчиком (см. строку %1).';uk='У списку ""Матеріали і роботи"" не допускається зазначення робіт для етапів, виконуваних переробником (див. рядок %1).'");
		
		Выборка = Результат[КоличествоПакетов - 2].Выбрать();
			
		Пока Выборка.Следующий() Цикл
			
			Поле           = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Выборка.НомерСтроки, "Номенклатура");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения, 
				ЭтотОбъект, 
				Поле,, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	//++ Устарело_Производство21
	// В старой концепции в списке ""Материалы и работы"" не допускается указание 
	//   полуфабрикатов производимых в процессе для этапов, выполняемых переработчиком
	Если Не Результат[КоличествоПакетов - 1].Пустой() Тогда
		
		ШаблонТекста = НСтр("ru='В списке ""Материалы и работы"" не допускается указание полуфабрикатов производимых в процессе для этапов, выполняемых переработчиком (см. строку %1).';uk='У списку ""Матеріали і роботи"" не допускається зазначення напівфабрикатів, що виробляються в процесі етапів, виконуваних переробником (див. рядок %1).'");
		
		Выборка = Результат[КоличествоПакетов - 1].Выбрать();
			
		Пока Выборка.Следующий() Цикл
			
			Поле           = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Выборка.НомерСтроки, "Номенклатура");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения, 
				ЭтотОбъект, 
				Поле,, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	//-- Устарело_Производство21
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Прочее

Процедура ПроверитьСкорректироватьЭтапыПроизводства(Отказ)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Этапы.Ссылка,
	|	Этапы.НомерЭтапа,
	|	Этапы.НомерСледующегоЭтапа,
	|	Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ОдновременноПроизводимоеКоличество
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Этапы
	|ГДЕ
	|	Этапы.Владелец = &Спецификация
	|	И НЕ Этапы.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Этапы.НомерЭтапа");
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	СтатусДействует = (Статус = Перечисления.СтатусыСпецификаций.Действует);
	Индекс = 1;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			Если МногоэтапныйПроизводственныйПроцесс Или (Индекс = 1) Тогда
				
				ОчиститьСсылкуНаЭтап = Ложь;
				ТребуетсяПеренумеровать = Не МногоэтапныйПроизводственныйПроцесс И (Выборка.НомерЭтапа <> 1 ИЛИ Выборка.НомерСледующегоЭтапа <> 0);
				
				Если СтатусДействует И Не ВыпускПроизвольнымиПорциями
					И Выборка.ОдновременноПроизводимоеКоличество <> Цел(Выборка.ОдновременноПроизводимоеКоличество) Тогда
					ТребуетсяОкруглитьКоличество = Истина;
				Иначе
					ТребуетсяОкруглитьКоличество = Ложь;
				КонецЕсли;
				
				Если ТребуетсяПеренумеровать ИЛИ ТребуетсяОкруглитьКоличество Тогда
					
					ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
					
					Если ТребуетсяПеренумеровать Тогда
						ЭтапОбъект.НомерЭтапа = 1;
						ЭтапОбъект.НомерСледующегоЭтапа = 0;
					КонецЕсли;
					
					Если ТребуетсяОкруглитьКоличество Тогда
						ЭтапОбъект.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий = Окр(ЭтапОбъект.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий);
					КонецЕсли;
					
					ЭтапОбъект.Записать();
					
				КонецЕсли;
				
			Иначе
				
				ОчиститьСсылкуНаЭтап = Истина;
				
				ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ЭтапОбъект.ДополнительныеСвойства.Вставить("РазрешитьЗапись");
				
				ЭтапОбъект.УстановитьПометкуУдаления(Истина);
				
			КонецЕсли;
			
		Исключение
			
			ИмяСобытия = НСтр("ru='Ресурсная спецификация';uk='Ресурсна специфікація'",ОбщегоНазначения.КодОсновногоЯзыка());
			ТекстШаблон = НСтр("ru='Не удалось скорректировать этапы ресурсной спецификации %1';uk='Не вдалося скоригувати етапи ресурсної специфікації %1'");
			
			ТекстСообщения = СтрШаблон(ТекстШаблон, НСтр("ru='(подробнее см. в журнале регистрации)';uk='(докладніше див. у журналі реєстрації)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
			ТекстСообщения = СтрШаблон(ТекстШаблон, НСтр("ru='по причине:';uk='через:'") + " " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Предупреждение,
				,
				Ссылка,
				ТекстСообщения);
			
		КонецПопытки;
		
		Если ОчиститьСсылкуНаЭтап Тогда
			
			ОчиститьСсылкуНаВыбранныйЭтап(ВыходныеИзделия,  Выборка.Ссылка);
			ОчиститьСсылкуНаВыбранныйЭтап(ВозвратныеОтходы, Выборка.Ссылка);
			ОчиститьСсылкуНаВыбранныйЭтап(МатериалыИУслуги, Выборка.Ссылка);
			ОчиститьСсылкуНаВыбранныйЭтап(Трудозатраты,     Выборка.Ссылка);
			
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьРасчетДлительностиПоСпецификации() Экспорт
	
	// При изменении статуса спецификации запускаем полный пересчет вторичных данных:
	// - рассчитываем всю очередь заданий, 
	// - рассчитываем количество дней до окончания.
	
	Если ( Статус = Перечисления.СтатусыСпецификаций.ВРазработке ) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПолныйПересчет
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|ГДЕ
	|	( РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке)
	|		ИЛИ НЕ (&ОптимальнаяПартияВыпуска <> 0
	|				ИЛИ &МинимальнаяПартияВыпуска <> 0
	|				ИЛИ &ЕстьПараметризацияРесурсов
	|				ИЛИ &ЕстьАвтовыборСпецификацийВПроцессе
	|				ИЛИ &ЕстьРасчетВероятности
	|				ИЛИ &ЕстьНекратныйВыпуск
	|				ИЛИ &ЕстьНекратныеНормативыВРЦ) <> (НЕ (РесурсныеСпецификации.ОптимальнаяПартияВыпуска <> 0
	|														ИЛИ РесурсныеСпецификации.МинимальнаяПартияВыпуска <> 0
	|														ИЛИ РесурсныеСпецификации.ЕстьПараметризацияРесурсов
	|														ИЛИ РесурсныеСпецификации.ЕстьАвтовыборСпецификацийВПроцессе
	|														ИЛИ РесурсныеСпецификации.ЕстьРасчетВероятности
	|														ИЛИ РесурсныеСпецификации.ЕстьНекратныйВыпуск
	|														ИЛИ РесурсныеСпецификации.ЕстьНекратныеНормативыВРЦ))
	|	)
	|	И РесурсныеСпецификации.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.УстановитьПараметр("ОптимальнаяПартияВыпуска",           ОптимальнаяПартияВыпуска);
	Запрос.УстановитьПараметр("МинимальнаяПартияВыпуска",           МинимальнаяПартияВыпуска);
	Запрос.УстановитьПараметр("ЕстьПараметризацияРесурсов",         ЕстьПараметризацияРесурсов);
	Запрос.УстановитьПараметр("ЕстьАвтовыборСпецификацийВПроцессе", ЕстьАвтовыборСпецификацийВПроцессе);
	Запрос.УстановитьПараметр("ЕстьРасчетВероятности",              ЕстьРасчетВероятности);
	Запрос.УстановитьПараметр("ЕстьНекратныйВыпуск",                ЕстьНекратныйВыпуск);
	Запрос.УстановитьПараметр("ЕстьНекратныеНормативыВРЦ",          ЕстьНекратныеНормативыВРЦ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ДополнительныеСвойства.Вставить("РассчитатьДлительностьПроизводства", Выборка.ПолныйПересчет);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьВыборЭтапов(ТабличнаяЧасть)

	Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		СтрокаТаблицы.Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		СтрокаТаблицы.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОчиститьСсылкуНаВыбранныйЭтап(ТабличнаяЧасть, ЭтапСсылка)

	Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		Если СтрокаТаблицы.Этап = ЭтапСсылка Тогда
			СтрокаТаблицы.Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		КонецЕсли; 
		Если СтрокаТаблицы.ЭтапРедактирование = ЭтапСсылка Тогда
			СтрокаТаблицы.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ЗаполнитьСлужебныеРеквизитыПовтИсп(); // отдельный метод требуется для обработчика обновления
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыПовтИсп() Экспорт
	
	Если ( Статус = Перечисления.СтатусыСпецификаций.ВРазработке ) Тогда
		
		ЕстьУточняемоеОсновноеИзделие      = Ложь;
//++ НЕ УТКА
		ЕстьПараметризацияРесурсов         = Ложь;
		ЕстьАвтовыборСпецификацийВПроцессе = Ложь;
		ЕстьРасчетВероятности              = Ложь;
//-- НЕ УТКА
		ЕстьНекратныйВыпуск                = Ложь;
//++ НЕ УТКА
		ЕстьНекратныеНормативыВРЦ          = Ложь;
//-- НЕ УТКА
	
	Иначе
		
		ИтогиПоЭтапам = Новый Структура("ЕстьПараметризацияРесурсов, ЕстьНекратныеНормативыВРЦ", Ложь, Ложь);
//++ НЕ УТКА
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК Таблица
		|				ГДЕ
		|					Таблица.Ссылка.Владелец = &Спецификация
		|					И НЕ (ВЫРАЗИТЬ(Таблица.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
		|					И НЕ Таблица.Ссылка.ПометкаУдаления)
		|	
		|			ИЛИ ИСТИНА В
		|	
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства.АльтернативныеВидыРабочихЦентров КАК Таблица
		|				ГДЕ
		|					Таблица.Ссылка.Владелец = &Спецификация
		|					И НЕ (ВЫРАЗИТЬ(Таблица.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
		|					И НЕ Таблица.Ссылка.ПометкаУдаления)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьПараметризацияРесурсов,
		|	ВЫБОР
		|		КОГДА ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства КАК Таблица
		|				ГДЕ
		|					Таблица.Владелец = &Спецификация
		|					И НЕ Таблица.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий В (0, 1)
		|					И НЕ Таблица.ПометкаУдаления)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьНекратныеНормативыВРЦ");
		Запрос.УстановитьПараметр("Спецификация", Ссылка);
		
		УстановитьПривилегированныйРежим(Истина);
		ЗаполнитьЗначенияСвойств(ИтогиПоЭтапам, Запрос.Выполнить().Выгрузить()[0]);
		УстановитьПривилегированныйРежим(Ложь);
//-- НЕ УТКА
		
		// используется параметризация основного изделия спецификации
		#Область ЕстьУточняемоеОсновноеИзделие
		
		ЕстьУточняемоеОсновноеИзделие = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры)
											ИЛИ (ОсновноеИзделиеХарактеристика.Пустая()
													И Справочники.Номенклатура.ХарактеристикиИспользуются(ОсновноеИзделиеНоменклатура));
		
		#КонецОбласти
		
//++ НЕ УТКА

		// используется параметризация ресурсов спецификации
		#Область ЕстьПараметризацияРесурсов
		
		ЕстьПараметризацияРесурсов = Ложь;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
			
			ЕстьПараметризацияРесурсов = ИтогиПоЭтапам.ЕстьПараметризацияРесурсов;
			Если Не ЕстьПараметризацияРесурсов Тогда
				
				СписокТЧ = Новый Массив;
				СписокТЧ.Добавить("ВыходныеИзделия");
				СписокТЧ.Добавить("МатериалыИУслуги");
				СписокТЧ.Добавить("ВозвратныеОтходы");
				СписокТЧ.Добавить("Трудозатраты");
				
				Для каждого ИмяТЧ Из СписокТЧ Цикл
					Для каждого Строка Из ЭтотОбъект[ИмяТЧ] Цикл
						Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества)
							ИЛИ ИмяТЧ <> "Трудозатраты"
								И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции
									ИЛИ Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоСвойствамПродукции
									ИЛИ Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоАлгоритму)
							ИЛИ ИмяТЧ = "МатериалыИУслуги"
								И Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации
									И НЕ ЗначениеЗаполнено(Строка.ИсточникПолученияПолуфабриката) Тогда
							ЕстьПараметризацияРесурсов = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ЕстьПараметризацияРесурсов Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		#КонецОбласти
		
		// используется автовыбор спецификации полуфабрикатов
		#Область ЕстьАвтовыборСпецификацийВПроцессе
		
		ЕстьАвтовыборСпецификацийВПроцессе = Ложь;
		
		НайденныеСтроки = МатериалыИУслуги.НайтиСтроки(Новый Структура("СпособПолученияМатериала", Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации));
		Для каждого Строка Из НайденныеСтроки Цикл
			Если ЗначениеЗаполнено(Строка.ИсточникПолученияПолуфабриката) Тогда
				Продолжить;
			КонецЕсли;
			ЕстьАвтовыборСпецификацийВПроцессе = Истина;
		КонецЦикла;
		
		#КонецОбласти
		
		// используется расчет вероятности применения
		#Область ЕстьРасчетВероятности
		
		ЕстьРасчетВероятности = Ложь;
		Для каждого Строка Из ВыходныеИзделия Цикл
			Если Строка.ПроцентБрака = 0 Тогда
				Продолжить;
			КонецЕсли;
			ЕстьРасчетВероятности = Истина;
			Прервать;
		КонецЦикла;
		Если Не ЕстьРасчетВероятности Тогда
			Для каждого Строка Из МатериалыИУслуги Цикл
				Если Строка.Вероятность = 0 Тогда
					Продолжить;
				КонецЕсли;
				ЕстьРасчетВероятности = Истина;
				Прервать;
			КонецЦикла;
		КонецЕсли;
		
		#КонецОбласти
		
//-- НЕ УТКА
		
		// некратный выпуск изделий
		#Область ЕстьНекратныйВыпуск
		
		ЕстьНекратныйВыпуск = Ложь;
		Если НЕ ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры Тогда
			Если (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
				ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка) Тогда
				Количество = ОсновноеИзделиеКоличествоУпаковок * Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ОсновноеИзделиеУпаковка, ОсновноеИзделиеНоменклатура);
				ЕстьНекратныйВыпуск = НЕ ( ВыпускПроизвольнымиПорциями ИЛИ Количество = 1 И УправлениеДаннымиОбИзделиях.ШтучноеИзделие(ОсновноеИзделиеНоменклатура));
			Иначе
				ЕстьНекратныйВыпуск = ( ВыходныеИзделия.Количество() > 1 );
				Если Не ЕстьНекратныйВыпуск Тогда
					Для каждого Строка Из ВыходныеИзделия Цикл
						Количество = Строка.КоличествоУпаковок * Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
						ЕстьНекратныйВыпуск = НЕ ( ВыпускПроизвольнымиПорциями ИЛИ Количество = 1 И УправлениеДаннымиОбИзделиях.ШтучноеИзделие(Строка.Номенклатура));
						Прервать;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		#КонецОбласти
		
//++ НЕ УТКА

		// некратные нормативы загрузки оборудования
		#Область ЕстьНекратныеНормативыВРЦ
			ЕстьНекратныеНормативыВРЦ = ИтогиПоЭтапам.ЕстьНекратныеНормативыВРЦ;
		#КонецОбласти
	
//-- НЕ УТКА
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеДанные()
	
	ИспользоватьПараметризациюРесурсныхСпецификаций = ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций");
	
	ЭтоСборка = (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	
	ВариантНазначенияСписокНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры);
	
	Для каждого ИмяТЧ Из СтрРазделить("ВыходныеИзделия,ВозвратныеОтходы,МатериалыИУслуги",",") Цикл
		Для каждого Строка Из ЭтотОбъект[ИмяТЧ] Цикл
			Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
				Строка.КоличествоУпаковок = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого Строка Из Трудозатраты Цикл
		Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
			Строка.Количество = 0;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры Тогда
		Для Индекс = -ВыходныеИзделия.Количество() + 1 По -1 Цикл
			//++ НЕ УТКА
			УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиАвтовыбораПоСтроке(
				ВыходныеИзделия[-Индекс], СоответствиеСвойств);
			УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиОтбораПоСвойствамПоСтроке(
				ВыходныеИзделия[-Индекс], ОтборПоСвойствам);
			//-- НЕ УТКА
			ВыходныеИзделия.Удалить(-Индекс);
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры
		ИЛИ СпособРаспределенияЗатратНаВыходныеИзделия <> Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоДолямСтоимости Тогда
		Для каждого Строка Из ВыходныеИзделия Цикл
			Строка.ДоляСтоимости = 0;
		КонецЦикла;
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ЭтоСборка И ВариантНазначенияСписокНоменклатуры Тогда
		УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиУточненияПримененияСпецификации(ЭтотОбъект);
	КонецЕсли;
	//-- НЕ УТКА
	
	Для каждого Строка Из МатериалыИУслуги Цикл
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации Тогда
			Строка.ПланироватьНеРанее = Неопределено;
		КонецЕсли;
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе Тогда
			Строка.СпецификацияРемонта = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обновляет значение реквизита Этап в табличных частях
//  - если реквизит ЭтапРедактирование не пустой то подставляет значение из него
//  - если реквизит ЭтапРедактирование пустой то
//		1. для изделий подставляет последний этап
//		2. для материалов подставляет первый этап
//
Процедура ОбновитьПривязкуЭтапов()
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтапПустаяСсылка = Справочники.ЭтапыПроизводства.ПустаяСсылка();
	
	ЗаполнитьОсновноеИзделиеЭтап = ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
										   ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт;
	
	ПервыйЭтап    = Неопределено;
	ПоследнийЭтап = Неопределено;
	Если (ЗаполнитьОсновноеИзделиеЭтап
			ИЛИ ВыходныеИзделия.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ ВозвратныеОтходы.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ МатериалыИУслуги.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ Трудозатраты.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
		) Тогда
		ПолучитьПервыйИПоследнийЭтап(ПервыйЭтап, ПоследнийЭтап);
	КонецЕсли;
	
	СписокТЧ = Новый Массив;
	СписокТЧ.Добавить("ВыходныеИзделия");
	СписокТЧ.Добавить("ВозвратныеОтходы");
	СписокТЧ.Добавить("МатериалыИУслуги");
	СписокТЧ.Добавить("Трудозатраты");
	
	Для каждого ИмяТЧ Из СписокТЧ Цикл
		
		Для каждого СтрокаТЧ Из ЭтотОбъект[ИмяТЧ] Цикл
			
			Если СтрокаТЧ.ЭтапРедактирование.Пустая() Тогда
				Если ИмяТЧ = "ВыходныеИзделия" ИЛИ ИмяТЧ = "ВозвратныеОтходы" Тогда
					СтрокаТЧ.Этап = ПоследнийЭтап;
				Иначе
					СтрокаТЧ.Этап = ПервыйЭтап;
				КонецЕсли;
			Иначе
				СтрокаТЧ.Этап = СтрокаТЧ.ЭтапРедактирование;
			КонецЕсли; 
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Если ЗаполнитьОсновноеИзделиеЭтап Тогда
		ОсновноеИзделиеЭтап = ПервыйЭтап;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПервыйИПоследнийЭтап(ПервыйЭтап, ПоследнийЭтап) Экспорт

	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЭтапыПроизводства.Ссылка,
		|	ЭтапыПроизводства.НомерЭтапа,
		|	ЭтапыПроизводства.НомерСледующегоЭтапа
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|ГДЕ
		|	(ЭтапыПроизводства.НомерЭтапа = 1
		|			ИЛИ ЭтапыПроизводства.НомерСледующегоЭтапа = 0)
		|	И ЭтапыПроизводства.Владелец = &Владелец
		|	И (НЕ ЭтапыПроизводства.ПометкаУдаления)");
		
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НомерЭтапа = 1 Тогда
			ПервыйЭтап = Выборка.Ссылка;
		КонецЕсли; 
		
		Если Выборка.НомерСледующегоЭтапа = 0 Тогда
			ПоследнийЭтап = Выборка.Ссылка;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьВидНоменклатуры()
	
	ЭтоСборка   = (ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Сборка"));
	ЭтоРемонт   = (ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Ремонт"));
	
	СписокСтрок = Новый Массив;
	СписокНоменклатуры = Новый Массив;
	
	Если (ЭтоСборка ИЛИ ЭтоРемонт) Тогда
		Для каждого Строка Из ВыходныеИзделия Цикл
			Если НЕ ЗначениеЗаполнено(Строка.ВидНоменклатуры) И ЗначениеЗаполнено(Строка.Номенклатура) Тогда
				СписокСтрок.Добавить(Строка);
				СписокНоменклатуры.Добавить(Строка.Номенклатура);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновноеИзделиеВидНоменклатуры) И ЗначениеЗаполнено(ОсновноеИзделиеНоменклатура) Тогда
		СписокСтрок.Добавить(ЭтотОбъект);
		СписокНоменклатуры.Добавить(ОсновноеИзделиеНоменклатура);
	КонецЕсли;
	
	Если СписокСтрок.Количество() > 0 Тогда
		ЗначенияРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокНоменклатуры, "ВидНоменклатуры");
		Для каждого СтрокаОбъект Из СписокСтрок Цикл
			Префикс = ?(СтрокаОбъект = ЭтотОбъект, "ОсновноеИзделие", "");
			СтрокаОбъект[Префикс+"ВидНоменклатуры"] = ЗначенияРеквизита[СтрокаОбъект[Префикс+"Номенклатура"]];
		КонецЦикла;
	КонецЕсли;
	
	СписокСтрок = Неопределено;
	
КонецПроцедуры

//++ НЕ УТКА

Процедура ОбновитьТребуетсяСправочноеУказаниеСерий()

	НаборЗаписей = РегистрыСведений.ТребуетсяСправочноеУказаниеСерий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Спецификация.Установить(Ссылка);
	
	СтруктураПоиска = Новый Структура("ТребуетсяУказыватьСерии", Истина);
	
	// ВыходныеИзделия
	ТаблицаНоменклатура = ВыходныеИзделия.Выгрузить(СтруктураПоиска, "Номенклатура");
 	СписокНоменклатуры = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(ТаблицаНоменклатура.ВыгрузитьКолонку("Номенклатура"));
	
	Для каждого НоменклатураСтроки Из СписокНоменклатуры Цикл
		Если НЕ ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Продолжить;
		КонецЕсли;
		СтроНоваяЗапись = НаборЗаписей.Добавить();
		СтроНоваяЗапись.Спецификация = Ссылка;
		СтроНоваяЗапись.Номенклатура = НоменклатураСтроки;
		СтроНоваяЗапись.ВидСтрокиСпецификации = Перечисления.ВидыСтрокДереваСпецификаций.ВыходноеИзделие;
	КонецЦикла; 
	
	// МатериалыИУслуги
	ТаблицаНоменклатура = МатериалыИУслуги.Выгрузить(СтруктураПоиска, "Номенклатура");
 	СписокНоменклатуры = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(ТаблицаНоменклатура.ВыгрузитьКолонку("Номенклатура"));
	Для каждого НоменклатураСтроки Из СписокНоменклатуры Цикл
		Если НЕ ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Продолжить;
		КонецЕсли;
		СтроНоваяЗапись = НаборЗаписей.Добавить();
		СтроНоваяЗапись.Спецификация = Ссылка;
		СтроНоваяЗапись.Номенклатура = НоменклатураСтроки;
		СтроНоваяЗапись.ВидСтрокиСпецификации = Перечисления.ВидыСтрокДереваСпецификаций.Материал;
	КонецЦикла; 
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ВыпускающиеЭтапы()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	Владелец = &Ссылка И НЕ ПометкаУдаления И НомерСледующегоЭтапа = 0");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	
КонецФункции

//-- НЕ УТКА

Функция ПредставлениеТаблицыВФорме(ИмяТЧ)
	
	ИмяСписка = "";
	
	Если ИмяТЧ = "ВыходныеИзделия"
		И ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
		
		ИмяСписка = НСтр("ru='Продукция';uk='Продукція'");
		
	Иначе
		
		ИмяСписка = Метаданные.Справочники.РесурсныеСпецификации.ТабличныеЧасти[ИмяТЧ].Синоним;
		
	КонецЕсли;
	
	Возврат ИмяСписка;
	
КонецФункции

Процедура ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ)
	
	Если НЕ ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		Возврат;
	КонецЕсли;
	
	Если Строка.КоличествоУпаковок = 0 Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru='Количество';uk='Кількість'"),
			Строка.НомерСтроки, ПредставлениеТЧ);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
		ЭтотОбъект,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "КоличествоУпаковок"),,
		Отказ);
	
КонецПроцедуры

Функция ПараметрыСинхронизацииКлючей()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Справочник.ПартииПроизводства", "ПометкаУдаления");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
