#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует компоновщик настроек компоновки данных,
// по данным источника получения данных хозяйственной операции.
//
// Параметры:
//	Форма - Форма - форма объекта.
//	ХозяйственнаяОперация - СправочникСсылка.НастройкиХозяйственныхОпераций - хозяйственная операция,
//							определяющая источник получения данных.
//
Процедура ИнициализироватьКомпоновщик(Форма, ХозяйственнаяОперация) Экспорт

	Форма.АдресСхемыКомпоновкиДанных = ПолучитьСхемуКомпоновкиДанных(Форма, ХозяйственнаяОперация);
	Если ЗначениеЗаполнено(Форма.АдресСхемыКомпоновкиДанных) Тогда
		Форма.Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма.АдресСхемыКомпоновкиДанных));
	КонецЕсли;

КонецПроцедуры

// Загружает настройки в компоновщик настроек.
//
// Параметры:
//	Форма - Форма - форма объекта.
//	Настройки - НастройкиКомпоновкиДанных - загружаемые настройки.
//
Процедура ЗагрузитьНастройки(Форма, Настройки) Экспорт

	Если Настройки <> Неопределено Тогда
		Форма.Компоновщик.ЗагрузитьНастройки(Настройки);
	КонецЕсли;

КонецПроцедуры

// Записывает информацию о дополнительном отборе, установленном в объекте.
// Вызывается в событиях "ПередЗаписьюНаСервере" в формах справочника.
//
// Параметры:
//	Форма - Форма - форма объекта.
//	Объект - СправочникОбъект.ШаблоныПроводокДляМеждународногоУчета - записываемый объект.  
//
Процедура ЗаписатьДополнительныйОтбор(Форма, Объект) Экспорт

	Объект.ДополнительныйОтбор = Новый ХранилищеЗначения(Форма.Компоновщик.ПолучитьНастройки());
	Объект.ПредставлениеОтбора = Строка(Форма.Компоновщик.Настройки.Отбор);

КонецПроцедуры

// Формирует индекс картинки для объекта справочника.
//
// Параметры:
//	ШаблонПроводки - СправочникОбъект.ШаблоныПроводокДляМеждународногоУчета - шаблон проводки.
//
// Возвращаемое значение:
//	Число - индекс картинки.
//
Функция ИндексКартинки(ШаблонПроводки) Экспорт

	Если ШаблонПроводки.ЭтоГруппаШаблонов Тогда
		Если ШаблонПроводки.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Все Тогда
			Результат = 0;
		Иначе
			Результат = 1;
		КонецЕсли;
	Иначе
		Результат = 2;
	КонецЕсли;
	
	Если ШаблонПроводки.ПометкаУдаления Тогда
		Результат = Результат + 3
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		СтандартнаяОбработка = Ложь;
		ЭтоГруппаШаблонов = Ложь;
		ШаблонПроводки = Неопределено;
		Если Параметры.Свойство("Ключ", ШаблонПроводки) И ЗначениеЗаполнено(ШаблонПроводки)
			ИЛИ Параметры.Свойство("ЗначениеКопирования", ШаблонПроводки) И ЗначениеЗаполнено(ШаблонПроводки) Тогда
			ЭтоГруппаШаблонов = МеждународнаяОтчетностьВызовСервера.ЭтоГруппаШаблонов(ШаблонПроводки);
		ИначеЕсли Параметры.Свойство("ДополнительныеПараметры") Тогда
			Если Параметры.ДополнительныеПараметры.Свойство("ЭтоГруппаШаблонов") Тогда
				ЭтоГруппаШаблонов = Параметры.ДополнительныеПараметры.ЭтоГруппаШаблонов;
			КонецЕсли;
		КонецЕсли;
		
		ВыбраннаяФорма = "Справочник.ШаблоныПроводокДляМеждународногоУчета.Форма." + ?(ЭтоГруппаШаблонов, "ФормаГруппы", "ФормаЭлемента");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСхемуКомпоновкиДанных(Форма, ХозяйственнаяОперация)

	АдресСКД = Неопределено;
	СхемаКомпоновкиДанных = ИсточникиДанныхПовтИсп.СхемаПолученияДанных(ХозяйственнаяОперация);
	Если СхемаКомпоновкиДанных <> Неопределено Тогда
		АдресСКД = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат АдресСКД;

КонецФункции


#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Справочники.ШаблоныПроводокДляМеждународногоУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ШаблоныПроводокДляМеждународногоУчета.ЗарегистироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d884c0c1-351d-49b9-961f-ca88911cc946");
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Справочник.ШаблоныПроводокДляМеждународногоУчета";
	Обработчик.ИзменяемыеОбъекты = "Справочник.ШаблоныПроводокДляМеждународногоУчета";
	Обработчик.БлокируемыеОбъекты = "Справочник.ШаблоныПроводокДляМеждународногоУчета";
	Обработчик.Комментарий = НСтр("ru='Замена значений источников уточнения счетов по умолчанию в справочнике шаблонов проводок для международного учета.
                                   |Заменяется источник уточнения счета ""ГФУ доходов/расходов"" на ""ГФУ учета доходов/расходов кредита"".
                                   |Пока работа обработчика не завершена, формирование проводок по международному учету невозможно.'
                                   |;uk='Заміна значень джерел уточнення рахунків по умовчанню у довіднику шаблонів проводок для міжнародного обліку.
                                   |Замінюється джерело уточнення рахунку ""ГФО доходів/витрат"" на ""ГФО обліку доходів/витрат кредиту"".
                                   |Поки робота обробника не завершена, формування проводок з міжнародного обліку неможливе.'");

КонецПроцедуры

Процедура ЗарегистироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ШаблоныПроводок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|ГДЕ
	|	ШаблоныПроводок.Операция.ИсточникДанных = ""ДвиженияДоходыРасходыПрочиеАктивыПассивы""
	|	И (ШаблоныПроводок.ТипИсточникаУточненияСчетаДт В (&ГруппыУчетаДоходовРасходов)
	|		ИЛИ ШаблоныПроводок.ТипИсточникаУточненияСчетаКт В (&ГруппыУчетаДоходовРасходов))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ШаблоныПроводок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|ГДЕ
	|	НЕ ШаблоныПроводок.ЭтоГруппаШаблонов
	|	И НЕ ШаблоныПроводок.Операция.ИсточникДанных В (
	|		""НДСРеестрПолученныхНалоговыхДокументов"",
	|		""НДСРеестрВыданныхНалоговыхДокументов"",
	|		"""")
	|");
	
	ГруппыУчетаДоходовРасходов = Новый Массив;
	ГруппыУчетаДоходовРасходов.Добавить(Перечисления.ТипыИсточниковУточненияСчета.ГруппаФинансовогоУчетаДоходовРасходов);
	Запрос.УстановитьПараметр("ГруппыУчетаДоходовРасходов", ГруппыУчетаДоходовРасходов);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ТипыДанныхУчета = Перечисления.ТипыДанныхУчета;
	ИсточникиНаправлений = Перечисления.ИсточникиНаправленийДеятельностиАналитическихРегистров;
	ПустойИсточник = ИсточникиНаправлений.ПустаяСсылка();
	
	НаправлениеДвижения = Новый Соответствие;
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.Номенклатура, ИсточникиНаправлений.НаправлениеДеятельностиНоменклатуры);
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.Контрагенты, ИсточникиНаправлений.НаправлениеДеятельностиКонтрагента);
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.ДенежныеСредства, ИсточникиНаправлений.НаправлениеДеятельностиДС);
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.ДоходыРасходы, ИсточникиНаправлений.НаправлениеДеятельностиСтатьи);
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.ПрочиеАктивыПассивы, ИсточникиНаправлений.НаправлениеДеятельностиСтатьи);
	НаправлениеДвижения.Вставить(ТипыДанныхУчета.ПустаяСсылка(), ПустойИсточник);
	
	СимметричныеРегистры = Новый Структура;
	СимметричныеРегистры.Вставить("ДвиженияДенежныхСредств");
	СимметричныеРегистры.Вставить("ДвиженияДоходыРасходыПрочиеАктивыПассивы");
	СимметричныеРегистры.Вставить("ДвиженияКонтрагентКонтрагент");
	СимметричныеРегистры.Вставить("ДвиженияНоменклатураНоменклатура");
	
	НаправлениеДеятельности = ИсточникиНаправлений.НаправлениеДеятельности;
	КорНаправлениеДеятельности = ИсточникиНаправлений.КорНаправлениеДеятельности;
	
	ГФУДоходовРасходов = Перечисления.ТипыИсточниковУточненияСчета.ГруппаФинансовогоУчетаДоходовРасходов;
	ГФУДоходовРасходовДебета = Перечисления.ТипыИсточниковУточненияСчета.ГруппаФинансовогоУчетаДоходовРасходовДебета;
	ГФУДоходовРасходовКредита = Перечисления.ТипыИсточниковУточненияСчета.ГруппаФинансовогоУчетаДоходовРасходовКредита;
	
	
	
	ПолноеИмяОбъекта = "Справочник.ШаблоныПроводокДляМеждународногоУчета";
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			ВидыДвижений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Операция, "ИсточникДанных,Приход,Расход");
			ИсточникДанных = ВидыДвижений.ИсточникДанных;
			
			#Область ТипИсточникаУточненияСчета
			
			Если ИсточникДанных = "ДвиженияДоходыРасходыПрочиеАктивыПассивы"
				И Объект.ТипИсточникаУточненияСчетаДт = ГФУДоходовРасходов Тогда
				Объект.ТипИсточникаУточненияСчетаДт = ГФУДоходовРасходовДебета;
			КонецЕсли;
			
			Если ИсточникДанных <> "ДвиженияДоходыРасходыПрочиеАктивыПассивы"
				И (Объект.ТипИсточникаУточненияСчетаДт = ГФУДоходовРасходовДебета
					ИЛИ Объект.ТипИсточникаУточненияСчетаДт = ГФУДоходовРасходовКредита) Тогда
				Объект.ТипИсточникаУточненияСчетаДт = ГФУДоходовРасходов;
			КонецЕсли;
			
			Если ИсточникДанных = "ДвиженияДоходыРасходыПрочиеАктивыПассивы"
				И Объект.ТипИсточникаУточненияСчетаКт = ГФУДоходовРасходов Тогда
				Объект.ТипИсточникаУточненияСчетаКт = ГФУДоходовРасходовКредита;
			КонецЕсли;
			
			Если ИсточникДанных <> "ДвиженияДоходыРасходыПрочиеАктивыПассивы" И 
				(Объект.ТипИсточникаУточненияСчетаКт = ГФУДоходовРасходовДебета
					ИЛИ Объект.ТипИсточникаУточненияСчетаКт = ГФУДоходовРасходовКредита) Тогда
				Объект.ТипИсточникаУточненияСчетаКт = ГФУДоходовРасходов;
			КонецЕсли;
			
			#КонецОбласти


			#Область ИсточникНаправления
			
			ВидыДвижений.Удалить("ИсточникДанных");
			Для Каждого ВидДвижения Из ВидыДвижений Цикл
				ДтКт = ?(ВидДвижения.Ключ = "Приход", "Дт", "Кт");
				ИсточникНаправления = НаправлениеДвижения[ВидДвижения.Значение];
				Если СимметричныеРегистры.Свойство(ИсточникДанных) Тогда
					ИсточникНаправления = ?(ВидДвижения.Ключ = "Приход", КорНаправлениеДеятельности, НаправлениеДеятельности);
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(Объект["ИсточникНаправления"+ДтКт])Тогда
					Объект["ИсточникНаправления"+ДтКт] = ИсточникНаправления;
				КонецЕсли;
			КонецЦикла;
			
			#КонецОбласти
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#КонецЕсли