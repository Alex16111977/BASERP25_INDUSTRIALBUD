#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мРежимОбновленияКода Экспорт; // если флаг установлен - не производится обновление наименования, 
// представления длительности, представления расписания 
Перем мРежимОбновленияНаименования Экспорт; // если флаг установлен - не производим проверки
Перем мПериодичностьРодителя Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Перем Ошибки;
	
	Если мРежимОбновленияКода Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли;
	
	ПроверенныеРеквизитыОбъекта = Новый Массив;
	Если ЭтоГруппа Тогда
		ПроверенныеРеквизитыОбъекта.Добавить("Наименование");
		ПроверенныеРеквизитыОбъекта.Добавить("Описание");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Родитель");
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		Если Не ВыполнятьАвтоматически Тогда
			ПроверяемыеРеквизиты.Добавить("Ответственный");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ПроверенныеРеквизитыОбъекта);
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Перем Ошибки, ПериодичностьРодителя;
	
	Если ОбменДанными.Загрузка ИЛИ мРежимОбновленияКода Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоГруппа Тогда
		Если мРежимОбновленияНаименования Тогда
			ПериодичностьРодителя = мПериодичностьРодителя;
		Иначе
			ПериодичностьРодителя = ?(ЗначениеЗаполнено(Родитель), Родитель.Периодичность, Владелец.Периодичность);
		КонецЕсли;
		
		ПредставлениеПериодичности = "";
		
		Если Периодичность = ПериодичностьРодителя Тогда
			ПредставлениеПериодичности = НСтр("ru='Однократно';uk='Одноразово'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.День Тогда
			ПредставлениеПериодичности = НСтр("ru='Ежедневно';uk='Щодня'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
			ПредставлениеПериодичности = НСтр("ru='Еженедельно';uk='Щотижня'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Декада Тогда
			ПредставлениеПериодичности = НСтр("ru='По декадам';uk='По декадах'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
			ПредставлениеПериодичности = НСтр("ru='Ежемесячно';uk='Щомісяця'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
			ПредставлениеПериодичности = НСтр("ru='Ежеквартально';uk='Щокварталу'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Полугодие Тогда
			ПредставлениеПериодичности = НСтр("ru='По полугодиям';uk='По півріччях'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Год Тогда
			ПредставлениеПериодичности = НСтр("ru='Ежегодно';uk='Щорічно'");
		КонецЕсли;
		Если ПустаяСтрока(Описание) Тогда
			Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1, %2';uk='%1, %2'"),
				ПредставлениеПериодичности,
				ПорядокВыполненияЭтапов);
		Иначе
			Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 (%2, %3)';uk='%1 (%2, %3)'"),
				Строка(Описание),
				ПредставлениеПериодичности,
				ПорядокВыполненияЭтапов);
		КонецЕсли;
		
		ЗаполнитьПредставлениеРасписания();
		
		Если мРежимОбновленияНаименования Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если ТипДлительности = Перечисления.ТипыСроковЭтаповПодготовкиБюджетов.ВКалендарныхДнях Тогда
			ОписаниеДлительности = НСтр("ru=';%1 календарный день;;%1 календарных дня;%1 календарных дней;';uk='; %1 календарний день ;; %1 календарні дні; %1 календарних днів;'");
		Иначе
			ОписаниеДлительности = НСтр("ru=';%1 рабочий день;;%1 рабочих дня;%1 рабочих дней;';uk=';%1 робочий день;;%1 робочих дні;%1 робочих днів;'");
		КонецЕсли;
		ПредставлениеДлительности = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ОписаниеДлительности, Длительность);
		НастройкаДействияТаблица = НастройкаДействия.Получить();
		Если ТипЗнч(НастройкаДействияТаблица) = Тип("ТаблицаЗначений") Тогда
			ПредставлениеНастройкиДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ПолучитьПредставлениеДействия(
				НастройкаДействияТаблица);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПодготовкиБюджетов.Родитель,
	|	ЭтапыПодготовкиБюджетов.Периодичность
	|ИЗ
	|	Справочник.ЭтапыПодготовкиБюджетов КАК ЭтапыПодготовкиБюджетов
	|ГДЕ
	|	ЭтапыПодготовкиБюджетов.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПредыдущийРодитель = Справочники.ЭтапыПодготовкиБюджетов.ПустаяСсылка();
	ПредыдущаяПериодичность = Периодичность;
	Если Выборка.Следующий() Тогда
		ПредыдущийРодитель = Выборка.Родитель;
		ПредыдущаяПериодичность = Выборка.Периодичность;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Если Не Ссылка.Пустая() Тогда
			ПроверитьВозможностьСменыПериодичности(Ошибки, ПредыдущаяПериодичность);
		КонецЕсли;
		
		ПроверитьПериодичностьПоРодителю(Ошибки, ПериодичностьРодителя);
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	КонецЕсли;
	
	Если Не Ссылка.Пустая() И Не Отказ Тогда
		
		Если Родитель <> ПредыдущийРодитель Тогда
			УстановитьНовыйКод();
			Справочники.ЭтапыПодготовкиБюджетов.ПеренумероватьГруппуЭлементов(ПредыдущийРодитель, Ссылка);
		КонецЕсли;
		
		Если ЭтоГруппа Тогда
			Если Периодичность <> ПредыдущаяПериодичность Тогда
				
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЭтапыПодготовкиБюджетов.Ссылка
				|ИЗ
				|	Справочник.ЭтапыПодготовкиБюджетов КАК ЭтапыПодготовкиБюджетов
				|ГДЕ
				|	ЭтапыПодготовкиБюджетов.ЭтоГруппа
				|	И ЭтапыПодготовкиБюджетов.Родитель = &Ссылка";
				
				РезультатЗапроса = Запрос.Выполнить();
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ЭтапыПодготовкиБюджетов -
					Объект.мРежимОбновленияНаименования = Истина;
					Объект.мПериодичностьРодителя = Периодичность;
					Объект.Записать();
				КонецЦикла;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет реквизит "Представление расписания"
// на основании выбранных параметров запуска для отображения в списке.
//
// Параметры
//  Нет
//
Процедура ЗаполнитьПредставлениеРасписания()
	
	Если Срок > 0 Тогда
		Если ТипСрока = Перечисления.ТипыСроковЭтаповПодготовкиБюджетов.ВКалендарныхДнях Тогда
			ШаблонПериода = НСтр("ru=';%1 календарный день;;%1 календарных дня;%1 календарных дней;';uk='; %1 календарний день ;; %1 календарні дні; %1 календарних днів;'");
		Иначе
			ШаблонПериода = НСтр("ru=';%1 рабочий день;;%1 рабочих дня;%1 рабочих дней;';uk=';%1 робочий день;;%1 робочих дні;%1 робочих днів;'");
		КонецЕсли;
		ПредставлениеСрока = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонПериода, Срок);
		
		Если УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ДоОкончанияПериода Тогда
			ПредставлениеРасписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Начать до окончания периода за %1';uk='Почати до закінчення періоду за %1'"),
				ПредставлениеСрока);
		ИначеЕсли УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ДоНачалаПериода Тогда
			ПредставлениеРасписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Начать до начала периода за %1';uk='Почати до початку періоду за %1'"),
				ПредставлениеСрока);
		ИначеЕсли УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ПослеОкончанияПериода Тогда
			ПредставлениеРасписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Начать после окончания периода через %1';uk='Почати після закінчення періоду через %1'"),
				ПредставлениеСрока);
		Иначе
			ПредставлениеРасписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Начать после начала периода через %1 %2';uk='Почати після початку періоду через %1 %2'"),
				Срок,
				НРег(ТипСрока));
		КонецЕсли;
	Иначе
		Если УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ДоОкончанияПериода
			ИЛИ УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ДоНачалаПериода Тогда
			ПредставлениеРасписания = НСтр("ru='Начать вместе с началом периода';uk='Почати разом з початком періоду'");
		Иначе
			ПредставлениеРасписания = НСтр("ru='Начать вместе с  окончанием периода';uk='Почати разом із закінченням періоду'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВозможностьСменыПериодичности(Ошибки, ПредыдущаяПериодичность)
	
	Если Периодичность = ПредыдущаяПериодичность Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПериодичностей = Перечисления.Периодичность.УпорядоченныеПериодичности(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	Запрос.УстановитьПараметр("ТаблицаПериодичностей", ТаблицаПериодичностей);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПериодичностей.Периодичность,
	|	ТаблицаПериодичностей.Порядок
	|ПОМЕСТИТЬ УпорядоченныеПериодичности
	|ИЗ
	|	&ТаблицаПериодичностей КАК ТаблицаПериодичностей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпорядоченныеПериодичности.Периодичность,
	|	УпорядоченныеПериодичности.Порядок
	|ПОМЕСТИТЬ ПериодичностьТекущая
	|ИЗ
	|	УпорядоченныеПериодичности КАК УпорядоченныеПериодичности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.Периодичность КАК ПеречислениеПериодичность
	|		ПО УпорядоченныеПериодичности.Периодичность = ПеречислениеПериодичность.Ссылка
	|			И (ПеречислениеПериодичность.Ссылка = &Периодичность)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(УпорядоченныеПериодичности.Порядок), 0) КАК ПорядокПодчиненного,
	|	ПериодичностьЗапускаЗадачиБюджетногоПроцесса.Порядок
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.ЭтапыПодготовкиБюджетов КАК ЭтапыПодготовкиБюджетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УпорядоченныеПериодичности КАК УпорядоченныеПериодичности
	|		ПО (УпорядоченныеПериодичности.Периодичность = ЭтапыПодготовкиБюджетов.Периодичность)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодичностьТекущая КАК ПериодичностьЗапускаЗадачиБюджетногоПроцесса
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЭтапыПодготовкиБюджетов.Ссылка В ИЕРАРХИИ(&Ссылка)
	|	И ЭтапыПодготовкиБюджетов.ЭтоГруппа
	|	И ЭтапыПодготовкиБюджетов.Ссылка <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодичностьЗапускаЗадачиБюджетногоПроцесса.Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|ГДЕ
	|	ВременнаяТаблица.Порядок < ВременнаяТаблица.ПорядокПодчиненного";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
		"Периодичность",
		НСтр("ru='Невозможно изменить периодичность. Сначала измените периодичность у подчиненных групп';uk='Неможливо змінити періодичність. Спочатку змініть періодичність у підпорядкованих груп'"), "");
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	БюджетнаяЗадача.Ссылка
	|ИЗ
	|	Задача.БюджетнаяЗадача КАК БюджетнаяЗадача
	|ГДЕ
	|	БюджетнаяЗадача.ЭтапПодготовкиБюджетов В ИЕРАРХИИ(&ЭтапПодготовкиБюджетов)";
	
	Запрос.УстановитьПараметр("ЭтапПодготовкиБюджетов", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
		"Периодичность",
		НСтр("ru='Невозможно изменить периодичность.
|Есть созданные задачи по подчиненным этапам. 
|Установите флаг ""Не выполняется"" у этапа и создайте новый этап.'
|;uk='Неможливо змінити періодичність.
|Є задачі, що створені по підпорядкованих етапах. 
|Встановіть прапорець ""Не виконується"" в етапу і створіть новий етап.'"), "");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПериодичностьПоРодителю(Ошибки, ПериодичностьРодителя)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПериодичностей.Периодичность,
	|	ТаблицаПериодичностей.Порядок
	|ПОМЕСТИТЬ УпорядоченныеПериодичности
	|ИЗ
	|	&ТаблицаПериодичностей КАК ТаблицаПериодичностей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпорядоченныеПериодичности.Порядок
	|ПОМЕСТИТЬ ПериодичностьРодитель
	|ИЗ
	|	УпорядоченныеПериодичности КАК УпорядоченныеПериодичности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.Периодичность КАК ПеречислениеПериодичность
	|		ПО УпорядоченныеПериодичности.Периодичность = ПеречислениеПериодичность.Ссылка
	|			И (ПеречислениеПериодичность.Ссылка = &ПериодичностьРодителя)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпорядоченныеПериодичности.Порядок
	|ПОМЕСТИТЬ Периодичность
	|ИЗ
	|	УпорядоченныеПериодичности КАК УпорядоченныеПериодичности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.Периодичность КАК ПеречислениеПериодичность
	|		ПО УпорядоченныеПериодичности.Периодичность = ПеречислениеПериодичность.Ссылка
	|			И (ПеречислениеПериодичность.Ссылка = &Периодичность)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВложенныйЗапрос.ПорядокРодителя) КАК ПорядокРодителя,
	|	МАКСИМУМ(ВложенныйЗапрос.Порядок) КАК Порядок
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПериодичностьРодитель.Порядок КАК ПорядокРодителя,
	|		NULL КАК Порядок
	|	ИЗ
	|		ПериодичностьРодитель КАК ПериодичностьРодитель
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		Периодичность.Порядок
	|	ИЗ
	|		Периодичность КАК Периодичность) КАК ВложенныйЗапрос";
	
	ТаблицаПериодичностей = Перечисления.Периодичность.УпорядоченныеПериодичности(Истина);
	Запрос.УстановитьПараметр("ТаблицаПериодичностей", ТаблицаПериодичностей);
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	Запрос.УстановитьПараметр("ПериодичностьРодителя", ПериодичностьРодителя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Порядок > Выборка.ПорядокРодителя Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
			"Периодичность",
			НСтр("ru='Периодичность должна быть меньше периодичности группы';uk='Періодичність повинна бути менше періодичності групи'"),
			"");
	КонецЕсли;
	
КонецПроцедуры

мРежимОбновленияКода = Ложь;
мРежимОбновленияНаименования = Ложь;

#КонецОбласти

#КонецЕсли
