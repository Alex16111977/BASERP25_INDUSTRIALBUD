
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++ НЕ УТКА

// Возвращает структуру по умолчанию передаваемую в качестве параметра в процедуру проверки "ПроверитьЗаполнение".
// 
// Возвращаемое значение:
// 	Структура - Описание:
//      * ПараметрыПроверкиУзлов - Структура -:
//           ** Форма - Неопределено -
//           ** СообщатьОшибки - Булево -
//           ** ПотокОшибок - Структура -
//      * ПроверятьУзлы - Булево -
//      * ИспользоватьУзлыОбъектовЭксплуатации - Булево -
//      * Форма - ФормаКлиентскогоПриложения - с реквизитами:
//           ** Свойства_ОписаниеДополнительныхРеквизитов - ДанныеФормыКоллекция - с колонками:
//                  *** Наименование - Строка -
//      * СообщатьОшибки - Булево -
//      * ПотокОшибок - Структура -
Функция ПараметрыПроверкиЗаполнения() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ПотокОшибок", Новый Структура);
	ОбъектыЭксплуатации.ИнициализироватьПотокОшибок(Параметры.ПотокОшибок);
	
	Параметры.Вставить("СообщатьОшибки", Истина);
	Параметры.Вставить("Форма", Неопределено);
	
	Параметры.Вставить("ИспользоватьУзлыОбъектовЭксплуатации", ПолучитьФункциональнуюОпцию("ИспользоватьУзлыОбъектовЭксплуатации"));
	
	Параметры.Вставить("ПроверятьУзлы", Истина);
	Параметры.Вставить("ПараметрыПроверкиУзлов", Справочники.УзлыОбъектовЭксплуатации.ПараметрыПроверкиЗаполнения());
	Параметры.ПараметрыПроверкиУзлов.Вставить("СообщатьОшибки", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает описание реквизитов объекта эксплуатации
//
// Возвращаемое значение:
//		ТаблицаЗначений - Таблица с описанием реквизитов элемента справочника объектов эксплуатации.
//
Функция СоставРеквизитовОбъектаЭксплуатации() Экспорт
	
	// Описание формата возвращаемой таблицы реквизитов
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("ОбязателенДляЗаполнения", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ТолькоПросмотрОбязательности", Новый ОписаниеТипов("Булево"));
	
	// Значения заполняемые по-умолчанию в строку каждого из реквизитов
	ЗначенияПоУмолчанию = Новый Структура;
	ЗначенияПоУмолчанию.Вставить("Имя", "");
	ЗначенияПоУмолчанию.Вставить("Синоним", "");
	ЗначенияПоУмолчанию.Вставить("ОбязателенДляЗаполнения", "Ложь");
	ЗначенияПоУмолчанию.Вставить("ТолькоПросмотрОбязательности", "Ложь");
	
	// Описания реквизитов, со значениями параметров отличающихся от "по-умолчанию"
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ДатаСведений", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Статус", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Класс", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Подкласс", Новый Структура("ОбязателенДляЗаполнения, ТолькоПросмотрОбязательности", Ложь, Истина));
	
	// Список реквизитов, которые должны быть исключены: служебные, не используемые, предназначенные для удаления, булевы признаки
	// 		или любые другие не предназначенные для интерактивной настройки пользователем.
	СтопЛист = Новый Структура(
		"ГруппаОСМеждународныйУчет,
		|Комментарий,
		|ДатаСведений");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		СтопЛист.Удалить("ГруппаОСМеждународныйУчет");
	КонецЕсли;
	
	// Получение таблицы реквизитов из описания метаданных
	СтруктураСправочника = Метаданные.Справочники.ОбъектыЭксплуатации;
	Для Каждого Реквизит Из СтруктураСправочника.Реквизиты Цикл
		
		Если СтопЛист.Свойство(Реквизит.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРеквизита = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРеквизита, ЗначенияПоУмолчанию);
		ЗаполнитьЗначенияСвойств(СтрокаРеквизита, Реквизит);
		
		СтрокаРеквизита.ОбязателенДляЗаполнения = (Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку);
		
		Если СтруктураРеквизитов.Свойство(Реквизит.Имя) Тогда
			ЗаполнитьЗначенияСвойств(СтрокаРеквизита, СтруктураРеквизитов[Реквизит.Имя]);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

// Выполняет проверку заполнения переданного объекта проверки по заданным параметрам.
//
// Параметры:
// 		ОбъектПроверки - ДанныеФормыСтруктура, СправочникОбъект.ОбъектыЭксплуатации, СправочникСсылка.ОбъектыЭксплуатации - Объект, ссылка или массив ссылок на объекты эксплуатации, которые необходимо проверить на корректность заполнения.
// 		ПараметрыПроверки - см. Справочники.ОбъектыЭксплуатации.ПараметрыПроверкиЗаполнения
// 		Отказ - Булево - Возвращаемый параметр, признак наличия ошибок при проверки.
//
Процедура ПроверитьЗаполнениеОбъектаЭксплуатации(ОбъектПроверки, ПараметрыПроверки, Отказ=Ложь) Экспорт
	
	КэшированныеЗначения = Неопределено;
	ОбъектыЭксплуатации.ИнициализироватьКэшированныеЗначенияПроверкиЗаполнения(КэшированныеЗначения);
	
	ТипОбъекта = ТипЗнч(ОбъектПроверки);
	
	МассивОбъектовЭксплуатации = Новый Массив;
	
	Если ТипОбъекта = Тип("СправочникСсылка.ОбъектыЭксплуатации")
		Или ТипОбъекта = Тип("Массив") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОбъектыЭксплуатации", ОбъектПроверки);
		СформироватьЗапросПроверкиЗаполнения(Запрос.Текст);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ПроверитьОбъектЭксплуатации(
				Выборка,
				ПараметрыПроверки,
				КэшированныеЗначения);
			
			МассивОбъектовЭксплуатации.Добавить(Выборка.Ссылка);
			
		КонецЦикла;
		
	Иначе
		
		ПроверитьОбъектЭксплуатации(
			ОбъектПроверки,
			ПараметрыПроверки,
			КэшированныеЗначения);
		
		МассивОбъектовЭксплуатации.Добавить(ОбъектПроверки.Ссылка);
		
	КонецЕсли;
	
	ПараметрыПроверки.Вставить("ОтказПроверкиУзлов", Ложь);
	
	Если ПараметрыПроверки.ИспользоватьУзлыОбъектовЭксплуатации
		И ПараметрыПроверки.ПроверятьУзлы Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОбъектыЭксплуатации", МассивОбъектовЭксплуатации);
		СформироватьЗапросПроверкиЗаполненияУзлов(Запрос.Текст);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Справочники.УзлыОбъектовЭксплуатации.ПроверитьЗаполнение(
					Выборка,
					ПараметрыПроверки.ПараметрыПроверкиУзлов,
					ПараметрыПроверки.ОтказПроверкиУзлов, // Отказ
					КэшированныеЗначения);
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПотокОшибок.СписокОшибок.Количество() <> 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПараметрыПроверкиУзлов.ПотокОшибок.СписокОшибок.Количество() <> 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыПроверки.СообщатьОшибки Тогда
		ОбъектыЭксплуатации.СообщитьОшибкиПроверкиЗаполнения(ПараметрыПроверки.ПотокОшибок, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет разблокировку объекта эксплуатации для разрешения возможности интерактивного редактирования.
//
// Параметры:
//	ОбъектЭксплуатации - СправочникСсылка.ОбъектыЭксплуатации - Ссылка на элемент справочника
//	Отказ - Булево - Возвращаемый параметр, признак наличия ошибок во время выполнения.
//
Процедура РазблокироватьОбъектЭксплуатации(ОбъектЭксплуатации, Отказ=Ложь) Экспорт
	
	ДанныеЗаполнения = Новый Структура("Статус", Перечисления.СтатусыОбъектовЭксплуатации.Редактируется);
	
	ОбъектСправочника = ОбъектЭксплуатации.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДанныеЗаполнения);
	Попытка
		ОбъектСправочника.Записать();
	Исключение
		Отказ = Истина;
		ВызватьИсключение НСтр("ru='Ошибка при попытке записи объекта эксплуатации:';uk='Помилка при спробі запису об''єкта експлуатації:'") + " " + ИнформацияОбОшибке().Описание;
	КонецПопытки;
	
КонецПроцедуры

//-- НЕ УТКА

// Формирует сведения об учете основного средства
//
// Параметры:
//  ОсновноеСредство - СправочникСсылка.ОбъектыЭксплуатации	 - Основное средство, для которого нужно получить сведения.
//  ОтборОрганизация - СправочникСсылка.Организациия		 - Организация, для которой нужно получить сведения.
// 
// Возвращаемое значение:
//  Структура - Содержит сведения об учете.
//
Функция СведенияОбУчете(ОсновноеСредство, ОтборОрганизация = Неопределено) Экспорт

	СведенияОбУчете = ОбъектыЭксплуатацииЛокализация.СведенияОбУчете(ОсновноеСредство, ОтборОрганизация);
	
	Если СведенияОбУчете = Неопределено Тогда
		
		Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПараметрыАмортизацииОСУУ) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВводаВЭксплуатациюБУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВводаВЭксплуатациюУУ,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВводаВЭксплуатациюБУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ, НЕОПРЕДЕЛЕНО) КАК ДокументВводаВЭксплуатациюУУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументСнятияСУчета, НЕОПРЕДЕЛЕНО) КАК ДокументСнятияСУчета,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументСнятияСУчета.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСнятияСУчета,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.Договор, НЕОПРЕДЕЛЕНО) КАК Договор,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.Договор.Представление, """") КАК ДоговорПредставление,
		|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ГруппаФинансовогоУчета, НЕОПРЕДЕЛЕНО) КАК ГруппаФинансовогоУчета,
		|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ГруппаФинансовогоУчета.Представление, """") КАК ГруппаФинансовогоУчетаПредставление,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету) КАК СостояниеБУ,
		|	ЕСТЬNULL(ПорядокУчетаОСУУСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеУУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.СрокИспользования, 0) КАК СрокИспользованияУУ,
		|	0 КАК СрокИспользованияБУ,
		|	0 КАК СрокИспользованияНУ,
		|	МестонахождениеОССрезПоследних.Период КАК ПериодСведенийОМестонахождении,
		|	МестонахождениеОССрезПоследних.Организация КАК Организация,
		|	ЕСТЬNULL(МестонахождениеОССрезПоследних.Организация.Представление, """") КАК ОрганизацияПредставление,
		|	МестонахождениеОССрезПоследних.МОЛ КАК МОЛ,
		|	ЕСТЬNULL(МестонахождениеОССрезПоследних.МОЛ.Представление, """") КАК МОЛПредставление,
		|	МестонахождениеОССрезПоследних.Местонахождение КАК Подразделение,
		|	ЕСТЬNULL(МестонахождениеОССрезПоследних.Местонахождение.Представление, """") КАК ПодразделениеПредставление,
		|	МестонахождениеОССрезПоследних.АдресМестонахождения КАК АдресМестонахождения,
		|	ЛОЖЬ КАК ПолученоВАрендуОтДругойОрганизации
		|ИЗ
		|	РегистрСведений.МестонахождениеОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК МестонахождениеОССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = МестонахождениеОССрезПоследних.ОсновноеСредство)
		|			И (ПервоначальныеСведенияОС.Организация = МестонахождениеОССрезПоследних.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПорядокУчетаОССрезПоследних
		|		ПО (ПорядокУчетаОССрезПоследних.ОсновноеСредство = МестонахождениеОССрезПоследних.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПорядокУчетаОСУУСрезПоследних
		|		ПО (ПорядокУчетаОСУУСрезПоследних.ОсновноеСредство = МестонахождениеОССрезПоследних.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУСрезПоследних.Организация = МестонахождениеОССрезПоследних.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПараметрыАмортизацииОСУУСрезПоследних
		|		ПО (ПараметрыАмортизацииОСУУСрезПоследних.ОсновноеСредство = МестонахождениеОССрезПоследних.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУСрезПоследних.Организация = МестонахождениеОССрезПоследних.Организация)";

		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", ОсновноеСредство);
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		СведенияОбУчете = Новый Структура;
		Для каждого КолонкаЗапроса Из Результат.Колонки Цикл
			СведенияОбУчете.Вставить(КолонкаЗапроса.Имя);
		КонецЦикла; 
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СведенияОбУчете, Выборка);
		
	КонецЕсли; 
	
	Возврат СведенияОбУчете;

КонецФункции

// Возвращает сведения об основном средстве.
//
// Параметры:
//  ОсновноеСредствоИлиСписок	 - Массив, СправочникСсылка.ОбъектыЭксплуатации	 - Основное средство для которого нужно получить сведения.
//  Период						 - Дата											 - На какую дату нужно получить сведения.
// 
// Возвращаемое значение:
//  Соответствие, Структура - Содержит сведения об основном средстве.
//
Функция ПервоначальныеСведения(ОсновноеСредствоИлиСписок, Период = '000101010000') Экспорт
	
	ПервоначальныеСведения = ОбъектыЭксплуатацииЛокализация.ПервоначальныеСведения(ОсновноеСредствоИлиСписок, Период);
	
	Если ПервоначальныеСведения <> Неопределено Тогда
		Возврат ПервоначальныеСведения;
	КонецЕсли; 
		
	ПервоначальныеСведения = Новый Структура;
	ПервоначальныеСведения.Вставить("Организация", Неопределено);
	ПервоначальныеСведения.Вставить("Местонахождение", Неопределено);
	ПервоначальныеСведения.Вставить("МОЛ", Неопределено);
	ПервоначальныеСведения.Вставить("НаправлениеДеятельности", Неопределено);
	ПервоначальныеСведения.Вставить("ДатаВводаВЭксплуатациюБУ", '000101010000');
	ПервоначальныеСведения.Вставить("ДатаВводаВЭксплуатациюУУ", '000101010000');
	ПервоначальныеСведения.Вставить("ДокументВводаВЭксплуатациюБУ", Неопределено);
	ПервоначальныеСведения.Вставить("ДокументВводаВЭксплуатациюУУ", Неопределено);
	ПервоначальныеСведения.Вставить("СостояниеБУ", Перечисления.СостоянияОС.НеПринятоКУчету);
	ПервоначальныеСведения.Вставить("СостояниеУУ", Перечисления.СостоянияОС.НеПринятоКУчету);
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		Возврат ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), Новый Соответствие, ПервоначальныеСведения);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Организация КАК Организация,
	|	МестонахождениеОС.Местонахождение КАК Местонахождение,
	|	МестонахождениеОС.МОЛ КАК МОЛ,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеБУ
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
	|		ПО МестонахождениеОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|			И МестонахождениеОС.Организация = ПервоначальныеСведенияОС.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОС
	|		ПО МестонахождениеОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСУУ
	|		ПО МестонахождениеОС.ОсновноеСредство = ПорядокУчетаОСУУ.ОсновноеСредство
	|			И МестонахождениеОС.Организация = ПорядокУчетаОСУУ.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Период);
	
	СписокОС = ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), ОсновноеСредствоИлиСписок, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОсновноеСредствоИлиСписок));
	Запрос.УстановитьПараметр("СписокОС", СписокОС);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), Новый Соответствие, ПервоначальныеСведения);
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Если ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив") Тогда
		Результат = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ПервоначальныеСведенияОС = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПервоначальныеСведения);
			ЗаполнитьЗначенияСвойств(ПервоначальныеСведенияОС, Выборка);
			Результат.Вставить(Выборка.ОсновноеСредство, ПервоначальныеСведенияОС);
		КонецЦикла;
	Иначе
		Результат = ПервоначальныеСведения;
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли; 
	
	Возврат Результат;

КонецФункции
 
// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП
//
// Возвращаемое значение:
// 		Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;    
	Результат.Добавить("ГруппаОСМеждународныйУчет");
	
	Возврат Результат;
	
КонецФункции

// Возвращает показатель наработки объекта эксплуатации.
//
// Параметры:
//  ОбъектЭксплуатации	 - СправочникСсылка.ОбъектыЭксплуатации	 - Объект эксплуатации.
//  Дата				 - Дата									 - Дата.
// 
// Возвращаемое значение:
//  СправочникСсылка.ПоказателиНаработки - Показатель наработки объекта эксплуатации.
//
Функция ПоказательНаработки(ОбъектЭксплуатации, Дата) Экспорт

	ТекстЗапроса = ОбъектыЭксплуатацииЛокализация.ТекстЗапросаПоказательНаработки();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(&Дата, ОсновноеСредство = &Объект) КАК ПорядокУчетаОС"
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Объект", ОбъектЭксплуатации);
	Запрос.УстановитьПараметр("ИспользуетсяУправлениеВНА_2_4", ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Дата));
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПоказателиНаработки.ПустаяСсылка();
	КонецЕсли;
		
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
		
	Возврат Выборка.ПоказательНаработки;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ОбработкаВыполнена = ВнеоборотныеАктивыВызовСервера.ОбработкаПолученияФормы_ОбъектыЭксплуатации(
							ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
	Если ВидФормы = "ФормаВыбора" И НЕ ОбработкаВыполнена Тогда
		
		ВыбраннаяФорма = "ФормаВыбора2_4";
		СтандартнаяОбработка = Ложь;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДанныеВыбора = ВнеоборотныеАктивыВызовСервера.ДанныеВыбораОбъектовЭксплуатации(Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ЕстьПраваНаЧтениеСведений() Экспорт

	Если НЕ ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4()
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.СтоимостьОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.АмортизацияОС)
		ИЛИ НЕ ОбъектыЭксплуатацииЛокализация.ЕстьПраваНаЧтениеСведений() Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

Функция ПредставлениеСведенийОбУчете(СведенияОбУчете, СтоимостьИАмортизация, ВключитьНезаполненные = Истина) Экспорт

	СведенияПринятКУчету1 = Новый Массив;
	СведенияПринятКУчету2 = Новый Массив;
	СведенияСрокИспользования1 = Новый Массив;
	СведенияСрокИспользования2 = Новый Массив;
	СведенияСрокИспользования3 = Новый Массив;
	СведенияЛиквидационнаяСтоимость = Новый Массив;
	СведенияСнятСУчета = Новый Массив;
	СведенияГФУ = Новый Массив;
	СведенияДоговор = Новый Массив;
	
	СведенияВосстановительнаяСтоимость = Новый Массив;
	СведенияНакопленнаяАмортизация = Новый Массив;
	СведенияОстаточнаяСтоимость = Новый Массив;
	
	СведенияМестонахождениеОрганизация = Новый Массив;
	СведенияМестонахождениеПодразделение = Новый Массив;
	СведенияМестонахождениеМОЛ = Новый Массив;
	СведенияМестонахождениеАдрес = Новый Массив;
	Период = '000101010000';
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
		
	ДоступенВыборОтраженияВУчетах = 
		ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	#Область ОбщиеСведения
	
	Если СведенияОбУчете <> Неопределено Тогда
		
		Если СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.ПринятоКУчету
			ИЛИ СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.ПринятоКУчету
			ИЛИ СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.СнятоСУчета
			ИЛИ СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.СнятоСУчета Тогда
		
			Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) 
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ)
					И СведенияОбУчете.ДатаВводаВЭксплуатациюУУ = СведенияОбУчете.ДатаВводаВЭксплуатациюБУ
				ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) 
					И НЕ ДоступенВыборОтраженияВУчетах Тогда
				
				СведенияПринятКУчету1.Добавить(НСтр("ru='Дата принятия к учету:';uk='Дата прийняття до обліку:'"));
				СведенияПринятКУчету1.Добавить(" ");
				ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ, "ДЛФ=D");
				Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ) Тогда
					СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ);
				Иначе
					СсылкаНаОбъект = Неопределено;
				КонецЕсли;
				СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				
			Иначе
				
				Если ДоступенВыборОтраженияВУчетах
					И (СведенияОбУчете.СостояниеУУ <> Перечисления.СостоянияОС.СнятоСУчета 
						ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ)) Тогда
						
					СведенияПринятКУчету1.Добавить(НСтр("ru='Дата принятия в регл. учете:';uk='Дата прийняття в регл. обліку:'"));
					СведенияПринятКУчету1.Добавить(" ");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ) Тогда
						ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ, "ДЛФ=D");
						Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ) Тогда
							СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ);
						Иначе
							СсылкаНаОбъект = Неопределено;
						КонецЕсли;
						СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
					Иначе	
						СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='принять к учету';uk='прийняти до обліку'"),,,, "#Создать"));
					КонецЕсли; 
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ)
					ИЛИ ОбъектыЭксплуатацииЛокализация.МожноПринятьКУчету(СведенияОбУчете) Тогда
					
					СведенияПринятКУчету2.Добавить(НСтр("ru='Дата принятия в упр. учете:';uk='Дата прийняття в упр. обліку:'"));
					СведенияПринятКУчету2.Добавить(" ");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) Тогда
						ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ, "ДЛФ=D");
						Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ) Тогда
							СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ);
						Иначе
							СсылкаНаОбъект = Неопределено;
						КонецЕсли;
						СведенияПринятКУчету2.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
					Иначе
						СведенияПринятКУчету2.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='принять к учету';uk='прийняти до обліку'"),,,, "#Создать"));
					КонецЕсли; 
				КонецЕсли; 
				
			КонецЕсли;
			
			Если СведенияОбУчете.ЛиквидационнаяСтоимость <> 0 Тогда
				СведенияСтрокой = СтрШаблон(НСтр("ru='Ликвидационная стоимость: %1 %2';uk='Ліквідаційна вартість: %1 %2'"), 
											Строка(СведенияОбУчете.ЛиквидационнаяСтоимость),
											Строка(Константы.ВалютаУправленческогоУчета.Получить()));
				СведенияЛиквидационнаяСтоимость.Добавить(СведенияСтрокой);
			КонецЕсли; 
			
			СрокИспользованияУУ = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СведенияОбУчете.СрокИспользованияУУ, Ложь);
			СрокИспользованияБУ = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СведенияОбУчете.СрокИспользованияБУ, Ложь);
			СрокИспользованияНУ = ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СведенияОбУчете.СрокИспользованияНУ, Ложь);
			
			Если СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияНУ
					И СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияУУ 
					И СведенияОбУчете.СрокИспользованияУУ <> 0
				ИЛИ СведенияОбУчете.СрокИспользованияУУ <> 0
					И СведенияОбУчете.СрокИспользованияБУ = 0
					И СведенияОбУчете.СрокИспользованияБУ = 0 Тогда
				
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru='Срок использования: %1';uk='Строк використання: %1'"), СрокИспользованияУУ));
				
			ИначеЕсли СведенияОбУчете.СрокИспользованияБУ <> 0
				И СведенияОбУчете.СрокИспользованияУУ = 0
				И (СведенияОбУчете.СрокИспользованияНУ = 0
					ИЛИ СведенияОбУчете.СрокИспользованияНУ = СведенияОбУчете.СрокИспользованияБУ) Тогда
					
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru='Срок использования: %1';uk='Строк використання: %1'"), СрокИспользованияБУ));
				
			ИначеЕсли СведенияОбУчете.СрокИспользованияНУ <> 0
				И СведенияОбУчете.СрокИспользованияУУ = 0
				И (СведенияОбУчете.СрокИспользованияБУ = 0
					ИЛИ СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияНУ) Тогда				
					
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru='Срок использования: %1';uk='Строк використання: %1'"), СрокИспользованияНУ));
				
			Иначе
				
				Если СведенияОбУчете.СрокИспользованияБУ <> 0 Тогда
					СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru='Срок использования (БУ):  %1';uk='Строк використання (БО): %1'"), СрокИспользованияБУ));
				КонецЕсли;
				Если СведенияОбУчете.СрокИспользованияНУ <> 0 Тогда
					СведенияСрокИспользования2.Добавить(СтрШаблон(НСтр("ru='Срок использования (НУ):  %1';uk='Строк використання (ПО): %1'"), СрокИспользованияНУ));
				КонецЕсли;
				Если СведенияОбУчете.СрокИспользованияУУ <> 0 Тогда
					СведенияСрокИспользования3.Добавить(СтрШаблон(НСтр("ru='Срок использования (УУ):  %1';uk='Строк використання (УО): %1'"), СрокИспользованияУУ));
				КонецЕсли;
				
			КонецЕсли;
				
			//
			Если СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.СнятоСУчета
				ИЛИ СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.СнятоСУчета Тогда
				СведенияСнятСУчета.Добавить(НСтр("ru='Дата снятия с учета:';uk='Дата зняття з обліку:'"));
				СведенияСнятСУчета.Добавить(" ");
				ДатаДокумента = Формат(СведенияОбУчете.ДатаСнятияСУчета, "ДЛФ=D");
				Если ЗначениеЗаполнено(СведенияОбУчете.ДокументСнятияСУчета) Тогда
					СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументСнятияСУчета);
					СведенияСнятСУчета.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				Иначе
					СведенияСнятСУчета.Добавить(ДатаДокумента);
				КонецЕсли;
			КонецЕсли; 
			
			СведенияГФУ.Добавить(НСтр("ru='Группа финансового учета:';uk='Група фінансового обліку:'"));
			СведенияГФУ.Добавить(" ");
			Если ЗначениеЗаполнено(СведенияОбУчете.ГруппаФинансовогоУчета) Тогда
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ГруппаФинансовогоУчета);
			Иначе
				СсылкаНаОбъект = Неопределено;
			КонецЕсли;
			СведенияГФУ.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ГруппаФинансовогоУчетаПредставление,,,, СсылкаНаОбъект));
		
		ИначеЕсли СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.ПринятоКЗабалансовомуУчету
			ИЛИ СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.ПринятоКЗабалансовомуУчету Тогда
			
			Если СведенияОбУчете.ПолученоВАрендуОтДругойОрганизации Тогда
				СведенияПринятКУчету1.Добавить(НСтр("ru='Дата принятия к учету:';uk='Дата прийняття до обліку:'"));
			Иначе
				СведенияПринятКУчету1.Добавить(НСтр("ru='Дата принятия к забалансовому учету:';uk='Дата прийняття до позабалансового обліку:'"));
			КонецЕсли;
			СведенияПринятКУчету1.Добавить(" ");
			ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ, "ДЛФ=D");
			Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ) Тогда
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ);
			Иначе
				СсылкаНаОбъект = Неопределено;
			КонецЕсли;
			СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
			
			//
			Если ЗначениеЗаполнено(СведенияОбУчете.Договор) Тогда
				СведенияДоговор.Добавить(НСтр("ru='По договору:';uk='За договором:'"));
				СведенияДоговор.Добавить(" ");
				Если ЗначениеЗаполнено(СведенияОбУчете.Договор) Тогда
					СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Договор);
				Иначе
					СсылкаНаОбъект = Неопределено;
				КонецЕсли;
				СведенияДоговор.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ДоговорПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 
			
		КонецЕсли; 
		
	Иначе
		СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Принять к учету';uk='Прийняти до обліку'"),,,, "#Создать"));
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Местонахождение
	
	Если СведенияОбУчете <> Неопределено Тогда
		
		Период = СведенияОбУчете.ПериодСведенийОМестонахождении; 
		
		СведенияМестонахождениеОрганизация.Добавить(СтрШаблон(НСтр("ru='Организация: %1';uk='Організація: %1'"), СведенияОбУчете.ОрганизацияПредставление));
		СведенияМестонахождениеПодразделение.Добавить(СтрШаблон(НСтр("ru='Подразделение: %1';uk='Підрозділ: %1'"), СведенияОбУчете.ПодразделениеПредставление));
		
		СведенияМестонахождениеМОЛ.Добавить(НСтр("ru='МОЛ:';uk='МВО:'"));
		СведенияМестонахождениеМОЛ.Добавить(" ");
		Если ЗначениеЗаполнено(СведенияОбУчете.МОЛ) Тогда
			СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.МОЛ);
			СведенияМестонахождениеМОЛ.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.МОЛПредставление,,,, СсылкаНаОбъект));
		ИначеЕсли ВключитьНезаполненные Тогда
			СведенияМестонахождениеМОЛ.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='не указан';uk='не зазначений'"),, ЦветаСтиля.НезаполненноеПолеТаблицы));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СведенияОбУчете.АдресМестонахождения) Тогда
			СведенияМестонахождениеАдрес.Добавить(НСтр("ru='Адрес:';uk='Адреса:'"));
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(СведенияОбУчете.АдресМестонахождения);
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.ПоказатьНаКарте,,,, СведенияОбУчете.АдресМестонахождения));
		ИначеЕсли ВключитьНезаполненные Тогда
			СведенияМестонахождениеАдрес.Добавить(НСтр("ru='Адрес:';uk='Адреса:'"));
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='не указан';uk='не зазначений'"),, ЦветаСтиля.НезаполненноеПолеТаблицы));
		КонецЕсли; 
		
	КонецЕсли; 
	
	#КонецОбласти
	
	#Область Суммы
	
	Если НЕ ДоступенВыборОтраженияВУчетах 
		И ВалютаРегл = ВалютаУпр
		И СведенияОбУчете <> Неопределено Тогда
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Стоимость, НСтр("ru='Восстановительная стоимость:';uk='Відновлювальна вартість:'"), ВалютаУпр);
		СведенияВосстановительнаяСтоимость.Добавить(ПредставлениеСуммы);
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Амортизация, НСтр("ru='Накопленная амортизация:';uk='Накопичена амортизація:'"), ВалютаУпр);
		СведенияНакопленнаяАмортизация.Добавить(ПредставлениеСуммы);
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Стоимость - СтоимостьИАмортизация.Амортизация, НСтр("ru='Остаточная стоимость:';uk='Залишкова вартість:'"), ВалютаУпр);
		СведенияОстаточнаяСтоимость.Добавить(ПредставлениеСуммы);
		
	КонецЕсли;
	
	#КонецОбласти
	
	ПредставлениеСведений = Новый Структура;
	ПредставлениеСведений.Вставить("Период", Период);
	ПредставлениеСведений.Вставить("СведенияПринятКУчету1", СведенияПринятКУчету1);
	ПредставлениеСведений.Вставить("СведенияПринятКУчету2", СведенияПринятКУчету2);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования1", СведенияСрокИспользования1);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования2", СведенияСрокИспользования2);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования3", СведенияСрокИспользования3);
	ПредставлениеСведений.Вставить("СведенияЛиквидационнаяСтоимость", СведенияЛиквидационнаяСтоимость);
	ПредставлениеСведений.Вставить("СведенияСнятСУчета", СведенияСнятСУчета);
	ПредставлениеСведений.Вставить("СведенияГФУ", СведенияГФУ);
	ПредставлениеСведений.Вставить("СведенияДоговор", СведенияДоговор);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеОрганизация", СведенияМестонахождениеОрганизация);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеПодразделение", СведенияМестонахождениеПодразделение);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеМОЛ", СведенияМестонахождениеМОЛ);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеАдрес", СведенияМестонахождениеАдрес);
	ПредставлениеСведений.Вставить("СведенияВосстановительнаяСтоимость", СведенияВосстановительнаяСтоимость);
	ПредставлениеСведений.Вставить("СведенияНакопленнаяАмортизация", СведенияНакопленнаяАмортизация);
	ПредставлениеСведений.Вставить("СведенияОстаточнаяСтоимость", СведенияОстаточнаяСтоимость);
	
	Возврат ПредставлениеСведений;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеЗапросаДляВыбора(Параметры, УстановитьВсеОтборы = Ложь, МассивОбъектов = Неопределено) Экспорт

	ОписаниеЗапросаДляВыбора = ОбъектыЭксплуатацииЛокализация.ОписаниеЗапросаДляВыбора(Параметры, УстановитьВсеОтборы, МассивОбъектов);
	
	Если ОписаниеЗапросаДляВыбора <> Неопределено Тогда
		Возврат ОписаниеЗапросаДляВыбора;
	КонецЕсли;
	
	Если Параметры.Свойство("Контекст") Тогда
		ДоступныеКонтексты = Новый Структура(Параметры.Контекст);
	Иначе
		ДоступныеКонтексты = Новый Структура("УУ"); // По умолчанию доступны все контексты.
	КонецЕсли;
	
	ОтборСписка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отбор", Новый Структура);
	
	Если ОтборСписка.Свойство("ОтражатьВРеглУчете")
		И НЕ ОтборСписка.ОтражатьВРеглУчете
		И ДоступныеКонтексты.Свойство("БУ") Тогда
		ДоступныеКонтексты.Удалить("БУ"); // Доступен выбор отражения в учете и в регл. учете нет отражения.
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("ОтражатьВУпрУчете")
		И НЕ ОтборСписка.ОтражатьВУпрУчете
		И ДоступныеКонтексты.Свойство("УУ") Тогда
		ДоступныеКонтексты.Удалить("УУ"); // Доступен выбор отражения в учете и в упр. учете нет отражения.
	КонецЕсли; 
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		ОтборСписка.Удалить("Подразделение");
		ОтборСписка.Удалить("Организация");
		ОтборСписка.Удалить("МОЛ");
	КонецЕсли;
	
	Если ОтборСписка.Свойство("Организация")
		И НЕ ЗначениеЗаполнено(ОтборСписка.Организация) Тогда
		ОтборСписка.Удалить("Организация");
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("Подразделение")
		И НЕ ЗначениеЗаполнено(ОтборСписка.Подразделение) Тогда
		ОтборСписка.Удалить("Подразделение");
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("МОЛ")
		И НЕ ЗначениеЗаполнено(ОтборСписка.МОЛ) Тогда
		ОтборСписка.Удалить("МОЛ");
	КонецЕсли; 
	
	ДополнительныеПоля = "";
	ТекстОтборы = "";
	ПараметрыЗапроса = Новый Структура;
	ДоступныеПоля = Новый Массив;
	НеобходимыеТаблицы = Новый Структура;
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		
		ПолеОрганизация = "ЕСТЬNULL(МестонахождениеОС.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		ПолеМОЛ = "ЕСТЬNULL(МестонахождениеОС.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))";
		ПолеПодразделение = "ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))";
		ПолеАдресМестонахождения = "ЕСТЬNULL(МестонахождениеОС.АдресМестонахождения, """""""")";
		
		НеобходимыеТаблицы.Вставить("МестонахождениеОС");
		
	Иначе
		
		ПолеОрганизация = "ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
		ПолеМОЛ = "ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)";
		ПолеПодразделение = "ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
		ПолеАдресМестонахождения = """""";
		
	КонецЕсли;
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОС) Тогда
		
		ПолеГФУ = "ЕСТЬNULL(ПорядокУчетаОС.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка))";
		
		НеобходимыеТаблицы.Вставить("ПорядокУчетаОС");
		
	Иначе
		
		ПолеГФУ = "ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка)";
		
	КонецЕсли;
	
	Если (ДоступныеКонтексты.Свойство("БУ") ИЛИ ДоступныеКонтексты.Свойство("УУ"))
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		
		ПолеДатаПринятияКУчетуРегл = "ДАТАВРЕМЯ(1, 1, 1)";
		ПолеДатаПринятияКУчетуУпр = "ЕСТЬNULL(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, ДАТАВРЕМЯ(1, 1, 1))";
		
		НеобходимыеТаблицы.Вставить("ПервоначальныеСведенияОС");
		
	Иначе
		ПолеДатаПринятияКУчетуРегл = "ДАТАВРЕМЯ(1, 1, 1)";
		ПолеДатаПринятияКУчетуУпр = "ДАТАВРЕМЯ(1, 1, 1)";
	КонецЕсли;
	
	Если Параметры.Свойство("РеквизитыКоторыеДолжныСовпадать")
			И СтрНайти(Параметры.РеквизитыКоторыеДолжныСовпадать, "СрокИспользованияУУ") <> 0 
		ИЛИ ОтборСписка.Свойство("СрокИспользованияУУ") Тогда
		
		ПолеСрокИспользованияУУ = "ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0)";
		
		НеобходимыеТаблицы.Вставить("ПараметрыАмортизацииОСУУ");
		
	Иначе
		
		ПолеСрокИспользованияУУ = "0";
		
	КонецЕсли;
	
	ДополнительныеПоля = ДополнительныеПоля + "
		|,%1 КАК Организация
		|,%2 КАК МОЛ
		|,%3 КАК Подразделение
		|,%4 КАК АдресМестонахождения
		|,%5 КАК ГруппаФинансовогоУчета
		|,%6 КАК СрокИспользованияУУ
		|,0 КАК СрокИспользованияБУ
		|,0 КАК СрокИспользованияНУ
		|,НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС
		|,%7 КАК ДатаПринятияКУчетуРегл
		|,%8 КАК ДатаПринятияКУчетуУпр";
	
	ДополнительныеПоля = СтрШаблон(
		ДополнительныеПоля, 
		ПолеОрганизация, 
		ПолеМОЛ, 
		ПолеПодразделение, 
		ПолеАдресМестонахождения, 
		ПолеГФУ,
		ПолеСрокИспользованияУУ,
		ПолеДатаПринятияКУчетуРегл,
		ПолеДатаПринятияКУчетуУпр);
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		ДополнительныеПоля = ДополнительныеПоля + "
			|,ВЫБОР 
			|	КОГДА НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа 
			|		ТОГДА ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету))
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка)
			|	КОНЕЦ КАК СостояниеУУ";
		
		НеобходимыеТаблицы.Вставить("ПорядокУчетаОСУУ");
	Иначе
		
		ДополнительныеПоля = ДополнительныеПоля + ",ВЫБОР 
			|	КОГДА НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа 
			|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка)
			|	КОНЕЦ КАК СостояниеУУ";
		
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		Если ОтборСписка.Свойство("Состояние") Тогда
				
			Если ДоступныеКонтексты.Свойство("УУ") Тогда
				ТекстОтборы = "
				|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) В(&Состояние)";
			КонецЕсли;
			
			Если ТипЗнч(ОтборСписка.Состояние) = Тип("ФиксированныйМассив") Тогда
				ПараметрыЗапроса.Вставить("Состояние", Новый Массив(ОтборСписка.Состояние));
			Иначе
				ПараметрыЗапроса.Вставить("Состояние", ОтборСписка.Состояние);
			КонецЕсли;
			
			Параметры.Отбор.Удалить("Состояние");
			
		Иначе
			
			Если ДоступныеКонтексты.Свойство("УУ") Тогда
				ТекстОтборы = "
				|	(ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) = &Состояние
				|		ИЛИ &Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка))";
			КонецЕсли;
			
			Если ТекстОтборы <> "" Тогда
				ПараметрыЗапроса.Вставить("Состояние", Перечисления.СостоянияОС.ПустаяСсылка());
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Если УстановитьВсеОтборы Тогда
		
		Для каждого КлючИЗначение Из ОтборСписка Цикл
			
			Если НЕ ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если КлючИЗначение.Ключ = "Организация" Тогда
				ПутьКПолю = ПолеОрганизация;
			ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
				ПутьКПолю = ПолеПодразделение;
			ИначеЕсли КлючИЗначение.Ключ = "МОЛ" Тогда
				ПутьКПолю = ПолеМОЛ;
			ИначеЕсли КлючИЗначение.Ключ = "СрокИспользованияУУ" Тогда
				ПутьКПолю = ПолеСрокИспользованияУУ;
			ИначеЕсли КлючИЗначение.Ключ = "НаправлениеДеятельности" Тогда
				ПутьКПолю = "ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))";
			ИначеЕсли КлючИЗначение.Ключ = "Статус" Тогда
				ПутьКПолю = "СправочникОбъектыЭксплуатации." + КлючИЗначение.Ключ;
			Иначе
				Продолжить;
			КонецЕсли;
			
			ЭтоМассив = ТипЗнч(КлючИЗначение.Значение) = Тип("ФиксированныйМассив") 
						ИЛИ ТипЗнч(КлючИЗначение.Значение) = Тип("Массив");
						
			ТекстОтборы = ТекстОтборы 
							+ Символы.ПС 
							+ ?(ТекстОтборы <> "", "И ","")
							+ ПутьКПолю 
							+ ?(ЭтоМассив, " В (&" + КлючИЗначение.Ключ + ")", " = &" + КлючИЗначение.Ключ);
							
			ПараметрыЗапроса.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла; 
		
	КонецЕсли; 
	
	ТекстСоединения = "";
	
	Если НеобходимыеТаблицы.Свойство("МестонахождениеОС")
		ИЛИ НеобходимыеТаблицы.Свойство("ПервоначальныеСведенияОС")
		ИЛИ НеобходимыеТаблицы.Свойство("ПорядокУчетаОСУУ") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//) КАК МестонахождениеОС
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = МестонахождениеОС.ОсновноеСредство";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПервоначальныеСведенияОС") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Организация = МестонахождениеОС.Организация
		|			И ПервоначальныеСведенияОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПорядокУчетаОСУУ") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//) КАК ПорядокУчетаОСУУ
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПорядокУчетаОСУУ.ОсновноеСредство
		|			И ПорядокУчетаОСУУ.Организация = МестонахождениеОС.Организация";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПараметрыАмортизацииОСУУ") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//) КАК ПараметрыАмортизацииОСУУ
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПараметрыАмортизацииОСУУ.ОсновноеСредство
		|			И ПараметрыАмортизацииОСУУ.Организация = МестонахождениеОС.Организация";
	КонецЕсли;
	
	ТекстПервые = "";
	Если Параметры.Свойство("СтрокаПоиска") Тогда
		ТекстОтборы = ТекстОтборы + "
		|" + ?(ТекстОтборы <> "", "И ","")
		+ "(СправочникОбъектыЭксплуатации.Наименование ПОДОБНО &СтрокаПоиска
			|			ИЛИ СправочникОбъектыЭксплуатации.ИнвентарныйНомер ПОДОБНО &СтрокаПоиска)
			|	И НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа
			|	И НЕ СправочникОбъектыЭксплуатации.ПометкаУдаления";
		ТекстПервые = "РАЗРЕШЕННЫЕ ПЕРВЫЕ 10";
		ПараметрыЗапроса.Вставить("СтрокаПоиска", "%" + Параметры.СтрокаПоиска + "%");
	КонецЕсли;
	
	Если МассивОбъектов <> Неопределено Тогда
		ТекстОтборы = ТекстОтборы + "
		|" + ?(ТекстОтборы <> "", "И ","")
		+ "СправочникОбъектыЭксплуатации.Ссылка В(&МассивОбъектов)
			|	И НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа
			|	И НЕ СправочникОбъектыЭксплуатации.ПометкаУдаления";
		ПараметрыЗапроса.Вставить("МассивОбъектов", МассивОбъектов);
	КонецЕсли; 
	
	Если ТекстОтборы <> "" Тогда
		ТекстОтборы = "
		|ГДЕ
		|" + ТекстОтборы;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ //ПЕРВЫЕ//
	|	СправочникОбъектыЭксплуатации.Ссылка,
	|	СправочникОбъектыЭксплуатации.ПометкаУдаления,
	|	СправочникОбъектыЭксплуатации.Родитель,
	|	СправочникОбъектыЭксплуатации.ЭтоГруппа,
	|	СправочникОбъектыЭксплуатации.Код,
	|	СправочникОбъектыЭксплуатации.Наименование,
	|	СправочникОбъектыЭксплуатации.ИнвентарныйНомер,
	|	СправочникОбъектыЭксплуатации.ГруппаОСМеждународныйУчет,
	|	СправочникОбъектыЭксплуатации.ДатаВыпуска,
	|	СправочникОбъектыЭксплуатации.ЗаводскойНомер,
	|	СправочникОбъектыЭксплуатации.Изготовитель,
	|	СправочникОбъектыЭксплуатации.Класс,
	|	СправочникОбъектыЭксплуатации.Модель,
	|	СправочникОбъектыЭксплуатации.НаименованиеПолное,
	|	СправочникОбъектыЭксплуатации.НомерПаспорта,
	|	СправочникОбъектыЭксплуатации.ОбъектБытовогоНазначения,
	|	СправочникОбъектыЭксплуатации.Подкласс,
	|	СправочникОбъектыЭксплуатации.Расположение,
	|	СправочникОбъектыЭксплуатации.РемонтирующееПодразделение,
	|	СправочникОбъектыЭксплуатации.СерийныйНомер,
	|	СправочникОбъектыЭксплуатации.Статус,
	|	ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СправочникОбъектыЭксплуатации.Комментарий
	|	//ДОПОЛНИТЕЛЬНЫЕ_ПОЛЯ//
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК СправочникОбъектыЭксплуатации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//) КАК ПорядокУчетаОС
	|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПорядокУчетаОС.ОсновноеСредство
	|	//СОЕДИНЕНИЯ//
	|	//ОТБОРЫ//";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДОПОЛНИТЕЛЬНЫЕ_ПОЛЯ//", ДополнительныеПоля);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СОЕДИНЕНИЯ//", ТекстСоединения);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ОТБОРЫ//", ТекстОтборы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПЕРВЫЕ//", ТекстПервые);

	ПараметрыСрезаПоследних = "";
	Если Параметры.Свойство("ДатаСведений") Тогда
		ПараметрыСрезаПоследних = "&ДатаСведений";
		ПараметрыЗапроса.Вставить("ДатаСведений", КонецДня(Параметры.ДатаСведений));
	КонецЕсли; 
	Если Параметры.Свойство("ТекущийРегистратор") Тогда
		ПараметрыСрезаПоследних = ПараметрыСрезаПоследних 
			+ ",Регистратор <> &ТекущийРегистратор";
		ПараметрыЗапроса.Вставить("ТекущийРегистратор", Параметры.ТекущийРегистратор);
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ//", ПараметрыСрезаПоследних);
	
	ОписаниеЗапросаДляВыбора = Новый Структура("ТекстЗапроса,ПараметрыЗапроса,ДоступныеПоля", ТекстЗапроса, ПараметрыЗапроса, ДоступныеПоля);

	
	Возврат ОписаниеЗапросаДляВыбора;
	
КонецФункции

Функция ИсторияИзмененияРеквизита(ОсновноеСредство, ИмяРеквизита) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыПоОС.Дата КАК Дата,
	|	ДокументыПоОС.Ссылка КАК Документ
	|ИЗ
	|	РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4 КАК ИзменениеПараметровОС
	|		ПО (ИзменениеПараметровОС.Ссылка = ДокументыПоОС.Ссылка)
	|ГДЕ
	|	ДокументыПоОС.ОсновноеСредство = &ОсновноеСредство
	|	И ДокументыПоОС.ТипСсылки = &ТипСсылки
	|	И ДокументыПоОС.НомерЗаписи = 0
	|	И ДокументыПоОС.Проведен
	|	И НЕ ДокументыПоОС.ДополнительнаяЗапись
	|	И (ИзменениеПараметровОС.НаправлениеДеятельностиФлаг
	|				И &ИмяРеквизита = ""НаправлениеДеятельности"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыПоОС.Дата";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр("ИмяРеквизита", ИмяРеквизита);
	Запрос.УстановитьПараметр("ТипСсылки", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.ИзменениеПараметровОС2_4")));
	
	УстановитьПривилегированныйРежим(Истина);
	ИсторияИзменений = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);

	Возврат ИсторияИзменений;
	
КонецФункции
 
//++ НЕ УТКА

#Область ПроверкаЗаполненияОбъектовЭксплуатации

Процедура СформироватьЗапросПроверкиЗаполнения(ТекстЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОбъектыЭксплуатации.*
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
		|ГДЕ
		|	ОбъектыЭксплуатации.Ссылка В (&ОбъектыЭксплуатации)";
	
КонецПроцедуры

Процедура СформироватьЗапросПроверкиЗаполненияУзлов(ТекстЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	УзлыОбъектовЭксплуатации.*
		|ИЗ
		|	Справочник.УзлыОбъектовЭксплуатации КАК УзлыОбъектовЭксплуатации
		|ГДЕ
		|	УзлыОбъектовЭксплуатации.Владелец В(&ОбъектыЭксплуатации)
		|	И УзлыОбъектовЭксплуатации.Владелец <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
		|	И НЕ УзлыОбъектовЭксплуатации.ПометкаУдаления
		|	И УзлыОбъектовЭксплуатации.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации), ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ПустаяСсылка))";
	
КонецПроцедуры

Функция ПолучитьОбязательныеРеквизиты(Класс, КэшированныеЗначения)
	
	ОбязательныеРеквизиты = КэшированныеЗначения.ОбязательныеРеквизиты.Получить(Класс);
	
	Если ОбязательныеРеквизиты = Неопределено Тогда
		
		Запрос = Новый Запрос;
		
		Если ЗначениеЗаполнено(Класс) Тогда
			Запрос.Текст =
				"ВЫБРАТЬ
				|	РеквизитыДляКонтроляОбъектов.Имя,
				|	РеквизитыДляКонтроляОбъектов.Синоним
				|ИЗ
				|	Справочник.КлассыОбъектовЭксплуатации.РеквизитыДляКонтроля КАК Реквизиты
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыОбъектов КАК РеквизитыДляКонтроляОбъектов
				|		ПО Реквизиты.ИмяРеквизита = РеквизитыДляКонтроляОбъектов.Имя
				|ГДЕ
				|	Реквизиты.Ссылка = &Класс
				|	И Реквизиты.ОбязателенДляЗаполнения";
		Иначе
			Запрос.Текст =
				"ВЫБРАТЬ
				|	Реквизиты.Имя,
				|	Реквизиты.Синоним
				|ИЗ
				|	РеквизитыОбъектов КАК Реквизиты
				|ГДЕ
				|	Реквизиты.ОбязателенДляЗаполнения";
			
		КонецЕсли;
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ОбязательныеРеквизиты = Запрос.Выполнить().Выгрузить();
		
		КэшированныеЗначения.ОбязательныеРеквизиты.Вставить(Класс, ОбязательныеРеквизиты);
		
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

Функция ПолучитьОбязательныеДопРеквизиты(Класс, КэшированныеЗначения)
	
	ОбязательныеДопРеквизиты = КэшированныеЗначения.ОбязательныеДопРеквизиты.Получить(Класс);
	
	Если ОбязательныеДопРеквизиты = Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопРеквизиты.Свойство
			|ИЗ
			|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДопРеквизиты
			|ГДЕ
			|	ДопРеквизиты.Ссылка = ВЫРАЗИТЬ(&Класс КАК Справочник.КлассыОбъектовЭксплуатации).НаборСвойств
			|	И (ДопРеквизиты.Свойство.ЗаполнятьОбязательно)
			|	И НЕ ДопРеквизиты.Свойство В
			|				(ВЫБРАТЬ
			|					ОбщиеДопРеквизитыОбъектов.Свойство
			|				ИЗ
			|					ОбщиеДопРеквизитыОбъектов КАК ОбщиеДопРеквизитыОбъектов)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопРеквизиты.Свойство
			|ИЗ
			|	ОбщиеДопРеквизитыОбъектов КАК ДопРеквизиты
			|ГДЕ
			|	ДопРеквизиты.ЗаполнятьОбязательно");
		
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ОбязательныеДопРеквизиты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Свойство");
		
		КэшированныеЗначения.ОбязательныеДопРеквизиты.Вставить(Класс, ОбязательныеДопРеквизиты);
		
	КонецЕсли;
	
	Возврат ОбязательныеДопРеквизиты;
	
КонецФункции

Функция ПолучитьПараметрыУчетаНаработок(Класс, КэшированныеЗначения)
	
	ПараметрыУчетаНаработок = КэшированныеЗначения.ПараметрыУчетаНаработок.Получить(Класс);
	
	Если ПараметрыУчетаНаработок = Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Параметры.ПоказательНаработки КАК ПоказательНаработки,
			|	Параметры.РассчитыватьОстаточныйРесурс КАК ЗаполнятьНазначенныйРесурс,
			|	Параметры.РегистрироватьОтИсточника КАК ЗаполнятьИсточник
			|ИЗ
			|	Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК Параметры
			|ГДЕ
			|	Параметры.Ссылка = &Класс
			|	И (Параметры.РегистрироватьОтИсточника
			|			ИЛИ Параметры.РассчитыватьОстаточныйРесурс)");
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ПараметрыУчетаНаработок = Запрос.Выполнить().Выгрузить();
		
		КэшированныеЗначения.ПараметрыУчетаНаработок.Вставить(Класс, ПараметрыУчетаНаработок);
		
	КонецЕсли;
	
	Возврат ПараметрыУчетаНаработок;
	
КонецФункции

Процедура ПроверитьОбъектЭксплуатации(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	ПроверитьРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьДопРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьРабочиеЦентры(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьПараметрыУчетаНаработок(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ПроверитьРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	ОбязательныеРеквизиты = ПолучитьОбязательныеРеквизиты(ОбъектПроверки.Класс, КэшированныеЗначения);
	
	Для Каждого Реквизит Из ОбязательныеРеквизиты Цикл
		
		Если Не ЗначениеЗаполнено(ОбъектПроверки[СокрЛП(Реквизит.Имя)]) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Поле ""%1"" не заполнено.';uk='Поле ""%1"" не заповнене.'"),
				СокрЛП(Реквизит.Синоним));
			
			ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
				ПараметрыПроверки.ПотокОшибок,
				ОбъектПроверки.Ссылка,
				"Объект."+Реквизит.Имя,
				ТекстОшибки,
				"Реквизиты",
				0,
				ТекстОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьДопРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	Если ПараметрыПроверки.Форма = Неопределено Тогда
		
		ОбязательныеДопРеквизиты = ПолучитьОбязательныеДопРеквизиты(ОбъектПроверки.Класс, КэшированныеЗначения);
		
		ТаблицаДопРеквизитов = ОбъектПроверки.ДополнительныеРеквизиты.Выгрузить();
		
		Для Каждого Свойство Из ОбязательныеДопРеквизиты Цикл
			
			Если ТаблицаДопРеквизитов.Найти(Свойство, "Свойство") = Неопределено Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Поле ""%1"" не заполнено.';uk='Поле ""%1"" не заповнене.'"),
					Свойство);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ГруппаДополнительныеРеквизиты",
					ТекстОшибки,
					"ДопРеквизиты",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если Не (ПараметрыПроверки.Форма.Свойства_ИспользоватьСвойства И ПараметрыПроверки.Форма.Свойства_ИспользоватьДопРеквизиты) Тогда
			Возврат;
		КонецЕсли;
		
		ТаблицаДопРеквизитов = ПараметрыПроверки.Форма.Свойства_ОписаниеДополнительныхРеквизитов;
		
		Для Каждого Строка Из ТаблицаДопРеквизитов Цикл
			Если Строка.ЗаполнятьОбязательно Тогда
				
				Если НЕ ЗначениеЗаполнено(ПараметрыПроверки.Форма[Строка.ИмяРеквизитаЗначение]) Тогда
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Поле ""%1"" не заполнено.';uk='Поле ""%1"" не заповнене.'"),
						Строка.Наименование);
					
					ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
						ПараметрыПроверки.ПотокОшибок,
						ОбъектПроверки.Ссылка,
						Строка.ИмяРеквизитаЗначение,
						ТекстОшибки,
						"ДопРеквизиты",
						0,
						ТекстОшибки);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРабочиеЦентры(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	РабочиеЦентры = ОбъектПроверки.РабочиеЦентры.Выгрузить();
	Для ТекИндекс=0 По РабочиеЦентры.Количество()-1 Цикл
		
		Строка = РабочиеЦентры[ТекИндекс];
		
		Если Не ЗначениеЗаполнено(Строка["РабочийЦентр"]) Тогда
			
			ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
				ПараметрыПроверки.ПотокОшибок,
				ОбъектПроверки.Ссылка,
				"Объект.РабочиеЦентры[%1].РабочийЦентр",
				НСтр("ru='Не заполнено поле ""Рабочий центр""';uk='Не заповнено поле ""Робочий центр""'"),
				"РабочиеЦентры",
				ТекИндекс,
				НСтр("ru='Не заполнена колонка ""Рабочий центр"" в строке %1 списка ""Рабочие центры""';uk='Не заповнена колонка ""Робочий центр"" в рядку %1 списку ""Робочі центри""'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПараметрыУчетаНаработок(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	Если ПараметрыПроверки.Форма = Неопределено Тогда
		
		ПараметрыУчетаНаработки = ПолучитьПараметрыУчетаНаработок(ОбъектПроверки.Класс, КэшированныеЗначения);
		
		ТаблицаПараметров = ОбъектПроверки.ПараметрыУчетаНаработок.Выгрузить(); 
		
		Для Каждого НастройкаПоказателя Из ПараметрыУчетаНаработки Цикл
			
			Строка = ТаблицаПараметров.Найти(НастройкаПоказателя.ПоказательНаработки, "ПоказательНаработки");
			
			Если Строка = Неопределено Или (НастройкаПоказателя.ЗаполнятьНазначенныйРесурс И Не ЗначениеЗаполнено(Строка.НазначенныйРесурс)) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Назначенный ресурс показателя наработки ""%1"" не заполнен';uk='Призначений ресурс показника напрацювання ""%1"" не заповнений'"),
					НастройкаПоказателя.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
			Если Строка = Неопределено Или (НастройкаПоказателя.ЗаполнятьИсточник И Не ЗначениеЗаполнено(Строка.ИсточникПараметра)) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Источник текущих значений показателя наработки ""%1"" не заполнен';uk='Джерело поточних значень показника напрацювання ""%1"" не заповнений'"),
					НастройкаПоказателя.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если ПараметрыПроверки.Форма.ПараметрыУчетаНаработок.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		
		Для ТекИндекс=0 По ПараметрыПроверки.Форма.ПараметрыУчетаНаработок.Количество()-1 Цикл
			
			Строка = ПараметрыПроверки.Форма.ПараметрыУчетаНаработок[ТекИндекс];
			
			Если Строка.ЗаполнятьНазначенныйРесурс И Не ЗначениеЗаполнено(Строка.НазначенныйРесурс) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Назначенный ресурс показателя наработки ""%1"" не заполнен';uk='Призначений ресурс показника напрацювання ""%1"" не заповнений'"),
					Строка.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок[%1].НазначенныйРесурс",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					ТекИндекс,
					ТекстОшибки);
				
			КонецЕсли;
			Если Строка.ЗаполнятьИсточник И Не ЗначениеЗаполнено(Строка.ИсточникПараметра) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Источник текущих значений показателя наработки ""%1"" не заполнен';uk='Джерело поточних значень показника напрацювання ""%1"" не заповнений'"),
					Строка.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок[%1].ИсточникПараметра",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					ТекИндекс,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТКА

#Область ПодключаемыеКоманды

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Изменение", Метаданные.Справочники.ОбъектыЭксплуатации) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКарточкиОС";
		КомандаПечати.Идентификатор = "ИнвКарточкаБюдж";
		КомандаПечати.Представление = НСтр("ru=""Инвентарная карточка учета объекта основных средств"";uk=""Інвентарна картка обліку об’єкта основних засобів""");
		
	КонецЕсли;         

	Если ПравоДоступа("Просмотр", Метаданные.Обработки.ПечатьЭтикетокОбъектовЭксплуатации) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ВнеоборотныеАктивыКлиент.ПечатьЭтикетокОбъектовЭксплуатации";
		КомандаПечати.Идентификатор = "ЭтикеткаОбъектовЭксплуатации";
		КомандаПечати.Представление = НСтр("ru='Этикетки';uk='Етикетки'");
		
	КонецЕсли;
	
	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = Документы.ПринятиеКУчетуОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.МодернизацияОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПереоценкаОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПеремещениеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПодготовкаКПередачеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.СписаниеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.НаработкаОбъектовЭксплуатации.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.РазукомплектацияОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ОбъединениеОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
		Команда.МножественныйВыбор = Истина;
	КонецЕсли;
	
	Команда = Документы.КорректировкаСтоимостиИАмортизацииОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
		Команда.МножественныйВыбор = Истина;
	КонецЕсли;
	
	//++ НЕ УТКА
	Команда = Документы.ЗаказНаРемонт.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.УстановкаЗначенийНаработки.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	//-- НЕ УТКА
	
	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область Печать  

Функция ПечатьИнвКарточка_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.ОбъектыЭксплуатации.ПФ_MXL_ИнвКарточкаБюдж_2016");
	Шапка						= Макет.ПолучитьОбласть("Шапка");
	СтрокаХарактеристики	 	= Макет.ПолучитьОбласть("ХарактеристикиСтрока");
	ШапкаИзмененияСтоимости		= Макет.ПолучитьОбласть("ИзмененияСтоимостиШапка");
	СтрокаИзмененияСтоимости	= Макет.ПолучитьОбласть("ИзмененияСтоимостиСтрока");
	Подвал						= Макет.ПолучитьОбласть("Подвал");
	Шапка2						= Макет.ПолучитьОбласть("Шапка2");
	СтрокаАмортизации	 		= Макет.ПолучитьОбласть("АмортизацииСтрока");
	ШапкаДвиженияОбъекта		= Макет.ПолучитьОбласть("ДвиженияОбъектаШапка");
	ДвиженияОбъектаСтрока	 	= Макет.ПолучитьОбласть("ДвиженияОбъектаСтрока");
	Подвал2						= Макет.ПолучитьОбласть("Подвал2");

	ТабДок = Новый ТабличныйДокумент();
	Организация      = Неопределено;
	
	Если ПараметрыПечати.Свойство("ДатаСведений") Тогда
		ДатаСведений = ПараметрыПечати.ДатаСведений;
	Иначе
		ДатаСведений = ТекущаяДатаСеанса();
	КонецЕсли;
	
	cВидаУчета   = "Бухгалтерський";
	ВалютаПечати = Константы.ВалютаРегламентированногоУчета.Получить().Наименование;
	ВидУчета 	 = "Бух";
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОсновноеСредство", Ссылка);
		Запрос.УстановитьПараметр("ДатаСведений"    , ДатаСведений); 
		СобытияВвода = Новый СписокЗначений;    
		СобытияВвода.Добавить(Перечисления.ВидыСобытийОС.ПринятиеКУчету);
		СобытияВвода.Добавить(Перечисления.ВидыСобытийОС.ПринятиеКУчетуСВводомВЭксплуатацию);
		СобытияВвода.Добавить(Перечисления.ВидыСобытийОС.ВводВЭксплуатацию);
		Запрос.УстановитьПараметр("СобытияВвода"  	, СобытияВвода);
		Запрос.УстановитьПараметр("СобытиеВыбытия"	, Перечисления.ВидыСобытийОС.Списание);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПервоначальныеСведенияОС.Организация КАК Организация,
		|	ПервоначальныеСведенияОС.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ПервоначальныеСведенияОС.Организация.Наименование КАК ОрганизацияНаименование,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеПолное,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.Наименование КАК Наименование,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.Модель КАК Модель,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.Изготовитель КАК ИзготовительОС,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
		|	ПервоначальныеСведенияОС.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	ВводВЭксплуатацию.Период КАК ДокументВводаВЭксплуатациюДата,
		|	ВводВЭксплуатацию.НомерДокумента КАК ДокументВводаВЭксплуатациюНомер,
		|	ПервоначальныеСведенияОС.Организация.КодПоЕДРПОУ КАК ЕДРПОУ,
		|	ПРЕДСТАВЛЕНИЕ(ПеремещенияОС.Местонахождение) КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ПеремещенияОС.СчетУчета) КАК СчетУчета,
		|	ПРЕДСТАВЛЕНИЕ(ПеремещенияОС.СчетНачисленияАмортизации) КАК СчетАмортизации,
		|	ПеремещенияОС.ДатаВыбытия КАК ДокументВыбытияДата,
		|	ПеремещенияОС.НомерДокумента КАК ДокументВыбытияНомер,
		|	ПеремещенияОС.Период КАК Период,
		|	ПеремещенияОС.МОЛ КАК МОЛ,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ КАК СрокИспользования,
		|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляАмортизации
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&ДатаСведений, ОсновноеСредство = &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК ВводВЭксплуатацию
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ВводВЭксплуатацию.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.Организация = ВводВЭксплуатацию.Организация
		|			И (ВводВЭксплуатацию.Событие.ВидСобытияОС В (&СобытияВвода))
		|			И (ВводВЭксплуатацию.Период <= &ДатаСведений)
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Перемещения.Организация КАК Организация,
		|			Перемещения.ОсновноеСредство КАК ОсновноеСредство,
		|			Перемещения.Местонахождение КАК Местонахождение,
		|			Перемещения.Период КАК Период,
		|			ЕСТЬNULL(Перемещения.ДатаВыбытия, СостоянияОС.Период) КАК ДатаВыбытия,
		|			МестонахождениеОС.Регистратор.Номер КАК НомерДокумента,
		|			СчетаУчетаОС.СчетУчета КАК СчетУчета,
		|			СчетаУчетаОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|			Перемещения.МОЛ КАК МОЛ
		|		ИЗ
		|			(ВЫБРАТЬ
		|				МестонахождениеОС.Организация КАК Организация,
		|				МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
		|				МестонахождениеОС.Местонахождение КАК Местонахождение,
		|				МестонахождениеОС.Период КАК Период,
		|				МИНИМУМ(МестонахождениеОС1.Период) КАК ДатаВыбытия,
		|				МАКСИМУМ(СчетаУчетаОС.Период) КАК ДатаСчета,
		|				МестонахождениеОС.МОЛ КАК МОЛ
		|			ИЗ
		|				РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОС1
		|					ПО МестонахождениеОС.Период < МестонахождениеОС1.Период
		|						И МестонахождениеОС.Организация = МестонахождениеОС1.Организация
		|						И МестонахождениеОС.ОсновноеСредство = МестонахождениеОС1.ОсновноеСредство
		|						И (МестонахождениеОС1.Период <= &ДатаСведений)
		|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК СчетаУчетаОС
		|					ПО МестонахождениеОС.ОсновноеСредство = СчетаУчетаОС.ОсновноеСредство
		|						И МестонахождениеОС.Период >= СчетаУчетаОС.Период
		|						И (СчетаУчетаОС.Период <= &ДатаСведений)
		|			ГДЕ
		|				МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
		|				И МестонахождениеОС.Период <= &ДатаСведений
		|			
		|			СГРУППИРОВАТЬ ПО
		|				МестонахождениеОС.Местонахождение,
		|				МестонахождениеОС.Период,
		|				МестонахождениеОС.Организация,
		|				МестонахождениеОС.ОсновноеСредство,
		|				МестонахождениеОС.МОЛ) КАК Перемещения
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|				ПО Перемещения.ДатаВыбытия = МестонахождениеОС.Период
		|					И Перемещения.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|					И Перемещения.Организация = МестонахождениеОС.Организация
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК СостоянияОС
		|				ПО Перемещения.ОсновноеСредство = СостоянияОС.ОсновноеСредство
		|					И Перемещения.Организация = СостоянияОС.Организация
		|					И (Перемещения.ДатаВыбытия ЕСТЬ NULL)
		|					И (СостоянияОС.Событие = &СобытиеВыбытия)
		|					И (СостоянияОС.Период <= &ДатаСведений)
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК СчетаУчетаОС
		|				ПО Перемещения.ОсновноеСредство = СчетаУчетаОС.ОсновноеСредство
		|					И Перемещения.ДатаСчета = СчетаУчетаОС.Период) КАК ПеремещенияОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ПеремещенияОС.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.Организация = ПеремещенияОС.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ПараметрыАмортизацииОСБУ.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.Организация = ПараметрыАмортизацииОСБУ.Организация
		|			И (ПараметрыАмортизацииОСБУ.Период <= &ДатаСведений)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПеремещенияОС.Период";
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() тогда
			
			СведенияОбОС = Результат.Выбрать();
			ПерваяСтрока = Истина;
			
			Пока СведенияОбОС.Следующий() Цикл
			
				Если ПерваяСтрока Тогда     
					
					Шапка.Параметры.Заполнить(СведенияОбОС);
					Шапка.Параметры.НаименованиеОС          = ? (НЕ ЗначениеЗаполнено(СведенияОбОС.НаименованиеПолное),
																СведенияОбОС.Наименование, СведенияОбОС.НаименованиеПолное);
					Шапка.Параметры.ОрганизацияНаименование = ? (НЕ ЗначениеЗаполнено(СведенияОбОС.ОрганизацияНаименованиеПолное),
																СведенияОбОС.ОрганизацияНаименование, СведенияОбОС.ОрганизацияНаименованиеПолное);
					Шапка.Параметры.ИнвентарныйНомер 		= СведенияОбОС.ИнвентарныйНомер;
					Шапка.Параметры.Модель	 				= СведенияОбОС.НомерПаспорта + ?(ЗначениеЗаполнено(СведенияОбОС.НомерПаспорта),", ","")
																+ СведенияОбОС.Модель;
					Шапка.Параметры.ГодВыпуска 				= Формат(СведенияОбОС.ДатаВыпуска, "ДФ=yyyy");
					ДатаВвода                               = Формат(СведенияОбОС.ДокументВводаВЭксплуатациюДата, "ДФ=dd.MM.yyyy");
					Шапка.Параметры.АктВвода 				= ДатаВвода + ", № " + СведенияОбОС.ДокументВводаВЭксплуатациюНомер;
					Шапка.Параметры.ЛиквидационнаяСтоимость	= Формат(СведенияОбОС.ЛиквидационнаяСтоимость, "ЧЦ=15; ЧДЦ=2; ЧН=");
					Организация                             = СведенияОбОС.Организация;    
					ПерваяСтрока = Ложь;
					
					ТабДок.Вывести(Шапка);       
					
				КонецЕсли;	
			
			КонецЦикла;
			
		Иначе
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='На момент печати основное средство %1 не принималось к учету.
                                                                                           |Нельзя сформировать инвентарную карточку объекта!'
                                                                                           |;uk='На момент друку основний засіб %1 не приймався до обліку.
                                                                                           |Не можна сформувати інвентарну картку об''єкта!'"), Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Продолжить;
			
		КонецЕсли;
		
		//характеристики    
		ТабДок.Вывести(СтрокаХарактеристики);
		
		ТабДок.Вывести(ШапкаИзмененияСтоимости);		

		// Модернизация ОС и ремонт		
		спВидОперации = Новый СписокЗначений; 
		спВидОперации.Добавить(Перечисления.ВидыСобытийОС.Модернизация);
		спВидОперации.Добавить(Перечисления.ВидыСобытийОС.Ремонт);
		Запрос.УстановитьПараметр("ВидОперации", спВидОперации);
		Запрос.УстановитьПараметр("Организация"        , Организация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ОперацииОС.СуммаЗатратБУ) КАК СуммаЗатрат,
		|	ОперацииОС.Регистратор КАК Регистратор,
		|	ОперацииОС.Период КАК Период,
		|	ОперацииОС.Событие КАК Операция,
		|	ОперацииОС.НомерДокумента КАК НомерДокумента,
		|	ОперацииОС.Событие.ВидСобытияОС КАК ВидСобытияОС,
		|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	МАКСИМУМ(ПараметрыАмортизацииОСБУ.Период) КАК ПериодПараметры,
		|	ОперацииОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК ОперацииОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ОперацииОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|			И ОперацииОС.Организация = ПервоначальныеСведенияОС.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО ОперацииОС.ОсновноеСредство = ПараметрыАмортизацииОСБУ.ОсновноеСредство
		|			И ОперацииОС.Период >= ПараметрыАмортизацииОСБУ.Период
		|			И ОперацииОС.Организация = ПараметрыАмортизацииОСБУ.Организация
		|ГДЕ
		|	ОперацииОС.Период < &ДатаСведений
		|	И ОперацииОС.ОсновноеСредство = &ОсновноеСредство
		|	И ОперацииОС.Организация = &Организация
		|	И (ОперацииОС.Регистратор ССЫЛКА Документ.РемонтОС
		|			ИЛИ ОперацииОС.Регистратор ССЫЛКА Документ.МодернизацияОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОперацииОС.Период,
		|	ОперацииОС.Регистратор,
		|	ОперацииОС.Событие,
		|	ОперацииОС.НомерДокумента,
		|	ОперацииОС.Событие.ВидСобытияОС,
		|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ,
		|	ОперацииОС.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Данные.СуммаЗатрат КАК СуммаЗатрат,
		|	Данные.Регистратор КАК Регистратор,
		|	Данные.Период КАК Период,
		|	Данные.Операция КАК Операция,
		|	Данные.НомерДокумента КАК НомерДокумента,
		|	Данные.ВидСобытияОС КАК ВидСобытияОС,
		|	Данные.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	Данные.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляАмортизации,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ КАК СрокИспользования
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО Данные.ОсновноеСредство = ПараметрыАмортизацииОСБУ.ОсновноеСредство
		|			И Данные.ПериодПараметры = ПараметрыАмортизацииОСБУ.Период";
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		НПП = 1;
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			СтрокаИзмененияСтоимости.Параметры.Заполнить(Выборка);
			СуммаМодернизаций = Выборка.СуммаЗатрат;
			СтрокаИзмененияСтоимости.Параметры.Дата 				    = Формат(Выборка.Период, "ДФ=dd.MM.yyyy");
			СтрокаИзмененияСтоимости.Параметры.СодержаниеОперации       = Выборка.ВидСобытияОС;     
			СтрокаИзмененияСтоимости.Параметры.ЛиквидационнаяСтоимость	= Формат(Выборка.ЛиквидационнаяСтоимость, "ЧЦ=15; ЧДЦ=2; ЧН=");
			СтрокаИзмененияСтоимости.Параметры.Дата			 			= Формат(Выборка.Период, "ДФ=dd.MM.yyyy");
			СтрокаИзмененияСтоимости.Параметры.СодержаниеОперации  		= Выборка.Операция;
			СтрокаИзмененияСтоимости.Параметры.ПервоначальнаяСтоимость	= СуммаМодернизаций;
			СтрокаИзмененияСтоимости.Параметры.НПП 						= НПП;
			ТабДок.Вывести(СтрокаИзмененияСтоимости);
			НПП = НПП +1;
			
		КонецЦикла;
		
		ТабДок.Вывести(Подвал);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();   
		
		//списание  
		ЗапросСписание = Новый Запрос;
		ЗапросСписание.УстановитьПараметр("ОсновноеСредство", Ссылка);
		ЗапросСписание.УстановитьПараметр("ДатаСведений" , ДатаСведений);
		ЗапросСписание.УстановитьПараметр("Организация", Организация);
		ЗапросСписание.УстановитьПараметр("СостояниеВыбытия", Перечисления.СостоянияОС.СнятоСУчета);
		ЗапросСписание.Текст = 
		"ВЫБРАТЬ
		|	СостоянияОСОрганизаций.Регистратор.Номер КАК НомерДокумента,
		|	СостоянияОСОрганизаций.Период КАК ДатаСостояния,
		|	ВЫБОР
		|		КОГДА СостоянияОСОрганизаций.Регистратор ССЫЛКА Документ.ПодготовкаКПередачеОС
		|			ТОГДА ""Підготовка до передачі ОС""
		|		ИНАЧЕ СобытияОСОрганизаций.НазваниеДокумента
		|	КОНЕЦ КАК НазваниеДокумента,
		|	ВЫБОР
		|		КОГДА СостоянияОСОрганизаций.Регистратор ССЫЛКА Документ.ПодготовкаКПередачеОС
		|			ТОГДА ""Продаж підготовлених ОЗ""
		|		ИНАЧЕ СобытияОСОрганизаций.Событие.Представление
		|	КОНЕЦ КАК Причина
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСБУ КАК СостоянияОСОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ПО СостоянияОСОрганизаций.ОсновноеСредство = СобытияОСОрганизаций.ОсновноеСредство
		|			И СостоянияОСОрганизаций.Регистратор = СобытияОСОрганизаций.Регистратор
		|ГДЕ
		|	СостоянияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|	И СостоянияОСОрганизаций.Организация = &Организация
		|	И СостоянияОСОрганизаций.Период <= &ДатаСведений
		|	И СостоянияОСОрганизаций.Состояние = &СостояниеВыбытия"; 
		
		РезультатСписания = ЗапросСписание.Выполнить();  
		ВыборкаСписание = РезультатСписания.Выбрать();
		Если ВыборкаСписание.Следующий() Тогда
			Шапка2.Параметры.ДатаСписания = Формат(ВыборкаСписание.ДатаСостояния, "ДФ=dd.MM.yyyy");
		КонецЕсли; 
		ВыборкаСписание.Сбросить();
		Шапка2.Параметры.ДатаВводаВЭксплуатацию = ДатаВвода;  

		ТабДок.Вывести(Шапка2);  
		//Амортизация
		Запрос.УстановитьПараметр("КорСчет", СведенияОбОС.СчетУчета);
		Запрос.УстановитьПараметр("СубконтоОС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапросПоАмортизации.ОС КАК ОС,
		|	ЗапросПоАмортизации.Период КАК Период,
		|	ЗапросПоАмортизации.СуммаОборотКт КАК АмортизацияБУ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ХозрасчетныйОбороты.Субконто1 КАК ОС,
		|		СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК СуммаОборотКт,
		|		ХозрасчетныйОбороты.Период КАК Период
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.Обороты(
		|				,
		|				&ДатаСведений,
		|				Месяц,
		|				Счет В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации
		|					ИЗ
		|						РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ДатаСведений, Организация = &Организация
		|							И ОсновноеСредство = &ОсновноеСредство) КАК СчетаБухгалтерскогоУчета),
		|				&СубконтоОС,
		|				Организация = &Организация
		|					И Субконто1 = &ОсновноеСредство,
		|				КорСчет <> &КорСчет,
		|				) КАК ХозрасчетныйОбороты
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ХозрасчетныйОбороты.Период,
		|		ХозрасчетныйОбороты.Субконто1) КАК ЗапросПоАмортизации
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапросПоАмортизации.ОС,
		|	ЗапросПоАмортизации.СуммаОборотКт,
		|	ЗапросПоАмортизации.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		ИндексСтроки = 0;
		НомерЗаписи = 0;
		Пока Выборка.Следующий() Цикл
			
			ИндексСтроки = ИндексСтроки + 1;	
			НомерЗаписи = НомерЗаписи + 1;
			
			Если ИндексСтроки = 4 Тогда 
				ТабДок.Вывести(СтрокаАмортизации);
				ИндексСтроки = 1;		
				СтрокаАмортизации = Макет.ПолучитьОбласть("АмортизацииСтрока");
			КонецЕсли;   
			
  			СтрокаАмортизации.Параметры["НПП" + Строка(ИндексСтроки)] = НомерЗаписи;
			СтрокаАмортизации.Параметры["ПериодАмортизации" + Строка(ИндексСтроки)] = Формат(Выборка.Период,"ДФ='MMMM yyyy'");
			СтрокаАмортизации.Параметры["ИспользованоМес" + Строка(ИндексСтроки)] = 1; 
			СтрокаАмортизации.Параметры["Сумма" + Строка(ИндексСтроки)] = Выборка.АмортизацияБУ;	
			
		КонецЦикла;
		ТабДок.Вывести(СтрокаАмортизации); 

		ТабДок.Вывести(ШапкаДвиженияОбъекта);

		//внутреннее перемещение
		Запрос.УстановитьПараметр("ОсновноеСредство"   , Ссылка);
		Запрос.УстановитьПараметр("ДатаСведений"       , ДатаСведений);
		Запрос.УстановитьПараметр("Организация"        , Организация);
		Запрос.УстановитьПараметр("ВидСобытияОС"       , Перечисления.ВидыСобытийОС.ВнутреннееПеремещение);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СобытияОСОрганизаций.Период КАК Период,
		|	МестонахождениеОС.МОЛ КАК МОЛ,
		|	МестонахождениеОС.Местонахождение КАК МестоХранения,
		|	СобытияОСОрганизаций.НазваниеДокумента КАК НазваниеДокумента,
		|	СобытияОСОрганизаций.НомерДокумента КАК НомерДокумента
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|		ПО СобытияОСОрганизаций.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|			И СобытияОСОрганизаций.Регистратор = МестонахождениеОС.Регистратор
		|ГДЕ
		|	СобытияОСОрганизаций.Период < &ДатаСведений
		|	И СобытияОСОрганизаций.ОсновноеСредство = &ОсновноеСредство
		|	И СобытияОСОрганизаций.Организация = &Организация
		|	И СобытияОСОрганизаций.Событие.ВидСобытияОС = &ВидСобытияОС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период"; 

		Результат = Запрос.Выполнить();

		НПП = 1;
		ВыборкаПеремещение = Результат.Выбрать();
		Пока ВыборкаПеремещение.Следующий() Цикл
			
			ДвиженияОбъектаСтрока.Параметры.Заполнить(ВыборкаПеремещение);
			ДвиженияОбъектаСтрока.Параметры.Дата 		= Формат(ВыборкаПеремещение.Период, "ДФ=dd.MM.yyyy");
			ДвиженияОбъектаСтрока.Параметры.НПП 		= НПП;
			ТабДок.Вывести(ДвиженияОбъектаСтрока);		
			НПП = НПП +1;
			
		КонецЦикла; 
		
		//списание
		Если ВыборкаСписание.Следующий() Тогда
			Подвал2.Параметры.Заполнить(ВыборкаСписание); 
			Подвал2.Параметры.ДатаВыбытия = Формат(ВыборкаСписание.ДатаСостояния, "ДФ=dd.MM.yyyy");
			Подвал2.Параметры.Документ	  = СокрЛП(ВыборкаСписание.НазваниеДокумента) + " № " + ВыборкаСписание.НомерДокумента; 
		КонецЕсли;

		Подвал2.Параметры.Дата = Формат(ДатаСведений, "ДЛФ=ДД");
		ТабДок.Вывести(Подвал2);		
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);

	КонецЦикла;	
		
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ПолеСлева     = 5;
	ТабДок.ПолеСправа    = 5;
	ТабДок.ПолеСверху    = 0;
	ТабДок.ПолеСнизу     = 0;
	ТабДок.ИмяПараметровПечати = НСтр("ru=""Инвентарная карточка учета объекта основных средств"";uk=""Інвентарна картка обліку об’єкта основних засобів""");

	Возврат ТабДок;
	
КонецФункции //ПечатьИнвКарточка_2016() 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	ОбъектыЭксплуатацииЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвКарточкаБюдж") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"ИнвКарточкаБюдж", 
			НСтр("ru=""Инвентарная карточка учета объекта основных средств""uk=""Інвентарна картка обліку об’єкта основних засобів"""), 
			ПечатьИнвКарточка_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;    
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ОбъектыЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0a97bfe9-77f9-4f51-970e-40c12228274b");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ОбъектыЭксплуатации.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет новый реквизит в справочнике ""Объекты эксплуатации"":
                                   |- Инвентарный номер'
                                   |;uk='Заповнює новий реквізит у довіднику ""Об''єкти експлуатації"": 
                                   |- Інвентарний номер'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьРеглУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних КАК ПервоначальныеСведенияОС
	|		ПО ОбъектыЭксплуатации.Ссылка = ПервоначальныеСведенияОС.ОсновноеСредство
	|ГДЕ
	|			ОбъектыЭксплуатации.ИнвентарныйНомер = """"
	|				И ПервоначальныеСведенияОС.ИнвентарныйНомер <> """" 
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.ОбъектыЭксплуатации";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПараметрыТаблицыДляОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц
	);

	Если НЕ ПараметрыТаблицыДляОбработки.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ ПараметрыТаблицыДляОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ЗаблокированныеДанные.Регистратор ЕСТЬ NULL КАК ЧитаемыеДанныеОбновлены
	|ПОМЕСТИТЬ ПервоначальныеСведенияОС
	|ИЗ
	|	РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ДанныеДляОбработки.Ссылка
	|				ИЗ
	|					ВТДляОбработки КАК ДанныеДляОбработки)) КАК ПервоначальныеСведенияОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано_ПервоначальныеСведения КАК ЗаблокированныеДанные
	|		ПО (ЗаблокированныеДанные.Регистратор = ПервоначальныеСведенияОС.Регистратор)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка КАК Ссылка,
	|	ОбъектыЭксплуатации.ВерсияДанных КАК ВерсияДанных,
	|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ИЗ
	|	ВТДляОбработки КАК ДанныеДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО (ОбъектыЭксплуатации.Ссылка = ДанныеДляОбработки.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ДанныеДляОбработки.Ссылка)
	|ГДЕ
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.ЧитаемыеДанныеОбновлены, ИСТИНА) 
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДляОбработки", ПараметрыТаблицыДляОбработки.ИмяВременнойТаблицы);
	
	СписокЧитаемыхДанных = Новый СписокЗначений;
	СписокЧитаемыхДанных.Добавить("РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет", "ВТЗаблокировано_ПервоначальныеСведения");
	Для каждого ЭлементСписка Из СписокЧитаемыхДанных Цикл
		ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
		ДополнительныеПараметры.ИмяВременнойТаблицы = ЭлементСписка.Представление;
		ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(
				Параметры.Очередь, 
				ЭлементСписка.Значение, 
				МенеджерВременныхТаблиц, 
				ДополнительныеПараметры
		);
	КонецЦикла;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[Результат.ВГраница()].Выбрать();
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ОбъектыЭксплуатации");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДанныеОбъекта = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ДанныеОбъекта.ИнвентарныйНомер)
				И ЗначениеЗаполнено(Выборка.ИнвентарныйНомер)Тогда
				ДанныеОбъекта.ИнвентарныйНомер = Выборка.ИнвентарныйНомер;
			КонецЕсли; 
			
			Если ДанныеОбъекта.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось обработать объект: %Ссылка% по причине: %Причина%';uk='Не вдалося обробити об''єкт: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Ссылка.Метаданные(),
				Выборка.Ссылка,
				ТекстСообщения
			);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
