
#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Значения реквизитов формы
	СоставНабораКонстантФормы    = ОбщегоНазначенияУТ.ПолучитьСтруктуруНабораКонстант(НаборКонстант);
	ВнешниеРодительскиеКонстанты = НастройкиСистемыПовтИсп.ПолучитьСтруктуруРодительскихКонстант(СоставНабораКонстантФормы);
	
	РежимРаботы = Новый Структура;
	РежимРаботы.Вставить("СоставНабораКонстантФормы",    Новый ФиксированнаяСтруктура(СоставНабораКонстантФормы));
	РежимРаботы.Вставить("ВнешниеРодительскиеКонстанты", Новый ФиксированнаяСтруктура(ВнешниеРодительскиеКонстанты));
	
	РежимРаботы = Новый ФиксированнаяСтруктура(РежимРаботы);
	
	ВариантПроверкиДокументов = ?(НаборКонстант.РазрешатьИзменятьПроверенныеДокументыПоРеглУчету, 1, 0);
	НачалоВедениеРеглУчета = ?(ЗначениеЗаполнено(НаборКонстант.ДатаНачалаВеденияРеглУчета), 1, 0);
	НачалоВедениеУУНаПланеСчетовХозрасчетный = ?(ЗначениеЗаполнено(НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный), 1, 0);

	// Допустимые форматы сохранения регистров бухгалтерского учета.
	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	Элементы.ФорматСохраненияРегистровУчета.СписокВыбора.ЗагрузитьЗначения(ТаблицаФорматов.ВыгрузитьКолонку("Ссылка"));
	
	// Обновление состояния элементов
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия <> "Запись_НаборКонстант" Тогда
		Возврат; // такие событие не обрабатываются
	КонецЕсли;
	
	// Если это изменена константа, расположенная в другой форме и влияющая на значения констант этой формы,
	// то прочитаем значения констант и обновим элементы этой формы.
	Если РежимРаботы.ВнешниеРодительскиеКонстанты.Свойство(Источник)
	 ИЛИ (ТипЗнч(Параметр) = Тип("Структура")
	 		И ОбщегоНазначенияУТКлиентСервер.ПолучитьОбщиеКлючиСтруктур(
	 			Параметр, РежимРаботы.ВнешниеРодительскиеКонстанты).Количество() > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность();
		
	ИначеЕсли (ТипЗнч(Источник) = Тип("Строка")
		И Найти(Источник, "ВестиУправленческийУчетОрганизаций") > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность("НаборКонстант.ВестиУправленческийУчетОрганизаций");
		
	ИначеЕсли (ТипЗнч(Источник) = Тип("Строка")
		И Найти(Источник, "ИспользоватьВнеоборотныеАктивы2_4") > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность("НаборКонстант.ИспользоватьВнеоборотныеАктивы2_4");
		
	ИначеЕсли (ТипЗнч(Источник) = Тип("Строка")
		И Найти(Источник, "ПартионныйУчетВерсии22") > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность("НаборКонстант.ПартионныйУчетВерсии22");
		
	ИначеЕсли (ТипЗнч(Источник) = Тип("Строка")
		И Найти(Источник, "ИспользоватьУчетПрочихДоходовРасходовРегл") > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность("НаборКонстант.ИспользоватьУчетПрочихДоходовРасходовРегл");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НаборКонстант.ИспользоватьРеглУчет
		И НачалоВедениеРеглУчета = 1
		И НЕ ЗначениеЗаполнено(НаборКонстант.ДатаНачалаВеденияРеглУчета) Тогда
		
		ТекстСообщения = НСтр("ru='Укажите дату начала отражения операций в регламентированном учете';uk='Вкажіть дату початку відображення операцій в регламентованому обліку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, , "НаборКонстант.ДатаНачалаВеденияРеглУчета", , Отказ);
		
	КонецЕсли;
	
	Если НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный
		И НачалоВедениеУУНаПланеСчетовХозрасчетный = 1
		И НЕ ЗначениеЗаполнено(НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный) Тогда
		
		ТекстСообщения = НСтр("ru='Укажите дату начала ведения управленческого учета на едином плане счетов регламентированного учета.';uk='Вкажіть дату початку ведення управлінського обліку на єдиному плані рахунків регламентованого обліку.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, , "НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный", , Отказ);
		
	КонецЕсли;
	
	Если НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности
		И НЕ ЗначениеЗаполнено(НаборКонстант.ВалютаФинОтчетности) Тогда
		
		ТекстСообщения = НСтр("ru='Укажите валюту управленческой финансовой отчетности.';uk='Вкажіть валюту управлінської фінансової звітності.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, , "НаборКонстант.ВалютаФинОтчетности", , Отказ);
		
	КонецЕсли;
	
	Если НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности
		И НЕ ЗначениеЗаполнено(НаборКонстант.ИсточникСуммыДляПересчетаВВалютуФинОтчетности) Тогда
		
		ТекстСообщения = НСтр("ru='Укажите источник для пересчета суммы в валюту управленческой финансовой отчетности.';uk='Вкажіть джерело для перерахунку суми в валюту управлінської фінансової звітності.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, , "НаборКонстант.ИсточникСуммыДляПересчетаВВалютуФинОтчетности", , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматСохраненияРегистровУчетаПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаКолонтитулов(Команда)

	ОткрытьФорму("ОбщаяФорма.НастройкаКолонтитулов", , ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРучнуюКорректировкуПроводокПоРеглУчетуПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПроверкуДокументовПоРегламентированномуУчетуПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВариантПроверкиДокументовПриИзменении(Элемент)
	НаборКонстант.РазрешатьИзменятьПроверенныеДокументыПоРеглУчету = ВариантПроверкиДокументов;
	Подключаемый_ПриИзмененииРеквизита(Элементы.РазрешатьИзменятьПроверенныеДокументыПоРеглУчету);	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРегламентированныйУчетПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НачалоВеденияРеглУчетаСНачальнойДатыПриИзменении(Элемент)
	
	НаборКонстант.ДатаНачалаВеденияРеглУчета = Дата(1,1,1);
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаВеденияРеглУчета, Ложь);
	Оповестить("ДатаНачалаВеденияРеглУчета_Изменение", НаборКонстант.ДатаНачалаВеденияРеглУчета, ЭтотОбъект);
	УстановитьДоступность("НаборКонстант.ДатаНачалаВеденияРеглУчета");
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВеденияРеглУчетаСОпределеннойДатыПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаВеденияРеглУчета, Ложь);
	Оповестить("ДатаНачалаВеденияРеглУчета_Изменение", НаборКонстант.ДатаНачалаВеденияРеглУчета, ЭтотОбъект);
	УстановитьДоступность("НаборКонстант.ДатаНачалаВеденияРеглУчета");
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиУУНаПланеСчетовХозрасчетныйПриИзменении(Элемент)
	
	Если НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный
		И ВедениеУУНаПланеСчетовТолькоСДаты() Тогда
		НачалоВедениеУУНаПланеСчетовХозрасчетный = 1;
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
	Если НачалоВедениеУУНаПланеСчетовХозрасчетный = 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаПослеИзмененияДатыНачалаУУНаПланеСчетовХозрасчетный", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Все документы будут возвращены к отражению в регламентированном учете. 
|Выполнить обработку?'
|;uk='Всі документи будуть повернуті до відображення в регламентованому обліку. 
|Виконати обробку?'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Выполнить';uk='Виконати'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не выполнять';uk='Не виконувати'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет, НСтр("ru='Обработка данных';uk='Обробка даних'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВедениеУУНаПланеСчетовХозрасчетныйПриИзменении(Элемент)
	
	НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный = Дата(1,1,1);
	
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаУУНаПланеСчетовХозрасчетный, Ложь);
	Оповестить("ДатаНачалаУУНаПланеСчетовХозрасчетный_Изменение", НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, ЭтотОбъект);
	УстановитьДоступность("НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаПослеИзмененияДатыНачалаУУНаПланеСчетовХозрасчетный", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Все документы будут возвращены к отражению в регламентированном учете. 
|Выполнить обработку?'
|;uk='Всі документи будуть повернуті до відображення в регламентованому обліку. 
|Виконати обробку?'");
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Выполнить';uk='Виконати'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не выполнять';uk='Не виконувати'"));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет, НСтр("ru='Обработка данных';uk='Обробка даних'"));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВедениеУУНаПланеСчетовХозрасчетныйСДатыПриИзменении(Элемент)

	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаУУНаПланеСчетовХозрасчетный, Ложь);
	Оповестить("ДатаНачалаУУНаПланеСчетовХозрасчетный_Изменение", НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, ЭтотОбъект);
	УстановитьДоступность("НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный");
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетностиПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаФинОтчетностиПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникСуммыДляПересчетаВВалютуФинОтчетностиПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникСуммыДляПересчетаВВалютуФинОтчетностиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаУУНаПланеСчетовХозрасчетныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбораПериода = Новый Структура("Значение, РежимВыбораПериода", НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, "МЕСЯЦ");
	ОписаниеОповещения = Новый ОписаниеОповещения("ДатаНачалаУУНаПланеСчетовХозрасчетныйЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборПериода",
		ПараметрыФормыВыбораПериода, 
		ЭтотОбъект, 
		ЭтотОбъект.УникальныйИдентификатор, 
		, 
		, 
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВеденияРеглУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбораПериода = Новый Структура("Значение, РежимВыбораПериода", НаборКонстант.ДатаНачалаВеденияРеглУчета, "МЕСЯЦ");
	ОписаниеОповещения = Новый ОписаниеОповещения("ДатаНачалаВеденияРеглУчетаЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборПериода",
		ПараметрыФормыВыбораПериода, 
		ЭтотОбъект, 
		ЭтотОбъект.УникальныйИдентификатор, 
		, 
		, 
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОтражатьВРеглУчетеТолькоПроверенныеДокументыПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ФормироватьРезервыПредстоящихРасходовПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НеИспользоватьПроверкуЗаданийЗакрытияМесяцаВОтчетахРеглПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаНумерацииНалоговыхДокументов(Команда)
	ОткрытьФорму("РегистрСведений.НастройкаНумерацииНалоговыхДокументов.ФормаСписка", , ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчкиКоманд

&НаКлиенте
Процедура ПроводкиВводаОстатковПоБУиНУ(Команда)
	
	Отбор = Новый Структура;
	Отбор.Вставить("КорСчет", ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Вспомогательный"));
	Отбор.Вставить("СуммаЗаполнена", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("РегистрБухгалтерии.Хозрасчетный.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроводкиВводаОстатковПоУУ(Команда)
	
	Отбор = Новый Структура;
	Отбор.Вставить("КорСчет", ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Вспомогательный"));
	Отбор.Вставить("СуммаУУЗаполнена", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("РегистрБухгалтерии.Хозрасчетный.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовВводаНачальныхОстатковПоБУиНУ(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоВидуУчета", "ОтражатьВБУиНУ");
	ОткрытьФорму("Обработка.ЖурналДокументовВводаНачальныхОстатков.Форма.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовВводаНачальныхОстатковПоУУ(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоВидуУчета", "ОтражатьВУУ");
	ОткрытьФорму("Обработка.ЖурналДокументовВводаНачальныхОстатков.Форма.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыВводаНачальныхОстатковПоБУиНУ(Команда)
	
	ДатаВвода = НаборКонстант.ДатаНачалаВеденияРеглУчета - 1;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтражатьВБУиНУ", Истина);
	ДопПараметры.Вставить("ОтражатьВУУ",    Ложь);
	ДопПараметры.Вставить("ДатаВвода",      ДатаВвода);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросВводНачальныхОстатков", ЭтотОбъект, ДопПараметры);
	
	ТекстВопроса = СтрШаблон(НСтр("ru='Сформировать документы ввода остатков по БУ и НУ на %1?';uk='Сформувати документи введення залишків по БО і ПО на %1?'"), ДатаВвода);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Сформировать';uk='Сформувати'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не формировать';uk='Не формувати'"));
	
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, Кнопки,,, НСтр("ru='Ввод начальных остатков';uk='Введення початкових залишків'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыВводаНачальныхОстатковПоУУ(Команда)
	
	ДатаВвода = НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный - 1;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтражатьВБУиНУ", Ложь);
	ДопПараметры.Вставить("ОтражатьВУУ",    Истина);
	ДопПараметры.Вставить("ДатаВвода",      ДатаВвода);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросВводНачальныхОстатков", ЭтотОбъект, ДопПараметры);
	
	ТекстВопроса = СтрШаблон(НСтр("ru='Сформировать документы ввода остатков по УУ на %1?';uk='Сформувати документи введення залишків по УО на %1?'"), ДатаВвода);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Сформировать';uk='Сформувати'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не формировать';uk='Не формувати'"));
	
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, Кнопки,,, НСтр("ru='Ввод начальных остатков';uk='Введення початкових залишків'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодНачалаВеденияРеглУчетаЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборКонстант.ДатаНачалаВеденияРеглУчета = РезультатВыбора;
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаВеденияРеглУчета, Ложь);
	Оповестить("ДатаНачалаВеденияРеглУчета_Изменение", НаборКонстант.ДатаНачалаВеденияРеглУчета, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросВводНачальныхОстатков(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Результат = ЗапуститьВводНачальныхОстатков(УникальныйИдентификатор, ДопПараметры);
		
		Если Результат.Статус = "Выполняется" Тогда
		
			ПараметрыФормаДлительнойОперации = ПараметрыФормыДлительнойОперации();
			ПараметрыФормаДлительнойОперации.АдресРезультата = Результат.АдресРезультата;
			ПараметрыФормаДлительнойОперации.ИдентификаторЗадания = Результат.ИдентификаторЗадания;
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИзменениеОбъектовЗавершено", ЭтотОбъект);
			
			ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормаДлительнойОперации, ЭтотОбъект, , , , ОповещениеОЗакрытии);
			
		Иначе
			
			ИзменениеОбъектовЗавершено(Результат);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыДлительнойОперации()
	
	ПараметрыДлительнойОперации = Новый Структура;
	ПараметрыДлительнойОперации.Вставить("ИдентификаторЗадания");
	ПараметрыДлительнойОперации.Вставить("АдресРезультата");
	ПараметрыДлительнойОперации.Вставить("ВыводитьОкноОжидания", Истина);
	ПараметрыДлительнойОперации.Вставить("ВыводитьПрогрессВыполнения", Истина);
	ПараметрыДлительнойОперации.Вставить("ВыводитьСообщения", Истина);
	
	Возврат ПараметрыДлительнойОперации;
	
КонецФункции

&НаКлиенте
Процедура ИзменениеОбъектовЗавершено(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(Результат.АдресРезультата) И Не Результат.Статус = "Ошибка" Тогда
			СписокСозданныхДокументов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			
			ТекстОповещения = НСтр("ru='Ввод начальных остатков...';uk='Введення початкових залишків ...'");
			Если СписокСозданныхДокументов.Количество() Тогда
				ТекстПояснения = СтрШаблон(НСтр("ru='Создано %1 документов ввода начальных остатков по регламентированному учету.';uk='Створено %1 документів введення початкових залишків по регламентованому обліку.'"), СписокСозданныхДокументов.Количество());
			Иначе
				ТекстПояснения = НСтр("ru='Не удалось создать документы ввода начальных остатков по регламентированному учету.';uk='Не вдалося створити документи введення початкових залишків з регламентованому обліку.'");
			КонецЕсли;
			ПоказатьОповещениеПользователя(ТекстОповещения,, ТекстПояснения);
			
		Иначе
			
			ТекстОповещения = НСтр("ru='Ввод начальных остатков не выполнен';uk='Введення початкових залишків не виконано'");
			ТекстПояснения = НСтр("ru='Ввод начальных остатков завершен с ошибкой, по причине: %1';uk='Введення початкових залишків завершено з помилкою, через: %1'");
			ТекстПояснения = СтрШаблон(ТекстПояснения, Результат.ПодробноеПредставлениеОшибки);
			ПоказатьПредупреждение(, ТекстПояснения, , ТекстОповещения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВызовСервера

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Новый Структура);
	
	УстановитьДоступность(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗапуститьВводНачальныхОстатков(УникальныйИдентификатор, ПараметрыЗапуска)
	
	ВыполняемыйМетод = "ВводОстатковСервер.СформироватьДокументыВводаОстатковПоДаннымОперативногоУчета";
	
	ОтражениеВУчетах = Новый Массив;
	Если ПараметрыЗапуска.ОтражатьВБУиНУ Тогда
		ОтражениеВУчетах.Добавить(НСтр("ru='БУ и НУ';uk='БО і ПО'"));
	КонецЕсли;
	Если ПараметрыЗапуска.ОтражатьВУУ Тогда
		ОтражениеВУчетах.Добавить(НСтр("ru='УУ';uk='УО'"));
	КонецЕсли;
	
	ШаблонНаименования = НСтр("ru='Создание документов ввода начальных остатков по %1 на дату %2.
|Заполнение документов выполняется по данным оперативного учета.'
|;uk='Створення документів введення початкових залишків з %1 на дату %2. 
|Заповнення документів виконується за даними оперативного обліку.'");

	НаименованиеЗадания = СтрШаблон(ШаблонНаименования, СтрСоединить(ОтражениеВУчетах, ","), ПараметрыЗапуска.ДатаВвода);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = "ВводНачальныхОстатков";
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗапуска, ПараметрыФоновогоЗадания);
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	Иначе
		// Определение имени и запись значения реквизита в соответствующей константе из "НаборКонстант".
		// Используется для тех реквизитов формы, которые связаны с константами напрямую (в отношении один-к-одному).
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
		Если НастройкиСистемыПовтИсп.ЕстьПодчиненныеКонстанты(КонстантаИмя, КонстантаЗначение) Тогда
			ЭтаФорма.Прочитать();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КонстантаИмя
	
КонецФункции

&НаСервере
Процедура УстановитьДоступность(РеквизитПутьКДанным = "")
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьПроверкуДокументовПоРегламентированномуУчету"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Элементы.ГруппаВариантовПроверкиДокумента.Доступность = НаборКонстант.ИспользоватьПроверкуДокументовПоРегламентированномуУчету;
		
	КонецЕсли;
	
	ЕстьОтражениеДокументовВРеглУчете = ЕстьОтражениеДокументовВРеглУчете();
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьРеглУчет"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Элементы.ГруппаДатаНачалаВеденияРеглУчетаГоризонтальная.Доступность = НаборКонстант.ИспользоватьРеглУчет;
		Элементы.ГруппаОтчетность.Доступность = НаборКонстант.ИспользоватьРеглУчет;
		Элементы.ГруппаСервисныеФункции.Доступность = НаборКонстант.ИспользоватьРеглУчет;
		
		Результат = ДоступенУУНаПланеСчетовХозрасчетный();
		Элементы.ВестиУУНаПланеСчетовХозрасчетный.Доступность = Результат.Доступность;
		Элементы.ГруппаВестиУУНаПланеСчетовХозрасчетныйЗависимости.Видимость = НЕ Результат.Доступность;
		Элементы.КомментарийВестиУУНаПланеСчетовХозрасчетныйЗависимости.Заголовок = Результат.Комментарий;
		
		Элементы.ГруппаВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности.Доступность = НаборКонстант.ИспользоватьРеглУчет;
		
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы.ИспользоватьРегламентированныйУчет, ЕстьОтражениеДокументовВРеглУчете);
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ДатаНачалаВеденияРеглУчета"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		РеглУчетСДаты = (НачалоВедениеРеглУчета = 1);
		
		Элементы.ДатаНачалаВеденияРеглУчета.Доступность = РеглУчетСДаты;
		Элементы.СоздатьДокументыВводаНачальныхОстатков.Доступность = РеглУчетСДаты;
		Элементы.СписокДокументовВводаНачальныхОстатков.Доступность = РеглУчетСДаты;
		Элементы.ПроводкиВводаОстатковБУНУ.Доступность = РеглУчетСДаты;
		Элементы.ДатаНачалаВеденияРеглУчета.АвтоОтметкаНезаполненного = РеглУчетСДаты;
		
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы.НачалоВеденияРеглУчетаСНачальнойДаты, ЕстьОтражениеДокументовВРеглУчете);
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы.НачалоВеденияРеглУчетаСОпределеннойДаты, ЕстьОтражениеДокументовВРеглУчете);
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы.ДатаНачалаВеденияРеглУчета, ЕстьОтражениеДокументовВРеглУчете);
			
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты = (НачалоВедениеУУнаПланеСчетовХозрасчетный = 1);
		
		Элементы.ГруппаДатаНачалаВеденияУпрУчетаГоризонтальная.Доступность = НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный;
		Элементы.НачалоВедениеУУнаПланеСчетовХозрасчетный.Доступность = НЕ ВедениеУУНаПланеСчетовТолькоСДаты();
		Элементы.ДатаНачалаУУНаПланеСчетовХозрасчетный.Доступность = НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты;
		Элементы.ДатаНачалаУУНаПланеСчетовХозрасчетный.АвтоОтметкаНезаполненного = НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты;
		
		Элементы.СформироватьДокументыВводаОстатковПоУУ.Доступность = НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты;
		Элементы.СписокДокументовВводаНачальныхОстатковУУ.Доступность = НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты;
		Элементы.ПроводкиВводаОстатковУУ.Доступность = НачалоВедениеУУнаПланеСчетовХозрасчетныйСДаты;
		
		ВалютыУпрИРеглУчетаСовпадают = Константы.ВалютыУпрИРеглУчетаСовпадают.Получить();
		Элементы.ГруппаИнформацияВалютыУУиРУОтличаются.Видимость = 
			НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный И НЕ ВалютыУпрИРеглУчетаСовпадают;
			
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьВнеоборотныеАктивы2_4"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ИспользоватьВнеоборотныеАктивы2_2"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ВестиУправленческийУчетОрганизаций"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Результат = ДоступенУУНаПланеСчетовХозрасчетный();
		Элементы.ВестиУУНаПланеСчетовХозрасчетный.Доступность = Результат.Доступность;
		Элементы.ГруппаВестиУУНаПланеСчетовХозрасчетныйЗависимости.Видимость = НЕ Результат.Доступность;
		Элементы.КомментарийВестиУУНаПланеСчетовХозрасчетныйЗависимости.Заголовок = Результат.Комментарий;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Элементы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Доступность = НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности;
		Элементы.ВалютаФинОтчетности.Доступность = НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИсточникСуммыДляПересчетаВВалютуФинОтчетности" Тогда
		
		Элементы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Доступность = НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности;
		Элементы.ВалютаФинОтчетности.Доступность = НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИсточникСуммыДляПересчетаВВалютуФинОтчетности"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		ВыполняетсяПересчетВВалютуФОизУУ = 
		    НаборКонстант.ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности
			И (НаборКонстант.ИсточникСуммыДляПересчетаВВалютуФинОтчетности = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.УУ);
		
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы.ВестиУУНаПланеСчетовХозрасчетный, 
			ВыполняетсяПересчетВВалютуФОизУУ И НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный);
	
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьПроверкуДокументовПоРегламентированномуУчету" 
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Элементы.ОтражатьВРеглУчетеТолькоПроверенныеДокументы.Доступность = НаборКонстант.ИспользоватьПроверкуДокументовПоРегламентированномуУчету;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьУчетПрочихДоходовРасходов"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ИспользоватьРеглУчет"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ПартионныйУчетВерсии22"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		Элементы.ФормироватьРезервыПредстоящихРасходов.Доступность = НаборКонстант.ИспользоватьУчетПрочихДоходовРасходов И НаборКонстант.ИспользоватьРеглУчет И НаборКонстант.ПартионныйУчетВерсии22;
		Элементы.ГруппаФормироватьРезервыПредстоящихРасходовЗависимости.Видимость = Не НаборКонстант.ИспользоватьУчетПрочихДоходовРасходов Или Не НаборКонстант.ПартионныйУчетВерсии22;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьВнеоборотныеАктивы2_4"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ПартионныйУчетВерсии22"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ИспользоватьУчетПрочихДоходовРасходовРегл"
		ИЛИ РеквизитПутьКДанным = "НаборКонстант.ВестиУправленческийУчетОрганизаций"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		
		ДоступностьУчетаВНА24 = Новый ФиксированнаяСтруктура(ВнеоборотныеАктивы.УсловияПереходаНаУчет2_4());
		
		ОпцияДоступна = 
			ДоступностьУчетаВНА24.ДоступенУчет 
			ИЛИ Константы.ИспользоватьВнеоборотныеАктивы2_4.Получить()
			ИЛИ НаборКонстант.ИспользоватьРеглУчет;
		
		Элементы.ИспользоватьРегламентированныйУчет.Доступность = ОпцияДоступна;
		Элементы.ГруппаКомментарийИспользоватьРегламентированныйУчет.Видимость = НЕ ОпцияДоступна;
		
		Если НЕ ОпцияДоступна Тогда
			Элементы.КомментарийИспользоватьРегламентированныйУчет.Заголовок = 
				СтрШаблон(НСтр("ru='Для включения регламентированного учета необходимо:%1';uk='Для включення регламентованого обліку необхідно: %1'"),
							ДоступностьУчетаВНА24.УсловияВключенияУчет2_4);
		КонецЕсли; 
		
	КонецЕсли;

	ОбменДаннымиУТУП.УстановитьДоступностьНастроекУзлаИнформационнойБазы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ЕстьОтражениеДокументовВРеглУчете()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Регистр.Регистратор
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Регистр
	|";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

&НаКлиенте
Процедура ДатаНачалаУУНаПланеСчетовХозрасчетныйЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокДат = СписокДатДляПроверкиНачалаВеденияУУ();
	
	Если РезультатВыбора < СписокДат[0].Значение Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СписокДат[0].Представление, , "НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный");
		Возврат;
	КонецЕсли;
	
	НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный = РезультатВыбора;
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаУУНаПланеСчетовХозрасчетный, Ложь);
	Оповестить("ДатаНачалаУУНаПланеСчетовХозрасчетныйЗавершение_Изменение", НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, ЭтотОбъект);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаПослеИзмененияДатыНачалаУУНаПланеСчетовХозрасчетный", ЭтотОбъект);
	ШаблонВопроса = НСтр("ru='Все документы с датой отражения больше %1 будут возвращены к отражению в регламентированном учете, 
|отражение документов ранее указанной даты по управленческому учету будет отменено.
|Выполнить обработку?'
|;uk='Всі документи з датою відображення більше %1 будуть повернуті до відображення в регламентованому обліку, 
|відображення документів раніше зазначеної дати з управлінського обліку буде скасовано.
|Виконати обробку?'");
	ТекстВопроса = СтрШаблон(ШаблонВопроса, Формат(НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, "ДЛФ=D"));
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Выполнить';uk='Виконати'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не выполнять';uk='Не виконувати'"));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет, НСтр("ru='Обработка данных';uk='Обробка даних'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВеденияРеглУчетаЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НаборКонстант.ВестиУУНаПланеСчетовХозрасчетный
		 И РезультатВыбора > НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный Тогда
		ШаблонСообщения = НСтр("ru='Управленческий учет на плане счетов регламентированного учета ведется с %1.
|Дата начала ведения регл. учета не может быть больше указанной даты.'
|;uk='Управлінський облік на плані рахунків регламентованого обліку ведеться з %1.
|Дата початку ведення регл. обліку не може бути більше зазначеної дати.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(НаборКонстант.ДатаНачалаУУНаПланеСчетовХозрасчетный, "ДЛФ=D"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, , "НаборКонстант.ДатаНачалаВеденияРеглУчета");
		Возврат;
	КонецЕсли;
	
	НаборКонстант.ДатаНачалаВеденияРеглУчета = РезультатВыбора;
	Подключаемый_ПриИзмененииРеквизита(Элементы.ДатаНачалаВеденияРеглУчета, Ложь);
	Оповестить("ДатаНачалаВеденияРеглУчета_Изменение", НаборКонстант.ДатаНачалаВеденияРеглУчета, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВедениеУУНаПланеСчетовТолькоСДаты()
	
	Результат = Ложь;
	
	Если Константы.ДатаНачалаВеденияРеглУчета.Получить() <> Дата(1,1,1) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Если Константы.ДатаНачалаВеденияУправленческогоУчетаОрганизаций.Получить() <> Дата(1,1,1) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Если РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22() <> Дата(1,1,1) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Если Константы.ИспользоватьВнеоборотныеАктивы2_4.Получить() 
		И ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4() <> Дата(1,1,1) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокДатДляПроверкиНачалаВеденияУУ()
	
	СписокДат = Новый СписокЗначений;
	
	ДатаНачалаВеденияРеглУчета = Константы.ДатаНачалаВеденияРеглУчета.Получить();
	СписокДат.Добавить(ДатаНачалаВеденияРеглУчета, 
		СтрШаблон(
			НСтр("ru='Выбранная дата должна быть больше %1 (начало ведения регл. учета).';uk='Обрана дата повинна бути більше %1 (початок ведення регл. обліку).'"),
			Формат(ДатаНачалаВеденияРеглУчета, "ДЛФ=D")));
			
	ДатаНачалаВеденияУправленческогоУчетаОрганизаций = Константы.ДатаНачалаВеденияУправленческогоУчетаОрганизаций.Получить();
	СписокДат.Добавить(ДатаНачалаВеденияУправленческогоУчетаОрганизаций, 
		СтрШаблон(
			НСтр("ru='Выбранная дата должна быть больше %1 (дата начала ведения упр. учета по правилам международного финансового учета)';uk='Обрана дата повинна бути більше %1 (дата початку ведення упр. обліку за правилами міжнародного фінансового обліку)'"),
			Формат(ДатаНачалаВеденияУправленческогоУчетаОрганизаций, "ДЛФ=D")));
			
	ДатаПереходаНаПартионныйУчетВерсии22 = РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22();
	СписокДат.Добавить(ДатаПереходаНаПартионныйУчетВерсии22, 
		СтрШаблон(
			НСтр("ru='Выбранная дата должна быть больше %1 (дата перехода на партионный учет 2.5)';uk='Обрана дата повинна бути більше %1 (дата переходу на партіонний облік 2.5)'"),
			Формат(ДатаПереходаНаПартионныйУчетВерсии22, "ДЛФ=D")));
			
	Если Константы.ИспользоватьВнеоборотныеАктивы2_4.Получить() Тогда
		ДатаНачалаУчетаВнеоборотныхАктивов2_4 = ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4();
		СписокДат.Добавить(ДатаНачалаУчетаВнеоборотныхАктивов2_4, 
			СтрШаблон(
				НСтр("ru='Выбранная дата должна быть больше %1 (начало использования необоротных активов версии 2.5.)';uk='Обрана дата повинна бути більше %1 (початок використання необоротних активів версії 2.5.)'"),
				Формат(ДатаНачалаУчетаВнеоборотныхАктивов2_4, "ДЛФ=D")));
	КонецЕсли;
	СписокДат.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	Возврат СписокДат;
	
КонецФункции

&НаСервере
Функция ДоступенУУНаПланеСчетовХозрасчетный()

	
	ИспользоватьРеглУчет = НаборКонстант.ИспользоватьРеглУчет;
	
	ИспользоватьУправленческийУчетОрганизаций = РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций();
	
	ДоступностьИспользования = ИспользоватьРеглУчет 
	                           И ИспользоватьУправленческийУчетОрганизаций
							   ;
	  
	Комментарий = "";  
	Если НЕ ДоступностьИспользования Тогда
		
		МассивПодстрок = Новый Массив;
		МассивПодстрок.Добавить(НСтр("ru='Для включения управленческого учета на едином плане счетов необходимо';uk='Для включення управлінського обліку на єдиному плані рахунків необхідно'"));
		
		Если НЕ ИспользоватьУправленческийУчетОрганизаций Тогда
			МассивПодстрок.Добавить(НСтр("ru='использовать партионный учет ""Версия 2.5"" (см раздел. ""НСИ и администрирование"" - ""Финансовый результат и контроллинг"" - ""Дополнительные настройки учета себестоимости"")';uk='використовувати партійний облік ""Версія 2.5"" (див. розділ ""НДІ і адміністрування"" - ""Фінансовий результат і контролінг"" - ""Додаткові настройки обліку собівартості"")'"));
		КонецЕсли;
		
		Если МассивПодстрок.Количество() > 2 Тогда
			Разделитель = "
			| - ";
		Иначе
			Разделитель = " ";
		КонецЕсли;
		
		Комментарий = СтрСоединить(МассивПодстрок, Разделитель);
		
	КонецЕсли;  
	
	Результат = Новый Структура;
	Результат.Вставить("Доступность", ДоступностьИспользования);
	Результат.Вставить("Комментарий", Комментарий);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗапуститьОбработкуУстановкиДатыНачалаУУНаПланеСчетовХозрасчетный(УникальныйИдентификатор)
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НСтр("ru='Обработка установки даты начала ведения УУ на едином плане счетов';uk='Обробка встановлення дати початку ведення УО на єдиному плані рахунків'");
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = "ОбработкаУстановкиДатыНачалаУУНаПланеСчетовХозрасчетный";
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	ПараметрыЗапуска = Новый Структура;
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(
		"РеглУчетПроведениеСервер.ОбработкаУстановкиДатыНачалаУУНаПланеСчетовХозрасчетный", 
		ПараметрыЗапуска, 
		ПараметрыФоновогоЗадания);
	
	Возврат РезультатФоновогоЗадания;

КонецФункции


&НаКлиенте
Функция ОбработкаУстановкиУстановкиДатыНачалаУУНаПланеСчетовХозрасчетныйЗавершена(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(Результат.АдресРезультата) 
			И Не Результат.Статус = "Ошибка" Тогда
			
			РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			
			ТекстОповещения = НСтр("ru='Обработка установки даты начала ведения УУ';uk='Обробка встановлення дати початку ведення УО'");
			
			Если РезультатВыполнения.ВозвращеноКОтражению <> 0 
				ИЛИ РезультатВыполнения.ОчищенаСуммаУУ <> 0 Тогда
				МассивТекстПояснения = Новый Массив;
				Если РезультатВыполнения.ВозвращеноКОтражению <> 0 Тогда
					МассивТекстПояснения.Добавить(
						СтрШаблон(НСтр("ru='Возвращено к отражению в регл. учете: %1';uk='Повернено до відображення в регл. обліку: %1'"), РезультатВыполнения.ВозвращеноКОтражению));
				КонецЕсли;
				Если РезультатВыполнения.ОчищенаСуммаУУ <> 0 Тогда
					МассивТекстПояснения.Добавить(
						СтрШаблон(НСтр("ru='Отменено отражение по УУ: %1';uk='Скасовано відображення по УО: %1'"), РезультатВыполнения.ОчищенаСуммаУУ));
				КонецЕсли;
				Разделитель = "
				|";
				ТекстПояснения = СтрСоединить(МассивТекстПояснения, Разделитель);
			Иначе
				ТекстПояснения = НСтр("ru='Изменений в отражении документов не требуется';uk='Змін в відображенні документів не потрібно'");
			КонецЕсли;
			
			ПоказатьОповещениеПользователя(ТекстОповещения,, ТекстПояснения);
			
		Иначе
			
			ПоказатьПредупреждение(, 
				НСтр("ru='Обработка завершилась с ошибкой. Подробную информацию см. в журнале регистрации.';uk='Обробка завершилася з помилкою. Більш докладну інформацію див. в журналі реєстрації.'"),
				, 
				НСтр("ru='Обработка установки даты начала ведения УУ';uk='Обробка встановлення дати початку ведення УО'"));
			
		КонецЕсли;
		
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ОбработкаПослеИзмененияДатыНачалаУУНаПланеСчетовХозрасчетный(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Результат = ЗапуститьОбработкуУстановкиДатыНачалаУУНаПланеСчетовХозрасчетный(УникальныйИдентификатор);
		
		Если Результат.Статус = "Выполняется" Тогда
		
			ПараметрыФормаДлительнойОперации = ПараметрыФормыДлительнойОперации();
			ПараметрыФормаДлительнойОперации.АдресРезультата = Результат.АдресРезультата;
			ПараметрыФормаДлительнойОперации.ИдентификаторЗадания = Результат.ИдентификаторЗадания;
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработкаУстановкиУстановкиДатыНачалаУУНаПланеСчетовХозрасчетныйЗавершена", ЭтотОбъект, ДопПараметры);
			
			ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормаДлительнойОперации, ЭтотОбъект, , , , ОповещениеОЗакрытии);
			
		Иначе
			
			ОбработкаУстановкиУстановкиДатыНачалаУУНаПланеСчетовХозрасчетныйЗавершена(Результат, ДопПараметры);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти