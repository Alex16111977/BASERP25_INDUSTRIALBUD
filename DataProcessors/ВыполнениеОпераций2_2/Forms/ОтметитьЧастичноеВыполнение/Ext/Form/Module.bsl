#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ссылка = Параметры.Ссылка;
	
	РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка,
		"Количество, Подразделение, Операция.ЕдиницаИзмерения");
	
	Количество = РеквизитыСсылки.Количество;
	
	Подразделение = РеквизитыСсылки.Подразделение;
	
	Элементы.Количество.МаксимальноеЗначение = РеквизитыСсылки.Количество;
	
	ЕдиницаИзмеренияОперации = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		РеквизитыСсылки.ОперацияЕдиницаИзмерения,
		,
		Истина);
		
	Элементы.ОтразитьНевыполнение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Отразить невыполнение операции в количестве 0 %1';uk='Відобразити невиконання операції в кількості 0 %1'"),
		ЕдиницаИзмеренияОперации);
	
	УстановитьСтатус();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если Количество < Элементы.Количество.МаксимальноеЗначение И Количество > 0 Тогда
		
		ЗаписатьИЗакрытьНаСервере(Ссылка, Количество, СтатусНовойОперации);
		ОперативныйУчетПроизводстваКлиент.ОповеститьОЗаписиПроизводственныхОпераций(Подразделение,, Ссылка);
		
		Закрыть();
		
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Введено неверное количество';uk='Введено невірну кількість'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьИЗакрытьНаСервере(Ссылка, Выполнено, СтатусНовой)
	Документы.ПроизводственнаяОперация2_2.ВыполнитьЧастично(Ссылка, Выполнено, СтатусНовой);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	КоличествоОсталось = Элементы.Количество.МаксимальноеЗначение - Количество;
	
	Элементы.ОтразитьНевыполнение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Отразить невыполнение операции в количестве %1 %2';uk='Відобразити невиконання операції в кількості %1 %2'"),
		КоличествоОсталось,
		ЕдиницаИзмеренияОперации);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьНевыполнениеПриИзменении(Элемент)
	УстановитьСтатус();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСтатус()
	
	Если Ссылка.Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполняется Тогда
		ТекущийСтатус = "Выполняется";
	ИначеЕсли Ссылка.Статус = Перечисления.СтатусыПроизводственныхОпераций.Создана Тогда
		ТекущийСтатус = "Создана";
	КонецЕсли;
	
	СтатусНовойОперации = ?(ОтразитьНевыполнение, "НеВыполнена", ТекущийСтатус);
КонецПроцедуры


#КонецОбласти
