
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПроверитьНаличиеНеиспользуемыхДвижений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьДвижения(Команда)
	
	ДлительнаяОперация	= ОчиститьНеиспользуемыеДвиженияПоРегистрамСебестоимости();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru='Выполняется очистка движений.';uk='Виконується очистка рухів.'");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОчиститьДвиженияЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьНаличиеНеиспользуемыхДвижений()
	
	КоличествоДокументов = Обработки.ОчисткаДвиженийПриОтключенномУчетеСебестоимости.КоличествоРегистраторовСНеиспользуемымиДвижениями();
	
	Если КоличествоДокументов = 0 Тогда
		
		Элементы.ИнформационнаяНадпись.Заголовок = НСтр("ru='При отключеном учете себестоимости отсутствуют документы с движениями по регистрам учета себестоимости.';uk='При відключенні обліку собівартості відсутні документи з рухами по регістрах обліку собівартості.'");
		
		Элементы.ФормаОчиститьДвижения.Доступность = Ложь;
		
		Возврат;
		
	КонецЕсли;
	
	ШаблонТекста = НСтр("ru='Учет себестоимости ведется начиная с периода %1.
|При отключеном учете себестоимости найдены документы с движениями по регистрам учета себестоимости: %2.
|Очистка движений этих документов может занять продолжительное время.'
|;uk='Облік собівартості ведеться починаючи з періоду %1. 
|При відключенні обліку собівартості знайдені документи з рухами по регістрах обліку собівартості: %2. 
|Очищення рухів цих документів може зайняти тривалий час.'");
	
	Элементы.ИнформационнаяНадпись.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекста,
		РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(Константы.ДатаНачалаУчетаСебестоимости.Получить()),
		Формат(КоличествоДокументов, "ЧГ="));
	
	Элементы.ФормаОчиститьДвижения.Доступность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ОчиститьНеиспользуемыеДвиженияПоРегистрамСебестоимости()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		Метаданные.Обработки.ОчисткаДвиженийПриОтключенномУчетеСебестоимости.Синоним;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ОчисткаДвиженийПриОтключенномУчетеСебестоимости.ОчиститьНеиспользуемыеДвиженияПоРегистрамСебестоимости",
		"",
		ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьДвиженияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПроверитьНаличиеНеиспользуемыхДвижений();
	
КонецПроцедуры

#КонецОбласти
