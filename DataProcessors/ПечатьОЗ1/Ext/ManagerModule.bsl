#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Печать

// Сформировать печатные формы объектов
//
// Параметры:
// 		МассивОбъектов - Массив - Массив ссылок на объекты которые нужно распечатать
// 		ПараметрыПечати - Структура - Структура дополнительных параметров печати
// 		КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
// 		ОбъектыПечати - Объекты печати
// 		ПараметрыВывода - Структура - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл	
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОЗ1") Тогда
		    ИмяМакета = "";
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ОЗ1",
				НСтр("ru='Форма ОЗ-1';uk='Форма ОЗ-1'"),
				СформироватьПечатнуюФормуОЗ1(
					МассивОбъектов,
					ОбъектыПечати,
					ПараметрыПечати,
					ИмяМакета),, ИмяМакета);
		
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция СформироватьПечатнуюФормуОЗ1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОЗ1";
	
	НомерТипаДокумента = 0;
	
	СведенияАктуальны = Истина;
		
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
				
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ); 
				
		Для Каждого Элемент Из СтруктураОбъектов.Значение Цикл
			
			НомерТипаДокумента = НомерТипаДокумента + 1;
			Если НомерТипаДокумента > 1 Тогда                          	
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыОЗ1(ПараметрыПечати, Элемент.Ссылка);
 	        НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ЗаполнитьМакетОЗ1(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, СведенияАктуальны, Элемент.Ссылка);
			
			// В табличном документе зададим имя области, в которую был 
			// выведен объект. Нужно для возможности печати покомплектно.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Элемент.Ссылка); 			
			
		КонецЦикла;
		

		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьМакетОЗ1(ТабличныйДокумент, ВыборкаПоДокументам, ОбъектыПечати, СведенияАктуальны, Ссылка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьОЗ1.ПФ_MXL_UK_ОЗ1");

	ВыборкаПоОС = ВыборкаПоДокументам.Выбрать();

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();		
	
	ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
						
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);
	
	Пока ВыборкаПоОС.Следующий() Цикл

		ОбластьМакета = Макет.ПолучитьОбласть("ОЗ1");
		Параметры     = ОбластьМакета.Параметры;
		Параметры.Заполнить(ВыборкаПоОС);			
		Параметры.Заполнить(ВыборкаПоКомиссии);
		
		Параметры.Валюта      = ВалютаРегламентированногоУчета;
		СведенияОбОрганизации	= ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаПоОС.Организация, Ссылка.Дата);	
		Параметры.Организация	= ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
		Параметры.ЕДРПОУ		= СведенияОбОрганизации.КодПоЕДРПОУ;		

		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПеремещениеВЭксплуатации") 
			ИЛИ ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
			Параметры.НаименованиеОС = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
													ВыборкаПоОС.НоменклатураНаименование,
													ВыборкаПоОС.ХарактеристикаНаименование);
		КонецЕсли; 

		ОсновныеСотрудникиФизическихЛицПринял = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(ВыборкаПоОС.Принял, Истина, Ссылка.Организация, Ссылка.Дата);
		Если ЗначениеЗаполнено(ОсновныеСотрудникиФизическихЛицПринял) Тогда
			Для каждого Строка Из ОсновныеСотрудникиФизическихЛицПринял Цикл
			    ДанныеФизЛицаПолучил = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Строка.Сотрудник, "Должность", Ссылка.Дата);			
			КонецЦикла;
			Для каждого СтрокаДанныеПолучил Из ДанныеФизЛицаПолучил Цикл			
				ОбластьМакета.Параметры.ПолучилДолжность= СтрокаДанныеПолучил.Должность;
				ОбластьМакета.Параметры.ПолучилФИО 		= СтрокаДанныеПолучил.ФизическоеЛицо;			
			КонецЦикла;
		КонецЕсли;
		
		ОсновныеСотрудникиФизическихЛицСдал = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(ВыборкаПоОС.Сдал, Истина, Ссылка.Организация, Ссылка.Дата);
		Если ЗначениеЗаполнено(ОсновныеСотрудникиФизическихЛицСдал) Тогда		
			Для каждого Строка Из ОсновныеСотрудникиФизическихЛицСдал Цикл
				ДанныеФизЛицаСдал = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Строка.Сотрудник, "Должность", Ссылка.Дата);
			КонецЦикла;
			Для каждого СтрокаДанныеСдал Из ДанныеФизЛицаСдал Цикл
				ОбластьМакета.Параметры.СдалДолжность 	= СтрокаДанныеСдал.Должность;
				ОбластьМакета.Параметры.СдалФИО	 		= СтрокаДанныеСдал.ФизическоеЛицо;
			КонецЦикла;			
		КонецЕсли;

		ОбластьМакета.Параметры.ГлавныйБухгалтер 	= ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


