
&НаКлиенте
Процедура ВыполнитьПоиск(Команда)
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не заполнена организация';uk='Не заповнена організація'"));
		Возврат;
	КонецЕсли;
	
	СписокДокументов.Очистить();
	
	МассивДокументов = ВыполнитьПоискНаСервере();
	Если МассивДокументов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не найдено данных для корректировки';uk='Не знайдено даних для коригування'"));
	Иначе	
		Для Каждого СтрокаДокумента из МассивДокументов Цикл
			НоваяСтрока = СписокДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПоискНаСервере()
	
	МассивДокументов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияОДоходахНДФЛ.Регистратор,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.НалоговыйПериод,
	|	СведенияОДоходахНДФЛ.СуммаДохода,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	СведенияОДоходахНДФЛ.ГруппаУчетаНачислений
	|ПОМЕСТИТЬ ВТНДФЛ
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.НалоговыйПериод >= &ДатаИзменения
	|	И СведенияОДоходахНДФЛ.Период < &ДатаИзменения 
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СведенияОДоходахНДФЛ.Регистратор,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.НалоговыйПериод,
	|	СведенияОДоходахНДФЛ.СуммаДохода,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	СведенияОДоходахНДФЛ.ГруппаУчетаНачислений
	|ПОМЕСТИТЬ ВТВС
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.Регистратор В (ВЫБРАТЬ Регистратор ИЗ ВТНДФЛ)
	|	И СведенияОДоходахНДФЛ.НалоговыйПериод < &ДатаИзменения
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	СведенияОДоходахНДФЛ.Регистратор,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.НалоговыйПериод,
	|	СведенияОДоходахНДФЛ.СуммаДохода
	|ПОМЕСТИТЬ ВТИсправления
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|ГДЕ
	|	СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|	И СведенияОДоходахНДФЛ.ФизическоеЛицо В (ВЫБРАТЬ ФизическоеЛицо ИЗ ВТНДФЛ)
	|;
	|
	|ВЫБРАТЬ
	|	СведенияОДоходахВС.Регистратор КАК Документ,
	|	СведенияОДоходахВС.ФизическоеЛицо КАК Сотрудник,
	|	СведенияОДоходахВС.КодДохода КАК ВидДохода,
	|	СведенияОДоходахВС.НалоговыйПериод,
	|	СведенияОДоходахНДФЛ.НалоговыйПериод КАК ИсправленныйПериод,
	|	СведенияОДоходахВС.СуммаДохода,
	|	СведенияОДоходахВС.Начисление,
	|	СведенияОДоходахВС.ГруппаУчетаНачислений КАК ГруппаУчета
	|ИЗ
	|	ВТВС КАК СведенияОДоходахВС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ВТНДФЛ КАК СведенияОДоходахНДФЛ
	|	ПО СведенияОДоходахВС.Регистратор = СведенияОДоходахНДФЛ.Регистратор 
	|	И СведенияОДоходахВС.ФизическоеЛицо = СведенияОДоходахНДФЛ.ФизическоеЛицо
	|	И СведенияОДоходахВС.КодДохода = СведенияОДоходахНДФЛ.КодДохода.ОблагаетсяВоеннымСбором2021
	|	И СведенияОДоходахВС.СуммаДохода = СведенияОДоходахНДФЛ.СуммаДохода
	|	И СведенияОДоходахВС.Начисление  = СведенияОДоходахНДФЛ.Начисление
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВТИсправления КАК Исправления
	|	ПО Исправления.ФизическоеЛицо = СведенияОДоходахВС.ФизическоеЛицо
	|	И Исправления.КодДохода = СведенияОДоходахВС.КодДохода
	|	И Исправления.СуммаДохода = -1*СведенияОДоходахВС.СуммаДохода
	|	И Исправления.НалоговыйПериод = СведенияОДоходахВС.НалоговыйПериод
	|ГДЕ
	|	Исправления.Регистратор ЕСТЬ NULL
	|";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДатаИзменения", УчетНДФЛ.ДатаИзмененияРазмераВС());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаОтбора = Новый Структура;
		СтрокаОтбора.Вставить("Документ", Выборка.Документ);
		СтрокаОтбора.Вставить("Сотрудник", Выборка.Сотрудник);
		СтрокаОтбора.Вставить("ВидДохода", Выборка.ВидДохода);
		СтрокаОтбора.Вставить("НалоговыйПериод", Выборка.НалоговыйПериод);
		СтрокаОтбора.Вставить("ИсправленныйПериод", Выборка.ИсправленныйПериод);
		СтрокаОтбора.Вставить("СуммаДохода", Выборка.СуммаДохода);
		СтрокаОтбора.Вставить("Начисление", Выборка.Начисление);
		СтрокаОтбора.Вставить("ГруппаУчета", Выборка.ГруппаУчета);
		СтрокаОтбора.Вставить("Отметка", Истина);
			
		МассивДокументов.Добавить(СтрокаОтбора);		
	КонецЦикла;	
	
	Возврат МассивДокументов

КонецФункции

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СписокДокументов.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПоказатьЗначение(,СписокДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока).Документ);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКорректировкуНаСервере(ДанныеДляИсправления)
	Попытка
		Документ = Документы.ОперацияНалоговогоУчетаПоНДФЛ.СоздатьДокумент();
		Документ.Организация = Объект.Организация;
		Документ.Дата = Объект.Период;
		Документ.ДатаОперации = Объект.Период;
		Документ.Сотрудник = ДанныеДляИсправления.Сотрудник;
		Документ.Ответственный = Объект.Ответственный;
		Документ.Комментарий =НСтр("ru='Исправление периода военного сбора по документу';uk='Виправлення періоду військового збору за документом'") + " " + МультиязычностьУкр.ПолучитьЛокализованноеПредставление(ДанныеДляИсправления.Документ, МультиязычностьУкр.КодЯзыкаИнформационнойБазы());
		
		Доход = Документ.СведенияОДоходах.Добавить();
		Доход.НалоговыйПериод = ДанныеДляИсправления.НалоговыйПериод;
		Доход.КодДохода = ДанныеДляИсправления.ВидДохода;
		Доход.СуммаДохода = -ДанныеДляИсправления.СуммаДохода;
		Доход.ГруппаУчетаНачислений = ДанныеДляИсправления.ГруппаУчета;
		
		Доход = Документ.СведенияОДоходах.Добавить();
		Доход.НалоговыйПериод = ДанныеДляИсправления.ИсправленныйПериод;
		Доход.КодДохода = ДанныеДляИсправления.ВидДохода;
		Доход.СуммаДохода = ДанныеДляИсправления.СуммаДохода;
		Доход.ГруппаУчетаНачислений = ДанныеДляИсправления.ГруппаУчета;
		
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		
		
	Исключение
		
		Возврат;
		
	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Создано документ';uk='Створено документ'")+ " "+МультиязычностьУкр.ПолучитьЛокализованноеПредставление(Документ, МультиязычностьУкр.КодЯзыкаИнтерфейса()));
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКорректировку(Команда)
	
	Для Каждого ДанныеДляИсправления из СписокДокументов Цикл
		Если ДанныеДляИсправления.Отметка Тогда
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Документ", ДанныеДляИсправления.Документ);
			СтруктураДанные.Вставить("Сотрудник", ДанныеДляИсправления.Сотрудник);
			СтруктураДанные.Вставить("ВидДохода", ДанныеДляИсправления.ВидДохода);
			СтруктураДанные.Вставить("НалоговыйПериод", ДанныеДляИсправления.НалоговыйПериод);
			СтруктураДанные.Вставить("ИсправленныйПериод", ДанныеДляИсправления.ИсправленныйПериод);
			СтруктураДанные.Вставить("СуммаДохода", ДанныеДляИсправления.СуммаДохода);
			СтруктураДанные.Вставить("Начисление", ДанныеДляИсправления.Начисление);
			СтруктураДанные.Вставить("ГруппаУчета", ДанныеДляИсправления.ГруппаУчета);
			ВыполнитьКорректировкуНаСервере(СтруктураДанные);
		КонецЕсли;	
	КонецЦикла;
	
	СписокДокументов.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначенияДляЗаполнения = Новый Структура;
	ЗначенияДляЗаполнения.Вставить("Месяц",				"Объект.Период");
	ЗначенияДляЗаполнения.Вставить("Организация",		"Объект.Организация");
	ЗначенияДляЗаполнения.Вставить("Ответственный",		"Объект.Ответственный");
	
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.Период = НачалоМесяца(Объект.Период);
	
КонецПроцедуры





