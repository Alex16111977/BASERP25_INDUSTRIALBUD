
&НаКлиенте
Процедура ВыполнитьПоиск(Команда)
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не заполнена организация';uk='Не заповнена організація'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не заполнен период';uk='Не заповнений період'"));
		Возврат;
	КонецЕсли;

	СписокДокументов.Очистить();
	
	МассивДокументов = ВыполнитьПоискНаСервере();
	Для Каждого СтрокаДокумента из МассивДокументов Цикл
		НоваяСтрока = СписокДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ВыполнитьПоискНаСервере()
	
	МассивДокументов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Командировка.Ссылка КАК Ссылка,
	|	Командировка.Сотрудник КАК Сотрудник,
	|	Командировка.ДатаНачала КАК ДатаНачалаСобытия,
	|	Командировка.СреднийЗаработок КАК СреднийЗаработокВДокументе,
	|	0 КАК СреднийЗаработокВыходногоПособияВДокументе,
	|	Командировка.РучнаяКорректировкаСреднегоЗаработка КАК РучноеИсправление,
	|	ЛОЖЬ КАК РучноеИсправлениеВыходногоПособия,
	|	Командировка.ВидРасчета КАК ВидРасчета,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК ВидРасчетаВыходноеПособие,
	|	0 КАК ДнейКомпенсацииУдержанияОтпуска,
	|	0 КАК ДнейЧасовВыходногоПособия
	|ИЗ
	|	Документ.Командировка КАК Командировка
	|ГДЕ
	|	Командировка.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Командировка.ПометкаУдаления = ЛОЖЬ
	|	И Командировка.Организация = &Организация
	|	И Командировка.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОплатаПоСреднемуЗаработку.Ссылка,
	|	ОплатаПоСреднемуЗаработку.Сотрудник,
	|	ОплатаПоСреднемуЗаработку.ДатаНачала,
	|	ОплатаПоСреднемуЗаработку.СреднийЗаработок,
	|	0,
	|	ОплатаПоСреднемуЗаработку.РучнаяКорректировкаСреднегоЗаработка,
	|	ЛОЖЬ,
	|	ОплатаПоСреднемуЗаработку.ВидРасчета,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка),
	|	0,
	|	0
	|ИЗ
	|	Документ.ОплатаПоСреднемуЗаработку КАК ОплатаПоСреднемуЗаработку
	|ГДЕ
	|	ОплатаПоСреднемуЗаработку.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ОплатаПоСреднемуЗаработку.ПометкаУдаления = ЛОЖЬ
	|	И ОплатаПоСреднемуЗаработку.Организация = &Организация
	|	И ОплатаПоСреднемуЗаработку.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Отпуск.Ссылка,
	|	Отпуск.Сотрудник,
	|	Отпуск.ДатаНачалаСобытия,
	|	Отпуск.СреднийЗаработок,
	|	0,
	|	Отпуск.РучнаяКорректировкаСреднегоЗаработка,
	|	ЛОЖЬ,
	|	Отпуск.ВидРасчетаОсновногоОтпуска,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка),
	|	0,
	|	0
	|ИЗ
	|	Документ.Отпуск КАК Отпуск
	|ГДЕ
	|	Отпуск.ДатаНачалаСобытия МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Отпуск.ПометкаУдаления = ЛОЖЬ
	|	И Отпуск.Организация = &Организация
	|	И Отпуск.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Увольнение.Ссылка,
	|	Увольнение.Сотрудник,
	|	Увольнение.ДатаУвольнения,
	|	Увольнение.СреднийЗаработок,
	|	Увольнение.СреднийЗаработокВыходногоПособия,
	|	Увольнение.РучнаяКорректировкаСреднегоЗаработка,
	|	Увольнение.РучнаяКорректировкаСреднегоЗаработкаВыходногоПособия,
	|	Увольнение.ВидРасчетаКомпенсацииУдержанияОтпуска,
	|	Увольнение.ВыходноеПособие,
	|	Увольнение.ДнейКомпенсацииУдержанияОтпуска,
	|	Увольнение.ДнейЧасовВыходногоПособия
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.ДатаУвольнения МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Увольнение.ПометкаУдаления = ЛОЖЬ
	|	И Увольнение.Организация = &Организация
	|	И Увольнение.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПростойСотрудниковНачисления.Ссылка,
	|	ПростойСотрудниковНачисления.Сотрудник,
	|	ПростойСотрудниковНачисления.ДатаНачала,
	|	ПростойСотрудниковПоказатели.Значение,
	|	0,
	|	ПростойСотрудниковНачисления.ФиксЗаполнение,
	|	ЛОЖЬ,
	|	ПростойСотрудниковНачисления.Ссылка.Начисление,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка),
	|	0,
	|	0
	|ИЗ
	|	Документ.ПростойСотрудников.Начисления КАК ПростойСотрудниковНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПростойСотрудников.Показатели КАК ПростойСотрудниковПоказатели
	|		ПО ПростойСотрудниковНачисления.ИдентификаторСтрокиВидаРасчета = ПростойСотрудниковПоказатели.ИдентификаторСтрокиВидаРасчета
	|ГДЕ
	|	ПростойСотрудниковНачисления.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПростойСотрудниковНачисления.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ПростойСотрудниковНачисления.Ссылка.Организация = &Организация
	|	И ПростойСотрудниковНачисления.Ссылка.ВидПростоя = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ПростойНеЗависящийОтРаботодателяИРаботника)
	|	И ПростойСотрудниковПоказатели.Показатель = ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокОбщий)
	|	И ПростойСотрудниковНачисления.Ссылка.Проведен = ИСТИНА";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СреднийЗаработок = 0; СреднийЗаработокВыходногоПособия = 0;
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.Командировка") Или ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку")
			Или ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ПростойСотрудников") Тогда
			ДополнительныеПараметры.ВидУчетаВремениДляСредней = ОпределитьВидУчетаВремениДляСредней(Выборка.ДатаНачалаСобытия, Выборка.Сотрудник); 
			ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010;
			СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, Выборка.ДатаНачалаСобытия, ДополнительныеПараметры);
		ИначеЕсли  ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.Отпуск") Тогда
			ДополнительныеПараметры.ПоЧасам = Ложь;
			ДополнительныеПараметры.Начисление = Выборка.ВидРасчета;
			ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные;
			СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, Выборка.ДатаНачалаСобытия, ДополнительныеПараметры);
		КонецЕсли;
		Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.Увольнение") Тогда
			Если Выборка.ДнейКомпенсацииУдержанияОтпуска <> 0 Тогда
				ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
				ДополнительныеПараметры.ПоЧасам = Ложь;	
				ДополнительныеПараметры.Начисление = Выборка.ВидРасчета;
				ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные;
				СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, Выборка.ДатаНачалаСобытия, ДополнительныеПараметры);
			КонецЕсли;
			Если Выборка.ДнейЧасовВыходногоПособия <> 0 Тогда
				ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
				ДополнительныеПараметры.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням; 
				ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010;
			    ДополнительныеПараметры.Начисление = Выборка.ВидРасчетаВыходноеПособие;
				СреднийЗаработокВыходногоПособия = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, Выборка.ДатаНачалаСобытия, ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;	
		
		Если ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010 И СреднийЗаработок = 0
			Или ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.Увольнение") И Выборка.ДнейЧасовВыходногоПособия <> 0 И СреднийЗаработокВыходногоПособия = 0 Тогда
			
			КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Выборка.Сотрудник, "ДатаПриема");
			Если ЗначениеЗаполнено(КадровыеДанные[0].ДатаПриема) И КадровыеДанные[0].ДатаПриема > НачалоМесяца(ДобавитьМесяц(Выборка.ДатаНачалаСобытия,-2)) Тогда
				ПериодРасчетаСреднего = УчетСреднегоЗаработка.ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(КадровыеДанные[0].ДатаПриема, Выборка.Сотрудник, Выборка.ВидРасчета);
				ДополнительныеПараметры.НачалоПериода = ПериодРасчетаСреднего.ДатаНачала;
				ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчетаСреднего.ДатаОкончания;
				СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, КадровыеДанные[0].ДатаПриема, ДополнительныеПараметры);
			Иначе
				ПериодРасчетаСреднего = УчетСреднегоЗаработка.ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(ДобавитьМесяц(Выборка.ДатаНачалаСобытия,-2), Выборка.Сотрудник, Выборка.ВидРасчета);
				ДополнительныеПараметры.НачалоПериода = ПериодРасчетаСреднего.ДатаНачала;
				ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчетаСреднего.ДатаОкончания;
				СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Выборка.Сотрудник, ДобавитьМесяц(Выборка.ДатаНачалаСобытия,-2), ДополнительныеПараметры);
			КонецЕсли;	
			
			Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.Увольнение") И Выборка.ДнейЧасовВыходногоПособия <> 0 И СреднийЗаработокВыходногоПособия = 0 Тогда
				СреднийЗаработокВыходногоПособия = СреднийЗаработок
			КонецЕсли	
		КонецЕсли;	
		
		Если Выборка.СреднийЗаработокВДокументе <> СреднийЗаработок ИЛИ Выборка.СреднийЗаработокВыходногоПособияВДокументе <> СреднийЗаработокВыходногоПособия Тогда
			СтрокаОтбора = Новый Структура;
			СтрокаОтбора.Вставить("Документ", Выборка.Ссылка);
			СтрокаОтбора.Вставить("Сотрудник", Выборка.Сотрудник);
			СтрокаОтбора.Вставить("СреднийЗаработок", СреднийЗаработок);
			СтрокаОтбора.Вставить("СреднийЗаработокВДокументе", Выборка.СреднийЗаработокВДокументе);
			СтрокаОтбора.Вставить("СреднийЗаработокВыходногоПособия", СреднийЗаработокВыходногоПособия);
			СтрокаОтбора.Вставить("СреднийЗаработокВыходногоПособияВДокументе", Выборка.СреднийЗаработокВыходногоПособияВДокументе);
			Если Выборка.РучноеИсправление Или Выборка.РучноеИсправлениеВыходногоПособия Тогда
				СтрокаОтбора.Вставить("РучноеИсправление", "+")
			Иначе
				СтрокаОтбора.Вставить("РучноеИсправление", "")
			КонецЕсли;	
			МассивДокументов.Добавить(СтрокаОтбора);		
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МассивДокументов

КонецФункции

&НаСервере
Функция ОпределитьВидУчетаВремениДляСредней(ДатаНачалаСобытия, Сотрудник)
			
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ОсновноеНачисление", ДатаНачалаСобытия);
	
	ВремяВЧасах = Ложь;
	Если КадровыеДанные.Количество() > 0 Тогда
		Если НЕ КадровыеДанные[0].ОсновноеНачисление = Null Тогда
			Если НЕ КадровыеДанные[0].ОсновноеНачисление.УчетВремениВЧасах = Null Тогда
				ВидУчетаВремениДляСредней = ?(КадровыеДанные[0].ОсновноеНачисление.УчетВремениВЧасах,Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам,
											Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням);
			Иначе
				ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням;
			КонецЕсли;									
		Иначе
			ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням;
		КонецЕсли;	

	КонецЕсли;
	Возврат ВидУчетаВремениДляСредней 

КонецФункции	

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СписокДокументов.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	СписокДокументов.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПоказатьЗначение(,СписокДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока).Документ);
КонецПроцедуры





