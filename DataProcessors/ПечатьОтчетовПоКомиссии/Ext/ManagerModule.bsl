#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОтчетКомиссионера") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ОтчетКомиссионера",
			НСтр("ru='Отчет комиссионера';uk='Звіт комісіонера'"),
			СформироватьПечатнуюФормуОтчетПоКомиссии(СтруктураТипов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода),
			,                                                                                                         
			,
			,
			Истина // ЭтоМногоязычнаяПечатнаяФорма
		);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОтчетКомиссионераСписание") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ОтчетКомиссионераСписание",
			НСтр("ru='Отчет комиссионера';uk='Звіт комісіонера'"),
			СформироватьПечатнуюФормуОтчетПоКомиссииСписание(СтруктураТипов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода),
			,
			,
			,
			Истина // ЭтоМногоязычнаяПечатнаяФорма
		);
	КонецЕсли;

	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, СтруктураТипов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

#Область Печать_ОтчетПоКомиссии

Функция СформироватьПечатнуюФормуОтчетПоКомиссии(СтруктураТипов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода)	

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтчетПоКомиссии";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		Если СтруктураОбъектов.Ключ = "Документ.ОтчетКомиссионераОСписании"
			Или СтруктураОбъектов.Ключ = "Документ.ОтчетКомитентуОСписании"
			Или СтруктураОбъектов.Ключ = "Документ.ОтчетКомитентуОЗакупках" Тогда
			Продолжить;
		КонецЕсли;

		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыОтчетПоКомиссии(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		ЗаполнитьТабличныйДокументОтчетПоКомиссииОПродажах(
			ТабличныйДокумент,
			ДанныеДляПечати,
			ОбъектыПечати,
			ПараметрыВывода
		);	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуОтчетПоКомиссииСписание(СтруктураТипов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода)	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтчетПоКомиссииСписание";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		Если СтруктураОбъектов.Ключ = "Документ.ОтчетКомиссионера"
			Или СтруктураОбъектов.Ключ = "Документ.ОтчетКомитенту"
			Или СтруктураОбъектов.Ключ = "Документ.ОтчетКомитентуОЗакупках" Тогда
			Продолжить;
		КонецЕсли;
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыОтчетПоКомиссииОСписании(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		ЗаполнитьТабличныйДокументОтчетПоКомиссииОСписании(
			ТабличныйДокумент,
			ДанныеДляПечати,
			ОбъектыПечати,
			ПараметрыВывода
		);	

	КонецЦикла;

	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьРеквизитыШапкиОтчетПоКомиссии(ДанныеПечати, Заголовок, Макет, ТабличныйДокумент, КодЯзыкаПечать)	

	// Выводим общие реквизиты шапки
	СведенияОКомитенте    = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Комитент, ДанныеПечати.Дата, , ДанныеПечати.БанковскийСчетКомитента);
	СведенияОКомиссионере = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Комиссионер, ДанныеПечати.Дата, , ДанныеПечати.БанковскийСчетКомиссионера);

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекстЗаголовка", ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, Заголовок, КодЯзыкаПечать));
	СтруктураПараметров.Вставить("ПредставлениеКомитента", ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКомитенте, "ПолноеНаименование,"));
	СтруктураПараметров.Вставить("ПредставлениеКомиссионера", ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКомиссионере, "ПолноеНаименование,"));
	
	Если ДанныеПечати.ЭтоОтчетКомиссионера Тогда
		СписокСведенийКомитента    = "НомерСчета,Банк,МФО,/,ЮридическийАдрес,Телефоны,/,КодПоЕДРПОУ,КодПоДРФО,ИНН,НомерСвидетельства,/,ИнформацияОСтатусеПлательщикаНалогов";
		СписокСведенийКомиссионера = "ФактическийАдрес,Телефоны";
	Иначе                              
		СписокСведенийКомитента    = "ФактическийАдрес,Телефоны";
		СписокСведенийКомиссионера = "НомерСчета,Банк,МФО,/,ЮридическийАдрес,Телефоны,/,КодПоЕДРПОУ,КодПоДРФО,ИНН,НомерСвидетельства,/,ИнформацияОСтатусеПлательщикаНалогов";		
	КонецЕсли;
	СтруктураПараметров.Вставить("РеквизитыКомитента",    ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКомитенте, СписокСведенийКомитента, Истина, КодЯзыкаПечать));
	СтруктураПараметров.Вставить("РеквизитыКомиссионера", ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКомиссионере, СписокСведенийКомиссионера, Истина, КодЯзыкаПечать));

	ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Выводим дополнительно информацию о договоре и заказе
	СписокДополнительныхПараметров = "ДоговорНаименованиеДляПечати,";	
	МассивСтруктурСтрок = ФормированиеПечатныхФорм.ДополнительнаяИнформация(ДанныеПечати, СписокДополнительныхПараметров, КодЯзыкаПечать);
	ОбластьМакета = Макет.ПолучитьОбласть("ДопИнформация");
	Для каждого СтруктураСтроки Из МассивСтруктурСтрок Цикл
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;		

КонецПроцедуры

Процедура ЗаполнитьРеквизитыПодвалаОтчетПоКомисии(ДанныеПечати, ВыборкаПоДокументам, Макет, ТабличныйДокумент, КодЯзыкаПечать)	

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СуммаКомитентаВсего", ВыборкаПоДокументам.СуммаКомитента);
	СтруктураПараметров.Вставить("СуммаПродажиВсего", ВыборкаПоДокументам.СуммаПродажи);
	СтруктураПараметров.Вставить("СуммаВознагражденияВсего", ВыборкаПоДокументам.СуммаВознаграждения);
	
	Если ВыборкаПоДокументам.СуммаПродажи < 0 Тогда
		ТекстИтоговаяСтрока = НСтр("ru='Всего возвращено наименований ';uk= 'Всього повернуто найменувань '", КодЯзыкаПечать);	
	Иначе
		ТекстИтоговаяСтрока = НСтр("ru='Всего продано наименований ';uk= 'Всього продано найменувань '", КодЯзыкаПечать);	
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ИтоговаяСтрока", ТекстИтоговаяСтрока + " "
		+ ВыборкаПоДокументам.Выбрать().Количество()
		+ ", " + НСтр("ru='на сумму';uk='на суму'") + " "
		+ ФормированиеПечатныхФорм.ФорматСумм(ВыборкаПоДокументам.СуммаПродажи, ДанныеПечати.Валюта));
	
	СтруктураПараметров.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		ВыборкаПоДокументам.СуммаПродажи, 
		ДанныеПечати.Валюта,
		,
		КодЯзыкаПечать
		)	
	);
	
	Если ДанныеПечати.СуммаВознаграждения <> 0 Тогда
		СтруктураПараметров.Вставить("СуммаВознаграждения", НСтр("ru='Сумма комиссионного вознаграждения составила';uk= 'Сума комісійної винагороди склала'", КодЯзыкаПечать) + " "
			+ ?(ДанныеПечати.СуммаВознаграждения < 0, НСтр("ru='минус';uk= 'мінус'", КодЯзыкаПечать) + " ", "")
			+ РаботаСКурсамиВалют.СформироватьСуммуПрописью(ДанныеПечати.СуммаВознаграждения, ДанныеПечати.Валюта, , КодЯзыкаПечать));
	КонецЕсли;
	
	ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);

	Если ЗначениеЗаполнено(ДанныеПечати.МестоСоставленияДокумента) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("МестоСоставления");
		СтруктураПараметров.Вставить("МестоСоставления", СокрЛП(ДанныеПечати.МестоСоставленияДокумента));
		ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	ДанныеДляФормированияПодписей = Новый Структура("ПредставительОрганизации, ПредставительОрганизацииДолжность, ПредставительКонтрагента, ПредставительОрганизацииПолучателя, ПредставительОрганизацииПолучателяДолжность");
	Если ДанныеПечати.ЭтоОтчетКомиссионера Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписиОтчетКомиссионера");
		ДанныеДляФормированияПодписей.ПредставительОрганизации                    = ДанныеПечати.ПредставительКомитента;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииДолжность           = ДанныеПечати.ПредставительКомитентаДолжность;
		ДанныеДляФормированияПодписей.ПредставительКонтрагента                    = ДанныеПечати.ПредставительКомиссионераСтрокой;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателяДолжность = ДанныеПечати.ПредставительКомиссионераДолжность;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателя          = ДанныеПечати.ПредставительКомиссионера;
	Иначе
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписиОтчетКомитенту");
		ДанныеДляФормированияПодписей.ПредставительОрганизации                    = ДанныеПечати.ПредставительКомиссионера;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииДолжность           = ДанныеПечати.ПредставительКомиссионераДолжность;
		ДанныеДляФормированияПодписей.ПредставительКонтрагента                    = ДанныеПечати.ПредставительКомитентаСтрокой;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателяДолжность = ДанныеПечати.ПредставительКомитентаДолжность;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателя          = ДанныеПечати.ПредставительКомитента;
	КонецЕсли;
	
	ОбластьМакета.Параметры.Заполнить(ФормированиеПечатныхФорм.СведенияОбОтветсвенныхЛицах(ДанныеДляФормированияПодписей, КодЯзыкаПечать));
	ТабличныйДокумент.Вывести(ОбластьМакета);

КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументОтчетПоКомиссииОПродажах(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыВывода) 
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;	
	
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьОтчетовПоКомиссии.ПФ_MXL_ОтчетПоКомиссии", КодЯзыкаПечать);
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	
	ДанныеПечати      	= ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
	
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВыборкаПоДокументам.Сбросить();
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		Если ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска) Тогда
			
			Если ДанныеПечати.ЭтоОтчетКомиссионера Тогда
				Заголовок = НСтр("ru='Отчет комиссионера о продажах';uk= 'Звіт комісіонера про продажі'", КодЯзыкаПечать); 
			Иначе
				Заголовок = НСтр("ru='Отчет комитенту о продажах';uk= 'Звіт комітенту про продажі'", КодЯзыкаПечать);
			КонецЕсли;

			// Выводим общие реквизиты шапки
			ЗаполнитьРеквизитыШапкиОтчетПоКомиссии(
				ДанныеПечати,
				Заголовок,
				Макет,
				ТабличныйДокумент,
				КодЯзыкаПечать);

			СтруктураПараметров = Новый Структура;
			
			// Выводим заголовок таблицы
			Если ЗначениеЗаполнено(КолонкаКодов) Тогда
				ЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицыСКодами");
				СтруктураПараметров.Вставить("ИмяКолонкиКодов", КолонкаКодов);
			Иначе
				ЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
			КонецЕсли;
			СтруктураПараметров.Вставить("Валюта", ДанныеПечати.Валюта);
			ЗаголовокТаблицы.Параметры.Заполнить(СтруктураПараметров);
			ЗаполнитьЗначенияСвойств(ЗаголовокТаблицы.Параметры, ФормированиеПечатныхФорм.СформироватьЗаголовкиДляСуммовыхПоказателей(ДанныеПечати, КодЯзыкаПечать, Истина));
			ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
			// Выводим многострочную часть документа
			Если ЗначениеЗаполнено(КолонкаКодов) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСКодами");
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			КонецЕсли;
				
			НомерСтроки = 0;
			
			СтрокаТовары = ВыборкаПоДокументам.Выбрать();
			Пока СтрокаТовары.Следующий() Цикл
				НомерСтроки = НомерСтроки + 1;
				
				ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
				
				СтруктураПараметров = Новый Структура;
				
				Если ЗначениеЗаполнено(КолонкаКодов) Тогда
					СтруктураПараметров.Вставить("Артикул", СтрокаТовары[КолонкаКодов]);
				КонецЕсли;
				
				СтруктураПараметров.Вставить("НомерСтроки", НомерСтроки);
				СтруктураПараметров.Вставить("ТоварНаименование", НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					СтрокаТовары.ТоварНаименование,
					СтрокаТовары.Характеристика));
				СтруктураПараметров.Вставить("ЦенаКомитента", ?(СтрокаТовары.Количество = 0, 0, Окр(СтрокаТовары.СуммаКомитента / СтрокаТовары.Количество, 2, 1)));
				СтруктураПараметров.Вставить("ЦенаПродажи", ?(СтрокаТовары.Количество = 0, 0, Окр(СтрокаТовары.СуммаПродажи / СтрокаТовары.Количество, 2, 1)));
				
				ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			
			ЗаполнитьРеквизитыПодвалаОтчетПоКомисии(
				ДанныеПечати,
				ВыборкаПоДокументам,
				Макет,
				ТабличныйДокумент,
				КодЯзыкаПечать
			);

			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент,
				НомерСтрокиНачало,
				ОбъектыПечати,
				ДанныеПечати.Ссылка);
			
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьРеквизитыПодвалаОтчетПоКомиссииОСписании(ДанныеПечати, ВыборкаПоДокументам, Макет, ТабличныйДокумент, КодЯзыкаПечать)

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СуммаВсего", ВыборкаПоДокументам.Сумма);
	СтруктураПараметров.Вставить("ИтоговаяСтрока", НСтр("ru='Всего списано наименований';uk= 'Всього списано найменувань'", КодЯзыкаПечать) + " "
		+ ВыборкаПоДокументам.Выбрать().Количество()
		+ ?(ВыборкаПоДокументам.Сумма <> 0, ", " + НСтр("ru='на сумму';uk= 'на суму'", КодЯзыкаПечать) + " ", "")
		+ ?(ВыборкаПоДокументам.Сумма <> 0, ФормированиеПечатныхФорм.ФорматСумм(ВыборкаПоДокументам.Сумма, ДанныеПечати.Валюта), ""));
	СтруктураПараметров.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		ВыборкаПоДокументам.Сумма, 
		ДанныеПечати.Валюта,
		,
		КодЯзыкаПечать
		)
	);


	ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	Если ЗначениеЗаполнено(ДанныеПечати.МестоСоставленияДокумента) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("МестоСоставления");
		СтруктураПараметров.Вставить("МестоСоставления", СокрЛП(ДанныеПечати.МестоСоставленияДокумента));
		ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	ДанныеДляФормированияПодписей = Новый Структура("ПредставительОрганизации, ПредставительОрганизацииДолжность, ПредставительКонтрагента, ПредставительОрганизацииПолучателя, ПредставительОрганизацииПолучателяДолжность");
	Если ДанныеПечати.ЭтоОтчетКомиссионера Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписиОтчетКомиссионера");
		ДанныеДляФормированияПодписей.ПредставительОрганизации                    = ДанныеПечати.ПредставительКомитента;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииДолжность           = ДанныеПечати.ПредставительКомитентаДолжность;
		ДанныеДляФормированияПодписей.ПредставительКонтрагента                    = ДанныеПечати.ПредставительКомиссионераСтрокой;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателяДолжность = ДанныеПечати.ПредставительКомиссионераДолжность;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателя          = ДанныеПечати.ПредставительКомиссионера;
	Иначе
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписиОтчетКомитенту");
		ДанныеДляФормированияПодписей.ПредставительОрганизации                    = ДанныеПечати.ПредставительКомиссионера;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииДолжность           = ДанныеПечати.ПредставительКомиссионераДолжность;
		ДанныеДляФормированияПодписей.ПредставительКонтрагента                    = ДанныеПечати.ПредставительКомитентаСтрокой;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателяДолжность = ДанныеПечати.ПредставительКомитентаДолжность;
		ДанныеДляФормированияПодписей.ПредставительОрганизацииПолучателя          = ДанныеПечати.ПредставительКомитента;
	КонецЕсли;
	
	ОбластьМакета.Параметры.Заполнить(ФормированиеПечатныхФорм.СведенияОбОтветсвенныхЛицах(ДанныеДляФормированияПодписей, КодЯзыкаПечать));
	ТабличныйДокумент.Вывести(ОбластьМакета);

КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументОтчетПоКомиссииОСписании(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыВывода) 
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьОтчетовПоКомиссии.ПФ_MXL_ОтчетПоКомиссииОСписании", КодЯзыкаПечать);

	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();

	ДанныеПечати      	= ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
	
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		Если ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска) Тогда
			
			Если ДанныеПечати.ЭтоОтчетКомиссионера Тогда
				Заголовок = НСтр("ru='Отчет комиссионера о списании';uk= 'Звіт комісіонера про списання'", КодЯзыкаПечать);
			Иначе
				Заголовок = НСтр("ru='Отчет комитенту о списании';uk= 'Звіт комітенту про списання'", КодЯзыкаПечать);
			КонецЕсли;

			// Выводим общие реквизиты шапки
			ЗаполнитьРеквизитыШапкиОтчетПоКомиссии(
				ДанныеПечати,
				Заголовок,
				Макет,
				ТабличныйДокумент,
				КодЯзыкаПечать
			);

			СтруктураПараметров = Новый Структура;
			
			// Выводим заголовок таблицы
			Если ЗначениеЗаполнено(КолонкаКодов) Тогда
				ЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицыСКодами");
				СтруктураПараметров.Вставить("ИмяКолонкиКодов", КолонкаКодов);
			Иначе
				ЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
			КонецЕсли;
			СтруктураПараметров.Вставить("Валюта", ДанныеПечати.Валюта);
			ЗаголовокТаблицы.Параметры.Заполнить(СтруктураПараметров);
			ЗаполнитьЗначенияСвойств(ЗаголовокТаблицы.Параметры, ФормированиеПечатныхФорм.СформироватьЗаголовкиДляСуммовыхПоказателей(ДанныеПечати, КодЯзыкаПечать));
			ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
			// Выводим многострочную часть документа
			Если ЗначениеЗаполнено(КолонкаКодов) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСКодами");
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			КонецЕсли;
				
			НомерСтроки = 0;
			
			СтрокаТовары = ВыборкаПоДокументам.Выбрать();
			Пока СтрокаТовары.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				
				ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
				
				СтруктураПараметров = Новый Структура;
				
				Если ЗначениеЗаполнено(КолонкаКодов) Тогда
					СтруктураПараметров.Вставить("Артикул", СтрокаТовары[КолонкаКодов]);
				КонецЕсли;
				
				СтруктураПараметров.Вставить("НомерСтроки", НомерСтроки);
				СтруктураПараметров.Вставить("ТоварНаименование", НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					СтрокаТовары.ТоварНаименование,
					СтрокаТовары.Характеристика));
				СтруктураПараметров.Вставить("Цена", ?(СтрокаТовары.Количество = 0, 0, Окр(СтрокаТовары.Сумма / СтрокаТовары.Количество, 2, 1)));
				
				ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			КонецЦикла;
			
			ЗаполнитьРеквизитыПодвалаОтчетПоКомиссииОСписании(
				ДанныеПечати,
				ВыборкаПоДокументам,
				Макет,
				ТабличныйДокумент,
				КодЯзыкаПечать
			);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент,
				НомерСтрокиНачало,
				ОбъектыПечати,
				ДанныеПечати.Ссылка);
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли