#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда	

#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	ДанныеДокументов = ДанныеДокументовРасчетаСреднегоЗаработка(МассивОбъектов);

	// Проверяем, нужно ли для макета РасчетСреднегоЗаработка формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработка") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовРасчетаСреднегоЗаработка(МассивОбъектов);
		ТабличныйДокумент = ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, "РасчетСреднегоЗаработка",, ПараметрыВывода);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработка", НСтр("ru='Расчет среднего заработка';uk='Розрахунок середнього заробітку'"), ТабличныйДокумент,,,,Истина);
		КонецЕсли;
	КонецЕсли;
	
	// Проверяем, нужно ли для макета РасчетСреднегоЗаработкаВыходногоПособия формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаВыходногоПособия") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ДанныеДокументов = ДанныеДокументовРасчетаСреднегоЗаработка(МассивОбъектов);
		ТабличныйДокумент = ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, "РасчетСреднегоЗаработкаВыходногоПособия",, ПараметрыВывода);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РасчетСреднегоЗаработкаВыходногоПособия", НСтр("ru='Расчет среднего заработка (для выходного пособия)';uk='Розрахунок середнього заробітку (для вихідної допомоги)'"), ТабличныйДокумент,,,,Истина);
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

#Область ОбщийСреднийЗаработок

// Функция возвращает табличный документ - печатную форму расчета среднего заработка.
//
Функция ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, ИмяМакета, ВыводитьЗаголовок = Истина, ПараметрыВывода) Экспорт
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетСреднегоЗаработка";
	
	Если ИмяМакета = "РасчетСреднегоЗаработкаКомпактный" Тогда
		ЗаполнитьТабличныйДокументРасчетаСреднегоЗаработкаКомпактный(ТабличныйДокумент, ОбъектыПечати, ИмяМакета, ДанныеДокументов, КодЯзыкаПечать);			
	Иначе	
		ЗаполнитьТабличныйДокументРасчетаСреднегоЗаработка(ТабличныйДокумент, ОбъектыПечати, ИмяМакета, ДанныеДокументов, ВыводитьЗаголовок, КодЯзыкаПечать);							
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 	

// Подготавливает табличный документ с печатными формами для массива ссылок.
//
// Параметры:
//	МассивСсылок 		- массив ссылок на документы поддерживающие печать среднего заработка.
//	ВыводитьЗаголовок 	- признак того, что надо формировать полную печатную форму.
//	ИмяМакета 			- имя варианта печати среднего заработка.
//
// Возвращаемое значение - структура в которой содержатся
// 		- ТабличныйДокумент, табличный документ с областями для каждой ссылки из массива ссылок.
// 		- ОбъектыПечати, соответствие, ключом которой является ссылка, а значением - имя области табличного документа,
// 			в которой хранится печатная форма для этой ссылки.
//
Функция ОбластиДляВстраивания(ДанныеДокументов, ИмяМакета, ВыводитьЗаголовок = Ложь, ПараметрыВывода) Экспорт
	
	ОбъектыПечати = ОбъектыПечатиДляВстраиваемыхОбластей();
	
	ТабличныйДокумент = ТабличныйДокументРасчетаСреднегоЗаработка(ДанныеДокументов, ОбъектыПечати, ИмяМакета, ВыводитьЗаголовок, ПараметрыВывода);
	
	Возврат Новый Структура("ОбъектыПечати,ТабличныйДокумент", ОбъектыПечати, ТабличныйДокумент);
	
КонецФункции

#Область ПолныйВариант

Процедура ЗаполнитьТабличныйДокументРасчетаСреднегоЗаработка(ТабличныйДокумент, ОбъектыПечати, ИмяМакета, ДанныеДокументов, ВыводитьЗаголовок, КодЯзыкаПечать)
		
	ЗначенияПараметров = Новый Структура;
	
	Для каждого ДанныеДокумента Из ДанныеДокументов Цикл
		
		Если ТипЗнч(ДанныеДокумента.РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.Командировка") Тогда
			ОплатитьПоДаннымФОТ = ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ;
		Иначе
			ОплатитьПоДаннымФОТ = Ложь;
		КонецЕсли;
		
		ЕстьВидОтпуска = Ложь;
		Если ДанныеДокумента.РеквизитыДокумента.Свойство("ВидОтпуска") Тогда
			Если ЗначениеЗаполнено(ДанныеДокумента.РеквизитыДокумента.ВидОтпуска) Тогда
				ЕстьВидОтпуска = Истина;
			КонецЕсли;
		КонецЕсли;			
		
		Если ОплатитьПоДаннымФОТ = Истина Тогда
			ИспользуетсяИндексация = Ложь;
		Иначе
			ИспользуетсяИндексация = ИспользуетсяИндексация(ДанныеДокумента.ДанныеРасчетаСреднего.Заработок);
		КонецЕсли;

		Если ОплатитьПоДаннымФОТ = Истина Тогда
			ДанныеОЗаработке = ДанныеДокумента.ДанныеРасчетаСреднего.Заработок;
		Иначе
			ДанныеОЗаработке = ПреобразоватьДанныеОЗаработкеВТаблицыЗначений(ДанныеДокумента.ДанныеРасчетаСреднего.Заработок);
			Если ДанныеДокумента.ДанныеРасчетаСреднего.Свойство("ИндексированнаяГодоваяПремия") и ДанныеОЗаработке.Свойство("ПремииГодовые") Тогда
				Если Не ДанныеДокумента.ДанныеРасчетаСреднего.ИндексированнаяГодоваяПремия.Количество()=0 Тогда
					ДанныеОЗаработке.ПремииГодовые.Очистить();
					ИндексированнаяГодоваяПремия=ДанныеДокумента.ДанныеРасчетаСреднего.ИндексированнаяГодоваяПремия;
					Для Каждого СтрокаЗнач Из ИндексированнаяГодоваяПремия Цикл
						СтрокаПремияГодовая=ДанныеОЗаработке.ПремииГодовые.Добавить();
						СтрокаПремияГодовая.Месяц=СтрокаЗнач.Ключ;
						СтрокаПремияГодовая.Учтено=СтрокаЗнач.Значение;
					КонецЦикла;
					Если ДанныеОЗаработке.ПремииГодовые.Количество() > 0 Тогда
						ДанныеОЗаработке.ПремииГодовые.Сортировать("Месяц");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ОтработанноеВремя = ПреобразоватьДанныеОВремениВТаблицуЗначений(ДанныеДокумента.ДанныеРасчетаСреднего.ОтработанноеВремя);

		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовим макеты для формирования табличного документа.
		ОбластиМакета = ОбластиМакетаСреднегоЗаработка(ИспользуетсяИндексация, ДанныеДокумента.ПараметрыРасчета.СпособРасчета, КодЯзыкаПечать, ЕстьВидОтпуска);
		
		Если ВыводитьЗаголовок Тогда
			ВывестиШапку(ТабличныйДокумент, ОбластиМакета.ОбластьШапка, ДанныеДокумента.РеквизитыДокумента, ДанныеДокумента.КадровыеДанныеСотрудника, ДанныеДокумента.ПараметрыРасчета, КодЯзыкаПечать);
		КонецЕсли;
		
		РезультирующаяТаблица = Новый ТаблицаЗначений;
		
		Если ОплатитьПоДаннымФОТ Тогда
			РезультирующаяТаблица = ДанныеОЗаработке;
		Иначе
			ОбъединитьТаблицыДляВывода(РезультирующаяТаблица, ДанныеОЗаработке, ОтработанноеВремя.Скопировать(), ИспользуетсяИндексация);
		КонецЕсли;
		
		// таблица с расчетом среднего заработка
		Если ДанныеДокумента.РеквизитыДокумента.ДатаНачалаСобытия >= Дата(2020,12,12) Тогда
			Если ОплатитьПоДаннымФОТ Тогда
				ВывестиОтработанноеВремя1213(ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней, ТабличныйДокумент, ОбластиМакета.ПоДаннымФОТШапка, ОбластиМакета.ПоДаннымФОТСтрока, ОбластиМакета.ПоДаннымФОТРасшифровка,ОбластиМакета.ИтогПодробно,,,РезультирующаяТаблица, ОплатитьПоДаннымФОТ, ДанныеДокумента.РеквизитыДокумента.НормаВДнях, ДанныеДокумента.РеквизитыДокумента.НормаВЧасах, КодЯзыкаПечать);
			Иначе
				// вид учета времени важен для документов ОплатаДнейПоСреденмуЗаработку и Командировка, т.к. у них м.б. внутрисменные командировки и средняя для сотро-в имеющих часовой оклад считается как среднечасовая
				ВывестиОтработанноеВремя1213(?(ДанныеДокумента.РеквизитыДокумента.Свойство("ВидУчетаВремениДляСредней"), ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней, Неопределено), ТабличныйДокумент, ОбластиМакета.ОбластьШапкаОтработанногоВремени, ОбластиМакета.ОбластьСтрокаОтработанногоВремени, ОбластиМакета.ОбластьПодвалОтработанногоВремени,ОбластиМакета.ИтогПодробно, ОбластиМакета.ОбластьПодвалОтработанногоВремениСУчетомОграничений,ОбластиМакета.ИтогПодробноСУчетомОграничений, РезультирующаяТаблица, ОплатитьПоДаннымФОТ, ДанныеДокумента.РеквизитыДокумента.НормаВДнях, ДанныеДокумента.РеквизитыДокумента.НормаВЧасах, КодЯзыкаПечать);
			КонецЕсли;
		Иначе	
			Если ОплатитьПоДаннымФОТ Тогда 
				ВывестиОтработанноеВремя(ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней, ТабличныйДокумент, ОбластиМакета.ПоДаннымФОТШапка, ОбластиМакета.ПоДаннымФОТСтрока, ОбластиМакета.ПоДаннымФОТРасшифровка, ОбластиМакета.ИтогПодробно, РезультирующаяТаблица, ОплатитьПоДаннымФОТ, ДанныеДокумента.РеквизитыДокумента.НормаВДнях, ДанныеДокумента.РеквизитыДокумента.НормаВЧасах, КодЯзыкаПечать);
			Иначе
				ВывестиОтработанноеВремя(ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней, ТабличныйДокумент, ОбластиМакета.ОбластьШапкаОтработанногоВремени, ОбластиМакета.ОбластьСтрокаОтработанногоВремени, ОбластиМакета.ОбластьПодвалОтработанногоВремени, ОбластиМакета.ИтогПодробно, РезультирующаяТаблица, ОплатитьПоДаннымФОТ, ДанныеДокумента.РеквизитыДокумента.НормаВДнях, ДанныеДокумента.РеквизитыДокумента.НормаВЧасах, КодЯзыкаПечать);
			КонецЕсли;
		КонецЕсли;
		//выод начислений из документа
		Если ДанныеДокумента.ДанныеНачислений <> Неопределено И ДанныеДокумента.ДанныеНачислений.Количество() > 0 Тогда
			ВывестиТаблицуНачислений(ТабличныйДокумент, ОбластиМакета.ОбластьНачисленияШапка, ОбластиМакета.ОбластьНачисленияСтрока, ДанныеДокумента.ДанныеНачислений);
		КонецЕсли;
		
		// расшифровка премий будет делатьcя только для постановлениz 917
		Если ДанныеДокумента.РеквизитыДокумента.ДатаНачалаСобытия >= Дата(2021,09,04) И (ДанныеДокумента.Свойство("РасшифровкаПремий")) Тогда
			Если ДанныеДокумента.РасшифровкаПремий.Количество() > 0 И Не ОплатитьПоДаннымФОТ Тогда
				Если ДанныеДокумента.РеквизитыДокумента.ДатаНачалаСобытия >= Дата(2022,4,29) Тогда 
					ВывестиТаблицуРасшифровкаПремий(ТабличныйДокумент, ОбластиМакета.ШапкаРасшифровкаПремий, ОбластиМакета.СтрокаРасшифровкаПремий,ОбластиМакета.СтрокаПериодПремии,ОбластиМакета.ИтогПериодПремии, ОбластиМакета.ИтогПериодПремииСУчетомОграничений,ОбластиМакета, ДанныеДокумента.РасшифровкаПремий, ДанныеОЗаработке.ПремииМесячные, КодЯзыкаПечать);
				Иначе
					ВывестиТаблицуРасшифровкаПремий(ТабличныйДокумент, ОбластиМакета.ШапкаРасшифровкаПремий, ОбластиМакета.СтрокаРасшифровкаПремий,ОбластиМакета.СтрокаПериодПремии,ОбластиМакета.ИтогПериодПремии, ОбластиМакета.ИтогПериодПремииСУчетомОграничений,, ДанныеДокумента.РасшифровкаПремий, , КодЯзыкаПечать);
				КонецЕсли
			КонецЕсли;
		КонецЕсли;
		
		ВывестиОбластьОтветственный(ТабличныйДокумент, ОбластиМакета.ОбластьОтветственный, ДанныеДокумента.РеквизитыДокумента);
		
		
		
		
		
		ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.РеквизитыДокумента.Ссылка, ДанныеДокумента.РеквизитыДокумента.Сотрудник);

	КонецЦикла;
	
КонецПроцедуры 

Процедура ОбъединитьТаблицыДляВывода(РезультирующаяТаблица, ДанныеОЗаработке, ТаблицаОтработанногоВремени, ИспользуетсяИндексация)
	
	СтруктураПромежуточныхТаблиц = ПодготовитьНаборыДанных(ДанныеОЗаработке, ТаблицаОтработанногоВремени);
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОтработанноеВремя", СтруктураПромежуточныхТаблиц.ОтработанноеВремя); 
	Запрос.УстановитьПараметр("МесячныеПремии", СтруктураПромежуточныхТаблиц.ПремииМесячные); 
	Запрос.УстановитьПараметр("ГодовыеПремии", СтруктураПромежуточныхТаблиц.ПремииГодовые); 
	Запрос.УстановитьПараметр("Зароботок", СтруктураПромежуточныхТаблиц.Зароботок); 
	Запрос.УстановитьПараметр("ИспользуетсяИндексация", ИспользуетсяИндексация); 
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПремииМесячные.Месяц,
		|	ПремииМесячные.ОтработаноДней,
		|	ПремииМесячные.ОтработаноЧасов,
		|	ПремииМесячные.КалендарныхДней,
		|	ПремииМесячные.СреднемесячноеКоличествоДней,
		|	ПремииМесячные.Учтено КАК УчтеноВремени
		|ПОМЕСТИТЬ ВТОтработанноеВремя
		|ИЗ
		|	&ОтработанноеВремя КАК ПремииМесячные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МесячныеПремии.Месяц,
		|	МесячныеПремии.Учтено КАК МесячнаяПремия, 
		|	МесячныеПремии.ПревышениеПремии КАК ПревышениеПремии
		|ПОМЕСТИТЬ ВТМесячныеПремии
		|ИЗ
		|	&МесячныеПремии КАК МесячныеПремии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОтработанноеВремя.Месяц,
		|	ВТОтработанноеВремя.ОтработаноДней,
		|	ВТОтработанноеВремя.ОтработаноЧасов,
		|	ВТОтработанноеВремя.КалендарныхДней,
		|	ВТОтработанноеВремя.СреднемесячноеКоличествоДней,
		|	ВТОтработанноеВремя.УчтеноВремени,
		|	ЕСТЬNULL(ВТМесячныеПремии.МесячнаяПремия, 0) КАК МесячнаяПремия, 
		|	ЕСТЬNULL(ВТМесячныеПремии.ПревышениеПремии, 0) КАК ПревышениеПремии	
		|ПОМЕСТИТЬ ВТОтрВремяПремииМесячные
		|ИЗ
		|	ВТОтработанноеВремя КАК ВТОтработанноеВремя
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМесячныеПремии КАК ВТМесячныеПремии
		|		ПО ВТОтработанноеВремя.Месяц = ВТМесячныеПремии.Месяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПремииГодовые.Месяц,
		|	ПремииГодовые.Учтено
		|ПОМЕСТИТЬ ВТГодовыеПремии
		|ИЗ
		|	&ГодовыеПремии КАК ПремииГодовые
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОтрВремяПремииМесячные.Месяц,
		|	ВТОтрВремяПремииМесячные.ОтработаноДней,
		|	ВТОтрВремяПремииМесячные.ОтработаноЧасов,
		|	ВТОтрВремяПремииМесячные.КалендарныхДней,
		|	ВТОтрВремяПремииМесячные.СреднемесячноеКоличествоДней,
		|	ВТОтрВремяПремииМесячные.УчтеноВремени,
		|	ВТОтрВремяПремииМесячные.МесячнаяПремия,
		|	ВТОтрВремяПремииМесячные.ПревышениеПремии,
		|	ЕСТЬNULL(ВТГодовыеПремии.Учтено, 0) КАК ГодоваяПремия
		|ПОМЕСТИТЬ ВТОтработанноеВремяСПремиями
		|ИЗ
		|	ВТОтрВремяПремииМесячные КАК ВТОтрВремяПремииМесячные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГодовыеПремии КАК ВТГодовыеПремии
		|		ПО ВТОтрВремяПремииМесячные.Месяц = ВТГодовыеПремии.Месяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Зароботок.Месяц,
		|	ВЫБОР
		|		КОГДА &ИспользуетсяИндексация
		|			ТОГДА Зароботок.Учтено
		|		ИНАЧЕ Зароботок.Сумма
		|	КОНЕЦ КАК РасчетнаяСумма,
		|	Зароботок.КоэффициентИндексации КАК Коэффициент
		|ПОМЕСТИТЬ ВТЗароботок
		|ИЗ
		|	&Зароботок КАК Зароботок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОтработанноеВремяСПремиями.Месяц КАК Месяц,
		|	ВТОтработанноеВремяСПремиями.ОтработаноДней,
		|	ВТОтработанноеВремяСПремиями.ОтработаноЧасов,
		|	ВТОтработанноеВремяСПремиями.КалендарныхДней,
		|	ВТОтработанноеВремяСПремиями.СреднемесячноеКоличествоДней,
		|	ВТОтработанноеВремяСПремиями.УчтеноВремени,
		|	ВТОтработанноеВремяСПремиями.ГодоваяПремия КАК ГодоваяПремия,
		|	ВТОтработанноеВремяСПремиями.МесячнаяПремия КАК МесячнаяПремия,
		|	ВТОтработанноеВремяСПремиями.ПревышениеПремии КАК ПревышениеПремии,
		|	ЕСТЬNULL(ВТЗароботок.РасчетнаяСумма, 0) КАК СуммаЗароботка,
		|	ЕСТЬNULL(ВТЗароботок.Коэффициент, 1) КАК Коэффициент
		|ИЗ
		|	ВТОтработанноеВремяСПремиями КАК ВТОтработанноеВремяСПремиями
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗароботок КАК ВТЗароботок
		|		ПО ВТОтработанноеВремяСПремиями.Месяц = ВТЗароботок.Месяц
		|
		|УПОРЯДОЧИТЬ ПО
		|	Месяц";
	
	РезультирующаяТаблица = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры	

Функция ПодготовитьНаборыДанных(ДанныеОЗаработке, ТаблицаОтработанногоВремени)

	ОписаниеТиповДата		 = Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
	ОписаниеТиповЧисла4 	 = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(4,0));
	ОписаниеТиповЧисла15	 = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15,2));
	
	//Формирование Таблицы месячных премий за расчетный период
	ПремииМесячные = Новый ТаблицаЗначений;
	ПремииМесячные.Колонки.Добавить("Месяц",ОписаниеТиповДата);
	ПремииМесячные.Колонки.Добавить("Учтено",ОписаниеТиповЧисла15);
	ПремииМесячные.Колонки.Добавить("ПревышениеПремии",ОписаниеТиповЧисла15);
	
	Для каждого Строка Из ДанныеОЗаработке.ПремииМесячные Цикл
		ЗаполнитьЗначенияСвойств(ПремииМесячные.Добавить(),Строка);
	КонецЦикла;
	
	ПремииМесячные.Свернуть("Месяц", "Учтено, ПревышениеПремии");
	
	//Формирование Таблицы отработанного времени за расчетный период
	ОтработанноеВремя = Новый ТаблицаЗначений;
	ОтработанноеВремя.Колонки.Добавить("Месяц",ОписаниеТиповДата);
	ОтработанноеВремя.Колонки.Добавить("ОтработаноДней",ОписаниеТиповЧисла4);
	ОтработанноеВремя.Колонки.Добавить("ОтработаноЧасов",ОписаниеТиповЧисла4);
	ОтработанноеВремя.Колонки.Добавить("КалендарныхДней",ОписаниеТиповЧисла4);
	ОтработанноеВремя.Колонки.Добавить("СреднемесячноеКоличествоДней",ОписаниеТиповЧисла15);
	ОтработанноеВремя.Колонки.Добавить("Учтено",ОписаниеТиповЧисла15);
	
	Для каждого Строка Из ТаблицаОтработанногоВремени Цикл
		ЗаполнитьЗначенияСвойств(ОтработанноеВремя.Добавить(),Строка);
	КонецЦикла;
	
	//Формирование Таблицы годовых премий за расчетный период
	ПремииГодовые = Новый ТаблицаЗначений;
	ПремииГодовые.Колонки.Добавить("Месяц",ОписаниеТиповДата);
	ПремииГодовые.Колонки.Добавить("Учтено",ОписаниеТиповЧисла15);
	
	Для каждого Строка Из ДанныеОЗаработке.ПремииГодовые Цикл
		ЗаполнитьЗначенияСвойств(ПремииГодовые.Добавить(),Строка);
	КонецЦикла;	
	
	//Формирование Таблицы зароботка за расчетный период
	Зароботок = Новый ТаблицаЗначений;
	Зароботок.Колонки.Добавить("Месяц",ОписаниеТиповДата);
	Зароботок.Колонки.Добавить("Учтено",ОписаниеТиповЧисла15);
	Зароботок.Колонки.Добавить("Сумма",ОписаниеТиповЧисла15);
	Зароботок.Колонки.Добавить("КоэффициентИндексации",ОписаниеТиповЧисла15);
	
	Для каждого Строка Из ДанныеОЗаработке.Заработок Цикл
		ЗаполнитьЗначенияСвойств(Зароботок.Добавить(),Строка);
	КонецЦикла;	
	
	Возврат Новый Структура("ПремииМесячные, ПремииГодовые, ОтработанноеВремя, Зароботок", ПремииМесячные, ПремииГодовые, ОтработанноеВремя, Зароботок); 
	
КонецФункции	

Процедура ВывестиШапку(ТабличныйДокумент, ОбластьШапка, РеквизитыДокумента, КадровыеДанныеСотрудника, ПараметрыРасчета, КодЯзыкаПечать)

	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("СинонимДокумента", 				МультиязычностьУкр.ПолучитьЛокализованныйСинонимОбъекта(РеквизитыДокумента.Ссылка, КодЯзыкаПечать));
	ЗначенияПараметров.Вставить("НомерДокумента", 					КадровыйУчетРасширенный.НомерКадровогоПриказа(РеквизитыДокумента.НомерДокумента));
	ЗначенияПараметров.Вставить("ДатаДокумента", 					Формат(РеквизитыДокумента.ДатаДокумента, "ДЛФ=DD;Л=" + КодЯзыкаПечать));
	ЗначенияПараметров.Вставить("Ссылка", 							РеквизитыДокумента.Ссылка);                                                                                                                         
	ЗначенияПараметров.Вставить("НаименованиеОрганизации", 			?(ЗначениеЗаполнено(РеквизитыДокумента.ПолноеНаименованиеОрганизации), РеквизитыДокумента.ПолноеНаименованиеОрганизации, РеквизитыДокумента.НаименованиеОрганизации));
	ЗначенияПараметров.Вставить("Организация", 						?(ЗначениеЗаполнено(РеквизитыДокумента.ПолноеНаименованиеОрганизации), РеквизитыДокумента.ПолноеНаименованиеОрганизации, РеквизитыДокумента.НаименованиеОрганизации));
	ЗначенияПараметров.Вставить("ДатаНачалаОтсутствия", 			Формат(РеквизитыДокумента.ДатаНачалаОтсутствия, "ДЛФ=D"));
	ЗначенияПараметров.Вставить("ДатаОкончанияОтсутствия", 			Формат(РеквизитыДокумента.ДатаОкончанияОтсутствия, "ДЛФ=D"));
	Если РеквизитыДокумента.ВидОтпуска = "Основной отпуск" Тогда
		ВидОтпуска = НСтр("ru='Основной отпуск';uk='Основна відпустка'",КодЯзыкаПечать);
	ИначеЕсли РеквизитыДокумента.ВидОтпуска = "Дополнительный отпуск" Тогда
		ВидОтпуска = НСтр("ru='Дополнительный отпуск';uk='Додаткова відпустка'",КодЯзыкаПечать);
	Иначе
		ВидОтпуска = РеквизитыДокумента.ВидОтпуска;
	КонецЕсли;
	ЗначенияПараметров.Вставить("ВидОтпуска", 						ВидОтпуска);
	ОписаниеЗаголовкаСреднегоЗаработка=НСтр("ru='Расчет среднедневного заработка';uk='Розрахунок середньоденного заробітку'",КодЯзыкаПечать);
	//не у всех документов (Коммандировка, ОПСЗ) заполняется ВидУчетаВремениДляСредней, но учитывать что в документе возможнен среднечасовой заработок нужно
	Если РеквизитыДокумента.Свойство("ВидУчетаВремениДляСредней") Тогда
		Если РеквизитыДокумента.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
			ОписаниеЗаголовкаСреднегоЗаработка	= НСтр("ru='Расчет среднечасового заработка';uk='Розрахунок середньогодинного заробітку'",КодЯзыкаПечать);
		КонецЕсли;
	КонецЕсли;
	ЗначенияПараметров.Вставить("ОписаниеЗаголовкаСреднегоЗаработка",ОписаниеЗаголовкаСреднегоЗаработка);
	
	ЗначенияПараметров.Вставить("Сотрудник", 						РеквизитыДокумента.Сотрудник);
	ЗначенияПараметров.Вставить("ФИОРаботника", 					КадровыеДанныеСотрудника.ФИОПолные);
	ЗначенияПараметров.Вставить("ТабельныйНомер", 					ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(КадровыеДанныеСотрудника.ТабельныйНомер, Истина, Истина));
	ЗначенияПараметров.Вставить("ВидЗанятости", 					МультиязычностьУкр.ПолучитьЛокализованныйСинонимОбъекта(КадровыеДанныеСотрудника.ВидЗанятости, КодЯзыкаПечать));
	ЗначенияПараметров.Вставить("Подразделение", 					КадровыеДанныеСотрудника.Подразделение);
	ЗначенияПараметров.Вставить("Должность", 						КадровыеДанныеСотрудника.Должность);		
	
	ЗначенияПараметров.Вставить("СпособРасчета", 					?(ЗначениеЗаполнено(ПараметрыРасчета.СпособРасчета), ПараметрыРасчета.СпособРасчета, НСтр("ru='Общий средний заработок';uk='Загальний середній заробіток'")));
	ЗначенияПараметров.Вставить("ДатаНачалаРасчетногоПериода", 		Формат(ПараметрыРасчета.НачалоРасчетногоПериода,"ДЛФ=D"));
	ЗначенияПараметров.Вставить("ДатаОкончанияРасчетногоПериода", 	Формат(ПараметрыРасчета.ОкончаниеРасчетногоПериода,"ДЛФ=D"));
	
	ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры, ЗначенияПараметров);
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
КонецПроцедуры

Процедура ВывестиОтработанноеВремя(ВидУчетаВремениДляСредней, ТабличныйДокумент, ОбластьШапкаОтработанногоВремени, ОбластьСтрокаОтработанногоВремени, ОбластьПодвалОтработанногоВремени, ОбластьИтогПодробно, РезультирующаяТаблица, ОплатитьПоДаннымФОТ, НормаВДнях, НормаВЧасах, КодЯзыкаПечать)


	ТабличныйДокумент.Вывести(ОбластьШапкаОтработанногоВремени);
	
	Для каждого Строка Из РезультирующаяТаблица Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаОтработанногоВремени.Параметры, Строка);
		Если НЕ ОплатитьПоДаннымФОТ Тогда
			ОбластьСтрокаОтработанногоВремени.Параметры.Месяц = Формат(Строка.Месяц, "ДФ='MMMM yyyy';Л=" + КодЯзыкаПечать); 
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьСтрокаОтработанногоВремени);
	КонецЦикла;
	
	ЗначенияПараметров = Новый Структура;
	
	Если ОплатитьПоДаннымФОТ Тогда
		ЗначенияПараметров.Вставить("УчтеноЗаработок",      	УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СуммаЗароботка"));
		Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
			УчтеноВремени 				= НормаВЧасах;
			СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из часовой заработной платы';uk='Розрахунок виконаний виходячи з годинної заробітної плати'",КодЯзыкаПечать);
		Иначе
			УчтеноВремени 				= НормаВДнях;
			СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из дневной заработной платы';uk='Розрахунок виконаний виходячи з денної заробітної плати'",КодЯзыкаПечать);
		КонецЕсли;
		ЗначенияПараметров.Вставить("УчтеноВремени", 			УчтеноВремени);
		ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	СреднедневнойЗаработок);	
		ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);		

	Иначе
		ЗначенияПараметров.Вставить("КалендарныхДней", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "КалендарныхДней"));
		ЗначенияПараметров.Вставить("ОтработаноДней", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ОтработаноДней"));
		ЗначенияПараметров.Вставить("ОтработаноЧасов", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ОтработаноЧасов"));
		ЗначенияПараметров.Вставить("СуммаЗароботка", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СуммаЗароботка"));
		ЗначенияПараметров.Вставить("ГодоваяПремия", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ГодоваяПремия"));
		ЗначенияПараметров.Вставить("МесячнаяПремия", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "МесячнаяПремия"));
		ЗначенияПараметров.Вставить("УчтеноЗаработок", 			ЗначенияПараметров.СуммаЗароботка + ЗначенияПараметров.ГодоваяПремия + ЗначенияПараметров.МесячнаяПремия);	
		СреднемесячноеКоличествоДней = УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СреднемесячноеКоличествоДней");
		УчтеноВремени 				= УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "УчтеноВремени");
		Если СреднемесячноеКоличествоДней > 0 Тогда
			УчтеноВремени 				= СреднемесячноеКоличествоДней;
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет произведен по окладу';uk='Розрахунок виконано по окладу'",КодЯзыкаПечать);
		ИначеЕсли ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
			ОписаниеСреднегоЗаработка	= НСтр("ru='Среднечасовая (по рабочим часам)';uk='Середньогодинна (по робочих годинах)'",КодЯзыкаПечать);
		Иначе
			ОписаниеСреднегоЗаработка	= НСтр("ru='Среднедневная (по рабочим дням)';uk='Середньоденна (по робочих днях)'",КодЯзыкаПечать);
		КонецЕсли;
		СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
		ЗначенияПараметров.Вставить("УчтеноВремени", 			УчтеноВремени);
		ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	СреднедневнойЗаработок);	
		ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);		
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ОбластьПодвалОтработанногоВремени.Параметры, ЗначенияПараметров);
	ТабличныйДокумент.Вывести(ОбластьПодвалОтработанногоВремени);   
	
	Если Не ОплатитьПоДаннымФОТ Тогда
		ЗаполнитьЗначенияСвойств(ОбластьИтогПодробно.Параметры, ЗначенияПараметров);
		ТабличныйДокумент.Вывести(ОбластьИтогПодробно); 
	КонецЕсли	                                       
	
КонецПроцедуры

Процедура ВывестиОтработанноеВремя1213(ВидУчетаВремениДляСредней, ТабличныйДокумент, ОбластьШапкаОтработанногоВремени, ОбластьСтрокаОтработанногоВремени, ОбластьПодвалОтработанногоВремени, 
	ОбластьИтогПодробно, ОбластьПодвалОтработанногоВремениСУчетомОграничений = Неопределено, ОбластьИтогПодробноСУчетомОграничений = Неопределено, РезультирующаяТаблица, ОплатитьПоДаннымФОТ, НормаВДнях, НормаВЧасах, КодЯзыкаПечать)


	ТабличныйДокумент.Вывести(ОбластьШапкаОтработанногоВремени);
	
	Для каждого Строка Из РезультирующаяТаблица Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаОтработанногоВремени.Параметры, Строка);
		Если НЕ ОплатитьПоДаннымФОТ Тогда
			ОбластьСтрокаОтработанногоВремени.Параметры.Месяц = Формат(Строка.Месяц, "ДФ='MMMM yyyy';Л=" + КодЯзыкаПечать); 
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьСтрокаОтработанногоВремени);
	КонецЦикла;
	
	ЗначенияПараметров = Новый Структура;
	
	Если ОплатитьПоДаннымФОТ Тогда
		ЗначенияПараметров.Вставить("УчтеноЗаработок",      	УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СуммаЗароботка"));
		Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
			УчтеноВремени 				= НормаВЧасах;
			СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из часовой заработной платы';uk='Розрахунок виконаний виходячи з годинної заробітної плати'",КодЯзыкаПечать);
		Иначе
			УчтеноВремени 				= НормаВДнях;
			СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из дневной заработной платы';uk='Розрахунок виконаний виходячи з денної заробітної плати'",КодЯзыкаПечать);
		КонецЕсли;
		ЗначенияПараметров.Вставить("УчтеноВремени", 			УчтеноВремени);
		ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	СреднедневнойЗаработок);	
		ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);		

	Иначе
		ЗначенияПараметров.Вставить("КалендарныхДней", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "КалендарныхДней"));
		ЗначенияПараметров.Вставить("ОтработаноДней", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ОтработаноДней"));
		ЗначенияПараметров.Вставить("ОтработаноЧасов", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ОтработаноЧасов"));
		ЗначенияПараметров.Вставить("СуммаЗароботка", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СуммаЗароботка"));
		ЗначенияПараметров.Вставить("ГодоваяПремия", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ГодоваяПремия"));
		ЗначенияПараметров.Вставить("МесячнаяПремия", 			УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "МесячнаяПремия"));
		ЗначенияПараметров.Вставить("УчтеноЗаработок", 			ЗначенияПараметров.СуммаЗароботка + ЗначенияПараметров.ГодоваяПремия + ЗначенияПараметров.МесячнаяПремия);	   
		
		РазницаПремия = УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "ПревышениеПремии");
		ЗначенияПараметров.Вставить("РазницаПремия", 			РазницаПремия);
		СреднемесячноеКоличествоДней = УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "СреднемесячноеКоличествоДней");
		УчтеноВремени 				= УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(РезультирующаяТаблица, "УчтеноВремени");  
		
		Если СреднемесячноеКоличествоДней > 0 Тогда
			Если НЕ ВидУчетаВремениДляСредней = Неопределено Тогда
				УчтеноВремени 				= СреднемесячноеКоличествоДней;
			КонецЕсли;	
			ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет произведен по окладу';uk='Розрахунок виконано по окладу'",КодЯзыкаПечать);
		ИначеЕсли ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
			ОписаниеСреднегоЗаработка	= НСтр("ru='Среднечасовая (по рабочим часам)';uk='Середньогодинна (по робочих годинах)'",КодЯзыкаПечать);
			ОписаниеСреднегоЗаработкаСУчетомОграничений	= НСтр("ru='Среднечасовая (по рабочим часам) с учетом ограничений';uk= 'Середньогодинна (по робочих годинах) з урахуванням обмежень'",КодЯзыкаПечать);
		Иначе
			ОписаниеСреднегоЗаработка	= НСтр("ru='Среднедневная (по рабочим дням)';uk='Середньоденна (по робочих днях)'",КодЯзыкаПечать); 
			ОписаниеСреднегоЗаработкаСУчетомОграничений = НСтр("ru='Среднедневная (по рабочим дням) с учетом ограничений';uk= 'Середньоденна (по робочих днях) з урахуванням обмежень'",КодЯзыкаПечать);
		КонецЕсли;
		СреднедневнойЗаработок 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, УчтеноВремени);
		ЗначенияПараметров.Вставить("УчтеноВремени", 			УчтеноВремени);
		ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	СреднедневнойЗаработок);	
		ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);		
	КонецЕсли;    
	ЗаполнитьЗначенияСвойств(ОбластьПодвалОтработанногоВремени.Параметры, ЗначенияПараметров);
	ТабличныйДокумент.Вывести(ОбластьПодвалОтработанногоВремени);

	Если НЕ ОплатитьПоДаннымФОТ Тогда
		Если РазницаПремия <> 0 Тогда
			ЗначенияПараметров.Вставить("ПремияБезПревышения", ЗначенияПараметров.МесячнаяПремия - РазницаПремия);
			ЗаполнитьЗначенияСвойств(ОбластьИтогПодробноСУчетомОграничений.Параметры, ЗначенияПараметров); 
			ТабличныйДокумент.Вывести(ОбластьИтогПодробноСУчетомОграничений);
			СреднедневнойЗаработокСУчетомОграничений 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок - РазницаПремия, УчтеноВремени);
			ЗначенияПараметров.Вставить("УчтеноЗаработокСУчетомОграничений", ЗначенияПараметров.УчтеноЗаработок - РазницаПремия);
			ЗначенияПараметров.Вставить("УчтеноВремени", 			УчтеноВремени);
			ЗначенияПараметров.Вставить("СреднедневнойЗаработокСУчетомОграничений", 	СреднедневнойЗаработокСУчетомОграничений);	
			ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработкаСУчетомОграничений", ОписаниеСреднегоЗаработкаСУчетомОграничений);		
			ЗаполнитьЗначенияСвойств(ОбластьПодвалОтработанногоВремениСУчетомОграничений.Параметры, ЗначенияПараметров);
			ТабличныйДокумент.Вывести(ОбластьПодвалОтработанногоВремениСУчетомОграничений); 
		Иначе
			ЗаполнитьЗначенияСвойств(ОбластьИтогПодробно.Параметры, ЗначенияПараметров);
			ТабличныйДокумент.Вывести(ОбластьИтогПодробно);
		КонецЕсли; 
	КонецЕсли
	
КонецПроцедуры

Процедура ВывестиТаблицуНачислений(ТабличныйДокумент, ОбластьШапкаНачислений, ОбластьСтрокаНачислений, ДанныеТаблицы)
	
	ТабличныйДокумент.Вывести(ОбластьШапкаНачислений);

	Для каждого Строка Из ДанныеТаблицы Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаНачислений.Параметры, Строка);
		ТабличныйДокумент.Вывести(ОбластьСтрокаНачислений);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиОбластьОтветственный(ТабличныйДокумент, ОбластьОтветственный, РеквизитыДокумента)
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("Ответственный", 		РеквизитыДокумента.Ответственный);
	ЗаполнитьЗначенияСвойств(ОбластьОтветственный.Параметры, ЗначенияПараметров);
	ТабличныйДокумент.Вывести(ОбластьОтветственный);
	
КонецПроцедуры

// вывод в печатную форму расшифровки по премиям
Процедура ВывестиТаблицуРасшифровкаПремий(ТабличныйДокумент, ОбластьШапкаНачислений, ОбластьСтрокаНачислений,ОбластьСтрокаПериодПремии,ОбластьИтогПериодПремии, ОбластьИтогПериодПремииСУчетомОграничений,ОбластиМакета, ДанныеТаблицы, ДанныеЗаработка = Неопределено, КодЯзыкаПечать)

	ТабличныйДокумент.Вывести(ОбластьШапкаНачислений);
	ДанныеТаблицы.Сортировать("Период Возр");
	//расшифровка по периодам премий
	ПериодыПермий = Новый ТаблицаЗначений;
	ПериодыПермий = ДанныеТаблицы.Скопировать();;
	ПериодыПермий.Свернуть("Период");

	Для каждого ПериодПремии Из ПериодыПермий Цикл
		
		ИтСумаПремии=0;
		ОбластьСтрокаПериодПремии.Параметры.Период = Формат(ПериодПремии.Период, "ДФ='MMMM yyyy';Л=" + КодЯзыкаПечать);
		ТабличныйДокумент.Вывести(ОбластьСтрокаПериодПремии);
		
		Для каждого Строка Из ДанныеТаблицы Цикл
			
			Если ПериодПремии.Период=Строка.Период Тогда
				
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаНачислений.Параметры, Строка);
				ОбластьСтрокаНачислений.Параметры.ДатаОкончания = КонецМесяца(ДобавитьМесяц(Строка.ДатаНачалаБазовогоПериода,?(Строка.КоличествоМесяцев>1,Строка.КоличествоМесяцев-1,0)));
				ИтСумаПремии=ИтСумаПремии+(Строка.Сумма*Строка.ОтработаноДней);
				ОбластьСтрокаНачислений.Параметры.УчитываемаяЧасть=(Строка.Сумма*Строка.ОтработаноДней);
				ТабличныйДокумент.Вывести(ОбластьСтрокаНачислений);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьИтогПериодПремии.Параметры.Период = Формат(ПериодПремии.Период, "ДФ='MMMM yyyy';Л=" + КодЯзыкаПечать);
		ОбластьИтогПериодПремии.Параметры.ВсегоПремии=ИтСумаПремии;
		ТабличныйДокумент.Вывести(ОбластьИтогПериодПремии);
		
	КонецЦикла;

	Если Не ДанныеЗаработка = Неопределено Тогда   
		СуммаОграничения = УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(ДанныеЗаработка, "ПревышениеПремии");
		Если СуммаОграничения > 0 Тогда      
			
			РасшифровкаОграниченияПремий = ПолучитьРасшифровкуОграниченияПремий(ДанныеТаблицы);
			Если РасшифровкаОграниченияПремий.Количество()>0 Тогда
				
				ТабличныйДокумент.Вывести(ОбластиМакета.ШапкаРасшифровкаОграничения);
				Для каждого Строка Из РасшифровкаОграниченияПремий Цикл
					
					ЗаполнитьЗначенияСвойств(ОбластиМакета.СтрокаРасшифровкаОграничения.Параметры, Строка);
					ОбластиМакета.СтрокаРасшифровкаОграничения.Параметры.ДатаОкончания = КонецМесяца(ДобавитьМесяц(Строка.ДатаНачалаБазовогоПериода,?(Строка.КоличествоМесяцев>1,Строка.КоличествоМесяцев-1,0)));
					ОбластиМакета.СтрокаРасшифровкаОграничения.Параметры.Сограничением = Строка.Учтено-Строка.ПревышениеПремии;
					ТабличныйДокумент.Вывести(ОбластиМакета.СтрокаРасшифровкаОграничения);
					
				КонецЦикла;
				
			КонецЕсли;
        	
			УчтеноПремий = УчетСреднегоЗаработкаКлиентСервер.ИтогиПоПолю(ДанныеЗаработка, "Учтено");
			ОбластьИтогПериодПремииСУчетомОграничений.Параметры.ВсегоПремииСОграничением = УчтеноПремий - СуммаОграничения;
			ТабличныйДокумент.Вывести(ОбластьИтогПериодПремииСУчетомОграничений);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьРасшифровкуОграниченияПремий(ДанныеТаблицы)
    Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Начисления", ДанныеТаблицы);
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Начисления.Период КАК Период,
	               |	Начисления.Сотрудник КАК Сотрудник,
	               |	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
	               |	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
	               |	Начисления.ОтработаноДнейВБазовомПериоде КАК ОтработаноДнейВБазовомПериоде,
				   |	Начисления.НормаДнейВБазовомПериоде КАК НормаДнейВБазовомПериоде,
	               |	Начисления.ПорядокРасчета КАК ПорядокРасчета,
	               |	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
	               |	Начисления.Сумма КАК Сумма,
	               |	Начисления.СуммаПремии КАК СуммаПремии,
				   |	Начисления.ОтработаноДней КАК ОтработаноДней
	               |ПОМЕСТИТЬ ВТВсеНачисления
	               |ИЗ
	               |	&Начисления КАК Начисления
	               |;
				   |
					// формування данних з розшифровкою обмежень для нарахованих премій в цілому в розрахунковому періоді середнього заробітку
				   |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Начисления.Сотрудник КАК Сотрудник,
	               |	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
	               |	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
	               |	Начисления.ОтработаноДнейВБазовомПериоде КАК ОтработаноДнейВБазовомПериоде,
				   |	Начисления.НормаДнейВБазовомПериоде КАК НормаДнейВБазовомПериоде,
	               |	Начисления.ПорядокРасчета КАК ПорядокРасчета,
	               |	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
	               |	Начисления.Сумма КАК Сумма,
	               |	Начисления.СуммаПремии КАК СуммаПремии,
				   |	ВЫБОР
				   |		КОГДА (СУММА(Начисления.Сумма * Начисления.ОтработаноДней) - Начисления.СуммаПремии)>0
				   |			  И (Начисления.ОтработаноДнейВБазовомПериоде < Начисления.НормаДнейВБазовомПериоде)		  
				   |			ТОГДА (СУММА(Начисления.Сумма * Начисления.ОтработаноДней) - Начисления.СуммаПремии)
				   |		ИНАЧЕ 0
				   |	КОНЕЦ КАК ПревышениеПремии,
	               |	СУММА(Начисления.Сумма * Начисления.ОтработаноДней) КАК Учтено,
				   |	СУММА(Начисления.ОтработаноДней) КАК ОтработаноДней
	               |ИЗ
	               |	ВТВсеНачисления КАК Начисления
	               |СГРУППИРОВАТЬ ПО
	               |	Начисления.Сотрудник,
	               |	Начисления.ДатаНачалаБазовогоПериода,
				   |	Начисления.КоличествоМесяцев,
	               |	Начисления.ОтработаноДнейВБазовомПериоде,
				   |	Начисления.НормаДнейВБазовомПериоде,
	               |	Начисления.ПорядокРасчета,
	               |	Начисления.СоставнаяЧасть,
	               |	Начисления.Сумма,
	               |	Начисления.СуммаПремии
				   |";

	Запрос.Текст=ТекстЗапроса;
	РасшифровкаОграниченияПремий = Запрос.Выполнить().Выгрузить();				
	Возврат  РасшифровкаОграниченияПремий;
КонецФункции	 



Функция ОбластиМакетаСреднегоЗаработка(ИспользуетсяИндексация = Истина, СпособРасчетаОтпуска = Неопределено, КодЯзыкаПечать, ЕстьВидОтпуска)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьРасчетаСреднегоЗаработка.ПФ_MXL_РасчетСреднегоЗаработка", КодЯзыкаПечать);
	
	ОбластиМакета = Новый Структура;
	
	Если НЕ ЕстьВидОтпуска Тогда 
		ОбластиМакета.Вставить("ОбластьШапка", 	Макет.ПолучитьОбласть("ШапкаБезВидаОтпуска"));
	Иначе
		ОбластиМакета.Вставить("ОбластьШапка", 	Макет.ПолучитьОбласть("Шапка"));
	КонецЕсли;		
	
	ОбластиМакета.Вставить("ОбластьШапкаОтработанногоВремени",  	Макет.ПолучитьОбласть("ОтработанноеВремяШапкаОтпуск"));
	ОбластиМакета.Вставить("ОбластьСтрокаОтработанногоВремени",  	Макет.ПолучитьОбласть("ОтработанноеВремяСтрокаОтпуск"));
	ОбластиМакета.Вставить("ОбластьПодвалОтработанногоВремени",  	Макет.ПолучитьОбласть("ОтработанноеВремяПодвалОтпуск"));
	
	ОбластиМакета.Вставить("ОбластьНачисленияШапка",  				Макет.ПолучитьОбласть("НачисленияШапка"));
	ОбластиМакета.Вставить("ОбластьНачисленияСтрока",  				Макет.ПолучитьОбласть("НачисленияСтрока"));
	
	ОбластиМакета.Вставить("ПоДаннымФОТШапка",  					Макет.ПолучитьОбласть("ПоДаннымФОТШапка"));
	ОбластиМакета.Вставить("ПоДаннымФОТСтрока",  					Макет.ПолучитьОбласть("ПоДаннымФОТСтрока"));
	ОбластиМакета.Вставить("ПоДаннымФОТРасшифровка",  				Макет.ПолучитьОбласть("ПоДаннымФОТРасшифровка"));

	ОбластиМакета.Вставить("ШапкаРасшифровкаПремий", 				Макет.ПолучитьОбласть("ШапкаРасшифровкаПремий"));
	ОбластиМакета.Вставить("СтрокаРасшифровкаПремий",				Макет.ПолучитьОбласть("СтрокаРасшифровкаПремий"));
	ОбластиМакета.Вставить("СтрокаПериодПремии",			 	    Макет.ПолучитьОбласть("СтрокаПериодПремии"));
	ОбластиМакета.Вставить("ИтогПериодПремии",				        Макет.ПолучитьОбласть("ИтогПериодПремии"));
	ОбластиМакета.Вставить("ИтогПодробно",    Макет.ПолучитьОбласть("ИтогПодробно"));  
	ОбластиМакета.Вставить("ИтогПериодПремииСУчетомОграничений",    Макет.ПолучитьОбласть("ИтогПериодПремииСУчетомОграничений"));  
	ОбластиМакета.Вставить("ОбластьПодвалОтработанногоВремениСУчетомОграничений",  	Макет.ПолучитьОбласть("ОтработанноеВремяПодвалОтпускСУчетомОграничений"));
	ОбластиМакета.Вставить("ИтогПодробноСУчетомОграничений",    Макет.ПолучитьОбласть("ИтогПодробноСУчетомОграничений"));  
	
	ОбластиМакета.Вставить("ШапкаРасшифровкаОграничения",    Макет.ПолучитьОбласть("ШапкаРасшифровкаОграничения"));  
	ОбластиМакета.Вставить("СтрокаРасшифровкаОграничения",    Макет.ПолучитьОбласть("СтрокаРасшифровкаОграничения"));  

	ОбластиМакета.Вставить("ОбластьОтветственный",  				Макет.ПолучитьОбласть("Ответственный"));
	
	Возврат ОбластиМакета;	
	
КонецФункции 

Функция ПреобразоватьДанныеОЗаработкеВТаблицыЗначений(ДанныеОЗаработке)
	
	Заработок 				= Новый Массив;
	ПремииГодовые	 		= Новый Массив;
	ПремииМесячные	 		= Новый Массив;

	ГодовыеПремии = Новый Массив;
	ГодовыеПремии.Добавить(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом);
	ГодовыеПремии.Добавить(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой);
	
	Для каждого СтрокаЗаработка Из ДанныеОЗаработке Цикл
		Если Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии().Найти(СтрокаЗаработка.СоставнаяЧасть) <> Неопределено Тогда
			Если ГодовыеПремии.Найти(СтрокаЗаработка.СоставнаяЧасть) <> Неопределено Тогда
				ПремииГодовые.Добавить(СтрокаЗаработка);
			Иначе
				ПремииМесячные.Добавить(СтрокаЗаработка);
			КонецЕсли;	
		Иначе
			Заработок.Добавить(СтрокаЗаработка);
		КонецЕсли;
	КонецЦикла;		
	
	Заработок = МассивСтруктурВТаблицуЗначений(Заработок);
	Если Заработок.Количество() > 0 Тогда
		Заработок.Сортировать("Месяц");
	КонецЕсли;
	
	
	ПремииГодовые = МассивСтруктурВТаблицуЗначений(ПремииГодовые);
	ПремииМесячные = МассивСтруктурВТаблицуЗначений(ПремииМесячные);
	
	Если ПремииМесячные.Количество() > 0 Тогда
		ПремииМесячные.Сортировать("Месяц");
	КонецЕсли;

	
	Возврат Новый Структура("Заработок,ПремииМесячные, ПремииГодовые", Заработок, ПремииМесячные, ПремииГодовые);
	
КонецФункции

Функция ПреобразоватьДанныеОВремениВТаблицуЗначений(ОтработанноеВремя)
	
	ОтработанноеВремя = МассивСтруктурВТаблицуЗначений(ОтработанноеВремя);  	
	Если ОтработанноеВремя.Количество() > 0 Тогда
		ОтработанноеВремя.Сортировать("Месяц");		
	КонецЕсли;
	
	Возврат ОтработанноеВремя;	
	
КонецФункции

Функция ИспользуетсяИндексация(Заработок)
	
	ИспользуетсяИндексация = Ложь;
	
	Для каждого СтрокаЗаработка Из Заработок Цикл
		Если СтрокаЗаработка.КоэффициентИндексации > 1 Тогда
			ИспользуетсяИндексация = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;		
	
	Возврат ИспользуетсяИндексация;	
	
	
КонецФункции 
	
#КонецОбласти

#Область КомпактныйВариант

Процедура ЗаполнитьТабличныйДокументРасчетаСреднегоЗаработкаКомпактный(ТабличныйДокумент, ОбъектыПечати, ИмяМакета, ДанныеДокументов, КодЯзыкаПечать)
		
	ЗначенияПараметров = Новый Структура;

	Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл
		
		// Подготовим макеты для формирования табличного документа.
		ОбластиМакета = ОбластиМакетаСреднегоЗаработкаКомпактный(ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ, КодЯзыкаПечать);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ЗначенияПараметров.Очистить();
		ЗначенияПараметров.Вставить("ЕдиницаИзмеренияВремени", НазваниеЕдиницыИзмеренияВремени(ДанныеДокумента.ПараметрыРасчета.ИспользоватьСреднеЧасовойЗаработок, КодЯзыкаПечать));	
		ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьШапкаКомпактный.Параметры, ЗначенияПараметров);
		ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьШапкаКомпактный);
		
		Если ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ = Истина Тогда
			СводнаяТаблица = ДанныеДокумента.ДанныеРасчетаСреднего.Заработок;
		Иначе
			СводнаяТаблица = СводнаяТаблицаЗаработкаИВремени(ДанныеДокумента.ДанныеРасчетаСреднего.Заработок, ДанныеДокумента.ДанныеРасчетаСреднего.ОтработанноеВремя);
		КонецЕсли;
		
		Для каждого Строка Из СводнаяТаблица Цикл														
			ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьСтрокаКомпактный.Параметры, Строка);
			ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьСтрокаКомпактный);
		КонецЦикла;
		
		ЗначенияПараметров.Очистить();
		
		Если ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ = Истина Тогда
			ЗначенияПараметров.Вставить("Заработок", 				СводнаяТаблица.Итог("СуммаЗароботка"));
			ЗначенияПараметров.Вставить("УчтеноЗаработок", 			ЗначенияПараметров.Заработок);	
			Если ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
				УчтеноВремени 				= ДанныеДокумента.РеквизитыДокумента.НормаВЧасах;
				ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из часовой заработной платы';uk='Розрахунок виконаний виходячи з годинної заробітної плати'",КодЯзыкаПечать); 
			Иначе
				УчтеноВремени				= ДанныеДокумента.РеквизитыДокумента.НормаВДнях;
				ОписаниеСреднегоЗаработка	= НСтр("ru='Расчет выполнен исходя из дневной заработной платы';uk='Розрахунок виконаний виходячи з денної заробітної плати'",КодЯзыкаПечать);
			КонецЕсли;
			ЗначенияПараметров.Вставить("УчтеноВремени", 			 УчтеноВремени);
			ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);
			ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	 УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, ЗначенияПараметров.УчтеноВремени));	
		Иначе
			ЗначенияПараметров.Вставить("Заработок", 				СводнаяТаблица.Итог("Заработок"));	
			ЗначенияПараметров.Вставить("Премии", 					СводнаяТаблица.Итог("Премии"));	
			ЗначенияПараметров.Вставить("УчтеноЗаработок", 			ЗначенияПараметров.Заработок + ЗначенияПараметров.Премии);	
			ЗначенияПараметров.Вставить("УчтеноВремени", 			СводнаяТаблица.Итог("Время"));
			РазницаПремия = СводнаяТаблица.Итог("ПревышениеПремии");
			ЗначенияПараметров.Вставить("ПревышениеПремии",			РазницаПремия);	 
			ЗначенияПараметров.Вставить("СреднедневнойЗаработок", 	УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок, ЗначенияПараметров.УчтеноВремени));	
			Если ДанныеДокумента.РеквизитыДокумента.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
				ОписаниеСреднегоЗаработка	= НСтр("ru='Среднечасовая (по рабочим часам)';uk='Середньогодинна (по робочих годинах)'",КодЯзыкаПечать);
			ОписаниеСреднегоЗаработкаСУчетомОграничений	= НСтр("ru='Среднечасовая (по рабочим часам) с учетом ограничений';uk= 'Середньогодинна (по робочих годинах) з урахуванням обмежень'",КодЯзыкаПечать);
			Иначе
				ОписаниеСреднегоЗаработка	= НСтр("ru='Среднедневная (по рабочим дням)';uk='Середньоденна (по робочих днях)'",КодЯзыкаПечать);
			ОписаниеСреднегоЗаработкаСУчетомОграничений = НСтр("ru='Среднедневная (по рабочим дням) с учетом ограничений';uk= 'Середньоденна (по робочих днях) з урахуванням обмежень'",КодЯзыкаПечать);
				
			КонецЕсли;
			ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработка", ОписаниеСреднегоЗаработка);
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьПодвалКомпактный.Параметры, ЗначенияПараметров);
		ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьПодвалКомпактный); 
		
		Если ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ = Ложь И РазницаПремия <> 0 Тогда 
			ЗначенияПараметров.Вставить("УчтеноЗаработокСУчетомОграничений", ЗначенияПараметров.УчтеноЗаработок - РазницаПремия);
			ЗначенияПараметров.Вставить("ПремииБезПревышения", ЗначенияПараметров.Премии - РазницаПремия);
			ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьИтогКомпактныйСУчетомОграничений.Параметры, ЗначенияПараметров);
			ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьИтогКомпактныйСУчетомОграничений);

			СреднедневнойЗаработокСУчетомОграничений 		= УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработок(ЗначенияПараметров.УчтеноЗаработок - РазницаПремия, ЗначенияПараметров.УчтеноВремени);
			ЗначенияПараметров.Вставить("УчтеноЗаработокСУчетомОграничений", ЗначенияПараметров.УчтеноЗаработок - РазницаПремия);
			ЗначенияПараметров.Вставить("УчтеноВремени", 			ЗначенияПараметров.УчтеноВремени);
			ЗначенияПараметров.Вставить("СреднедневнойЗаработокСУчетомОграничений", 	СреднедневнойЗаработокСУчетомОграничений);	
			ЗначенияПараметров.Вставить("ОписаниеСреднегоЗаработкаСУчетомОграничений", ОписаниеСреднегоЗаработкаСУчетомОграничений);		
			ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьПодвалКомпактныйСУчетомОграничений.Параметры, ЗначенияПараметров);
			ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьПодвалКомпактныйСУчетомОграничений);
		ИначеЕсли ДанныеДокумента.РеквизитыДокумента.ОплатитьПоДаннымФОТ = Ложь Тогда
			ЗаполнитьЗначенияСвойств(ОбластиМакета.ОбластьИтогПодвалКомпактный.Параметры, ЗначенияПараметров);
			ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьИтогПодвалКомпактный);
		КонецЕсли;

		ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.РеквизитыДокумента.Ссылка, ДанныеДокумента.РеквизитыДокумента.Сотрудник);
	КонецЦикла;
	
КонецПроцедуры 

Функция СводнаяТаблицаЗаработкаИВремени(Заработок, Время)
	
	СводнаяТаблица = Новый ТаблицаЗначений;
	СводнаяТаблица.Колонки.Добавить("Месяц", 		Новый ОписаниеТипов("Дата"));
	СводнаяТаблица.Колонки.Добавить("Заработок", 	Новый ОписаниеТипов("Число"));
	СводнаяТаблица.Колонки.Добавить("Премии", 		Новый ОписаниеТипов("Число"));
	СводнаяТаблица.Колонки.Добавить("Время", 		Новый ОписаниеТипов("Число")); 
	СводнаяТаблица.Колонки.Добавить("ПревышениеПремии", 		Новый ОписаниеТипов("Число"));
	
	Для каждого Строка Из Заработок Цикл
		НоваяСтрока 			= СводнаяТаблица.Добавить();
		НоваяСтрока.Месяц 		= Строка.Месяц;
		Если Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии().Найти(Строка.СоставнаяЧасть) <> Неопределено Тогда
			НоваяСтрока.Премии 		= Строка.Учтено;
			НоваяСтрока.ПревышениеПремии = Строка.ПревышениеПремии;
		Иначе
			НоваяСтрока.Заработок 	= Строка.Учтено;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Строка Из Время Цикл
		НоваяСтрока 		= СводнаяТаблица.Добавить();
		НоваяСтрока.Месяц 	= Строка.Месяц;
		НоваяСтрока.Время 	= Строка.Учтено;
	КонецЦикла;
	
	СводнаяТаблица.Свернуть("Месяц", "Заработок,Премии,ПревышениеПремии,Время");
	СводнаяТаблица.Сортировать("Месяц");
	
	Возврат СводнаяТаблица;
	
КонецФункции

Функция ОбластиМакетаСреднегоЗаработкаКомпактный(ОплатитьПоДаннымФОТ, КодЯзыкаПечать)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьРасчетаСреднегоЗаработка.ПФ_MXL_РасчетСреднегоЗаработка",КодЯзыкаПечать);
	
	ОбластиМакета = Новый Структура;
	Если ОплатитьПоДаннымФОТ = Истина Тогда
		ОбластиМакета.Вставить("ОбластьШапкаКомпактный",  Макет.ПолучитьОбласть("ПоДаннымФОТШапка"));
		ОбластиМакета.Вставить("ОбластьСтрокаКомпактный", Макет.ПолучитьОбласть("ПоДаннымФОТСтрока"));
		ОбластиМакета.Вставить("ОбластьПодвалКомпактный", Макет.ПолучитьОбласть("ПоДаннымФОТРасшифровка"));
	Иначе
		ОбластиМакета.Вставить("ОбластьШапкаКомпактный",  Макет.ПолучитьОбласть("ШапкаКомпактный"));
		ОбластиМакета.Вставить("ОбластьСтрокаКомпактный", Макет.ПолучитьОбласть("СтрокаКомпактный"));
		ОбластиМакета.Вставить("ОбластьПодвалКомпактный", Макет.ПолучитьОбласть("ПодвалКомпактный"));   
		ОбластиМакета.Вставить("ОбластьИтогПодвалКомпактный",  	Макет.ПолучитьОбласть("ИтогПодвалКомпактный"));
		ОбластиМакета.Вставить("ОбластьПодвалКомпактныйСУчетомОграничений",  	Макет.ПолучитьОбласть("ПодвалКомпактныйСУчетомОграничений"));    
		ОбластиМакета.Вставить("ОбластьИтогКомпактныйСУчетомОграничений",  		Макет.ПолучитьОбласть("ИтогКомпактныйСУчетомОграничений"));   
	КонецЕсли;
	
	Возврат ОбластиМакета;	
	
КонецФункции 
	
#КонецОбласти

Функция НазваниеЕдиницыИзмеренияВремени(ИспользоватьСреднеЧасовойЗаработок, КодЯзыкаПечать)
	Возврат ?(ИспользоватьСреднеЧасовойЗаработок, НСтр("ru='часов';uk='годин'",КодЯзыкаПечать), НСтр("ru='дней';uk='днів'",КодЯзыкаПечать));
КонецФункции

Функция МассивСтруктурВТаблицуЗначений(МассивСтруктур)
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	Если МассивСтруктур.Количество() = 0 Тогда
		Возврат ТаблицаЗначений;
	КонецЕсли;
	
	Для каждого Свойство Из МассивСтруктур[0] Цикл
		ТаблицаЗначений.Колонки.Добавить(Свойство.Ключ);
	КонецЦикла;
	
	Для каждого Элемент Из МассивСтруктур Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаЗначений.Добавить(), Элемент);
	КонецЦикла;
	
	Возврат ТаблицаЗначений;
	
КонецФункции

Процедура УстановитьНомерРазделаВОбласти(НомерРаздела, Область)
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("НомерРаздела", НомерРаздела);
	ЗаполнитьЗначенияСвойств(Область.Параметры, ЗначенияПараметров);
	НомерРаздела = НомерРаздела + 1;
КонецПроцедуры

Функция ПустаяСтруктураДанныхДляПечатиСреднегоЗаработка() Экспорт 
	
	СтруктураДанных = Новый Структура;
	
	СтруктураДанных.Вставить("РеквизитыДокумента", 			ПустаяСтруктураРеквизитовДокументаДляПечатиСреднегоЗаработка());
	СтруктураДанных.Вставить("КадровыеДанныеСотрудника", 	ПустаяСтруктураКадровыхДанныхСотрудникаДляПечатиСреднегоЗаработка());
	СтруктураДанных.Вставить("ПараметрыРасчета", 			ПустаяСтруктураПараметровРасчетаСреднегоЗаработка());
	СтруктураДанных.Вставить("ДанныеРасчетаСреднего", 		Новый Массив); 
	СтруктураДанных.Вставить("ДанныеНачислений", 			);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ПустаяСтруктураРеквизитовДокументаДляПечатиСреднегоЗаработка()
	
	ПустаяСтруктура = Новый Структура;
	
	ПустаяСтруктура.Вставить("Ссылка");
	ПустаяСтруктура.Вставить("Организация");
	ПустаяСтруктура.Вставить("Сотрудник");
	ПустаяСтруктура.Вставить("ПолноеНаименованиеОрганизации");
	ПустаяСтруктура.Вставить("НаименованиеОрганизации");
	ПустаяСтруктура.Вставить("КодПоОКПО");
	ПустаяСтруктура.Вставить("ИНН");
	ПустаяСтруктура.Вставить("КПП");
	ПустаяСтруктура.Вставить("НомерДокумента");
	ПустаяСтруктура.Вставить("ДатаДокумента");
	ПустаяСтруктура.Вставить("ДатаНачалаОтсутствия");
	ПустаяСтруктура.Вставить("ДатаОкончанияОтсутствия");
	ПустаяСтруктура.Вставить("НачалоПериодаЗаКоторыйПредоставляетсяОтпуск");
	ПустаяСтруктура.Вставить("КонецПериодаЗаКоторыйПредоставляетсяОтпуск");
	ПустаяСтруктура.Вставить("Ответственный");
	ПустаяСтруктура.Вставить("ВидОтпуска");
		
	ПустаяСтруктура.Вставить("ВидУчетаВремениДляСредней");
	ПустаяСтруктура.Вставить("ОплатитьПоДаннымФОТ");
	ПустаяСтруктура.Вставить("НормаВДнях");
	ПустаяСтруктура.Вставить("НормаВЧасах");
	ПустаяСтруктура.Вставить("ДатаНачалаСобытия");
	Возврат ПустаяСтруктура;

КонецФункции

Функция ПустаяСтруктураКадровыхДанныхСотрудникаДляПечатиСреднегоЗаработка()
	
	ПустаяСтруктура = Новый Структура;
	ПустаяСтруктура.Вставить("ФизическоеЛицо");
	ПустаяСтруктура.Вставить("ФИОПолные");
	ПустаяСтруктура.Вставить("ТабельныйНомер");
	ПустаяСтруктура.Вставить("Подразделение");
	ПустаяСтруктура.Вставить("Должность");
	ПустаяСтруктура.Вставить("ВидЗанятости");
	
	Возврат ПустаяСтруктура;

КонецФункции

Функция ПустаяСтруктураПараметровРасчетаСреднегоЗаработка()
	
	ПустаяСтруктура = Новый Структура;
	ПустаяСтруктура.Вставить("СпособРасчета");
	ПустаяСтруктура.Вставить("ИспользоватьСреднеЧасовойЗаработок");
	ПустаяСтруктура.Вставить("НачалоРасчетногоПериода");
	ПустаяСтруктура.Вставить("ОкончаниеРасчетногоПериода");
	
	Возврат ПустаяСтруктура;

КонецФункции

Функция ДанныеДокументовРасчетаСреднегоЗаработка(МассивСсылок)
	
	ДанныеДокументов = Новый Массив;
	
	ДокументыСгруппированныеПоТипам = ДокументыСгруппированныеПоТипам(МассивСсылок);
	
	Для каждого ОписаниеТипаДокумента Из ДокументыСгруппированныеПоТипам Цикл
		
		Менеджер = ОписаниеТипаДокумента.Ключ;
		МассивСсылок = ОписаниеТипаДокумента.Значение;
		
		ДанныеДокументовПоТипу = Менеджер.ДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивСсылок);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеДокументов, ДанныеДокументовПоТипу);
	     		
	КонецЦикла;

	Возврат ДанныеДокументов
	
КонецФункции

#КонецОбласти
Функция ДокументыСгруппированныеПоТипам(МассивСсылок)
	
	ДокументыСгруппированныеПоТипам = Новый Соответствие;
	
	Для каждого Ссылка Из МассивСсылок Цикл
		
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
		
		МассивСсылок = ДокументыСгруппированныеПоТипам.Получить(Менеджер);
		
		Если МассивСсылок = Неопределено Тогда
			ДокументыСгруппированныеПоТипам.Вставить(Менеджер, Новый Массив);
			МассивСсылок = ДокументыСгруппированныеПоТипам.Получить(Менеджер);
		КонецЕсли;
		
		МассивСсылок.Добавить(Ссылка);
		
	КонецЦикла;
	
	Возврат ДокументыСгруппированныеПоТипам;
	
КонецФункции

#Область ИменованиеОбластейДляНавигацииПоТабличномуДокументу

// Задает область печати объекта в табличном документе.
// Применяется для связывания области в табличном документе, с объектом печати (ссылка).
// Необходимо вызывать при формировании очередной области печатной формы в табличном
// документе.
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент - печатная форма;
//  НомерСтрокиНачало - Число - позиция начала очередной области в документе;
//  ОбъектыПечати - СписокЗначений - список объектов печати;
//  Ссылка - ЛюбаяСсылка - объект печати.
Процедура ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка, Сотрудник) Экспорт
	
	Если ТипЗнч(ОбъектыПечати) = Тип("СписокЗначений") Тогда
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
	иначе
		ЗадатьОбластьПечатиВстраиваемойОбласти(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка, Сотрудник);		
	КонецЕсли;	
	
КонецПроцедуры

Функция ОбъектыПечатиДляВстраиваемыхОбластей() Экспорт
	ОбъектыПечати = Новый ТаблицаЗначений;
	ОбъектыПечати.Колонки.Добавить("Ссылка");
	ОбъектыПечати.Колонки.Добавить("Сотрудник");
	ОбъектыПечати.Колонки.Добавить("ИмяОбласти");
	Возврат ОбъектыПечати;
КонецФункции 

Функция ИмяВстраиваемойОбласти(ОбъектыПечати, Ссылка, Сотрудник = Неопределено) Экспорт
	
	ИмяВстраиваемойОбласти = Неопределено;
	ВстраиваемаяОбласть = ВстраиваемаяОбласть(ОбъектыПечати, Ссылка, Сотрудник);
	Если НЕ ВстраиваемаяОбласть = Неопределено Тогда
	   ИмяВстраиваемойОбласти = ВстраиваемаяОбласть.ИмяОбласти;
	КонецЕсли;
	
	Возврат ИмяВстраиваемойОбласти;
	
КонецФункции

Процедура ЗадатьОбластьПечатиВстраиваемойОбласти(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка, Сотрудник)
	
	Элемент = ВстраиваемаяОбласть(ОбъектыПечати, Ссылка, Сотрудник);
	Если Элемент = Неопределено Тогда
		Элемент = НоваяВстраиваемаяОбласть(ОбъектыПечати, Ссылка, Сотрудник);
		Элемент.ИмяОбласти = "Документ_" + Формат(ОбъектыПечати.Количество() + 1, "ЧН=; ЧГ=");
	КонецЕсли;
	
	НомерСтрокиОкончание = ТабличныйДокумент.ВысотаТаблицы;
	ТабличныйДокумент.Область(НомерСтрокиНачало, , НомерСтрокиОкончание, ).Имя = Элемент.ИмяОбласти;		
	
КонецПроцедуры

Функция ВстраиваемаяОбласть(ОбъектыПечати, Ссылка, Сотрудник = Неопределено)
	
	ВстраиваемаяОбласть = Неопределено;
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат ВстраиваемаяОбласть;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", Ссылка);
	Если НЕ Сотрудник = Неопределено Тогда
		Отбор.Вставить("Сотрудник", Сотрудник);
	КонецЕсли;
	
	СтрокиПоОтбору = ОбъектыПечати.НайтиСтроки(Отбор);
	Если СтрокиПоОтбору.Количество() > 0 Тогда
		ВстраиваемаяОбласть = СтрокиПоОтбору[0];
	КонецЕсли;
	
	Возврат ВстраиваемаяОбласть;

КонецФункции

Функция НоваяВстраиваемаяОбласть(ОбъектыПечати, Ссылка, Сотрудник)
	НоваяВстраиваемаяОбласть = ОбъектыПечати.Добавить();
	НоваяВстраиваемаяОбласть.Ссылка = Ссылка;
	НоваяВстраиваемаяОбласть.Сотрудник = Сотрудник;
	Возврат НоваяВстраиваемаяОбласть;	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли