#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НавигационнаяСсылка = "e1cib/app/" + ЭтотОбъект.ИмяФормы;
	
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Нет доступа к просмотру настроек счетов учета!';uk='Немає доступу до перегляду настройок рахунків обліку!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("НастройкаСчетовУчета") Тогда
		ПараметрыНастройкиСчетовУчета = Параметры.НастройкаСчетовУчета;
	Иначе
		ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(Неопределено);
	КонецЕсли;
	
	ЗаполнитьНастройкуСчетовУчета(ПараметрыНастройкиСчетовУчета);
	
	УстановитьУсловноеОформление();
	
	#Область ЗаполнениеПоПараметрам
	
	Организация = Параметры.Организация;
	ДатаОкончанияПериода = Параметры.ДатаОкончанияПериода;
	
	ОрганизацииДляНастройки = Неопределено; // Пусто - все организации
	Параметры.Свойство("ОрганизацииДляНастройки", ОрганизацииДляНастройки);
	
	Если Параметры.Свойство("ПоказыватьТолькоТребующиеНастройки") Тогда
		ПоказыватьТолькоТребующиеНастройки = Параметры.ПоказыватьТолькоТребующиеНастройки;
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытьИсключения") Тогда
		ПоказыватьОбщуюНастройку = 0;
	КонецЕсли;
	
	Если Параметры.Свойство("АналитикаУчета") Тогда
		РедактируемаяАналитика = Параметры.АналитикаУчета;
		ОтборАналитикиВключен = Истина;
		РедактируемаяАналитикаПредставление = ПредставлениеОтбора(ОтборАналитикиВключен, РедактируемаяАналитика);
		РазделыУчета = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.РазделыУчетаПоАналитикеУчета(РедактируемаяАналитика, Истина);
		ТекущийРазделУчета = РазделыУчета.Получить(0);
		ПоказыватьОбщуюНастройку = 0;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекущаяСтраница") Тогда
		ТекущийРазделУчета = Параметры.ТекущаяСтраница;
	КонецЕсли;
	
	#КонецОбласти
	
	ЗаполнитьСписокВыбораОрганизации(ОрганизацииДляНастройки);
	
	ПанельУправленияСвернута = Ложь;
	РедактированиеЗаполненныхСчетов = Ложь;
	ПоказыватьОбщуюНастройку = 1;
	
	УстановитьСвойстваЭлементовФормы();
	
	ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц();
	
	Если ТекущийРазделУчета = "" Или Элементы.ТекущийРазделУчета.СписокВыбора.НайтиПоЗначению(ТекущийРазделУчета) = Неопределено Тогда
		ТекущийРазделУчета = Элементы.ТекущийРазделУчета.СписокВыбора.Получить(0).Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РедактируемаяАналитика) Тогда
		ПоказыватьОбщуюНастройку = 0;
		ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета);
		СтруктураОтбора = Новый Структура("АналитикаУчета", РедактируемаяАналитика);
		ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		ВыполняетсяЗакрытие = Истина;
		ТекстПредупреждения = 
		НСтр("ru='Настройки счетов были изменены, но не сохранены. Для того, чтобы изменения вступили в силу следует сохранить настройку.';uk='Настройки рахунків були змінені, але не збережені. Для того, щоб зміни вступили в силу слід зберегти настройку.'");
		Возврат;
	КонецЕсли;
		
	Если НЕ ВыполняетсяЗакрытие И Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru='Настройки счетов были изменены. Сохранить изменения?';uk='Настройки рахунків були змінені. Зберегти зміни?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СохранитьНастройкиОтраженияВУчете();
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ИзмененыНастройкиОтраженияВУчете И Не ЗавершениеРаботы Тогда
		Оповестить("ИзмененыНастройкиОтраженияВУчете");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаконченаНастройкаСчетовУчета" Тогда
		ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц(СтрСоединить(Параметр, ","));
		ОбработатьРезультатВыполненияФоновогоЗадания();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбработатьРезультатВыполненияФоновогоЗадания();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОтбораНастроек();
	ОбработатьРезультатВыполненияФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоТребующиеНастройкиПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьОбщуюНастройкуПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийРазделУчетаПриИзменении(Элемент)
	
	РедактируемаяАналитика = Неопределено;
	ОтборАналитикиВключен = Ложь;
	РедактируемаяАналитикаПредставление = ПредставлениеОтбора(ОтборАналитикиВключен, РедактируемаяАналитика);
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ТаблицаНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если СтрНайти(Поле.Имя, "СчетУчета") + СтрНайти(Поле.Имя, "Субконто") = 0
		И Не СтрЗаканчиваетсяНа(Поле.Имя, "Организация") И Не СтрЗаканчиваетсяНа(Поле.Имя, "МестоУчета") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИмяПоля = СтрЗаменить(Поле.Имя, ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета) + "_", "");
		Если ИмяПоля = "КоличествоИсключений" Или ИмяПоля = "КоличествоИсключенийПоОрганизациям" Тогда
			ПоказыватьОбщуюНастройку = 0;
			РедактируемаяАналитика = СтрокаТаблицы.АналитикаУчета;
			ОтборАналитикиВключен = Истина;
			РедактируемаяАналитикаПредставление = ПредставлениеОтбора(ОтборАналитикиВключен, РедактируемаяАналитика);
			УстановитьТекущуюСтраницу(ЭтотОбъект);
			ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
		Иначе
			ПоказатьЗначение(Неопределено, СтрокаТаблицы[ИмяПоля]);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.ТекущийЭлемент.Имя);
	ОбновитьИзмененныеДанные(ИмяЭлементаНастройки, Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбработатьИзменениеСчетовУчетаСтроки(Элемент.ТекущиеДанные);
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Если СтрНайти(Элемент.ТекущийЭлемент.Имя, "СчетУчета") + СтрНайти(Элемент.ТекущийЭлемент.Имя, "Субконто") <> 0
		Или СтрЗаканчиваетсяНа(Элемент.ТекущийЭлемент.Имя, "Организация") Или СтрЗаканчиваетсяНа(Элемент.ТекущийЭлемент.Имя, "МестоУчета") Тогда
		ПроверкаВозможностиРедактирования(Элемент, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПередУдалением(Элемент, Отказ)
	
	Если ПоказыватьОбщуюНастройку = 1 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Элемент <> Неопределено И Элемент.ТекущиеДанные <> Неопределено Тогда
		СтруктураОтбора = Новый Структура("АналитикаУчета, ЭтоОбщаяНастройка", Элемент.ТекущиеДанные.АналитикаУчета, Истина);
		СтрокиСДаннойАналитикой = ЭтотОбъект[Элемент.Имя].НайтиСтроки(СтруктураОтбора);
		
		ЗначенияПоУмолчанию = ?(СтрокиСДаннойАналитикой.Количество() > 0, СтрокиСДаннойАналитикой.Получить(0), Неопределено);
		Если ЗначенияПоУмолчанию <> Неопределено Тогда
			ЗначенияПоУмолчанию.КоличествоИсключений = ЗначенияПоУмолчанию.КоличествоИсключений - 1;
			Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.МестоУчета) Тогда
				ЗначенияПоУмолчанию.КоличествоИсключенийПоОрганизациям = ЗначенияПоУмолчанию.КоличествоИсключенийПоОрганизациям - 1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура("Организация", Организация);
	Если ЗначениеЗаполнено(РедактируемаяАналитика) Тогда
		ЗначенияЗаполнения.Вставить("АналитикаУчета", РедактируемаяАналитика);
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура("ЗначенияЗаполнения, ВозвратЗначенияБезЗаписи", ЗначенияЗаполнения, Истина);
	ОповещениеОДобавленииЗаписиИсключений = Новый ОписаниеОповещения("ЗаполнениеНовойЗаписиИсключения", ЭтотОбъект, ТекущийРазделУчета);
	ОткрытьФормуСчетовУчета(СтруктураПараметры, ОповещениеОДобавленииЗаписиИсключений);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроекПриАктивизацииЯчейки(Элемент)
	
	Если Элемент <> Неопределено И Элемент.ТекущийЭлемент <> Неопределено И Элемент.ТекущиеДанные <> Неопределено Тогда
		ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.ТекущийЭлемент.Имя);
		Если КэшДанныхМеханизмов.НастройкаСчетовУчета.СчетаСДинамическимиПараметрамиВыбора.Найти(ИмяЭлементаНастройки) <> Неопределено Тогда
			Элемент.ТекущийЭлемент.ПараметрыВыбора = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСчетаУчета(ИмяЭлементаНастройки, КэшДанныхМеханизмов.НастройкаСчетовУчета, Элемент.ТекущиеДанные, ТекущийРазделУчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.Имя);
	ТекущаяСтрока = Элементы[ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета)].ТекущиеДанные;
	
	// Сбросим флаг по умолчанию:
	ТекущаяСтрока.СчетаУчетаПоУмолчанию = СтрЗаменить(ТекущаяСтрока.СчетаУчетаПоУмолчанию, ИмяЭлементаНастройки + ",", "");
	
	ОбновитьИзмененныеДанные(ИмяЭлементаНастройки, ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.Имя);
	ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета);
	ТекущаяСтрока = Элементы[ИмяТаблицыДанных].ТекущиеДанные;
	
	НастройкаТекущегоРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ТекущийРазделУчета);
	СтруктураОтбора = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущаяСтрока);
	СтруктураОтбора[ИмяЭлементаНастройки] = ВыбранноеЗначение;
	НастройкиМестаУчета = НастройкаТекущегоРаздела.СвойстваМестаУчета;
	Если Не НастройкиМестаУчета.Используется Тогда
		// Для данной таблицы не используется настройка по местам учета, удалим соответствующее измерение:
		СтруктураОтбора.Удалить("МестоУчета");
	КонецЕсли;
	
	СтрокиССовпадающимиИзмерениями = ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиССовпадающимиИзмерениями.Количество() > 0 Тогда
		ТекстПредупреждения = НСтр("ru='Строка с соответствующими измерениями уже есть в таблице исключений.';uk='Рядок з відповідними вимірами вже є в таблиці винятків.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроек_ОтборАналитикиВключенПриИзменении(Элемент)
	РедактируемаяАналитикаПредставление = ПредставлениеОтбора(ОтборАналитикиВключен, РедактируемаяАналитика);
	УстановитьТекущуюСтраницу(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНастроек_РедактируемаяАналитикаПриИзменении(Элемент)
	РедактируемаяАналитикаПредставление = ПредставлениеОтбора(ОтборАналитикиВключен, РедактируемаяАналитика);
	Если ОтборАналитикиВключен Тогда
		УстановитьТекущуюСтраницу(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройкуИЗакрыть(Команда)
	
	СохранитьНастройкиОтраженияВУчете();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройку(Команда)
	
	СохранитьНастройкиОтраженияВУчете();
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройку(Команда)
	
	ЗаполнитьТаблицуКПрименениюНастроки();
	
	КоличествоДокументов = ТаблицаДокументов.Количество();
	Если КоличествоДокументов > 0 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Новая настройка будет применена для %1. Продолжить?';uk='Нова настройка буде застосована для %1. Продовжити?'"),
			ЧислоДокументовПрописью(КоличествоДокументов));

		ПоказатьВопрос(Новый ОписаниеОповещения("ПрименитьНастройкуЗавершение", ЭтотОбъект, Новый Структура("КоличествоДокументов", КоличествоДокументов)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	Иначе
		СохранитьНастройкиОтраженияВУчете();
	КонецЕсли;
	
	ПрименитьНастройкуФрагмент(КоличествоДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуФрагмент(Знач КоличествоДокументов)
	
	ОповеститьПользователяОПримененииНастройки(КоличествоДокументов);

КонецПроцедуры

&НаКлиенте
Процедура АнализСчетовТребующихНастройки(Команда)
	
	АнализСчетовТребующихНастройкиСервер();
	ОбработатьРезультатВыполненияФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьПанельУправления(Команда)
	
	ПанельУправленияСвернута = НЕ ПанельУправленияСвернута;
	
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьПанельУправления(Команда)
	
	ПанельУправленияСвернута = НЕ ПанельУправленияСвернута;
	
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьСчетаУчета(Команда)
	
	ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета);
	
	НастройкаСчетовУчетаФормы = СкопироватьНастройкуСчетовУчета(КэшДанныхМеханизмов.НастройкаСчетовУчета, ТекущийРазделУчета);
	НастройкаРазделаФормы = НастройкаСчетовУчетаФормы.НастройкиРазделов.Получить(ТекущийРазделУчета);
	
	СписокСчетов = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ТекущийРазделУчета).СчетаУчета;
	СписокСубконто = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ТекущийРазделУчета).Субконто;
	
	ТребуетсяНастройка = Ложь;
	РедактированиеРазрешено = РедактированиеЗаполненныхСчетов;
	ВыделенныеСтроки = Элементы[ИмяТаблицыДанных].ВыделенныеСтроки;
	МассивСчетовДляУказания = Новый Массив;
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		СтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].НайтиПоИдентификатору(ИдентификаторСтроки);
		
		СчетаТребующиеНастройки = СтрРазделить(СтрокаТаблицы.ТребуетсяНастройкаСчетов, ",", Ложь);
		
		Если СчетаТребующиеНастройки.Количество() Тогда
			Если МассивСчетовДляУказания.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(НастройкаСчетовУчетаФормы.РеквизитыАналитики, СтрокаТаблицы);
			Иначе
				Для каждого РеквизитАналитики Из НастройкаСчетовУчетаФормы.РеквизитыАналитики Цикл
					Если РеквизитАналитики.Значение <> СтрокаТаблицы[РеквизитАналитики.Ключ] Тогда
						РеквизитАналитики.Значение = Неопределено;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Для каждого СчетТребующийНастройки Из СчетаТребующиеНастройки Цикл
			
			ТребуетсяНастройка = Истина;
			ЭлементСписка = СписокСчетов.НайтиПоЗначению(СчетТребующийНастройки);
			Если ЭлементСписка <> Неопределено И ЭлементСписка.Пометка И НастройкаРазделаФормы.СчетаУчета.НайтиПоЗначению(СчетТребующийНастройки) = Неопределено Тогда
				Если РедактированиеЗаполненныхСчетов Или Не ЗначениеЗаполнено(СтрокаТаблицы["СчетУчета_" + СчетТребующийНастройки]) Тогда
					НастройкаРазделаФормы.СчетаУчета.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, Истина);
					РедактированиеРазрешено = Истина;
				КонецЕсли;
			КонецЕсли;
			ЭлементСписка = СписокСубконто.НайтиПоЗначению(СчетТребующийНастройки);
			Если ЭлементСписка <> Неопределено И ЭлементСписка.Пометка И НастройкаРазделаФормы.Субконто.НайтиПоЗначению(СчетТребующийНастройки) = Неопределено Тогда
				Если РедактированиеЗаполненныхСчетов Или Не ЗначениеЗаполнено(СтрокаТаблицы["Субконто_" + СчетТребующийНастройки]) Тогда
					НастройкаРазделаФормы.Субконто.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, Истина);
					РедактированиеРазрешено = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Не ТребуетсяНастройка Тогда 
		ПоказатьПредупреждение(,НСтр("ru='Для выделенных строк все необходимые счета указаны.';uk='Для виділених рядків всі необхідні рахунки вказані.'")); 
		Возврат; 
	КонецЕсли;
	
	Если Не РедактированиеРазрешено Тогда
		ПоказатьПредупреждение(,НСтр("ru='Для выделенных строк нельзя изменить счета учета (нельзя изменять заполненные счета учета).';uk='Для виділених рядків неможна змінити рахунки обліку (можна змінювати заповнені рахунки обліку).'")); 
		Возврат;
	КонецЕсли;

	СтруктураПараметры = Новый Структура("Организация, ТолькоУказаниеСчетов, НастройкаСчетовУчета", Организация, Истина, НастройкаСчетовУчетаФормы);
	ОповещениеОВыборе = Новый ОписаниеОповещения("УказатьСчетаУчетаЗавершение", ЭтотОбъект, ВыделенныеСтроки);
	ОткрытьФормуСчетовУчета(СтруктураПараметры, ОповещениеОВыборе);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	ОповещениеРазблокированиеРеквизитовЗакрытие = Новый ОписаниеОповещения("РазрешитьРедактированиеРеквизитовЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("РедактированиеРазрешено", РедактированиеЗаполненныхСчетов);
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РазблокированиеРеквизитов",
		ПараметрыФормы, ЭтотОбъект, , , , ОповещениеРазблокированиеРеквизитовЗакрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
КонецПроцедуры

#Область ПроцедурыОбработчикиКомандОбновленияТаблицНастроек

&НаКлиенте
Процедура ОповеститьПользователяОПримененииНастройки(КоличествоДокументов)
	
	ТекстСообщения = "";
	
	Если КоличествоДокументов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Настройка применена для %1';uk='Настройка застосована для %1'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Отработано %1';uk='Відпрацьовано %1'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
	Иначе
		ТекстСообщения = НСтр("ru='Отсутствуют документы, требующие применения настройки';uk='Відсутні документи, що потребують застосування настройки'");
		ТекстЗаголовка = НСтр("ru='Отсутствуют документы для настройки';uk='Відсутні документи для настройки'");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицу(Команда)
	ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц(ТекущийРазделУчета);
	ОбработатьРезультатВыполненияФоновогоЗадания("ОбновитьПредставлениеСпискаВыбора");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаПериод(Команда)
	ПараметрыПериода = Новый Структура("НачалоПериода, КонецПериода", ДобавитьМесяц(ДатаОкончанияПериода.Дата, -1), ДатаОкончанияПериода.Дата);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборПериодаДобавленияЗавершение", ЭтотОбъект, ТекущийРазделУчета);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыПериода, ЭтотОбъект, , , , ОповещениеОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоНезаполненнымСчетам(Команда)
	
	ДобавитьЗаписиВТаблицуСервер("ПоНеотраженным");
	ОбработатьРезультатВыполненияФоновогоЗадания("ИзменитьМодифицированность");
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиКомандОткрытияФормРасшифровокДанных

&НаКлиенте
Процедура ПоказатьПредупреждениеЕслиНеВыбранаСтрока()
	
	ПоказатьПредупреждение(, НСтр("ru='Выберите строку для расшифровки';uk='Виберіть рядок для розшифровки'"), 10);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоНоменклатуре(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_Номенклатура.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Справочник.Номенклатура.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамПродажи(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_Номенклатура.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Склад, ГруппаФинансовогоУчета",
			СтрокаТаблицы.Организация, СтрокаТаблицы.МестоУчета, СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамПродаж", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамПоступления(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_Номенклатура.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Склад, ГруппаФинансовогоУчета",
			СтрокаТаблицы.Организация, СтрокаТаблицы.МестоУчета, СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамПоступления", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоСоглашениямСКлиентами(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_РасчетыСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.АналитикаУчета);
		СтруктураМенеджер = Новый Структура("Менеджер");
		СтруктураПараметры = Новый Структура("Отбор, СтруктураБыстрогоОтбора",
			СтруктураОтбор,
			СтруктураМенеджер);
		ОткрытьФорму("Справочник.СоглашенияСКлиентами.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоСоглашениямСПоставщиками(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_РасчетыСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.АналитикаУчета);
		СтруктураМенеджер = Новый Структура("Менеджер");
		СтруктураПараметры = Новый Структура("Отбор, СтруктураБыстрогоОтбора",
			СтруктураОтбор,
			СтруктураМенеджер);
		ОткрытьФорму("Справочник.СоглашенияСПоставщиками.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетовСКлиентами(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_РасчетыСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ГруппаФинансовогоУчета", СтрокаТаблицы.Организация, СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамРасчетовСКлиентами", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетовСПоставщиками(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_РасчетыСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ГруппаФинансовогоУчета", СтрокаТаблицы.Организация, СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамРасчетовСПоставщиками", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамДоходы(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_Доходы.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Подразделение, СтатьяДоходов",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.МестоУчета,
			СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияДоходов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасходы(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_Расходы.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Подразделение, СтатьяРасходов",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.МестоУчета,
			СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияРасходов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетыПоСертификатам(Команда)
	
	СтрокаТаблицы = Элементы.ТаблицаНастроек_ПодарочныеСертификаты.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ВидСертификата",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.АналитикаУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияСертификатов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ПроцедурыОбработчикиКомандСозданияНовыхНастроекСчетовУчета

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчета(Команда)
	
	ЗначенияЗаполнения = Новый Структура("Организация", Организация);
	Если РедактируемаяАналитика <> Неопределено И ОтборАналитикиВключен Тогда
		ЗначенияЗаполнения.Вставить("АналитикаУчета", РедактируемаяАналитика);
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура("ЗначенияЗаполнения, ВозвратЗначенияБезЗаписи, ЭтоНовый", ЗначенияЗаполнения, Истина, Истина);
	ОповещениеОДобавленииЗаписиИсключений = Новый ОписаниеОповещения("ЗаполнениеНовойЗаписиИсключения", ЭтотОбъект, ТекущийРазделУчета);
	ОткрытьФормуСчетовУчета(СтруктураПараметры, ОповещениеОДобавленииЗаписиИсключений);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеФормы

&НаСервере
Процедура ЗаполнитьНастройкуСчетовУчета(ПараметрыНастройки)
	
	ПараметрыНастройки.ИмяГруппыНастроекСчетовУчета = "Группа_ТаблицыНастроек";
	ПараметрыНастройки.ПрефиксЭлементовФормы = "ТаблицаНастроек";
	ПараметрыНастройки.ПрефиксПутиКДанным = "ТаблицаНастроек_";
	ПараметрыНастройки.ПрефиксПутиКДаннымРеквизитов = "ТаблицаНастроек_";
	ПараметрыНастройки.РедактированиеВСписке = Истина;
	ПараметрыНастройки.РазбитьПоРазделам = Истина;
	ПараметрыНастройки.РазбитьПоРазделамЭлементы = Истина;
	ПараметрыНастройки.СоздаватьВСлучаеОтсутствия = Ложь;
	
	Для каждого НастройкаРаздела Из ПараметрыНастройки.НастройкиРазделов Цикл
		НастройкаРаздела.Значение.Вставить("ТребуютНастройки", 0);
		НастройкаРаздела.Значение.Вставить("ТребуютНастройкиОбщие", 0);
	КонецЦикла;
	
	КэшДанныхМеханизмов = Новый Структура;
	ОбщегоНазначенияУТ.СохранитьДанныеМеханизмаВКэшФормы(ЭтотОбъект, "НастройкаСчетовУчета", ПараметрыНастройки);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область ОформлениеСчетов
	
	СписокРазделов = Новый СписокЗначений;
	СписокРазделов.Добавить("НоменклатураПереданная");
	СписокРазделов.Добавить("НоменклатураПринятая");
		
	Для каждого НастройкаРаздела Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
		
		ЭлементУсловногоОформленияСтроки = ?(СписокРазделов.НайтиПоЗначению(НастройкаРаздела.Ключ) = Неопределено, Неопределено, УсловноеОформление.Элементы.Добавить());
		ЭлементУсловногоОформленияСтроки = Неопределено;
		
		СчетаУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Значение.СчетаУчета);
		Для каждого СчетУчета Из СчетаУчета Цикл
			
			ПутьКДанным = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, СчетУчета);
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, СчетУчета);
			
			Если ЭлементУсловногоОформленияСтроки <> Неопределено Тогда
				КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформленияСтроки.Поля, Элементы[ИмяЭлемента].Имя);
			КонецЕсли;
			
			// Оформление счетов по умолчанию:
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяЭлемента].Имя);
			
			ПутьСчетаУчетаПоУмолчанию = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "СчетаУчетаПоУмолчанию", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьСчетаУчетаПоУмолчанию, "," + СчетУчета + ",", ВидСравненияКомпоновкиДанных.Содержит);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьКДанным, Неопределено, ВидСравненияКомпоновкиДанных.Заполнено);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстИнформационнойНадписи);
			
			// Оформление счетов требующих заполнения:
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяЭлемента].Имя);
			
			ПутьТребуетсяНастройкаСчетов = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "ТребуетсяНастройкаСчетов", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьТребуетсяНастройкаСчетов, "," + СчетУчета + ",", ВидСравненияКомпоновкиДанных.Содержит);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьКДанным, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
			
			// Оформление счетов не требующих заполнения:
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяЭлемента].Имя);
			
			ПутьИзмененныеДанные = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "ИзмененныеДанные", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьТребуетсяНастройкаСчетов, "," + СчетУчета + ",", ВидСравненияКомпоновкиДанных.НеСодержит);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьКДанным, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ПутьИзмененныеДанные, "," + СчетУчета + ",", ВидСравненияКомпоновкиДанных.НеСодержит);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Не требуется';uk='Не потрібно'"));
			
			НастройкаСчетовУчета.УстановитьУсловноеОформление(ЭтотОбъект, НастройкаРаздела.Ключ, СчетУчета);
			
		КонецЦикла;
		
		НастройкаСчетовУчета.УстановитьУсловноеОформление(ЭтотОбъект, НастройкаРаздела.Ключ, "АналитикаУчета");
		НастройкаСчетовУчета.УстановитьУсловноеОформление(ЭтотОбъект, НастройкаРаздела.Ключ, "МестоУчета");
		
		Если ЭлементУсловногоОформленияСтроки <> Неопределено Тогда
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "АналитикаУчета", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформленияСтроки.Поля, Элементы[ИмяЭлемента].Имя);
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "МестоУчета", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформленияСтроки.Поля, Элементы[ИмяЭлемента].Имя);
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "Организация", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформленияСтроки.Поля, Элементы[ИмяЭлемента].Имя);
		КонецЕсли;
		
		МассивСубконто = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Значение.Субконто);
		Для каждого Субконто Из МассивСубконто Цикл
			
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, Субконто, "Субконто");
			Если ЭлементУсловногоОформленияСтроки <> Неопределено Тогда
				КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформленияСтроки.Поля, Элементы[ИмяЭлемента].Имя);
			КонецЕсли;
			
			НастройкаСчетовУчета.УстановитьУсловноеОформление(ЭтотОбъект, НастройкаРаздела.Ключ, Субконто);
			
		КонецЦикла;
		
		Если НастройкаСчетовУчетаКлиентСервер.РазделыУчетаДляКоторыхНастраиваютсяИсключения().Найти(НастройкаРаздела.Ключ) <> Неопределено
			И НастройкаРаздела.Значение.СвойстваАналитики.ТипЗначения <> Неопределено Тогда
			
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "КоличествоИсключений", "Реквизит");
			ИмяРеквизита = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "КоличествоИсключений", "Реквизит");
		
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяЭлемента].Имя);
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ИмяРеквизита, 0);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Настроить';uk='Настроїти'"));
			
		КонецЕсли;
		
		Если НастройкаСчетовУчетаКлиентСервер.РазделыУчетаДляКоторыхНастраиваютсяИсключения().Найти(НастройкаРаздела.Ключ) <> Неопределено
			И НастройкаРаздела.Значение.СвойстваАналитики.ТипЗначения <> Неопределено И ИспользоватьДопИсключения(НастройкаРаздела.Ключ) Тогда
			
			ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "КоличествоИсключенийПоОрганизациям", "Реквизит");
			ИмяРеквизита = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "КоличествоИсключенийПоОрганизациям", "Реквизит");
		
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяЭлемента].Имя);
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ИмяРеквизита, 0);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Настроить';uk='Настроїти'"));
			
		КонецЕсли;
		
		Если ЭлементУсловногоОформленияСтроки <> Неопределено Тогда
			ИмяРеквизитаМестоУчета = НастройкаСчетовУчетаКлиентСервер.ИмяРеквизитаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "МестоУчета", "Реквизит");
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ЭлементУсловногоОформленияСтроки.Отбор, ИмяРеквизитаМестоУчета, Неопределено, ВидСравненияКомпоновкиДанных.Равно);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ЭлементУсловногоОформленияСтроки.Отбор, "ТекущийРазделУчета", СписокРазделов, ВидСравненияКомпоновкиДанных.ВСписке);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ЭлементУсловногоОформленияСтроки.Отбор, "ПоказыватьОбщуюНастройку", 0, ВидСравненияКомпоновкиДанных.Равно);
			
			ЭлементУсловногоОформленияСтроки.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		КонецЕсли;
		
		
	КонецЦикла;
	
	#КонецОбласти
	
	#Область Номенклатура
	
	
	#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	#Область ЗаполнениеСпискаКомандИзменяющихДанные
	
	МассивКоманд = Новый Массив;
	МассивКоманд.Добавить("СоздатьНастройкуСчетовУчета");
	МассивКоманд.Добавить("Удалить");
	МассивКоманд.Добавить("Добавить");
	МассивКоманд.Добавить("УказатьСчетаУчета");
	
	#КонецОбласти
	
	МожноИзменять = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета);
	Настройка = КэшДанныхМеханизмов.НастройкаСчетовУчета;
	
	#Область ЗаполнениеСпискаВыбораСтраниц
	
	Элементы.ТекущийРазделУчета.СписокВыбора.Очистить();
	Для каждого РазделУчета Из РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СписокРазделовСчетовУчета() Цикл
		Если РазделУчета.Пометка И Настройка.НастройкиРазделов.Получить(РазделУчета.Значение) <> Неопределено Тогда
			Элементы.ТекущийРазделУчета.СписокВыбора.Добавить(РазделУчета.Значение, РазделУчета.Представление);
		КонецЕсли;
		Элементы["ТаблицаНастроек_Группа_" + РазделУчета.Значение].Видимость = Настройка.НастройкиРазделов.Получить(РазделУчета.Значение) <> Неопределено;
	КонецЦикла;
	
	// Если показывается только одна страница - скрываем поле переключателя страниц:
	Элементы.ГруппаУправлениеОткрытиемСтраниц.Видимость = Элементы.ТекущийРазделУчета.СписокВыбора.Количество() <> 1;
	
	#КонецОбласти
	
	НастройкаСчетовУчета.УстановитьНастройкиСчетовУчетаВФорме(ЭтотОбъект);
	
	Для каждого НастройкаРаздела Из Настройка.НастройкиРазделов Цикл
		
		ИмяЭлементаОтбораАналитики = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "РедактируемаяАналитика", "Реквизит");
		ЭлементФормы = Элементы.Найти(ИмяЭлементаОтбораАналитики);
		Если ЭлементФормы <> Неопределено Тогда
			ЭлементФормы.ОграничениеТипа = НастройкаРаздела.Значение.СвойстваАналитики.ТипЗначения;
		КонецЕсли;
		
		Если Элементы.Организация.СписокВыбора.Количество() Тогда
			ИмяЭлементаОрганизации = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, "Организация", "Реквизит");
			ЭлементФормы = Элементы.Найти(ИмяЭлементаОрганизации);
			Если ЭлементФормы <> Неопределено Тогда
				ЭлементФормы.СписокВыбора.ЗагрузитьЗначения(Элементы.Организация.СписокВыбора.ВыгрузитьЗначения());
			КонецЕсли;
		КонецЕсли;
		
		Если Не МожноИзменять Тогда
			Для каждого Команда Из МассивКоманд Цикл
				ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(
					КэшДанныхМеханизмов.НастройкаСчетовУчета, НастройкаРаздела.Ключ, Команда, ?(Команда = "Добавить", "Группа", "Реквизит"));
				Элементы[ИмяЭлемента].Доступность = Ложь;
			КонецЦикла;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОрганизации(ОграничениеВПараметрахВыбора)
	
	СписокВыбора = Элементы.Организация.СписокВыбора;
	
	ОграничениеНаУровнеЗаписей = УправлениеДоступом.РазрешенныеЗначенияДляДинамическогоСписка("РегистрСведений.ПорядокОтраженияНаСчетахУчета", Тип("СправочникСсылка.Организации"));
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеСправочника.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(&НетОрганизацийОтбора ИЛИ ДанныеСправочника.Ссылка В (&СписокОрганизацийОтбора))
	|	И (&ВсеОрганизацииДоступны ИЛИ ДанныеСправочника.Ссылка В (&СписокДоступныхОрганизаций))
	|	И ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|";
	
	Запрос.УстановитьПараметр("СписокОрганизацийОтбора", ОграничениеВПараметрахВыбора);
	Запрос.УстановитьПараметр("НетОрганизацийОтбора", ОграничениеВПараметрахВыбора = Неопределено);
	Запрос.УстановитьПараметр("СписокДоступныхОрганизаций", ОграничениеНаУровнеЗаписей);
	Запрос.УстановитьПараметр("ВсеОрганизацииДоступны", ОграничениеНаУровнеЗаписей = Неопределено);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
	КонецЦикла;
	Если СписокВыбора.Количество() = 1 Тогда
		Организация = СписокВыбора.Получить(0).Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПанелейУправления(Форма)
	
	Форма.Элементы.ГруппаПанельРазвернута.Видимость = НЕ Форма.ПанельУправленияСвернута;
	Форма.Элементы.ГруппаПанельСвернута.Видимость = Форма.ПанельУправленияСвернута;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовПоОрганизацииИФункциональнымОпциям()
	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиСохраненияНастроекФормы(Форма)
	
	Форма.Элементы.СохранитьНастройкуИЗакрыть.Доступность = Форма.Модифицированность;
	Форма.Элементы.СохранитьНастройку.Доступность = Форма.Модифицированность;
	Форма.Элементы.ПрименитьНастройку.Доступность = Форма.Модифицированность;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Форма)

	ЭтоПодраздел = НастройкаСчетовУчетаКлиентСервер.ЭтоПодраздел(Форма.ТекущийРазделУчета);
	Настройка = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	НастройкаТекущегоРаздела = Настройка.НастройкиРазделов.Получить(Форма.ТекущийРазделУчета);
	ЕстьНастройкаИсключений = НастройкаСчетовУчетаКлиентСервер.РазделыУчетаДляКоторыхНастраиваютсяИсключения().Найти(Форма.ТекущийРазделУчета) <> Неопределено;
	ЕстьОбщаяНастройка = НастройкаТекущегоРаздела.СвойстваАналитики.ТипЗначения <> Неопределено;
	
	СтруктураОтбора = Новый Структура;
	
	Если Форма.ПоказыватьТолькоТребующиеНастройки Тогда
		ИмяКолонкиТребуетсяНастройка = "ТребуетсяНастройка" + ?(ЭтоПодраздел, Форма.ТекущийРазделУчета, "");
		СтруктураОтбора.Вставить(ИмяКолонкиТребуетсяНастройка, Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.Организация) И (Форма.ПоказыватьОбщуюНастройку = 0 Или Форма.ТекущийРазделУчета = "ДенежныеСредства")
		И НастройкаСчетовУчетаКлиентСервер.РазделыУчетаНеЗависящиеОтОрганизации().Найти(Форма.ТекущийРазделУчета) = Неопределено Тогда
		// Отбор по организации добавляем, если организация заполнена и в выводимой таблице есть колонка "Организация" (это
		// все настройки исключений).
		СтруктураОтбора.Вставить("Организация", Форма.Организация);
	КонецЕсли;
	
	Если НастройкаСчетовУчетаКлиентСервер.ОбщийРазделУчетаПоИмениТекущегоРаздела(Форма.ТекущийРазделУчета) = "РасчетыСПартнерами" Тогда
		
		// Отбор по виду расчетов добавляем только для таблиц расчетов с партнерами (клиенты, поставщики, комиссионеры,
		// комитенты, кредиторы, дебиторы, лизинг).
		СтруктураОтбора.Вставить(Форма.ТекущийРазделУчета, Истина);
		
	КонецЕсли;
	
	Если ЕстьНастройкаИсключений И ЕстьОбщаяНастройка Тогда
		// Отбираем по общей настройке только тогда, когда для раздела может быть настройка по аналитике учета (т.е. есть общая настройка) и для него настраиваются исключения.
		СтруктураОтбора.Вставить("ЭтоОбщаяНастройка", Форма.ПоказыватьОбщуюНастройку = 1);
	КонецЕсли;
	
	Если НастройкаСчетовУчетаКлиентСервер.ОбщийРазделУчетаПоИмениТекущегоРаздела(Форма.ТекущийРазделУчета) = "Номенклатура" Тогда
		
		Если Форма.ТекущийРазделУчета <> "НоменклатураСобственная" И Форма.ПоказыватьОбщуюНастройку = 0 Тогда
			СтруктураОтбора.Вставить("МестоУчета", ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.ОтборАналитикиВключен И Форма.ПоказыватьОбщуюНастройку = 0 Тогда
		СтруктураОтбора.Вставить("АналитикаУчета", Форма.РедактируемаяАналитика);
	КонецЕсли;
	
	ТекущийОбщийРаздел = НастройкаСчетовУчетаКлиентСервер.ОбщийРазделУчетаПоИмениТекущегоРаздела(Форма.ТекущийРазделУчета);
	Для каждого РазделУчета Из Настройка.СписокРазделовУчета Цикл
		
		ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(РазделУчета.Значение);
		ИмяТаблицы = СтрЗаменить(ИмяТаблицы, "ТаблицаНастроек_", "");
		Форма.Элементы["Страница_" + ИмяТаблицы].Видимость = (ИмяТаблицы = ТекущийОбщийРаздел);
		Форма.Элементы["ТаблицаНастроек_Группа_" + РазделУчета.Значение].Видимость = (РазделУчета.Значение = Форма.ТекущийРазделУчета);
		
	КонецЦикла;
	
	ПредставлениеТаблицы = Настройка.НастройкиРазделов.Получить(Форма.ТекущийРазделУчета).Представление;
	Форма.Элементы["ТаблицаНастроек_Группа_" + ТекущийОбщийРаздел].Заголовок = НСтр("ru='Счета учета';uk='Рахунки'") + " " + НРег(ПредставлениеТаблицы);
	
	ОтборСтрок = ?(СтруктураОтбора.Количество(), Новый ФиксированнаяСтруктура(СтруктураОтбора), Неопределено);
	
	Форма.Элементы[ИмяТаблицыДанныхПоИмениТекущейТаблицы(Форма.ТекущийРазделУчета)].ОтборСтрок = ОтборСтрок;
	
	МассивЭлементовНастройки = Новый Массив;
	МассивЭлементовНастройки.Добавить("СоздатьНастройкуСчетовУчета");
	МассивЭлементовНастройки.Добавить("Удалить");
	МассивЭлементовНастройки.Добавить("Добавить");
	МассивЭлементовНастройки.Добавить("Расшифровать");
	Если Форма.ТекущийРазделУчета <> "ДенежныеСредства" Тогда
		МассивЭлементовНастройки.Добавить("Организация");
	КонецЕсли;
	МассивЭлементовНастройки.Добавить("МестоУчета");
	МассивЭлементовНастройки.Добавить("ОтборАналитики");
	МассивЭлементовНастройки.Добавить("КоличествоИсключений");
	МассивЭлементовНастройки.Добавить("КоличествоИсключенийПоОрганизациям");
	
	Для каждого ЭлементНастройки Из МассивЭлементовНастройки Цикл
		ТипЭлемента = ?(ЭлементНастройки = "Добавить" Или ЭлементНастройки = "Расшифровать" Или ЭлементНастройки = "ОтборАналитики", "Группа", "Реквизит");
		ИмяЭлемента = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНаФорме(Настройка, Форма.ТекущийРазделУчета, ЭлементНастройки, ТипЭлемента);
		Если Форма.Элементы.Найти(ИмяЭлемента) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементНастройки = "КоличествоИсключений" Тогда
			Форма.Элементы[ИмяЭлемента].Видимость = Форма.ПоказыватьОбщуюНастройку И ЕстьНастройкаИсключений И ЕстьОбщаяНастройка
				И Не ИспользоватьДопИсключения(Форма.ТекущийРазделУчета);
		ИначеЕсли ЭлементНастройки = "КоличествоИсключенийПоОрганизациям" Тогда
			Форма.Элементы[ИмяЭлемента].Видимость = Форма.ПоказыватьОбщуюНастройку И ИспользоватьДопИсключения(Форма.ТекущийРазделУчета);
		ИначеЕсли ЭлементНастройки = "МестоУчета" Тогда
			Форма.Элементы[ИмяЭлемента].Видимость = НастройкаТекущегоРаздела.СвойстваМестаУчета.Используется И Не (Форма.ПоказыватьОбщуюНастройку И ЕстьОбщаяНастройка);
		ИначеЕсли ЭлементНастройки = "ОтборАналитики" Тогда
			Форма.Элементы[ИмяЭлемента].Видимость = НастройкаТекущегоРаздела.СвойстваАналитики.Используется;
		Иначе
			Форма.Элементы[ИмяЭлемента].Видимость = Не (Форма.ПоказыватьОбщуюНастройку И ЕстьОбщаяНастройка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеСпискаВыбораСтраниц(Форма)
	
	Для каждого РазделУчета Из Форма.Элементы.ТекущийРазделУчета.СписокВыбора Цикл
		
		НастройкаРаздела = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(РазделУчета.Значение);
		
		Если НастройкаРаздела = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьОбщаяНастройка = НастройкаРаздела.СвойстваАналитики.ТипЗначения <> Неопределено;
		
		ТребуютНастройки = ?(Форма.ПоказыватьОбщуюНастройку = 0 Или Не ЕстьОбщаяНастройка, НастройкаРаздела.ТребуютНастройки, НастройкаРаздела.ТребуютНастройкиОбщие);
		РазделУчета.Представление = НастройкаРаздела.Представление + ?(ТребуютНастройки, " (" + ТребуютНастройки + ")", "");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПриИзмененииОтбораНастроек()
	
	УстановитьСвойстваЭлементовПоОрганизацииИФункциональнымОпциям();
	ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц(, Истина);
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполнено" Тогда
		УстановитьТекущуюСтраницу(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОрганизацииДляНастройки()
	
	МассивОрганизаций = Новый Массив;
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивОрганизаций.Добавить(Организация);
	Иначе
		МассивОрганизаций = Элементы.Организация.СписокВыбора.ВыгрузитьЗначения();
	КонецЕсли;
	
	Возврат МассивОрганизаций;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЧислоДокументовПрописью(КоличествоДокументов)
	
	КоличествоПрописью = ЧислоПрописью(
		КоличествоДокументов,
		"Л = ru_RU; НП = Истина; НД = Ложь; ДП = Ложь;",
		НСтр("ru='документ,документа,документов,м,,,,,0';uk='документ,документа,документів,м,,,,,0'"));
	Поз = СтрНайти(КоличествоПрописью, "документ");
	Если Поз <> 0 Тогда
		КоличествоПрописью = Сред(КоличествоПрописью, Поз);
	КонецЕсли;
	КоличествоПрописью = Строка(КоличествоДокументов) + " " + НРег(КоличествоПрописью);
	
	Возврат КоличествоПрописью;
	
КонецФункции

&НаСервере
Процедура ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц(ИменаТаблицДляЗаполнения = Неопределено, ОбновлятьТолькоТребуемыеСчета = Ложь)
	
	НаименованиеЗадания = НСтр("ru='Обновление таблиц настроек счетов учета';uk='Оновлення таблиць настройок рахунків обліку'");
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("Организации", Элементы.Организация.СписокВыбора.ВыгрузитьЗначения());
	ПараметрыОбработки.Вставить("СтруктураТаблиц", Новый Структура); // Заполняется в цикле
	ПараметрыОбработки.Вставить("ОрганизацияОтбора", Организация);
	
	МассивТаблицДляЗаполнения = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблицДляЗаполнения);
	
	Для каждого НастройкаРаздела Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
		Если ИменаТаблицДляЗаполнения <> Неопределено И МассивТаблицДляЗаполнения.Найти(НастройкаРаздела.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(НастройкаРаздела.Ключ);
		ИмяТаблицыДляЗаполнения = СтрЗаменить(ИмяТаблицы, "ТаблицаНастроек_", "");
		Если Не ПараметрыОбработки.СтруктураТаблиц.Свойство(ИмяТаблицыДляЗаполнения) Тогда
			Значение = ?(ОбновлятьТолькоТребуемыеСчета, ЭтотОбъект[ИмяТаблицы].Выгрузить(), Неопределено);
			ПараметрыОбработки.СтруктураТаблиц.Вставить(ИмяТаблицыДляЗаполнения, Значение);
		КонецЕсли;
	КонецЦикла;
	
	ВыполняемыйМетод = "Обработки.НастройкаОтраженияДокументовВРеглУчете.ТаблицыНастроекСчетов";
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = ЭтотОбъект.УникальныйИдентификатор;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатВыполненияФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатВыполненияФоновогоЗадания.АдресРезультата);
	ИначеЕсли РезультатВыполненияФоновогоЗадания.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При получении настроек счетов возникли ошибки. Подробнее см. в журнале регистрации.';uk='При отриманні настройок рахунків виникли помилки. Детальніше див. у журналі реєстрації.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатВыполненияФоновогоЗадания(ДействиеПосле = Неопределено)
	
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполняется" Тогда
		ОповещениеФормаДлительнойОперацииЗакрыта = Новый ОписаниеОповещения("ВыполнениеФоновогоЗаданияЗавершено", ЭтотОбъект, ДействиеПосле);
		ПараметрыФормы = ИнициализироватьПараметрыФормыДлительнойОперации(РезультатВыполненияФоновогоЗадания);
		ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеФормаДлительнойОперацииЗакрыта, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АнализСчетовТребующихНастройкиСервер()
	
	ТаблицыДляЗаполнения = Новый Структура;
	Для каждого НастройкаРаздела Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
		ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(НастройкаРаздела.Ключ);
		ИмяТаблицыДляЗаполнения = СтрЗаменить(ИмяТаблицы, "ТаблицаНастроек_", "");
		Если Не ТаблицыДляЗаполнения.Свойство(ИмяТаблицыДляЗаполнения) Тогда
			ТаблицыДляЗаполнения.Вставить(ИмяТаблицыДляЗаполнения, ЭтотОбъект[ИмяТаблицы].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
	НаименованиеЗадания = НСтр("ru='Анализ счетов требующих настройки и обновление таблиц соответствующих счетов';uk='Аналіз рахунків, що потребують настройки і оновлення таблиць відповідних рахунків'");
	ВыполняемыйМетод = "Обработки.НастройкаОтраженияДокументовВРеглУчете.АнализСчетовТребующихНастройкиИОбновлениеТаблиц";
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("Таблицы", ТаблицыДляЗаполнения);
	ПараметрыОбработки.Вставить("МассивОрганизаций", ОрганизацииДляНастройки());
	ПараметрыОбработки.Вставить("ДатаОкончанияПериода", ДатаОкончанияПериода);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = ЭтотОбъект.УникальныйИдентификатор;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатВыполненияФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатВыполненияФоновогоЗадания.АдресРезультата);
	ИначеЕсли РезультатВыполненияФоновогоЗадания.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При анализе требующихся счетов возникли ошибки. Подробнее см. в журнале регистрации.';uk='При аналізі потрібних рахунків виникли помилки. Детальніше див. у журналі реєстрації.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаписиВТаблицуСервер(РежимАнализа, ДатаНачала = Неопределено, ДатаОкончания = Неопределено)
	
	НаименованиеЗадания = НСтр("ru='Обновление таблицы настройки счетов';uk='Оновлення таблиці настройки рахунків'");
	ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета);
	ВыполняемыйМетод = "РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ДополнитьТаблицуНаОснованииАнализаОпераций";
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("РазделыУчета", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрЗаменить(ИмяТаблицы, "ТаблицаНастроек_", "")));
	ПараметрыОбработки.Вставить("РежимАнализа", РежимАнализа);
	ПараметрыОбработки.Вставить("МассивОрганизаций", ОрганизацииДляНастройки());
	ПараметрыОбработки.Вставить("ЗаполняемаяТаблица", ЭтотОбъект[ИмяТаблицы].Выгрузить());
	Если РежимАнализа = "ЗаПериод" Тогда
		ПараметрыОбработки.Вставить("НачалоПериода", ДатаНачала);
		ПараметрыОбработки.Вставить("КонецПериода", ДатаОкончания);
	Иначе
		ПараметрыОбработки.Вставить("ДатаОкончанияПериода", ДатаОкончанияПериода);
	КонецЕсли;
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = ЭтотОбъект.УникальныйИдентификатор;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатВыполненияФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатВыполненияФоновогоЗадания.АдресРезультата);
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	ИначеЕсли РезультатВыполненияФоновогоЗадания.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При обновлении таблицы счетов учета возникли ошибки. Подробнее см. в журнале регистрации.';uk='При оновленні таблиці рахунків обліку виникли помилки. Детальніше див. у журналі реєстрації.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновлениеТаблиц(Знач Результат)
	
	Результат = ПолучитьИзВременногоХранилища(Результат);
	
	Для каждого Таблица Из Результат Цикл
		ЭтотОбъект[Таблица.Ключ].Загрузить(Таблица.Значение);
	КонецЦикла;
	
	ОбновитьИтоговыеДанныеПоКоличествуСтрокТребующихНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоговыеДанныеПоКоличествуСтрокТребующихНастройки()

	ВидыРасчетовСПартнерами = НастройкаСчетовУчетаКлиентСервер.ВидыРасчетовСПартнерами();
	Для каждого НастройкаРаздела Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
		НастройкаРаздела.Значение.ТребуютНастройки = 0;
		НастройкаРаздела.Значение.ТребуютНастройкиОбщие = 0;
		Для каждого Строка Из ЭтотОбъект[ИмяТаблицыДанныхПоИмениТекущейТаблицы(НастройкаРаздела.Ключ)] Цикл
			Если ЗначениеЗаполнено(Организация) И Строка.Организация <> Организация И (Не Строка.ЭтоОбщаяНастройка Или НастройкаРаздела.Ключ = "ДенежныеСредства") Тогда
				Продолжить;
			КонецЕсли;
			Если ВидыРасчетовСПартнерами.Найти(НастройкаРаздела.Ключ) <> Неопределено Тогда
				Если Не Строка[НастройкаРаздела.Ключ] Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ЧислоТребующихНастройкиВСтроке = ЧислоТребующихНастройкиВСтроке(Строка, НастройкаРаздела.Ключ, НастройкаРаздела.Значение);
			Если Строка.ЭтоОбщаяНастройка Тогда
				НастройкаРаздела.Значение.ТребуютНастройкиОбщие = НастройкаРаздела.Значение.ТребуютНастройкиОбщие + ЧислоТребующихНастройкиВСтроке;
			Иначе
				НастройкаРаздела.Значение.ТребуютНастройки = НастройкаРаздела.Значение.ТребуютНастройки + ЧислоТребующихНастройкиВСтроке;
			КонецЕсли;
			Если НастройкаСчетовУчетаКлиентСервер.ЭтоПодраздел(НастройкаРаздела.Ключ) Тогда
				ИмяРеквизитаТребуетсяНастройка = "ТребуетсяНастройка" + НастройкаРаздела.Ключ;
				Строка[ИмяРеквизитаТребуетсяНастройка] = ЧислоТребующихНастройкиВСтроке <> 0;
				Строка.ТребуетсяНастройка = Строка.ТребуетсяНастройка Или Строка[ИмяРеквизитаТребуетсяНастройка];
			Иначе
				Строка.ТребуетсяНастройка = ЧислоТребующихНастройкиВСтроке <> 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ИнициализироватьПараметрыФормыДлительнойОперации(РезультатЗапускаФоновогоЗадания)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторЗадания", РезультатЗапускаФоновогоЗадания.ИдентификаторЗадания);
	ПараметрыФормы.Вставить("ВыводитьОкноОжидания", Истина);
	ПараметрыФормы.Вставить("АдресРезультата", РезультатЗапускаФоновогоЗадания.АдресРезультата);
		
	Возврат ПараметрыФормы;

КонецФункции

&НаКлиенте
Процедура ПроверкаВозможностиРедактирования(Элемент, Отказ)
	
	Если Не РедактированиеЗаполненныхСчетов Тогда
		
		ИмяРеквизита = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.ТекущийЭлемент.Имя, Истина);
		
		Если СтрЗаканчиваетсяНа(ИмяРеквизита, "Организация") Или СтрЗаканчиваетсяНа(ИмяРеквизита, "МестоУчета") Тогда
			ТекстПредупреждения = НСтр("ru='Редактирование указанных настроек не рекомендуется.';uk='Редагування зазначених настройок не рекомендується.'");
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		
		ТекущееЗначение = Элемент.ТекущиеДанные[ИмяРеквизита];
		ИмяРеквизита = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Элемент.ТекущийЭлемент.Имя);
		
		Если ЗначениеЗаполнено(ТекущееЗначение) И Не СтрНайти(Элемент.ТекущиеДанные.ИзмененныеДанные, "," + ИмяРеквизита + ",") Тогда
			ТекстПредупреждения = НСтр("ru='Редактирование указанных счетов не рекомендуется.';uk='Редагування зазначених рахунків не рекомендується.'");
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСчетовУчета(СтруктураПараметров, ОповещениеОВыборе)
	
	СтруктураПараметров.Вставить("РазделыУчета", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекущийРазделУчета));
	ИмяОткрываемойФормы = "РегистрСведений.ПорядокОтраженияНаСчетахУчета.Форма.ФормаЗаписиДинамически";
	ОткрытьФорму(ИмяОткрываемойФормы, СтруктураПараметров, ЭтотОбъект,,,, ОповещениеОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы)
	
	СтрокаВозврата = НастройкаСчетовУчетаКлиентСервер.ОбщийРазделУчетаПоИмениТекущегоРаздела(ИмяТаблицы);
	
	СтрокаВозврата = "ТаблицаНастроек_" + СтрокаВозврата;
	
	Возврат СтрокаВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СкопироватьНастройкуСчетовУчета(Знач НастройкаСчетовУчета, РазделУчета = Неопределено)
	
	ФиксСтр = Новый ФиксированнаяСтруктура(НастройкаСчетовУчета);
	НастройкаСчетовУчетаФормы = Новый Структура(ФиксСтр);
	
	Если РазделУчета <> Неопределено Тогда
		НастройкаСчетовУчетаФормы.РазбитьПоРазделам = Ложь;
		НастройкаСчетовУчетаФормы.РазбитьПоРазделамЭлементы = Ложь;
		СписокРазделовУчета = Новый СписокЗначений;
		ЭлементСписка = НастройкаСчетовУчета.СписокРазделовУчета.НайтиПоЗначению(РазделУчета);
		СписокРазделовУчета.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, ЭлементСписка.Пометка);
		НастройкаСчетовУчетаФормы.СписокРазделовУчета = СписокРазделовУчета;
		НастройкаРаздела = Новый Структура;
		Для каждого ЭлементНастройки Из НастройкаСчетовУчета.НастройкиРазделов.Получить(РазделУчета) Цикл
			Значение = ?(ЭлементНастройки.Ключ = "СчетаУчета" Или ЭлементНастройки.Ключ = "Субконто", Новый СписокЗначений, ЭлементНастройки.Значение);
			НастройкаРаздела.Вставить(ЭлементНастройки.Ключ, Значение);
		КонецЦикла;
		НастройкаСчетовУчетаФормы.НастройкиРазделов = Новый Соответствие;
		НастройкаСчетовУчетаФормы.НастройкиРазделов.Вставить(РазделУчета, НастройкаРаздела);
		НастройкаСчетовУчетаФормы.РеквизитыАналитики = НастройкаРаздела.РеквизитыАналитики;
		НастройкаСчетовУчетаФормы.СчетаСДинамическимиПараметрамиВыбора = Новый Массив;
		НастройкаСчетовУчетаФормы.СчетаСДинамическимиСвойствами = Новый Массив;
	КонецЕсли;
	
	Возврат НастройкаСчетовУчетаФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеОтбора(ОтборВключен, ЗначениеОтбора)
	СтрокаОтбора = ?(ЗначениеЗаполнено(ЗначениеОтбора), ЗначениеОтбора, НСтр("ru='<пустое значение>';uk='<пусте значення>'"));
	Возврат ?(ОтборВключен, СтрокаОтбора, "");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИспользоватьДопИсключения(РазделУчета)
	
	Возврат РазделУчета = "НоменклатураПереданная" Или РазделУчета = "НоменклатураПринятая";
	
КонецФункции

#КонецОбласти

#Область РаботаСоСтроками

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭлементыТребующиеНастройки(Строка, МассивСубконто)
	МассивСчетовТребующихНастройки = СтрРазделить(Строка.ТребуетсяНастройкаСчетов, ",", Ложь);
	Для каждого ЭлементТребующийНастройки Из МассивСчетовТребующихНастройки Цикл
		// Возможно требует настройки значение субконто, учтем это, проверкой есть ли элементы с таким именем в списке субконто:
		Префикс = ?(МассивСубконто.Найти(ЭлементТребующийНастройки) = Неопределено, "СчетУчета_", "Субконто_");
		Если ЗначениеЗаполнено(Строка[Префикс + ЭлементТребующийНастройки]) Тогда
			Строка.ТребуетсяНастройкаСчетов = СтрЗаменить(Строка.ТребуетсяНастройкаСчетов, "," + ЭлементТребующийНастройки + ",", ",,");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЧислоТребующихНастройкиВСтроке(Строка, ИмяРаздела, НастройкаРаздела)
	
	ЭтоПодраздел = НастройкаСчетовУчетаКлиентСервер.ЭтоПодраздел(ИмяРаздела);
	ЧислоТребующихНастройки = 0;
	Если ЭтоПодраздел Тогда
		СчетаИСубконто = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.СчетаУчета);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИСубконто, ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Субконто));
		Для каждого ЭлементТребующийНастройки Из СчетаИСубконто Цикл
			Если СтрНайти(Строка.ТребуетсяНастройкаСчетов, "," + ЭлементТребующийНастройки + ",") <> 0 Тогда
				ЧислоТребующихНастройки = ЧислоТребующихНастройки + 1;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЧислоТребующихНастройки = СтрРазделить(Строка.ТребуетсяНастройкаСчетов, ",", Ложь).Количество();
	КонецЕсли;
	Возврат ЧислоТребующихНастройки;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИзмененныеДанные(ИмяПоля, СтрокаТаблицы);
	
	Если Не СтрНайти(СтрокаТаблицы.ИзмененныеДанные, "," + ИмяПоля + ",") Тогда
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИзмененныеДанные) Тогда
			СтрокаТаблицы.ИзмененныеДанные = ",";
		КонецЕсли;
		СтрокаТаблицы.ИзмененныеДанные = СтрокаТаблицы.ИзмененныеДанные + ИмяПоля + ",";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСчетовУчетаСтроки(СтрокаТаблицы)
	
	РазделыДляОбновленияТребующихсяНастроек = Новый Массив;
	РазделыДляОбновленияТребующихсяНастроек.Добавить(ТекущийРазделУчета);
	Если ТекущийРазделУчета = "РасчетыСКлиентами" Или ТекущийРазделУчета = "РасчетыСПоставщиками" Тогда
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомиссионерами");
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомитентами");
		Если ТекущийРазделУчета = "РасчетыСКлиентами" Тогда
			РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомитентамиПоЗакупке");
		КонецЕсли;
	КонецЕсли;
	Если ТекущийРазделУчета = "РасчетыСКомиссионерами" Или ТекущийРазделУчета = "РасчетыСКомитентами" Тогда
		РазделыДляОбновленияТребующихсяНастроек.Добавить(?(ТекущийРазделУчета = "РасчетыСКомиссионерами", "РасчетыСКомитентами", "РасчетыСКомиссионерами"));
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКлиентами");
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСПоставщиками");
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомитентамиПоЗакупке");
	КонецЕсли;
	Если ТекущийРазделУчета = "РасчетыСКомитентамиПоЗакупке" Тогда
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомиссионерами");
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКомитентами");
		РазделыДляОбновленияТребующихсяНастроек.Добавить("РасчетыСКлиентами");
	КонецЕсли;
	
	СубконтоСодержащиесяВСтроке = Новый Массив;
	
	Для каждого Раздел Из РазделыДляОбновленияТребующихсяНастроек Цикл
		
		НастройкаРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(Раздел);
		Если (РазделыДляОбновленияТребующихсяНастроек.Количество() > 1 И Не СтрокаТаблицы[Раздел]) Или НастройкаРаздела = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЧислоТребующихНастройкиВСтроке = ЧислоТребующихНастройкиВСтроке(СтрокаТаблицы, Раздел, НастройкаРаздела);
		Если СтрокаТаблицы.ЭтоОбщаяНастройка Тогда
			НастройкаРаздела.ТребуютНастройкиОбщие = НастройкаРаздела.ТребуютНастройкиОбщие - ЧислоТребующихНастройкиВСтроке;
		ИначеЕсли Не ИспользоватьДопИсключения(Раздел) Или Не ЗначениеЗаполнено(СтрокаТаблицы.МестоУчета) Тогда
			НастройкаРаздела.ТребуютНастройки = НастройкаРаздела.ТребуютНастройки - ЧислоТребующихНастройкиВСтроке;
		КонецЕсли;
		СтрокаТаблицы.ТребуетсяНастройка = Ложь;
		МассивСубконтоРаздела = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Субконто);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СубконтоСодержащиесяВСтроке, МассивСубконтоРаздела, Истина);
		
	КонецЦикла;
	
	Если ТекущийРазделУчета = "НоменклатураПринятая" Тогда
		НастройкаРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить("НоменклатураСобственная");
		МассивСубконтоРаздела = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Субконто);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СубконтоСодержащиесяВСтроке, МассивСубконтоРаздела, Истина);
	КонецЕсли;
	
	Если СтрокаТаблицы.ЭтоОбщаяНастройка Тогда
		ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(СтрокаТаблицы, СубконтоСодержащиесяВСтроке);
	КонецЕсли;
	ОбновитьЭлементыТребующиеНастройки(СтрокаТаблицы, СубконтоСодержащиесяВСтроке);
	
	Для каждого Раздел Из РазделыДляОбновленияТребующихсяНастроек Цикл
		
		НастройкаРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(Раздел);
		Если (РазделыДляОбновленияТребующихсяНастроек.Количество() > 1 И Не СтрокаТаблицы[Раздел]) Или НастройкаРаздела = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЧислоТребующихНастройкиВСтроке = ЧислоТребующихНастройкиВСтроке(СтрокаТаблицы, Раздел, НастройкаРаздела);
		Если СтрокаТаблицы.ЭтоОбщаяНастройка Тогда
			НастройкаРаздела.ТребуютНастройкиОбщие = НастройкаРаздела.ТребуютНастройкиОбщие + ЧислоТребующихНастройкиВСтроке;
		ИначеЕсли Не ИспользоватьДопИсключения(Раздел) Или Не ЗначениеЗаполнено(СтрокаТаблицы.МестоУчета) Тогда
			НастройкаРаздела.ТребуютНастройки = НастройкаРаздела.ТребуютНастройки + ЧислоТребующихНастройкиВСтроке;
		КонецЕсли;
		Если НастройкаСчетовУчетаКлиентСервер.ЭтоПодраздел(Раздел) Тогда
			ИмяРеквизитаТребуетсяНастройка = "ТребуетсяНастройка" + Раздел;
			СтрокаТаблицы[ИмяРеквизитаТребуетсяНастройка] = ЧислоТребующихНастройкиВСтроке <> 0;
			СтрокаТаблицы.ТребуетсяНастройка = СтрокаТаблицы.ТребуетсяНастройка Или СтрокаТаблицы[ИмяРеквизитаТребуетсяНастройка];
		Иначе
			СтрокаТаблицы.ТребуетсяНастройка = ЧислоТребующихНастройкиВСтроке <> 0;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(СтрокаОбщихНастроек, МассивСубконто)
	
	ТаблицаИсключений = ЭтотОбъект[ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета)];
	
	ПараметрыОтбора = Новый Структура("АналитикаУчета, ЭтоОбщаяНастройка", СтрокаОбщихНастроек.АналитикаУчета, Ложь);
	СтрокиДляИзменения = ТаблицаИсключений.НайтиСтроки(ПараметрыОтбора);
	
	Для каждого ИзменяемаяСтрока Из СтрокиДляИзменения Цикл
		МассивЭлементовПоУмолчанию = СтрРазделить(ИзменяемаяСтрока.СчетаУчетаПоУмолчанию, ",", Ложь);
		Для каждого ИмяЭлемента Из МассивЭлементовПоУмолчанию Цикл
			Префикс = ?(МассивСубконто.Найти(ИмяЭлемента) = Неопределено, "СчетУчета_", "Субконто_");
			ИзменяемаяСтрока[Префикс + ИмяЭлемента] = СтрокаОбщихНастроек[Префикс + ИмяЭлемента];
		КонецЦикла;
		ОбработатьИзменениеСчетовУчетаСтроки(ИзменяемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура УказатьСчетаУчетаЗавершение(Результат, ВыделенныеСтроки) Экспорт
	
	СтруктураСчетов = Результат;
	
	Если СтруктураСчетов <> Неопределено Тогда
		
		ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущийРазделУчета);
		
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].НайтиПоИдентификатору(ИдентификаторСтроки);
			
			Для каждого ВыбранныйСчет Из СтруктураСчетов Цикл
				
				ИмяЭлементаНастройки = СтрЗаменить(СтрЗаменить(ВыбранныйСчет.Ключ, "СчетУчета_", ""), "Субконто_", "");
				
				ТребуетсяНастройкаСчета = СтрНайти(СтрокаТаблицы.ТребуетсяНастройкаСчетов, "," + ИмяЭлементаНастройки + ",") <> 0;
				
				Если Не ЗначениеЗаполнено(СтрокаТаблицы[ВыбранныйСчет.Ключ]) И ЗначениеЗаполнено(ВыбранныйСчет.Значение) И ТребуетсяНастройкаСчета Тогда
						
					СтрокаТаблицы[ВыбранныйСчет.Ключ] = ВыбранныйСчет.Значение;
					Если Не СтрокаТаблицы.ЭтоОбщаяНастройка Тогда
						СтрокаТаблицы.СчетаУчетаПоУмолчанию = СтрЗаменить(СтрокаТаблицы.СчетаУчетаПоУмолчанию, ИмяЭлементаНастройки + ",", "");
					КонецЕсли;
					ОбновитьИзмененныеДанные(ИмяЭлементаНастройки, СтрокаТаблицы);
						
				КонецЕсли;
				
			КонецЦикла;
			
			ОбработатьИзменениеСчетовУчетаСтроки(СтрокаТаблицы);
			
		КонецЦикла;
		
		ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
		
		Модифицированность = Истина;
			
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаДобавленияЗавершение(Результат, ИмяТаблицы = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДобавитьЗаписиВТаблицуСервер("ЗаПериод", Результат.НачалоПериода, Результат.КонецПериода);
		ОбработатьРезультатВыполненияФоновогоЗадания("ИзменитьМодифицированность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	РедактированиеЗаполненныхСчетов = Результат = Истина;
	Элементы.ФормаРазрешитьРедактированиеРеквизитовОбъекта.Пометка = Результат = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеФоновогоЗаданияЗавершено(РезультатЗакрытияФормыДлительнойОперации, ДействиеПосле) Экспорт
	Если РезультатЗакрытияФормыДлительнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если РезультатЗакрытияФормыДлительнойОперации.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатЗакрытияФормыДлительнойОперации.АдресРезультата);
		Если ДействиеПосле = "ИзменитьМодифицированность" Тогда
			УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
		ИначеЕсли ДействиеПосле = "ОбновитьПредставлениеСпискаВыбора" Тогда
			ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
		ИначеЕсли ДействиеПосле = "УстановкаСтраницы" Тогда
			УстановитьТекущуюСтраницу(ЭтотОбъект);
		КонецЕсли;
	ИначеЕсли РезультатЗакрытияФормыДлительнойОперации.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При обновлении таблицы счетов учета возникли ошибки. Подробнее см. в журнале регистрации.';uk='При оновленні таблиці рахунків обліку виникли помилки. Детальніше див. у журналі реєстрації.'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеНовойЗаписиИсключения(Результат, ИмяТаблицы) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы);
	
	#Область ПроверкиВозможностиДобавления
	
	НастройкаТекущегоРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ИмяТаблицы);
	МассивСчетов = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаТекущегоРаздела.СчетаУчета);
	МассивСубконто = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаТекущегоРаздела.Субконто);
	СтруктураОтбора = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, Результат);
	НастройкиМестаУчета = НастройкаТекущегоРаздела.СвойстваМестаУчета;
	Если Не НастройкиМестаУчета.Используется Тогда
		// Для данной таблицы не используется настройка по местам учета, удалим соответствующее измерение:
		СтруктураОтбора.Удалить("МестоУчета");
	КонецЕсли;
	
	СтрокиССовпадающимиИзмерениями = ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиССовпадающимиИзмерениями.Количество() > 0 Тогда
		Элементы[ИмяТаблицыДанных].ТекущаяСтрока = СтрокиССовпадающимиИзмерениями.Получить(0).ПолучитьИдентификатор();
		ТекстПредупреждения = НСтр("ru='Строка с соответствующими измерениями уже есть в таблице исключений.';uk='Рядок з відповідними вимірами вже є в таблиці винятків.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПолучениеЗначенийПоУмолчанию
	
	СтруктураОтбора = Новый Структура("АналитикаУчета, ЭтоОбщаяНастройка", Результат.АналитикаУчета, Истина);
	СтрокиСДаннойАналитикой = ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
	
	ЗначенияПоУмолчанию = ?(СтрокиСДаннойАналитикой.Количество() > 0, СтрокиСДаннойАналитикой.Получить(0), Неопределено);
	Если ЗначенияПоУмолчанию <> Неопределено Тогда
		ЗначенияПоУмолчанию.КоличествоИсключений = ЗначенияПоУмолчанию.КоличествоИсключений + 1;
		Если Не ЗначениеЗаполнено(Результат.МестоУчета) Тогда
			ЗначенияПоУмолчанию.КоличествоИсключенийПоОрганизациям = ЗначенияПоУмолчанию.КоличествоИсключенийПоОрганизациям + 1;
		КонецЕсли;
	КонецЕсли;
	
	#КонецОбласти
	
	НоваяСтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].Добавить();
	НоваяСтрокаТаблицы.Организация = Результат.Организация;
	НоваяСтрокаТаблицы.АналитикаУчета = Результат.АналитикаУчета;
	НоваяСтрокаТаблицы.МестоУчета = Результат.МестоУчета;
	МассивИзмененныхСчетов = Новый Массив;
	МассивСчетовПоУмолчанию = Новый Массив;
	Для каждого СчетУчета Из МассивСчетов Цикл
		НоваяСтрокаТаблицы["СчетУчета_" + СчетУчета] = Результат["СчетУчета_" + СчетУчета];
		Если ЗначениеЗаполнено(Результат["СчетУчета_" + СчетУчета]) Тогда
			МассивИзмененныхСчетов.Добавить(СчетУчета);
		ИначеЕсли ЗначенияПоУмолчанию <> Неопределено И ЗначениеЗаполнено(ЗначенияПоУмолчанию["СчетУчета_" + СчетУчета]) Тогда
			НоваяСтрокаТаблицы["СчетУчета_" + СчетУчета] = ЗначенияПоУмолчанию["СчетУчета_" + СчетУчета];
			МассивСчетовПоУмолчанию.Добавить(СчетУчета);
		КонецЕсли;
	КонецЦикла;
	Для каждого Субконто Из МассивСубконто Цикл
		НоваяСтрокаТаблицы["Субконто_" + Субконто] = Результат["Субконто_" + Субконто];
		Если ЗначениеЗаполнено(Результат["Субконто_" + Субконто]) Тогда
			МассивИзмененныхСчетов.Добавить(Субконто);
		ИначеЕсли ЗначенияПоУмолчанию <> Неопределено И ЗначениеЗаполнено(ЗначенияПоУмолчанию["Субконто_" + Субконто]) Тогда
			НоваяСтрокаТаблицы["Субконто_" + Субконто] = ЗначенияПоУмолчанию["Субконто_" + Субконто];
			МассивСчетовПоУмолчанию.Добавить(Субконто);
		КонецЕсли;
	КонецЦикла;
	Для каждого РеквизитАналитики Из НастройкаТекущегоРаздела.РеквизитыАналитики Цикл
		Если НастройкаСчетовУчетаКлиентСервер.ЭтоНеРеквизитАналитики(РеквизитАналитики.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрокаТаблицы[РеквизитАналитики.Ключ] = Результат[РеквизитАналитики.Ключ];
	КонецЦикла;
	НоваяСтрокаТаблицы.ИзмененныеДанные = "," + СтрСоединить(МассивИзмененныхСчетов, ",") + ",";
	НоваяСтрокаТаблицы.СчетаУчетаПоУмолчанию = "," + СтрСоединить(МассивСчетовПоУмолчанию, ",") + ",";
	
	Элементы[ИмяТаблицыДанных].ТекущаяСтрока = НоваяСтрокаТаблицы.ПолучитьИдентификатор();
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	КоличествоДокументов = ДополнительныеПараметры.КоличествоДокументов;
	
	
	КодОтвета = РезультатВопроса;
	Если КодОтвета = КодВозвратаДиалога.Да Тогда
		ПрименитьНастройкуОтраженияВУчете();
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	КонецЕсли;
	
	ПрименитьНастройкуФрагмент(КоличествоДокументов);

КонецПроцедуры

#КонецОбласти

#Область СохранениеДанных

&НаСервере
Процедура ЗаполнитьТаблицуКПрименениюНастроки()
	
	ТаблицаДокументов.Очистить();
	
	СтруктураТаблиц = Новый Структура;
	СтруктураТаблиц.Вставить("НоменклатураСобственная", ЭтотОбъект["ТаблицаНастроек_Номенклатура"]);
	СтруктураТаблиц.Вставить("НоменклатураПереданная", ЭтотОбъект["ТаблицаНастроек_Номенклатура"]);
	СтруктураТаблиц.Вставить("РасчетыСПартнерами", ЭтотОбъект["ТаблицаНастроек_РасчетыСПартнерами"]);
	СтруктураТаблиц.Вставить("Доходы", ЭтотОбъект["ТаблицаНастроек_Доходы"]);
	СтруктураТаблиц.Вставить("Расходы", ЭтотОбъект["ТаблицаНастроек_Расходы"]);
	СтруктураТаблиц.Вставить("Производство", ЭтотОбъект["ТаблицаНастроек_Производство"]);
	
	РегистрыСведений.ПорядокОтраженияНаСчетахУчета.НайтиДокументыСоответствующиеНастройкам(СтруктураТаблиц,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПорядокОтраженияСчетовУчета(ИмяТаблицы)
	
	Для каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы)] Цикл
		
		Измерения = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
		ЗаполняемыеПоля = ?(СтрокаТаблицы.ЭтоОбщаяНастройка, "АналитикаУчета", Неопределено);
		ЗаполнитьЗначенияСвойств(Измерения, СтрокаТаблицы, ЗаполняемыеПоля);
		СохраняемыеЗначения = Новый Структура;
		СписокСчетов = ?(ИмяТаблицы = "РасчетыСПартнерами" Или ИмяТаблицы = "Номенклатура",
			РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СписокСчетовУчетаПоРазделуУчета(ИмяТаблицы),
			КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ИмяТаблицы).СчетаУчета);
		СписокСубконто = ?(ИмяТаблицы = "РасчетыСПартнерами" Или ИмяТаблицы = "Номенклатура",
			РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СписокСубконтоПоРазделуУчета(ИмяТаблицы),
			КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ИмяТаблицы).Субконто);
		Для каждого СчетУчета Из СписокСчетов Цикл
			Если Не СчетУчета.Пометка Или (Не СтрокаТаблицы.ЭтоОбщаяНастройка И СтрНайти(СтрокаТаблицы.СчетаУчетаПоУмолчанию, "," + СчетУчета.Значение + ",") <> 0) Тогда
				Продолжить;
			КонецЕсли;
			СохраняемыеЗначения.Вставить("СчетУчета_" + СчетУчета.Значение);
		КонецЦикла;
		Для каждого Субконто Из СписокСубконто Цикл
			Если Не Субконто.Пометка Тогда
				Продолжить;
			КонецЕсли;
			СохраняемыеЗначения.Вставить("Субконто_" + Субконто.Значение);
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(СохраняемыеЗначения, СтрокаТаблицы);
		
		РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СохранитьЗначенияСчетовУчета(Измерения, СохраняемыеЗначения, Ложь);
		
		СтрокаТаблицы.ИзмененныеДанные = "";
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтраженияВУчете()
	
	Если КэшДанныхМеханизмов.НастройкаСчетовУчета.ОбщиеРеквизиты.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
		
		Для каждого ОрганизацияДляОчисткиЗаписей Из ОрганизацииДляНастройки() Цикл
			НаборЗаписей.Отбор.Организация.Установить(ОрганизацияДляОчисткиЗаписей);
			НаборЗаписей.Записать();
		КонецЦикла;
		НаборЗаписей.Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
		НаборЗаписей.Записать();
		
		НаборЗаписей.Отбор.Сбросить();
		
		ИменаТаблиц = Новый Массив;
		Для каждого Таблица Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
			
			ИмяТаблицы = НастройкаСчетовУчетаКлиентСервер.ОбщийРазделУчетаПоИмениТекущегоРаздела(Таблица.Ключ);
			Если ИменаТаблиц.Найти(ИмяТаблицы) = Неопределено Тогда
				ИменаТаблиц.Добавить(ИмяТаблицы);
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ИмяТаблицы Из ИменаТаблиц Цикл
			СохранитьПорядокОтраженияСчетовУчета(ИмяТаблицы);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(НСтр("ru='Не удалось сохранить настройки отражения в регл. учете по причине: %1';uk='Неможливо зберегти настройки відображення в регл. обліку по причині: %1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Настройка отражения в регл. учете';uk='Настройка відображення в регл. обліку'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			
		ВызватьИсключение НСтр("ru='Не удалось сохранить настройки отражения. Подробнее см. в Журнале регистрации';uk='Неможливо зберегти настройки відображення. Детальніше див. у Журналі реєстрації'");
	КонецПопытки;
	
	Модифицированность = Ложь;
	ИзмененыНастройкиОтраженияВУчете = Истина;
	
	ЗапуститьФоновоеЗаданиеПоЗаполнениюТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкуОтраженияВУчете()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеглУчетСервер.ВернутьДокументыКОтражению(ТаблицаДокументов.Выгрузить());
	СохранитьНастройкиОтраженияВУчете();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти