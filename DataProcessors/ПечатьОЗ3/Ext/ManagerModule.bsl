#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Печать

// Сформировать печатные формы объектов
//
// Параметры:
// 		МассивОбъектов - Массив - Массив ссылок на объекты которые нужно распечатать
// 		ПараметрыПечати - Структура - Структура дополнительных параметров печати
// 		КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
// 		ОбъектыПечати - Объекты печати
// 		ПараметрыВывода - Структура - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл	
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОЗ3") Тогда
		    ИмяМакета = "";
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ОЗ3",
				НСтр("ru='Форма ОЗ-3';uk='Форма ОЗ-3'"),
				СформироватьПечатнуюФормуОЗ3(
					МассивОбъектов,
					ОбъектыПечати,
					ПараметрыПечати,
					ИмяМакета),, ИмяМакета);
		
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция СформироватьПечатнуюФормуОЗ3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОЗ3";
	
	НомерТипаДокумента = 0;
	
	СведенияАктуальны = Истина;
		
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
				
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ); 
				
		Для Каждого Элемент Из СтруктураОбъектов.Значение Цикл
			
			НомерТипаДокумента = НомерТипаДокумента + 1;
			Если НомерТипаДокумента > 1 Тогда                          	
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыОЗ3(ПараметрыПечати, Элемент.Ссылка);
 	        НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ЗаполнитьМакетОЗ3(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, СведенияАктуальны, Элемент.Ссылка);
			
			// В табличном документе зададим имя области, в которую был 
			// выведен объект. Нужно для возможности печати покомплектно.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Элемент.Ссылка); 			
			
		КонецЦикла;
		

		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьМакетОЗ3(ТабличныйДокумент, ВыборкаПоДокументам, ОбъектыПечати, СведенияАктуальны, Ссылка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьОЗ3.ПФ_MXL_UK_ОЗ3");

	ВыборкаПоОС = ВыборкаПоДокументам.ВыборкаПоОС.Выбрать();
	ВыборкаПоШапке = ВыборкаПоДокументам.ВыборкаПоШапке.Выбрать();		
	
	ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СписаниеОС") Тогда		
		СписокПодразделений = ВыборкаПоДокументам.ВыборкаПоОС.Выгрузить().ВыгрузитьКолонку("СдалоПодразделение");		
	КонецЕсли;	
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СписаниеИзЭксплуатации") Тогда
		Если ВыборкаПоДокументам.ВыборкаПоОС.Пустой() Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			Возврат;
		КонецЕсли;
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);		
	КонецЕсли;
	
	Пока ВыборкаПоШапке.Следующий() Цикл
			
		ОбластьМакета = Макет.ПолучитьОбласть("ОЗ3");
		Параметры     = ОбластьМакета.Параметры;
		Параметры.Заполнить(ВыборкаПоШапке);
		
		Пока ВыборкаПоОС.Следующий() Цикл
								
			Параметры.Заполнить(ВыборкаПоОС);
			Параметры.Заполнить(ВыборкаПоКомиссии);
			
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СписаниеИзЭксплуатации") Тогда		
				Параметры.НаименованиеОС = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
											ВыборкаПоОС.НоменклатураНаименование,
											ВыборкаПоОС.Характеристика);		
			КонецЕсли;		
		
			СведенияОбОрганизации	= ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Ссылка.Организация, Ссылка.Дата);	
			Параметры.Организация	= ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,",,);
			Параметры.ЕДРПОУ		= СведенияОбОрганизации.КодПоЕДРПОУ;
			
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СписаниеОС") Тогда		
				Параметры.ВидОперации = "Списання";		
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли