#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


// Выполняет проверку настройки правил отражения в международном учете
//
// Параметры:
// 	 Организация - СправочникСсылка.Организации - Ораганизация, для которой необходимо выполнить проверку правил отражения
// 	 Документ - Массив, ДокументСсылка - Документы для которых необходимо выполнить проверку правил отражения, Неопределено - по всем.
//
// Возвращаемое значение:
//	Результат - Структура - Структура содержащая:
//								ХозяйственныеОперацииБезПравилОтражения - Таблица операция, по которым не настроены правила отражения
//								СчетаБезПравилОтражения - Таблица счетов регл. учета, по которым не настроены правила отражения.
//
Функция ПроверитьНастройкуПравилОтраженияУчете(Организация, Документ = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтражениеДокументов.Регистратор КАК Регистратор,
	|	ОтражениеДокументов.Организация КАК Организация,
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК ПериодУчетнойПолитики
	|ПОМЕСТИТЬ ОтражениеДокументов
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|		ПО ОтражениеДокументов.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ОтражениеДокументов.Период >= УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	(ОтражениеДокументов.Регистратор = &Документ
	|			ИЛИ &Документ = НЕОПРЕДЕЛЕНО)
	|	И ОтражениеДокументов.Организация = &Организация
	|	И ОтражениеДокументов.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете), 
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеДокументов.Регистратор,
	|	ОтражениеДокументов.Организация,
	|	ОтражениеДокументов.ДатаОтражения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодУчетнойПолитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	ОтражениеДокументов КАК ДокументыКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|		ПО ДокументыКОтражению.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ДокументыКОтражению.ПериодУчетнойПолитики = УчетнаяПолитикаОрганизаций.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументыКОтражению.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтражениеДокументов";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Документ",    Документ);
	Запрос.Выполнить();
	
	ОперацииБезПравилОтражения = МеждународныйУчетПоДаннымОперативногоУчета.ПроверитьПравилаОтражения(МенеджерВременныхТаблиц, Организация);
	СчетаБезПравилОтражения    = МеждународныйУчетПоДаннымРеглУчета.ПроверитьПравилаОтражения(МенеджерВременныхТаблиц, Организация);
	
	Результат = Новый Структура;
	Результат.Вставить("ХозяйственныеОперацииБезПравилОтражения", ОперацииБезПравилОтражения);
	Результат.Вставить("СчетаБезПравилОтражения", СчетаБезПравилОтражения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли