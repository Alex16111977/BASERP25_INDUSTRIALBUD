#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Параметры.АдресРезультатаПроверки);
	
	ХозяйственныеОперацииБезПравилОтражения.Загрузить(РезультатПроверки.ХозяйственныеОперацииБезПравилОтражения);
	СчетаБезПравилОтражения.Загрузить(РезультатПроверки.СчетаБезПравилОтражения);
	
	Организация = Параметры.Организация;
	Документ = Параметры.Документ;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ХозяйственныеОперацииБезПравилОтраженияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	
	ТекущиеДанные = ХозяйственныеОперацииБезПравилОтражения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	МеждународныйУчетПоДаннымОперативногоУчетаКлиент.ОткрытьНастройкуШаблоновПроводок(
		ТекущиеДанные.УчетнаяПолитика, ТекущиеДанные.ХозяйственнаяОперация, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаБезПравилОтраженияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	НастроитьСоответствиеСчетов(ВыбраннаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьШаблонПроводки(Команда)
	
	ТекущиеДанные = Элементы.ХозяйственныеОперацииБезПравилОтражения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МеждународныйУчетПоДаннымОперативногоУчетаКлиент.ОткрытьНастройкуШаблоновПроводок(
		ТекущиеДанные.УчетнаяПолитика, ТекущиеДанные.ХозяйственнаяОперация, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствиеСчетов(Команда)
	
	ТекущиеДанные = Элементы.СчетаБезПравилОтражения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МеждународныйУчетПоДаннымРеглУчетаКлиент.ОткрытьНастройкуСоответствияСчетовИОборотов(
		ТекущиеДанные.УчетнаяПолитика,
		ТекущиеДанные.Счет,
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПроверитьНастройкуПравилСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДокументВМеждународномУчете(Команда)
	
	ПараметрЗакрытия = Новый Структура;
	ПараметрЗакрытия.Вставить("ОтразитьДокументВМеждународномУчете", Истина);
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.СтраницаХозяйственныеОперацииБезПравилОтражения.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Шаблоны проводок (%1)';uk='Шаблони проводок (%1)'"), 
			ХозяйственныеОперацииБезПравилОтражения.Количество());
	
	Элементы.СтраницаСчетаБезПравилОтражения.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Соответствующие счета международного учета (%1)';uk='Відповідні рахунки міжнародного обліку (%1)'"), 
			СчетаБезПравилОтражения.Количество());
	
	Элементы.СтраницаХозяйственныеОперацииБезПравилОтражения.Видимость = ХозяйственныеОперацииБезПравилОтражения.Количество() > 0;
	
	Элементы.СтраницаСчетаБезПравилОтражения.Видимость = СчетаБезПравилОтражения.Количество() > 0;;
	
	Элементы.СтраницаНастроеныВсеПравилаОтражения.Видимость = 
		(ХозяйственныеОперацииБезПравилОтражения.Количество() = 0)
		И (СчетаБезПравилОтражения.Количество() = 0);
	
	ВидимыеСтраницы = Новый Массив;
	Для каждого Страница Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		Если Страница.Видимость Тогда
			ВидимыеСтраницы.Добавить(Страница);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Страницы.ОтображениеСтраниц = 
		?(ВидимыеСтраницы.Количество() = 1, ОтображениеСтраницФормы.Нет, ОтображениеСтраницФормы.ЗакладкиСверху);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНастройкуПравилСервер()
	
	РезультатПроверки = Обработки.ОтражениеДокументовВМеждународномУчете.ПроверитьНастройкуПравилОтраженияУчете(Организация, Документ);
	
	ХозяйственныеОперацииБезПравилОтражения.Загрузить(РезультатПроверки.ХозяйственныеОперацииБезПравилОтражения);
	СчетаБезПравилОтражения.Загрузить(РезультатПроверки.СчетаБезПравилОтражения);
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти