
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
	ВызватьИсключение НСтр("ru='Работа в web-клиенте не поддерживается.';uk='Робота в web-клієнті не підтримується.'");
	#КонецЕсли
	
	ЗапускИзКонфигурации = ТипЗнч(ОбъектОбработки()) = Тип("ОбработкаОбъект.ОписаниеОбработчиковОбновления");
	Если ЗапускИзКонфигурации И НЕ ОбщегоНазначения.РежимОтладки() Тогда
		ТекстСообщения = НСтр("ru='Запуск обработки возможен только при использовании параметра запуска ""РежимОтладки"".';uk='Запуск обробки можливий тільки при використанні параметра запуску ""РежимОтладки"".'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ИнициализироватьКонстантыОбработки();
	
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	РежимОтладкиОписанияОбработчиков = СтрНайти(ПараметрЗапускаПриложения, "РежимОтладкиОписанияОбработчиков") > 0;
	
	Элементы.ГруппаОтладка.Видимость = РежимОтладкиОписанияОбработчиков;
	Элементы.ОбработчикиОбновленияПоказатьТекстМодулей.Видимость = РежимОтладкиОписанияОбработчиков;
	Элементы.КаталогФайловОшибок.Видимость = РежимОтладкиОписанияОбработчиков;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрНайти(ПараметрЗапуска,"=") > 0 Тогда
		
		СтруктураПараметров = ПолучитьПараметрыИзСтроки(ПараметрЗапуска);
		
		Если Не ЗначениеЗаполнено(КаталогФайловОшибок) Тогда
			КаталогФайловОшибок = КаталогВременныхФайлов() + "";
		КонецЕсли; 
		
		Если СтруктураПараметров.Свойство("ErrorLogFolder") Тогда
			КаталогФайловОшибок = СтруктураПараметров.ErrorLogFolder;
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ErrorAddInfo") Тогда
			ДополнениеТекстаОшибки = СтруктураПараметров.ErrorAddInfo;
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ResultFile") Тогда
			ФайлРезультата = СтруктураПараметров.ResultFile;
		КонецЕсли;
		
		#Если НЕ ВебКлиент Тогда
		Если СтруктураПараметров.Свойство("DetectionTime") И ЗначениеЗаполнено(СтруктураПараметров.DetectionTime) Тогда
			ДатаОбнаруженияОшибки = XMLЗначение(Тип("Дата"), СтруктураПараметров.DetectionTime);
		КонецЕсли;
		#КонецЕсли
		
		Если СтруктураПараметров.Свойство("RepoPath") Тогда
			АдресРепозитория = СтруктураПараметров.RepoPath;
		КонецЕсли;
		
		ЗапуститьПроверку = Найти(ПараметрЗапуска,"Выполнить") <> 0;
		ЗавершатьРаботу   = ЗапуститьПроверку;
		
		Попытка
			
			Если ЗапуститьПроверку Тогда
				ВыполнитьТестПостроенияОчереди(Неопределено);
			КонецЕсли;
			
		Исключение
		КонецПопытки; 
		
		Попытка
			
			Если ЗапуститьПроверку И ЗначениеЗаполнено(ФайлРезультата) Тогда
				Результат = Новый ТекстовыйДокумент;
				Результат.УстановитьТекст(Объект.Ошибки.Количество());
				Результат.Записать(ФайлРезультата);
			КонецЕсли;
			
		Исключение
		КонецПопытки;
		
		Если ЗавершатьРаботу Тогда
			ПрекратитьРаботуСистемы();
		КонецЕсли;
		
	КонецЕсли;
	
	ЗагрузитьОписание = Найти(ПараметрЗапуска,"LoadDescription") <> 0;
	Если ЗагрузитьОписание Тогда
		ЗагрузитьОбработчики(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ ЗавершениеРаботы Тогда
		Отказ = Истина;
		ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеЗакрытияФормы", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОтвета, НСтр("ru='Данные были изменены. Сохранить изменения в репозиторий?';uk='Дані були змінені. Зберегти зміни в репозиторій?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЗакрытияФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ЗаписатьДанныНаДиск();
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		// ничего не делаем
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Объект.КаталогSRC = Настройки["Объект.КаталогSRC"];
	ОформитьГруппуНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменилосьОписаниеОбработчиткаОбновления" Тогда
		ОбновитьДанныеОКонфликтахОбработчика(Параметр);
		Оповестить("ОбновленыДанныеОКонфликтахОбработчикаОбновления", Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КаталогSRCНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		СтандартнаяОбработка = Ложь;
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ДиалогВыбораФайлаЗавершение", ЭтотОбъект, "КаталогSRC"));
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура КаталогSRCОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.КаталогSRC = ДобавитьРазделительПутиВКонец(Текст);
	ОформитьГруппуНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогФайловОшибокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		СтандартнаяОбработка = Ложь;
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбораФайла.ПолноеИмяФайла = КаталогФайловОшибок;
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ДиалогВыбораФайлаЗавершение", ЭтотОбъект, "КаталогФайловОшибок"));
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура КаталогФайловОшибокОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КаталогФайловОшибок = ДобавитьРазделительПутиВКонец(Текст);
	ОформитьГруппуНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсистемаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Отбор = Новый Структура("Подсистема", ВыбранноеЗначение);
		УстановитьОтборОбработчиков(Отбор);
	Иначе
		ОтключитьОтборОбработчиков("Подсистема");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменяемыйОбъектПриИзменении(Элемент)
	
	ПриИзмененииОбъектаПоискаНаСервере("ИзменяемыеОбъекты", ИзменяемыйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменяемыйОбъектОчистка(Элемент, СтандартнаяОбработка)
	
	ПриИзмененииОбъектаПоискаНаСервере("ИзменяемыеОбъекты", ИзменяемыйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧитаемыйОбъектПриИзменении(Элемент)
	
	ПриИзмененииОбъектаПоискаНаСервере("ЧитаемыеОбъекты", ЧитаемыйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧитаемыйОбъектОчистка(Элемент, СтандартнаяОбработка)
	
	ПриИзмененииОбъектаПоискаНаСервере("ЧитаемыеОбъекты", ЧитаемыйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеОтборыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "Прочие" Тогда
		ВыборБыстрогоОтбора = Новый ОписаниеОповещения("ОбработкаВыбораБыстрогоОтбора", ЭтотОбъект);
		ПрочиеБыстрыеОтборы.ПоказатьВыборЭлемента(ВыборБыстрогоОтбора);
		Возврат;
	Иначе
		УстановитьБыстрыйОтбор(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораБыстрогоОтбора(Результат, ДополнительныйПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьБыстрыйОтбор(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ОбработчикиОбновленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьРедакторОписанияОбработчика(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактированияОбработчика(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ОбновитьДанныеОбработчика(Результат);
	ИначеЕсли Элементы.ОбработчикиОбновления.ТекущиеДанные <> Неопределено 
			И Элементы.ОбработчикиОбновления.ТекущиеДанные.НоваяСтрока Тогда
		ТекущаяСтрока = Объект.ОбработчикиОбновления.НайтиПоИдентификатору(Элементы.ОбработчикиОбновления.ТекущаяСтрока);
		Объект.ОбработчикиОбновления.Удалить(ТекущаяСтрока);
		Элементы.ОбработчикиОбновления.ТекущаяСтрока = ИсходнаяТекущаяСтрока;
	КонецЕсли;
	ИсходнаяТекущаяСтрока = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиОбновленияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеУдаленияОписанияОбработчика", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Удалить описание обработчика?';uk='Вилучити опис обробника?'");
	ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеУдаленияОписанияОбработчика(Результат, ДополнительныеСвойства) Экспорт
	
	Элемент = Элементы.ОбработчикиОбновления;
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Истина;
		ИсходнаяТекущаяСтрока = Элемент.ТекущаяСтрока;
		Если Элемент.ВыделенныеСтроки.Количество() > 0 Тогда
			УдаляемыеСтроки = Новый Массив;
			Для Каждого ИдентификаторСтроки Из Элемент.ВыделенныеСтроки Цикл
				УдаляемыеСтроки.Добавить(ИдентификаторСтроки);
			КонецЦикла;
			Для Каждого ИдентификаторСтроки Из УдаляемыеСтроки Цикл
				Элемент.ТекущаяСтрока = ИдентификаторСтроки;
				ОчиститьСвязанныеДанныеОбработчика(Элемент.ТекущиеДанные.Ссылка);
				ИсходнаяТекущаяСтрока = ИдентификаторСтроки;
			КонецЦикла;
		Иначе
			ОчиститьСвязанныеДанныеОбработчика(Элемент.ТекущиеДанные.Ссылка);
		КонецЕсли;
		Элемент.ТекущаяСтрока = ИсходнаяТекущаяСтрока+1;
		ИсходнаяТекущаяСтрока = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиОбновленияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИсходнаяТекущаяСтрока = Элемент.ТекущаяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиОбновленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ИсходнаяСсылка = Элемент.ТекущиеДанные.Ссылка;
		Элемент.ТекущиеДанные.НоваяСтрока = Истина;
		Элемент.ТекущиеДанные.Изменен = Истина;
		Элемент.ТекущиеДанные.Ссылка = Новый УникальныйИдентификатор;
		Если Копирование Тогда
			Элемент.ТекущиеДанные.Идентификатор = "";
		Иначе
			Элемент.ТекущиеДанные.РежимВыполнения = "Отложенно";
			Элемент.ТекущиеДанные.РежимВыполненияОтложенныхОбработчиков = "Параллельно";
			Если ЗначениеЗаполнено(Элемент.ОтборСтрок) И Элемент.ОтборСтрок.Свойство("Подсистема") Тогда
				Элемент.ТекущиеДанные.Подсистема = Элемент.ОтборСтрок.Подсистема;
			КонецЕсли;
		КонецЕсли;
		ОткрытьРедакторОписанияОбработчика(Элемент.ТекущиеДанные.Ссылка, НоваяСтрока, ИсходнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиОбновленияТехПроектПриИзменении(Элемент)
	
	ЗаполнитьБыстрыеОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьОбработчики(Команда)
	
	ОбработчикиТП = ЗагрузитьСписокОбработчиковТехПроекта();
	ЗагрузитьИзТекущейКонфигурацииНаСеревере(ОбработчикиТП);
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьОчередь(Команда)
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru='Начало построение очереди = ';uk='Початок побудови черги ='") + ТекущаяДата()
	);
	Результат = ПостроитьОчередьНаСервере();
	Если Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Очередь построена успешно!';uk='Черга збудована успішно!'")
		);
	КонецЕсли;
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru='Окончание построение очереди = ';uk='Закінчення побудови черги ='") + ТекущаяДата()
	);
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВРепозиторий(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.КаталогSRC) Тогда
		Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Показать();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Не указан путь к каталогу SRC.';uk='Не вказаний шлях до каталогу SRC.'"),
			,
			,
			"Объект.КаталогSRC");
		Возврат;
	КонецЕсли;
	
	ЗаписатьДанныНаДиск();
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНомерСборкиОбработчикам(Команда)
	
	ДополнительныеПараметры = Новый Структура("НомерСборкиДляОбработчиков", Истина);
	ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеВводаНомераСборки", ЭтотОбъект, ДополнительныеПараметры);
	НомераВерсии = СтрРазделить(ВерсияКонфигурации,".");
	ПоказатьВводЧисла(ОбработчикОтвета, Число(НомераВерсии[3]), НСтр("ru='Укажите новый номер сборки';uk='Вкажіть новий номер збірки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНомерСборкиВМодулиИКорень(Команда)
	
	ДополнительныеПараметры = Новый Структура("НомерСборкиДляОбработчиков", Ложь);
	ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеВводаНомераСборки", ЭтотОбъект, ДополнительныеПараметры);
	НомераВерсии = СтрРазделить(ВерсияКонфигурации,".");
	ПоказатьВводЧисла(ОбработчикОтвета, Число(НомераВерсии[3]), НСтр("ru='Укажите новый номер сборки';uk='Вкажіть новий номер збірки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСписокОбработчиковТехПроекта(Команда)
	
	СохранитьСписокОбработчиковТехПроекта();
	
КонецПроцедуры

#Область ПодменюОтладки

&НаКлиенте
Процедура СохранитьВРепозиторийВсеОбработчики(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.КаталогSRC) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Не указан путь к каталогу SRC.';uk='Не вказаний шлях до каталогу SRC.'"),
			,
			,
			"Объект.КаталогSRC");
			Возврат;
	КонецЕсли;
	
	ПараметрыСохранения = Новый Структура("ВсеОбработчики", Истина);
	ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеСохраненияВРепозиторий", ЭтотОбъект, ПараметрыСохранения);
	ТекстВопроса = НСтр("ru='Внимание! Будут перезаписны все процедуры описания обработчиков обновления.
|Продолжить?'
|;uk='Увага! Будуть перезаписані всі процедури опису обробників оновлення. 
|Продовжити?'");
	ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонфликты(Команда)
	
	ПроверитьКонфликтыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриДобавленииОбработчиковОбновления_Вызовы(Команда)
	
	ТекстыМодулей = ПолучитьТекстыМодулей();
	Для Каждого КодМодуля из ТекстыМодулей Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(КодМодуля.ТекстПроцедуры);
		ТекстовыйДокумент.Показать(КодМодуля.ИмяМодуля);
	КонецЦикла;
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриДобавленииОбработчиковОбновления_Описания(Команда)
	
	ОписаниеОбработчиковВОбщемМодуле = Истина;
	ТекстыМодулей = ПолучитьТекстыМодулей(ОписаниеОбработчиковВОбщемМодуле);
	Для Каждого КодМодуля из ТекстыМодулей Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(КодМодуля.ТекстПроцедуры);
		ТекстовыйДокумент.Показать(КодМодуля.ИмяМодуля);
	КонецЦикла;
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройкиERP(Команда)
	
	ТекстыНастроекERP = ПолучитьТекстыНастроекERP();
	Для Каждого ТекстФайла из ТекстыНастроекERP Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ТекстФайла.Значение);
		ТекстовыйДокумент.Показать(ТекстФайла.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТабличныеЧасти(Команда)
	
	ДанныеТаблиц = ВыгрузитьТаблицыВMXL();
	Для Каждого Таблица Из ДанныеТаблиц Цикл
		Таблица.Значение.Показать(Таблица.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПриоритетыКонфликтыОбработчика(Команда)
	
	Если Элементы.ОбработчикиОбновления.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не выбран обработчки обновления';uk='Не встановлено обрабники оновлення'"));
		Возврат;
	КонецЕсли;
	
	ДанныеТаблиц = ВыгрузитьТаблицыВMXL(Элементы.ОбработчикиОбновления.ТекущиеДанные.Ссылка);
	Для Каждого Таблица Из ДанныеТаблиц Цикл
		Таблица.Значение.Показать(Таблица.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЦиклВПорядкеОбработчиков(Команда)
	
	ЕстьЦиклВыполненияОбработчиковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьТестПостроенияОчереди(Команда)
	
	#Область ПроверкаКаталогаФайловОшибок
	Если Команда <> Неопределено Тогда
		Элементы.КаталогФайловОшибок.Видимость = Истина;
		ОформитьГруппуНастроек();
		Если НЕ ЗначениеЗаполнено(КаталогФайловОшибок) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru='Не указан каталог файлов ошибок.';uk='Не вказаний каталог файлів помилок.'"),
				,
				,
				"КаталогФайловОшибок");
				Возврат;
		КонецЕсли;
	КонецЕсли;
	#КонецОбласти
	
	Объект.Ошибки.Очистить();
	ПостроитьОчередьНаСервере();
	Для Каждого Ошибка Из Объект.Ошибки Цикл
		ЗаписатьОшибку(Ошибка.ОбъектМетаданных, Ошибка.Сообщение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОчередь(Команда)
	РассчитатьОчередьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстыВсехМодулей(Команда)
	
	ТекстыМодулей = ПолучитьТекстыМодулей(,Ложь);
	Для Каждого КодМодуля из ТекстыМодулей Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(КодМодуля.ТекстПроцедуры);
		ТекстовыйДокумент.Показать(КодМодуля.ИмяМодуля);
	КонецЦикла;
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстМодулей(Команда)
	
	ТолькоИзмененные = Ложь;
	ТолькоВыделенные = Истина;
	ТекстыМодулей = ПолучитьТекстыМодулей(,ТолькоИзмененные,ТолькоВыделенные);
	Для Каждого КодМодуля из ТекстыМодулей Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(КодМодуля.ТекстПроцедуры);
		ТекстовыйДокумент.Показать(КодМодуля.ИмяМодуля);
	КонецЦикла;
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБлокироватьИнтерфейс(Команда)
	
	ПараметрыСохранения = Новый Структура("ВсеОбработчики", Истина);
	ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеУстановкиБлокировкиИнтерфейса", ЭтотОбъект, ПараметрыСохранения);
	ТекстВопроса = НСтр("ru='Внимание! Всем изменяемым объектам обработчиков ERP будет установлен флаг ""Блокировать интерфейс"".
|Продолжить?'
|;uk='Увага! Всім змінюваним об''єктам оброників ERP буде встановлено прапор ""Блокувати інтерфейс"". 
|Продовжити?'");
	ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.Скрыть();
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область СтатусНеобходимАнализ
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбработчикиОбновленияСтатусПроблемы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элементы.ОбработчикиОбновленияСтатусПроблемы.ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = СтатусНеобходимАнализ;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	#КонецОбласти
	
	#Область ТехническийПроект
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого ЭлементФормы Из Элементы.ОбработчикиОбновления.ПодчиненныеЭлементы Цикл
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	КонецЦикла;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элементы.ОбработчикиОбновленияТехПроект.ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СеребристоСерый);
	#КонецОбласти
	
КонецПроцедуры

#Область ЗагрузкаОбработчиков

&НаСервере
Процедура ЗагрузитьИзТекущейКонфигурацииНаСеревере(ОбработчикиТП = Неопределено)
	
	Обработка = ОбъектОбработки();
	Обработка.ЗагрузитьОбработчики();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
	Если ОбработчикиТП <> Неопределено Тогда
		Для Каждого Обработчик Из ОбработчикиТП Цикл
			Если СтрНайти(Обработчик,":") = 0 Тогда
				Продолжить;
			КонецЕсли;
			Данные = СтрРазделить(Обработчик, ":");
			Отбор = Новый Структура;
			Если ЗначениеЗаполнено(Данные[0]) Тогда
				Отбор.Вставить("Идентификатор", Данные[0]);
			КонецЕсли;
			Если ЗначениеЗаполнено(Данные[1]) Тогда
				Отбор.Вставить("Процедура", Данные[1]);
			КонецЕсли;
			НайденныеОбработчики = Объект.ОбработчикиОбновления.НайтиСтроки(Отбор);
			Для Каждого Найденный Из НайденныеОбработчики Цикл
				Найденный.ТехническийПроект = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьДанныеФормы(Обработка);
	
	ТекущаяБиблиотека = "";
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузитьСписокОбработчиковТехПроекта()
	
	ИмяФайла = КаталогНастроек() + "tech-project.updaters";
	
	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФайлНастроек = Новый ТекстовыйДокумент;
	#Если НЕ ВебКлиент Тогда
	ФайлНастроек.Прочитать(ИмяФайла, КодировкаТекста.UTF8);
	#КонецЕсли
	ТекстФайла = ФайлНастроек.ПолучитьТекст();
	ОбработчикиТП = СтрРазделить(ТекстФайла, Символы.ПС);
	
	Возврат ОбработчикиТП;
	
КонецФункции

#КонецОбласти

#Область СохранениеВРепозиторий

&НаКлиенте
Процедура ЗавершениеСохраненияВРепозиторий(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьДанныНаДиск(ДополнительныеПараметры.ВсеОбработчики);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныНаДиск(ВсеОбработчики = Ложь)
	
	ОчиститьСообщения();
	ТекстыМодулей = ПолучитьТекстыМодулей(,НЕ ВсеОбработчики);
	СохранитьВерсиюВКореньКонфигурации();
	Для Каждого ТекстМодуля из ТекстыМодулей Цикл
		ЗаменитьПроцедуруМодуля(ТекстМодуля);
	КонецЦикла;
	СохранитьСписокОбработчиковТехПроекта();
	НовыйНомерСборкиКонфигурации = 0;
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьПроцедуруМодуля(Код)
	
	ДобавленаПроцедура = Ложь;
	ПолноеИмяФайла = Объект.КаталогSRC + Код.ПутьМодуля;
	СтрокиМодуля = ТекстФайлаВМассив(ПолноеИмяФайла);
	ЭтоМодульМенеджера = СтрНайти(Код.ИмяМодуля, ".") > 0;
	
	КомментарийПроцедуры = 
	"// Добавляет в список процедуры-обработчики обновления данных ИБ
	|// для всех поддерживаемых версий библиотеки или конфигурации.
	|// Вызывается перед началом обновления данных ИБ для построения плана обновления.
	|//
	|//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
	|//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
	|//
	|// Пример добавления процедуры-обработчика в список:
	|//  Обработчик = Обработчики.Добавить();
	|//  Обработчик.Версия              = ""1.1.0.0"";
	|//  Обработчик.Процедура           = ""ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0"";
	|//  Обработчик.МонопольныйРежим    = Ложь;
	|//
	|// Параметры:
	|// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
	|//
	|%1";
	ТекстПроцедуры = СтрШаблон(КомментарийПроцедуры, Код.ТекстПроцедуры);
	
	// Если Модуль пустой заполнить по шаблону пустого модуля - только модули менеджера
	Если ЭтоМодульМенеджера И СтрокиМодуля.Количество() = 0 Тогда
		ПустойМодульМенеджера = "#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		|
		|#Область СлужебныеПроцедурыИФункции
		|
		|#Область ОбновлениеИнформационнойБазы
		|
		|%1
		|
		|КонецПроцедуры
		|
		|#КонецОбласти
		|
		|#КонецОбласти
		|
		|#КонецЕсли";
		НовыйТекст = СтрШаблон(ПустойМодульМенеджера, ТекстПроцедуры);
	КонецЕсли;
	
	// Если процедура уже есть - уходим
	Если СтрокиМодуля.Найти(Код.ЗаголовокПроцедуры) <> Неопределено Тогда
		Возврат ДобавленаПроцедура;
	КонецЕсли;
	
	// Если есть #Область ОбновлениеНовыхВерсийИБ или #Область ОбновлениеИнформационнойБазы,
	// то вставить сразу после начала области
	Индекс = СтрокиМодуля.Найти("#Область ОбновлениеИнформационнойБазы");
	Если Индекс = Неопределено Тогда
		Индекс = СтрокиМодуля.Найти("#Область ОбновлениеНовыхВерсийИБ");
	КонецЕсли;
	Если Индекс <> Неопределено Тогда
		Индекс = Индекс + 1;
		СтрокиМодуля.Вставить(Индекс, "");
		СтрокиПроцедуры = СтрРазделить(ТекстПроцедуры, Символы.ПС);
		Для Каждого НоваяСтрока Из СтрокиПроцедуры Цикл
			Индекс = Индекс + 1;
			СтрокиМодуля.Вставить(Индекс, НоваяСтрока);
		КонецЦикла;
		ДобавленаПроцедура = Истина;
	КонецЕсли;
	
	// Если ничего не нашлось, то вставить перед процедурой обработки
	Если НЕ ДобавленаПроцедура И НЕ ПустаяСтрока(Код.ПроцедураОбработки) Тогда
		Имена = СтрРазделить(Код.ПроцедураОбработки, ".");
		ИмяПроцедурыОбновления = Имена[Имена.ВГраница()];
		ИндексВставки = СтрокиМодуля.Найти("Процедура "+ИмяПроцедурыОбновления+"(Параметры) Экспорт");
		Если ИндексВставки <> Неопределено Тогда
			СтрокиПроцедуры = СтрРазделить(ТекстПроцедуры, Символы.ПС);
			СтрокиМодуля.Вставить(ИндексВставки, "");
			Индекс = СтрокиПроцедуры.ВГраница();
			Для СдвигКНачалу = 0 По СтрокиПроцедуры.ВГраница() Цикл
				СтрокаПроцедуры = СтрокиПроцедуры[Индекс - СдвигКНачалу];
				СтрокиМодуля.Вставить(ИндексВставки, СтрокаПроцедуры);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ИмяПроцедуры = ИмяПроцедуры(Код.ЗаголовокПроцедуры);
	Если ДобавленаПроцедура Тогда
		НовыйТекст = СтрСоединить(СтрокиМодуля, Символы.ПС);
		ЗаписатьТекстМодуля(НовыйТекст, ПолноеИмяФайла);
		
		Шаблон = НСтр("ru='В модуле ""%1"" добавлена процедура ""%2""';uk='У модулі ""%1"" додана процедура ""%2""'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(Шаблон, Код.ИмяМодуля, ИмяПроцедуры));
	Иначе
		Шаблон = НСтр("ru='В модуле ""%1"" не удалось добавить процедуру ""%2""
|Возможно в модуле не задана область ""ОбновлениеИнформационнойБазы"".'
|;uk='У модулі ""%1"" не вдалося додати процедуру ""%2""
|Можливо в модулі не задана область ""ОбновлениеИнформационнойБазы"".'");
		ТекстСобщения = СтрШаблон(Шаблон, Код.ИмяМодуля, ИмяПроцедуры);
		ТекстСобщения = ТекстСобщения + Символы.ПС + 
			НСтр("ru='Добавьте вручную процедуру ""ОписаниеОбработчиковОбновления"" или область ""ОбновлениеИнформационнойБазы"" 
|и повторите запись обработчиков.'
|;uk='Додайте вручну процедуру ""ОписаниеОбработчиковОбновления"" або область ""ОбновлениеИнформационнойБазы"" 
|і повторіть запис обробників.'");
		ВызватьИсключение ТекстСобщения;
	КонецЕсли;
	
	Возврат ДобавленаПроцедура;
	
КонецФункции

&НаКлиенте
Процедура ЗаменитьПроцедуруМодуля(Код)
	
	Если ДобавитьПроцедуруМодуля(Код) Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = Объект.КаталогSRC + Код.ПутьМодуля;
	НовыйМодуль = Новый ТекстовыйДокумент;
	
	НовыйТекстПроцедуры = СтрРазделить(Код.ТекстПроцедуры, Символы.ПС);
	ЗаголовокПроцедуры = НовыйТекстПроцедуры[0];
	ОкончаниеПроцедуры = НовыйТекстПроцедуры[НовыйТекстПроцедуры.ВГраница()];
	
	УстановитьВерсию = ЗначениеЗаполнено(Код.Версия);
	
	ВыполненаЗамена = Ложь;
	УстановленаВерсия = Ложь;
	ЭтоТекстЗаменяемойПроцедуры = Ложь;
	ЭтоПроцедураВерсии = Ложь;
	#Если НЕ ВебКлиент Тогда
	Модуль = Новый ЧтениеТекста;
	Модуль.Открыть(ПолноеИмяФайла, "UTF-8");
	СтрокаМодуля = "";
	Пока СтрокаМодуля <> Неопределено Цикл

		СтрокаМодуля = Модуль.ПрочитатьСтроку();
		Если СтрокаМодуля = Неопределено Тогда
			Прервать;
		КонецЕсли;
	
		#Область ПриДобавленииПодсистемы
		Если УстановитьВерсию И СокрЛП(СтрокаМодуля) = Код.ПроцедураВерсии Тогда
			ЭтоПроцедураВерсии = Истина;
		КонецЕсли;
		
		Если ЭтоПроцедураВерсии И СтрНайти(СтрокаМодуля, "Описание.Версия =") > 0
			И СтрНайти(СтрокаМодуля,  Код.Версия) = 0 Тогда
				УстановленаВерсия = Истина;
				СтрокаМодуля = СтрШаблон("	Описание.Версия = ""%1"";", Код.Версия);
		КонецЕсли;
		
		Если ЭтоПроцедураВерсии И СокрЛП(СтрокаМодуля) = ОкончаниеПроцедуры Тогда
			ЭтоПроцедураВерсии = Ложь;
		КонецЕсли;
		#КонецОбласти
	
		#Область ПриДобавленииОбработчиковОбновления
		Если СокрЛП(СтрокаМодуля) = ЗаголовокПроцедуры Тогда
			ЭтоТекстЗаменяемойПроцедуры = Истина;
			ВыполненаЗамена = Истина;
			Для Каждого НоваяСтрока Из НовыйТекстПроцедуры Цикл
				НовыйМодуль.ДобавитьСтроку(НоваяСтрока);
			КонецЦикла;
		КонецЕсли;
		
		Если ЭтоТекстЗаменяемойПроцедуры И СокрЛП(СтрокаМодуля) = ОкончаниеПроцедуры Тогда
			ЭтоТекстЗаменяемойПроцедуры = Ложь;
			Продолжить;
		КонецЕсли;
		#КонецОбласти
		
		Если НЕ ЭтоТекстЗаменяемойПроцедуры Тогда
			НовыйМодуль.ДобавитьСтроку(СтрокаМодуля);
		КонецЕсли;
		
	КонецЦикла;
	#КонецЕсли
	
	ИмяПроцедуры = ИмяПроцедуры(ЗаголовокПроцедуры);
	Если УстановленаВерсия Тогда
		Шаблон = НСтр("ru='В модуле ""%1"" установлена новая версия ""%2""';uk='У модулі ""%1"" встановлена нова версія ""%2""'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(Шаблон, Код.ИмяМодуля, Код.Версия));
	КонецЕсли;
	Если ВыполненаЗамена Тогда
		Шаблон = НСтр("ru='В модуле ""%1"" заменена процедура ""%2""';uk='У модулі ""%1"" замінена процедура ""%2""'");
		НовыйТекст = НовыйМодуль.ПолучитьТекст();
		ЗаписатьТекстМодуля(НовыйТекст, ПолноеИмяФайла);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(Шаблон, Код.ИмяМодуля, ИмяПроцедуры));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТекстФайлаВМассив(ПолноеИмяФайла)
	
	Результат = Новый Массив;
	
	#Если НЕ ВебКлиент Тогда
	Модуль = Новый ЧтениеТекста;
	Модуль.Открыть(ПолноеИмяФайла, "UTF-8");
	СтрокаМодуля = "";
	Пока СтрокаМодуля <> Неопределено Цикл
		СтрокаМодуля = Модуль.ПрочитатьСтроку();
		Если СтрокаМодуля = Неопределено Тогда
			Прервать;
		КонецЕсли;
		Результат.Добавить(СтрокаМодуля);
	КонецЦикла;
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ИмяПроцедуры(ЗаголовокПроцедуры)
	
	ИмяПроцедуры = СтрЗаменить(ЗаголовокПроцедуры, "Процедура", "");
	ИмяПроцедуры = СокрЛП(СтрЗаменить(ИмяПроцедуры, "Экспорт", ""));
	Скобка1 = СтрНайти(ИмяПроцедуры, "(");
	Возврат Лев(ИмяПроцедуры, Скобка1-1);
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьТекстМодуля(ТекстФайла, ПолноеИмяФайла)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ДополнительныеПараметры.Вставить("ТекстФайла", ТекстФайла);
	ОбработчикУдаления = Новый ОписаниеОповещения("УдалениеФайлаМодуляЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	НачатьУдалениеФайлов(ОбработчикУдаления, ПолноеИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалениеФайлаМодуляЗавершение(ДополнительныеПараметры) Экспорт
	
	ПолноеИмяФайла = ДополнительныеПараметры.ПолноеИмяФайла;
	ТекстФайла = ДополнительныеПараметры.ТекстФайла;
	
	#Если НЕ ВебКлиент Тогда
	ЗаписатьBOM = Ложь;
	Поток = Новый ФайловыйПоток(ПолноеИмяФайла, РежимОткрытияФайла.СоздатьНовый);
	РазделительСтрок = Символы.ВК + Символы.ПС;
	ЗаписьТекста = Новый ЗаписьТекста(Поток, КодировкаТекста.UTF8, РазделительСтрок,,ЗаписатьBOM);
	
	ЗаписьТекста.Записать(ТекстФайла);
	ЗаписьТекста.Закрыть();

	Поток.Закрыть();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСписокОбработчиковТехПроекта()
	
	ИмяФайла = КаталогНастроек() + "tech-project.updaters";
	
	ОбработчикиТП = ОбработчикиТехническогоПроекта();
	ТекстФайла = СтрСоединить(ОбработчикиТП, Символы.ПС);
	ЗаписатьФайлНаДиск(ИмяФайла, ТекстФайла);
	
КонецПроцедуры

&НаСервере
Функция ОбработчикиТехническогоПроекта()
	
	Отбор = Новый Структура("ТехническийПроект", Истина);
	Обработчики = Объект.ОбработчикиОбновления.Выгрузить(Отбор);
	ОбработчикиТП = Новый Массив;
	Для Каждого Обработчик Из Обработчики Цикл
		Описание = СтрШаблон("%1:%2", Обработчик.Идентификатор, Обработчик.Процедура);
		ОбработчикиТП.Добавить(Описание);
	КонецЦикла;
	
	Возврат ОбработчикиТП;
	
КонецФункции

&НаКлиенте
Процедура СохранитьВерсиюВКореньКонфигурации()
	
	ПолноеИмяФайла = Объект.КаталогSRC + "Configuration\Configuration.mdo";
	
	#Если НЕ ВебКлиент Тогда
	УстановленаВерсия = Ложь;
	НовыйФайл = Новый ТекстовыйДокумент;
	ТекстФайла = Новый ЧтениеТекста;
	ТекстФайла.Открыть(ПолноеИмяФайла, "UTF-8");
	СтрокаМодуля = "";
	Пока СтрокаМодуля <> Неопределено Цикл

		СтрокаМодуля = ТекстФайла.ПрочитатьСтроку();
		Если СтрокаМодуля = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если СтрНайти(СтрокаМодуля, "<version>") > 0 
			И СтрНайти(СтрокаМодуля,  НоваяВерсияКонфигурации) = 0 Тогда
				УстановленаВерсия = Истина;
				СтрокаМодуля = СтрШаблон("  <version>%1</version>", НоваяВерсияКонфигурации);
		КонецЕсли;
		НовыйФайл.ДобавитьСтроку(СтрокаМодуля);
		
	КонецЦикла;
	
	ТекстФайла = НовыйФайл.ПолучитьТекст();
	Если УстановленаВерсия Тогда
		Шаблон = НСтр("ru='Установлена новая версия ""%1"" в корне конфигурации';uk='Встановлено нову версія ""%1"" в корені конфігурації'");
		НовыйТекст = НовыйФайл.ПолучитьТекст();
		ЗаписатьТекстМодуля(НовыйТекст, ПолноеИмяФайла);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(Шаблон, НоваяВерсияКонфигурации));
	КонецЕсли;
	ЗаписатьФайлНаДиск(ПолноеИмяФайла, ТекстФайла);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьФайлНаДиск(ПолноеИмяФайла, ТекстФайла)
	
	#Если НЕ ВебКлиент Тогда
	ЗаписатьBOM = Ложь;
	УдалитьФайлы(ПолноеИмяФайла);
	Поток = Новый ФайловыйПоток(ПолноеИмяФайла, РежимОткрытияФайла.СоздатьНовый);
	РазделительСтрок = Символы.ВК + Символы.ПС;
	ЗаписьТекста = Новый ЗаписьТекста(Поток, КодировкаТекста.UTF8, РазделительСтрок, , ЗаписатьBOM);
	
	ЗаписьТекста.Записать(ТекстФайла);
	ЗаписьТекста.Закрыть();
	
	Поток.Закрыть();
	#КонецЕсли
	
	Возврат ПолноеИмяФайла;
	
КонецФункции

#КонецОбласти

#Область РедактированиеОбработчика

&НаКлиенте
Процедура ОткрытьРедакторОписанияОбработчика(СсылкаОбработчика, НоваяСтрока = Ложь, ИсточникКопирования = Неопределено)
	
	АдресДанных = ПоместитьДанныеОбработчикаВХранилище(СсылкаОбработчика,,ИсточникКопирования);
	ПараметрыФормы = Новый Структура("АдресОбработчика, НоваяСтрока", АдресДанных, НоваяСтрока);
	ПараметрыФормы.Вставить("ЗапускИзКонфигурации", ЗапускИзКонфигурации);
	
	ДополнительныеПараметры = Новый Структура("Ссылка", СсылкаОбработчика);
	ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ЗавершениеРедактированияОбработчика", ЭтотОбъект, ДополнительныеПараметры);
	
	ИмяФормыОбработчика = "Обработка.ОписаниеОбработчиковОбновления.Форма.ФормаОбработчика";
	Если НЕ ЗапускИзКонфигурации Тогда
		ИмяФормыОбработчика = "ВнешняяОбработка.ОписаниеОбработчиковОбновления.Форма.ФормаОбработчика";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыОбработчика,
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор,
		,
		,
		ОбработчикЗакрытияФормы,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьДанныеОбработчикаВХранилище(СсылкаОбработчика, Адрес = Неопределено, ИсточникКопирования = Неопределено)
	
	ТабЧасти = Новый Структура;
	Данные = Новый Структура("ТабличныеЧасти", ТабЧасти);
	
	Данные.Вставить("МодулиПодсистем", МодулиПодсистем);
	
	Отбор = Новый Структура("Ссылка", СсылкаОбработчика);
	Данные.ТабличныеЧасти.Вставить("ОбработчикиОбновления", Объект.ОбработчикиОбновления.Выгрузить(Отбор));
	Если ИсточникКопирования = Неопределено Тогда
		ТабЧасти.Вставить("ЧитаемыеОбъекты", Объект.ЧитаемыеОбъекты.Выгрузить(Отбор));
		ТабЧасти.Вставить("ИзменяемыеОбъекты", Объект.ИзменяемыеОбъекты.Выгрузить(Отбор));
		ТабЧасти.Вставить("БлокируемыеОбъекты", Объект.БлокируемыеОбъекты.Выгрузить(Отбор));
		ТабЧасти.Вставить("ПриоритетыВыполнения", Объект.ПриоритетыВыполнения.Выгрузить(Отбор));
	
		Отбор = Новый Структура("ОбработчикПисатель", СсылкаОбработчика);
		ТабЧасти.Вставить("КонфликтыОбработчиков", Объект.КонфликтыОбработчиков.Выгрузить(Отбор));
		
		Отбор = Новый Структура("ОбработчикЧитательИлиПисатель2", СсылкаОбработчика);
		ЕщеКонфликты = Объект.КонфликтыОбработчиков.Выгрузить(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ЕщеКонфликты, ТабЧасти.КонфликтыОбработчиков);
		
	Иначе
		ДобавитьОбъектыИсходногоОбработчика(Данные, ИсточникКопирования);
	КонецЕсли;
	
	Если Адрес = Неопределено Тогда
		Адрес = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);
	Иначе
		ПоместитьВоВременноеХранилище(Данные, Адрес);
	КонецЕсли;
	
	Возврат Адрес;
	
КонецФункции


// Описание
// 
// Параметры:
// 	Данные - Структура - Описание:
// * МодулиПодсистем - Массив из Строка - 
// * ТабличныеЧасти - КлючИЗначение -:
// 	 **ОбработчикиОбновления - ТаблицаЗначений - :
// 	  *** Ссылка - Строка - 
// 	ИсточникКопирования - Неопределено, Строка - Описание
&НаСервере
Процедура ДобавитьОбъектыИсходногоОбработчика(Данные, ИсточникКопирования)
	
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить("ЧитаемыеОбъекты");
	ТабличныеЧасти.Добавить("ИзменяемыеОбъекты");
	ТабличныеЧасти.Добавить("БлокируемыеОбъекты");
	
	Отбор = Новый Структура("Ссылка", ИсточникКопирования);
	Обработчик = Данные.ТабличныеЧасти.ОбработчикиОбновления[0];
	НоваяСсылка = Обработчик.Ссылка;
	Для Каждого ИмяТабЧасти Из ТабличныеЧасти Цикл
		тз = Объект[ИмяТабЧасти].Выгрузить(Отбор);
		тз.ЗаполнитьЗначения(НоваяСсылка, "Ссылка");
		Данные.ТабличныеЧасти.Вставить(ИмяТабЧасти, тз);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьДанныеОбработчика(АдресОбработчика)
	
	Обработка = ОбъектОбработки();
	Данные = ПолучитьИзВременногоХранилища(АдресОбработчика); // см. ДобавитьОбъектыИсходногоОбработчика.Данные
	
	НовоеОписание = Данные.ТабличныеЧасти.ОбработчикиОбновления[0];
	Модифицированность = НовоеОписание.Изменен;
	
	НовоеОписание.ВерсияЧислом = Обработка.ВерсияЧислом(НовоеОписание.Версия);
	НомераВерсии = СтрРазделить(НовоеОписание.Версия, ".");
	НомераВерсии.Удалить(0);
	НовоеОписание.РедакцияЧислом = Обработка.ВерсияЧислом(НомераВерсии);
	
	ОписаниеОбработчика = Обработка.ОбработчикиОбновления.Найти(НовоеОписание.Ссылка, "Ссылка");
	Если ОписаниеОбработчика <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ОписаниеОбработчика, НовоеОписание);
		Если МодулиПодсистем <> Неопределено И НовоеОписание.Подсистема <> "" Тогда
			ОписаниеОбработчика.ИмяОсновногоСерверногоМодуля = МодулиПодсистем[НовоеОписание.Подсистема];
		КонецЕсли;
		
		ИнверсныйПорядок = ИнверсныйПорядок();
		Отбор = Новый Структура("Ссылка", ОписаниеОбработчика.Ссылка);
		Для Каждого ТабличнаяЧасть Из Данные.ТабличныеЧасти Цикл
			
			Если ТабличнаяЧасть.Ключ <> "ОбработчикиОбновления" Тогда
				НайденныеСтроки = Обработка[ТабличнаяЧасть.Ключ].НайтиСтроки(Отбор);
				Для Каждого СтрокаКУдалению Из НайденныеСтроки Цикл
					Обработка[ТабличнаяЧасть.Ключ].Удалить(СтрокаКУдалению);
				КонецЦикла;
				
				Для Каждого Запись Из ТабличнаяЧасть.Значение Цикл
					НоваяСтрока = Обработка[ТабличнаяЧасть.Ключ].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись,,"НомерСтроки");
					Если ТабличнаяЧасть.Ключ = "ПриоритетыВыполнения" Тогда
						ИнвертироватьПорядок(Обработка, Запись, ИнверсныйПорядок)
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		ОбновитьСведенияОКонфликтахОбработчиков(Обработка);
		ЗначениеВРеквизитФормы(Обработка, "Объект");
		
		Отбор = Новый Структура("Ссылка", НовоеОписание.Ссылка);
		НайденныеСтроки = Объект.ОбработчикиОбновления.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].НоваяСтрока = Ложь;
			Элементы.ОбработчикиОбновления.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
			ИсходнаяТекущаяСтрока = Элементы.ОбработчикиОбновления.ТекущаяСтрока;
		КонецЕсли;
		
		ОбновитьОтборыОбработчиков();
		
	КонецЕсли;
	
	Возврат НовоеОписание.Ссылка;
	
КонецФункции

&НаСервере
Функция ИнверсныйПорядок()
	
	ИнверсныйПорядок = Новый Соответствие;
	ИнверсныйПорядок.Вставить("До","После");
	ИнверсныйПорядок.Вставить("После","До");
	ИнверсныйПорядок.Вставить("Любой","Любой");
	Возврат ИнверсныйПорядок;
	
КонецФункции

&НаСервере
Процедура ИнвертироватьПорядок(Обработка, Запись, ИнверсныйПорядок)
	
	Отбор = Новый Структура("Ссылка, Обработчик2");
	Отбор.Ссылка = Запись.Обработчик2;
	Отбор.Обработчик2 = Запись.Ссылка;
	НайденныеПриоритеты = Обработка["ПриоритетыВыполнения"].НайтиСтроки(Отбор);
	Для Каждого Приоритет Из НайденныеПриоритеты Цикл
		Если НЕ ПустаяСтрока(Запись.Порядок) Тогда
			НовыйПорядок = ИнверсныйПорядок[Запись.Порядок];
			Если НовыйПорядок <> Приоритет.Порядок Тогда
				Приоритет.Порядок = НовыйПорядок;
				Приоритет.ЗаданПорядокВыполнения = Истина;
				Отбор = Новый Структура("Ссылка", Запись.Обработчик2);
				НайденныеОбработчики = Обработка["ОбработчикиОбновления"].НайтиСтроки(Отбор);
				Если НайденныеОбработчики.Количество() > 0 Тогда
					НайденныеОбработчики[0].Изменен = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОКонфликтахОбработчика(АдресОбработчика)
	
	СсылкаОбработчика = ОбновитьДанныеОбработчика(АдресОбработчика);
	ПоместитьДанныеОбработчикаВХранилище(СсылкаОбработчика, АдресОбработчика);
	
КонецПроцедуры

&НаСервере
Процедура ПриоритетыКонфликтовЧитаемыхИлиДвойнойЗаписи(ПриоритетыВыполнения, Конфликты)
	
	Приоритеты = ПриоритетыВыполнения.Скопировать();
	ПриоритетыВыполнения.Очистить();
	
	Для Каждого Приоритет Из Приоритеты Цикл
		ЧитаемыеДанныеИзменяютсяДругими = Конфликты.Найти(Приоритет.Процедура2, "ПроцедураПисатель") <> Неопределено;
		БиблиотекаERP = ИменаБиблиотекERP.Найти(Приоритет.Подсистема2) <> Неопределено;
		Если ЧитаемыеДанныеИзменяютсяДругими ИЛИ НЕ БиблиотекаERP Тогда
			НоваяСтрока = ПриоритетыВыполнения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Приоритет);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПостроениеОчереди

&НаСервере
Функция ПостроитьОчередьНаСервере()
	
	Обработка = ОбъектОбработки();
	ВсеХорошо = Обработка.ПостроитьОчередь();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
	ОбновитьДанныеФормы(Обработка);
	
	Возврат ВсеХорошо;
	
КонецФункции

#КонецОбласти

#Область ОбнаружениеКонфликтов

&НаСервере
Процедура ОбновитьСведенияОКонфликтахОбработчиков(Обработка)
	
	НовоеОписание = Обработка.ОбновитьСведенияОКонфликтахОбработчиков();
	ЗаполнитьБыстрыеОтборы(НовоеОписание);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБыстрыеОтборы(Знач ОписаниеОбработчиков = Неопределено)
	
	Если ОписаниеОбработчиков = Неопределено Тогда
		ОписаниеОбработчиков = Объект.ОбработчикиОбновления.Выгрузить();
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	ПрочиеБыстрыеОтборы.Очистить();
	
	ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, "СтатусПроблемы", СтатусНеобходимАнализ, ЦветаСтиля.ЦветОсобогоТекста);
	ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, "РежимВыполнения", "Отложенно");
	ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, "РежимВыполнения", "Монопольно");
	ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, "РежимВыполнения", "Оперативно");
	ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, "ТехническийПроект", "Тех.проект");
	
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "РежимВыполненияОтложенныхОбработчиков", "Параллельно");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "РежимВыполненияОтложенныхОбработчиков", "Последовательно");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "НачальноеЗаполнение", "Начальное заполнение");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "ВыполнятьВГруппеОбязательных", "Обязательный");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "Многопоточный", "Многопоточный");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "ЗаписьЧитаемых", "Запись читаемых");
	ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, "ПовторнаяЗапись", "Повторная запись");
	
	ТекстСтатуса = НСтр("ru='Прочие';uk='Інші'");
	СтрокаГиперссылки = Новый ФорматированнаяСтрока(
		ТекстСтатуса,
		,// Новый Шрифт(,,Истина), 
		ЦветаСтиля.ЦветТекстаКнопки, ,
		"Прочие");
	МассивСтрок.Добавить(СтрокаГиперссылки);
	МассивСтрок.Добавить("  ");
	
	ТекстСтатуса = СтрШаблон("%1 (%2)", НСтр("ru='Все';uk='Всі'"), ОписаниеОбработчиков.Количество());
	СтрокаГиперссылки = Новый ФорматированнаяСтрока(
		ТекстСтатуса, 
    	,// Новый Шрифт(,,Истина), 
		ЦветаСтиля.ЦветТекстаКнопки, ,
		"Все");
		
	МассивСтрок.Добавить(СтрокаГиперссылки);
	
	Элементы.БыстрыеОтборы.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьБыстрыйОтбор(ОписаниеОбработчиков, МассивСтрок, ИмяПоляОтбора, Знач ЗначениеОтбора, Знач Цвет = Неопределено)
	
	Если Цвет = Неопределено Тогда
		Цвет = ЦветаСтиля.ЦветТекстаКнопки;
	КонецЕсли;
	
	ПредставлениеОтбора = ЗначениеОтбора;
	
	Для Каждого ТипПоля Из ОписаниеОбработчиков.Колонки[ИмяПоляОтбора].ТипЗначения.Типы() Цикл
		Если ТипПоля = Тип("Булево") Тогда
			ЗначениеОтбора = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Отбор = Новый Структура(ИмяПоляОтбора, ЗначениеОтбора);
	СтрокиОтбора = ОписаниеОбработчиков.Скопировать(Отбор);
	Если СтрокиОтбора.Количество() > 0 Тогда
		
		ТекстСтатуса = СтрШаблон("%1 (%2)", ПредставлениеОтбора, СтрокиОтбора.Количество());
		СтрокаГиперссылки = Новый ФорматированнаяСтрока(
			ТекстСтатуса,
			,// Новый Шрифт(,,Истина),
			Цвет, ,
			СтрЗаменить(ТРег(ПредставлениеОтбора)," ",""));
		
		МассивСтрок.Добавить(СтрокаГиперссылки);
		МассивСтрок.Добавить("  ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПрочийБыстрыйОтбор(ОписаниеОбработчиков, ИмяПоляОтбора, Знач ЗначениеОтбора)
	
	ПредставлениеОтбора = ЗначениеОтбора;
	Для Каждого ТипПоля Из ОписаниеОбработчиков.Колонки[ИмяПоляОтбора].ТипЗначения.Типы() Цикл
		Если ТипПоля = Тип("Булево") Тогда
			ЗначениеОтбора = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Отбор = Новый Структура(ИмяПоляОтбора, ЗначениеОтбора);
	СтрокиОтбора = ОписаниеОбработчиков.Скопировать(Отбор);
	Если СтрокиОтбора.Количество() > 0 Тогда
		ТекстСтатуса = СтрШаблон("%1 (%2)", ПредставлениеОтбора, СтрокиОтбора.Количество());
		ПрочиеБыстрыеОтборы.Добавить(ИмяПоляОтбора, ТекстСтатуса);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ГенерацияКода

&НаКлиенте
Функция ПолучитьТекстыМодулей(ОписаниеОбработчиковВОбщемМодуле = Ложь, ТолькоИзмененные = Истина, ТолькоВыделенные = Ложь)
	
	ИмяФайла = КаталогНастроек() + "cut_tags.yml";
	ОписаниеТэгов = ПрочитатьФайлYaml(ИмяФайла);
	
	Если ОписаниеТэгов["configurations"] <> Неопределено Тогда
		ПолучитьОбъектыПодсистем(ОписаниеТэгов["configurations"]);
	КонецЕсли;
	ВсеОбъекты = ПолучитьВсеОбъектыИсходнойКонфигурации();
	Настройки = Новый Структура;
	Если НЕ ОписаниеОбработчиковВОбщемМодуле И ТолькоИзмененные Тогда
		Настройки.Вставить("ТолькоИзмененные", Истина);
	КонецЕсли;
	Если НЕ ОписаниеОбработчиковВОбщемМодуле И ТолькоВыделенные Тогда
		Настройки.Вставить("ТолькоВыделенные", Истина);
	КонецЕсли;
	ОпределитьТэгиДляОбъектовНеВходящихВКонфигурацию(ОписаниеТэгов);
	Настройки.Вставить("ОписаниеТэгов", ОписаниеТэгов);
	Настройки.Вставить("ВсеОбъекты", ВсеОбъекты);
	АдресНастроек = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	
	Если ОписаниеОбработчиковВОбщемМодуле Тогда
		Возврат ОписанияОбработчиковПоМодулямОбновления(АдресНастроек);
	Иначе
		Возврат ОписанияОбработчиковПоМодулямМенеджеров(АдресНастроек);
	КонецЕсли;
	
КонецФункции

#Область ПоМодулямОбновления

&НаСервере
Функция ОписанияОбработчиковПоМодулямОбновления(АдресНастроек)
	
	Настройки = ПолучитьИзВременногоХранилища(АдресНастроек);
	ОписаниеКонфигураций = Настройки.ОписаниеТэгов["configurations"];
	
	ТекстыМодулей = Новый Массив;
	Если НЕ ПостроитьОчередьНаСервере() Тогда
		Возврат ТекстыМодулей;
	КонецЕсли;
	
	ИменаБиблиотек = Новый Массив;
	Тэги = ПолучитьСведенияОТегахВырезки(Настройки);
	РазметкаКода = Новый Структура;
	РазметкаКода.Вставить("Тэги", Тэги);
	ВерсииМодулей = ОписаниеВерсийМодулей();
	Для Каждого ОписаниеКонфигурации Из ОписаниеКонфигураций Цикл
		
		ИмяМодуля = ОписаниеКонфигурации.Значение["module"];
		Если ИмяМодуля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИменаБиблиотек.Добавить(ОписаниеКонфигурации.Значение["name"]);
		
		РазметкаКода.Вставить("ИмяБиблиотеки", ОписаниеКонфигурации.Ключ);
		РазметкаКода.Вставить("ИмяМодуля", ИмяМодуля);
		РазметкаКода.Вставить("ИмяПроцедуры", "ПриДобавленииОбработчиковОбновления");
		РазметкаКода.Вставить("ЭтоМодульМенеджера", Ложь);
		РазметкаКода.Вставить("ДобавитьОбластьОписания", Истина);
		РазметкаКода.Вставить("МодульЛокализации", ОписаниеКонфигурации.Значение["localization"] <> Неопределено);
		Если РазметкаКода.МодульЛокализации Тогда
			Если ОписаниеКонфигурации.Значение["outTags"].Количество() > 0 Тогда
				РазметкаКода.Вставить("ТэгиЛокализации", ОписаниеКонфигурации.Значение["outTags"][0]);
			КонецЕсли;
		КонецЕсли;
		
		Отбор = Новый Структура("ИмяОсновногоСерверногоМодуля", ИмяМодуля);
		ОбработчикиМодуля = Объект.ОбработчикиОбновления.Выгрузить(Отбор);
		ОбработчикиМодуля.Сортировать("Процедура");
		ТекстОбработчиков = ТекстОбработчиков(ОбработчикиМодуля, РазметкаКода);
		
		Если РазметкаКода.МодульЛокализации Тогда
			ТэгБиблиотеки = Тэги.ПоБиблиотекам.Найти(ОписаниеКонфигурации.Ключ);
			Если ТэгБиблиотеки <> Неопределено Тогда
				ТэгЛокализации = Тэги.Описание[ТэгБиблиотеки.Тэг]; // см. ОписаниеТэга
				Если ТэгЛокализации <> Неопределено Тогда
					Шаблон = "%1
					|%2
					|%3";
					ТекстОбработчиков = СтрШаблон(Шаблон, ТэгЛокализации.Начало, ТекстОбработчиков, ТэгЛокализации.Конец);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТекстПроцедуры = 
		"Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
		|
		|"+ ТекстОбработчиков +"
		|
		|КонецПроцедуры";
		
		КодМодуля = ОписаниеКодаМодуля(ИмяМодуля, ТекстПроцедуры);
		КодМодуля.ПутьМодуля = "CommonModules\" + ИмяМодуля + "\Module.bsl";
		ДобавитьВерсиюМодуля(ВерсииМодулей, ОбработчикиМодуля);
		ТекстыМодулей.Добавить(КодМодуля);
		
	КонецЦикла;
	
	Если ВерсииМодулей.Количество() > 0 Тогда
		УстановитьВерсиюМодуля(ТекстыМодулей, ВерсииМодулей);
	КонецЕсли;
	
	ТекстПроцедуры = ТекстПриФормированииОчередейОтложенныхОбработчиков(ИменаБиблиотек, РазметкаКода);
	КодМодуля = ОписаниеКодаМодуля("ОбновлениеИнформационнойБазыПереопределяемый", ТекстПроцедуры);
	ТекстыМодулей.Добавить(КодМодуля);
	
	Возврат ТекстыМодулей;
	
КонецФункции

#КонецОбласти

#Область ПоМодулямОбъектовОбновления

&НаСервере
Функция ОписанияОбработчиковПоМодулямМенеджеров(АдресНастроек)
	
	Настройки = ПолучитьИзВременногоХранилища(АдресНастроек);
	ТолькоИзмененные = Настройки.Свойство("ТолькоИзмененные");
	ТолькоВыделенные = Настройки.Свойство("ТолькоВыделенные");
	ВыделенныеОбработчики = Новый Массив;
	Если ТолькоВыделенные Тогда
		ВыделенныеОбработчики = ВыделенныеОбработчики();
	КонецЕсли;
	ОписаниеКонфигураций = Настройки.ОписаниеТэгов["configurations"];
	
	ТекстыМодулей = Новый Массив;
	
	Если НЕ ТолькоВыделенные И НЕ ПостроитьОчередьНаСервере() Тогда
		Возврат ТекстыМодулей;
	КонецЕсли;
	
	ИменаБиблиотек = Новый Массив;
	Тэги = ПолучитьСведенияОТегахВырезки(Настройки);
	РазметкаКода = Новый Структура;
	РазметкаКода.Вставить("Тэги", Тэги);
	ВерсииМодулей = ОписаниеВерсийМодулей();
	Для Каждого ОписаниеКонфигурации Из ОписаниеКонфигураций Цикл
		
		ИмяОсновногоСерверногоМодуля = ОписаниеКонфигурации.Значение["module"];
		Если ИмяОсновногоСерверногоМодуля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИменаБиблиотек.Добавить(ОписаниеКонфигурации.Значение["name"]);
		
		РазметкаКода.Вставить("ИмяБиблиотеки", ОписаниеКонфигурации.Ключ);
		РазметкаКода.Вставить("ИмяМодуля", ИмяОсновногоСерверногоМодуля);
		РазметкаКода.Вставить("ИмяПроцедуры", "ПриДобавленииОбработчиковОбновления");
		РазметкаКода.Вставить("МодульЛокализации", ОписаниеКонфигурации.Значение["localization"] <> Неопределено);
		РазметкаКода.Вставить("ТэгиЛокализации", "");
		РазметкаКода.Вставить("ЭтоМодульМенеджера", Ложь);
		РазметкаКода.Вставить("ДобавитьОбластьОписания", Истина);
		Если РазметкаКода.МодульЛокализации Тогда
			Если ОписаниеКонфигурации.Значение["outTags"].Количество() > 0 Тогда
				РазметкаКода.Вставить("ТэгиЛокализации", ОписаниеКонфигурации.Значение["outTags"][0]);
			КонецЕсли;
		КонецЕсли;
		
		Отбор = Новый Структура("ИмяОсновногоСерверногоМодуля", ИмяОсновногоСерверногоМодуля);
		ОбработчикиМодуля = Объект.ОбработчикиОбновления.Выгрузить(Отбор);
		КодМодуля = ОписаниеКодаМодуля(ИмяОсновногоСерверногоМодуля);
		ДобавитьВерсиюМодуля(ВерсииМодулей, ОбработчикиМодуля);
		ОбработчикиМодуля.Сортировать("Процедура");
		
		МодулиОбработчиков = МодулиОбработчиковОбновления(ОбработчикиМодуля, ВыделенныеОбработчики);
		Если НЕ ТолькоВыделенные Тогда
			КодМодуля.ТекстПроцедуры = ТекстПриДобавленииОбработчиковОбновления(МодулиОбработчиков, РазметкаКода);
			КодМодуля.ПутьМодуля = "CommonModules\" + ИмяОсновногоСерверногоМодуля + "\Module.bsl";
			КодМодуля.ЗаголовокПроцедуры = "Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт";
			ТекстыМодулей.Добавить(КодМодуля);
		КонецЕсли;
		
		ЗаполнитьТэгиМодулей("ИмяОбъекта", МодулиОбработчиков, РазметкаКода,, Ложь);
		РазметкаКода.Вставить("ИмяПроцедуры", "ОписаниеОбработчиковОбновления");
		Для Каждого Модуль Из МодулиОбработчиков Цикл
			Если ТолькоИзмененные И НЕ Модуль.Изменен Тогда
				Продолжить;
			КонецЕсли;
			
			РазметкаКода.ЭтоМодульМенеджера = СтрНайти(Модуль.ИмяОбъекта, "ОбщийМодуль") = 0;
			Отбор = Новый Структура("МодульОбработчика", Модуль.МодульОбработчика);
			ОбработчикиОбъекта = ОбработчикиМодуля.Скопировать(Отбор);
			ОбработчикиОбъекта.Сортировать("Процедура");
			РазметкаКода.ДобавитьОбластьОписания = ОбработчикиОбъекта.Количество() > 1;
			
			ТекстОбработчиков = ТекстОбработчиков(ОбработчикиОбъекта, РазметкаКода, Модуль.Тэги);
			ПоместитьВПроцедуру("ОписаниеОбработчиковОбновления", ТекстОбработчиков, РазметкаКода);
			
			КодМодуля = ОписаниеКодаМодуля(Модуль.МодульОбработчика, ТекстОбработчиков, ОбработчикиОбъекта[0].Процедура);
			КодМодуля.ПутьМодуля = "CommonModules\" + Модуль.МодульОбработчика + "\Module.bsl";
			КодМодуля.ЗаголовокПроцедуры = "Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт";
			Если СтрНайти(Модуль.МодульОбработчика, ".") > 0 Тогда
				Имена = СтрРазделить(Модуль.МодульОбработчика, ".");
				Имена[0] = АнглийскиеИмена[Имена[0]];
				КодМодуля.ПутьМодуля = СтрСоединить(Имена, "\") + "\ManagerModule.bsl";
			КонецЕсли;
			ТекстыМодулей.Добавить(КодМодуля);
		КонецЦикла;
		
	КонецЦикла;
	
	Если ВерсияКонфигурации <> НоваяВерсияКонфигурации Тогда
		УстановитьВерсиюМодуля(ТекстыМодулей, ВерсииМодулей);
	КонецЕсли;
	
	Если НЕ РежимОтладкиОписанияОбработчиков Тогда
		СброситьФлагИзменен();
	КонецЕсли;
	
	Возврат ТекстыМодулей;
	
КонецФункции

&НаСервере
Процедура ДобавитьВерсиюМодуля(ВерсииМодулей, ОбработчикиМодуля)
	
	Если ОбработчикиМодуля.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Обработка = ОбъектОбработки();
	ОбработчикиМодуля.Сортировать("РедакцияЧислом УБЫВ");
	
	Максимум = ОбработчикиМодуля[0];
	
	Описание = ВерсииМодулей.Добавить();
	Описание.ИмяМодуля = Максимум.ИмяОсновногоСерверногоМодуля;
	Описание.Версия = Максимум.Версия;
	
	// Проверим максимальный номер сборки из обработчиков модуля
	НомераМаксимум = СтрРазделить(Максимум.Версия, ".");
	НомераМаксимум.Удалить(0);
	Максимум3Последних = Обработка.ВерсияЧислом(НомераМаксимум);
	
	НомераКонфигурации = СтрРазделить(НоваяВерсияКонфигурации, ".");
	ПерваяЦифра = НомераКонфигурации[0];
	НомераКонфигурации.Удалить(0);
	Конфигурация3Последних = Обработка.ВерсияЧислом(НомераКонфигурации);
	Если Максимум3Последних > Конфигурация3Последних Тогда
		НоваяВерсияКонфигурации = ПерваяЦифра + "." + СтрСоединить(НомераМаксимум, ".");
	КонецЕсли;
	
	// Проверим номер сборки заданный пользователем
	НомераКонфигурации = СтрРазделить(НоваяВерсияКонфигурации, ".");
	Если НовыйНомерСборкиКонфигурации > Число(НомераКонфигурации[3]) Тогда
		НомераКонфигурации[3] = Формат(НовыйНомерСборкиКонфигурации, "ЧН=0; ЧГ=0");
		НоваяВерсияКонфигурации = СтрСоединить(НомераКонфигурации, ".");
		НомераМаксимум[3] = НомераКонфигурации[3];
	КонецЕсли;
	
	НомераМаксимум.Вставить(0,0);
	Описание.ВерсияРедакции = СтрСоединить(НомераМаксимум, ".");
	Описание.РедакцияЧислом = Обработка.ВерсияЧислом(Описание.ВерсияРедакции);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВерсиюМодуля(ТекстыМодулей, Модули)
	
	Модули.Сортировать("РедакцияЧислом УБЫВ");
	Максимум = Модули[0];
	Для Каждого Описание Из ТекстыМодулей Цикл
		
		Модуль = Модули.Найти(Описание.ИмяМодуля, "ИмяМодуля");
		Если Модуль = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НомераВерсии = СтрРазделить(Модуль.Версия, ".");
		НовыеНомера = СтрРазделить(Максимум.ВерсияРедакции, ".");
		НовыеНомера[0] = НомераВерсии[0];
		Описание.Версия = СтрСоединить(НовыеНомера, ".");
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКодаМодуля(ИмяМодуля = "", ТекстПроцедуры = "", ПроцедураОбработки = "", ПутьМодуля = "")
	
	Описание = Новый Структура;
	Описание.Вставить("ИмяМодуля", ИмяМодуля);
	Описание.Вставить("ЗаголовокПроцедуры", "");
	Описание.Вставить("ТекстПроцедуры", ТекстПроцедуры);
	Описание.Вставить("ПроцедураОбработки", ПроцедураОбработки);
	Описание.Вставить("ПутьМодуля", ПутьМодуля);
	Описание.Вставить("Версия", "");
	Описание.Вставить("ПроцедураВерсии", "Процедура ПриДобавленииПодсистемы(Описание) Экспорт");
	Возврат Описание;
	
КонецФункции

&НаСервере
Функция ОписаниеВерсийМодулей()
	
	Описание = Новый ТаблицаЗначений;
	Описание.Колонки.Добавить("ИмяМодуля", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
	Описание.Колонки.Добавить("Версия", , Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(30)));
	Описание.Колонки.Добавить("ВерсияРедакции", , Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(30)));
	Описание.Колонки.Добавить("РедакцияЧислом", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0)));
	Возврат Описание;
	
КонецФункции

&НаСервере
Функция МодулиОбработчиковОбновления(Обработчики, ВыделенныеОбработчики = Неопределено)
	
	ТипСтрока150 = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150));
	Обработчики.Колонки.Добавить("МодульОбработчика", ТипСтрока150);
	Обработчики.Колонки.Добавить("ИмяОбъекта", ТипСтрока150);
	
	ВыводитьТолькоВыделенные = ВыделенныеОбработчики <> Неопределено И ВыделенныеОбработчики.Количество() > 0;
	ВыделенныеОбработчикиМодуля = Новый Массив;
	Для Каждого Обработчик Из Обработчики Цикл
		
		ЧастиИмени = СтрРазделить(Обработчик.Процедура, ".");
		ЧастиИмени.Удалить(ЧастиИмени.ВГраница());
		Обработчик.МодульОбработчика = СтрСоединить(ЧастиИмени, ".");
		Обработчик.ИмяОбъекта = Обработчик.МодульОбработчика;
		Если ЧастиИмени.Количество() = 1 Тогда
			ЧастиИмени.Вставить(0,"ОбщийМодуль");
		Иначе
			ЧастиИмени[0] = ЕдинственноеЧисло[ЧастиИмени[0]];
		КонецЕсли;
		Обработчик.ИмяОбъекта = СтрСоединить(ЧастиИмени, ".");
		
		Если ВыводитьТолькоВыделенные Тогда
			ОбработчикВыделен = ВыделенныеОбработчики.Найти(Обработчик.Ссылка) <> Неопределено;
		КонецЕсли;
			
		Если ВыводитьТолькоВыделенные И ОбработчикВыделен Тогда
			ВыделенныеОбработчикиМодуля.Добавить(Обработчик.ИмяОбъекта);
		КонецЕсли;
		
	КонецЦикла;
	МодулиОбработчиков = Обработчики.Скопировать();
	МодулиОбработчиков.Свернуть("МодульОбработчика,ИмяОбъекта", "Изменен");
	МодулиОбработчиков.Сортировать("МодульОбработчика");
	
	Если ВыводитьТолькоВыделенные Тогда
		Выделенные = МодулиОбработчиков.Скопировать();
		Выделенные.Очистить();
		Для Каждого Выделенный Из ВыделенныеОбработчикиМодуля Цикл
			Отбор = Новый Структура("ИмяОбъекта", Выделенный);
			НайденныеСтроки = МодулиОбработчиков.НайтиСтроки(Отбор);
			Для Каждого Найденный Из НайденныеСтроки Цикл
				НовыйОбработчик = Выделенные.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйОбработчик, Найденный);
			КонецЦикла;
		КонецЦикла;
		Возврат Выделенные;
	КонецЕсли;
	
	Возврат МодулиОбработчиков;
	
КонецФункции

&НаСервере
Функция ВыделенныеОбработчики()
	
	Результат = Новый Массив;
	Для Каждого ИдСтроки Из Элементы.ОбработчикиОбновления.ВыделенныеСтроки Цикл
		Обработчик = Объект.ОбработчикиОбновления.НайтиПоИдентификатору(ИдСтроки);
		Результат.Добавить(Обработчик.Ссылка);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СброситьФлагИзменен()
	
	Отбор = Новый Структура("Изменен", Истина);
	Найденные = Объект.ОбработчикиОбновления.НайтиСтроки(Отбор);
	Для Каждого Обработчик Из Найденные Цикл
		Обработчик.Изменен = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеКодаПроцедур

// Параметры:
// 	ИменаБиблиотек - Массив - Описание
// 	РазметкаКода - Структура - Описание:
// * Тэги - см. ПолучитьСведенияОТегахВырезки
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ИмяПроцедуры - Строка -
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстПриФормированииОчередейОтложенныхОбработчиков(ИменаБиблиотек, РазметкаКода)
	
	Обработка = ОбъектОбработки();
	Обработчики = Обработка.ОбработчикиСПереопределеннойОчередью(ИменаБиблиотек);
	
	РазметкаКода.ИмяБиблиотеки = "ПереопределениеОчереди";
	РазметкаКода.ИмяМодуля = "ПереопределениеОчереди";
	Тэги = Новый Массив;
	Тэги.Добавить("НЕ_МЕЖДУНАРОДНАЯ");
	РазметкаКода.Тэги.ПоМодулям.Вставить("ПереопределениеОчереди", Тэги);
	ЗаполнитьТэгиОбъектов("Процедура", Обработчики, РазметкаКода);
	
	тз = Обработчики.Скопировать(,"Тэги");
	тз.Свернуть("Тэги");
	тз.Сортировать("Тэги");
	ТэгиВсехОбъектов = тз.ВыгрузитьКолонку("Тэги");
	
	ОписанияВсехТэгов = Новый Соответствие;
	ТекстыТэгов = Новый Массив;
	Для Каждого Тэги Из ТэгиВсехОбъектов Цикл
		
		Отбор = Новый Структура("Тэги", Тэги);
		ОбработчикиТэга = Обработчики.НайтиСтроки(Отбор);
		
		// группировка
		ПереопределяемыеОбработчики = Новый Соответствие;
		Для Каждого СтрокаОбработчиков Из ОбработчикиТэга Цикл
			ПереопределяемыеОбработчики.Вставить(СтрокаОбработчиков.Процедура, Формат(СтрокаОбработчиков.ОчередьУстановленная,"ЧН=; ЧГ="));
		КонецЦикла;
		// сортировка
		СписокПереопределяемых = Новый СписокЗначений;
		Для Каждого Обработчик Из ПереопределяемыеОбработчики Цикл
			СписокПереопределяемых.Добавить(Обработчик.Ключ, Обработчик.Значение);
		КонецЦикла;
		СписокПереопределяемых.СортироватьПоЗначению();
		// вывод
		ШаблонСтроки = "	ОбработчикИОчередь.Вставить(""%1"", %2);";
		СтрокиТэга = Новый Массив;
		Для Каждого Обработчик Из СписокПереопределяемых Цикл
			Строка = СтрШаблон(ШаблонСтроки, Обработчик.Значение, Обработчик.Представление);
			СтрокиТэга.Добавить(Строка);
		КонецЦикла;
		ТекстТэга = СтрСоединить(СтрокиТэга, Символы.ПС);
		
		Если НЕ ПустаяСтрока(Тэги) Тогда
			Для Каждого Тэг Из СтрРазделить(Тэги, ",") Цикл
				Шаблон = "%1
						|%2
						|%3";
				ОписаниеТэга = РазметкаКода.Тэги.Описание[Тэг]; // см. ОписаниеТэга
				ТекстТэга = СтрШаблон(Шаблон, ОписаниеТэга.Начало, ТекстТэга, ОписаниеТэга.Конец);
				ОписанияВсехТэгов.Вставить(Тэг, ОписаниеТэга);
			КонецЦикла;
		КонецЕсли;
		ТекстыТэгов.Добавить(ТекстТэга);
	КонецЦикла;// по тэгам
	ТекстПроцедуры = СтрСоединить(ТекстыТэгов, Символы.ПС);
	
	Шаблон = "%1
			|%2";
	Для Каждого ОписаниеТэга Из ОписанияВсехТэгов Цикл
		КонецНачало = СтрШаблон(Шаблон, ОписаниеТэга.Значение.Конец, ОписаниеТэга.Значение.Начало);
		ТекстПроцедуры = СтрЗаменить(ТекстПроцедуры, КонецНачало, "");
	КонецЦикла;
	
	ТекстПроцедуры = 
	"Процедура ПриФормированииОчередейОтложенныхОбработчиков(ОбработчикИОчередь) Экспорт
	|
	|"+ СтрСоединить(ТекстыТэгов, Символы.ПС) +"
	|
	|КонецПроцедуры";
	
	Возврат ТекстПроцедуры;
	
КонецФункции

&НаСервере
Функция ТекстОбработчиков(Обработчики, РазметкаКода, ТэгиМодуля = Неопределено)
	
	Обработчики.Колонки.Добавить("Тэги", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	Если РазметкаКода.МодульЛокализации И ТэгиМодуля = Неопределено Тогда
		ЗаполнитьТэгиОбъектов("Процедура", Обработчики, РазметкаКода, РазметкаКода.ТэгиЛокализации, Ложь);
	КонецЕсли;
	Если ТэгиМодуля <> Неопределено Тогда
		Обработчики.ЗаполнитьЗначения(ТэгиМодуля, "Тэги");
	КонецЕсли;
	
	ДеревоТэгов = СвернутьПоТегам(Обработчики, РазметкаКода);
	СтрокиГруппОбработчиков = Новый Массив;
	Для Каждого Ветка Из ДеревоТэгов.Строки Цикл
		ТекстГруппыОбработчиков = ТекстГруппыОбработчиковРекурсивно(Ветка, РазметкаКода, СтрокиГруппОбработчиков.Количество() = 0);
		СтрокиГруппОбработчиков.Добавить(ТекстГруппыОбработчиков);
	КонецЦикла;
	ТекстОбработчиков = СтрСоединить(СтрокиГруппОбработчиков, Символы.ПС);
	
	Возврат ТекстОбработчиков;
	
КонецФункции

// Параметры:
// 	Ветка - СтрокаДереваЗначений - Описание
// 	РазметкаКода - Структура - Описание:
// * Тэги - см. ПолучитьСведенияОТегахВырезки
// * ИмяПроцедуры - Строка -
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
// 	ПервыйВГруппе - Булево - Описание
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстГруппыОбработчиковРекурсивно(Ветка, РазметкаКода, ПервыйВГруппе = Истина)
	
	СтрокиОбработчиков = Новый Массив;
	Для Каждого Обработчик Из Ветка.Строки Цикл
		Если Обработчик.Строки.Количество() > 0 Тогда
			ТекстГруппыОбработчиков = ТекстГруппыОбработчиковРекурсивно(Обработчик, РазметкаКода, СтрокиОбработчиков.Количество() = 0);
			СтрокиОбработчиков.Добавить(ТекстГруппыОбработчиков);
			Продолжить;
		КонецЕсли;
		ТекстОбработчика = ТекстОписанияОбработчика(Обработчик, РазметкаКода, Ветка.Тэги);
		Если СтрокиОбработчиков.Количество() > 0 Тогда
			ТекстОбработчика = Символы.ПС + ТекстОбработчика;
		КонецЕсли;
		СтрокиОбработчиков.Добавить(ТекстОбработчика);
	КонецЦикла;
	
	ТекстОбработчиков = СтрСоединить(СтрокиОбработчиков, Символы.ПС);
	Если НЕ ПустаяСтрока(Ветка.Тэги) И РазметкаКода.ИмяПроцедуры = "ПриДобавленииОбработчиковОбновления" Тогда
		Шаблон = "%1" + Символы.ПС + "%2" + Символы.ПС + "%3";
		Если НЕ ПервыйВГруппе Тогда
			Шаблон = "%1" + Символы.ПС + Символы.ПС + "%2" + Символы.ПС + "%3";
		КонецЕсли;
		Для Каждого Тэг Из СтрРазделить(Обработчик.Тэги, ",") Цикл
			ОписаниеТэга = РазметкаКода.Тэги.Описание[Тэг]; // см. ОписаниеТэга
			ТекстОбработчиков = СтрШаблон(Шаблон, ОписаниеТэга.Начало, ТекстОбработчиков, ОписаниеТэга.Конец);
		КонецЦикла;
		
	ИначеЕсли НЕ ПервыйВГруппе Тогда
		ТекстОбработчиков = Символы.ПС + ТекстОбработчиков;
		
	КонецЕсли;
	
	Возврат ТекстОбработчиков;
	
КонецФункции

&НаСервере
Функция ТекстПриДобавленииОбработчиковОбновления(МодулиОбработчиков, РазметкаКода)
	
	МодулиОбработчиков.Колонки.Добавить("Тэги", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	Если РазметкаКода.МодульЛокализации Тогда
		ЗаполнитьТэгиОбъектов("ИмяОбъекта", МодулиОбработчиков, РазметкаКода, РазметкаКода.ТэгиЛокализации, Ложь);
	КонецЕсли;
	
	ДеревоТэгов = СвернутьПоТегам(МодулиОбработчиков, РазметкаКода);
	СтрокиГруппВызовов = Новый Массив;
	Для Каждого Ветка Из ДеревоТэгов.Строки Цикл
		ТекстГруппыВызовов = ТекстГруппыВызововРекурсивно(Ветка, РазметкаКода);
		СтрокиГруппВызовов.Добавить(ТекстГруппыВызовов);
	КонецЦикла;
	ТекстПроцедуры = СтрСоединить(СтрокиГруппВызовов, Символы.ПС);
	
	ПоместитьВПроцедуру("ПриДобавленииОбработчиковОбновления", ТекстПроцедуры, РазметкаКода);
	
	Возврат ТекстПроцедуры;
	
КонецФункции

// Параметры:
// 	Ветка - СтрокаДереваЗначений - Описание
// 	РазметкаКода - Структура - Описание:
// * Тэги - см. ПолучитьСведенияОТегахВырезки
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстГруппыВызововРекурсивно(Ветка, РазметкаКода)
	
	СтрокиВызовов = Новый Массив;
	Для Каждого МодульОбработчика Из Ветка.Строки Цикл
		Если МодульОбработчика.Строки.Количество() > 0 Тогда
			ТекстГруппыОбработчиков = ТекстГруппыВызововРекурсивно(МодульОбработчика, РазметкаКода);
			СтрокиВызовов.Добавить(ТекстГруппыОбработчиков);
			Продолжить;
		КонецЕсли;
		ТекстВызова = Символы.Таб + МодульОбработчика.МодульОбработчика + ".ОписаниеОбработчиковОбновления(Обработчики);";
		СтрокиВызовов.Добавить(ТекстВызова);
	КонецЦикла;
	
	ТекстОбработчиков = СтрСоединить(СтрокиВызовов, Символы.ПС);
	Если НЕ ПустаяСтрока(Ветка.Тэги) Тогда
		Шаблон = "%1" + Символы.ПС + "%2" + Символы.ПС + "%3";
		Для Каждого Тэг Из СтрРазделить(МодульОбработчика.Тэги, ",") Цикл
			ОписаниеТэга = РазметкаКода.Тэги.Описание[Тэг]; // см. ОписаниеТэга
			ТекстОбработчиков = СтрШаблон(Шаблон, ОписаниеТэга.Начало, ТекстОбработчиков, ОписаниеТэга.Конец);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекстОбработчиков;
	
КонецФункции

// Параметры:
// 	Обработчик - СтрокаДереваЗначений - Описание:
// 	 * Идентификатор - УникальныйИдентификатор -
// 	РазметкаКода - Структура - Описание:
// * Тэги - см. ПолучитьСведенияОТегахВырезки
// * ИмяПроцедуры - Строка -
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
// 	ТэгиОбработчика - Строка -
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстОписанияОбработчика(Обработчик, РазметкаКода, ТэгиОбработчика)
	
	СтрокиОбработчика = Новый Массив;
	СтрокиОбработчика.Добавить("	Обработчик = Обработчики.Добавить();");
	СтрокиОбработчика.Добавить("Обработчик.Процедура = """ + Обработчик.Процедура + """;");
	СтрокиОбработчика.Добавить("Обработчик.Версия = """ + Обработчик.Версия + """;");
	СтрокиОбработчика.Добавить("Обработчик.РежимВыполнения = """ + Строка(Обработчик.РежимВыполнения) + """;");
	
	Если Обработчик.НачальноеЗаполнение Тогда
		СтрокиОбработчика.Добавить("Обработчик.НачальноеЗаполнение = Истина;");
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Обработчик.Идентификатор) Тогда
		ИД = Новый УникальныйИдентификатор(Обработчик.Идентификатор);
		Если ЗначениеЗаполнено(ИД) Тогда
			СтрокиОбработчика.Добавить("Обработчик.Идентификатор = Новый УникальныйИдентификатор(""" + Обработчик.Идентификатор + """);");
		КонецЕсли;
	КонецЕсли;
	
	Если Обработчик.ОбщиеДанные Тогда
		СтрокиОбработчика.Добавить("Обработчик.ОбщиеДанные = Истина;");
	КонецЕсли;
	
	Если Обработчик.УправлениеОбработчиками Тогда
		СтрокиОбработчика.Добавить("Обработчик.УправлениеОбработчиками = Истина;");
	КонецЕсли;
	
	Если Обработчик.Многопоточный Тогда
		СтрокиОбработчика.Добавить("Обработчик.Многопоточный = Истина;");
	КонецЕсли;
	
	ОтложенныйРежимВыполнения = Обработчик.РежимВыполнения = "Отложенно";
	Если ОтложенныйРежимВыполнения Тогда
		СтрокиОбработчика.Добавить("Обработчик.ПроцедураЗаполненияДанныхОбновления = """ + Обработчик.ПроцедураЗаполненияДанныхОбновления + """;");
		СтрокиОбработчика.Добавить("Обработчик.ПроцедураПроверки = """ + Обработчик.ПроцедураПроверки + """;");
	КонецЕсли;
	
	Если Обработчик.ЗапускатьТолькоВГлавномУзле Тогда
		СтрокиОбработчика.Добавить("Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;");
	КонецЕсли;
	
	Если Обработчик.ЗапускатьИВПодчиненномУзлеРИБСФильтрами Тогда
		СтрокиОбработчика.Добавить("Обработчик.ЗапускатьИВПодчиненномУзлеРИБСФильтрами = Истина;");
	КонецЕсли;
	
	ТекстКомментария = СокрЛП(Обработчик.Комментарий);
	ТекстКомментария = СтрЗаменить(ТекстКомментария, Символы.ПС, Символы.ПС + "	|");
	ТекстКомментария = СтрЗаменить(ТекстКомментария, """", """""");
	СтрокиОбработчика.Добавить("Обработчик.Комментарий = НСтр(""ru = '" + ТекстКомментария +"'"");");
	
	ТекстЧитаемыеОбъекты = ТекстИнформацииОбОбъектах(Обработчик, "ЧитаемыеОбъекты", РазметкаКода, ТэгиОбработчика);
	Если НЕ ПустаяСтрока(ТекстЧитаемыеОбъекты) Тогда
		СтрокиОбработчика.Добавить(ТекстЧитаемыеОбъекты);
	КонецЕсли;
	
	ТекстИзменяемыеОбъекты = ТекстИнформацииОбОбъектах(Обработчик, "ИзменяемыеОбъекты", РазметкаКода, ТэгиОбработчика);
	Если НЕ ПустаяСтрока(ТекстИзменяемыеОбъекты) Тогда
		СтрокиОбработчика.Добавить(ТекстИзменяемыеОбъекты);
	КонецЕсли;
	
	Если ОтложенныйРежимВыполнения Тогда
		ТекстБлокируемыеОбъекты = ТекстИнформацииОбОбъектах(Обработчик, "БлокируемыеОбъекты", РазметкаКода, ТэгиОбработчика);
		Если НЕ ПустаяСтрока(ТекстБлокируемыеОбъекты) Тогда
			СтрокиОбработчика.Добавить(ТекстБлокируемыеОбъекты);
		КонецЕсли;
	КонецЕсли;
	
	Если ОтложенныйРежимВыполнения Тогда
		ТекстПриоритетыВыполнения = ТекстПриоритетовВыполнения(Обработчик, РазметкаКода, ТэгиОбработчика);
		Если НЕ ПустаяСтрока(ТекстПриоритетыВыполнения) Тогда
			СтрокиОбработчика.Добавить(ТекстПриоритетыВыполнения);
		КонецЕсли;
	КонецЕсли;
	ТекстОбработчика = СтрСоединить(СтрокиОбработчика, Символы.ПС + Символы.Таб);
	
	ШаблонОбласти = 
	"#Область %1
	|
	|%2
	|
	|#КонецОбласти";
	ИмяОбласти = СтрЗаменить(Обработчик.Процедура, ".", "_");
	Если РазметкаКода.ЭтоМодульМенеджера Тогда
		Имена = СтрРазделить(Обработчик.Процедура, ".");
		ИмяОбласти = Имена[Имена.ВГраница()];
	КонецЕсли;
	Результат = ТекстОбработчика;
	Если РазметкаКода.ДобавитьОбластьОписания Тогда
		Результат = СтрШаблон(ШаблонОбласти, ИмяОбласти, ТекстОбработчика);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
// 	Обработчик - СтрокаДереваЗначений - Описание:
// * Ссылка - Строка -
// * Идентификатор - УникальныйИдентификатор -
// 	РазметкаКода - Структура - Описание:
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
// 	ТэгиОбработчика - Строка - Описание
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстПриоритетовВыполнения(Обработчик, РазметкаКода, ТэгиОбработчика)
	
	Отбор = Новый Структура("Ссылка", Обработчик.Ссылка);
	ПриоритетыВыполнения = Объект.ПриоритетыВыполнения.Выгрузить(Отбор);
	Отбор = Новый Структура("ОбработчикЧитательИлиПисатель2", Обработчик.Ссылка);
	Конфликты = Объект.КонфликтыОбработчиков.Выгрузить(Отбор);
	ПриоритетыКонфликтовЧитаемыхИлиДвойнойЗаписи(ПриоритетыВыполнения, Конфликты);
	
	ТекстПриоритетов = "";
	Если ПриоритетыВыполнения.Количество() = 0 Тогда
		Возврат ТекстПриоритетов;
	КонецЕсли;
		
	ТекстПриоритетов = ТекстПриоритетов + "
	|	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	|
	|";
	
	ВнешниеТэги = ТэгиОбработчика;
	Если РазметкаКода.МодульЛокализации Тогда
		ВнешниеТэги = ТэгиОбработчика + "," + РазметкаКода.ТэгиЛокализации;
	КонецЕсли;
	ЗаполнитьТэгиОбъектов("Процедура2", ПриоритетыВыполнения, РазметкаКода, ВнешниеТэги);
	тз = ПриоритетыВыполнения.Скопировать(,"Тэги");
	тз.Свернуть("Тэги");
	тз.Сортировать("Тэги");
	ТэгиВсехОбъектов = тз.ВыгрузитьКолонку("Тэги");
	
	ОписанияВсехТэгов = Новый Соответствие;
	ШаблонПриоритета = 
	"	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	|	НоваяСтрока.Процедура = ""%1"";
	|	НоваяСтрока.Порядок = ""%2"";";
	ТекстыТэгов = Новый Массив;
	Для Каждого Тэги Из ТэгиВсехОбъектов Цикл
		Отбор = Новый Структура("Тэги", Тэги);
		ПриоритетыТэга = ПриоритетыВыполнения.НайтиСтроки(Отбор);
		СтрокиТэга = Новый Массив;
		Для Каждого Приоритет Из ПриоритетыТэга Цикл
			СтрокаПриоритета = СтрШаблон(ШаблонПриоритета,Приоритет.Процедура2,Приоритет.Порядок);
			СтрокиТэга.Добавить(СтрокаПриоритета);
		КонецЦикла;
		
		ТекстТэга = СтрСоединить(СтрокиТэга, Символы.ПС + Символы.ПС);
		Если НЕ ПустаяСтрока(Тэги) Тогда
			Для Каждого Тэг Из СтрРазделить(Тэги, ",") Цикл
				Шаблон = "	%1
						|	%2
						|	%3";
				ОписаниеТэга = РазметкаКода.Тэги.Описание[Тэг]; // см. ОписаниеТэга
				ТекстТэга = СтрШаблон(Шаблон, ОписаниеТэга.Начало, ТекстТэга, ОписаниеТэга.Конец);
				ОписанияВсехТэгов.Вставить(Тэг, ОписаниеТэга);
			КонецЦикла;
			ТекстТэга = СтрЗаменить(ТекстТэга, "		", "	");
		КонецЕсли;
		ТекстыТэгов.Добавить(ТекстТэга);
	КонецЦикла;
	ТекстТэгов = СтрСоединить(ТекстыТэгов, Символы.ПС);
	
	Шаблон = "	%1
			|	%2
			|";
	Для Каждого ОписаниеТэга Из ОписанияВсехТэгов Цикл
		КонецНачало = СтрШаблон(Шаблон, ОписаниеТэга.Значение.Конец, ОписаниеТэга.Значение.Начало);
		ТекстТэгов = СтрЗаменить(ТекстТэгов, КонецНачало, "");
	КонецЦикла;
	ТекстПриоритетов = ТекстПриоритетов + ТекстТэгов; 
	
	Возврат ТекстПриоритетов;
	
КонецФункции


// Описание
// 
// Параметры:
// 	Обработчик - СтрокаДереваЗначений - Описание:
// * Ссылка - Строка -
// * Идентификатор - УникальныйИдентификатор -
// 	ИмяНабораОбъектов - Строка - Описание
// 	РазметкаКода - Структура - Описание:
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
// 	ТэгиОбработчика - Строка - Описание
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ТекстИнформацииОбОбъектах(Обработчик, ИмяНабораОбъектов, РазметкаКода, ТэгиОбработчика)
	
	Отбор = Новый Структура("Ссылка", Обработчик.Ссылка);
	ОбъектыОбработчика = Объект[ИмяНабораОбъектов].Выгрузить(Отбор);
	Если ИмяНабораОбъектов = "БлокируемыеОбъекты" Тогда
		Отбор.Вставить("БлокироватьИнтерфейс", Истина);
		ИзменяемыеОбъекты = Объект["ИзменяемыеОбъекты"].Выгрузить(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИзменяемыеОбъекты, ОбъектыОбработчика);
		ОбъектыОбработчика.Свернуть("ОбъектМетаданных");
		ОбъектыОбработчика.Сортировать("ОбъектМетаданных");
	КонецЕсли;
	Если ОбъектыОбработчика.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	ВнешниеТэги = ТэгиОбработчика;
	Если РазметкаКода.МодульЛокализации Тогда
		ВнешниеТэги = ТэгиОбработчика + "," + РазметкаКода.ТэгиЛокализации;
	КонецЕсли;
	ЗаполнитьТэгиОбъектов("ОбъектМетаданных", ОбъектыОбработчика, РазметкаКода, ВнешниеТэги);
	
	ИмяНабора = СтрЗаменить(ИмяНабораОбъектов, "Объекты", "");
	ДеревоТэгов = СвернутьПоТегам(ОбъектыОбработчика, РазметкаКода);
	СтрокиГруппОбъектов = Новый Массив;
	Для Каждого Ветка Из ДеревоТэгов.Строки Цикл
		ТекстГруппыОбъектов = ТекстГруппыОбъектовРекурсивно(Ветка, РазметкаКода, ИмяНабора);
		СтрокиГруппОбъектов.Добавить(ТекстГруппыОбъектов);
	КонецЦикла;
	ТекстОбъектов = СтрСоединить(СтрокиГруппОбъектов, Символы.ПС);
	Шаблон = "
	|	%1 = Новый Массив;
	|%2
	|	Обработчик.%3 = СтрСоединить(%1, "","");";
	Результат = СтрШаблон(Шаблон, ИмяНабора, ТекстОбъектов, ИмяНабораОбъектов);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТекстГруппыОбъектовРекурсивно(Ветка, РазметкаКода, ИмяНабораОбъектов)
	
	СтрокиОбъектов = Новый Массив;
	Для Каждого ОбъектМетаданных Из Ветка.Строки Цикл
		Если ОбъектМетаданных.Строки.Количество() > 0 Тогда
			ТекстГруппыОбъектов = ТекстГруппыОбъектовРекурсивно(ОбъектМетаданных, РазметкаКода, ИмяНабораОбъектов);
			СтрокиОбъектов.Добавить(ТекстГруппыОбъектов);
			Продолжить;
		КонецЕсли;
		ТекстОбъекта = Символы.Таб + ИмяНабораОбъектов + ".Добавить(" + ПолныйПутьМетаданныхОбъекта(ОбъектМетаданных.ОбъектМетаданных) + ");";
		СтрокиОбъектов.Добавить(ТекстОбъекта);
	КонецЦикла;
	
	ТекстОбъектов = СтрСоединить(СтрокиОбъектов, Символы.ПС);
	Если НЕ ПустаяСтрока(Ветка.Тэги) Тогда
		Шаблон = "	%1" + Символы.ПС + "%2" + Символы.ПС + "	%3";
		Для Каждого Тэг Из СтрРазделить(Ветка.Тэги, ",") Цикл
			ОписаниеТэга = РазметкаКода.Тэги.Описание[Тэг]; // см. ОписаниеТэга
			ТекстОбъектов = СтрШаблон(Шаблон, ОписаниеТэга.Начало, ТекстОбъектов, ОписаниеТэга.Конец);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекстОбъектов;
	
КонецФункции

&НаСервере
Функция ПолныйПутьМетаданныхОбъекта(ИмяОбъекта)
	
	ЧастиИмени = СтрРазделить(ИмяОбъекта, ".");
	ЧастиИмени[0] = МножественноеЧисло[ЧастиИмени[0]];
	ЧастиИмени.Вставить(0, "Метаданные");
	ЧастиИмени.Добавить("ПолноеИмя()");
	Возврат СтрСоединить(ЧастиИмени,".");
	
КонецФункции

&НаСервере
Функция ЗаполнитьТэгиОбъектов(РеквизитИмяОбъекта, ОбъектыОбработчика, РазметкаКода, ТэгиВерхнегоУровня = Неопределено, СортироватьПоТэгам = Истина)
	
	ЭтоИмяПроцедуры = РеквизитИмяОбъекта = "Процедура" ИЛИ РеквизитИмяОбъекта = "Процедура2";
	ТэгиМодуля = РазметкаКода.Тэги.ПоМодулям[РазметкаКода.ИмяМодуля];
	Если ОбъектыОбработчика.Колонки.Найти("Тэги") = Неопределено Тогда
		ОбъектыОбработчика.Колонки.Добавить("Тэги", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	КонецЕсли;
	Если ТэгиМодуля = Неопределено Тогда
		Возврат ОбъектыОбработчика;
	КонецЕсли;
	ЛишниеТэги = Новый Массив;
	ЛишниеТэги = ЛишниеТэгиВОбласти(ТэгиВерхнегоУровня, РазметкаКода);
	Для Каждого ИспользуемыйОбъект Из ОбъектыОбработчика Цикл
		Если ЭтоИмяПроцедуры Тогда
			ИмяОбъекта = ИмяОбъектаИзПроцедурыОбработки(ИспользуемыйОбъект[РеквизитИмяОбъекта]);
		Иначе
			ИмяОбъекта = ИспользуемыйОбъект[РеквизитИмяОбъекта];
		КонецЕсли;
		ОписаниеТэгов = РазметкаКода.Тэги.ПоОбъектам.Найти(ИмяОбъекта, "Имя");
		Если ОписаниеТэгов <> Неопределено Тогда
			ТэгиОбъекта = Новый Массив;
			Для Каждого Тэг Из СтрРазделить(ОписаниеТэгов.Тэги,",") Цикл
				ЛишнийТэг = ЛишниеТэги.Найти(Тэг) <> Неопределено;
				Если НЕ ЛишнийТэг Тогда
					ТэгиОбъекта.Добавить(Тэг);
				КонецЕсли;
			КонецЦикла;
			ИспользуемыйОбъект.Тэги = СтрСоединить(ТэгиОбъекта, ",");
		КонецЕсли;// найденны тэги объекта
	КонецЦикла;
	ПоляСортировки = РеквизитИмяОбъекта;
	Если СортироватьПоТэгам Тогда
		ПоляСортировки = "Тэги," + РеквизитИмяОбъекта;
	КонецЕсли;
	ОбъектыОбработчика.Сортировать(ПоляСортировки);
	
	Возврат ОбъектыОбработчика;
	
КонецФункции

&НаСервере
Функция ЗаполнитьТэгиМодулей(РеквизитИмяОбъекта, ОбъектыОбработчика, РазметкаКода, ТэгиВерхнегоУровня = Неопределено, СортироватьПоТэгам = Истина)
	
	ЭтоИмяПроцедуры = РеквизитИмяОбъекта = "Процедура" ИЛИ РеквизитИмяОбъекта = "Процедура2";
	ТэгиМодуля = РазметкаКода.Тэги.ПоМодулям[РазметкаКода.ИмяМодуля];
	Если ОбъектыОбработчика.Колонки.Найти("Тэги") = Неопределено Тогда
		ОбъектыОбработчика.Колонки.Добавить("Тэги", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	КонецЕсли;
	Если ТэгиМодуля = Неопределено Тогда
		Возврат ОбъектыОбработчика;
	КонецЕсли;
	ВнешниеТэги = Новый Массив;
	ВнешниеТэги = ЛишниеТэгиВОбласти(ТэгиВерхнегоУровня, РазметкаКода);
	Для Каждого ИспользуемыйОбъект Из ОбъектыОбработчика Цикл
		Если ЭтоИмяПроцедуры Тогда
			ИмяОбъекта = ИмяОбъектаИзПроцедурыОбработки(ИспользуемыйОбъект[РеквизитИмяОбъекта]);
		Иначе
			ИмяОбъекта = ИспользуемыйОбъект[РеквизитИмяОбъекта];
		КонецЕсли;
		ОписаниеТэгов = РазметкаКода.Тэги.ПоОбъектам.Найти(ИмяОбъекта, "Имя");
		Если ОписаниеТэгов <> Неопределено Тогда
			ТэгиОбъекта = ОбщегоНазначения.СкопироватьРекурсивно(ВнешниеТэги);
			Для Каждого Тэг Из СтрРазделить(ОписаниеТэгов.Тэги,",") Цикл
				ВнешнийТэг = ВнешниеТэги.Найти(Тэг) <> Неопределено;
				Если НЕ ВнешнийТэг Тогда
					ТэгиОбъекта.Добавить(Тэг);
				КонецЕсли;
			КонецЦикла;
			ИспользуемыйОбъект.Тэги = СтрСоединить(ТэгиОбъекта, ",");
		КонецЕсли;// найденны тэги объекта
	КонецЦикла;
	ПоляСортировки = РеквизитИмяОбъекта;
	Если СортироватьПоТэгам Тогда
		ПоляСортировки = "Тэги," + РеквизитИмяОбъекта;
	КонецЕсли;
	ОбъектыОбработчика.Сортировать(ПоляСортировки);
	
	Возврат ОбъектыОбработчика;
	
КонецФункции

&НаСервере
Функция ЛишниеТэгиВОбласти(ТэгиВерхнегоУровня, РазметкаКода)
	
	ЛишниеТэги = СтрРазделить(ТэгиВерхнегоУровня, ",", Ложь);
	ВложенныеТэги = Новый Массив;
	Для Каждого ЛишнийТэг Из ЛишниеТэги Цикл
		БиблиотекиТэга = РазметкаКода.Тэги.ПоБиблиотекам.НайтиСтроки(Новый Структура("Тэг",ЛишнийТэг));
		Если БиблиотекиТэга = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Имя Из БиблиотекиТэга Цикл
			ТэгиВложенныхБиблиотек = РазметкаКода.Тэги.ВложенныхБиблиотек[Имя.Библиотека];
			Если ТэгиВложенныхБиблиотек <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВложенныеТэги, ТэгиВложенныхБиблиотек, Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЛишниеТэги, ВложенныеТэги, Истина);
	//Тэги вложенной библиотеки не ставим в модуле
	ТэгиВложенныхБиблиотек = РазметкаКода.Тэги.ВложенныхБиблиотек[РазметкаКода.ИмяБиблиотеки];
	Если ТэгиВложенныхБиблиотек <> Неопределено И НЕ РазметкаКода.ЭтоМодульМенеджера Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЛишниеТэги, ТэгиВложенныхБиблиотек, Истина);
	КонецЕсли;
	
	Возврат ЛишниеТэги;
	
КонецФункции

&НаСервере
Функция ИмяОбъектаИзПроцедурыОбработки(ИмяПроцедурыОбработки)
	
	ЧастиИмени = СтрРазделить(ИмяПроцедурыОбработки, ".");
	Если ЧастиИмени.Количество() = 2 Тогда
		ИмяОбъекта = ЧастиИмени[0];
	Иначе
		ЧастиИмени[0] = ЕдинственноеЧисло[ЧастиИмени[0]];
		ЧастиИмени.Удалить(ЧастиИмени.ВГраница());
		ИмяОбъекта = СтрСоединить(ЧастиИмени, ".");
	КонецЕсли;
	
	Возврат ИмяОбъекта;
	
КонецФункции

&НаСервере
Функция СвернутьПоТегам(Обработчики, РазметкаКода) 
	
	ДеревоТэгов = Новый ДеревоЗначений;
	Для Каждого Колонка Из Обработчики.Колонки Цикл
		ДеревоТэгов.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	Вложенность = РазметкаКода.Тэги.Вложенность;
	ВеткаТэга = Неопределено; // СтрокаДереваЗначений -
	Индекс = 0;
	Пока Индекс < Обработчики.Количество() Цикл
		Обработчик = Обработчики[Индекс];
		Если ВеткаТэга = Неопределено ИЛИ ВеткаТэга.Тэги <> Обработчик.Тэги Тогда
			ВеткаТэга = ДеревоТэгов.Строки.Добавить(); 
			ВеткаТэга.Тэги = Обработчик.Тэги;
			ДобавитьОбработчикиТэга(Индекс, Обработчики, ВеткаТэга, Вложенность);
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ВеткаТэга.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Обработчик);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат ДеревоТэгов;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбработчикиТэга(Индекс, Обработчики, ВеткаТэга, Вложенность)
	
	ОбщийТэг = Вложенность.Строки.Найти(ВеткаТэга.Тэги,"Тэг",Истина);
	Пока Индекс < Обработчики.Количество() Цикл
		Обработчик = Обработчики[Индекс];
		Если ВеткаТэга.Тэги <> Обработчик.Тэги Тогда
			Если ОбщийТэг = Неопределено
				ИЛИ ОбщийТэг.Строки.Найти(Обработчик.Тэги) = Неопределено Тогда
				Прервать;
			КонецЕсли;
			ВложеннаяВеткаТэга = ВеткаТэга.Строки.Добавить();
			ВложеннаяВеткаТэга.Тэги = Обработчик.Тэги;
			ДобавитьОбработчикиТэга(Индекс, Обработчики, ВложеннаяВеткаТэга, Вложенность);
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ВеткаТэга.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Обработчик);
		Индекс = Индекс + 1;
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
// 	ИмяПроцедуры - Строка - Описание
// 	ТекстПроцедуры - Строка - Описание
// 	РазметкаКода - Структура - Описание:
// * Тэги - см. ПолучитьСведенияОТегахВырезки
// * ИмяПроцедуры - Строка -
// * ДобавитьОбластьОписания - Булево -
// * ЭтоМодульМенеджера - Булево -
// * ТэгиЛокализации - Строка -
// * ИмяПроцедуры - Строка -
&НаСервере
Процедура ПоместитьВПроцедуру(ИмяПроцедуры, ТекстПроцедуры, РазметкаКода)
	
	Если РазметкаКода.МодульЛокализации И ИмяПроцедуры = "ПриДобавленииОбработчиковОбновления" Тогда
		ТэгЛокализации = РазметкаКода.Тэги.Описание[РазметкаКода.ТэгиЛокализации]; // см. ОписаниеТэга
		Если ТэгЛокализации <> Неопределено Тогда
			Шаблон = "%1
			|%2
			|%3";
			ТекстПроцедуры = СтрШаблон(Шаблон, ТэгЛокализации.Начало, ТекстПроцедуры, ТэгЛокализации.Конец);
		КонецЕсли;
	КонецЕсли;
	
	ТекстПроцедуры = 
	"Процедура " + ИмяПроцедуры + "(Обработчики) Экспорт
	|
	|"+ ТекстПроцедуры +"
	|
	|КонецПроцедуры";
	
КонецПроцедуры

#КонецОбласти

#Область СведенияОТегахВырезки

// Параметры:
// 	Настройки - Произвольный - Описание
// Возвращаемое значение:
// 	Структура - Описание:
// * ПоОбъектам - ТаблицаЗначений - 
// * ПоБиблиотекам - ТаблицаЗначений -
// * ПоМодулям - Соответствие - 
// * Описание - 
// * РасширениеБиблиотеки - Структура - 
// * БиблиотекиСтрокой - Строка -
// * Порядок - Массив -  	
// * ВложенныхБиблиотек - Соответствие -
// * Вложенность - ДеревоЗначений -:
// ** Тэг - Строка
// * ПоМодулям - Соответствие -
&НаСервере
Функция ПолучитьСведенияОТегахВырезки(Настройки)
	
	СведенияОБиблиотеках = ПолучитьСведенияОБиблиотеках(Настройки);
	
	ОбъектыБиблиотек = СведенияОБиблиотеках.ОбъектыБиблиотек;
	ТэгиБиблиотек = СведенияОБиблиотеках.ТэгиБиблиотек;
	БиблиотекиСтрокой = СведенияОБиблиотеках.БиблиотекиСтрокой;
	РасширениеБиблиотеки = СведенияОБиблиотеках.РасширениеБиблиотеки;
	
	ТэгиОбъектов = ОбъектыБиблиотек.СкопироватьКолонки(); // ТаблицаЗначений - 
	ТипСтрока100 = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100));
	ТэгиОбъектов.Колонки.Добавить("Тэги", ТипСтрока100);
	ОисаниеТэгов = ПолучитьОписаниеИПорядокТэгов(Настройки);
	ПорядокТэгов = Настройки.ОписаниеТэгов["OrderTags"];
	
	ВсеБиблиотеки = СтрРазделить(БиблиотекиСтрокой, ",",Ложь);
	ВсегоКонфигураций = ВсеБиблиотеки.Количество();
	Для Каждого ОбъектБиблиотеки Из ОбъектыБиблиотек Цикл
		Если ОбъектБиблиотеки.ВсегоВхождений = ВсегоКонфигураций Тогда
			Продолжить;
		КонецЕсли;
		
		ИсключитьИзСтрокой = "";
		Для Каждого ИмяБиблиотеки Из ВсеБиблиотеки Цикл
			Расширяемая = РасширениеБиблиотеки[ИмяБиблиотеки];
			Если НЕ ОбъектБиблиотеки[ИмяБиблиотеки] Тогда
				ИсключитьИзСтрокой = ИсключитьИзСтрокой + ?(НЕ ПустаяСтрока(ИсключитьИзСтрокой), ",", "") + ИмяБиблиотеки;
				Если Расширяемая <> Неопределено Тогда
					Если ТипЗнч(Расширяемая) = Тип("Массив") Тогда
						Для Каждого стр Из Расширяемая Цикл
							ИсключитьИзСтрокой = СтрЗаменить(ИсключитьИзСтрокой, стр, ИмяБиблиотеки);
						КонецЦикла;
					Иначе
						ИсключитьИзСтрокой = СтрЗаменить(ИсключитьИзСтрокой, Расширяемая, ИмяБиблиотеки);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ТэгиОбъекта = Новый Массив;
		ИсключитьИзКонфигурации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтрРазделить(ИсключитьИзСтрокой, ",",Ложь));
		Для Каждого ИмяБиблиотеки Из ИсключитьИзКонфигурации Цикл
			Отбор = Новый Структура("Библиотека", ИмяБиблиотеки);
			НайденныеТэги = ТэгиБиблиотек.НайтиСтроки(Отбор);
			Для Каждого Строка Из НайденныеТэги Цикл 
				ТэгиОбъекта.Добавить(Строка.Тэг);
			КонецЦикла;
		КонецЦикла;
		
		УстановитьПорядокТэгов(ТэгиОбъекта, ПорядокТэгов);
		
		НоваяСтрока = ТэгиОбъектов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектБиблиотеки);
		НоваяСтрока.Тэги = СтрСоединить(ТэгиОбъекта, ",");
		
	КонецЦикла;
	
	ТэгиМодулей = Новый Соответствие;
	Для Каждого Описание Из Настройки.ОписаниеТэгов["configurations"] Цикл
		Модуль = Описание.Значение["module"];
		Если Модуль = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТэгиМодуля = Описание.Значение["outTags"];
		Если ТэгиМодуля <> Неопределено Тогда
			ТэгиМодулей.Вставить(Модуль, ТэгиМодуля);
		КонецЕсли;
	КонецЦикла;
	
	Тэги = Новый Структура;
	Тэги.Вставить("ПоОбъектам", ТэгиОбъектов);
	Тэги.Вставить("ПоБиблиотекам", ТэгиБиблиотек);
	Тэги.Вставить("ПоМодулям", ТэгиМодулей);
	Тэги.Вставить("Описание", ОисаниеТэгов);
	Тэги.Вставить("РасширениеБиблиотеки", РасширениеБиблиотеки);
	Тэги.Вставить("БиблиотекиСтрокой", БиблиотекиСтрокой);
	Тэги.Вставить("Порядок", ПорядокТэгов);
	ДеревоТэгов = Новый ДеревоЗначений;
	ДеревоТэгов.Колонки.Добавить("Тэг", ТипСтрока100);
	Тэги.Вставить("Вложенность", ДеревоТэгов);
	
	ВложенныхБиблиотек = Новый Соответствие;
	Для Каждого Расширение Из РасширениеБиблиотеки Цикл
		ТэгиВложеннойБиблиотеки = Новый Массив;
		ДобавитьТэгиВложенныхБиблиотек(ТэгиВложеннойБиблиотеки, Расширение.Ключ, Тэги);
		ВложенныхБиблиотек.Вставить(Расширение.Ключ, ТэгиВложеннойБиблиотеки);
		
		Тэг = ТэгБиблиотеки(Расширение.Значение, ТэгиБиблиотек);
		Если НЕ ПустаяСтрока(Тэг) Тогда
			ВеткаТэга = ДеревоТэгов.Строки.Добавить();
			ВеткаТэга.Тэг = Тэг;
			Тэг = ТэгБиблиотеки(Расширение.Ключ, ТэгиБиблиотек);
			Если НЕ ПустаяСтрока(Тэг) Тогда
				ВложенныйТэг = ВеткаТэга.Строки.Добавить();
				ВложенныйТэг.Тэг = Тэг;
				ДобавитьВложенныеТэги(ВложенныйТэг, Расширение.Ключ, Тэги);
			КонецЕсли;
			Если ВеткаТэга.Строки.Количество() = 0 Тогда
				ДеревоТэгов.Строки.Удалить(ВеткаТэга);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Тэги.Вставить("ВложенныхБиблиотек", ВложенныхБиблиотек);
	
	Возврат Тэги;
	
КонецФункции

&НаСервере
Процедура ДобавитьВложенныеТэги(ВеткаТэга, ИмяБиблиотеки, ОписаниеТэгов)
	
	БиблиотекаВхождения = Неопределено;
	Для Каждого Расширение Из ОписаниеТэгов.РасширениеБиблиотеки Цикл
		Если ИмяБиблиотеки = Расширение.Значение Тогда
			БиблиотекаВхождения = Расширение.Ключ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если БиблиотекаВхождения <> Неопределено Тогда
		Тэг = ТэгБиблиотеки(БиблиотекаВхождения, ОписаниеТэгов.ПоБиблиотекам);
		Если НЕ ПустаяСтрока(Тэг) Тогда
			ВложенныйТэг = ВеткаТэга.Строки.Добавить();
			ВложенныйТэг.Тэг = Тэг;
			ДобавитьВложенныеТэги(ВложенныйТэг, БиблиотекаВхождения, ОписаниеТэгов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТэгБиблиотеки(Библиотека, ТэгиБиблиотек)
	
	Результат = "";
	НайденнаяСтрока = ТэгиБиблиотек.Найти(Библиотека, "Библиотека");
	Если НайденнаяСтрока <> Неопределено Тогда
		Результат = НайденнаяСтрока.Тэг;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьПорядокТэгов(Тэги, ПорядокТэгов)
	
	УпорядоченныйРезультат = Новый Массив;
	Для Каждого ТэгПоПорядку Из ПорядокТэгов Цикл
		
		Если Тэги.Найти(ТэгПоПорядку) <> Неопределено Тогда
			УпорядоченныйРезультат.Добавить(ТэгПоПорядку);
		КонецЕсли;
		
	КонецЦикла;
	
	Тэги = УпорядоченныйРезультат;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТэгиВложенныхБиблиотек(Тэги, ИмяБиблиотеки, ОписаниеТэгов)
	
	ВложеннаяБиблиотека = ОписаниеТэгов.РасширениеБиблиотеки[ИмяБиблиотеки];
	Если ВложеннаяБиблиотека <> Неопределено Тогда
		Отбор = Новый Структура("Библиотека", ВложеннаяБиблиотека);
		НайденныеСтроки = ОписаниеТэгов.ПоБиблиотекам.НайтиСтроки(Отбор);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Тэги.Добавить(Строка.Тэг);
		КонецЦикла;
		ДобавитьТэгиВложенныхБиблиотек(Тэги, ВложеннаяБиблиотека, ОписаниеТэгов);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Настройки - Произвольный - Описание
// Возвращаемое значение:
// 	КлючИЗначение - Описание:
//	 * Ключ - Строка - Наименование тэга
//	 * Значение - см. ОписаниеТэга
&НаСервере
Функция ПолучитьОписаниеИПорядокТэгов(Настройки)
	
	Тэги = Новый Соответствие;
	Если Настройки.ОписаниеТэгов["tags"] <> Неопределено Тогда
		ПорядокТэгов = Новый Массив;
		Для Каждого Описание Из Настройки.ОписаниеТэгов["tags"] Цикл
			
			ГраницыТэга = ОписаниеТэга();
			ГраницыТэга.Начало = Описание["begin"];
			ГраницыТэга.Конец = Описание["end"];
			Тэги.Вставить(Описание["id"], ГраницыТэга);
			ПорядокТэгов.Добавить(Описание["id"]);
			
		КонецЦикла;
		Настройки.ОписаниеТэгов.Вставить("OrderTags", ПорядокТэгов);
	КонецЕсли;
	
	Возврат Тэги;
	
КонецФункции

// Возвращаемое значение:
// 	Структура - Описание:
// * Начало - Строка - 
// * Конец - Строка - 
&НаСервере
Функция ОписаниеТэга()
	
	Возврат Новый Структура("Начало, Конец");
	
КонецФункции

&НаСервере
Функция ПолучитьСведенияОБиблиотеках(Настройки)
	
	ТэгиБиблиотек = Новый ТаблицаЗначений;
	ТэгиБиблиотек.Колонки.Добавить("Библиотека", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТэгиБиблиотек.Колонки.Добавить("Тэг", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипСтрока300 = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(300));
	
	ОбъектыБиблиотек = Новый ТаблицаЗначений;
	ОбъектыБиблиотек.Колонки.Добавить("Имя", ТипСтрока300);
	ОбъектыБиблиотек.Колонки.Добавить("ВсегоВхождений", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0)));
	Для Каждого Имя Из Настройки.ВсеОбъекты Цикл
		НоваяСтрока = ОбъектыБиблиотек.Добавить();
		НоваяСтрока.Имя = Имя;
	КонецЦикла;
	
	БиблиотекиСтрокой = "";
	РасширениеБиблиотеки = Новый Соответствие;
	Если Настройки.ОписаниеТэгов["configurations"] <> Неопределено Тогда
		Для Каждого Библиотека Из Настройки.ОписаниеТэгов["configurations"] Цикл
			
			ВложеннаяБиблиотека = Библиотека.Значение["extends"];
			Если ВложеннаяБиблиотека <> Неопределено Тогда
				РасширениеБиблиотеки.Вставить(Библиотека.Ключ, ВложеннаяБиблиотека);
			КонецЕсли;
			
			Содержимое = Библиотека.Значение["content"];
			Если НЕ ЗначениеЗаполнено(Содержимое) Тогда
				Продолжить;
			КонецЕсли;
			
			ТэгиВырезки = Библиотека.Значение["outTags"];
			Если ТэгиВырезки = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяМодуля = Библиотека.Значение["module"];
			Если НЕ ЗначениеЗаполнено(ИмяМодуля) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ОбъектыБиблиотек.Колонки.Найти(Библиотека.Ключ) = Неопределено Тогда
				ОбъектыБиблиотек.Колонки.Добавить(Библиотека.Ключ, ТипБулево);
				БиблиотекиСтрокой = БиблиотекиСтрокой + ?(БиблиотекиСтрокой <> "", ",","") + Библиотека.Ключ;
			КонецЕсли;
			
			Для Каждого Тэг Из ТэгиВырезки Цикл
				НовыйТэг = ТэгиБиблиотек.Добавить();
				НовыйТэг.Библиотека = Библиотека.Ключ;
				НовыйТэг.Тэг = Тэг;
			КонецЦикла;
			Объекты = Библиотека.Значение["Объекты"];
			Для Каждого ИмяОбъекта Из Объекты Цикл
				НоваяСтрока = ОбъектыБиблиотек.Добавить();
				НоваяСтрока.Имя = ИмяОбъекта.Ключ;
				НоваяСтрока.ВсегоВхождений = 1;
				НоваяСтрока[Библиотека.Ключ] = Истина;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	ОбъектыБиблиотек.Свернуть("Имя", "ВсегоВхождений," + БиблиотекиСтрокой);
	ОбъектыБиблиотек.Сортировать("Имя, ВсегоВхождений УБЫВ");
	ОбъектыБиблиотек.Индексы.Добавить("Имя");
	
	ТэгиБиблиотек.Сортировать("Библиотека, Тэг");
	ТэгиБиблиотек.Индексы.Добавить("Библиотека");
	
	Результат = Новый Структура;
	Результат.Вставить("ОбъектыБиблиотек", ОбъектыБиблиотек);
	Результат.Вставить("ТэгиБиблиотек", ТэгиБиблиотек);
	Результат.Вставить("БиблиотекиСтрокой", БиблиотекиСтрокой);
	Результат.Вставить("РасширениеБиблиотеки", РасширениеБиблиотеки);
	
	Возврат Результат
	
КонецФункции

&НаСервере
Процедура ОпределитьТэгиДляОбъектовНеВходящихВКонфигурацию(ОписаниеТэгов)
	
	ТэгиРасширители = ТэгиРасширители(ОписаниеТэгов);
	Для Каждого Описание Из ОписаниеТэгов["configurations"] Цикл
		Описание.Значение.Вставить("outTags", Новый Массив);
		ТэгиВырезки = Описание.Значение["outTags"]; // Массив - 
		
		Тэги = Описание.Значение["Tags"];
		Если Тэги <> Неопределено Тогда
			Расширители = ТэгиРасширители[Описание.Ключ];
			Если Расширители = Неопределено Тогда
				Расширители = Новый Массив; 
			КонецЕсли;
			Для Каждого Тэг Из Тэги Цикл
				Индекс = Расширители.Найти(Тэг);
				Если Индекс = Неопределено Тогда
					ТэгиВырезки.Добавить(Тэг);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТэгиРасширители(ОписаниеТэгов)
	
	ТэгиРасширители = Новый Соответствие;
	Для Каждого Описание Из ОписаниеТэгов["configurations"] Цикл
		РасширяемаяКонфигурация = Описание.Значение["extends"];
		Если РасширяемаяКонфигурация <> Неопределено Тогда
			Тэги = Описание.Значение["Tags"];
			Если Тэги <> Неопределено Тогда
				Конфигурация = ТэгиРасширители[РасширяемаяКонфигурация];
				Если Конфигурация = Неопределено Тогда
					ТэгиРасширители.Вставить(РасширяемаяКонфигурация, Тэги);
				Иначе
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Конфигурация, Тэги, Истина);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТэгиРасширители;
	
КонецФункции

#КонецОбласти

#Область СоставКонфигурации

&НаКлиенте
Функция ПолучитьВсеОбъектыИсходнойКонфигурации()
	
	ИмяФайла = Объект.КаталогSRC + "Configuration\Configuration.mdo";
	ВсеОбъекты = Новый Массив;
	
	ФайлПодсистемы = Новый Файл(ИмяФайла);
	Если НЕ ФайлПодсистемы.Существует() Тогда
		Возврат ВсеОбъекты;
	КонецЕсли;
	
	ИмяКлассаRU = РусскиеИменаКлассов();
	
	Тэги = Новый Массив;
	Тэги.Добавить("accountingRegisters");
	Тэги.Добавить("accumulationRegisters");
	Тэги.Добавить("businessProcesses");
	Тэги.Добавить("calculationRegisters");
	Тэги.Добавить("catalogs");
	Тэги.Добавить("chartsOfAccounts");
	Тэги.Добавить("chartsOfCalculationTypes");
	Тэги.Добавить("chartsOfCharacteristicTypes");
	Тэги.Добавить("constants");
	Тэги.Добавить("commonModules");
	Тэги.Добавить("documents");
	Тэги.Добавить("informationRegisters");
	Тэги.Добавить("reports");
	Тэги.Добавить("tasks");
	
	#Если НЕ ВебКлиент Тогда
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла, КодировкаТекста.UTF8);
	
	СтрокаФайла = Текст.ПрочитатьСтроку();
	Пока СтрокаФайла <> Неопределено Цикл
		
		СтрокаФайла = Текст.ПрочитатьСтроку();
		Тэг = НайтиТэг(СтрокаФайла, Тэги);
		Если Тэг <> Неопределено Тогда
			
			СтрокаФайла = СтрЗаменить(СтрокаФайла, "<"+Тэг+">", "");
			ИмяОбъекта = СокрЛП(СтрЗаменить(СтрокаФайла, "</"+Тэг+">", ""));
			
			ЧастиИмени = СтрРазделить(ИмяОбъекта, ".");
			ЧастиИмени[0] = ИмяКлассаRU[ЧастиИмени[0]];
			
			ВсеОбъекты.Добавить(СтрСоединить(ЧастиИмени, "."));
			
		КонецЕсли;
		
	КонецЦикла;
	#КонецЕсли
	
	Возврат ВсеОбъекты;
	
КонецФункции

&НаКлиенте
Функция НайтиТэг(СтрокаФайла, Тэги)
	
	Для Каждого Тэг Из Тэги Цикл
		Если СтрНайти(СтрокаФайла, Тэг) > 0 Тогда
			 Возврат Тэг;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СоставПодсистемы
&НаКлиенте
Процедура ПолучитьОбъектыПодсистем(ОписаниеКонфигураций)
	
	РусскиеИменаКлассов = РусскиеИменаКлассов();
	Для Каждого Описание Из ОписаниеКонфигураций Цикл
		ОбъектыПодсистемы = Новый Соответствие;
		Подсистемы = Описание.Значение["content"];
		Если ЗначениеЗаполнено(Подсистемы) Тогда
			Для Каждого Подсистема Из Подсистемы Цикл
				ДобавитьОбъектыПодсистемы(ОбъектыПодсистемы, Подсистема, РусскиеИменаКлассов);
			КонецЦикла;
		КонецЕсли;
		ПодсистемыИсключения = Описание.Значение["except_content"];
		Если ЗначениеЗаполнено(ПодсистемыИсключения) Тогда
			ОбъектыИсключения = Новый Соответствие;
			Для Каждого Подсистема Из ПодсистемыИсключения Цикл
				ДобавитьОбъектыПодсистемы(ОбъектыИсключения, Подсистема, РусскиеИменаКлассов);
			КонецЦикла;
			Для Каждого ОбъектИсключения Из ОбъектыИсключения Цикл
				ОбъектыПодсистемы.Удалить(ОбъектИсключения.Ключ);
			КонецЦикла;
		КонецЕсли;
		
		Описание.Значение.Вставить("Объекты", ОбъектыПодсистемы);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОбъектыПодсистемы(ОбъектыКонфигурации, Подсистема, РусскиеИменаКлассов)
	
	ЧастиИмени = СтрРазделить(Подсистема, ".");
	ЧастьПути = "Subsystems\" + СтрСоединить(ЧастиИмени, "\Subsystems\");
	ИмяФайла = Объект.КаталогSRC + ЧастьПути + "\" + ЧастиИмени[ЧастиИмени.ВГраница()] + ".mdo";
	ФайлПодсистемы = Новый Файл(ИмяФайла);
	
	Если НЕ ФайлПодсистемы.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	#Если НЕ ВебКлиент Тогда
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла, КодировкаТекста.UTF8);
	
	СтрокаФайла = Текст.ПрочитатьСтроку();
	Пока СтрокаФайла <> Неопределено Цикл
		
		Если СтрНайти(СтрокаФайла, "<content>") > 0 Тогда
			СтрокаФайла = СокрЛП(СтрЗаменить(СтрокаФайла, "<content>", ""));
			СтрокаФайла = СокрЛП(СтрЗаменить(СтрокаФайла, "</content>", ""));
			ЧастиИмени = СтрРазделить(СтрокаФайла, ".");
			ЧастиИмени[0] = РусскиеИменаКлассов[ЧастиИмени[0]];
			Если ЧастиИмени[0] <> Неопределено Тогда
				ИмяОбъекта = СтрСоединить(ЧастиИмени, ".");
				ОбъектыКонфигурации.Вставить(ИмяОбъекта, ИмяОбъекта);
			КонецЕсли;
			
		ИначеЕсли СтрНайти(СтрокаФайла, "<subsystems>") > 0 Тогда
			СтрокаФайла = СокрЛП(СтрЗаменить(СтрокаФайла, "<subsystems>", ""));
			СтрокаФайла = СокрЛП(СтрЗаменить(СтрокаФайла, "</subsystems>", ""));
			ЧастиИмени = Новый Массив;
			ЧастиИмени.Добавить(Подсистема);
			ЧастиИмени.Добавить(СтрокаФайла);
			ИмяВложеннойПодсистемы = СтрСоединить(ЧастиИмени, ".");
			ДобавитьОбъектыПодсистемы(ОбъектыКонфигурации, ИмяВложеннойПодсистемы, РусскиеИменаКлассов);
		КонецЕсли;
		
		СтрокаФайла = Текст.ПрочитатьСтроку();
		
	КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
// Возвращает соответствие английских имен русским для обрабатываемых классов объектов метаданых.
Функция РусскиеИменаКлассов()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("AccountingRegister", "РегистрБухгалтерии");
	Результат.Вставить("AccumulationRegister", "РегистрНакопления");
	Результат.Вставить("BusinessProcess", "БизнесПроцесс");
	Результат.Вставить("CalculationRegister", "РегистрРасчета");
	Результат.Вставить("Catalog", "Справочник");
	Результат.Вставить("ChartOfAccounts", "ПланСчетов");
	Результат.Вставить("ChartOfCalculationTypes", "ПланВидовРасчета");
	Результат.Вставить("ChartOfCharacteristicTypes", "ПланВидовХарактеристик");
	Результат.Вставить("Constant", "Константа");
	Результат.Вставить("CommonModule", "ОбщийМодуль");// указываются в приоритетах дргуих обработчиков
	Результат.Вставить("Document", "Документ");
	Результат.Вставить("ExchangePlan", "ПланОбмена");
	Результат.Вставить("InformationRegister", "РегистрСведений");
	Результат.Вставить("Task", "Задача");
	Результат.Вставить("Sequence", "Последовательность");
	Результат.Вставить("Report", "Отчет");// указываются в блокируемых объектах
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЧтениеYAML
&НаКлиенте
Функция ПрочитатьФайлYaml(ИмяФайла)
	
	Результат = Новый Соответствие;
	
	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Шаблон = НСтр("ru='Не обнаружен файл настроек ""%1""';uk='Не знайдено файл настройок ""%1""'");
		ВызватьИсключение СтрШаблон(Шаблон, ИмяФайла);
	КонецЕсли;
	
	#Если НЕ ВебКлиент Тогда
	ТекстФайла = Новый ТекстовыйДокумент;
	ТекстФайла.Прочитать(ИмяФайла, КодировкаТекста.UTF8);
	Попытка
		
		ПрочитатьСтрокиYAML(Результат, ТекстФайла);
		
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = Символы.ПС + СтрШаблон(НСтр("ru='Не удалось прочитать файл %1.';uk='Не вдалося прочитати файл %1.'"), ИмяФайла);
		ТекстСообщения = ТекстСообщения  + Символы.ПС + НСтр("ru='По причине';uk='З причини'")+":" + Символы.ПС;
		ТекстСообщения = ТекстСообщения  + ТекстОшибки;
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПрочитатьСтрокиYAML(Результат, ТекстФайла, НомерСтроки = 0, Уровень = 0, ЧтениеМногострочнойСтроки = Ложь)
	
	Попытка
		ВсегоСтрок = ТекстФайла.КоличествоСтрок();
		Пока НомерСтроки <= ВсегоСтрок Цикл
			
			НомерСтроки = НомерСтроки + 1;
			СтрокаФайла = ТекстФайла.ПолучитьСтроку(НомерСтроки);
			Если СокрЛП(СтрокаФайла) = "" И НЕ ЧтениеМногострочнойСтроки ИЛИ Лев(СокрЛП(СтрокаФайла), 1) = "#" Тогда
				Продолжить;
			КонецЕсли;
			
			Параметр = ЗначениеИзСтрокиYaml(СтрокаФайла);
			Если Параметр.Уровень < Уровень Тогда
				НомерСтроки = НомерСтроки - 1;
				Прервать;
			КонецЕсли;
			
			Если Параметр.ЭлементМассива Тогда
				Если ТипЗнч(Результат) <> Тип("Массив") Тогда
					Результат = Новый Массив;
				КонецЕсли;
				Если ПустаяСтрока(Параметр.Ключ) Тогда
					ДобавитьВРезультатYAML(Результат, Параметр);
				Иначе
					Результат.Добавить(Новый Соответствие);
					ДобавитьВРезультатYAML(Результат[Результат.ВГраница()], Параметр);
					ПрочитатьСтрокиYAML(Результат[Результат.ВГраница()], ТекстФайла, НомерСтроки, Параметр.Уровень+1);
				КонецЕсли;
				
			ИначеЕсли Параметр.Значение = "|" Тогда
				Параметр.Значение = "";
				ПрочитатьСтрокиYAML(Параметр.Значение, ТекстФайла, НомерСтроки, Параметр.Уровень+1, Истина);
				ДобавитьВРезультатYAML(Результат, Параметр);
				
			ИначеЕсли НЕ ЗначениеЗаполнено(Параметр.Значение) Тогда
				
				ПрочитатьСтрокиYAML(Параметр.Значение, ТекстФайла, НомерСтроки, Параметр.Уровень+1);
				ДобавитьВРезультатYAML(Результат, Параметр);
				
			Иначе
				ДобавитьВРезультатYAML(Результат, Параметр);
				
			КонецЕсли;
			
		КонецЦикла;
	
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = Символы.ПС + СтрШаблон(НСтр("ru='Не удалось разобрать строку: %1';uk='Не вдалося розібрати рядок: %1'"), СтрокаФайла);
		ТекстСообщения = ТекстСообщения  + Символы.ПС + НСтр("ru='По причине';uk='З причини'")+":" + Символы.ПС;
		ТекстСообщения = ТекстСообщения  + ТекстОшибки;
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВРезультатYAML(Результат, Параметр)
	
	Если ЗначениеЗаполнено(Параметр.Ключ) И ТипЗнч(Результат) <> Тип("Соответствие") Тогда
		Результат = Новый Соответствие;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Соответствие") Тогда
		Результат.Вставить(Параметр.Ключ, Параметр.Значение);
		
	ИначеЕсли ТипЗнч(Результат) = Тип("Массив") Тогда
		Результат.Добавить(Параметр.Значение);
		
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		Результат = Результат + Параметр.Значение + Символы.ПС;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗначениеИзСтрокиYaml(Знач СтрокаYaml)
	
	Ключ = "";
	Значение = Неопределено;
	
	СтрокаБезОтступа = СокрЛ(СтрокаYaml);
	Отступ = Найти(СтрокаYaml,СтрокаБезОтступа)-1;
	Уровень = Отступ;
	ЭлементМассива = Ложь;
	
	СтрокаYaml = СокрЛП(СтрокаYaml);
	Если Лев(СтрокаYaml,1) = "-" Тогда
		ЭлементМассива = Истина;
		Уровень = Уровень + 1;
		СтрокаYaml = СокрЛП(Прав(СтрокаYaml, СтрДлина(СтрокаYaml)-1));
	КонецЕсли;
	
	Двреточие = СтрНайти(СтрокаYaml,":");
	Если Двреточие <> 0 И Сред(СтрокаYaml, Двреточие, 2) <> ":\" И Сред(СтрокаYaml, Двреточие, 2) <> ":/" Тогда
		Ключ     = СокрЛП(Лев(СтрокаYaml,  Двреточие - 1));
		Значение = СокрЛП(Сред(СтрокаYaml, Двреточие + 1));
		
	Иначе
		Значение = СтрокаYaml;
		
	КонецЕсли;
	
	Решетка = СтрНайти(Значение, "#");
	Если Решетка <> 0 Тогда
		Значение = СокрЛП(Лев(Значение,  Решетка - 1));
	КонецЕсли;
	
	Результат = Новый Структура("Ключ,Значение,Уровень", Ключ,Значение,Уровень);
	Результат.Вставить("ЭлементМассива", ЭлементМассива);
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#КонецОбласти

#Область ТестПостроенияОчереди

&НаКлиенте
Процедура ЗаписатьОшибку(ИмяОбъекта, ОписаниеОшибки = "",
                         ИнформацияОбОшибке = Неопределено, Ключ = Неопределено)
	 
	Ошибка = СоздатьОписаниеОшибки();
	Ошибка.ТипОшибки              = НСтр("ru='Ошибка построения очереди отложенных обработчиков обновления';uk='Помилка побудови черги відкладених обробників оновлення'");
	Ошибка.ПорядокВоспроизведения = ОписаниеОшибки;
	Ошибка.ОбъектМетаданных       = ИмяОбъекта;
	//Ошибка.УточнениеРасположения  = ОписаниеОшибки;
	
	Если ИнформацияОбОшибке <> Неопределено Тогда
		Ошибка.ИсходнаяИнформация   = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДополнениеТекстаОшибки) Тогда
		Ошибка.ПорядокВоспроизведения = Ошибка.ПорядокВоспроизведения + Символы.ПС + ДополнениеТекстаОшибки;
	КонецЕсли; 
	Если ЗначениеЗаполнено(ДатаОбнаруженияОшибки) Тогда
		Ошибка.ДатаОбнаружения = ДатаОбнаруженияОшибки;
	КонецЕсли; 
	Если ЗначениеЗаполнено(АдресРепозитория) Тогда
		Ошибка.АдресРепозитория = АдресРепозитория;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КаталогФайловОшибок) Тогда
		
		Файл = Новый Файл(КаталогФайловОшибок);
		Если НЕ Файл.Существует() Тогда
			СоздатьКаталог(КаталогФайловОшибок);
		ИначеЕсли НЕ Файл.ЭтоКаталог() Тогда
			УдалитьФайлы(КаталогФайловОшибок);
			СоздатьКаталог(КаталогФайловОшибок);
		КонецЕсли; 
		
		ИмяФайлаОшибки = ДобавитьРазделительПутиВКонец(КаталогФайловОшибок);
		ИмяФайлаОшибки = ИмяФайлаОшибки + Формат(ТекущаяДата(), "ДФ=yyyyMMddHHmmss") 
		               + "_" + Строка(Новый УникальныйИдентификатор())+".xml";
		
		Результат = Новый ТекстовыйДокумент;
		#Если НЕ ВебКлиент Тогда
		Результат.УстановитьТекст(ТекстОшибкиXML(Ошибка));
		#КонецЕсли
		Результат.Записать(ИмяФайлаОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СоздатьОписаниеОшибки() Экспорт 

	Ошибка = Новый Структура;
	
	Ошибка.Вставить("ВерсияФормата", "1.4");
	Ошибка.Вставить("УИ", Строка(Новый УникальныйИдентификатор));
	Ошибка.Вставить("ТипОшибки", "");
	Ошибка.Вставить("ИсходнаяИнформация", "");
	Ошибка.Вставить("ПорядокВоспроизведения", "");
	Ошибка.Вставить("ОжидаемоеПоведение", "");
	Ошибка.Вставить("Ответственный", "");
	Ошибка.Вставить("ДостоверностьОбнаружения", "Низкая"); // "Высокая" либо "Низкая"
	Ошибка.Вставить("АдресРепозитория", "");
	
	#Если Клиент Или ТолстыйКлиентУправляемоеПриложение Или ТонкийКлиент Или ВебКлиент  Тогда
	
		СисИнфо = Новый СистемнаяИнформация;
		Ошибка.Вставить("ВерсияПлатформы", СисИнфо.ВерсияПриложения);
		Ошибка.Вставить("КлиентОперативнаяПамять", СисИнфо.ОперативнаяПамять);
		Ошибка.Вставить("КлиентВерсияОС", СисИнфо.ВерсияОС);
		Ошибка.Вставить("КлиентПроцессор", СисИнфо.Процессор);
		Ошибка.Вставить("КлиентТипПлатформы", Строка(СисИнфо.ТипПлатформы));
		Ошибка.Вставить("КлиентИнформацияПрограммыПросмотра", СисИнфо.ИнформацияПрограммыПросмотра);
		Ошибка.Вставить("КлиентТекущаяДата", ТекущаяДата());
	
	#КонецЕсли
	
	Ошибка.Вставить("ДатаОбнаружения",   Дата(1,1,1));
	Ошибка.Вставить("ОбъектыМетаданных", Новый Массив); // Возможность добавить массив объектов метаданных
	// Регистрация по одному объекту метаданных
	Ошибка.Вставить("ОбъектМетаданных", "");
	Ошибка.Вставить("УточнениеРасположения", "");
	Ошибка.Вставить("КодСценария", "");
	Ошибка.Вставить("ИмяСценария", "");
	
	Ошибка.Вставить("ИменаФайлов", Новый Массив);
	
	Возврат Ошибка;
	
КонецФункции

#Если НЕ ВебКлиент Тогда
&НаКлиенте
Функция ТекстОшибкиXML(Ошибка) Экспорт
	
	Если ЗначениеЗаполнено(Ошибка.ОбъектМетаданных) ИЛИ ЗначениеЗаполнено(Ошибка.УточнениеРасположения) Тогда
		Ошибка.ОбъектыМетаданных.Добавить(Новый Структура("ОбъектМетаданных, УточнениеРасположения", Ошибка.ОбъектМетаданных, Ошибка.УточнениеРасположения));
	КонецЕсли; 
	
	Ошибка.Удалить("ОбъектМетаданных");
	Ошибка.Удалить("УточнениеРасположения");
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Ошибка);
	
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции
#КонецЕсли

#КонецОбласти

#Область ЗаписьНастроекERP

&НаСервере
Функция ПолучитьТекстыНастроекERP()
	
	Обработка = ОбъектОбработки();
	ТэгиВырезки = Обработка.ПолучитьМакет("cut_tags");
	
	ТекстыНастроек = Новый СписокЗначений;
	ТекстыНастроек.Добавить(ТэгиВырезки.ПолучитьТекст(), "Файл cut_tags.yml сохранить в каталог .settings локальной папки проекта в кодировке UTF-8");
	
	Возврат ТекстыНастроек;
	
КонецФункции

#КонецОбласти

#Область ЕстьЦиклПорядкаВыполнения

&НаСервере
Функция ЕстьЦиклВыполненияОбработчиковНаСервере()
	
	Обработка = ОбъектОбработки();
	ЕстьЦикл = Обработка.ЕстьЦиклВыполненияОбработчиков();
	Возврат ЕстьЦикл;
	
КонецФункции

&НаСервере
Функция ЕстьЦиклПорядкаВыполненияОбработчиков(Знач ЭтотОбработчик, Знач ПриоритетыВыполнения = Неопределено, СообщатьОбОшибках = Истина) Экспорт

	Обработка = ОбъектОбработки();
	ЕстьЦикл = Обработка.ЕстьЦиклПорядкаВыполненияОбработчика(ЭтотОбработчик, ПриоритетыВыполнения, СообщатьОбОшибках);
	Возврат ЕстьЦикл;

КонецФункции

#КонецОбласти

#Область УстановитьНомерСборки

&НаКлиенте
Процедура ЗавершениеВводаНомераСборки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат > 0 Тогда
		Если ДополнительныеПараметры.НомерСборкиДляОбработчиков Тогда
			УстановитьНомерСборкиОбработчикамНаСервере(Результат);
		Иначе
			НовыйНомерСборкиКонфигурации = Результат;
		КонецЕсли;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНомерСборкиОбработчикамНаСервере(НомерСборки)
	
	Обработка = ОбъектОбработки();
	МаксНомерРедакции = 0;
	МаксНомерПодРедакции = 0;
	ОбработчикиОднойБиблиотеки = Истина;
	УстановленаВерсия = Новый Массив;
	ПодсистемаПредыдущего = Неопределено;
	Для Каждого ИдСтроки Из Элементы.ОбработчикиОбновления.ВыделенныеСтроки Цикл
		Обработчик = Объект.ОбработчикиОбновления.НайтиПоИдентификатору(ИдСтроки);
		Если ПодсистемаПредыдущего = Неопределено Тогда
			ПодсистемаПредыдущего = Обработчик.Подсистема;
		КонецЕсли;
		Если СтрЧислоВхождений(Обработчик.Версия, ".") = 3 Тогда
			
			НомераВерсии = СтрРазделить(Обработчик.Версия, ".");
			НомераВерсии[3] = Формат(НомерСборки, "ЧН=0; ЧГ=0");
			Обработчик.Версия = СтрСоединить(НомераВерсии, ".");
			Обработчик.ВерсияЧислом = Обработка.ВерсияЧислом(Обработчик.Версия);
			Обработчик.Изменен = Истина;
			
			УстановленаВерсия.Добавить(Обработчик);
			
			МаксНомерРедакции = Макс(Число(НомераВерсии[1]), МаксНомерРедакции);
			МаксНомерПодРедакции = Макс(Число(НомераВерсии[2]), МаксНомерПодРедакции);
			
			Если ПодсистемаПредыдущего <> Обработчик.Подсистема Тогда
				ОбработчикиОднойБиблиотеки = Ложь;
			КонецЕсли;
			ПодсистемаПредыдущего = Обработчик.Подсистема;
		КонецЕсли;
	КонецЦикла;
	
	Если ОбработчикиОднойБиблиотеки Тогда
		Для Каждого Обработчик Из УстановленаВерсия Цикл
			НомераВерсии = СтрРазделить(Обработчик.Версия, ".");
			НомераВерсии[1] = Формат(МаксНомерРедакции, "ЧН=0; ЧГ=0");
			НомераВерсии[2] = Формат(МаксНомерПодРедакции, "ЧН=0; ЧГ=0");
			Обработчик.Версия = СтрСоединить(НомераВерсии, ".");
			Обработчик.ВерсияЧислом = Обработка.ВерсияЧислом(Обработчик.Версия);
			НомераВерсии.Удалить(0);
			Обработчик.РедакцияЧислом = Обработка.ВерсияЧислом(СтрСоединить(НомераВерсии, "."));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ОбъектОбработки()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаКлиенте
Функция КаталогНастроек()
	Возврат СтрЗаменить(Объект.КаталогSRC, "\src\","\.settings\");
КонецФункции

&НаСервере
Процедура ИнициализироватьКонстантыОбработки()
	
	ВерсияКонфигурации = Метаданные.Версия;
	НоваяВерсияКонфигурации = Метаданные.Версия;
	
	Обработка = ОбъектОбработки();
	
	СтатусНетПроблем = Обработка.СтатусНетПроблем;
	СтатусНеобходимАнализ = Обработка.СтатусНеобходимАнализ;
	ИменаБиблиотекERP = Новый ФиксированныйМассив(Обработка.ИменаБиблиотекERP());
	
	МножественноеЧисло = Новый ФиксированноеСоответствие(Обработка.МножественноеЧисло);
	ЕдинственноеЧисло = Новый ФиксированноеСоответствие(Обработка.ЕдинственноеЧисло);
	
	АнглийскиеКлассы = Новый Соответствие;
	АнглийскиеКлассы.Вставить("Константы", "Constants");
	АнглийскиеКлассы.Вставить("Справочники", "Catalogs");
	АнглийскиеКлассы.Вставить("Документы", "Documents");
	АнглийскиеКлассы.Вставить("ЖурналыДокументов", "DocumentJournals");
	АнглийскиеКлассы.Вставить("Обработки", "DataProcessors");
	АнглийскиеКлассы.Вставить("Перечисления", "Enums");
	АнглийскиеКлассы.Вставить("ПланыВидовХарактеристик", "ChartsOfCharacteristicTypes");
	АнглийскиеКлассы.Вставить("ПланыОбменов", "ExchangePlans");
	АнглийскиеКлассы.Вставить("ПланыСчетов", "ChartsOfAccounts");
	АнглийскиеКлассы.Вставить("ПланыВидовРасчета", "ChartsOfCalculationTypes");
	АнглийскиеКлассы.Вставить("РегистрыСведений", "InformationRegisters");
	АнглийскиеКлассы.Вставить("РегистрыНакопления", "AccumulationRegisters");
	АнглийскиеКлассы.Вставить("РегистрыБухгалтерии", "AccountingRegisters");
	АнглийскиеКлассы.Вставить("РегистрыРасчета", "CalculationRegisters");
	АнглийскиеКлассы.Вставить("БизнесПроцессы", "BusinessProcesses");
	АнглийскиеКлассы.Вставить("Задачи", "Tasks");
	
	АнглийскиеИмена =  Новый ФиксированноеСоответствие(АнглийскиеКлассы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПараметрыИзСтроки(Знач СтрокаПараметров) Экспорт
	
	Результат = Новый Структура;
	
	СимволДвойныеКавычки = Символ(34); // (")
	
	МассивПодстрок = СтрРазделить(СтрокаПараметров, ";");
	
	Для Каждого СтрокаПараметра Из МассивПодстрок Цикл
		
		ПозицияПервогоЗнакаРавенства = Найти(СтрокаПараметра, "=");
		
		// Получаем имя параметра
		ИмяПараметра = СокрЛП(Лев(СтрокаПараметра, ПозицияПервогоЗнакаРавенства - 1));
		
		// Получаем значение параметра.
		ЗначениеПараметра = СокрЛП(Сред(СтрокаПараметра, ПозицияПервогоЗнакаРавенства + 1));
		
		Если  Лев(ЗначениеПараметра, 1) = СимволДвойныеКавычки
			И Прав(ЗначениеПараметра, 1) = СимволДвойныеКавычки Тогда
			
			ЗначениеПараметра = Сред(ЗначениеПараметра, 2, СтрДлина(ЗначениеПараметра) - 2);
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ИмяПараметра) Тогда
			
			Результат.Вставить(ИмяПараметра, ЗначениеПараметра);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьРазделительПутиВКонец(Знач Путь)
	
	Если ПустаяСтрока(Путь) Тогда
		Возврат Путь;
	КонецЕсли;
	
	РазделительПути = ПолучитьРазделительПути();
	Если Прав(Путь, 1) <> РазделительПути Тогда
		Путь = Путь + РазделительПути;
	КонецЕсли;
	
	Возврат Путь;
	
КонецФункции

&НаСервере
Процедура ОформитьГруппуНастроек()
	
	ШаблонПредставления = НСтр("ru='Сохранять в %1';uk='Зберігати в %1'");
	Приемник = Объект.КаталогSRC;
	Если НЕ ЗначениеЗаполнено(Объект.КаталогSRC) Тогда
		Приемник = НСтр("ru='<Укажите каталог src репозитория>';uk='<Вкажіть каталог src репозитарія>'");
	КонецЕсли;
	
	ПредставлениеГруппы = СтрШаблон(ШаблонПредставления, Приемник);
	
	Если Элементы.КаталогФайловОшибок.Видимость Тогда
		ШаблонПредставления =  ШаблонПредставления + " " + НСтр("ru='Ошибки: %2';uk='Помилки: %2'");
		КаталогОшибок = КаталогФайловОшибок;
		Если НЕ ЗначениеЗаполнено(КаталогФайловОшибок) Тогда
			КаталогОшибок = НСтр("ru='<Укажите каталог ошибок>';uk='<Вкажіть каталог помилок>'");
		КонецЕсли;
		ПредставлениеГруппы = СтрШаблон(ШаблонПредставления, Приемник, КаталогОшибок);
	КонецЕсли;
	
	Элементы.ГруппаНастройкиЗагрузкиВыгрузки.ЗаголовокСвернутогоОтображения = ПредставлениеГруппы;
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаСоединенияИБ(Знач ПутьИБ) Экспорт 
	
	СтрокаСоединения = "";
	
	Если ЭтоПутьФайловойСистемы(ПутьИБ) Тогда
		
		СтрокаСоединения = " /F ""%Путь%""";
		СтрокаСоединения = СтрЗаменить(СтрокаСоединения, "%Путь%", ПутьИБ);
		
	ИначеЕсли Найти(ПутьИБ, "\") > 0 Тогда
		
		СтрокаСоединения = " /S ""%Путь%""";
		СтрокаСоединения = СтрЗаменить(СтрокаСоединения, "%Путь%", ПутьИБ);
	
	КонецЕсли;
	
	Возврат СтрокаСоединения;
	
КонецФункции

&НаКлиенте
Функция ЭтоПутьФайловойСистемы(Знач Путь) Экспорт 
	
	Если ТипЗнч(Путь) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Путь = СокрЛП(Путь);
	
	Если Найти(Путь, "\\")>0
		ИЛИ Найти(Путь, ":\")>0
		ИЛИ (Найти(Путь, ":/")>0 И Найти(Путь, "://")= 0)
		ИЛИ Лев(Путь, 1) = ПолучитьРазделительПутиКлиента()
		ИЛИ Лев(Путь, 1) = ПолучитьРазделительПути() Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции 

&НаКлиенте
Процедура ДиалогВыбораФайлаЗавершение(ВыбранныеФайлы, ИмяРеквизита) Экспорт

	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() <> 0 Тогда
		Если ИмяРеквизита = "КаталогФайловОшибок" Тогда
			ЭтотОбъект[ИмяРеквизита] = ВыбранныеФайлы[0] + ?(ЗначениеЗаполнено(ВыбранныеФайлы[0]),ПолучитьРазделительПути(),"");
		Иначе
			Объект[ИмяРеквизита] = ВыбранныеФайлы[0] + ?(ЗначениеЗаполнено(ВыбранныеФайлы[0]),ПолучитьРазделительПути(),"");
		КонецЕсли;
		ОформитьГруппуНастроек();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОбъектаПоискаНаСервере(ИмяТабличнойЧасти, ТекстОтбора)
	
	Если ЗначениеЗаполнено(ТекстОтбора) Тогда
		Отбор = ЗаполнитьПолеОтбораПоОбъектуНаСервере(ИмяТабличнойЧасти, ТекстОтбора);
		УстановитьОтборОбработчиков(Отбор);
	Иначе
		ОтключитьОтборОбработчиков("Отбор" + ИмяТабличнойЧасти);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборыОбработчиков()
	
	Если Элементы.ОбработчикиОбновления.ОтборСтрок <> Неопределено Тогда
		Если Элементы.ОбработчикиОбновления.ОтборСтрок.Свойство("ОтборЧитаемыеОбъекты") <> Неопределено Тогда
			ПриИзмененииОбъектаПоискаНаСервере("ЧитаемыеОбъекты", ЧитаемыйОбъект);
		КонецЕсли;
		Если Элементы.ОбработчикиОбновления.ОтборСтрок.Свойство("ОтборИзменяемыеОбъекты") <> Неопределено Тогда
			ПриИзмененииОбъектаПоискаНаСервере("ИзменяемыеОбъекты", ИзменяемыйОбъект);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормы(Обработка)
	
	ЗаполнитьБыстрыеОтборы();
	ОбновитьОтборыОбработчиков();
	Если Обработка.МодулиПодсистем <> Неопределено Тогда
		МодулиПодсистем = Новый ФиксированнаяСтруктура(Обработка.МодулиПодсистем);
		ОбновитьСписокБиблиотек(Обработка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокБиблиотек(Обработка)
	
	СписокПодсистем = Элементы.ТекущаяБиблиотека.СписокВыбора;
	СписокПодсистем.Очистить();
	Для Каждого Библиотека Из Обработка.МодулиПодсистем Цикл
		СписокПодсистем.Добавить(Библиотека.Ключ);
	КонецЦикла;
	СписокПодсистем.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	СписокПодсистем.Вставить(0,"");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборОбработчиков(Отбор)
	
	Если ЗначениеЗаполнено(Элементы.ОбработчикиОбновления.ОтборСтрок) Тогда
		ТекущийОтбор = Новый Структура(Элементы.ОбработчикиОбновления.ОтборСтрок);
		Для Каждого НовыйОтбор Из Отбор Цикл
			ТекущийОтбор.Вставить(НовыйОтбор.Ключ, НовыйОтбор.Значение);
		КонецЦикла;
		Элементы.ОбработчикиОбновления.ОтборСтрок = Новый ФиксированнаяСтруктура(ТекущийОтбор);
	Иначе
		Элементы.ОбработчикиОбновления.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьОтборОбработчиков(ИмяПоля)
	
	Если Элементы.ОбработчикиОбновления.ОтборСтрок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Элементы.ОбработчикиОбновления.ОтборСтрок.Свойство(ИмяПоля) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ИмяПоля, "Отбор") > 0 Тогда
		Отбор = Новый Структура(ИмяПоля, Элементы.ОбработчикиОбновления.ОтборСтрок[ИмяПоля]);
		НайденныеСтроки = Объект.ОбработчикиОбновления.НайтиСтроки(Отбор);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка[ИмяПоля] = "";
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элементы.ОбработчикиОбновления.ОтборСтрок)
		И Элементы.ОбработчикиОбновления.ОтборСтрок.Свойство(ИмяПоля) Тогда
		
		ТекущийОтбор = Новый Структура(Элементы.ОбработчикиОбновления.ОтборСтрок);
		ТекущийОтбор.Удалить(ИмяПоля);
		Если ЗначениеЗаполнено(ТекущийОтбор) Тогда
			Элементы.ОбработчикиОбновления.ОтборСтрок = Новый ФиксированнаяСтруктура(ТекущийОтбор);
		Иначе
			Элементы.ОбработчикиОбновления.ОтборСтрок = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ИмяБыстрогоОтбора) Экспорт
	
	Если ИмяБыстрогоОтбора = "НеобходимАнализ" Тогда
		Отбор = Новый Структура("СтатусПроблемы", СтатусНеобходимАнализ);
		
	ИначеЕсли ИмяБыстрогоОтбора = "Отложенно" Тогда
		Отбор = Новый Структура("РежимВыполнения", "Отложенно");
		
	ИначеЕсли ИмяБыстрогоОтбора = "Монопольно" Тогда
		Отбор = Новый Структура("РежимВыполнения", "Монопольно");
		
	ИначеЕсли ИмяБыстрогоОтбора = "Оперативно" Тогда
		Отбор = Новый Структура("РежимВыполнения", "Оперативно");
		
	ИначеЕсли ИмяБыстрогоОтбора = "Последовательно" Тогда
		Отбор = Новый Структура("РежимВыполненияОтложенныхОбработчиков", "Последовательно");
		
	ИначеЕсли ИмяБыстрогоОтбора = "Параллельно" Тогда
		Отбор = Новый Структура("РежимВыполненияОтложенныхОбработчиков", "Параллельно");
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора,"Начальное заполнение") > 0 Тогда
		Отбор = Новый Структура("НачальноеЗаполнение", Истина);
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора, "Обязательный") > 0 Тогда
		Отбор = Новый Структура("ВыполнятьВГруппеОбязательных", Истина);
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора, "Многопоточный") > 0 Тогда
		Отбор = Новый Структура("Многопоточный", Истина);
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора, "Запись читаемых") > 0 Тогда
		Отбор = Новый Структура("ЗаписьЧитаемых", Истина);
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора, "Повторная запись") > 0 Тогда
		Отбор = Новый Структура("ПовторнаяЗапись", Истина);
		
	ИначеЕсли СтрНайти(ИмяБыстрогоОтбора, "Тех.проект") > 0 Тогда
		Отбор = Новый Структура("ТехническийПроект", Истина);
		
	Иначе
		ОтключитьОтборОбработчиков("СтатусПроблемы");
		ОтключитьОтборОбработчиков("РежимВыполнения");
		ОтключитьОтборОбработчиков("РежимВыполненияОтложенныхОбработчиков");
		ОтключитьОтборОбработчиков("НачальноеЗаполнение");
		ОтключитьОтборОбработчиков("ВыполнятьВГруппеОбязательных");
		ОтключитьОтборОбработчиков("Многопоточный");
		ОтключитьОтборОбработчиков("ЗаписьЧитаемых");
		ОтключитьОтборОбработчиков("ПовторнаяЗапись");
		ОтключитьОтборОбработчиков("ТехническийПроект");
		Возврат;
	КонецЕсли;
	
	УстановитьОтборОбработчиков(Отбор);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПолеОтбораПоОбъектуНаСервере(ИмяТабличнойЧасти, Текст)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&тз КАК Т
	|ГДЕ
	|	Т.ОбъектМетаданных ПОДОБНО &СтрокаПоиска";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("тз", Объект[ИмяТабличнойЧасти].Выгрузить());
	Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Текст + "%");
	Запрос.Выполнить();
	УникальныеСсылки = Запрос.МенеджерВременныхТаблиц.Таблицы["вт"].ПолучитьДанные().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ИмяПоляОтбора = "Отбор" + ИмяТабличнойЧасти;
	Отбор = Новый Структура("Ссылка");
	Для Каждого Ссылка Из УникальныеСсылки Цикл
		Отбор.Ссылка = Ссылка;
		Обработчики = Объект.ОбработчикиОбновления.НайтиСтроки(Отбор);
		Для Каждого Обработчик Из Обработчики Цикл
			Обработчик[ИмяПоляОтбора] = Текст;
		КонецЦикла;
	КонецЦикла;
	
	Отбор = Новый Структура(ИмяПоляОтбора, Текст);
	
	Возврат Отбор;
	
КонецФункции

&НаСервере
Процедура ОчиститьСвязанныеДанныеОбработчика(СсылкаОбработчика)
	
	УдалитьДанныеОбработчика("ОбработчикиОбновления", СсылкаОбработчика);
	УдалитьДанныеОбработчика("ЧитаемыеОбъекты", СсылкаОбработчика);
	УдалитьДанныеОбработчика("ИзменяемыеОбъекты", СсылкаОбработчика);
	УдалитьДанныеОбработчика("БлокируемыеОбъекты", СсылкаОбработчика);
	УдалитьДанныеОбработчика("ПриоритетыВыполнения", СсылкаОбработчика);
	
	ПроверитьКонфликтыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДанныеОбработчика(ИмяТЧ, СсылкаОбработчика)
	
	ТабличнаяЧасть = Объект[ИмяТЧ];
	УдалитьСтрокиТаблицы(ТабличнаяЧасть, Новый Структура("Ссылка", СсылкаОбработчика));
	Если ИмяТЧ = "ПриоритетыВыполнения" Тогда
		УдалитьСтрокиТаблицы(ТабличнаяЧасть, Новый Структура("Обработчик2", СсылкаОбработчика));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиТаблицы(Таблица, Отбор)
	
	НайденныеСтроки = Таблица.НайтиСтроки(Отбор);
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		Таблица.Удалить(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонфликтыНаСервере()
	
	Обработка = ОбъектОбработки();
	ОбновитьСведенияОКонфликтахОбработчиков(Обработка);
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьТаблицыВMXL(Ссылка = Неопределено, КаталогВыгрузкиТЧ = "")
	
	Обработка = ОбъектОбработки().Метаданные();
	Результат = Новый Структура;
	
	Построитель = Новый ПостроительОтчета;
	Для Каждого ТЧ Из Обработка.ТабличныеЧасти Цикл
		
		Если Ссылка <> Неопределено
			И (ТЧ.Имя = "ОбработчикиОбновления"
				ИЛИ ТЧ.Имя = "ЧитаемыеОбъекты"
				ИЛИ ТЧ.Имя = "ИзменяемыеОбъекты"
				ИЛИ ТЧ.Имя = "БлокируемыеОбъекты"
				ИЛИ ТЧ.Имя = "Ошибки") Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеТЧ = Объект[ТЧ.Имя].Выгрузить();
		Если Ссылка <> Неопределено Тогда
			ДанныеТЧ = ДанныеОбработчика(ТЧ.Имя, Ссылка);
		КонецЕсли;
		ТабДок = Новый ТабличныйДокумент;
		Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ДанныеТЧ);
		
		
		Построитель.Вывести(ТабДок);
		
		Область = ТабДок.Область("R1:R3");
		ТабДок.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоВертикали);
		ПервыеДвеКолонки = "R1C1:R" + Формат(ТабДок.ВысотаТаблицы, "ЧГ=0") + "C2";
		Область = ТабДок.Область(ПервыеДвеКолонки);
		ТабДок.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
		
		Область = ТабДок.Область();
		Область.ШиринаКолонки = 20;
		Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Обрезать;
		
		Результат.Вставить(ТЧ.Имя, ТабДок);
		
		Если НЕ ПустаяСтрока(КаталогВыгрузкиТЧ) Тогда
			ИмяФайла = КаталогВыгрузкиТЧ + "\" + ТЧ.Имя + ".mxl";
			ТабДок.Записать(ИмяФайла);
			Результат.Вставить(ТЧ.Имя, ИмяФайла);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ДанныеОбработчика(ИмяТЧ, Ссылка)
	
	Если ИмяТЧ = "ОбработчикиОбновления" Тогда
		ДанныеТЧ = Объект[ИмяТЧ].Выгрузить();
		
	ИначеЕсли ИмяТЧ = "ПриоритетыВыполнения" Тогда
		Отбор = Новый Структура("Ссылка", Ссылка);
		ДанныеТЧ = Объект[ИмяТЧ].Выгрузить(Отбор);
		
	ИначеЕсли ИмяТЧ = "КонфликтыОбработчиков" Тогда
		Отбор = Новый Структура("ОбработчикПисатель", Ссылка);
		ДанныеТЧ = Объект[ИмяТЧ].Выгрузить(Отбор);
		Отбор = Новый Структура("ОбработчикЧитательИлиПисатель2", Ссылка);
		ДанныеТЧ2 = Объект[ИмяТЧ].Выгрузить(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеТЧ2, ДанныеТЧ);
		
	КонецЕсли;
	
	Возврат ДанныеТЧ;
	
КонецФункции

&НаСервере
Процедура РассчитатьОчередьНаСервере()
	
	ИтерацииОбновления = Неопределено; // ОбновлениеИнформационнойБазыСлужебный.ИтерацииОбновления();
	Если ИтерацииОбновления <> Неопределено Тогда
		Обработка = ОбъектОбработки();
		Обработка.ЗаполнитьНомерОчереди(ИтерацииОбновления);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеУстановкиБлокировкиИнтерфейса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьБлокироватьИнтерфейсНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьБлокироватьИнтерфейсНаСервере()
	
	Обработка = ОбъектОбработки();
	Для Каждого Библиотека Из Обработка.ИменаБиблиотекERP() Цикл
		ОтборОбработчиков = Новый Структура("Подсистема", Библиотека);
		ОбработчикиБиблиотеки = Объект.ОбработчикиОбновления.НайтиСтроки(ОтборОбработчиков);
		Для Каждого Обработчик Из ОбработчикиБиблиотеки Цикл
			Имена = СтрРазделить(Обработчик.Процедура,".");
			Если Имена[0] = "РегистрыНакопления" Тогда
				Продолжить;
			КонецЕсли;
			ОтборИзменяемых = Новый Структура("Ссылка", Обработчик.Ссылка);
			Если ПустаяСтрока(Обработчик.ПроцедураПроверки) Тогда
				Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
			КонецЕсли;
			ИзменяемыеОбъекты = Объект.ИзменяемыеОбъекты.НайтиСтроки(ОтборИзменяемых);
			Для Каждого Изменяемый Из ИзменяемыеОбъекты Цикл
				Изменяемый.БлокироватьИнтерфейс = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти