#Область ОписаниеПеременных

&НаКлиенте
Перем ФормаДлительнойОперации;
&НаКлиенте
Перем ПараметрыОжидания;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И ТипЗнч(Параметры.Отбор) = Тип("Структура")
		И Параметры.Отбор.Свойство("Регистратор") И ЗначениеЗаполнено(Параметры.Отбор.Регистратор) Тогда
		Документ = Параметры.Отбор.Регистратор;
		ОбновитьДанныеРеквизитовДокумента();
		УстановитьНастройкиПолейНУ();
	Иначе
		ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено!';uk='Безпосереднє відкриття цієї форми не передбачено!'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ОписаниеТиповВалюта        = Новый ОписаниеТипов("СправочникСсылка.Валюты");
	ОписаниеТиповКоличество    = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3));
	ОписаниеТиповПодразделение = БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения();
	ОписаниеТиповНалоговыеНазначения = Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат");
	ОписаниеТиповСумма         = ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля();
	УведомлятьОПустомКомментарии = Истина;
	
	ВалютаРУ = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУУ = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаФО = Константы.ВалютаФинОтчетности.Получить();
	ИсточникСуммыДляПересчетаВВалютуФинОтчетности = Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить();
	ПравоИзмененияПроводок = ПравоДоступа("Изменение", Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	
	ДокументПроводится = (Документ.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить);
	
	ОбновитьДанныеОВведенныхКорректировках();
	
	УстановитьДоступностьСубконто();
	
	НастроитьОтображениеТаблицыПроводок();
	
	СтатусПроверкиДокумента = СтатусПроверкиДокумента(Документ);
	ПолучитьСостояниеОтраженияДокумента();
	ПредставлениеДокумента = Строка(Документ) + ПолучитьСостояниеПроверкиДокумента(СтатусПроверкиДокумента);
	
	УстановитьВидКоманднойПанелиНабораЗаписей();
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	ПовторноеОткрытиеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.АктуализацияПроводок.Пометка = АктуализацияПроводок;
	Если АктуализацияПроводок
		И (СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете")
		ИЛИ СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета"))
		И РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		
			ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьПредставлениеВидовСубконто();
	УстановитьДоступностьСубконто();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Проверки перед записью:
	//	- Если автоматически отражаем, спрашиваем о продолжении записи с тем, что проводки будут сформированы автоматически;
	//	- Если отражаем вручную, задаем дополнительный вопрос только в том случае, когда не указан комментарий ручного отражения.
	
	ЗадаватьВопросПередЗаписью = Не Объект.РучнаяКорректировкаПроводок
		ИЛИ Объект.РучнаяКорректировкаПроводок И Не ЗначениеЗаполнено(Комментарий) И УведомлятьОПустомКомментарии;
	
	Если ПараметрыЗаписи.Свойство("ЗадаватьВопросПередЗаписью") Тогда
		ЗадаватьВопросПередЗаписью = ПараметрыЗаписи.ЗадаватьВопросПередЗаписью;
	КонецЕсли;
	
	Если ЗадаватьВопросПередЗаписью Тогда
		Отказ = Истина;
		ПараметрыЗаписи.Вставить("ЗадаватьВопросПередЗаписью", Ложь);
		Если Объект.РучнаяКорректировкаПроводок Тогда
			ПоказатьФормуКомментария(ПараметрыЗаписи);
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередЗаписью", ЭтотОбъект, ПараметрыЗаписи);
			ТекстВопроса = НСтр("ru='Проводки будут сформированы автоматически. Продолжить?';uk='Проводки будуть сформовані автоматично. Продовжити?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписыватьПустойНабор", Истина);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("РучноеОтражение", Объект.РучнаяКорректировкаПроводок);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПроверкаИзмененияРегистра", Объект.РучнаяКорректировкаПроводок);
	
	Если Объект.РучнаяКорректировкаПроводок Тогда
		
		СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную;
		
	Иначе
				
		ПараметрыОтражения = ПараметрыОтраженияДокументаВРеглУчете();
		АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ЭтотОбъект.УникальныйИдентификатор);
		РеглУчетПроведениеСервер.ВернутьРезультатОтраженияДокумента(ПараметрыОтражения, АдресРезультата);
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		
		СтатусОтраженияДокумента = Результат.СтатусОтражения;
		Комментарий = Результат.КомментарийОшибки;
		Если СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете Тогда
			ТекущийОбъект.Загрузить(Результат.ТаблицаПроводок);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Отказ Тогда
		ТаблицаОтражения = Неопределено;
		Если СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную Тогда
			ТаблицаОтражения = ТекущийОбъект.Выгрузить(, "Период, Регистратор, Организация");
			ТаблицаОтражения.Свернуть("Период, Регистратор, Организация");
		КонецЕсли;
		РеглУчетПроведениеСервер.ЗарегистрироватьОтражениеДокумента(Документ, СтатусОтраженияДокумента, Комментарий, ТаблицаОтражения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ПолучитьСостояниеОтраженияДокумента();
	ЗаполнитьПредставлениеВидовСубконто();
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ОтражениеДокументовВРегламентированномУчете", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СтатусПроверкиДокумента"
		ИЛИ ИмяСобытия = "Запись_ОтражениеДокументовВРегламентированномУчете" 
		И ((ТипЗнч(Параметр) = Тип("Массив") И Не Параметр.Найти(Документ) = Неопределено)
		ИЛИ ТипЗнч(Параметр) = Тип("ДокументСсылка.ОперацияБух")) Тогда
		ПовторноеОткрытиеСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если АктуализацияПроводок И ПравоИзмененияПроводок
		И (СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете")
		ИЛИ СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета")) Тогда
		РезультатФоновогоЗаданияОтраженияПроводок = ОтразитьДокументВРеглУчетеСервер();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РучнаяКорректировкаПроводокПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если Объект.РучнаяКорректировкаПроводок Тогда
		
		Если НаборЗаписей.Количество() = 0 Тогда
			ТекстВопроса = НСтр("ru='Заполнить проводки автоматически?';uk='Заповнити проводки автоматично?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок", ЭтотОбъект, Новый Структура);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
		УстановитьДоступностьЭлементовФормы("РучнаяКорректировкаПроводок");
		
	Иначе
		  
		ТекстВопроса = НСтр("ru='Документ будет отражен в учете автоматически. Продолжить?';uk='Документ буде відображений в обліку автоматично. Продовжити?'");  
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияПриСнятииФлагаРучнаяКорректировкаПроводок", ЭтотОбъект, Новый Структура);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДокументаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Документ);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьФормуКомментария();
		
КонецПроцедуры

&НаКлиенте
Процедура ТекстКорректировкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СписокКорректировок.Количество() = 1 Тогда
		ПоказатьЗначение(, СписокКорректировок.Получить(0).Значение);
	Иначе
		ЗаголовокФормы = НСтр("ru='Выберите документ корректировки для просмотра';uk='Виберіть документ коригування для перегляду'");
		ПараметрыФормы = Новый Структура("СписокДокументов, Заголовок", СписокКорректировок, ЗаголовокФормы);
		
		ОткрытьФорму("ОбщаяФорма.ПросмотрСпискаДокументов", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,
		,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаСтатуса" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьРасшифровкуСтатуса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбраннаяОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборПоОрганизации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НаборЗаписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Не Объект.РучнаяКорректировкаПроводок Тогда
		Значение = Элемент.ТекущиеДанные[Поле.Имя];
		Если ЗначениеЗаполнено(Значение) 
			И ТипЗнч(Значение) <> Тип("Число") 
			И ТипЗнч(Значение) <> Тип("Дата")
			И ТипЗнч(Значение) <> Тип("Булево") Тогда
			ПоказатьЗначение(, Значение);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоДт1", "СубконтоДт2", "СубконтоДт3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетДт, ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоКт1", "СубконтоКт2", "СубконтоКт3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетКт, ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Если Элемент.ТекущиеДанные.Свойство("Период") И ЗначениеЗаполнено(ДатаОтражения) Тогда
			Элемент.ТекущиеДанные.Период = ДатаОтражения;
		КонецЕсли;
		Если Элемент.ТекущиеДанные.Свойство("Организация") И ЗначениеЗаполнено(Организация) Тогда
			Элемент.ТекущиеДанные.Организация = Организация;
		КонецЕсли;
		Если Элемент.ТекущиеДанные.Свойство("Регистратор") И ЗначениеЗаполнено(Документ) Тогда
			Элемент.ТекущиеДанные.Регистратор = Документ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура СчетДтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДтПриИзменении(Элемент)
	
	РасчетСуммы("ВалютнаяСуммаДт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;

	Если ЗначениеЗаполнено(ТекущиеДанные.СчетДт) Тогда
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетДт);
		Если СвойстваСчета.Валютный Тогда
			Возврат; // Если оба счета валютные, сумма пересчитывается при изменении счета Дт
		КонецЕсли;
	КонецЕсли;

	РасчетСуммы("ВалютнаяСуммаКт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаДтПриИзменении(Элемент)
	
	РасчетСуммы("ВалютнаяСуммаДт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СчетДт) Тогда
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетДт);
		Если СвойстваСчета.Валютный Тогда
			Возврат; // Если оба счета валютные, сумма пересчитывается при изменении счета Дт
		КонецЕсли;
	КонецЕсли;
	
	РасчетСуммы("ВалютнаяСуммаКт");
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	РасчетСуммы("Сумма");
	
КонецПроцедуры

#Область ОбработчикиСобытийСубконто

&НаКлиенте
Процедура СубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	
	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	РезультатЗаписи = Записать(Новый Структура("ЗакрыватьПриЗаписи", Истина));
	Если РезультатЗаписи И Объект.РучнаяКорректировкаПроводок Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПроводкиАвтоматически(Команда)
	
	// Для документов амортизации не делаем автозаполнение, так как они формируют проводки при проведении и только:
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияНМА") ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияОС") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередПроведениемАмортизации", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Данное действие приведет к перепроведению документа, перепровести документ амортизации?';uk='Дана дія призведе до перепроведення документа, перепровести документ амортизації?'");
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перепровести';uk='Перепровести'"));
		СписокВыбора.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не перепроводить';uk='Не перепроводити'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокВыбора);
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		ДопПараметры = Новый Структура;
		// Некоторые документы при создании проводок по регистру Хозрасчетный опираются на свои же остатки на этом регистре,
		// и добавляют только те проводки которые необходимы. В нашем случае (когда документ не проводится,
		// а проводки соответственно не очищаются) необходимо вручную очищать проводки документа:
		Если МассивДокументовТребующихОчисткиПроводок().Найти(ТипЗнч(Документ)) = Неопределено Тогда
			// Документ не относится к списку документов, для которых необходимо очищать проводки, задаем стандартный вопрос:
			ТекстВопроса = НСтр("ru='Существующие проводки будут удалены. Продолжить?';uk='Існуючі проводки будуть вилучені. Продовжити?'");
		Иначе
			ТекстВопроса = НСтр("ru='Существующие проводки будут удалены без возможности отмены изменений. Продолжить?';uk='Існуючі проводки будуть видалені без можливості скасування змін. Продовжити?'");
			ДопПараметры.Вставить("ОчисткаПроводок");
		КонецЕсли;
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьАктуальностьПроводок(Команда)
	
	ПодтвердитьАктуальностьПроводокСервер();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаПроводок(Команда)
	
	ПараметрыОперации = Новый Структура("Основание", Документ);
	ОткрытьФорму("Документ.ОперацияБух.ФормаОбъекта", ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДокументВРеглУчете(Команда)
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияНМА") ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияОС") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередПроведениемАмортизации", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Данное действие приведет к перепроведению документа, перепровести документ амортизации?';uk='Дана дія призведе до перепроведення документа, перепровести документ амортизації?'");
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перепровести';uk='Перепровести'"));
		СписокВыбора.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не перепроводить';uk='Не перепроводити'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокВыбора);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	РезультатФоновогоЗаданияОтраженияПроводок = ОтразитьДокументВРеглУчетеСервер();
	Если РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);
	ИначеЕсли РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Ошибка" Тогда
		ВызватьИсключение(НСтр("ru='При отражении документа в регламентированном учете произошла ошибка.
|Подробности см. в Журнале регистрации.'
|;uk='При відображенні документа в регламентованому обліку сталася помилка.
|Подробиці див. у Журналі реєстрації.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьКОтражениюВРеглУчете()
    РеглУчетПроведениеСервер.ЗарегистрироватьОтражениеДокумента(Документ, ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете"));
КонецПроцедуры

&НаКлиенте
Процедура ПереОтразитьДокументВРеглУчете(Команда)
    
    ЗарегистрироватьКОтражениюВРеглУчете();
    ОтразитьДокументВРеглУчете(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПовторноеОткрытиеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСчетаУчета(Команда)
	
	ОрганизацииДляНастройки = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокОрганизаций);
	
	Если ОрганизацииДляНастройки.Количество() = 1 Тогда
		ОткрытьФормуНастройкиСчетовУчета(Организация);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораОрганизацииДляНастройкиСчетов", ЭтотОбъект);
		СписокВыбораОрагнизации = Новый СписокЗначений;
		СписокВыбораОрагнизации.ЗагрузитьЗначения(ОрганизацииДляНастройки);
		СписокВыбораОрагнизации.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru='Выберите организацию для настройки';uk='Виберіть організацію для настройки'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализацияПроводок(Команда)
	АктуализацияПроводок = Не АктуализацияПроводок;
	Элементы.АктуализацияПроводок.Пометка = АктуализацияПроводок;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаИзмененияРеквизитов

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, ДтКт = "")
	
	ИдСтроки = Форма.Элементы.НаборЗаписей.ТекущаяСтрока;
	Если ИдСтроки <> Неопределено Тогда
		СтрокаТаблицы = Форма.НаборЗаписей.НайтиПоИдентификатору(ИдСтроки);
		Если ДтКт <> "Кт" Тогда
			ПараметрыДокумента = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(СтрокаТаблицы.Организация, СтрокаТаблицы, "СубконтоДт%Индекс%", "СчетДт");
			НастройкаСчетовУчетаКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СубконтоДт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
		Если ДтКт <> "Дт" Тогда
			ПараметрыДокумента = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(СтрокаТаблицы.Организация, СтрокаТаблицы, "СубконтоКт%Индекс%", "СчетКт");
			НастройкаСчетовУчетаКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СубконтоКт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСчета(ДтКт)

	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПредставлениеВидовСубконто(ТекущиеДанные["Счет"+ДтКт], ДтКт));
		
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто"+ДтКт+"1", "Субконто"+ДтКт+"2", "Субконто"+ДтКт+"3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные["Счет"+ДтКт], ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто"+ДтКт+"1", "Субконто"+ДтКт+"2", "Субконто"+ДтКт+"3");
	ПоляОбъекта.Вставить("Подразделение", "Подразделение"+ДтКт);
	ПоляОбъекта.Вставить("Организация"  , Организация);
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(ТекущиеДанные["Счет"+ДтКт], ТекущиеДанные, ПоляОбъекта, Истина);
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные["Счет"+ДтКт]);
	Если СвойстваСчета.Валютный Тогда
		ТекущиеДанные["Валюта" + ДтКт]        = ОписаниеТиповВалюта.ПривестиЗначение(ТекущиеДанные["Валюта" + ДтКт]);
		ТекущиеДанные["ВалютнаяСумма" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["ВалютнаяСумма" + ДтКт]);
	Иначе
		ТекущиеДанные["Валюта"+ДтКт]        = NULL;
		ТекущиеДанные["ВалютнаяСумма"+ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.УчетПоПодразделениям Тогда
		ТекущиеДанные["Подразделение" + ДтКт] = ОписаниеТиповПодразделение.ПривестиЗначение(ТекущиеДанные["Подразделение" + ДтКт]);
	Иначе
		ТекущиеДанные["Подразделение" + ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.УчетПоНалоговымНазначениямНДС Тогда
		ТекущиеДанные["НалоговоеНазначение" + ДтКт] = ОписаниеТиповНалоговыеНазначения.ПривестиЗначение(ТекущиеДанные["НалоговоеНазначение" + ДтКт]);
	Иначе
		ТекущиеДанные["НалоговоеНазначение" + ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.НалоговыйУчет Тогда
		ТекущиеДанные["СуммаНУ" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["СуммаНУ" + ДтКт]);
	Иначе
		ТекущиеДанные["СуммаНУ" + ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.Количественный Тогда
		ТекущиеДанные["Количество" + ДтКт] = ОписаниеТиповКоличество.ПривестиЗначение(ТекущиеДанные["Количество" + ДтКт]);
	Иначе
		ТекущиеДанные["Количество" + ДтКт] = NULL;
	КонецЕсли;
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, ДтКт);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконто(ДтКт, Элемент, СтандартнаяОбработка)

	ТекущиеДанные      = Элементы.НаборЗаписей.ТекущиеДанные;
	ПараметрыДокумента = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(ТекущиеДанные.Организация, ТекущиеДанные, "Субконто" + ДтКт + "%Индекс%", "Счет" + ДтКт);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПараметрыВыбора(Элемент)
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеВидовСубконто()

	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, ПредставлениеВидовСубконто(Запись.СчетДт, "Дт"));
		ЗаполнитьЗначенияСвойств(Запись, ПредставлениеВидовСубконто(Запись.СчетКт, "Кт"));
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПредставлениеВидовСубконто(Счет, ВидДвижения)

	ПредставлениеВидовСубконто = Новый Структура;
	МаксКоличествоСубконто = Метаданные.ПланыСчетов.Хозрасчетный.МаксКоличествоСубконто;
	КоличествоВидовСубконто = Счет.ВидыСубконто.Количество();
	Для НомерСубконто = 1 По МаксКоличествоСубконто Цикл
		ПредставлениеВидаСубконто = ?(КоличествоВидовСубконто >= НомерСубконто, "<" + Счет.ВидыСубконто[НомерСубконто - 1].ВидСубконто + ">", Неопределено);
		ПредставлениеВидовСубконто.Вставить("ПредставлениеВидСубконто" + ВидДвижения + НомерСубконто, ПредставлениеВидаСубконто);
	КонецЦикла;

	Возврат ПредставлениеВидовСубконто;

КонецФункции

&НаСервере
Процедура УстановитьДоступностьСубконто()
	
	Для каждого Проводка Из НаборЗаписей Цикл
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "СубконтоДт1", "СубконтоДт2", "СубконтоДт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетДт, Проводка, ПоляОбъекта);
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "СубконтоКт1", "СубконтоКт2", "СубконтоКт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетКт, Проводка, ПоляОбъекта);
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ОтборСтрок = Неопределено;
	Если Не Объект.РучнаяКорректировкаПроводок Тогда
		Если ЗначениеЗаполнено(Организация) И СписокОрганизаций.Количество() > 1 Тогда
			ОтборСтрок = Новый ФиксированнаяСтруктура("Организация", Организация);
		КонецЕсли;
		ПолучитьСостояниеОтраженияДокумента();
	КонецЕсли;
	Элементы.НаборЗаписей.ОтборСтрок = ОтборСтрок;
		УстановитьНастройкиПолейНУ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаИзмененияСуммовыхРеквизитов

&НаКлиенте
Процедура РасчетСуммы(Источник)
	
	ТекущиеДанные     = Элементы.НаборЗаписей.ТекущиеДанные;
	СтруктураПроводки = ПреобразоватьДанныеЭлементаФормыВСтруктуру(ТекущиеДанные);
	
	ПересчитатьСуммыПоСтрокеНаСервере(СтруктураПроводки, Источник);
	
	ЗаполнитьЗначенияСвойств(Элементы.НаборЗаписей.ТекущиеДанные, СтруктураПроводки);
	
КонецПроцедуры

&НаКлиенте
Функция ПреобразоватьДанныеЭлементаФормыВСтруктуру(ТекущиеДанные)

	СтруктураПроводки = Новый Структура("НомерСтроки,Период,
		|СчетДт,ПодразделениеДт,СубконтоДт1,СубконтоДт2,СубконтоДт3,
		|НалоговоеНазначениеДт,
		|КоличествоДт,ВалютаДт,ВалютнаяСуммаДт,
		|СчетКт,ПодразделениеКт,СубконтоКт1,СубконтоКт2,СубконтоКт3,
		|НалоговоеНазначениеКт,
		|КоличествоКт,ВалютаКт,ВалютнаяСуммаКт,
		|Сумма,СуммаУУ,СуммаФО,Содержание,СуммаНУДт,СуммаНУКт");
	ЗаполнитьЗначенияСвойств(СтруктураПроводки, ТекущиеДанные);

	Возврат СтруктураПроводки;

КонецФункции

&НаСервере
Процедура ПересчитатьСуммыПоСтрокеНаСервере(Проводка, Знач Источник)
	
	Дата = ?(ЗначениеЗаполнено(Проводка.Период), Проводка.Период, ДатаОтражения);
	
	ПересчитатьФО = Ложь;
	
	Если Источник = "ВалютнаяСуммаДт" Тогда
		Проводка.Сумма   = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаДт, Проводка.ВалютаДт, ВалютаРУ, Дата);
		Проводка.СуммаУУ = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаДт, Проводка.ВалютаДт, ВалютаУУ, Дата);
		Если Проводка.ВалютаДт = ВалютаФО Тогда
			Проводка.СуммаФО = Проводка.ВалютнаяСуммаДт;
		Иначе
			ПересчитатьФО = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Источник = "ВалютнаяСуммаКт" Тогда
		Проводка.Сумма   = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаКт, Проводка.ВалютаКт, ВалютаРУ, Дата);
		Проводка.СуммаУУ = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаКт, Проводка.ВалютаКт, ВалютаУУ, Дата);
		Если Проводка.ВалютаКт = ВалютаФО Тогда
			Проводка.СуммаФО = Проводка.ВалютнаяСуммаКт;
		Иначе
			ПересчитатьФО = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Источник = "Сумма" Тогда
		Проводка.СуммаУУ = ПересчетСуммыПоКурсу(Проводка.Сумма, ВалютаРУ, ВалютаУУ, Дата);
		ПересчитатьФО = Истина;
	КонецЕсли;
	
	Если Источник = "СуммаУУ" 
		И ИсточникСуммыДляПересчетаВВалютуФинОтчетности = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.УУ Тогда
		ПересчитатьФО = Истина;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности")
		И ПересчитатьФО Тогда
		
		Если ИсточникСуммыДляПересчетаВВалютуФинОтчетности = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.УУ Тогда
			Проводка.СуммаФО = ПересчетСуммыПоКурсу(Проводка.СуммаУУ, ВалютаУУ, ВалютаФО, Дата);
		Иначе
			Проводка.СуммаФО = ПересчетСуммыПоКурсу(Проводка.Сумма, ВалютаРУ, ВалютаФО, Дата);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПересчетСуммыПоКурсу(Знач Сумма, Знач Валюта, Знач НоваяВалюта, Знач Дата)
	
	Возврат РаботаСКурсамиВалют.ПересчитатьВВалюту(Сумма, Валюта, НоваяВалюта, Дата);
	
КонецФункции

#КонецОбласти

#Область РаботаСФормой

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
#Область ОтображениеВидаСубконтоДт1
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоДт2
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоДт3
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти
	
#Область ОтображениеВидаСубконтоКт1
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоКт2
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоКт3
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура ПовторноеОткрытиеСервер()

	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроводится = (Документ.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить);
	ДокументПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Проведен");
	
	ОбновитьДанныеРеквизитовДокумента();
	СтатусПроверкиДокумента = СтатусПроверкиДокумента(Документ);
	ПолучитьСостояниеОтраженияДокумента();
	УстановитьДоступностьЭлементовФормы();
	
	Если НЕ ДокументПроведен 
		ИЛИ (ДокументПроведен И НЕ Объект.РучнаяКорректировкаПроводок) Тогда
		
		НаборЗаписей.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.Период,
		|	ХозрасчетныйДвиженияССубконто.Регистратор,
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки, 
		|	ХозрасчетныйДвиженияССубконто.Активность,
		|	ХозрасчетныйДвиженияССубконто.СчетДт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
		|	ХозрасчетныйДвиженияССубконто.СчетКт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
		|	ХозрасчетныйДвиженияССубконто.Организация,
		|	ХозрасчетныйДвиженияССубконто.ВалютаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаКт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт,
		|	ХозрасчетныйДвиженияССубконто.НаправлениеДеятельностиДт,
		|	ХозрасчетныйДвиженияССубконто.НаправлениеДеятельностиКт,
		|	ХозрасчетныйДвиженияССубконто.НалоговоеНазначениеДт,
		|	ХозрасчетныйДвиженияССубконто.НалоговоеНазначениеКт,
		|	ХозрасчетныйДвиженияССубконто.Сумма,
		|	ХозрасчетныйДвиженияССубконто.СуммаУУ,
		|	ХозрасчетныйДвиженияССубконто.СуммаФО,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоДт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУКт,
		|	ХозрасчетныйДвиженияССубконто.Содержание,
		|	ХозрасчетныйДвиженияССубконто.НеКорректироватьСтоимостьАвтоматически
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор = &Документ, , ) КАК ХозрасчетныйДвиженияССубконто";
		
		Запрос.УстановитьПараметр("Документ", Документ);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
		
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);	
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	
	ПредставлениеДокумента = Строка(Документ) + ПолучитьСостояниеПроверкиДокумента(СтатусПроверкиДокумента);
	
	ЗаполнитьПредставлениеВидовСубконто();

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы(ИдентификаторСобытия = Неопределено)
	
	Если ИдентификаторСобытия = "РучнаяКорректировкаПроводок" ИЛИ ИдентификаторСобытия = Неопределено Тогда
		УстановитьОтборПоОрганизации();
		УстановитьДоступностьПриИзмененииРучнаяКорректировкаПроводок();
	КонецЕсли;

	Если ИдентификаторСобытия = Неопределено Тогда
		УстановитьДоступностьПриОткрытииФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПриИзмененииРучнаяКорректировкаПроводок()

	Элементы.ГруппаПроводкиРегламентированногоУчета.ТолькоПросмотр = НЕ (Объект.РучнаяКорректировкаПроводок И Пользователи.РолиДоступны("ПравоРучнойКорректировкиПроводок"));
	
	Элементы.КомментарийРучногоОтражения.Видимость = ЗначениеЗаполнено(Комментарий) ИЛИ Объект.РучнаяКорректировкаПроводок;
	Элементы.КомментарийРучногоОтражения.ЦветТекста = ?(Объект.РучнаяКорректировкаПроводок, ЦветаСтиля.ЦветГиперссылки, ЦветаСтиля.ПоясняющийОшибкуТекст);
	ВидимостьКомментарияАвтоматическогоОтражения = Не Объект.РучнаяКорректировкаПроводок И
		СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета
		И (Не ЗначениеЗаполнено(Организация) Или СписокОрганизаций.НайтиПоЗначению(Организация).Пометка);
	Элементы.ГруппаКомментарийАвтоматическогоОтраженияВУчете.Видимость = ВидимостьКомментарияАвтоматическогоОтражения;
	Элементы.ГруппаКомментарийРучногоОтраженияВУчете.Видимость = Объект.РучнаяКорректировкаПроводок;
	
	УстановитьДоступностьАвтоматическогоОтраженияВРегламентированномУчете();
	
	Элементы.ЗаполнитьПроводкиАвтоматически.Видимость = Объект.РучнаяКорректировкаПроводок;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьАвтоматическогоОтраженияВРегламентированномУчете()

	СтатусыОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	
	Элементы.ФормаГруппаКомандыАвтоматическогоОтражения.Видимость = НЕ Объект.РучнаяКорректировкаПроводок;
	Элементы.ФормаОтразитьДокументВРеглУчете.Доступность = (СтатусОтраженияДокумента <> СтатусыОтражения.ОтраженоВРеглУчете)
		И (Не СтатусОтраженияДокумента.Пустая());
		
	Элементы.ГруппаАктуальность.Видимость = НЕ Объект.РучнаяКорректировкаПроводок
		ИЛИ СтатусОтраженияДокумента = СтатусыОтражения.ОтраженоВУчетеВручную
		ИЛИ СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	
	Элементы.ФормаОтразитьДокументВРеглУчете.КнопкаПоУмолчанию = Не Объект.РучнаяКорректировкаПроводок;
	Элементы.ФормаГруппаКомандыРучногоОтражения.Видимость = Объект.РучнаяКорректировкаПроводок;
	Элементы.ПодтвердитьАктуальностьПроводок.Видимость = Объект.РучнаяКорректировкаПроводок И СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	Элементы.ПодтвердитьАктуальностьПроводок.КнопкаПоУмолчанию = Объект.РучнаяКорректировкаПроводок И СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Объект.РучнаяКорректировкаПроводок И Не СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную;
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = Объект.РучнаяКорректировкаПроводок И Не СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную;
	Элементы.КоманднаяПанельНабораЗаписей.Видимость = Объект.РучнаяКорректировкаПроводок;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПриОткрытииФормы()
	
	ПравоРучнойКорректировкиПроводок = Пользователи.РолиДоступны("ПравоРучнойКорректировкиПроводок") И ПравоДоступа("Редактирование", Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	НеИзменятьПроводки = Не РеглУчетПроведениеСервер.ДокументОтражаетсяВРеглУчете(Документ, ДатаОтражения) ИЛИ Не ПравоИзмененияПроводок;
	Элементы.СоздатьНаОсновании.Доступность = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОперацияБух) И (ДокументПроводится ИЛИ ДокументПроведен);
	ТолькоПросмотр = НЕ (ДокументПроводится ИЛИ ДокументПроведен) ИЛИ НеИзменятьПроводки;
	Элементы.ФормаГруппаСтандартныеКоманды.Доступность = (ДокументПроводится ИЛИ ДокументПроведен) И ПравоИзмененияПроводок;
	Элементы.ФормаЗаписатьИЗакрыть.Доступность = Элементы.ФормаЗаписатьИЗакрыть.Доступность И ПравоРучнойКорректировкиПроводок;
	Элементы.ФормаЗаписать.Доступность = Элементы.ФормаЗаписать.Доступность И ПравоРучнойКорректировкиПроводок;
	Элементы.РучнаяКорректировкаПроводок.Доступность = (ДокументПроводится ИЛИ ДокументПроведен) И ТипДокументаКорректируетсяВручную(Документ)
		И ПравоРучнойКорректировкиПроводок И Не НеИзменятьПроводки;
	Элементы.АктуализацияПроводок.Видимость = ПравоИзмененияПроводок;
    Элементы.ФормаПереОтразитьДокументВРеглУчете.Видимость   = ОбщегоНазначения.РежимОтладки();
    Элементы.ФормаПереОтразитьДокументВРеглУчете.Доступность = ОбщегоНазначения.РежимОтладки();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОВведенныхКорректировках()
	
	Если ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОперацияБух) Тогда
	
		СписокКорректировок.ЗагрузитьЗначения(ВернутьСписокКорректировок(Документ));
		
		Элементы.ГруппаКорректировкиДокументамиРеглУчета.Видимость = СписокКорректировок.Количество();
		
		Если СписокКорректировок.Количество() = 1 Тогда
			ТекстКорректировки = СписокКорректировок.Получить(0).Значение;
		Иначе
			ТекстКорректировки = НСтр("ru='Открыть список введенных корректировок (%Количество%)';uk='Відкрити список введених коригувань (%Количество%)'");
			ТекстКорректировки = СтрЗаменить(ТекстКорректировки, "%Количество%", СписокКорректировок.Количество());
		КонецЕсли;
		
	Иначе
		
		Элементы.ГруппаКорректировкиДокументамиРеглУчета.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидКоманднойПанелиНабораЗаписей()
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейДобавить.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейУдалить.ТолькоВоВсехДействиях = Ложь;
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейУдалить.Отображение = ОтображениеКнопки.Картинка;
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейСкопировать.ТолькоВоВсехДействиях = Ложь;
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейСкопировать.Отображение = ОтображениеКнопки.Картинка;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеТаблицыПроводок()
	
	ИспользуетсяУчетПоНаправлениям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности")
								ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности")
								ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДСпоНаправлениямДеятельностиРаздельно")
								ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДСпоНаправлениямДеятельностиПоКорреспонденции")
								ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности");
								
	ВалютаРУ = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУУ = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаФО = Константы.ВалютаФинОтчетности.Получить();
	
	ВестиУУНаПланеСчетовХозрасчетный = ПолучитьФункциональнуюОпцию("ВестиУУНаПланеСчетовХозрасчетный");
	ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности = ПолучитьФункциональнуюОпцию("ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности");
	ИсточникСуммыДляПересчетаВВалютуФинОтчетности = Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить();

	Шапка3Эатажа = ВестиУУНаПланеСчетовХозрасчетный И ВестиУчетНаПланеСчетовХозрасчетныйВВалютеФинОтчетности;
	
	ШаблонЗаголовка = НСтр("ru='Подразделение %1';uk='Підрозділ %1'");
	Если ИспользуетсяУчетПоНаправлениям И НЕ Шапка3Эатажа Тогда
		ШаблонЗаголовка = ШаблонЗаголовка + ", " + НСтр("ru='Направление %1';uk='Напрям %1'")
	КонецЕсли;
	Элементы.ПодразделениеДт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='Дт';uk='Дт'"));
	Элементы.ПодразделениеКт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='Кт';uk='Кт'"));
	
	Элементы.НаправлениеДеятельностиДт.Доступность = ИспользуетсяУчетПоНаправлениям;
	Элементы.НаправлениеДеятельностиДт.ОтображатьВШапке = Шапка3Эатажа;
	
	Элементы.НаправлениеДеятельностиКт.Доступность = ИспользуетсяУчетПоНаправлениям;
	Элементы.НаправлениеДеятельностиКт.ОтображатьВШапке = Шапка3Эатажа;
	
	Элементы.Сумма.Заголовок   = СтрШаблон(НСтр("ru='Сумма БУ (%1)';uk='Сума БО (%1)'"), ВалютаРУ);
	
	Элементы.СуммаУУ.Заголовок = СтрШаблон(НСтр("ru='Сумма УУ (%1)';uk='Сума УО (%1)'"), ВалютаУУ);
	Элементы.СуммаФО.Заголовок = СтрШаблон(НСтр("ru='Сумма ФО (%1)';uk='Сума ФЗ (%1)'"), ВалютаФО);

КонецПроцедуры

Процедура УстановитьНастройкиПолейНУ()
	
	ПлательщикНалогаНаПрибыль             = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		
		Если НЕ ПлательщикНалогаНаПрибыль
			И УчетнаяПолитика.ПлательщикНалогаНаПрибыль(СписокГоловныхОрганизаций, Дата) Тогда
			ПлательщикНалогаНаПрибыль = Истина;
		КонецЕсли;
		
	Иначе
		
		ПлательщикНалогаНаПрибыль             = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата)
		
	КонецЕсли;
	
	Элементы.СуммаНУДт.Заголовок   = СтрШаблон(НСтр("ru='Сумма НУ Дт (%1)';uk='Сума ПО Дт (%1)'"), ВалютаРУ);
	Элементы.СуммаНУКт.Заголовок   = СтрШаблон(НСтр("ru='Сумма НУ Кт (%1)';uk='Сума ПО Кт (%1)'"), ВалютаРУ);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазделУчетаДляНастройки(Статус, Комментарий)
	
	РазделУчетаДляНастройки = "";
	
	Если Статус <> Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета Тогда
		Возврат РазделУчетаДляНастройки;
	КонецЕсли;
	
	Для каждого РазделУчета Из РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СписокРазделовСчетовУчета("ru") Цикл  
		ПредставлениеРаздела = СтрЗаменить(РазделУчета.Представление, "расчетов ", "");
		Если СтрНайти(Комментарий, ПредставлениеРаздела) > 0 Тогда
			РазделУчетаДляНастройки = РазделУчета.Значение;
			Если РазделУчета.Значение <> "НоменклатураСобственная" Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(РазделУчетаДляНастройки)Тогда
		Для каждого РазделУчета Из РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СписокРазделовСчетовУчета("uk") Цикл  
			ПредставлениеРаздела = СтрЗаменить(РазделУчета.Представление, "розрахунків ", "");
			Если СтрНайти(Комментарий, ПредставлениеРаздела) > 0 Тогда
				РазделУчетаДляНастройки = РазделУчета.Значение;
				Если РазделУчета.Значение <> "НоменклатураСобственная" Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	Возврат РазделУчетаДляНастройки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеОтраженияДокумента(Документ, ДатаДокумента, ЕстьПроводки, ДокументПроведен, Организация)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	// 0
	"ВЫБРАТЬ
	|	ОтражениеДокументовВРеглУчете.Регистратор КАК Документ,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК НеУказаныСчетаУчета,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КОтражениюВРеглУчете,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтраженоВРеглУчете,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КОтражениюВУчетеВручную,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтраженоВУчетеВручную
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|	И (ОтражениеДокументовВРеглУчете.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеДокументовВРеглУчете.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ДанныеДляПроверкиРасчетовПоСебестоимости
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ПО ОтражениеДокументовВРеглУчете.Организация = СебестоимостьТоваров.Организация
	|			И ОтражениеДокументовВРеглУчете.Регистратор = СебестоимостьТоваров.Регистратор
	|			И (СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И (ОтражениеДокументовВРеглУчете.Регистратор = &Документ)
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|	И (ОтражениеДокументовВРеглУчете.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКРасчетуСебестоимости.Организация,
	|	НАЧАЛОПЕРИОДА(ЗаданияКРасчетуСебестоимости.Месяц, МЕСЯЦ) КАК Месяц
	|ИЗ
	|	ДанныеДляПроверкиРасчетовПоСебестоимости КАК ДанныеДляПроверкиРасчетаСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуСебестоимости КАК ЗаданияКРасчетуСебестоимости
	|		ПО ДанныеДляПроверкиРасчетаСебестоимости.Организация = ЗаданияКРасчетуСебестоимости.Организация
	|			И ДанныеДляПроверкиРасчетаСебестоимости.ДатаОтражения >= НАЧАЛОПЕРИОДА(ЗаданияКРасчетуСебестоимости.Месяц, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетовСПоставщиками КАК ЗаданияКРаспределениюРасчетовСПоставщиками
	|		ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюРасчетовСПоставщиками.АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ПО ОтражениеДокументовВРеглУчете.Регистратор = РасчетыСПоставщиками.Регистратор
	|			И ОтражениеДокументовВРеглУчете.Организация = РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация
	|ГДЕ
	|	РасчетыСПоставщиками.Регистратор = &Документ
	|	И РасчетыСПоставщиками.Активность
	|	И НЕ &НовыеВзаиморасчеты
	|	И (ОтражениеДокументовВРеглУчете.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|
	|ИМЕЮЩИЕ
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц <= МИНИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|		ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ПО ОтражениеДокументовВРеглУчете.Регистратор = РасчетыСКлиентами.Регистратор
	|			И ОтражениеДокументовВРеглУчете.Организация = РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор = &Документ
	|	И РасчетыСКлиентами.Активность
	|	И НЕ &НовыеВзаиморасчеты
	|	И (ОтражениеДокументовВРеглУчете.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|
	|ИМЕЮЩИЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц <= МИНИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 5
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуСтоимостиВНА КАК Задания
	|		ПО (Задания.Организация = СтоимостьОС.Организация)
	|			И (Задания.ОбъектУчета = СтоимостьОС.АналитикаКапитализацииРасходов)
	|			И (Задания.Месяц <= СтоимостьОС.Период)
	|ГДЕ
	|	СтоимостьОС.Регистратор = &Документ
	|	И (СтоимостьОС.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуСтоимостиВНА КАК Задания
	|		ПО (Задания.Организация = СтоимостьНМА.Организация)
	|			И (Задания.ОбъектУчета = СтоимостьНМА.НематериальныйАктив)
	|			И (Задания.Месяц <= СтоимостьНМА.Период)
	|ГДЕ
	|	СтоимостьНМА.Регистратор = &Документ
	|	И (СтоимостьНМА.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 6
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК Задания
	|		ПО (Задания.Организация = ДокументыПоОС.Организация)
	|			И (Задания.ОбъектУчета = ДокументыПоОС.ОсновноеСредство)
	|			И (Задания.Месяц <= ДокументыПоОС.Дата)
	|ГДЕ
	|	ДокументыПоОС.Ссылка = &Документ
	|	И ДокументыПоОС.Проведен
	|	И ДокументыПоОС.ТипСсылки В(&ТипыДокументов)
	|	И (ДокументыПоОС.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрСведений.ДокументыПоНМА КАК ДокументыПоНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК Задания
	|		ПО (Задания.Организация = ДокументыПоНМА.Организация)
	|			И (Задания.ОбъектУчета = ДокументыПоНМА.НематериальныйАктив)
	|			И (Задания.Месяц <= ДокументыПоНМА.Дата)
	|ГДЕ
	|	ДокументыПоНМА.Ссылка = &Документ
	|	И ДокументыПоНМА.Проведен
	|	И ДокументыПоНМА.ТипСсылки В(&ТипыДокументов)
	|	И (ДокументыПоНМА.Организация = &Организация ИЛИ &Организация = НЕОПРЕДЕЛЕНО)";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("НовыеВзаиморасчеты", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.УстановитьПараметр("ТипыДокументов", ВнеоборотныеАктивы.ИдентификаторыДокументовПоКоторымФормируютсяОтложенныеДвижения());
	Запрос.УстановитьПараметр("Организация", Организация);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаСостоянияОтражения = Результат.Получить(0).Выбрать();
	
	Если ВыборкаСостоянияОтражения.Следующий() Тогда
		Если ВыборкаСостоянияОтражения.КОтражениюВУчетеВручную Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Документ был изменен. Скорректируйте проводки или подтвердите актуальность текущих.';uk='Документ був змінений. Скоригуйте проводки або підтвердіть актуальність поточних.'"),,
							ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет);
		ИначеЕсли ВыборкаСостоянияОтражения.ОтраженоВУчетеВручную Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Отражен в учете вручную';uk='Відображено в обліку вручну'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
		ИначеЕсли ВыборкаСостоянияОтражения.НеУказаныСчетаУчета Тогда
			Состояние = 
				?(ВыборкаСостоянияОтражения.ОтраженоВРеглУчете, 
					Новый ФорматированнаяСтрока(
							НСтр("ru='Частично отражен в учете';uk='Частково відображений в обліку'"),, 
							ЦветаСтиля.ПросроченныеДанныеЦвет, , "РасшифровкаСтатуса"), 
					Новый ФорматированнаяСтрока(
							НСтр("ru='Не удалось отразить в учете';uk='Не вдалося відобразити в обліку'"),,
							ЦветаСтиля.ПоясняющийОшибкуТекст));
		ИначеЕсли ВыборкаСостоянияОтражения.КОтражениюВРеглУчете И ВыборкаСостоянияОтражения.ОтраженоВРеглУчете Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Частично отражен в учете';uk='Частково відображений в обліку'"),, 
							ЦветаСтиля.ТекстИнформационнойНадписи, , "РасшифровкаСтатуса");
		ИначеЕсли ВыборкаСостоянияОтражения.КОтражениюВРеглУчете Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Ожидает отражения в учете';uk='Очікує відображення в обліку'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
		Иначе
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Отражен в учете';uk='Відображено в обліку'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
		КонецЕсли;
	ИначеЕсли ЕстьПроводки Тогда
		
		Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Отражен в учете';uk='Відображено в обліку'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
	ИначеЕсли НЕ ДокументПроведен Тогда
		
		Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru='Документ не проведен';uk='Документ не проведено'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
	Иначе
		
		ДатаНачалаОтраженияВРеглУчете = Константы.ДатаНачалаВеденияРеглУчета.Получить();
		Состояние = ?(ДатаДокумента < ДатаНачалаОтраженияВРеглУчете, 
						Новый ФорматированнаяСтрока(
							СтрШаблон(НСтр("ru='Регламентированный учет ведется с %1, отражение документа в учете не требуется';uk='Регламентований облік ведеться з %1, відображення документу в обліку не потрібно'"), 
								Формат(ДатаНачалаОтраженияВРеглУчете, "ДЛФ=D")),,
							ЦветаСтиля.ТекстИнформационнойНадписи), 
						Новый ФорматированнаяСтрока(
							НСтр("ru='Не требует отражения в учете';uk='Не потребує відображення в обліку'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи));
	КонецЕсли;
	
	ВзаиморасчетыАкуальны = Истина;
	СебестоимостьАкуальна = Истина;
	СтоимостьВНААктуальна = Истина;
	
	МассивУточнениеСостояния = Новый Массив;
	Если Не Результат.Получить(2).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru='Расчет себестоимости не выполнен.';uk='Розрахунок собівартості не виконаний.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		СебестоимостьАкуальна = Ложь;
	КонецЕсли;
	Если Не Результат.Получить(3).Пустой() ИЛИ Не Результат.Получить(4).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru='Распределение расчетов не выполнено.';uk='Розподіл розрахунків не виконано.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		ВзаиморасчетыАкуальны = Ложь;
	КонецЕсли;
	Если Не Результат.Получить(5).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru='Расчет стоимости ОС и НМА не выполнен.';uk='Розрахунок вартості ОЗ і НМА не виконано.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		СтоимостьВНААктуальна = Ложь;
	КонецЕсли;
	Если Не Результат.Получить(6).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru='Формирование движений по ОС и НМА не выполнено.';uk='Формування рухів з ОЗ і НМА не виконано.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		СтоимостьВНААктуальна = Ложь;
	КонецЕсли;
	
	Если НЕ (СебестоимостьАкуальна И ВзаиморасчетыАкуальны И СтоимостьВНААктуальна) 
			И ВыборкаСостоянияОтражения.ОтраженоВРеглУчете Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru='Проводки по документу могут измениться.';uk='Проводки по документу можуть змінитися.'"),,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Состояние",            Состояние);
	Результат.Вставить("УточнениеСостояния",   Новый ФорматированнаяСтрока(МассивУточнениеСостояния));
	Результат.Вставить("ВзаиморасчетыАктуальны", ВзаиморасчетыАкуальны);
	Результат.Вставить("СебестоимостьАкуальна",  СебестоимостьАкуальна);
	
	Комментарий = РеглУчетПроведениеСервер.СводныйКомментарийПоДокументу(Документ, Организация);
	Результат.Вставить("Комментарий", Комментарий);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОтражениеДокументов

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если ДополнительныеПараметры.Свойство("ОчисткаПроводок") Тогда
			НаборЗаписей.Очистить();
			Записать(Новый Структура("ЗадаватьВопросПередЗаписью", Ложь));
		КонецЕсли;
		
		ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияПриСнятииФлагаРучнаяКорректировкаПроводок(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("ЗадаватьВопросПередЗаписью", Ложь));
	Иначе
		Объект.РучнаяКорректировкаПроводок = Истина;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы("РучнаяКорректировкаПроводок");
	
КонецПроцедуры

&НаСервере
Функция ОтразитьДокументВРеглУчетеСервер()
	
	СтруктураРеквизиты = Новый Структура("Ссылка, Дата, Организация", Документ, ДатаОтражения, Организация);
	
	
	ВыполняемыйМетод = "РеглУчетПроведениеСервер.ОтразитьДокументВФоне";
	
	НаименованиеЗадания = СтрШаблон(НСтр("ru='Актуализация проводок документа ""%1""';uk='Актуалізація проводок документа ""%1""'"), Документ);
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ПредставлениеДокумента);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = Документ.УникальныйИдентификатор();
	ПараметрыФоновогоЗадания.ЗапуститьВФоне = Истина;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	ПараметрыОбработки = Новый Структура("РеквизитыДокумента, ВыполнитьПересчеты", СтруктураРеквизиты, Истина);
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	РезультатФоновогоЗадания.Вставить("ВыводитьПрогрессВыполнения", Ложь);
	РезультатФоновогоЗадания.Вставить("ВыводитьСообщения", Ложь);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ПовторноеОткрытиеСервер();
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаСервере
Процедура ПодтвердитьАктуальностьПроводокСервер()

	СтатусРучногоОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную;
	РеглУчетПроведениеСервер.ЗарегистрироватьОтражениеДокумента(Документ, СтатусРучногоОтражения, Комментарий);

КонецПроцедуры

&НаСервере
Функция ПараметрыОтраженияДокументаВРеглУчете()
	
	ПараметрыОтражения = Новый Структура;
	ПараметрыОтражения.Вставить("Ссылка", Документ);
	ПараметрыОтражения.Вставить("Дата", ДатаОтражения);
	ПараметрыОтражения.Вставить("Организация", Организация);
	ПараметрыОтражения.Вставить("ВыполнитьПересчеты", Истина);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

&НаСервере
Функция ЗаполнитьПроводкиАвтоматическиСервер() //выполняется по кнопке набора записей

	ПараметрыОтражения = ПараметрыОтраженияДокументаВРеглУчете();
	ПараметрыОтражения.Организация = Неопределено;
	
	ВыполняемыйМетод = "РеглУчетПроведениеСервер.ВернутьРезультатОтраженияДокумента";
	
	НаименованиеЗадания = СтрШаблон(НСтр("ru='Актуализация проводок документа ""%1""';uk='Актуалізація проводок документа ""%1""'"), Документ);
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ПредставлениеДокумента);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = Документ.УникальныйИдентификатор();
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;	
	
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОтражения, ПараметрыФоновогоЗадания);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(РезультатФоновогоЗадания.АдресРезультата);
	Иначе
		РезультатФоновогоЗадания.Вставить("ВыводитьПрогрессВыполнения", Ложь);
		РезультатФоновогоЗадания.Вставить("ВыводитьСообщения", Ложь);
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок()
	РезультатФоновогоЗаданияОтраженияПроводок = ЗаполнитьПроводкиАвтоматическиСервер();
	Если РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);
	ИначеЕсли РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Ошибка" Тогда
		ВызватьИсключение(НСтр("ru='При автоматическом заполнении проводок документа произошла ошибка. Подробности см. в Журнале регистрации.';uk='При автоматичному заповненні проводок документа сталася помилка. Подробиці див. у Журналі реєстрації.'"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если Результат.ТаблицаПроводок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Изначально результат получаем без колонки активности, из-за чего проводки будут записываться неактивными, добавим
	// данную колонку:
	Результат.ТаблицаПроводок.Колонки.Добавить("Активность");
	Результат.ТаблицаПроводок.ЗаполнитьЗначения(Истина, "Активность");
	
	Проводки = РеквизитФормыВЗначение("НаборЗаписей");
	Проводки.Загрузить(Результат.ТаблицаПроводок);
	РегистрыБухгалтерии.Хозрасчетный.ВыполнитьДопОбработкуПроводок(Проводки);
	ЗначениеВРеквизитФормы(Проводки, "НаборЗаписей");
	
	Если ЗначениеЗаполнено(Результат.КомментарийОшибки) Тогда
		Комментарий = Результат.КомментарийОшибки;
		Элементы.ГруппаКомментарийАвтоматическогоОтраженияВУчете.Видимость = Истина;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьПредставлениеВидовСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСостояниеОтраженияДокумента()
	
	ЕстьПроводки = НаборЗаписей.Количество() > 0;
	
	ДанныеОтражения = РеглУчетПроведениеСервер.ПолучитьДанныеОтраженияДокумента(Документ, Организация);
	СтатусОтраженияДокумента = ДанныеОтражения.Статус;
	Объект.РучнаяКорректировкаПроводок = 
		ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету")
		И РеглУчетВыборкиСерверПовтИсп.ИсключенияИзРучнойКорректировкиПроводок().Найти(ТипЗнч(Документ)) = Неопределено
	 	И ДанныеОтражения.РучноеОтражение;
		
	ПредставлениеОтражения = ПредставлениеОтраженияДокумента(Документ, ДатаОтражения, ЕстьПроводки, ДокументПроведен, Организация);
	Состояние = ПредставлениеОтражения.Состояние;
	УточнениеСостояния = ПредставлениеОтражения.УточнениеСостояния;
	Комментарий = ?(ДанныеОтражения.РучноеОтражение, ДанныеОтражения.Комментарий, ПредставлениеОтражения.Комментарий);
	ВзаиморасчетыСПартнерамиАктуальны = ПредставлениеОтражения.ВзаиморасчетыАктуальны;
	
	СостояниеОтображенияКомментария = ПолучитьСостояниеОтображенияКомментария(Комментарий);
	
	Элементы.НастроитьСчетаУчета.Видимость = Ложь;
	
	Элементы.ГруппаДопУточнениеСостояния.Видимость = ЗначениеЗаполнено(УточнениеСостояния);
	
	Если Не СтатусОтраженияДокумента.Пустая() Тогда
		РазделУчетаДляНастройки = РазделУчетаДляНастройки(СтатусОтраженияДокумента, Комментарий);
		Элементы.НастроитьСчетаУчета.Видимость = ЗначениеЗаполнено(РазделУчетаДляНастройки);
	Иначе
		Если НЕ ЕстьПроводки Тогда
			Элементы.ЗаполнитьПроводкиАвтоматически.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	ВидимостьКомментарияАвтоматическогоОтражения = Не Объект.РучнаяКорректировкаПроводок И
		СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета
		И (Не ЗначениеЗаполнено(Организация) Или СписокОрганизаций.НайтиПоЗначению(Организация).Пометка);
	Элементы.ГруппаКомментарийАвтоматическогоОтраженияВУчете.Видимость = ВидимостьКомментарияАвтоматическогоОтражения;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтатусПроверкиДокумента(Документ)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПроверкуДокументовПоРегламентированномуУчету")
		И ПроверкаДокументовСервер.ЭтотТипДокументаДолженПроверяться(ТипЗнч(Документ)) Тогда
		ДанныеПроверки = РегистрыСведений.СтатусыПроверкиДокументов.ПолучитьДанныеПроверкиДокумента(Документ);
		Возврат ?(ДанныеПроверки.СтатусПроверки.Пустая(), Перечисления.ЭтапыПроверкиДокументаВРеглУчете.НеПроверен, ДанныеПроверки.СтатусПроверки);
	Иначе
		Возврат Перечисления.ЭтапыПроверкиДокументаВРеглУчете.ПустаяСсылка();
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ОбновитьДанныеРеквизитовДокумента()

	Если Не Документ.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОтражениеДокументовВРеглУчете.Организация,
		|	ОтражениеДокументовВРеглУчете.ТребуетсяУказаниеСчетовУчета,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Организация.ОбособленноеПодразделение
		|			ТОГДА ОтражениеДокументовВРеглУчете.Организация.ГоловнаяОрганизация
		|		ИНАЧЕ ОтражениеДокументовВРеглУчете.Организация
		|	КОНЕЦ КАК ГоловнаяОрганизация
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ОтражениеДокументовВРеглУчете.Организация,
		|		ОтражениеДокументовВРеглУчете.Статус = &Статус КАК ТребуетсяУказаниеСчетовУчета
		|	ИЗ
		|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|	ГДЕ
		|		ОтражениеДокументовВРеглУчете.Регистратор = &Документ
		|	) КАК ОтражениеДокументовВРеглУчете
		|;
		|
		|////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения) КАК ДатаОтражения,
		|	МАКСИМУМ(ВЫБОР КОГДА ОтражениеДокументовВРеглУчете.Статус = &Статус ТОГДА ОтражениеДокументовВРеглУчете.ДатаОтражения ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ) КАК ДатаОтраженияНастройки,
		|	МАКСИМУМ(ОтражениеДокументовВРеглУчете.Период) КАК Дата
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
		|";
		Запрос.УстановитьПараметр("Документ", Документ);
		Запрос.УстановитьПараметр("Статус",   Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		РезультатЗапросаОрганизации = РезультатЗапроса[0];
		
		Если НЕ РезультатЗапросаОрганизации.Пустой() Тогда
			
			ТаблицаОрганизации   = РезультатЗапросаОрганизации.Выгрузить();
			ВыборкаДатаОтражения = РезультатЗапроса[1].Выбрать();
			ДокументПроведен = ВыборкаДатаОтражения.Следующий();
			Если ДокументПроведен Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДатаОтражения);
			КонецЕсли;
			
			СписокОрганизаций.Очистить();
			Элементы.ВыбраннаяОрганизация.Видимость = ТаблицаОрганизации.Количество() > 1;
			Если ТаблицаОрганизации.Количество() = 1 Тогда
				СтрокаТаблицы = ТаблицаОрганизации.Получить(0);
				Организация = СтрокаТаблицы.Организация;
				СписокОрганизаций.Добавить(СтрокаТаблицы.Организация, , СтрокаТаблицы.ТребуетсяУказаниеСчетовУчета);
				СписокГоловныхОрганизаций.Добавить(
					ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(СтрокаТаблицы.Организация));
			Иначе
				СписокВыбора = Элементы.ВыбраннаяОрганизация.СписокВыбора;
				СписокВыбора.Очистить();
				СписокВыбора.Добавить(Неопределено, НСтр("ru='<По всем организациям>';uk='<За всіма організаціями>'"));
				Для каждого СтрокаТаблицы Из ТаблицаОрганизации Цикл
					СписокВыбора.Добавить(СтрокаТаблицы.Организация);
					СписокОрганизаций.Добавить(СтрокаТаблицы.Организация, , СтрокаТаблицы.ТребуетсяУказаниеСчетовУчета);
				КонецЦикла;
				
				СписокГоловныхОрганизаций.ЗагрузитьЗначения(
					ОбщегоНазначенияКлиентСервер.СвернутьМассив(
						ТаблицаОрганизации.ВыгрузитьКолонку("ГоловнаяОрганизация")));
			КонецЕсли
			
		Иначе
			
			// Случай, когда документ не отражен в регистре сведений "ОтражениеДокументовВРеглУчете"
			РеквизитыДляПоиска = "Дата, Проведен" 
				+ ?(ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", Документ.Метаданные()),
					", Организация, Организация.ГоловнаяОрганизация", "")
				+ ?(ОбщегоНазначения.ЕстьРеквизитОбъекта("ОрганизацияПолучатель", Документ.Метаданные()),
					", ОрганизацияПолучатель, ОрганизацияПолучатель.ГоловнаяОрганизация", "")
				+ ?(ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПроведенияБанком", Документ.Метаданные()),
					", ДатаПроведенияБанком", "");
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, РеквизитыДляПоиска);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
			ДокументПроведен = ЗначенияРеквизитов.Проведен;
			
			Если ДокументПроведен Тогда
				ДатаОтражения = ?(ЗначенияРеквизитов.Свойство("ДатаПроведенияБанком"),
								ЗначенияРеквизитов.ДатаПроведенияБанком, Дата);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				// присваиваем "Неопределено"
				Организация = Неопределено;
			КонецЕсли;
			
			СписокОрганизаций.Очистить();
			
			ОрганизацияПолучатель = Неопределено;
			Если Не ЗначенияРеквизитов.Свойство("ОрганизацияПолучатель", ОрганизацияПолучатель)
				Или Не ЗначениеЗаполнено(ОрганизацияПолучатель) Тогда
				Элементы.ВыбраннаяОрганизация.Видимость = Ложь;
				СписокОрганизаций.Добавить(Организация);
				СписокГоловныхОрганизаций.Добавить(
					ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						ЗначенияРеквизитов, "ОрганизацияГоловнаяОрганизация"));
			Иначе
				
				СписокОрганизаций.Добавить(Организация);
				СписокОрганизаций.Добавить(ОрганизацияПолучатель);
				
				СписокГоловныхОрганизаций.Добавить(
					ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						ЗначенияРеквизитов, "ОрганизацияГоловнаяОрганизация"));
				СписокГоловныхОрганизаций.Добавить(
					ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						ЗначенияРеквизитов, "ОрганизацияПолучательГоловнаяОрганизация"));
				
				СписокВыбора = Элементы.ВыбраннаяОрганизация.СписокВыбора;
				СписокВыбора.Добавить(Неопределено, НСтр("ru='<По всем организациям>';uk='<За всіма організаціями>'"));
				
				Для каждого текОрг Из СписокОрганизаций Цикл
					СписокВыбора.Добавить(текОрг);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередЗаписью(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(ДополнительныеПараметры);
		Если ДополнительныеПараметры.Свойство("ЗакрыватьПриЗаписи") Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередПроведениемАмортизации(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		МассивДокументовДляПроведения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ);
		ОбщегоНазначенияВызовСервера.ПровестиДокументы(МассивДокументовДляПроведения);
		ЭтотОбъект.Прочитать();
		Если Объект.РучнаяКорректировкаПроводок Тогда
			ЭтотОбъект.Модифицированность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипДокументаКорректируетсяВручную(Документ)
	
	МассивТиповИсключений = РеглУчетВыборкиСерверПовтИсп.ИсключенияИзРучнойКорректировкиПроводок();
	
	Возврат МассивТиповИсключений.Найти(ТипЗнч(Документ)) = Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОтражениеПроводокПроверитьНаКлиенте()
	МассивЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РезультатФоновогоЗаданияОтраженияПроводок);
	Результат = ДлительныеОперацииВызовСервера.ОперацииВыполнены(МассивЗаданий, Новый Массив);
	РезультатЗадания = Результат.Получить(РезультатФоновогоЗаданияОтраженияПроводок.ИдентификаторЗадания);
	СтатусРезультата = ?(РезультатЗадания = Неопределено, "Ошибка", РезультатЗадания.Статус);
	Если СтатусРезультата = "Выполняется" Тогда
		Если ФормаДлительнойОперации = Неопределено ИЛИ РезультатФоновогоЗаданияОтраженияПроводок.ИдентификаторЗадания <> ФормаДлительнойОперации.ИдентификаторЗадания Тогда
			ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, РезультатФоновогоЗаданияОтраженияПроводок.ИдентификаторЗадания);
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОжидания);
		КонецЕсли;
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", ПараметрыОжидания.ТекущийИнтервал, Истина);
	Иначе
		Если ФормаДлительнойОперации <> Неопределено Тогда
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		КонецЕсли;
		Если СтатусРезультата = "Ошибка" Тогда
			ТекстОшибки = ?(Объект.РучнаяКорректировкаПроводок,
				НСтр("ru='При автоматическом заполнении проводок документа произошла ошибка.';uk='При автоматичному заповненні проводок документа сталася помилка.'"),
				НСтр("ru='При отражении документа в регламентированном учете произошла ошибка.';uk='При відображенні документа в регламентованому обліку сталася помилка.'"));
			ДополнительныйТекст = ?(РезультатЗадания = Неопределено,
				НСтр("ru='Не найдено выполняемое задание.';uk='Не знайдено завдання що виконується.'"),
				НСтр("ru='Подробности см. в Журнале регистрации.';uk='Подробиці див. у Журналі реєстрації.'"));
			ТекстОшибки = ТекстОшибки + Символы.ПС + ДополнительныйТекст;
			ВызватьИсключение(ТекстОшибки);
		Иначе
			Если Объект.РучнаяКорректировкаПроводок Тогда
				ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(РезультатФоновогоЗаданияОтраженияПроводок.АдресРезультата);
			Иначе
				ПовторноеОткрытиеСервер();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция ВернутьСписокКорректировок(Документ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ОперацияБухЗаполнениеДвижений.Ссылка
	                      |ИЗ
	                      |	Документ.ОперацияБух.ЗаполнениеДвижений КАК ОперацияБухЗаполнениеДвижений
	                      |ГДЕ
	                      |	ОперацияБухЗаполнениеДвижений.Документ = &Документ
	                      |	И НЕ ОперацияБухЗаполнениеДвижений.Ссылка.ПометкаУдаления");
	Запрос.УстановитьПараметр("Документ", Документ);
	
	МассивКорректировок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивКорректировок;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуКомментария(ДополнительныеПараметры = Неопределено)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ФормаКомментарияЗакрытие", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура("РучнаяКорректировкаПроводок, Комментарий, УведомлятьОПустомКомментарии",
								Объект.РучнаяКорректировкаПроводок, Комментарий, УведомлятьОПустомКомментарии);
	ОткрытьФорму("Обработка.ОтражениеДокументовВРеглУчете.Форма.ФормаКомментария", ПараметрыФормы, ЭтотОбъект,, ВариантОткрытияОкна.ОтдельноеОкно,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаКомментарияЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Не Объект.РучнаяКорректировкаПроводок Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РезультатЗакрытия = Неопределено Тогда
		
		Если Не Комментарий = РезультатЗакрытия.Комментарий Тогда
			Модифицированность = Истина;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗакрытия);
		
		Если ДополнительныеПараметры = Неопределено Тогда
			СостояниеОтображенияКомментария = ПолучитьСостояниеОтображенияКомментария(Комментарий);
		Иначе
			Записать(ДополнительныеПараметры);
			Если ДополнительныеПараметры.Свойство("ЗакрыватьПриЗаписи") Тогда
				Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСостояниеОтображенияКомментария(Комментарий)
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		Возврат СтрПолучитьСтроку(Комментарий, 1);
	Иначе
		Возврат НСтр("ru='добавить';uk='додати'");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСостояниеПроверкиДокумента(СтатусПроверки)
	
	Если СтатусПроверки = ПредопределенноеЗначение("Перечисление.ЭтапыПроверкиДокументаВРеглУчете.Проверен") Тогда
		СтрокаВозврата = НСтр("ru='(проверен)';uk='(перевірений)'");
	ИначеЕсли СтатусПроверки = ПредопределенноеЗначение("Перечисление.ЭтапыПроверкиДокументаВРеглУчете.ПустаяСсылка") Тогда
		СтрокаВозврата = "";
	Иначе
		СтрокаВозврата = НСтр("ru='(не проверен)';uk='(не перевірений)'");
	КонецЕсли;
	
	Возврат ?(ЗначениеЗаполнено(СтрокаВозврата), " " + СтрокаВозврата, "");
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораОрганизацииДляНастройкиСчетов(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуНастройкиСчетовУчета(Результат.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиСчетовУчета(Организация)
	
	ОрганизацииДляНастройки = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокОрганизаций);
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Организация", Организация);
	СтруктураПараметры.Вставить("ОрганизацииДляНастройки", ОрганизацииДляНастройки);
	СтруктураПараметры.Вставить("ДатаОкончанияПериода", Новый СтандартнаяДатаНачала(ДатаОтраженияНастройки));
	Если ЗначениеЗаполнено(РазделУчетаДляНастройки) Тогда
		СтруктураПараметры.Вставить("ТекущаяСтраница", РазделУчетаДляНастройки);
	КонецЕсли;
	СтруктураПараметры.Вставить("ПоказыватьТолькоТребующиеНастройки", Истина);
	
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.ФормаНастройки", СтруктураПараметры, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция МассивДокументовТребующихОчисткиПроводок()
	
	МассивВозврата = Новый Массив;
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПередачаОСАрендатору"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ВозвратОСОтАрендатора"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ВыбытиеАрендованныхОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ИзменениеПараметровОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.МодернизацияОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПеремещениеОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПереоценкаНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПереоценкаОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.СписаниеНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.СписаниеОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.РемонтОС"));
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьРасшифровкуСтатуса()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", Документ);
	
	ОткрытьФорму("Обработка.ОтражениеДокументовВРеглУчете.Форма.ФормаРасшифровкиСтатуса", ПараметрыФормы, ЭтаФорма); 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти