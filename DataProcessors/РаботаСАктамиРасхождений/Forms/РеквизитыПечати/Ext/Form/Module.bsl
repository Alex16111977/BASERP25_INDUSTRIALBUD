#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДокументОбъект") 
		Или Не ТипЗнч(Параметры.ДокументОбъект) = Тип("ДанныеФормыСтруктура")
		Или НЕ Параметры.ДокументОбъект.Свойство("Ссылка") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДокументОбъект,,"ПриемкаТоваров");
	ПриемкаТоваров.Загрузить(Параметры.ДокументОбъект.ПриемкаТоваров.Выгрузить());
	
	НастроитьФормуВЗависимостиОтТипаДокумента(Параметры.ДокументОбъект);
	
	Если ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь() Тогда
		
		Если Параметры.ДокументОбъект.Статус <> Перечисления.СтатусыАктаОРасхождениях.НеСогласовано Тогда
			ТолькоПросмотр = Истина;
		КонецЕсли;
		
		
	КонецЕсли;
	
	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСпискиВыбораДолжностейЧленовКомиссии(Элементы, ЭтотОбъект);
 	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И Модифицированность И Не СохранитьПараметры Тогда
		
		СохранитьПараметры = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстПредупреждения = НСтр("ru='Реквизиты печати акта о расхождениях были изменены. Перенести изменения?';uk='Реквізити друку акта про розбіжності були змінені. Перенести зміни?'");
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ВыполняетсяЗакрытие = Истина;
	СохранитьПараметры  = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если СохранитьПараметры И Не ЗавершениеРаботы Тогда
		
		ОповеститьОВыборе(СтруктраВозвращаемыхДанных());
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура ЧленКомиссии1ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии1,
		ЧленКомиссии1, Организация, Дата, ДолжностьЧленаКомиссии1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧленКомиссии2ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии2,
		ЧленКомиссии2, Организация, Дата, ДолжностьЧленаКомиссии2);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧленКомиссии3ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии3,
		ЧленКомиссии3, Организация, Дата, ДолжностьЧленаКомиссии3);
	
КонецПроцедуры
 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ТолькоПросмотр Тогда
		СохранитьПараметры = Истина;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Функция СтруктраВозвращаемыхДанных()
	
	СтруктураВозврата = Новый Структура;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		
		СтруктураВозврата.Вставить(РеквизитФормы.Имя, ЭтотОбъект[РеквизитФормы.Имя]);
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Настраивает форму в зависимости от типа документа
// 
// Параметры:
// 	ДокументОбъект - ДокументОбъект.АктОРасхожденияхПослеОтгрузки - 
//                 - ДокументОбъект.АктОРасхожденияхПослеПриемки -
//                 - ДокументОбъект.АктОРасхожденияхПослеПеремещения - акт о расхождениях.
//
&НаСервере
Процедура НастроитьФормуВЗависимостиОтТипаДокумента(ДокументОбъект)
	
	ЕстьПравоИзменения = Ложь;
	
	Если ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.АктОРасхожденияхПослеОтгрузки") Тогда
		
		Элементы.Руководитель.ОграничениеТипа     = Новый ОписаниеТипов("Строка");
		Элементы.ГлавныйБухгалтер.ОграничениеТипа = Новый ОписаниеТипов("Строка");
		Элементы.Грузоотправитель.ОграничениеТипа = Новый ОписаниеТипов("Строка");
		
		ЕстьПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.АктОРасхожденияхПослеОтгрузки);
		
	ИначеЕсли ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.АктОРасхожденияхПослеПриемки") Тогда
		
		Элементы.Руководитель.ОграничениеТипа     = Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций");
		Элементы.ГлавныйБухгалтер.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций");
		Элементы.Грузоотправитель.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
		
		ЕстьПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.АктОРасхожденияхПослеПриемки);
		
	КонецЕсли;
	
	ТолькоПросмотр = Не ЕстьПравоИзменения;

КонецПроцедуры

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
