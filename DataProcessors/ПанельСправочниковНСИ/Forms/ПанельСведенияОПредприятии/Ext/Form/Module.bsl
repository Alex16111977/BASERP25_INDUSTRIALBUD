
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	УправлениеЭлементами();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда
		// Изменены настройки программы в панелях администрирования
		УправлениеЭлементами();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбщиеСправочники

&НаКлиенте
Процедура ОткрытьКассуККМ(Команда)
	
	//++ НЕ БЗК
	ОткрытьФорму("Справочник.КассыККМ.ФормаОбъекта", Новый Структура("Ключ", КассаККМ()));
	//-- НЕ БЗК
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСклад(Команда)
	
	//++ НЕ БЗК
	ОткрытьФорму("Справочник.Склады.ФормаОбъекта", Новый Структура("Ключ", Склад()));
	//-- НЕ БЗК
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОрганизацию(Команда)
	
	ОткрытьФорму("Справочник.Организации.ФормаОбъекта", Новый Структура("Ключ", Организация()));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвойствоБанковскийСчет(Команда)
	
	//++ НЕ БЗК
	ОткрытьФорму("Справочник.БанковскиеСчетаОрганизаций.ФормаОбъекта",
		Новый Структура("Ключ", ПолучитьПервыйБанковскийСчетОрганизацииНаСервере()),
		ЭтаФорма);
	//-- НЕ БЗК	

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвойствоКасса(Команда)
	
	//++ НЕ БЗК
	ОткрытьФорму("Справочник.Кассы.ФормаОбъекта",
		Новый Структура("Ключ", ПолучитьПервуюКассуНаСервере()),
		ЭтаФорма);
	//-- НЕ БЗК	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВалюту(Команда)
	
	//++ НЕ БЗК
	ОткрытьФорму("Справочник.Валюты.ФормаОбъекта", Новый Структура("Ключ", Валюта()));
	//-- НЕ БЗК
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементами()

	// Функциональные опции
	ИспользоватьНесколькоОрганизаций     = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	//++ НЕ БЗК
	ИспользоватьНесколькоВалют           = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
	ИспользоватьНесколькоСкладов         = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	ИспользоватьНесколькоКассККМ         = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКассККМ");
	ИспользоватьНесколькоКасс            = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКасс");
	ИспользоватьНесколькоРасчетныхСчетов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоРасчетныхСчетов");
	ИспользоватьРозничныеПродажи         = ПолучитьФункциональнуюОпцию("ИспользоватьРозничныеПродажи");
	//-- НЕ БЗК
	
	// Право чтения
	ПравоДоступаОрганизации = ПравоДоступа("Просмотр", Метаданные.Справочники.Организации);
	//++ НЕ БЗК
	ПравоДоступаВалюты      = ПравоДоступа("Просмотр", Метаданные.Справочники.Валюты);
	ПравоДоступаСклады      = ПравоДоступа("Просмотр", Метаданные.Справочники.Склады);
	ПравоДоступаКассыККМ    = ПравоДоступа("Просмотр", Метаданные.Справочники.КассыККМ);
	//-- НЕ БЗК
	
	//БанковскийСчетИКасса
	//Для БЗК
	Элементы.ГруппаНастроекБанковскиеСчета.Видимость = Ложь;
	Элементы.ГруппаНастроекКассы.Видимость           = Ложь;
	//++ НЕ БЗК
	Элементы.ГруппаНастроекБанковскиеСчета.Видимость = НЕ ИспользоватьНесколькоРасчетныхСчетов;
	Элементы.ГруппаНастроекКассы.Видимость           = НЕ ИспользоватьНесколькоКасс;
	//-- НЕ БЗК
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СведенияОбОрганизации", "Видимость", Не ИспользоватьНесколькоОрганизаций И ПравоДоступаОрганизации);
	
	//Для БЗК
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаВалюты", "Видимость", Ложь);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкаСкладскогоУчета", "Видимость", Ложь);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкаКассыККМ", "Видимость", Ложь);
	//++ НЕ БЗК
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаВалюты", "Видимость", Не ИспользоватьНесколькоВалют И ПравоДоступаВалюты);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкаСкладскогоУчета", "Видимость", Не ИспользоватьНесколькоСкладов И ПравоДоступаСклады);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкаКассыККМ", "Видимость", Не ИспользоватьНесколькоКассККМ И ПравоДоступаКассыККМ И ИспользоватьРозничныеПродажи);
	//-- НЕ БЗК
	
	ЕстьДанныеДляОтображения = Элементы.ГруппаОрганизации.Видимость
		ИЛИ Элементы.ГруппаНастроекБанковскиеСчета.Видимость
		ИЛИ Элементы.ГруппаНастроекКассы.Видимость
		ИЛИ Элементы.ГруппаВалюты.Видимость
		ИЛИ Элементы.ГруппаНастройкаСкладскогоУчета.Видимость
		ИЛИ Элементы.ГруппаНастройкаКассыККМ.Видимость;
		
	Если ЕстьДанныеДляОтображения Тогда
		Элементы.ГруппаНеУстановленыНеобходимыеНастройки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#Область Прочее

//++ НЕ БЗК
&НаСервереБезКонтекста
Функция Склад()
	Возврат Справочники.Склады.СкладПоУмолчанию();
КонецФункции

&НаСервереБезКонтекста
Функция Валюта()
	Возврат ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета();
КонецФункции
//-- НЕ БЗК

&НаСервереБезКонтекста
Функция Организация()
	Возврат Справочники.Организации.ПолучитьОрганизациюПоУмолчанию();
КонецФункции

//++ НЕ БЗК
&НаСервереБезКонтекста
Функция КассаККМ()
	Возврат Справочники.КассыККМ.КассаККМПоУмолчанию();
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПервуюКассуНаСервере()
	
	Возврат ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию()
	
КонецФункции // ПолучитьПервуюКассуНаСервере()

&НаСервереБезКонтекста
Функция ПолучитьПервыйБанковскийСчетОрганизацииНаСервере()

	Возврат ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию();

КонецФункции // ПолучитьПервыйБанковскийСчетОрганизацииНаСервере()
//-- НЕ БЗК

#КонецОбласти

#КонецОбласти
