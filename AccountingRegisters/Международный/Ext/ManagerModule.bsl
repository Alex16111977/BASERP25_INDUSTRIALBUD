#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ОписаниеПолейРегистра() Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	
	ОписаниеПолей = Новый Структура();
	ОписаниеПолей.Вставить("КоличествоСубконто",    МетаданныеРегистра.ПланСчетов.МаксКоличествоСубконто);
	ОписаниеПолей.Вставить("БалансовыеИзмерения",   Новый Массив());
	ОписаниеПолей.Вставить("НебалансовыеИзмерения", Новый Массив());
	ОписаниеПолей.Вставить("БалансовыеРесурсы",     Новый Массив());
	ОписаниеПолей.Вставить("НебалансовыеРесурсы",   Новый Массив());
	ОписаниеПолей.Вставить("Реквизиты",             Новый Массив());
	
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		ИмяИзмерения = Измерение.Имя;
		Если СтрНачинаетсяС(ИмяИзмерения, "Удалить") Тогда
		ИначеЕсли Измерение.Балансовый Тогда
			ОписаниеПолей.БалансовыеИзмерения.Добавить(ИмяИзмерения);
		Иначе
			ОписаниеПолей.НебалансовыеИзмерения.Добавить(ИмяИзмерения);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		ИмяРесурса = Ресурс.Имя;
		Если СтрНачинаетсяС(ИмяРесурса, "Удалить") Тогда
		ИначеЕсли Ресурс.Балансовый Тогда
			ОписаниеПолей.БалансовыеРесурсы.Добавить(ИмяРесурса);
		Иначе
			ОписаниеПолей.НебалансовыеРесурсы.Добавить(ИмяРесурса);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл
		ИмяРеквизита = Реквизит.Имя;
		Если СтрНачинаетсяС(ИмяРеквизита, "Удалить") Тогда
		ИначеЕсли ИмяРеквизита = "ИдентификаторФинЗаписи" Тогда
		Иначе
			ОписаниеПолей.Реквизиты.Добавить(ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ИнвертируемыеПоля(ОписаниеПолейРегистра) Экспорт
	
	ИнвертируемыеПоля = Новый Массив();
	ИнвертируемыеПоля.Добавить("Счет");
	ИнвертируемыеПоля.Добавить("Субконто");
	ИнвертируемыеПоля.Добавить("ВидСубконто");
	ИнвертируемыеПоля.Добавить("СоответствиеСчета");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИнвертируемыеПоля, ОписаниеПолейРегистра.НебалансовыеИзмерения);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИнвертируемыеПоля, ОписаниеПолейРегистра.НебалансовыеРесурсы);
	
	Возврат ИнвертируемыеПоля;
	
КонецФункции

Функция ИнвертируемыеРесурсы(ОписаниеПолейРегистра) Экспорт
	
	ИнвертируемыеРесурсы = ОбщегоНазначения.СкопироватьРекурсивно(ОписаниеПолейРегистра.БалансовыеРесурсы);
	
	Для Каждого ИмяРесурса Из ОписаниеПолейРегистра.НебалансовыеРесурсы Цикл
		ИнвертируемыеРесурсы.Добавить(ИмяРесурса + "Дт");
		ИнвертируемыеРесурсы.Добавить(ИмяРесурса + "Кт");
	КонецЦикла;
	
	Возврат ИнвертируемыеРесурсы;
	
КонецФункции

Процедура ИнвертироватьПроводку(Проводка, ИнвертируемыеПоля, ИнвертируемыеРесурсы, КоличествоСубконто) Экспорт
	
	Для Каждого ИмяПоля Из ИнвертируемыеПоля Цикл
		Если ИмяПоля = "Субконто" ИЛИ ИмяПоля = "ВидСубконто" Тогда
			Для НомерСубконто = 1 По КоличествоСубконто Цикл
				ЗначениеПоляДт = Проводка[ИмяПоля + "Дт" + НомерСубконто];
				Проводка[ИмяПоля + "Дт" + НомерСубконто] = Проводка[ИмяПоля + "Кт" + НомерСубконто];
				Проводка[ИмяПоля + "Кт" + НомерСубконто] = ЗначениеПоляДт;
			КонецЦикла;
		Иначе
			ЗначениеПоляДт = Проводка[ИмяПоля + "Дт"];
			Проводка[ИмяПоля + "Дт"] = Проводка[ИмяПоля + "Кт"];
			Проводка[ИмяПоля + "Кт"] = ЗначениеПоляДт;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИмяРесурса Из ИнвертируемыеРесурсы Цикл
		Если ЗначениеЗаполнено(Проводка[ИмяРесурса]) Тогда
			Проводка[ИмяРесурса] = -Проводка[ИмяРесурса];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Получает сторнирующие проводки документа(ов).
//
// Параметры:
//  Регистраторы - ДокументСсылка - документы (массив документов) для которых необходимо получить сторнирующие проводки.
//  ПериодРегистратора - Булево - признак заполнения даты проводки датой сторнируемого документа если не указан, то дата
//                                не заполнена.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица проводок переданных регистраторов с обратным знаком сумм и типом проводки "Реверс".
//
Функция ПроводкиСторно(Регистраторы, ПериодРегистратора = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПроводокСторно();
	Запрос.УстановитьПараметр("СписокДокументов", Регистраторы);
	Запрос.УстановитьПараметр("Ссылка", Документы.ОперацияМеждународный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	Запрос.УстановитьПараметр("ПериодРегистратора", ПериодРегистратора);
	
	Результа = Запрос.Выполнить().Выгрузить();
	
	Возврат Результа;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПроводокСторно()
	
	Возврат
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ПериодРегистратора
	|			ТОГДА Проводки.Период
	|		ИНАЧЕ &ПустаяДата
	|	КОНЕЦ КАК Период,
	|	ВЫБОР
	|		КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
	|			ТОГДА Проводки.Регистратор
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ КАК Регистратор,
	|	Проводки.Организация,
	|	Проводки.СчетДт,
	|	Проводки.ПодразделениеДт,
	|	Проводки.НаправлениеДеятельностиДт,
	|	Проводки.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|	Проводки.СубконтоДт1,
	|	Проводки.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|	Проводки.СубконтоДт2,
	|	Проводки.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|	Проводки.СубконтоДт3,
	|	Проводки.СчетКт,
	|	Проводки.ПодразделениеКт,
	|	Проводки.НаправлениеДеятельностиКт,
	|	Проводки.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|	Проводки.СубконтоКт1,
	|	Проводки.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|	Проводки.СубконтоКт2,
	|	Проводки.ВидСубконтоКт3 КАК ВидСубконтоКт3,
	|	Проводки.СубконтоКт3,
	|	Проводки.ВалютаДт,
	|	-Проводки.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	Проводки.ВалютаКт,
	|	-Проводки.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	-Проводки.КоличествоДт КАК КоличествоДт,
	|	-Проводки.КоличествоКт КАК КоличествоКт,
	|	-Проводки.Сумма КАК Сумма,
	|	-Проводки.СуммаПредставления КАК СуммаПредставления,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроводокМеждународныйУчет.Реверс) КАК ТипПроводки,
	|	Проводки.Содержание КАК Содержание
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(, , Регистратор В (&СписокДокументов), , ) КАК Проводки";
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыБухгалтерии.Международный.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыБухгалтерии.Международный.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f5e76e09-26bb-407a-90cc-6550d152782f");
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ЧитаемыеОбъекты = "ПланСчетов.Международный,"
		+ "РегистрБухгалтерии.Международный";
	Обработчик.ИзменяемыеОбъекты = "Документ.ОперацияМеждународный,"
		+ "РегистрБухгалтерии.Международный";
	Обработчик.БлокируемыеОбъекты = "Документ.ОперацияМеждународный";
	Обработчик.Комментарий = НСтр("ru='Перенос значений измерения ""Подразделения"".
    |Переносит проводки МФУ документов Внесение денежных средств в ККМ и Выемка денежных средств из ККМ в документы Операция (международный учет)
    |Пока работа обработчика не завершена возможно неккоректное формирование отчетов по международному учету.'
    |;uk='Перенесення значень вимірювання ""Підрозділи"".
    |Переносить проведення МФО документів Внесення грошових коштів до ККМ та Вилучення грошових коштів з ККМ до документів Операція (міжнародний облік)
    |Поки робота обробника не завершена можливе некоректне формування звітів за міжнародним обліком.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ПланыСчетов.Международный.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";  
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ОбновлениеИнформационнойБазыУТ.ОбновитьПредставленияПредопределенныхЭлементов";
	НоваяСтрока.Порядок = "Любой";  
	
КонецПроцедуры 

// Регистрирует к последующей обработке документы, отраженные в международном учете для переноса значений измерения УдалитьПодразделение.
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Международный.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрБухгалтерии.Международный КАК Международный
	|ГДЕ
	|	(Международный.УдалитьПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ Международный.УдалитьПодразделениеКт <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|	И НЕ ТИПЗНАЧЕНИЯ(Международный.Регистратор) В (
	|		ТИП(Документ.ВнесениеДенежныхСредствВКассуККМ),
	|		ТИП(Документ.ВыемкаДенежныхСредствИзКассыККМ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Международный.Регистратор
	|ИЗ
	|	РегистрБухгалтерии.Международный КАК Международный
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Международный.Регистратор) В (
	|		ТИП(Документ.ВнесениеДенежныхСредствВКассуККМ),
	|		ТИП(Документ.ВыемкаДенежныхСредствИзКассыККМ))";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрБухгалтерии.Международный";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
КонецПроцедуры

// Переносит значение измерения в регистр бухгалтерии "Международный".
// Переносит проводоки МФУ удаляемых документов ВнесениеДенежныхСредствВКассуККМ, ВыемкаДенежныхСредствИзКассыККМ
// под специально созданный документ ОперацияМеждународный
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(
		Параметры.Очередь, 
		"ПланСчетов.Международный", 
		МенеджерВТ
	);
	Если Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяРегистра = "РегистрБухгалтерии.Международный";
	ВыборкаПоРегистраторам = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(
								Параметры.Очередь,
								Неопределено,
								ПолноеИмяРегистра);
	
	ПустаяОперацияМФУ = Документы.ОперацияМеждународный.ПустаяСсылка();
	ПереносПроводокМФУ = Новый Соответствие;
	ПереносПроводокМФУ.Вставить(Тип("ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ"), ПустаяОперацияМФУ);
	ПереносПроводокМФУ.Вставить(Тип("ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ"),ПустаяОперацияМФУ);
	
	Пока ВыборкаПоРегистраторам.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаПоРегистраторам.Регистратор);
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоРегистраторам.Регистратор);
			НаборЗаписей.Прочитать();
			
			#Область ПодготовкаПереносаПроводок
			ОперацияМФУ = Неопределено;
			НаборПриемник = Неопределено;
			Если ПереносПроводокМФУ[ТипЗнч(ВыборкаПоРегистраторам.Регистратор)] = ПустаяОперацияМФУ Тогда
				ОперацияМФУ = СоздатьОперациюМФУ(ВыборкаПоРегистраторам.Регистратор);
			КонецЕсли;
			Если ОперацияМФУ <> Неопределено Тогда
				НаборПриемник = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
				НаборПриемник.Отбор.Регистратор.Установить(ОперацияМФУ);
				НаборПриемник.ОбменДанными.Загрузка = Истина;
				НаборПриемник.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
				НаборПриемник.ДополнительныеСвойства.Вставить("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", Неопределено);
			КонецЕсли;
			#КонецОбласти
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Если ЗначениеЗаполнено(Запись.УдалитьПодразделениеДт) И НЕ ЗначениеЗаполнено(Запись.ПодразделениеДт) Тогда
					Запись.ПодразделениеДт = Запись.УдалитьПодразделениеДт; 
				КонецЕсли;
				Если ЗначениеЗаполнено(Запись.УдалитьПодразделениеКт) И НЕ ЗначениеЗаполнено(Запись.ПодразделениеКт) Тогда
					Запись.ПодразделениеКт = Запись.УдалитьПодразделениеКт; 
				КонецЕсли;
				Если НаборПриемник <> Неопределено Тогда
					НаборПриемник.ДобавитьПроводоку(Запись);
				КонецЕсли;
			КонецЦикла;
			
			Если НаборПриемник <> Неопределено Тогда
				НаборПриемник.Записать();
				НаборЗаписей.Очистить();
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ВыборкаПоРегистраторам.Регистратор);
				
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

Функция СоздатьОперациюМФУ(Регистратор)
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор,"Дата,Организация");
	ОперацияМФУ = Документы.ОперацияМеждународный.СоздатьДокумент();
	ОперацияМФУ.Дата = Реквизиты.Дата;
	ОперацияМФУ.Организация = Реквизиты.Организация;
	ОперацияМФУ.Ответственный = Пользователи.ТекущийПользователь();
	ОперацияМФУ.СодержаниеОперации = СтрШаблон(НСтр("ru='###Перенос проводок### ""%1""';uk='###Перенесення проводок### ""%1""'"),Строка(Регистратор));
	ОперацияМФУ.Комментарий = НСтр("ru='###Создан при обновлении### 
                                        |После обновления перенесенные проводки формируются в документах ПКО и РКО.
                                        |Распровести в случае задвоения проводок.'
                                        |;uk='###Створено при оновленні###
                                        |Після оновлення перенесені проводки формуються в документах ПКО та ВКО.
                                        |Розпровести у разі задвоювання проводок.'");
	ОперацияМФУ.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат ОперацияМФУ.Ссылка;
	
КонецФункции


#КонецОбласти

#КонецЕсли