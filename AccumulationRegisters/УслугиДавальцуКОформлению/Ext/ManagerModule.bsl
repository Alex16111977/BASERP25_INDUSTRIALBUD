#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.УслугиДавальцуКОформлению.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";  
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("713b2e1d-bf5e-4892-a5f3-34c61240ef19");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.УслугиДавальцуКОформлению.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет измерение Договор по данным заказов давальцев (измерение ЗаказДавальца).
                                   |Исправляет движения документа ""Выпуск продукции"".'
                                   |;uk='Заповнює вимір Договір за даними замовлень давальців (вимір ЗаказДавальца).
                                   |Виправляє рухи документа ""Випуск продукції"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ЗаказДавальца.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УслугиДавальцуКОформлению.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.УслугиДавальцуКОформлению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказДавальца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВыпускПродукции.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыНакопления.УслугиДавальцуКОформлению.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки; // см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Регистратор.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Регистратор");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	// Выпуск продукции
	ПолноеИмяДокумента = "Документ.ВыпускПродукции";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТовары.Ссылка.Дата              КАК Период,
	|	ТаблицаТовары.Назначение.Заказ         КАК ЗаказДавальца,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК КОформлению,
	|	0 КАК УдалитьЗаказано,
	|	0 КАК УдалитьСумма,
	|	НЕОПРЕДЕЛЕНО КАК ПричинаОтмены
	|ИЗ
	|	Документ.ВыпускПродукции.Товары КАК ТаблицаТовары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ТаблицаТовары.Распоряжение.Распоряжение = ЗаказНаПроизводствоПродукция.Ссылка
	|		И ТаблицаТовары.Распоряжение.КодСтроки = ЗаказНаПроизводствоПродукция.КодСтроки
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК ВыходныеИзделияМаршрутногоЛиста
	|		ПО ТаблицаТовары.Распоряжение = ВыходныеИзделияМаршрутногоЛиста.Ссылка
	|		И ТаблицаТовары.КодСтроки = ВыходныеИзделияМаршрутногоЛиста.КодСтроки
	|		И ВыходныеИзделияМаршрутногоЛиста.ПроизводитсяВПроцессе
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВозвратныеОтходы КАК ВозвратныеОтходыМаршрутногоЛиста
	|		ПО ТаблицаТовары.Распоряжение = ВозвратныеОтходыМаршрутногоЛиста.Ссылка
	|		И ТаблицаТовары.КодСтроки = ВозвратныеОтходыМаршрутногоЛиста.КодСтроки
	|		И ВозвратныеОтходыМаршрутногоЛиста.ПроизводитсяВПроцессе
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|		ПО СпрНазначения.Ссылка = ТаблицаТовары.Назначение
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И СпрНазначения.Заказ ССЫЛКА Документ.ЗаказДавальца
	|	И ЗаказНаПроизводствоПродукция.КлючСвязиПродукция = &ПустойКлючСвязи
	|	И ВыходныеИзделияМаршрутногоЛиста.Ссылка ЕСТЬ NULL
	|	И ВозвратныеОтходыМаршрутногоЛиста.Ссылка ЕСТЬ NULL
	|";
	
	СинонимТаблицыДокумента = "ТаблицаТовары";
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров.Вставить("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
		ТекстЗапроса, 
		ПолноеИмяДокумента, 
		СинонимТаблицыДокумента
	);
	
	Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
		Результат, 
		ПолноеИмяРегистра, 
		ПолноеИмяДокумента
	);
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы, 
		ПолноеИмяРегистра
	);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УслугиДавальцуКОформлению.Регистратор
	|ИЗ
	|	РегистрНакопления.УслугиДавальцуКОформлению КАК УслугиДавальцуКОформлению
	|ГДЕ
	|	УслугиДавальцуКОформлению.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	И УслугиДавальцуКОформлению.ЗаказДавальца <> ЗНАЧЕНИЕ(Документ.ЗаказДавальца.ПустаяСсылка)";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы, 
		ПолноеИмяРегистра
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.УслугиДавальцуКОформлению;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	ИмяРегистра = "УслугиДавальцуКОформлению";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбновляемыеДанные.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ОбновляемыеДанные
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УслугиДавальцуКОформлению.Регистратор КАК Регистратор,
	|	УслугиДавальцуКОформлению.НомерСтроки - 1 КАК ИндексСтроки,
	|	УслугиДавальцуКОформлению.ЗаказДавальца.Договор КАК Договор
	|ИЗ
	|	РегистрНакопления.УслугиДавальцуКОформлению КАК УслугиДавальцуКОформлению
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ОбновляемыеДанные КАК ОбновляемыеДанные
	|		ГДЕ
	|			ОбновляемыеДанные.Регистратор = УслугиДавальцуКОформлению.Регистратор)
	|	И УслугиДавальцуКОформлению.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	И УслугиДавальцуКОформлению.ЗаказДавальца <> ЗНАЧЕНИЕ(Документ.ЗаказДавальца.ПустаяСсылка)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗаписейСПустымиДоговорами = МассивРезультатов[1].Выгрузить(); // ТаблицаЗначений
	ТаблицаЗаписейСПустымиДоговорами.Индексы.Добавить("Регистратор");
	
	Для Каждого Выборка Из ОбновляемыеДанные цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Регистратор = Выборка.Регистратор;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
							
			НаборЗаписей = РегистрыНакопления.УслугиДавальцуКОформлению.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			
			НаборИзменен = Ложь;
				
			Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
			    ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(Регистратор, ИмяРегистра);
			    Результат = ТаблицыДляДвижений["Таблица" + ИмяРегистра];
			    НаборЗаписей.Загрузить(Результат);
				НаборИзменен = Истина;
			Иначе	
				МассивСтрок = ТаблицаЗаписейСПустымиДоговорами.НайтиСтроки(Новый Структура("Регистратор", Выборка.Регистратор));
				Для каждого СтрокаТаблицы Из МассивСтрок Цикл
					ЗаполнитьЗначенияСвойств(НаборЗаписей[СтрокаТаблицы.ИндексСтроки], СтрокаТаблицы, "Договор");
					НаборИзменен = Истина;
				КонецЦикла;
			КонецЕсли;	
				
			Если НаборИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = 
				СтрШаблон(
					НСтр("ru='Не удалось записать данные в регистр %1 по регистратору ""%2"", по причине: %3';uk='Не вдалося записати дані в регістр %1 по реєстратору ""%2"", по причині: %3'"), 
					ПолноеИмяРегистра, 
					Выборка.Регистратор, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
				);
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеРегистра, 
				, 
				ТекстСообщения
			);
			
		КонецПопытки;
		
	КонецЦикла;
		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли
