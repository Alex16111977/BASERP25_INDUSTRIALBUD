#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(СтатьяБюджетов)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыНакопления.ФактическиеДанныеБюджетирования.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("598c4611-f826-4a5e-945f-69b043cbe43c");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ФактическиеДанныеБюджетирования.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "БюджетированиеСервер.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрНакопления.ФактическиеДанныеБюджетирования";
	Обработчик.ИзменяемыеОбъекты = "РегистрНакопления.ФактическиеДанныеБюджетирования";
	Обработчик.БлокируемыеОбъекты = "Документ.ЭкземплярБюджета,"
		+ "Отчет.ПроверкаСвязейПоказателейБюджетов,"
		+ "Отчет.ОборотнаяВедомостьБюджетирования,"
		+ "Отчет.ЛимитыРасходаДенежныхСредствПоДаннымБюджетирования,"
		+ "Отчет.БюджетныйОтчет,"
		+ "Отчет.ОборотноСальдоваяВедомостьБюджетирования";
	Обработчик.Комментарий = НСтр("ru='Замена пустых значений аналитики на единое значение пустой аналитики Неопределено';uk='Заміна порожніх значень аналітики на єдине значення порожньої аналітики Неопределено'");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ФактическиеДанныеБюджетирования";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрНакопления.ФактическиеДанныеБюджетирования";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФактическиеДанныеБюджетирования.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ФактическиеДанныеБюджетирования КАК ФактическиеДанныеБюджетирования
	|ГДЕ
	|	ФактическиеДанныеБюджетирования.Правило.Коэффициент < 0
	|	И ФактическиеДанныеБюджетирования.СуммаВВалюте <> 0 
	|	И (ФактическиеДанныеБюджетирования.СуммаУпр / ФактическиеДанныеБюджетирования.СуммаВВалюте < 0
	|			ИЛИ ФактическиеДанныеБюджетирования.СуммаРегл / ФактическиеДанныеБюджетирования.СуммаВВалюте < 0)";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры,
		Регистраторы,
		ПолноеИмяРегистра
	);
	
	ПустыеЗначенияДляОбработки = БюджетированиеВызовСервера.ЗаменяемыеПустыеЗначенияАналитики();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
		|	Таблица.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ФактическиеДанныеБюджетирования КАК Таблица
		|ГДЕ
		|	(Таблица.Аналитика1 В (&ПустыеЗначенияДляОбработки)
		|			ИЛИ Таблица.Аналитика2 В (&ПустыеЗначенияДляОбработки)
		|			ИЛИ Таблица.Аналитика3 В (&ПустыеЗначенияДляОбработки)
		|			ИЛИ Таблица.Аналитика4 В (&ПустыеЗначенияДляОбработки)
		|			ИЛИ Таблица.Аналитика5 В (&ПустыеЗначенияДляОбработки)
		|			ИЛИ Таблица.Аналитика6 В (&ПустыеЗначенияДляОбработки))";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияДляОбработки", ПустыеЗначенияДляОбработки);
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ФактическиеДанныеБюджетирования;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ПустыеЗначенияДляОбработки = БюджетированиеВызовСервера.ЗаменяемыеПустыеЗначенияАналитики();
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	ЗначениеЗамены = БюджетированиеКлиентСервер.ПустоеЗначениеАналитики();
	
	РеквизитыПоиска = Новый Массив;
	Для Сч = 1 По МаксимальноеКоличествоАналитик Цикл
		РеквизитыПоиска.Добавить("Аналитика" + Сч);
	КонецЦикла;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ТипыДокументовКОбработке = ТипыДокументовКОбработке();
	
	Для Каждого ПолноеИмяДокумента Из ТипыДокументовКОбработке Цикл
		
		Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(Параметры.Очередь, ПолноеИмяДокумента, ПолноеИмяРегистра);
		
		Пока Выборка.Следующий() Цикл
			
			Регистратор = Выборка.Регистратор;
			
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
				ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				Блокировка.Заблокировать();
				
				Набор = РегистрыНакопления.ФактическиеДанныеБюджетирования.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(Регистратор);
				Набор.Прочитать();
				
				Если Набор.Количество() = 0 Тогда
					
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор, ДополнительныеПараметры);
					
				Иначе
					
					ОбъектИзменен = Ложь;
					
					Для Каждого Запись Из Набор Цикл
						
						ПравилоКоэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Правило, "Коэффициент");
						Если ПравилоКоэффициент < 0 Тогда
							// Три ресура должны быть одного знака. "Ведущим" является ресурс "СуммаВВалюте"
							Если Запись.СуммаВВалюте < 0 Тогда
								Если Запись.СуммаРегл > 0 Тогда
									Запись.СуммаРегл = -Запись.СуммаРегл;
									ОбъектИзменен = Истина;
								КонецЕсли;
								Если Запись.СуммаУпр > 0 Тогда
									Запись.СуммаУпр = -Запись.СуммаУпр;
									ОбъектИзменен = Истина;
								КонецЕсли;
							ИначеЕсли Запись.СуммаВВалюте > 0 Тогда
								Если Запись.СуммаРегл < 0 Тогда
									Запись.СуммаРегл = -Запись.СуммаРегл;
									ОбъектИзменен = Истина;
								КонецЕсли;
								Если Запись.СуммаУпр < 0 Тогда
									Запись.СуммаУпр = -Запись.СуммаУпр;
									ОбъектИзменен = Истина;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;	
						
						БюджетированиеВызовСервера.ВыполнитьЗаменыЗначенийВОбъекте(
							Запись,
						    РеквизитыПоиска,
						    ПустыеЗначенияДляОбработки,
						    ЗначениеЗамены,
						    ОбъектИзменен
						);
						
					КонецЦикла;
					
					Если ОбъектИзменен Тогда
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
					Иначе
						ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор, ДополнительныеПараметры);
					КонецЕсли;
					
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				ТекстСообщения = НСтр("ru='Не удалось обработать документ: %Регистратор% по причине: %Причина%';uk='Не вдалося обробити документ: %Регистратор% по причині: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Регистратор%", Регистратор);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Ошибка,
					Регистратор.Метаданные(),
					ТекстСообщения
				);
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

Функция ТипыДокументовКОбработке()
	
	МассивТипов = Новый Массив;
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ФактическиеДанныеБюджетирования;
	ТипыРегистраторов = МетаданныеРегистра.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	
	Для каждого ТипРегистратора Из ТипыРегистраторов Цикл
		
		МетаданныеРегистратора = Метаданные.НайтиПоТипу(ТипРегистратора);
		МассивТипов.Добавить(МетаданныеРегистратора.ПолноеИмя());
		
	КонецЦикла;
	
	Возврат МассивТипов;
	
КонецФункции

#КонецОбласти

#КонецЕсли