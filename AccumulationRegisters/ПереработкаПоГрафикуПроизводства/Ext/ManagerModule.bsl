//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура отражает в регистре ЭтапыПроизводства факт оформления заказа переработчику.
//
// Параметры:
//  ТаблицаЭтаповГрафика - ТаблицаЗначений - таблица этапов производства на стороне, по которым необходимо
//                                             зафиксировать факт оформления заказа переработчику
//    * Распоряжение - ДокументСсылка.ЗаказНаПроизводство - заказ на производство
//    * КодСтрокиЭтапыГрафик - Число - код строки этапа графика производства
//  ВызовИзОбработчикаОбновления - Булево - признак, что вызов выполняется из обработчика обновления.
//
Процедура ЗафиксироватьВводЗаказаПереработчикуПоЭтапамПроизводства(ТаблицаЭтаповГрафика, ВызовИзОбработчикаОбновления = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТаблицаЭтаповГрафика) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.Распоряжение         КАК Распоряжение,
	|	Т.КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик
	|ПОМЕСТИТЬ ВТ_ЭтапыПереработки
	|ИЗ
	|	&ТаблицаЭтаповГрафика КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтрокиЭтапыГрафик
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПроизводства.Распоряжение                         КАК Распоряжение,
	|	ЭтапыПроизводства.КодСтрокиПродукция                   КАК КодСтрокиПродукция,
	|	ЭтапыПроизводства.КодСтрокиЭтапыГрафик                 КАК КодСтрокиЭтапыГрафик,
	|	ЭтапыПроизводства.Этап                                 КАК Этап,
	|	ЭтапыПроизводства.Подразделение                        КАК Подразделение,
	|	СУММА(ЭтапыПроизводства.ЗапланированоЗаказом)
	|	 - СУММА(ЭтапыПроизводства.Выполнено)                  КАК ЗапланированоОстаток
	|
	|ПОМЕСТИТЬ ВТ_ОстаткиЭтапов
	|
	|ИЗ
	|	РегистрНакопления.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЭтапыПереработки КАК ЭтапыПереработки
	|		ПО ЭтапыПроизводства.Распоряжение = ЭтапыПереработки.Распоряжение
	|			И ЭтапыПроизводства.КодСтрокиЭтапыГрафик = ЭтапыПереработки.КодСтрокиЭтапыГрафик
	|
	|ГДЕ
	|	ЭтапыПроизводства.ПроизводствоНаСтороне
	|	И ЭтапыПроизводства.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыПроизводства.Распоряжение,
	|	ЭтапыПроизводства.КодСтрокиПродукция,
	|	ЭтапыПроизводства.КодСтрокиЭтапыГрафик,
	|	ЭтапыПроизводства.Этап,
	|	ЭтапыПроизводства.Подразделение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЭтапыПроизводства.ЗапланированоЗаказом)
	|	 - СУММА(ЭтапыПроизводства.Выполнено) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтрокиЭтапыГрафик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереработкаПоГрафикуПроизводства.Распоряжение         КАК Распоряжение,
	|	ПереработкаПоГрафикуПроизводства.КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик,
	|	ПереработкаПоГрафикуПроизводства.Регистратор          КАК Регистратор,
	|	ЗаказПереработчику.Дата                               КАК Период
	|ПОМЕСТИТЬ ВТ_ПереработкаПоГрафикуПроизводства
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПереработкаПоГрафикуПроизводства.ЗаказНаПроизводство КАК Распоряжение,
	|		ПереработкаПоГрафикуПроизводства.КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик,
	|		МИНИМУМ(ПереработкаПоГрафикуПроизводства.Регистратор) КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.ПереработкаПоГрафикуПроизводства КАК ПереработкаПоГрафикуПроизводства
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЭтапыПереработки КАК ЭтапыПереработки
	|			ПО ПереработкаПоГрафикуПроизводства.ЗаказНаПроизводство = ЭтапыПереработки.Распоряжение
	|				И ПереработкаПоГрафикуПроизводства.КодСтрокиЭтапыГрафик = ЭтапыПереработки.КодСтрокиЭтапыГрафик
	|	ГДЕ
	|		ПереработкаПоГрафикуПроизводства.Активность
	|		И ПереработкаПоГрафикуПроизводства.Регистратор ССЫЛКА Документ.ЗаказПереработчику
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПереработкаПоГрафикуПроизводства.ЗаказНаПроизводство,
	|		ПереработкаПоГрафикуПроизводства.КодСтрокиЭтапыГрафик) КАК ПереработкаПоГрафикуПроизводства
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ЗаказПереработчику
	|	ПО ПереработкаПоГрафикуПроизводства.Регистратор = ЗаказПереработчику.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереработкаПоГрафикуПроизводства.Регистратор КАК Регистратор,
	|	ПереработкаПоГрафикуПроизводства.Период      КАК Период,
	|	ОстаткиЭтапов.Распоряжение                   КАК Распоряжение,
	|	ОстаткиЭтапов.КодСтрокиПродукция             КАК КодСтрокиПродукция,
	|	ОстаткиЭтапов.КодСтрокиЭтапыГрафик           КАК КодСтрокиЭтапыГрафик,
	|	ОстаткиЭтапов.Этап                           КАК Этап,
	|	ОстаткиЭтапов.Подразделение                  КАК Подразделение,
	|	ОстаткиЭтапов.ЗапланированоОстаток           КАК ЗапланированоОстаток
	|ИЗ
	|	ВТ_ПереработкаПоГрафикуПроизводства КАК ПереработкаПоГрафикуПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОстаткиЭтапов КАК ОстаткиЭтапов
	|		ПО ПереработкаПоГрафикуПроизводства.Распоряжение = ОстаткиЭтапов.Распоряжение
	|			И ПереработкаПоГрафикуПроизводства.КодСтрокиЭтапыГрафик = ОстаткиЭтапов.КодСтрокиЭтапыГрафик
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	Распоряжение,
	|	КодСтрокиПродукция,
	|	КодСтрокиЭтапыГрафик,
	|	Этап,
	|	Подразделение");
	
	Запрос.УстановитьПараметр("ТаблицаЭтаповГрафика", ТаблицаЭтаповГрафика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЕстьЗаписиВВыборке = Выборка.Следующий();
	
	НаборЗаписей = РегистрыНакопления.ЭтапыПроизводства.СоздатьНаборЗаписей();
	
	Пока ЕстьЗаписиВВыборке Цикл

		ТекРегистратор = Выборка.Регистратор;
		
		// Чтение существующего набора
		НаборЗаписей.Отбор.Регистратор.Установить(ТекРегистратор);
		НаборЗаписей.Прочитать();
		
		Пока ЕстьЗаписиВВыборке И Выборка.Регистратор = ТекРегистратор Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			ЗаполнитьЗначенияСвойств(Запись, Выборка, "Период,Распоряжение,КодСтрокиПродукция,КодСтрокиЭтапыГрафик,Этап,Подразделение");
			
			Запись.ПроизводствоНаСтороне = Истина;
			Запись.Выполнено             = Выборка.ЗапланированоОстаток;
			
			// Переход к следующей записи в выборке.
			ЕстьЗаписиВВыборке = Выборка.Следующий();

		КонецЦикла;
		
		// Запись и очистка набора.
		Если ВызовИзОбработчикаОбновления Тогда

			Попытка

				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);

			Исключение

				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Регистратор);
				ВызватьИсключение;

			КонецПопытки;

		Иначе
			
			НаборЗаписей.Записать(Истина);
			
		КонецЕсли;
		
		НаборЗаписей.Очистить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыНакопления.ПереработкаПоГрафикуПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("5671a5eb-1539-4f50-b217-eff6c59653ea");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ПереработкаПоГрафикуПроизводства.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Документ.ЗаказПереработчику,"
	//++ Локализация
		+ "РегистрНакопления.ПереработкаПоГрафикуПроизводства"
	//-- Локализация
	;
	Обработчик.ИзменяемыеОбъекты = ""
	//++ Локализация
		+ "РегистрНакопления.ПереработкаПоГрафикуПроизводства"
	//-- Локализация
	;
	Обработчик.БлокируемыеОбъекты = "";
	Обработчик.Комментарий = НСтр("ru='Обновляет движения по данным документов ""Заказ переработчику"". Пока обработчик не выполнен, возможны ошибки при работе с документами ""Заказ переработчику"".';uk='Оновлює рухи за даними документів ""Замовлення переробнику"". Поки обробник не виконаний, можливі помилки під час роботи з документами ""Замовлення переробнику"".'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказПереработчику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИмяРегистра = "ПереработкаПоГрафикуПроизводства";
	ПолноеИмяРегистра = "РегистрНакопления.ПереработкаПоГрафикуПроизводства";
	СписокДокументов = Новый Массив;
	СписокДокументов.Добавить("Документ.ЗаказПереработчику");
	
	Для Каждого ПолноеИмяДокумента Из СписокДокументов Цикл
		ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];
		ТекстЗапросаМеханизмаПроведения = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра);
		Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
			ТекстЗапросаМеханизмаПроведения,
			ПолноеИмяРегистра,
			ПолноеИмяДокумента);
		
		ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ПереработкаПоГрафикуПроизводства";
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ЗаказПереработчику");
	
	ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(Регистраторы, ПолноеИмяРегистра, Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21