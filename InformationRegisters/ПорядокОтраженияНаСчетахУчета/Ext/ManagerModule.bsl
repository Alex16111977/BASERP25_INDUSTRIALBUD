#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает значения счетов учета по заданным измерениям.
//
//	Параметры:
//		СтруктураИзмерений - Структура - измерения регистра, по которым производится запись, в случае записи счетов учета
//			по НСИ (ГФУ номенклатуры, кассы и т.д.) заполняется лишь "АналитикаУчета".
//			см. НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
//		СтруктураСчетов - Структура - все записываемые счета, структура со следующими ключами и значениями:
//			* Ключ - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл),
//					см. СписокСчетовУчетаПоРазделуУчета();
//			* Значение - ПланСчетов.Хозрасчетный - значение счета;
//		Отказ - Булево - признак успешной записи.
//
Процедура СохранитьЗначенияСчетовУчета(СтруктураИзмерений, СтруктураСчетов, Отказ) Экспорт
	
	Если СтруктураИзмерений.МестоУчета <> Неопределено И СтруктураИзмерений.МестоУчета.Пустая() Тогда
		СтруктураИзмерений.МестоУчета = Неопределено;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		ЗначенияБлокировки = Новый Структура;
		Для каждого ЭлементИзмерения Из СтруктураИзмерений Цикл
			ЗначенияБлокировки.Вставить(ЭлементИзмерения.Ключ, ЭлементИзмерения.Значение);
		КонецЦикла;
		ПараметрыБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрСведений", "ПорядокОтраженияНаСчетахУчета");
		ОбщегоНазначенияБПВызовСервера.УстановитьУправляемуюБлокировку(ПараметрыБлокировки, ЗначенияБлокировки);
		
		Для каждого Счет Из СтруктураСчетов Цикл
			
			ПорядокОтраженияСчетов = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
			
			Для каждого ЭлементИзмерения Из СтруктураИзмерений Цикл
				ПорядокОтраженияСчетов.Отбор[ЭлементИзмерения.Ключ].Установить(ЭлементИзмерения.Значение);
			КонецЦикла;
			
			Если СтрНачинаетсяС(Счет.Ключ, "Субконто_") Или Счет.Значение.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗаписьСчета = ПорядокОтраженияСчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗаписьСчета, СтруктураИзмерений);
			ИмяСчета = СтрЗаменить(Счет.Ключ, "СчетУчета_", "");
			ЗаписываемыйВидСчета = Перечисления.ВидыСчетовРеглУчета[ИмяСчета];
			ПорядокОтраженияСчетов.Отбор["ВидСчета"].Установить(ЗаписываемыйВидСчета);
			НоваяЗаписьСчета.ВидСчета = ЗаписываемыйВидСчета;
			НоваяЗаписьСчета.СчетУчета = Счет.Значение;
			ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИмяСчета);
			Если ЗначениеЗаполнено(ИмяСубконто) И СтруктураСчетов.Свойство("Субконто_"+ИмяСубконто) Тогда
				НоваяЗаписьСчета.Субконто = СтруктураСчетов["Субконто_"+ИмяСубконто];
			КонецЕсли;
			
			ПорядокОтраженияСчетов.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИмяСобытия = НСтр("ru='Запись порядка отражения на счетах учета';uk='Запис порядку відображення на рахунках обліку'",ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает значения счетов учета для НСИ (ГФУ номенклатуры, кассы и т.д.).
//
//	Параметры:
//		АналитикаУчета - ОпределяемыйТип.НастройкаСчетовУчетаАналитикаУчета - НСИ настройки счетов учета;
//		СчетаУчета - Массив - массив имен настроек счетов учета (идентично имени счета в перечислении ВидыСчетовРегл),
//			  см. СписокСчетовУчетаПоРазделуУчета(). Если не заполнен - заполняется всеми доступными счетами для данной НСИ;
//		Субконто - Массив - массив имен настроек субконто счетов учета,
//			  см. СписокСубконтоПоРазделуУчета(). Если не заполнен - заполняется всеми доступными субконто для данной НСИ;
//
//	Возвращаемое значение:
//		Структура - структура со следующими ключами и значениями:
//			* Ключ - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл),
//					см. СписокСчетовУчетаПоРазделуУчета() с префиксом "СчетУчета_", так же может содержать
//					имя настройки субконто счета учета, см. СписокСубконтоПоРазделуУчета() с префиксом "Субконто_";
//			* Значение - ПланСчетов.Хозрасчетный - если в качестве ключа указан счет учета,
//					- ЛюбаяСсылка - если в качестве ключа указано субконто.
//
Функция СтруктураЗначенийПоАналитикеУчета(АналитикаУчета, СчетаУчета = Неопределено, Субконто = Неопределено) Экспорт
	
	Если СчетаУчета = Неопределено Тогда
		РазделыУчета = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.РазделыУчетаПоАналитикеУчета(АналитикаУчета);
		ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(РазделыУчета);
		НастройкиРазделов = ПараметрыНастройкиСчетовУчета.НастройкиРазделов;
		СчетаУчета = Новый Массив;
		Субконто = Новый Массив;
		
		Для каждого НастройкаРаздела Из НастройкиРазделов Цикл
			СчетаРаздела = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.СчетаУчета);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчета, СчетаРаздела, Истина);
			СубконтоРаздела = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.Субконто);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Субконто, СубконтоРаздела, Истина);
		КонецЦикла;
	КонецЕсли;
	
	Если Субконто = Неопределено Тогда
		Субконто = Новый Массив;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	Для каждого СчетУчета Из СчетаУчета Цикл
		СтруктураВозврата.Вставить("СчетУчета_" + СчетУчета);
	КонецЦикла;
	Для каждого ИмяСубконто Из Субконто Цикл
		СтруктураВозврата.Вставить("Субконто_" + ИмяСубконто);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СоставитьДинамическийТекстЗапросаПолученияСчетовУчета(СчетаУчета);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеВыборкиПереопределяемый",
		"ПорядокОтраженияНаСчетахУчета.Организация = &Организация И ПорядокОтраженияНаСчетахУчета.АналитикаУчета = &АналитикаУчета И ПорядокОтраженияНаСчетахУчета.МестоУчета = &МестоУчета");
	Запрос.УстановитьПараметр("АналитикаУчета", АналитикаУчета);
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПустаяСсылка());
	Запрос.УстановитьПараметр("МестоУчета", Неопределено);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Добавляет временную таблицу с данными по счетам учета с учетом настроек исключений.
//
//	Параметры:
//		ВременныеТаблицы - МенеджерВременныхТаблиц - менеджер временных таблиц, куда необходимо добавить таблицу с данными настроек счетов учета.
//			должен содержать таблицу в параметре "ИмяИсходнойВременнойТаблицы";
//		СчетаУчета - Массив, Строка - массив строк имен счетов учета, или строка имен, разделенных запятыми.
//			* Значение - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл,
//					см. СписокСчетовУчетаПоРазделуУчета();
//		ИмяИсходнойВременнойТаблицы - Строка - имя временной таблицы, откуда необходимо брать информацию по измерениям,
//			должна содержать следующие поля:
//				* Организация - СправочникСсылка.Организации - организация для которой необходимо получить значение настройки счетов учета;
//				* АналитикаУчета - ОпределяемыйТип.НастройкаСчетовУчетаАналитикаУчета - НСИ для которой необходимо получить значение настройки счетов учета;
//				* МестоУчета - ОпределяемыйТип.НастройкаСчетовУчетаМестоУчета - место учета для которого необходимо получить значение настройки счетов учета;
//		ИмяТаблицыРезультата - Строка - Имя временной таблицы, куда необходимо поместить результат. Получается добавлением к ВТ ИмяИсходнойВременнойТаблицы
//			счетов учета с именами полей вида "СчетУчета_%ИмяВидаСчетаУчета%".
//
Процедура ДобавитьВременнуюТаблицуПорядкаОтраженияСчетовУчета(ВременныеТаблицы, СчетаУчета, ИмяИсходнойВременнойТаблицы = "ИсходнаяТаблица", ИмяТаблицыРезультата = "ПорядокОтраженияНаСчетахУчета") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.Текст = ТекстТаблицыПорядкаОтраженияСчетовУчета(СчетаУчета, ИмяИсходнойВременнойТаблицы, ИмяТаблицыРезультата);
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает текст формирования временной таблицы с данными по счетам учета с учетом настроек исключений.
//
//	Параметры:
//		СчетаУчета - Массив, Строка - массив строк имен счетов учета, или строка имен, разделенных запятыми.
//			* Значение - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл,
//					см. СписокСчетовУчетаПоРазделуУчета();
//		ИмяИсходнойВременнойТаблицы - Строка - имя временной таблицы, откуда необходимо брать информацию по измерениям,
//			должна содержать следующие поля:
//				* Организация - СправочникСсылка.Организации - организация для которой необходимо получить значение настройки счетов учета;
//				* АналитикаУчета - ОпределяемыйТип.НастройкаСчетовУчетаАналитикаУчета - НСИ для которой необходимо получить значение настройки счетов учета;
//				* МестоУчета - ОпределяемыйТип.НастройкаСчетовУчетаМестоУчета - место учета для которого необходимо получить значение настройки счетов учета;
//		ИмяТаблицыРезультата - Строка - Имя временной таблицы, куда необходимо поместить результат. Получается добавлением к ВТ ИмяИсходнойВременнойТаблицы
//			счетов учета с именами полей вида "СчетУчета_%ИмяВидаСчетаУчета%".
//
//	Возвращаемое значение:
//		Строка - текст запроса для встраивания в основной текст запроса.
//
Функция ТекстТаблицыПорядкаОтраженияСчетовУчета(СчетаУчета, ИмяИсходнойВременнойТаблицы = "ИсходнаяТаблица", ИмяТаблицыРезультата = "ПорядокОтраженияНаСчетахУчета") Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИсходныеДанные.Организация,
	|	ИсходныеДанные.АналитикаУчета,
	|	ИсходныеДанные.МестоУчета,
	|	&ПоляПереопределяемый
	|ПОМЕСТИТЬ ИмяВременнойТаблицыПереопределяемый
	|ИЗ
	|	ИмяИсходнойВременнойТаблицыПереопределяемый КАК ИсходныеДанные
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Перечисление.ВидыСчетовРеглУчета КАК ВидыСчетовРеглУчета
	|	ПО 
	|		ВидыСчетовРеглУчета.Ссылка В (&ВидыСчетов)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый КАК ПорядокОтраженияНаСчетахУчетаПереопределяемый
	|	ПО
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый.ВидСчета = ВидыСчетовРеглУчета.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Организация,
	|	ИсходныеДанные.АналитикаУчета,
	|	ИсходныеДанные.МестоУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИсходныеДанные.Организация,
	|	ИсходныеДанные.АналитикаУчета,
	|	ИсходныеДанные.МестоУчета";
	
	ШаблонСчета = "МАКСИМУМ(ВЫБОР
	|		КОГДА ВидыСчетовРеглУчета.Ссылка = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|			ТОГДА ПорядокОтраженияНаСчетахУчетаПереопределяемый.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ) КАК СчетУчета_%1";
	ШаблонСубконто = "МАКСИМУМ(ВЫБОР
	|		КОГДА %1
	|			ТОГДА ПорядокОтраженияНаСчетахУчетаПереопределяемый.Субконто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК Субконто_%2";
	ШаблонПеречисленияСчетовДляСубконто = "ВидыСчетовРеглУчета.Ссылка = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)";
	
	МассивТекстовПолей = Новый Массив;
	МассивТекстовВидовСчетов = Новый Массив;
	
	СоответствиеСубконтоИСчетов = Новый Соответствие;
	Если ТипЗнч(СчетаУчета) = Тип("Строка") Тогда
		СчетаУчета = СтрРазделить(СчетаУчета, ",");
	КонецЕсли;
	Для каждого СчетУчета Из СчетаУчета Цикл
		ТекстСчета = СтрШаблон(ШаблонСчета, СчетУчета);
		МассивТекстовПолей.Добавить(ТекстСчета);
		
		ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(СчетУчета);
		Если ЗначениеЗаполнено(ИмяСубконто) Тогда
			Если СоответствиеСубконтоИСчетов.Получить(ИмяСубконто) = Неопределено Тогда
				СоответствиеСубконтоИСчетов.Вставить(ИмяСубконто, Новый Массив);
			КонецЕсли;
			ЗначениеСоответствияСубконто = СоответствиеСубконтоИСчетов.Получить(ИмяСубконто);
			ЗначениеСоответствияСубконто.Добавить(СтрШаблон(ШаблонПеречисленияСчетовДляСубконто, СчетУчета));
		КонецЕсли;
		
		МассивТекстовВидовСчетов.Добавить(СтрШаблон("ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)", СчетУчета));
		
	КонецЦикла;
	
	Если СоответствиеСубконтоИСчетов.Количество() Тогда
		Для каждого ЭлементСоответствия Из СоответствиеСубконтоИСчетов Цикл
			Разделитель = Символы.ПС + Символы.Таб + Символы.Таб + Символы.Таб + "ИЛИ" + " ";
			ТекстПеречисленияСчетов = СтрСоединить(ЭлементСоответствия.Значение, Разделитель);
			ТекстПоля = СтрШаблон(ШаблонСубконто, ТекстПеречисленияСчетов, ЭлементСоответствия.Ключ);
			МассивТекстовПолей.Добавить(ТекстПоля);
		КонецЦикла;
	КонецЕсли;
	
	ТекстСчетовУчета = СтрСоединить(МассивТекстовПолей, ","+Символы.ПС+Символы.Таб);
	ТекстВидовСчетов = СтрСоединить(МассивТекстовВидовСчетов, ","+Символы.ПС+Символы.Таб);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляПереопределяемый", ТекстСчетовУчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидыСчетов", ТекстВидовСчетов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицыПереопределяемый", ИмяТаблицыРезультата);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяИсходнойВременнойТаблицыПереопределяемый", ИмяИсходнойВременнойТаблицы);
	
	#Область ПереопределениеТекстаЗапросаВЗависимостиОтРазделов
	РазделыУчета = Новый Массив;
	Для каждого СчетУчета Из СчетаУчета Цикл
		РазделыУчета.Добавить(НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоИмениСчета(СчетУчета));
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(РазделыУчета);
	ПутиКИзмерениям = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
	ПутиКИзмерениям.Организация = "ИсходныеДанные.Организация";
	ПутиКИзмерениям.АналитикаУчета = "ИсходныеДанные.АналитикаУчета";
	ПутиКИзмерениям.МестоУчета = "ИсходныеДанные.МестоУчета";
	ПереопределитьТекстЗапросаПоРазделамУчета(ТекстЗапроса, РазделыУчета, ПутиКИзмерениям);
	#КонецОбласти
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Переопределяет обращение к временной таблице "ПорядокОтраженияНаСчетахУчетаПереопределяемый" на обращение к регистру сведений, с учетом иерархии настроек.
//
//	Параметры:
//		ТекстЗапроса - Строка - текст запроса, в котором встречается обращение к таблице которую необходимо переопределить.
//			Условия:
//			-	в тексте запроса должно быть только одно обращение к таблице "ПорядокОтраженияНаСчетахУчетаПереопределяемый", в противном случае текст необходимо разделить;
//			-	в тексте запроса допускается отбор по виду счета только на равенство, например: "ПорядокОтраженияНаСчетахУчетаПереопределяемый.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)".
//			-	в тексте запроса должны быть перечислены все измерения регистра сведений настроек счетов, обращения к измерениям должно находиться справа, например:
//					"И ПорядокОтраженияНаСчетахУчетаПереопределяемый.Организация = ПсевдонимТаблицыСвязи.ИмяПоляОрганизация".
//
Процедура ПереопределитьВТекстеЗапросаПорядокОтраженияСчетаУчета(ТекстЗапроса) Экспорт
	
	ПозицияОпределенияПорядкаОтраженияВЗапросе = СтрНайти(ТекстЗапроса, "ПорядокОтраженияНаСчетахУчетаПереопределяемый КАК ПорядокОтраженияНаСчетахУчетаПереопределяемый");
	Если ПозицияОпределенияПорядкаОтраженияВЗапросе = 0 Тогда
		// В тексте запроса нет обращений к настройкам порядка отражения счетов, не переопределяем текст запроса.
		Возврат;
	КонецЕсли;
	
	ЗначениеВидаСчета = ЗначениеРавенстваИзмеренияПорядкаОтражения(ТекстЗапроса, "ВидСчета", ПозицияОпределенияПорядкаОтраженияВЗапросе);
	Если ЗначениеВидаСчета = Неопределено И СтрНачинаетсяС(ЗначениеВидаСчета, "ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.") Тогда
		// Не нашли регламентируемое обращение к виду счета, не переопределяем текст запроса.
		Возврат;
	КонецЕсли;
	
	ИмяВидаСчета = СтрЗаменить(ЗначениеВидаСчета, "ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.", "");
	ИмяВидаСчета = СтрЗаменить(ИмяВидаСчета, ")", "");
	
	РазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоИмениСчета(ИмяВидаСчета);
	Если РазделУчета = Неопределено Тогда
		// Счет учета является не настраиваемым, не переопределяем текст запроса.
		Возврат;
	КонецЕсли;
	
	ПутиКИзмерениям = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
	ПутиКИзмерениям.Организация = ЗначениеРавенстваИзмеренияПорядкаОтражения(ТекстЗапроса, "Организация", ПозицияОпределенияПорядкаОтраженияВЗапросе);
	ПутиКИзмерениям.АналитикаУчета = ЗначениеРавенстваИзмеренияПорядкаОтражения(ТекстЗапроса, "АналитикаУчета", ПозицияОпределенияПорядкаОтраженияВЗапросе);
	ПутиКИзмерениям.МестоУчета = ЗначениеРавенстваИзмеренияПорядкаОтражения(ТекстЗапроса, "МестоУчета", ПозицияОпределенияПорядкаОтраженияВЗапросе);
	
	// Проверим регламентируемое обращение к измерениям в запросе, в случае ошибки - не переопределяем текст запроса:
	Для каждого Измерение Из ПутиКИзмерениям Цикл
		Если Измерение.Значение = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	РазделыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РазделУчета);
	
	// Заменим в запросе обращение к переопределяемой таблице настроек обращением на основании которого будет переопределяться запрос по разделам учета
	ПозицияОкончанияОбращенияКПереопределениюПорядкаОтражения = СтрНайти(ТекстЗапроса, Символы.ПС, , ПозицияОпределенияПорядкаОтраженияВЗапросе, 5);
	ТекстОбращенияКПереопределениюПорядкаОтражения = Сред(ТекстЗапроса, ПозицияОпределенияПорядкаОтраженияВЗапросе, ПозицияОкончанияОбращенияКПереопределениюПорядкаОтражения - ПозицияОпределенияПорядкаОтраженияВЗапросе);
	ТекстЗамены = "Перечисление.ВидыСчетовРеглУчета КАК ВидыСчетовРеглУчета
	|	ПО 
	|		ВидыСчетовРеглУчета.Ссылка = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый КАК ПорядокОтраженияНаСчетахУчетаПереопределяемый
	|	ПО
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый.ВидСчета = ВидыСчетовРеглУчета.Ссылка";
	ТекстЗамены = СтрШаблон(ТекстЗамены, ИмяВидаСчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстОбращенияКПереопределениюПорядкаОтражения, ТекстЗамены);
	
	ПереопределитьТекстЗапросаПоРазделамУчета(ТекстЗапроса, РазделыУчета, ПутиКИзмерениям);
	
КонецПроцедуры

// Возвращает значения счетов учета и субконто для раздела учета и измерений регистра. Значение возвращается с учетом приоритетов:
// 	Если не будет найдено соответствие по переданным измерениям, включая организацию и место учета, будет подставлено значение для
// 	измерений только с учетом Аналитики и организации, если и в этом случае не будет найдено значений, подставится значение только по аналитике.
//
//	Параметры:
//		РазделУчета - Строка - имя раздела учета, см. СписокРазделовСчетовУчета();
//		СтруктураИзмерений - Структура - измерения регистра, по которым производится запись, в случае записи счетов учета
//			по НСИ (ГФУ номенклатуры, кассы и т.д.) заполняется лишь "АналитикаУчета".
//			см. НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
//		СчетаУчета - Массив, Строка - массив имен настроек счетов учета (идентично имени счета в перечислении ВидыСчетовРегл),
//			  см. СписокСчетовУчетаПоРазделуУчета(). Если передана строка - будет выполнено преобразование в массив с разделителем ",".
//			  Если не передано - заполняется всеми доступными счетами для данному разделу учета; 
//
//	Возвращаемое значение:
//		Структура - структура со следующими ключами и значениями:
//			* Ключ - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл),
//					см. СписокСчетовУчетаПоРазделуУчета(), так же может содержать
//					имя настройки субконто счета учета, см. СписокСубконтоПоРазделуУчета();
//			* Значение - ПланСчетов.Хозрасчетный - если в качестве ключа указан счет учета,
//					- ЛюбаяСсылка - если в качестве ключа указано субконто.
//
Функция НастройкаОтраженияРаздела(РазделУчета, СтруктураИзмерений, СчетаУчета = Неопределено) Экспорт
	
	Если СчетаУчета = Неопределено Тогда
		СчетаУчета = Новый Массив;
		ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РазделУчета));
		НастройкаРаздела = ПараметрыНастройкиСчетовУчета.НастройкиРазделов.Получить(0);
		СчетаУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.СчетаУчета);
	ИначеЕсли ТипЗнч(СчетаУчета) = Тип("Строка") Тогда
		СчетаУчета = СтрРазделить(СчетаУчета, ",");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураИзмерений.Организация);
	Запрос.УстановитьПараметр("АналитикаУчета", СтруктураИзмерений.АналитикаУчета);
	Запрос.УстановитьПараметр("МестоУчета", СтруктураИзмерений.МестоУчета);
	ВидыСчетов = Новый Массив;
	Для каждого ИмяСчета Из СчетаУчета Цикл
		ВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета[ИмяСчета]);
	КонецЦикла;
	Запрос.УстановитьПараметр("ВидыСчетов", ВидыСчетов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(НастройкиПоМестуУчета.Организация, ЕСТЬNULL(НастройкиПоОрганизации.Организация, ЕСТЬNULL(НастройкиПоАналитике.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))) КАК Организация,
	|	ЕСТЬNULL(НастройкиПоМестуУчета.АналитикаУчета, ЕСТЬNULL(НастройкиПоОрганизации.АналитикаУчета, ЕСТЬNULL(НастройкиПоАналитике.АналитикаУчета, НЕОПРЕДЕЛЕНО))) КАК АналитикаУчета,
	|	ЕСТЬNULL(НастройкиПоМестуУчета.МестоУчета, ЕСТЬNULL(НастройкиПоОрганизации.МестоУчета, ЕСТЬNULL(НастройкиПоАналитике.МестоУчета, НЕОПРЕДЕЛЕНО))) КАК МестоУчета,
	|	ВидыСчетов.Ссылка КАК ВидСчета,
	|	ЕСТЬNULL(НастройкиПоМестуУчета.СчетУчета, ЕСТЬNULL(НастройкиПоОрганизации.СчетУчета, ЕСТЬNULL(НастройкиПоАналитике.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)))) КАК СчетУчета,
	|	ЕСТЬNULL(НастройкиПоМестуУчета.Субконто, ЕСТЬNULL(НастройкиПоОрганизации.Субконто, ЕСТЬNULL(НастройкиПоАналитике.Субконто, НЕОПРЕДЕЛЕНО))) КАК Субконто
	|ИЗ
	|	Перечисление.ВидыСчетовРеглУчета КАК ВидыСчетов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоМестуУчета
	|	ПО 
	|		НастройкиПоМестуУчета.ВидСчета = ВидыСчетов.Ссылка
	|		И НастройкиПоМестуУчета.Организация = &Организация
	|		И НастройкиПоМестуУчета.АналитикаУчета = &АналитикаУчета
	|		И НастройкиПоМестуУчета.МестоУчета = &МестоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоОрганизации
	|	ПО 
	|		НастройкиПоОрганизации.ВидСчета = ВидыСчетов.Ссылка
	|		И НастройкиПоОрганизации.Организация = &Организация
	|		И НастройкиПоОрганизации.АналитикаУчета = &АналитикаУчета
	|		И НастройкиПоОрганизации.МестоУчета = НЕОПРЕДЕЛЕНО
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитике
	|	ПО 
	|		НастройкиПоАналитике.ВидСчета = ВидыСчетов.Ссылка
	|		И НастройкиПоАналитике.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитике.АналитикаУчета = &АналитикаУчета
	|		И НастройкиПоАналитике.МестоУчета = НЕОПРЕДЕЛЕНО
	|ГДЕ
	|	ВидыСчетов.Ссылка В(&ВидыСчетов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураВозврата = Новый Структура;
	Пока Выборка.Следующий() Цикл
		ИмяВидаСчета = XMLСтрока(Выборка.ВидСчета);
		СтруктураВозврата.Вставить(ИмяВидаСчета, Выборка.СчетУчета);
		ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИмяВидаСчета);
		Если ЗначениеЗаполнено(ИмяСубконто) Тогда
			СтруктураВозврата.Вставить(ИмяСубконто, Выборка.Субконто);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает массив разделов учета, которые могут применяться для переденной Аналитики (НСИ - ГФУ номенклатуры, кассы и т.д.).
//
//	Параметры:
//		АналитикаУчета - ОпределяемыйТип.НастройкаСчетовУчетаАналитикаУчета - НСИ настройки счетов учета;
//		АнализироватьАналитику - Булево - признак выполнения запроса к реквизитам аналитики, если для данной аналитики используется
//			сразу несколько разделов, для получения конкретного значения.
//
//	Возвращаемое значение:
//		Массив - Строка - массив имен разделов настроек счетов учета, см. СписокРазделовСчетовУчета().
// 
Функция РазделыУчетаПоАналитикеУчета(АналитикаУчета, АнализироватьАналитику = Ложь) Экспорт
	
	СоответствиеТиповАналитикУчетаРазделамУчета = Новый Соответствие;

	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"), "Расходы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов"), "Доходы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"), "ДенежныеСредства");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.Кассы"), "ДенежныеСредства");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаНоменклатуры"), "НоменклатураСобственная,НоменклатураПереданная,НоменклатураПринятая");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаРасчетов"), "РасчетыСПартнерами");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.Резервы"), "Резервы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.КатегорииЭксплуатации"), "ТМЦВЭксплуатации");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаВнеоборотныхАктивов"), "ВнеоборотныеАктивы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ВидыПодарочныхСертификатов"), "ПодарочныеСертификаты");
	
	РазделыУчета = СоответствиеТиповАналитикУчетаРазделамУчета.Получить(ТипЗнч(АналитикаУчета));
	
	Если РазделыУчета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РазделыУчета = "РасчетыСПартнерами" Тогда
		Если АнализироватьАналитику Тогда
			РазделыУчета = РазделыУчетаПоВидуРасчетов(АналитикаУчета);
		Иначе
			РазделыУчета = СтрСоединить(Справочники.ГруппыФинансовогоУчетаРасчетов.ВидыРасчетов(), ",");
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрРазделить(РазделыУчета, ",");
	
КонецФункции

// Возвращает список счетов учета для переданного раздела.
//
//	Параметры:
//		РазделУчета - Строка - имя раздела учета, см. СписокРазделовСчетовУчета();
//
//	Возвращаемое значение:
//		СписокЗначений - список значений со следующими элементами:
//			Значение - Строка - Имя настройки счета учета (идентично имени счета в перечислении ВидыСчетовРегл);
//			Представление - Строка - представление этой настройки в интерфейсе;
//			Пометка - Булево - Признак применения этой настройки, как правило зависит от ФО, если установлен в ЛОЖЬ, такая
//				настройка будет скрываться с формы.
//
Функция СписокСчетовУчетаПоРазделуУчета(РазделУчета) Экспорт
    
    
	СчетаУчетаРегл = Новый СписокЗначений;
	
	
	Если РазделУчета = "Расходы" Тогда
		СчетаУчетаРегл.Добавить("Расходы", НСтр("ru='Счет учета';uk='Рахунок'"), Истина);
	КонецЕсли;
	
	Если РазделУчета = "Доходы" Тогда
		СчетаУчетаРегл.Добавить("Доходы", НСтр("ru='Счет учета';uk='Рахунок'"), Истина);
	КонецЕсли;
	
	Если РазделУчета = "ДенежныеСредства" Тогда
		СчетаУчетаРегл.Добавить("ДенежныеСредства", НСтр("ru='Счет учета';uk='Рахунок'"), Истина);
	КонецЕсли;
	
	Если РазделУчета = "НоменклатураСобственная" Или РазделУчета = "Номенклатура" Тогда
		
		СчетаУчетаРегл.Добавить("НаСкладе", НСтр("ru='На складе';uk='На складі'"), Истина);
        СчетаУчетаРегл.Добавить("ВыручкаОтПродаж", НСтр("ru='Выручки';uk='Виторгу'"), Истина);
        СчетаУчетаРегл.Добавить("СебестоимостьПродаж", НСтр("ru='Себестоимости';uk='Собівартості'"), Истина);
        СчетаУчетаРегл.Добавить("ВычетыИзДоходов", НСтр("ru='Вычетов из доходов';uk='Вирахувань з доходів'"), Истина);
	КонецЕсли;
	
	Если РазделУчета = "НоменклатураПереданная" Или РазделУчета = "Номенклатура" Тогда
		СчетаУчетаРегл.Добавить("ЗатратыНаПриобретениеТМЦ", НСтр("ru='Затраты на приобретение';uk='Витрати на придбання'"), ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков"));
		СчетаУчетаРегл.Добавить("ПередачаВПереработку", НСтр("ru='В переработке';uk='У переробці'"), ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне"));
		ИспользоватьНоменклатуруПереданную = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
		СчетаУчетаРегл.Добавить("НоменклатураПереданная", НСтр("ru='Счет учета';uk='Рахунок'"), ИспользоватьНоменклатуруПереданную);
	КонецЕсли;
	
	Если РазделУчета = "НоменклатураПринятая" Или РазделУчета = "Номенклатура" Тогда
		ИспользоватьПроизводствоИзДавальческогоСырья = Ложь;
		//++ НЕ УТКА
		ИспользоватьПроизводствоИзДавальческогоСырья = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья");
		//-- НЕ УТКА
		СчетаУчетаРегл.Добавить("ТМЦНаСкладах", НСтр("ru='На складе';uk='На складі'"), ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранениеВПроцессеЗакупки") ИЛИ ИспользоватьПроизводствоИзДавальческогоСырья);
		СчетаУчетаРегл.Добавить("ТМЦВПроизводстве", НСтр("ru='В производстве';uk='У виробництві'"), ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранениеВПроцессеЗакупки") ИЛИ ИспользоватьПроизводствоИзДавальческогоСырья);
		СчетаУчетаРегл.Добавить("ТМЦУХранителей", НСтр("ru='У хранителей';uk='У зберігачів'"), ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи"));
		СчетаУчетаРегл.Добавить("ТМЦКСписанию", НСтр("ru='К списанию';uk='До списання'"), ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранениеВПроцессеЗакупки"));
		СчетаУчетаРегл.Добавить("МатериалыПринятыеВПереработку", НСтр("ru='На складе';uk='На складі'"), ИспользоватьПроизводствоИзДавальческогоСырья);
		СчетаУчетаРегл.Добавить("МатериалыПринятыеВПереработкуВПроизводстве", НСтр("ru='В производстве';uk='У виробництві'"), ИспользоватьПроизводствоИзДавальческогоСырья);
		СчетаУчетаРегл.Добавить("ТоварыНаСкладе", НСтр("ru='На складе';uk='На складі'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках"));
		СчетаУчетаРегл.Добавить("ТоварыПереданныеНаКомиссию", НСтр("ru='Переданные на комиссию';uk='Передані на комісію'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСКлиентами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКлиентами", НСтр("ru='Долг';uk='Борг'"), Истина);
		СчетаУчетаРегл.Добавить("АвансыПолученные", НСтр("ru='Авансы';uk='Аванси'"), Истина);
		СчетаУчетаРегл.Добавить("РасчетыСКлиентамиТара", НСтр("ru='Тара';uk='Тара'"), ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСПоставщиками" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСПоставщиками", НСтр("ru='Долг';uk='Борг'"), Истина);
		СчетаУчетаРегл.Добавить("АвансыВыданные", НСтр("ru='Авансы';uk='Аванси'"), Истина);
		СчетаУчетаРегл.Добавить("РасчетыСПоставщикамиТара", НСтр("ru='Тара';uk='Тара'"), ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару"));
		СчетаУчетаРегл.Добавить("НеотфактурованныеПоставки", НСтр("ru='Неотфакурованные поставки';uk='Невідфактуровані поставки'"), ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки"));
   	КонецЕсли;
	
	Если РазделУчета = "РасчетыСКомиссионерами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКлиентами", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("АвансыПолученные", НСтр("ru='Авансы';uk='Аванси'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("РасчетыСПоставщиками", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("АвансыВыданные", НСтр("ru='Авансы';uk='Аванси'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСКомитентами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКлиентами", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("АвансыПолученные", НСтр("ru='Авансы';uk='Аванси'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("РасчетыСПоставщиками", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("АвансыВыданные", НСтр("ru='Авансы';uk='Аванси'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("ТоварыКомитентовКОформлениюПоПродаже", НСтр("ru='По продаже';uk='По продажу'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСКомитентамиПоЗакупке" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКлиентами", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьОказаниеАгентскихУслугПриЗакупке"));
		СчетаУчетаРегл.Добавить("АвансыПолученные", НСтр("ru='Авансы';uk='Аванси'"), ПолучитьФункциональнуюОпцию("ИспользоватьОказаниеАгентскихУслугПриЗакупке"));
		СчетаУчетаРегл.Добавить("ТоварыКомитентовКОформлениюПоЗакупке", НСтр("ru='По закупке';uk='По купівлі'"), ПолучитьФункциональнуюОпцию("ИспользоватьОказаниеАгентскихУслугПриЗакупке"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСКредиторами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиОсновнойДолг", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиПроценты", НСтр("ru='Проценты';uk='Відсотки'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиКомиссия", НСтр("ru='Комиссия';uk='Комісія'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	КонецЕсли;
	
	Если РазделУчета = "РасчетыСДебиторами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиОсновнойДолг", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиПроценты", НСтр("ru='Проценты';uk='Відсотки'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиКомиссия", НСтр("ru='Комиссия';uk='Комісія'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	КонецЕсли;
    
    
	Если РазделУчета = "РасчетыСПартнерами" Тогда
		СчетаУчетаРегл.Добавить("РасчетыСКлиентами", НСтр("ru='Долг';uk='Борг'"), Истина);
		СчетаУчетаРегл.Добавить("АвансыПолученные", НСтр("ru='Авансы';uk='Аванси'"), Истина);
		СчетаУчетаРегл.Добавить("РасчетыСПоставщиками", НСтр("ru='Долг';uk='Борг'"), Истина);
		СчетаУчетаРегл.Добавить("АвансыВыданные", НСтр("ru='Авансы';uk='Аванси'"), Истина);
		СчетаУчетаРегл.Добавить("РасчетыСКлиентамиТара", НСтр("ru='Тара';uk='Тара'"), ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару"));
		СчетаУчетаРегл.Добавить("РасчетыСПоставщикамиТара", НСтр("ru='Тара';uk='Тара'"), ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару"));
		СчетаУчетаРегл.Добавить("НеотфактурованныеПоставки", НСтр("ru='Неотфакурованные поставки';uk='Невідфактуровані поставки'"), ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки"));
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиОсновнойДолг", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиПроценты", НСтр("ru='Проценты';uk='Відсотки'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСКредиторамиКомиссия", НСтр("ru='Комиссия';uk='Комісія'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиОсновнойДолг", НСтр("ru='Долг';uk='Борг'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиПроценты", НСтр("ru='Проценты';uk='Відсотки'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("РасчетыСДебиторамиКомиссия", НСтр("ru='Комиссия';uk='Комісія'"), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
		СчетаУчетаРегл.Добавить("ОбеспечительныйПлатежПоЛизингу", НСтр("ru='Обеспечительный платеж';uk='Забезпечувальний платіж'"), ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг"));
		СчетаУчетаРегл.Добавить("АрендныеОбязательстваПоЛизингу", НСтр("ru='Арендные обязательства';uk='Орендні зобов''язання'"), ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг"));
		СчетаУчетаРегл.Добавить("ЛизинговыеУслуги", НСтр("ru='Лизинговые услуги';uk='Лізингові послуги'"), ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг"));
		СчетаУчетаРегл.Добавить("ВыкупПредметаЛизинга", НСтр("ru='Выкуп предмета лизинга';uk='Викуп предмета лізингу'"), ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг"));
		СчетаУчетаРегл.Добавить("ТоварыКомитентовКОформлениюПоПродаже", НСтр("ru='По продаже';uk='По продажу'"), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
		СчетаУчетаРегл.Добавить("ТоварыКомитентовКОформлениюПоЗакупке", НСтр("ru='По закупке';uk='По купівлі'"), ПолучитьФункциональнуюОпцию("ИспользоватьОказаниеАгентскихУслугПриЗакупке"));
	КонецЕсли;
	
	Если РазделУчета = "Производство" Тогда
		СчетаУчетаРегл.Добавить("Производство", НСтр("ru='Счет учета';uk='Рахунок'"), Истина);
	КонецЕсли;
	
	Если РазделУчета = "Резервы" Тогда
		СчетаУчетаРегл.Добавить("Резервы", НСтр("ru='Счет учета';uk='Рахунок'"), ПолучитьФункциональнуюОпцию("ФормироватьРезервыПредстоящихРасходов"));
	КонецЕсли;
	
	Если РазделУчета = "ТМЦВЭксплуатации" Тогда
		СчетаУчетаРегл.Добавить("ТМЦВЭксплуатации", НСтр("ru='В эксплуатации';uk='В експлуатації'"), Истина);
		СчетаУчетаРегл.Добавить("ТМЦВЭксплуатацииАмортизация", НСтр("ru='Амортизация';uk='Амортизація'"), Истина);
	КонецЕсли;
    
	Если РазделУчета = "ВнеоборотныеАктивы" Тогда
		СчетаУчетаРегл.Добавить("СтоимостьВНА", НСтр("ru='Стоимость';uk='Вартість'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
		СчетаУчетаРегл.Добавить("АмортизацияВНА", НСтр("ru='Амортизация';uk='Амортизація'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
		СчетаУчетаРегл.Добавить("РезервДооценкиВНА", НСтр("ru='Резерв дооценки';uk='Резерв дооцінки'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
		СчетаУчетаРегл.Добавить("ВыбытиеВНА", НСтр("ru='Выбытие';uk='Вибуття'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
		СчетаУчетаРегл.Добавить("СтоимостьВНА_ЦФ", НСтр("ru='Стоимость';uk='Вартість'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") И ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности"));
		СчетаУчетаРегл.Добавить("АмортизацияВНА_ЦФ", НСтр("ru='Амортизация';uk='Амортизація'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") И ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности"));
		СчетаУчетаРегл.Добавить("ЗабалансовыйУчетВНА", НСтр("ru='Залоговая стоимость';uk='Заставна вартість'"), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
	КонецЕсли;
	
	Если РазделУчета = "ПодарочныеСертификаты" Тогда
		СчетаУчетаРегл.Добавить("РасчетыПоПодарочнымСертификатам", НСтр("ru='Счет учета';uk='Рахунок'"), ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты"));
	КонецЕсли;
	
	Возврат СчетаУчетаРегл;
	
КонецФункции

// Возвращает список субконто счетов учета для переданного раздела.
//
//	Параметры:
//		РазделУчета - Строка - имя раздела учета, см. СписокРазделовСчетовУчета();
//
//	Возвращаемое значение:
//		СписокЗначений - список значений со следующими элементами:
//			Значение - Строка - Имя настройки субконто счета учета;
//			Представление - Строка - представление этой настройки в интерфейсе;
//			Пометка - Булево - Признак применения этой настройки, как правило зависит от ФО, если установлен в ЛОЖЬ, такая
//				настройка будет скрываться с формы.
//
Функция СписокСубконтоПоРазделуУчета(РазделУчета) Экспорт
	
	СписокСубконто = Новый СписокЗначений;
	
	
	Если РазделУчета = "НоменклатураСобственная" Или РазделУчета = "Номенклатура" Тогда
        СписокСубконто.Добавить("СтатьяДоходов", НСтр("ru='Статья доходов';uk='Стаття доходів'"), Истина);
        СписокСубконто.Добавить("СтатьяЗатрат", НСтр("ru='Статья затрат';uk='Стаття витрат'"), Истина);
	КонецЕсли;
	
	Возврат СписокСубконто;
	
КонецФункции

// Возвращает список разделов учета.
//
//	Возвращаемое значение:
//		СписокЗначений - список значений со следующими элементами:
//			Значение - Строка - Имя настройки раздела учета;
//			Представление - Строка - представление этой настройки в интерфейсе;
//			Пометка - Булево - Признак применения этой настройки, как правило зависит от ФО, если установлен в ЛОЖЬ,
//				все элементы с настройками данного раздела будут скрываться с формы.
//
Функция СписокРазделовСчетовУчета(КодЯзыка=Неопределено) Экспорт

	КодЯзыкаСпискаРазделов = ?(КодЯзыка = Неопределено, МультиязычностьУкр.КодЯзыкаИнтерфейса(), КодЯзыка);
	
	СписокВозврата = Новый СписокЗначений;
	
	СписокВозврата.Добавить("НоменклатураСобственная", НСтр("ru='номенклатуры';uk='номенклатури'",КодЯзыкаСпискаРазделов), Истина);
	ИспользованиеНоменклатурыПереданной = ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне")
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
	СписокВозврата.Добавить("НоменклатураПереданная", НСтр("ru='номенклатуры переданной';uk='номенклатури переданої'",КодЯзыкаСпискаРазделов), ИспользованиеНоменклатурыПереданной);
	ИспользованиеНоменклатурыПринятой = ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранениеВПроцессеЗакупки") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках")
	//++ НЕ УТКА
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья")
	//-- НЕ УТКА
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
	СписокВозврата.Добавить("НоменклатураПринятая", НСтр("ru='номенклатуры принятой';uk='номенклатури прийнятої'",КодЯзыкаСпискаРазделов), ИспользованиеНоменклатурыПринятой);
	СписокВозврата.Добавить("ТМЦВЭксплуатации", НСтр("ru='ТМЦ в эксплуатации';uk='ТМЦ в експлуатації'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("ДенежныеСредства", НСтр("ru='денежных средств';uk='грошових коштів'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("ПодарочныеСертификаты", НСтр("ru='подарочных сертификатов';uk='подарункових сертифікатів'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты"));
	СписокВозврата.Добавить("РасчетыСКлиентами", НСтр("ru='расчетов с клиентами';uk='розрахунків з клієнтами'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("РасчетыСПоставщиками", НСтр("ru='расчетов с поставщиками';uk='розрахунків з постачальниками'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("РасчетыСКомиссионерами", НСтр("ru='расчетов с комиссионерами';uk='розрахунків з комісіонерами'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах")
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	СписокВозврата.Добавить("РасчетыСКомитентами", НСтр("ru='расчетов с комитентами по продаже';uk='розрахунків з комітентами з продажу'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках")
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	СписокВозврата.Добавить("РасчетыСКомитентамиПоЗакупке", НСтр("ru='расчетов с комитентами по закупке';uk='розрахунків з комітентами з купівлі'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьОказаниеАгентскихУслугПриЗакупке"));
	СписокВозврата.Добавить("РасчетыСКредиторами", НСтр("ru='расчетов с кредиторами';uk='розрахунків з кредиторами'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	СписокВозврата.Добавить("РасчетыСДебиторами", НСтр("ru='расчетов с дебиторами';uk='розрахунків з дебіторами'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	СписокВозврата.Добавить("Производство", НСтр("ru='производства';uk='виробництва'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("ВнеоборотныеАктивы", НСтр("ru='необоротных активов';uk='необоротних активів'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
	СписокВозврата.Добавить("Расходы", НСтр("ru='расходов';uk='витрат'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("Доходы", НСтр("ru='доходов';uk='доходів'",КодЯзыкаСпискаРазделов), Истина);
	СписокВозврата.Добавить("Резервы", НСтр("ru='резервов (оценочных обязательств)';uk='резервів (оціночних зобов''язань)'",КодЯзыкаСпискаРазделов), ПолучитьФункциональнуюОпцию("ФормироватьРезервыПредстоящихРасходов"));

	
	Возврат СписокВозврата;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	ИЛИ Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицыНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивТекстовЗапроса = Новый Массив;
	МассивВременныхТаблиц = Новый Массив;
	МассивТекстовЗапросаДокументов = Новый Массив;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	"""" КАК РазделУчета
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ГДЕ ЛОЖЬ";
	МассивТекстовЗапросаДокументов.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаИнициализацииТаблицыНастроек =
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.АналитикаУчета КАК АналитикаУчета,
	|	ИсходнаяТаблица.МестоУчета КАК МестоУчета
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ИзмененныеДанные <> """" И НЕ ИсходнаяТаблица.ЭтоОбщаяНастройка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчета,
	|	МестоУчета";
	
	ТекстЗапросаВыборкиДокументов =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	""ПереопределяемыйРазделУчета"" КАК РазделУчета
	|ИЗ
	|	ТаблицаДокументов_ПереопределяемыйРазделУчета КАК ТаблицаДокументов";
	
	Для каждого ЭлементТаблицы Из ТаблицыНастроек Цикл
		
		ТекстЗапроса = ТекстЗапросаИнициализацииТаблицыНастроек;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсходнаяТаблица", "&ИсходнаяТаблица_" + ЭлементТаблицы.Ключ);
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		МассивВременныхТаблиц.Добавить("ТаблицаНастроек");
		ИсходнаяТаблица = ЭлементТаблицы.Значение.Выгрузить(, "Организация, МестоУчета, АналитикаУчета, ИзмененныеДанные, ЭтоОбщаяНастройка");
		Запрос.УстановитьПараметр("ИсходнаяТаблица_" + ЭлементТаблицы.Ключ, ИсходнаяТаблица);
		
		Если ЭлементТаблицы.Ключ = "НоменклатураСобственная" Тогда
			НоменклатураЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "НоменклатураПереданная" Тогда
			НоменклатураПереданнаяЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "РасчетыСПартнерами" Тогда
			РасчетыСПартнерамиЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "Доходы" Тогда
			ДоходыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "Расходы" Тогда
			РасходыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "Производство" Тогда
			ПроизводствоЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "ПодарочныеСертификаты" Тогда
			ПодарочныеСертификатыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		ИначеЕсли ЭлементТаблицы.Ключ = "ТМЦВЭксплуатации" Тогда
			ТМЦВЭксплуатацииЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстовЗапроса, МассивВременныхТаблиц);
		Иначе
			Продолжить;
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапросаВыборкиДокументов;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПереопределяемыйРазделУчета", ЭлементТаблицы.Ключ);
		МассивТекстовЗапросаДокументов.Добавить(ТекстЗапроса);
	
		Для каждого ИмяВременнойТаблицы Из МассивВременныхТаблиц Цикл
			МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ" + " " + ИмяВременнойТаблицы);
		КонецЦикла;
		МассивВременныхТаблиц.Очистить();
	
	КонецЦикла;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросаДокументов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(ИСТИНА));
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокументов.Регистратор КАК Документ,
	|	ТаблицаДокументов.РазделУчета КАК РазделУчета,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ТаблицаДокументов.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделУчета,
	|	Документ";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет анализ всех неотраженных операций отражения,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ПараметрыАнализатора.РазделыУчета, ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвиженийСоСчетами.МестоУчета КАК МестоУчета,
	|	ТаблицаДвиженийСоСчетами.ВидСчета КАК ВидСчета
	|ПОМЕСТИТЬ ВидыСчетовТребующиеНастройки
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.Требуется";
	
	Запрос.Выполнить();
	
	ИмяСобытия = НСтр("ru='Анализ порядка отражения %1, требующего настройки';uk='Аналіз порядку відображення %1, що вимагає настройки'",ОбщегоНазначения.КодОсновногоЯзыка());
	ИмяСобытия = СтрШаблон(ИмяСобытия, НастройкаСчетовУчетаКлиентСервер.ЗаголовокПоРазделамУчета(ПараметрыАнализатора.РазделыУчета));
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(Запрос.МенеджерВременныхТаблиц, ИмяСобытия);
	
КонецПроцедуры

// Выполняет анализ операций отражения, в зависимости от параметров и дополняет исходную таблицу операций
// новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ПараметрыАнализа.РазделыУчета, ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	РазделУчета = ПараметрыАнализа.РазделыУчета.Получить(0);
	 
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.АналитикаУчета,
	|	ТекущаяТаблицаСчетовУчета.МестоУчета,
	|	&ПоляРеквизитовПереопределяемый,
	|
	|	&СписокЗначенийСчетовУчетаТекущие,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ЭтоОбщаяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетов,
	|	ТекущаяТаблицаСчетовУчета.СчетаУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные,
	|	ТекущаяТаблицаСчетовУчета.КоличествоИсключений,
	|	ТекущаяТаблицаСчетовУчета.КоличествоИсключенийПоОрганизациям
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.АналитикаУчета,
	|	ТекущаяТаблицаСчетовУчета.МестоУчета,
	|	&ПоляРеквизитовПереопределяемый,
	|
	|	&СписокЗначенийСчетовУчетаТекущие,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ЭтоОбщаяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетов,
	|	ТекущаяТаблицаСчетовУчета.СчетаУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные,
	|	ВЫБОР КОГДА ТекущаяТаблицаСчетовУчета.ЭтоОбщаяНастройка ТОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР КОГДА ТаблицаИсключений.МестоУчета = ТекущаяТаблицаСчетовУчета.МестоУчета ТОГДА ТаблицаИсключений.Организация ИНАЧЕ NULL КОНЕЦ) ИНАЧЕ 0 КОНЕЦ КАК КоличествоИсключенийПоОрганизациям,
	|	ВЫБОР КОГДА ТекущаяТаблицаСчетовУчета.ЭтоОбщаяНастройка ТОГДА КОЛИЧЕСТВО(ТаблицаИсключений.Организация) ИНАЧЕ 0 КОНЕЦ КАК КоличествоИсключений
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ТаблицаДвиженийСоСчетами.Организация
	|		И ТекущаяТаблицаСчетовУчета.АналитикаУчета = ТаблицаДвиженийСоСчетами.АналитикаУчета
	|		И ТекущаяТаблицаСчетовУчета.МестоУчета = ТаблицаДвиженийСоСчетами.МестоУчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ТекТабИск.Организация,ТекТабИск.АналитикаУчета,ТекТабИск.МестоУчета ИЗ ТекущаяТаблицаСчетовУчета КАК ТекТабИск ГДЕ НЕ ТекТабИск.ЭтоОбщаяНастройка
	|						ОБЪЕДИНИТЬ ВЫБРАТЬ НовТабИск.Организация,НовТабИск.АналитикаУчета,НовТабИск.МестоУчета ИЗ ТаблицаДвиженийСоСчетами КАК НовТабИск) КАК ТаблицаИсключений
	|		ПО ТаблицаИсключений.АналитикаУчета = ТекущаяТаблицаСчетовУчета.АналитикаУчета
	|			И ТаблицаИсключений.Организация <> ТекущаяТаблицаСчетовУчета.Организация
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.Организация ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.АналитикаУчета,
	|	ТекущаяТаблицаСчетовУчета.МестоУчета,
	|	&ПоляРеквизитовПереопределяемыйГруппировка,
	|	&СписокЗначенийСчетовУчетаТекущие,
	|	ТекущаяТаблицаСчетовУчета.СчетаУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ЭтоОбщаяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетов,
	|	ТекущаяТаблицаСчетовУчета.КоличествоИсключений,
	|	ТекущаяТаблицаСчетовУчета.КоличествоИсключенийПоОрганизациям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.АналитикаУчета,
	|	ТаблицаДвиженийСоСчетами.МестоУчета,
	|	&ПоляПолученияРеквизитовПереопределяемый,
	|
	|	&СписокЗначенийСчетовУчетаИсключения,
	|
	|	МАКСИМУМ(ТаблицаДвиженийСоСчетами.Требуется И ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК ТребуетсяНастройка,
	|	ЛОЖЬ КАК ЭтоОбщаяНастройка,
	|	&ТребуетсяНастройкаСчетов,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетаУчетаПоУмолчанию, &СчетаУчетаПоУмолчанию),
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, &ИзмененныеДанные),
	|	0,
	|	0
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ТаблицаДвиженийСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ТаблицаДвиженийСоСчетами.АналитикаУчета = ТекущаяТаблицаСчетовУчета.АналитикаУчета
	|		И ТаблицаДвиженийСоСчетами.МестоУчета = ТекущаяТаблицаСчетовУчета.МестоУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.АналитикаУчета,
	|	ТаблицаДвиженийСоСчетами.МестоУчета,
	|	&СписокЗначенийСчетовУчетаТекущие,
	|	ТекущаяТаблицаСчетовУчета.СчетаУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные";
	
	МассивТекстовСписокЗначенийСчетовУчета = Новый Массив;
	МассивТекстовСписокЗначенийСчетовУчетаИсключения = Новый Массив;
	МассивТекстовТребуетсяНастройкаСчетов = Новый Массив;
	МассивТекстовСчетаУчетаПоУмолчанию = Новый Массив;
	МассивТекстовИзмененныеДанные = Новый Массив;
	
	ШаблонСтрокиЗначенияСчетаУчета = "ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета_%1, МАКСИМУМ(ВЫБОР
	|		КОГДА ТаблицаДвиженийСоСчетами.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|			ТОГДА ТаблицаДвиженийСоСчетами.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ))";
	ШаблонСтрокиСчетаУчетаПоУмолчанию = "ВЫБОР КОГДА
	|		МАКСИМУМ(ТаблицаДвиженийСоСчетами.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|		И ТаблицаДвиженийСоСчетами.СчетУчетаПоУмолчанию) ТОГДА ""%1,"" ИНАЧЕ """" КОНЕЦ";
	ШаблонСтрокиТребуетсяНастройкаСчетов = "ВЫБОР КОГДА
	|		МАКСИМУМ(ТаблицаДвиженийСоСчетами.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|		И ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.Требуется) ТОГДА ""%1,"" ИНАЧЕ """" КОНЕЦ";
	ШаблонСтрокиИзмененныеДанные = """%1,""";
	
	СчетаУчетаПоРазделуУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокСчетовУчетаПоРазделуУчета(РазделУчета));
	Для каждого ИмяВидаСчета Из СчетаУчетаПоРазделуУчета Цикл
		
		МассивТекстовСписокЗначенийСчетовУчета.Добавить("ТекущаяТаблицаСчетовУчета.СчетУчета_" + ИмяВидаСчета);
		МассивТекстовСписокЗначенийСчетовУчетаИсключения.Добавить(СтрШаблон(ШаблонСтрокиЗначенияСчетаУчета, ИмяВидаСчета));
		МассивТекстовСчетаУчетаПоУмолчанию.Добавить(СтрШаблон(ШаблонСтрокиСчетаУчетаПоУмолчанию, ИмяВидаСчета));
		МассивТекстовТребуетсяНастройкаСчетов.Добавить(СтрШаблон(ШаблонСтрокиТребуетсяНастройкаСчетов, ИмяВидаСчета));
		МассивТекстовИзмененныеДанные.Добавить(СтрШаблон(ШаблонСтрокиИзмененныеДанные, ИмяВидаСчета));
		
	КонецЦикла;
	
	МассивСубконтоПоРазделуУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокСубконтоПоРазделуУчета(РазделУчета));
	Для каждого ИмяСубконто Из МассивСубконтоПоРазделуУчета Цикл
		
		ИмяВидаСчета = НастройкаСчетовУчетаКлиентСервер.СчетаИспользующиеСубконто(ИмяСубконто).Получить(0);
		
		МассивТекстовСписокЗначенийСчетовУчета.Добавить("ТекущаяТаблицаСчетовУчета.Субконто_" + ИмяСубконто);
		ШаблонСтрокиЗначенияСубконто = СтрЗаменить(ШаблонСтрокиЗначенияСчетаУчета, "СчетУчета", "Субконто");
		ШаблонСтрокиЗначенияСубконто = СтрЗаменить(ШаблонСтрокиЗначенияСчетаУчета, "Субконто_%1", "Субконто_" + ИмяСубконто);
		МассивТекстовСписокЗначенийСчетовУчетаИсключения.Добавить(СтрШаблон(ШаблонСтрокиЗначенияСубконто, ИмяВидаСчета));
		ШаблонСтрокиСубконтоПоУмолчанию = СтрЗаменить(ШаблонСтрокиСчетаУчетаПоУмолчанию, "СчетУчета", "Субконто");
		ШаблонСтрокиСубконтоПоУмолчанию = СтрЗаменить(ШаблонСтрокиСубконтоПоУмолчанию, "ВидыСчетовРеглУчета.%1", "ВидыСчетовРеглУчета." + ИмяВидаСчета);
		МассивТекстовСчетаУчетаПоУмолчанию.Добавить(СтрШаблон(ШаблонСтрокиСубконтоПоУмолчанию, ИмяСубконто));
		МассивТекстовИзмененныеДанные.Добавить(СтрШаблон(ШаблонСтрокиИзмененныеДанные, ИмяСубконто));
		
	КонецЦикла;
	
	Префикс = "," + Символы.ПС + Символы.Таб;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СписокЗначенийСчетовУчетаТекущие", СтрСоединить(МассивТекстовСписокЗначенийСчетовУчета, Префикс));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СписокЗначенийСчетовУчетаИсключения", СтрСоединить(МассивТекстовСписокЗначенийСчетовУчетаИсключения, Префикс));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СчетаУчетаПоУмолчанию", """,""+" + СтрСоединить(МассивТекстовСчетаУчетаПоУмолчанию, "+" + Символы.ПС + Символы.Таб));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТребуетсяНастройкаСчетов", """,""+" + СтрСоединить(МассивТекстовТребуетсяНастройкаСчетов, "+" + Символы.ПС + Символы.Таб));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИзмененныеДанные", """,""+" + СтрСоединить(МассивТекстовИзмененныеДанные));
	
	ПоляПолученияРеквизитовПереопределяемый = МассивТекстовОбращенийКПолямАналитики("ТаблицаДвиженийСоСчетами", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РазделУчета));
	
	ТекстПоляПереопределяемый = ?(ПоляПолученияРеквизитовПереопределяемый.Количество(), Префикс + СтрСоединить(ПоляПолученияРеквизитовПереопределяемый, Префикс), "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Префикс + "&ПоляПолученияРеквизитовПереопределяемый", ТекстПоляПереопределяемый);
	
	МассивТекстовПолей = Новый Массив;
	МассивТекстовПолейГруппировка = Новый Массив;
	Для каждого РеквизитАналитики Из НастройкаСчетовУчета.РеквизитыАналитикиВлияющиеНаНастройкуСчетовУчета(РазделУчета) Цикл
		Если НастройкаСчетовУчетаКлиентСервер.ЭтоНеРеквизитАналитики(РеквизитАналитики.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		МассивТекстовПолей.Добавить(СтрШаблон("ТекущаяТаблицаСчетовУчета.%1 КАК %1", РеквизитАналитики.Ключ));
		МассивТекстовПолейГруппировка.Добавить(СтрШаблон("ТекущаяТаблицаСчетовУчета.%1", РеквизитАналитики.Ключ));
	КонецЦикла;
	ТекстПоляПереопределяемый = ?(МассивТекстовПолейГруппировка.Количество(), Префикс + СтрСоединить(МассивТекстовПолейГруппировка, Префикс), "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Префикс + "&ПоляРеквизитовПереопределяемыйГруппировка", ТекстПоляПереопределяемый);
	ТекстПоляПереопределяемый = ?(МассивТекстовПолей.Количество(), Префикс + СтрСоединить(МассивТекстовПолей, Префикс), "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Префикс + "&ПоляРеквизитовПереопределяемый", ТекстПоляПереопределяемый);
	
	Запрос.Текст = ТекстЗапроса;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("ТаблицаНастроек_" + РазделУчета, Таблица), АдресХранилища);
	
КонецПроцедуры

Функция СоставитьДинамическийТекстЗапросаПолученияСчетовУчета(СписокСчетов, РазделыУчета = Неопределено, ИмяВременнойТаблицы = Неопределено) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПорядокОтраженияНаСчетахУчета.Организация,
	|	ПорядокОтраженияНаСчетахУчета.АналитикаУчета,
	|	ПорядокОтраженияНаСчетахУчета.МестоУчета,
	|	&ПоляПереопределяемый
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
	|ГДЕ
	|	ПорядокОтраженияНаСчетахУчета.ВидСчета В (&ВидыСчетов)
	|	И &УсловиеВыборкиПереопределяемый
	|СГРУППИРОВАТЬ ПО
	|	ПорядокОтраженияНаСчетахУчета.Организация,
	|	ПорядокОтраженияНаСчетахУчета.АналитикаУчета,
	|	ПорядокОтраженияНаСчетахУчета.МестоУчета";
	
	ШаблонСчета = "МАКСИМУМ(ВЫБОР
	|		КОГДА ПорядокОтраженияНаСчетахУчета.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)
	|			ТОГДА ПорядокОтраженияНаСчетахУчета.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ) КАК СчетУчета_%1";
	ШаблонСубконто = "МАКСИМУМ(ВЫБОР
	|		КОГДА %1
	|			ТОГДА ПорядокОтраженияНаСчетахУчета.Субконто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК Субконто_%2";
	ШаблонПеречисленияСчетовДляСубконто = "ПорядокОтраженияНаСчетахУчета.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)";
	
	МассивТекстовПолей = Новый Массив;
	МассивТекстовВидовСчетов = Новый Массив;
	
	Если РазделыУчета <> Неопределено Тогда
		МассивТекстовПолей = МассивТекстовОбращенийКПолямАналитики("ПорядокОтраженияНаСчетахУчета", РазделыУчета);
	КонецЕсли;
	
	СоответствиеСубконтоИСчетов = Новый Соответствие;
	Для каждого СчетУчета Из СписокСчетов Цикл
		ТекстСчета = СтрШаблон(ШаблонСчета, СчетУчета);
		МассивТекстовПолей.Добавить(ТекстСчета);
		
		ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(СчетУчета);
		Если ЗначениеЗаполнено(ИмяСубконто) Тогда
			Если СоответствиеСубконтоИСчетов.Получить(ИмяСубконто) = Неопределено Тогда
				СоответствиеСубконтоИСчетов.Вставить(ИмяСубконто, Новый Массив);
			КонецЕсли;
			ЗначениеСоответствияСубконто = СоответствиеСубконтоИСчетов.Получить(ИмяСубконто);
			ЗначениеСоответствияСубконто.Добавить(СтрШаблон(ШаблонПеречисленияСчетовДляСубконто, СчетУчета));
		КонецЕсли;
		
		МассивТекстовВидовСчетов.Добавить(СтрШаблон("ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.%1)", СчетУчета));
		
	КонецЦикла;
	
	Если СоответствиеСубконтоИСчетов.Количество() Тогда
		Для каждого ЭлементСоответствия Из СоответствиеСубконтоИСчетов Цикл
			Разделитель = Символы.ПС + Символы.Таб + Символы.Таб + Символы.Таб + "ИЛИ" + " ";
			ТекстПеречисленияСчетов = СтрСоединить(ЭлементСоответствия.Значение, Разделитель);
			ТекстПоля = СтрШаблон(ШаблонСубконто, ТекстПеречисленияСчетов, ЭлементСоответствия.Ключ);
			МассивТекстовПолей.Добавить(ТекстПоля);
		КонецЦикла;
	КонецЕсли;
	
	ТекстСчетовУчета = СтрСоединить(МассивТекстовПолей, ","+Символы.ПС+Символы.Таб);
	ТекстВидовСчетов = СтрСоединить(МассивТекстовВидовСчетов, ","+Символы.ПС+Символы.Таб);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляПереопределяемый", ТекстСчетовУчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидыСчетов", ТекстВидовСчетов);
	ТекстПомещенияВоВременнуюТаблицу = ?(ИмяВременнойТаблицы = Неопределено, "", "ПОМЕСТИТЬ" + " " + ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ИмяВременнойТаблицы", ТекстПомещенияВоВременнуюТаблицу);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция МассивТекстовОбращенийКПолямАналитики(ИмяТаблицы, РазделыУчета)
	
	МассивВозврата = Новый Массив;
	
	ШаблонРеквизитаАналитики = "МАКСИМУМ(ВЫБОР %Выражение% ИНАЧЕ %ЗначениеПоУмолчанию% КОНЕЦ) КАК %ИмяРеквизита%";
	ШаблонВыражения = "КОГДА %ИмяТаблицы%.АналитикаУчета ССЫЛКА %ИмяМетаданных% ТОГДА ЕСТЬNULL(%ИмяТаблицы%.АналитикаУчета.%ИмяРеквизитаАналитики%, %ЗначениеПоУмолчанию%)";
	ШаблонВыражения = СтрЗаменить(ШаблонВыражения, "%ИмяТаблицы%", ИмяТаблицы);
	
	РасчетыСПартнерами = Ложь;
	
	Для каждого РазделУчета Из РазделыУчета Цикл
		
		Если НастройкаСчетовУчетаКлиентСервер.ВидыРасчетовСПартнерами().Найти(РазделУчета) <> Неопределено Тогда
			Если РасчетыСПартнерами Тогда
				Продолжить;
			Иначе
				РасчетыСПартнерами = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеТиповАналитикиУчета = НастройкаСчетовУчета.НастройкиАналитикиУчетаПоРазделуУчета(РазделУчета).ТипЗначения;
		
		Если ОписаниеТиповАналитикиУчета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыАналитики = НастройкаСчетовУчета.РеквизитыАналитикиВлияющиеНаНастройкуСчетовУчета(РазделУчета);
		
		СоответствиеИменРеквизитовСтруктураНастроек = Новый Соответствие;
		Для каждого Тип Из ОписаниеТиповАналитикиУчета.Типы() Цикл
			МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
			ИмяМетаданных = МетаданныеТипа.ПолноеИмя();
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМетаданных);
			СоответствиеИменРеквизитов = Новый Структура;
			НастройкаСчетовУчета.ЗаполнитьИменаРеквизитовАналитикиВСоответствииИмен(СоответствиеИменРеквизитов, РеквизитыАналитики);
			МенеджерОбъекта.ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(СоответствиеИменРеквизитов);
			Для каждого ЗначениеСоответствия Из СоответствиеИменРеквизитов Цикл
				Если ЗначениеСоответствия.Значение = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				СтруктураНастроек = Новый Структура;
				СтруктураНастроек.Вставить("ИмяМетаданных", ИмяМетаданных);
				СтруктураНастроек.Вставить("ПутьКРеквизиту", ЗначениеСоответствия.Значение);
				МассивСтруктурНастроек = СоответствиеИменРеквизитовСтруктураНастроек.Получить(ЗначениеСоответствия.Ключ);
				Если МассивСтруктурНастроек = Неопределено Тогда
					МассивСтруктурНастроек = Новый Массив;
					СоответствиеИменРеквизитовСтруктураНастроек.Вставить(ЗначениеСоответствия.Ключ, МассивСтруктурНастроек);
				КонецЕсли;
				МассивСтруктурНастроек.Добавить(СтруктураНастроек);
			КонецЦикла;
		КонецЦикла;
		
		Для каждого РеквизитАналитики Из РеквизитыАналитики Цикл
			
			НастройкиРеквизитаАналитики = СоответствиеИменРеквизитовСтруктураНастроек.Получить(РеквизитАналитики.Ключ);
			
			Если НастройкиРеквизитаАналитики = Неопределено Или РеквизитАналитики.Ключ = "Организация" Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстПоля = ШаблонРеквизитаАналитики;
			
			МассивВыражений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("");
			
			Для каждого СтруктураНастройкиРеквизитаДляТипа Из НастройкиРеквизитаАналитики Цикл
			
				ТекстВыражения = ШаблонВыражения;
				ТекстВыражения = СтрЗаменить(ТекстВыражения, "%ИмяМетаданных%", СтруктураНастройкиРеквизитаДляТипа.ИмяМетаданных);
				ТекстВыражения = СтрЗаменить(ТекстВыражения, "%ИмяРеквизитаАналитики%", СтруктураНастройкиРеквизитаДляТипа.ПутьКРеквизиту);
				МассивВыражений.Добавить(ТекстВыражения);
				
			КонецЦикла;
			
			МассивВыражений.Добавить("");
			
			ТекстПоля = СтрЗаменить(ТекстПоля, "%Выражение%", СтрСоединить(МассивВыражений, Символы.ПС + Символы.Таб));
			ТекстПоля = СтрЗаменить(ТекстПоля, "%ЗначениеПоУмолчанию%", РеквизитАналитики.Значение);
			ТекстПоля = СтрЗаменить(ТекстПоля, "%ИмяРеквизита%", РеквизитАналитики.Ключ);
			МассивВозврата.Добавить(ТекстПоля);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазделыУчетаПоВидуРасчетов(АналитикаУчета)
	
	ВидыРасчетовСтрокой = СтрСоединить(Справочники.ГруппыФинансовогоУчетаРасчетов.ВидыРасчетов(), ",");
	РеквизитыАналитикиУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(АналитикаУчета, ВидыРасчетовСтрокой);
	
	РазделыУчета = Новый Массив;
	
	Для каждого Реквизит Из РеквизитыАналитикиУчета Цикл
		Если Реквизит.Значение Тогда
			РазделыУчета.Добавить(Реквизит.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Если РазделыУчета.Количество() > 1 Тогда
		Возврат "РасчетыСКлиентами,РасчетыСПоставщиками,РасчетыСДебиторами,РасчетыСКредиторами";
	КонецЕсли;
	
	Возврат СтрСоединить(РазделыУчета, ",");
	
КонецФункции

#Область АнализОперацийСлужебный

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	КонецПериода = Неопределено;
	ПараметрыАнализатора.Свойство("КонецПериода", КонецПериода);
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		ОграничениеПериода = Неопределено;
		Если Не ПараметрыАнализатора.Свойство("ДатаОкончанияПериода", ОграничениеПериода) Тогда
			ОграничениеПериода = Новый СтандартнаяДатаНачала();
		КонецЕсли;
		КонецПериода = Обработки.НастройкаОтраженияДокументовВРеглУчете.ПериодКОтражениюВРеглУчете(ПараметрыАнализатора.МассивОрганизаций, ОграничениеПериода).ДатаОкончания;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Если ПараметрыАнализатора.МассивОрганизаций = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация В (&МассивОрганизаций)", "");
	Иначе
		Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрыАнализатора.МассивОрганизаций);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	
	МассивТекстовЗапроса = Новый Массив;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицГоловнымиОрганизациями(Запрос.МенеджерВременныхТаблиц, ПараметрыАнализатора.МассивОрганизаций);
	
	ДополнитьТекстЗапросаОперацийДаннымиНоменклатуры(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиПроизводства(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиРасчетовСПартнерами(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиДоходов(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиПодарочныхСертификатов(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиРасходов(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиРезервов(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиТМЦВЭксплуатации(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиДенежныхСредств(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	ДополнитьТекстЗапросаОперацийДаннымиВнеоборотныхАктивов(ПараметрыАнализатора.РазделыУчета, МассивТекстовЗапроса);
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	УсловиеВхожденияВРегистр = "ГДЕ
	|	&УсловиеДляРегистра
	|	";
	
	УсловиеВхожденияВДокумент = "ГДЕ
	|	&УсловиеДляДокумента
	|	";
	
	УсловиеВхожденияВРегистрССоединениемПоОрганизации = "ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	";
	
	УсловиеВхожденияВСправочникССоединениемПоОрганизации = "ГДЕ
	|	&ОрганизацияИзСправочника_УсловиеДляРегистра
	|";
	
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ
	|	";
	
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)
	|	";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Регистратор", "Ссылка");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "КлючиПартнеров", "ВидыЗапасовСпр");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВСправочникССоединениемПоОрганизации, ТекстУсловий);
	Иначе
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Период", "Дата");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		ТекстУсловий = ТекстУсловий + "И ДанныеДокумента.Проведен" + Символы.ПС + Символы.Таб;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "КлючиПартнеров", "ВидыЗапасовСпр");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВСправочникССоединениемПоОрганизации, ТекстУсловий);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(СписокРазделов, ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	
	МассивТекстовЗапроса = Новый Массив;
	ШаблонЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ТаблицаДвижений.ВидСчета КАК ВидСчета,
	|	ТаблицаДвижений.Требуется КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам
	|ИЗ
	|	ТаблицаДвиженийПоСчетам_%1 КАК ТаблицаДвижений";
	
	Для каждого РазделУчета Из СписокРазделов Цикл
		МассивТекстовЗапроса.Добавить(СтрШаблон(ШаблонЗапроса, РазделУчета));
		ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам", "");
	КонецЦикла;
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	МассивТекстовЗапроса.Очистить();
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Для каждого РазделУчета Из СписокРазделов Цикл
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ" + " ТаблицаДвиженийПоСчетам_" + РазделУчета);
	КонецЦикла;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ТаблицаДвижений.ВидСчета КАК ВидСчета,
	|	ТаблицаДвижений.Требуется КАК Требуется,
	|	ЕСТЬNULL(ПорядокОтраженияПоМестамУчета.СчетУчета, ЕСТЬNULL(ПорядокОтраженияПоОрганизациям.СчетУчета, ЕСТЬNULL(ПорядокОтраженияПоАналитикам.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)))) КАК СчетУчета,
	|	ЕСТЬNULL(ПорядокОтраженияПоМестамУчета.СчетУчета, ПорядокОтраженияПоОрганизациям.СчетУчета) ЕСТЬ NULL И НЕ ПорядокОтраженияПоАналитикам.СчетУчета ЕСТЬ NULL КАК СчетУчетаПоУмолчанию,
	|	ЕСТЬNULL(ПорядокОтраженияПоМестамУчета.Субконто, ЕСТЬNULL(ПорядокОтраженияПоОрганизациям.Субконто, ЕСТЬNULL(ПорядокОтраженияПоАналитикам.СчетУчета, НЕОПРЕДЕЛЕНО))) КАК Субконто,
	|	ЕСТЬNULL(ПорядокОтраженияПоМестамУчета.Субконто, ПорядокОтраженияПоОрганизациям.Субконто) ЕСТЬ NULL И НЕ ПорядокОтраженияПоАналитикам.Субконто ЕСТЬ NULL КАК СубконтоПоУмолчанию
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвиженийПоСчетам КАК ТаблицаДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияПоМестамУчета
	|		ПО ТаблицаДвижений.Организация = ПорядокОтраженияПоМестамУчета.Организация
	|			И ТаблицаДвижений.АналитикаУчета = ПорядокОтраженияПоМестамУчета.АналитикаУчета
	|			И ТаблицаДвижений.МестоУчета = ПорядокОтраженияПоМестамУчета.МестоУчета
	|			И ТаблицаДвижений.ВидСчета = ПорядокОтраженияПоМестамУчета.ВидСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияПоОрганизациям
	|		ПО ТаблицаДвижений.Организация = ПорядокОтраженияПоОрганизациям.Организация
	|			И ТаблицаДвижений.АналитикаУчета = ПорядокОтраженияПоОрганизациям.АналитикаУчета
	|			И ПорядокОтраженияПоОрганизациям.МестоУчета = НЕОПРЕДЕЛЕНО
	|			И ТаблицаДвижений.ВидСчета = ПорядокОтраженияПоОрганизациям.ВидСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияПоАналитикам
	|		ПО ТаблицаДвижений.АналитикаУчета = ПорядокОтраженияПоАналитикам.АналитикаУчета
	|			И ПорядокОтраженияПоАналитикам.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И ПорядокОтраженияПоАналитикам.МестоУчета = НЕОПРЕДЕЛЕНО
	|			И ТаблицаДвижений.ВидСчета = ПорядокОтраженияПоАналитикам.ВидСчета";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Процедура ДополнитьТекстЗапросаОперацийДаннымиПроизводства(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("Производство") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение КАК МестоУчета,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Подразделение = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ИНАЧЕ ДанныеРегистра.Подразделение
	|	КОНЕЦ,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|	И НЕ ДанныеРегистра.Подразделение ССЫЛКА Справочник.Партнеры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ВЫБОР
	|		КОГДА  ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение),ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер))
	|			ТОГДА ДанныеРегистра.АналитикаУчетаНоменклатуры.МестоХранения
	|		ИНАЧЕ ЕСТЬNULL(ДанныеРегистра.АналитикаУчетаНоменклатуры.МестоХранения.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|	КОНЕЦ,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Партнер,
	|	ИСТИНА КАК ТребуетсяСчетУчета_Производство
	|ИЗ
	|	Документ.ПоступлениеОтПереработчика КАК ДанныеДокумента
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_Производство
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиНоменклатуры(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("Номенклатура") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	ДанныеРегистра.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) КАК ЕстьРеализацияБезПереходаПраваСобственности
	|ПОМЕСТИТЬ ДвиженияРегистраСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	КлючиПартнеров.Организация КАК Организация,
	|	КлючиНоменклатуры.СкладскаяТерритория КАК Склад,
	|	КлючиНоменклатуры.Номенклатура КАК Номенклатура,
	|	КлючиНоменклатуры.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)) КАК ЕстьПродажаСобственныхТоваровВКомиссию
	|ПОМЕСТИТЬ ДвиженияРегистраВыручки
	|ИЗ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиПартнеров
	|			ПО КлючиПартнеров.Ссылка = ДанныеРегистра.АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиНоменклатуры
	|			ПО КлючиНоменклатуры.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	И (КлючиНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		ИЛИ КлючиНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|		ИЛИ (НЕ ДанныеРегистра.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)) И КлючиНоменклатуры.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация))))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ВидЗапасов.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|	ДанныеРегистра.КорАналитикаУчетаНоменклатуры.Номенклатура,
	|	ЛОЖЬ
	|ИЗ РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|	И (ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		ИЛИ ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраСебестоимости.Организация КАК Организация,
	|	Склады.Ссылка КАК Склад,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		НоменклатураСпр.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ИСТИНА КАК ЕстьСобственныеТовары,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПродажаУслуг
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ ДвиженияРегистраСебестоимости КАК ДвиженияРегистраСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО ИСТИНА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|			ПО Аналитика.Ссылка = ДвиженияРегистраСебестоимости.АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО Склады.Ссылка = Аналитика.МестоХранения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|			ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|			ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраСебестоимости.ВидЗапасов
	|
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЕСТЬNULL(Услуга.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьСобственныеТовары,
	|	ИСТИНА КАК ЕстьПродажаСобственныхТоваров,
	|	ИСТИНА КАК ЕстьПродажаУслуг
	|ИЗ
	|	Документ.ОтчетДавальцу КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Услуга
	|			ПО ДанныеДокумента.Номенклатура = Услуга.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	//-- НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраВыручки.Организация,
	|	ВЫБОР КОГДА ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДвиженияРегистраВыручки.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	КОНЕЦ КАК Склад,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ИЛИ ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	ТОГДА
	|		НоменклатураСпр.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга) КАК ЕстьСобственныеТовары,
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИЛИ ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга)
	|		ИЛИ НоменклатураСпр.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ИЛИ ДвиженияРегистраВыручки.ЕстьПродажаСобственныхТоваровВКомиссию КАК ЕстьПродажаСобственныхТоваров,
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) КАК ЕстьПродажаУслуг
	|ИЗ ДвиженияРегистраВыручки КАК ДвиженияРегистраВыручки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО ИСТИНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|			ПО НоменклатураСпр.Ссылка = ДвиженияРегистраВыручки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|			ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраВыручки.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьСобственныеТовары) КАК ЕстьСобственныеТовары,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСобственныхТоваров) КАК ЕстьПродажаСобственныхТоваров,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаУслуг) КАК ЕстьПродажаУслуг
	|ПОМЕСТИТЬ ТаблицаДвиженийНоменклатураСобственная
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраСебестоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраВыручки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	КлючиНоменклатуры.Номенклатура,
	|	ДанныеРегистра.ВидЗапасов,
	|	ДанныеРегистра.РазделУчета
	|ПОМЕСТИТЬ ДвиженияРегистраСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатуры
	|		ПО КлючиНоменклатуры.КлючАналитики = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	КлючиПартнеров.Организация,
	|	КлючиНоменклатуры.Номенклатура,
	|	НЕ ДанныеРегистра.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)) КАК ЕстьПродажаСобственныхТоваров,
	|	ДанныеРегистра.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)) КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ДвиженияРегистраВыручки
	|ИЗ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиПартнеров
	|		ПО КлючиПартнеров.Ссылка = ДанныеРегистра.АналитикаУчетаПоПартнерам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиНоменклатуры
	|		ПО КлючиНоменклатуры.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	И (КлючиНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|		ИЛИ КлючиНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ЛОЖЬ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДанныеРегистра.ВидЗапасов
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ВидыЗапасовСпр.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасовСпр.Организация КАК Организация,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ИСТИНА КАК ЕстьПередачаТоваровПереработчику,
	|	ЛОЖЬ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ РегистрНакопления.ТоварыПереданныеПереработчику КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДанныеРегистра.ВидЗапасов
	|ГДЕ
	|	&ОрганизацияИзСправочника_УсловиеДляРегистра
	|	И ВидыЗапасовСпр.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраВыручки.Организация,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраВыручки.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ДвиженияРегистраВыручки.ЕстьЗатратыНаПриобретениеТМЦ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ ДвиженияРегистраВыручки КАК ДвиженияРегистраВыручки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = ДвиженияРегистраВыручки.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраВыручки.ВидЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраСебестоимости.Организация,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам) КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути) КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ ДвиженияРегистраСебестоимости КАК ДвиженияРегистраСебестоимости
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = ДвиженияРегистраСебестоимости.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраСебестоимости.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК МестоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСобственныхТоваров) КАК ЕстьПродажаСобственныхТоваров,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПередачаТоваровПереработчику) КАК ЕстьПередачаТоваровПереработчику,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьЗатратыНаПриобретениеТМЦ) КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ТаблицаДвиженийНоменклатураПереданная
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраСебестоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраВыручки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	КлючиНоменклатуры.Номенклатура,
	|	КлючиНоменклатуры.ТипМестаХранения,
	|	ДанныеРегистра.РазделУчета,
	|	ДанныеРегистра.ВидЗапасов,
	|	ЕСТЬNULL(КлючиНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|	ИЛИ КлючиНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение) КАК ЕстьПроизводство
	|ПОМЕСТИТЬ ДвиженияРегистраСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиНоменклатуры
	|		ПО КлючиНоменклатуры.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиКОформлениюСписания),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраСебестоимости.Организация,
	|	ВЫБОР КОГДА НЕ Константы.ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраСебестоимости.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)) И НЕ ДвиженияРегистраСебестоимости.ЕстьПроизводство КАК ЕстьТМЦНаСкладах,
	|	ДвиженияРегистраСебестоимости.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)) И ДвиженияРегистраСебестоимости.ЕстьПроизводство КАК ЕстьТМЦВПроизводстве,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам) КАК ЕстьТМЦУХранителей,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиКОформлениюСписания) КАК ЕстьТМЦКСписанию,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку) И НЕ ДвиженияРегистраСебестоимости.ЕстьПроизводство КАК ЕстьМатериалыПринятыеВПереработку,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку) И ДвиженияРегистраСебестоимости.ЕстьПроизводство КАК ЕстьМатериалыПринятыеВПереработкуВПроизводстве,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		И НЕ ДвиженияРегистраСебестоимости.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)) КАК ЕстьТоварыНаСкладе,
	|	ДвиженияРегистраСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		И ДвиженияРегистраСебестоимости.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)) КАК ЕстьТоварыПереданныеНаКомиссию
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ ДвиженияРегистраСебестоимости КАК ДвиженияРегистраСебестоимости
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = ДвиженияРегистраСебестоимости.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраСебестоимости.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК МестоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТМЦНаСкладах) КАК ЕстьТМЦНаСкладах,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТМЦВПроизводстве) КАК ЕстьТМЦВПроизводстве,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТМЦУХранителей) КАК ЕстьТМЦУХранителей,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТМЦКСписанию) КАК ЕстьТМЦКСписанию,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьМатериалыПринятыеВПереработку) КАК ЕстьМатериалыПринятыеВПереработку,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьМатериалыПринятыеВПереработкуВПроизводстве) КАК ЕстьМатериалыПринятыеВПереработкуВПроизводстве,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТоварыНаСкладе) КАК ЕстьТоварыНаСкладе,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТоварыПереданныеНаКомиссию) КАК ЕстьТоварыПереданныеНаКомиссию
	|ПОМЕСТИТЬ ТаблицаДвиженийНоменклатураПринятая
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраСебестоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.Склад КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьСобственныеТовары КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_Номенклатура
	|ИЗ ТаблицаДвиженийНоменклатураСобственная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.Склад КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж) КАК ВидСчета,
	|	(ТаблицаДвижений.ЕстьПродажаСобственныхТоваров ИЛИ ТаблицаДвижений.ЕстьПродажаУслуг) КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураСобственная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.Склад КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродаж) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьПродажаСобственныхТоваров КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураСобственная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НоменклатураПереданная) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьПродажаСобственныхТоваров КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВычетыИзДоходов) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьПродажаСобственныхТоваров КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродаж) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьПродажаСобственныхТоваров КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьЗатратыНаПриобретениеТМЦ КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаВПереработку) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьПередачаТоваровПереработчику КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПереданная КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦНаСкладах) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТМЦНаСкладах КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВПроизводстве) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТМЦВПроизводстве КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦУХранителей) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТМЦУХранителей КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦКСписанию) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТМЦКСписанию КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.МатериалыПринятыеВПереработку) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьМатериалыПринятыеВПереработку КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.МатериалыПринятыеВПереработкуВПроизводстве) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьМатериалыПринятыеВПереработкуВПроизводстве КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыНаСкладе) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТоварыНаСкладе КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыПереданныеНаКомиссию) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТоварыПереданныеНаКомиссию КАК Требуется
	|ИЗ ТаблицаДвиженийНоменклатураПринятая КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийНоменклатураСобственная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийНоменклатураПереданная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийНоменклатураПринятая";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиРасчетовСПартнерами(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("РасчетыСПартнерами") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"
	// Есть расчеты с клиентами (в том числе по таре и по комиссии)
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлючиПартнеров.Организация КАК Организация,
	|	ДанныеРегистра.ОбъектРасчетов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ИСТИНА КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ИспользоватьМногооборотнуюТару.Значение КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = КлючиПартнеров.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ИспользоватьМногооборотнуюТару КАК ИспользоватьМногооборотнуюТару
	|		ПО ИСТИНА
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	
	|
	// Есть расчеты по депозитам и займам выданным, по оплате комиссии по кредитам и займам полученным.
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлючиПартнеров.Организация КАК Организация,
	|	ЕСТЬNULL(ДанныеРегистра.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ДанныеРегистра.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм) КАК ЕстьРасчетыСКредиторами,
	|	ДанныеРегистра.Договор.ХарактерДоговора <> ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм) КАК ЕстьРасчетыСДебиторами,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия) КАК ЕстьРасчетыПоКомиссии,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты) КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = КлючиПартнеров.КлючАналитики
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	
	|
	// Есть расчеты с поставщиками (в том числе по таре и по комиссии)
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлючиПартнеров.Организация КАК Организация,
	|	ДанныеРегистра.ОбъектРасчетов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ИСТИНА КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ИспользоватьМногооборотнуюТару.Значение КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = КлючиПартнеров.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ИспользоватьМногооборотнуюТару КАК ИспользоватьМногооборотнуюТару
	|		ПО ИСТИНА
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	
	|
	// Есть расчеты по неотфактуренным поставкам
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ЕСТЬNULL(КлючиНоменклатуры.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ИСТИНА КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиНоменклатуры
	|			ПО КлючиНоменклатуры.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|
	// Есть расчеты по лизингу
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлючиПартнеров.Организация КАК Организация,
	|	КлючиПартнеров.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ЛизинговыйПлатеж) КАК ЕстьЛизинговыеУслуги,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж) КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ВыкупПредметаЛизинга) КАК ЕстьВыкупПредметаЛизинга,
	|	ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.АрендныеОбязательства) КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыЛизинга КАК КлючиПартнеров
	|		ПО ДанныеРегистра.Договор = КлючиПартнеров.Ссылка
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	
	|
	// Есть расчеты с комитентами по товарам к оформлению
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлючиПартнеров.Организация КАК Организация,
	|	КлючиПартнеров.Договор.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ИСТИНА КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КлючиПартнеров
	|		ПО ДанныеРегистра.ВидЗапасов = КлючиПартнеров.Ссылка
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Назначение.Договор.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСКредиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыСДебиторами,
	|	ЛОЖЬ КАК ЕстьРасчетыПоОсновномуДолгу,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоПроцентам,
	|	ЛОЖЬ КАК ЕстьНеотфактурованныеПоставки,
	|	ЛОЖЬ КАК ЕстьЛизинговыеУслуги,
	|	ЛОЖЬ КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	ЛОЖЬ КАК ЕстьВыкупПредметаЛизинга,
	|	ЛОЖЬ КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	ЛОЖЬ КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	ИСТИНА КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ИЗ
	|	РегистрНакопления.ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКлиентами) КАК ЕстьРасчетыСКлиентами,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСПоставщиками) КАК ЕстьРасчетыСПоставщиками,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКлиентамиТара) КАК ЕстьРасчетыСКлиентамиТара,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСПоставщикамиТара) КАК ЕстьРасчетыСПоставщикамиТара,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКредиторами И ВложенныйЗапрос.ЕстьРасчетыПоОсновномуДолгу) КАК ЕстьРасчетыСКредиторамиПоОсновномуДолгу,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКредиторами И ВложенныйЗапрос.ЕстьРасчетыПоКомиссии) КАК ЕстьРасчетыСКредиторамиПоКомиссии,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКредиторами И ВложенныйЗапрос.ЕстьРасчетыПоПроцентам) КАК ЕстьРасчетыСКредиторамиПоПроцентам,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСДебиторами И ВложенныйЗапрос.ЕстьРасчетыПоОсновномуДолгу) КАК ЕстьРасчетыСДебиторамиПоОсновномуДолгу,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСДебиторами И ВложенныйЗапрос.ЕстьРасчетыПоКомиссии) КАК ЕстьРасчетыСДебиторамиПоКомиссии,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСДебиторами И ВложенныйЗапрос.ЕстьРасчетыПоПроцентам) КАК ЕстьРасчетыСДебиторамиПоПроцентам,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьНеотфактурованныеПоставки) КАК ЕстьНеотфактурованныеПоставки,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьЛизинговыеУслуги) КАК ЕстьЛизинговыеУслуги,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьОбеспечительныйПлатежПоЛизингу) КАК ЕстьОбеспечительныйПлатежПоЛизингу,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьВыкупПредметаЛизинга) КАК ЕстьВыкупПредметаЛизинга,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьАрендныеОбязательстваПоЛизингу) КАК ЕстьАрендныеОбязательстваПоЛизингу,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТоварыКомитентовКОформлениюПоПродаже) КАК ЕстьТоварыКомитентовКОформлениюПоПродаже,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьТоварыКомитентовКОформлениюПоЗакупке) КАК ЕстьТоварыКомитентовКОформлениюПоЗакупке
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСПоставщиками КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_РасчетыСПартнерами
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСПоставщиками КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьНеотфактурованныеПоставки КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКлиентами КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКлиентами КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСПоставщикамиТара КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентамиТара) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКлиентамиТара КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиОсновнойДолг) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКредиторамиПоОсновномуДолгу КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиПроценты) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКредиторамиПоПроцентам КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиКомиссия) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСКредиторамиПоКомиссии КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиОсновнойДолг) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСДебиторамиПоОсновномуДолгу КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиПроценты) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСДебиторамиПоПроцентам КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиКомиссия) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыСДебиторамиПоКомиссии КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЛизинговыеУслуги) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьЛизинговыеУслуги КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбеспечительныйПлатежПоЛизингу) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьОбеспечительныйПлатежПоЛизингу КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыкупПредметаЛизинга) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьВыкупПредметаЛизинга КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательстваПоЛизингу) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьАрендныеОбязательстваПоЛизингу КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТоварыКомитентовКОформлениюПоПродаже КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.АналитикаУчета,
	|	ТаблицаДвижений.МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоЗакупке) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьТоварыКомитентовКОформлениюПоЗакупке КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиДоходов(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("Доходы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МестоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	МестоУчетаНМА.Подразделение КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	РегистрСведений.МестоУчетаНМА.СрезПоследних(&ДатаОкончания) КАК МестоУчетаНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.СтатьяДоходов, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)) КАК СтатьяДоходов
	|ПОМЕСТИТЬ втСтатьиЦелевогоФинансированияКОтражению
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК ПервоначальныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО ПервоначальныеСведения.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(&ДатаОкончания, ) КАК ПараметрыЦелевогоФинансирования
	|		ПО ПервоначальныеСведения.НематериальныйАктив = ПараметрыЦелевогоФинансирования.НематериальныйАктив
	|ГДЕ
	|	ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПервоначальныеСведения.Организация,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)),
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.СтатьяДоходов, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК ПервоначальныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО ПервоначальныеСведения.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияОС.СрезПоследних(&ДатаОкончания, ) КАК ПараметрыЦелевогоФинансирования
	|		ПО ПервоначальныеСведения.ОсновноеСредство = ПараметрыЦелевогоФинансирования.ОсновноеСредство
	|ГДЕ
	|	ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втМестонахожденияНМА;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяДоходов КАК СтатьяДоходов
	|
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.Статья КАК СтатьяДоходов
	|
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.Статья ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.КорПодразделение КАК Подразделение,
	|	ДанныеРегистра.КорСтатья КАК СтатьяДоходов
	|
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.КорСтатья ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяДоходов КАК СтатьяДоходов
	|
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
	|
	|ИЗ
	|	Документ.ПереоценкаНМА КАК ДанныеДокумента
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
	|
	|ИЗ
	|	Документ.ПереоценкаОС КАК ДанныеДокумента
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСтатьиЦелевогоФинансированияКОтражению.Организация КАК Организация,
	|	втСтатьиЦелевогоФинансированияКОтражению.Подразделение КАК Подразделение,
	|	втСтатьиЦелевогоФинансированияКОтражению.СтатьяДоходов КАК СтатьяДоходов
	|
	|ИЗ
	|	втСтатьиЦелевогоФинансированияКОтражению КАК втСтатьиЦелевогоФинансированияКОтражению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.СтатьяДоходов КАК АналитикаУчета,
	|	ТаблицаДвижений.Подразделение КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_Доходы
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиПодарочныхСертификатов(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("ПодарочныеСертификаты") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	Документ.АннулированиеПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АннулированиеПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВводОстатков
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВводОстатков КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатков.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВводОстатковОПродажахЗаПрошлыеПериоды
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВводОстатковОПродажахЗаПрошлыеПериоды КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковОПродажахЗаПрошлыеПериоды.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВозвратПодарочныхСертификатов
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ОтчетОРозничныхПродажах
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// РеализацияПодарочныхСертификатов
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.РеализацияПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|			ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|			ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ВидСертификата КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПодарочнымСертификатам) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_ПодарочныеСертификаты
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиРасходов(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("Расходы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МестоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	МестоУчетаНМА.Подразделение КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	РегистрСведений.МестоУчетаНМА.СрезПоследних(&ДатаОкончания) КАК МестоУчетаНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходовБУ) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиАмортизацииКОтражению
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходовБУ = ОтражениеАмортизационныхРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО (ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка)
	|			И (ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И (СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО СпособыОтраженияРасходов.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходовБУ)
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходовБУ = ОтражениеАмортизационныхРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО (ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка)
	|			И (ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И (СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втМестонахожденияНМА;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиПрочихОпераций.Ссылка
	|ПОМЕСТИТЬ СтатьиПрочихОпераций
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ДанныеРегистра.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов), ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.СтатьяРасходов
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО ДанныеДокумента.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|	И ДанныеДокумента.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ТаблицаРаспределениеРасходов.СтатьяРасходов
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК ТаблицаРаспределениеРасходов
	|		ПО ДанныеДокумента.Ссылка = ТаблицаРаспределениеРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (ТаблицаРаспределениеРасходов.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка)
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаНачисления.СтатьяДоходовРасходов КАК ПланВидовХарактеристик.СтатьиРасходов), ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
	|ИЗ
	|	Документ.НачисленияКредитовИДепозитов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачисленияКредитовИДепозитов.Начисления КАК ТаблицаНачисления
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНачисления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (ТаблицаНачисления.СтатьяДоходовРасходов = СтатьиПрочихОпераций.Ссылка)
	|ГДЕ
	|	&УсловиеДляДокумента
	|	
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.СтатьяДоходовРасходов
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО (Статьи.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСтатьиАмортизацииКОтражению.Организация,
	|	втСтатьиАмортизацииКОтражению.Подразделение,
	|	втСтатьиАмортизацииКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиАмортизацииКОтражению КАК втСтатьиАмортизацииКОтражению
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.КорСтатьяРасходов
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО (Статьи.Ссылка = ДанныеРегистра.КорСтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.КорСтатьяРасходов
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО (Статьи.Ссылка = ДанныеРегистра.КорСтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ИСТИНА;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.СтатьяРасходов КАК АналитикаУчета,
	|	ТаблицаДвижений.Подразделение КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_Расходы
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиРезервов(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("Резервы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ВидРезервов КАК ВидРезервов
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.РезервыПредстоящихРасходов КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ВидРезервов КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Резервы) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_Резервы
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиТМЦВЭксплуатации(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("ТМЦВЭксплуатации") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЕСТЬNULL(Таблица.КатегорияЭксплуатации, ЗНАЧЕНИЕ(Справочник.КатегорииЭксплуатации.ПустаяСсылка)) КАК КатегорияЭксплуатации
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Товары КАК Таблица
	|			ПО ДанныеДокумента.Ссылка = Таблица.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ЕСТЬNULL(Таблица.КатегорияЭксплуатации, ЗНАЧЕНИЕ(Справочник.КатегорииЭксплуатации.ПустаяСсылка))
	|ИЗ
	|	Документ.ВводОстатков КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатков.ТМЦВЭксплуатации КАК Таблица
	|		ПО ДанныеДокумента.Ссылка = Таблица.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И ДанныеДокумента.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковТМЦВЭксплуатации))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ЕСТЬNULL(Таблица.КатегорияЭксплуатации, ЗНАЧЕНИЕ(Справочник.КатегорииЭксплуатации.ПустаяСсылка))
	|ИЗ
	|	Документ.ВводОстатковТМЦВЭксплуатации КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковТМЦВЭксплуатации.ТМЦВЭксплуатации КАК Таблица
	|		ПО ДанныеДокумента.Ссылка = Таблица.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И ДанныеДокумента.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковТМЦВЭксплуатации))
	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.КатегорияЭксплуатации КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_ТМЦВЭксплуатации
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиДенежныхСредств(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("ДенежныеСредства") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.БанковскийСчет КАК АналитикаУчета
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Касса КАК АналитикаУчета
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.АналитикаУчета КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ДенежныеСредства) КАК ВидСчета,
	|	ИСТИНА КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_ДенежныеСредства
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОперацийДаннымиВнеоборотныхАктивов(СписокРазделов, МассивТекстовЗапроса)
	
	Если СписокРазделов.Найти("ВнеоборотныеАктивы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеРегистра.СтоимостьРегл <> 0 КАК ЕстьРасчетыПоСтоимости,
	|	ДанныеРегистра.СтоимостьЦФ <> 0 КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизации,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ДанныеРегистра.СтоимостьРегл <> 0 КАК ЕстьРасчетыПоСтоимости,
	|	ДанныеРегистра.СтоимостьЦФ <> 0 КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизации,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ДанныеРегистра.Регистратор ССЫЛКА Документ.СписаниеОС2_4 КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ДанныеРегистра.ЗалоговаяСтоимость <> 0 КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимости,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ДанныеРегистра.АмортизацияРегл <> 0 КАК ЕстьРасчетыПоАмортизации,
	|	ДанныеРегистра.АмортизацияЦФ <> 0 КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимости,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ДанныеРегистра.АмортизацияРегл <> 0 КАК ЕстьРасчетыПоАмортизации,
	|	ДанныеРегистра.АмортизацияЦФ <> 0 КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоСтоимости КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДвиженийПоСчетам_ВнеоборотныеАктивы
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоСтоимостиЦФ КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоАмортизации КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоАмортизации КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоВыбытию КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДооценкиВНА) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоРезервуДооценки КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвижений.Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА) КАК ВидСчета,
	|	ТаблицаДвижений.ЕстьРасчетыПоЗабалансовойСтоимости КАК Требуется
	|ИЗ ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвижений";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
КонецПроцедуры

#КонецОбласти

#Область ДокументыСоответствующиеНастройкам

Процедура НоменклатураЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНастроек.Организация КАК Организация,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ТаблицаНастроек КАК ТаблицаНастроек
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаНастроек.АналитикаУчета = Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|		И ТаблицаНастроек.МестоУчета = Аналитика.МестоХранения
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_НоменклатураСобственная
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, &ДатаОкончания, Регистратор,
	|		ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		И (АналитикаУчетаПоПартнерам.Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра";
	МассивТекстов.Добавить(ТекстЗапроса);
	МассивВременныхТаблиц.Добавить("ВтАналитика");
	
КонецПроцедуры

Процедура НоменклатураПереданнаяЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНастроек.Организация КАК Организация,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ТаблицаНастроек КАК ТаблицаНастроек
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета = ТаблицаНастроек.АналитикаУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_НоменклатураПереданная
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра";
	МассивТекстов.Добавить(ТекстЗапроса);
	МассивВременныхТаблиц.Добавить("ВтАналитика");
	
КонецПроцедуры

Процедура РасчетыСПартнерамиЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_РасчетыСПартнерами
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(, &ДатаОкончания, Регистратор,
	|		(АналитикаУчетаПоПартнерам.Организация, ОбъектРасчетов.ГруппаФинансовогоУчета) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.АналитикаУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(, &ДатаОкончания, Регистратор,
	|		(АналитикаУчетаПоПартнерам.Организация, ОбъектРасчетов.ГруппаФинансовогоУчета) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.АналитикаУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДоходыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_Доходы
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, СтатьяДоходов, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.АналитикаУчета,
	|				ТаблицаНастроек.МестоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Статья, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.АналитикаУчета,
	|				ТаблицаНастроек.МестоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, КорСтатья, КорПодразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.АналитикаУчета,
	|				ТаблицаНастроек.МестоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		ДанныеРегистра.Организация = ТаблицаНастроек.Организация
	|		И ДанныеРегистра.СтатьяДоходов = ТаблицаНастроек.АналитикаУчета
	|		И ДанныеРегистра.Подразделение = ТаблицаНастроек.МестоУчета
	|ГДЕ
	|	(ДанныеРегистра.Период <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура РасходыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_Расходы
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, СтатьяРасходов, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.МестоУчета,
	|				ТаблицаНастроек.АналитикаУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		ДанныеРегистра.Организация = ТаблицаНастроек.Организация
	|		И ДанныеРегистра.СтатьяРасходовСписания = ТаблицаНастроек.АналитикаУчета
	|		И ДанныеРегистра.Подразделение = ТаблицаНастроек.МестоУчета
	|ГДЕ
	|	(ДанныеРегистра.Период <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПроизводствоЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_Производство
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.МестоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|			ГДЕ
	|				ТаблицаНастроек.АналитикаУчета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		)
	|	) КАК ДанныеРегистра";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПодарочныеСертификатыЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	// АнулированиеПодарочныхСертификатов
	"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ПОМЕСТИТЬ втДвиженияСертификатов
	|ИЗ
	|	Документ.АннулированиеПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АннулированиеПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВводОстатков
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВводОстатков КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатков.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВводОстатковОПродажахЗаПрошлыеПериоды
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВводОстатковОПродажахЗаПрошлыеПериоды КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковОПродажахЗаПрошлыеПериоды.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ВозвратПодарочныхСертификатов
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// ОтчетОРозничныхПродажах
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// РеализацияПодарочныхСертификатов
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВидыСертификатов.Ссылка КАК ВидСертификата
	|ИЗ
	|	Документ.РеализацияПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыСертификатов
	|		ПО Строки.ПодарочныйСертификат.Владелец = ВидыСертификатов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеДокумента.Организация В(&Организации)
	|	И ДанныеДокумента.Проведен
	|	И &ИспользоватьПодарочныеСертификаты
	|	И ДанныеДокумента.Дата <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВидСертификата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияСертификатов.Регистратор,
	|	ДвиженияСертификатов.Организация,
	|	ДвиженияСертификатов.ВидСертификата
	|ПОМЕСТИТЬ ТаблицаДокументов_ПодарочныеСертификаты
	|ИЗ
	|	втДвиженияСертификатов КАК ДвиженияСертификатов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНастроек КАК ТаблицаНастроек
	|		ПО ДвиженияСертификатов.Организация = ТаблицаНастроек.Организация
	|		И ДвиженияСертификатов.ВидСертификата = ТаблицаНастроек.ВидСертификата";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТМЦВЭксплуатацииЗаполнитьМассивТекстовЗапросаДокументов(МассивТекстов, МассивВременныхТаблиц)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаДокументов_ТМЦВЭксплуатации
	|ИЗ
	|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(
	|		,
	|		&ДатаОкончания,
	|		Регистратор,
	|		Организация В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра";
	МассивТекстов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1b81975e-b1fe-43d1-ae81-54f2e0dc6ce1");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Перенос счетов учета из ГФУ и отдельных регистров сведений в общий регистр сведений.';uk='Перенесення рахунків обліку з ГФО і окремих регістрів відомостей до загального регістру відомостей.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиДоходов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияДоходов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияНоменклатуры.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияНоменклатурыПереданной.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияПодарочныхСертификатов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияРасходов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияРасчетовСПартнерами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияРезервов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПорядокОтраженияТМЦВЭксплуатации.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.БанковскиеСчетаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ВидыПодарочныхСертификатов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ГруппыФинансовогоУчетаНоменклатуры.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ГруппыФинансовогоУчетаРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Кассы.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.КатегорииЭксплуатации.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Резервы.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ОбновлениеИнформационнойБазыУТ.ОбновитьПредставленияПредопределенныхЭлементов";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ПланыВидовХарактеристик.СтатьиРасходов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ГруппыФинансовогоУчетаРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ГруппыФинансовогоУчетаНоменклатуры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ПорядокОтраженияНаСчетахУчета";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИсточникиДанныхНастроекСчетовУчета();
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ТекстыЗапросов = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТДанныеДляОбработки.АналитикаУчета КАК АналитикаУчета
	|ИЗ
	|	ВТДанныеДляОбработки КАК ВТДанныеДляОбработки
	|ГДЕ
	|	ВТДанныеДляОбработки.МестоУчета = НЕОПРЕДЕЛЕНО
	|	И ВТДанныеДляОбработки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВТДанныеДляОбработки.АналитикаУчета <> НЕОПРЕДЕЛЕНО
	|	И ВТДанныеДляОбработки.ВидСчета <> ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	ТекстыЗапросов.Добавить(ТекстЗапросаИсточникиДанныхНастроекРегистрыСведений());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Организация КАК Организация,
	|	ДанныеДляОбработки.АналитикаУчета КАК АналитикаУчета,
	|	ДанныеДляОбработки.МестоУчета КАК МестоУчета,
	|	ДанныеДляОбработки.ВидСчета КАК ВидСчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитентуОСписании) КАК СчетУчета
	|ИЗ
	|	ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
	|		ПО ПорядокОтраженияНаСчетахУчета.ВидСчета = ДанныеДляОбработки.ВидСчета
	|		И ПорядокОтраженияНаСчетахУчета.АналитикаУчета = ДанныеДляОбработки.АналитикаУчета
	|		И ПорядокОтраженияНаСчетахУчета.МестоУчета = ДанныеДляОбработки.МестоУчета
	|		И ПорядокОтраженияНаСчетахУчета.Организация = ДанныеДляОбработки.Организация
	|		И ПорядокОтраженияНаСчетахУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|ГДЕ
	|	ДанныеДляОбработки.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|	И ПорядокОтраженияНаСчетахУчета.ВидСчета ЕСТЬ NULL";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаСсылочныхИсточниковДанных = ПакетРезультатовЗапроса[0].Выбрать();
	
	//Перенос данных из ссылочных источников данных
	Пока ВыборкаСсылочныхИсточниковДанных.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			МетаданныеАналитикиУчета = ВыборкаСсылочныхИсточниковДанных.АналитикаУчета.Метаданные();
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(МетаданныеАналитикиУчета.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаСсылочныхИсточниковДанных.АналитикаУчета);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			ПеренестиСчетаУчетаВПорядокОтраженияНаСчетахУчета(МетаданныеАналитикиУчета.Имя, ВыборкаСсылочныхИсточниковДанных.АналитикаУчета);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	//Перенос данных из регистров сведений
	ВыборкаРегистровСведений = ПакетРезультатовЗапроса[1].Выбрать();
	
	ИсточникиДанных = Новый ТаблицаЗначений();
	ИсточникиДанных.Колонки.Добавить("ИсточникДанных");
	ИсточникиДанных.Колонки.Добавить("ВидСчета");
	ИсточникиДанных.Колонки.Добавить("Организация");
	ИсточникиДанных.Колонки.Добавить("АналитикаУчета");
	ИсточникиДанных.Колонки.Добавить("МестоУчета");
	ИсточникиДанных.Колонки.Добавить("СчетУчета");
	
	ДобавитьИсточникиДанныхРегистрыСведений(ИсточникиДанных, Истина);
	
	Пока ВыборкаРегистровСведений.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ВыборкаРегистровСведений.ТипМетаданных);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			ЗапросДанныеРегистраСведений = Новый Запрос();
			ЗапросДанныеРегистраСведений.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			ЗапросДанныеРегистраСведений.Текст = ТекстЗапросаПолученияДанныхИзРегистраСведений(ИсточникиДанных, ВыборкаРегистровСведений.ТипМетаданных);
			ВыборкаДанныхРегистраСведений = ЗапросДанныеРегистраСведений.Выполнить().Выбрать();
			
			Пока ВыборкаДанныхРегистраСведений.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
				МестоУчета = ?(ЗначениеЗаполнено(ВыборкаДанныхРегистраСведений.МестоУчета), ВыборкаДанныхРегистраСведений.МестоУчета, Неопределено);
				
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Отбор.ВидСчета.Установить(ВыборкаДанныхРегистраСведений.ВидСчета);
				НаборЗаписей.Отбор.Организация.Установить(ВыборкаДанныхРегистраСведений.Организация);
				НаборЗаписей.Отбор.АналитикаУчета.Установить(ВыборкаДанныхРегистраСведений.АналитикаУчета);
				НаборЗаписей.Отбор.МестоУчета.Установить(МестоУчета);
				
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДанныхРегистраСведений);
				НоваяЗапись.МестоУчета = МестоУчета;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	// Обновление данных на новом регистре сведений
	ВыборкаДанных = ПакетРезультатовЗапроса[2].Выбрать();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
				
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Отбор.ВидСчета.Установить(ВыборкаДанных.ВидСчета);
			НаборЗаписей.Отбор.Организация.Установить(ВыборкаДанных.Организация);
			НаборЗаписей.Отбор.АналитикаУчета.Установить(ВыборкаДанных.АналитикаУчета);
			НаборЗаписей.Отбор.МестоУчета.Установить(ВыборкаДанных.МестоУчета);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДанных);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось обновить данные в регистре %ИмяРегистра% по причине: %Причина%';uk='Не вдалося оновити дані в регістрі %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ПеренестиСчетаУчетаВПорядокОтраженияНаСчетахУчета(ИмяОбъектаМетаданных, Данные)
	
	СчетаУчета = ПолучитьСчетаУчетаРеглПоТипуМетаданных(ИмяОбъектаМетаданных);
	
	Для каждого СчетУчета из СчетаУчета Цикл
		ИмяВидаСчетовРеглУчета = XMLСтрока(СчетУчета.Ключ);
		РеквизитСчетУчета = "Удалить" + СчетУчета.Значение;
		РеквизитыЗаполнения = РеквизитСчетУчета;
		ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИмяВидаСчетовРеглУчета);
		Если ЗначениеЗаполнено(ИмяСубконто) Тогда
			РеквизитыЗаполнения = РеквизитыЗаполнения + ", " + "Удалить" + ИмяСубконто + "Регл";
		КонецЕсли;
		
		ДанныеЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные, РеквизитыЗаполнения);
		
		Если ЗначениеЗаполнено(ДанныеЗаполнения[РеквизитСчетУчета]) Тогда
			НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Отбор.АналитикаУчета.Установить(Данные);
			НаборЗаписей.Отбор.ВидСчета.Установить(СчетУчета.Ключ);
			НаборЗаписей.Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
			НаборЗаписей.Отбор.МестоУчета.Установить(Неопределено);

			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.АналитикаУчета = Данные;
			НоваяЗапись.ВидСчета = СчетУчета.Ключ;
			НоваяЗапись.СчетУчета = ДанныеЗаполнения[РеквизитСчетУчета];
			НоваяЗапись.Субконто = ?(ЗначениеЗаполнено(ИмяСубконто), ДанныеЗаполнения["Удалить" + ИмяСубконто + "Регл"], Неопределено);
			НоваяЗапись.Организация = Справочники.Организации.ПустаяСсылка();
			НоваяЗапись.МестоУчета = Неопределено;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		КонецЕсли;
	КонецЦикла;
	
	Если ИмяОбъектаМетаданных = "ГруппыФинансовогоУчетаНоменклатуры" Тогда
		СписокСчетовНоменклатурыПринятой = СписокСчетовУчетаПоРазделуУчета("НоменклатураПринятая");
		Для каждого СчетУчета Из СписокСчетовНоменклатурыПринятой Цикл
			ТекущийВидСчета = Перечисления.ВидыСчетовРеглУчета[СчетУчета.Значение];
			НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Отбор.АналитикаУчета.Установить(Данные);
			НаборЗаписей.Отбор.ВидСчета.Установить(ТекущийВидСчета);
			НаборЗаписей.Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
			НаборЗаписей.Отбор.МестоУчета.Установить(Неопределено);

			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.АналитикаУчета = Данные;
			НоваяЗапись.ВидСчета = ТекущийВидСчета;           
			ИмяСчетаУчетаНоменклатурыПринятой = СтрЗаменить(СчетУчета.Значение, "ТМЦ", "МатериальныеЦенностиНаОтветственномХранении");
			Если ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыНаСкладе" Тогда
				ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыПринятыеНаКомиссию";
			ИначеЕсли ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыПереданныеНаКомиссию" Тогда
				ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыПринятыеНаКомиссиюНаСубкомиссии";
			КонецЕсли;
			НоваяЗапись.СчетУчета = ПланыСчетов.Хозрасчетный[ИмяСчетаУчетаНоменклатурыПринятой];
			НоваяЗапись.Субконто = Неопределено;
			НоваяЗапись.Организация = Справочники.Организации.ПустаяСсылка();
			НоваяЗапись.МестоУчета = Неопределено;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЦикла;
	КонецЕсли;
	
	Если ИмяОбъектаМетаданных = "ГруппыФинансовогоУчетаРасчетов" Тогда
		РасчетыСКомитентами = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Данные, "РасчетыСКомитентами");
		Если Данные.Пустая() Или РасчетыСКомитентами Тогда
			ТекущийВидСчета = Перечисления.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже;
			НаборЗаписей = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.СоздатьНаборЗаписей();
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Отбор.АналитикаУчета.Установить(Данные);
			НаборЗаписей.Отбор.ВидСчета.Установить(ТекущийВидСчета);
			НаборЗаписей.Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
			НаборЗаписей.Отбор.МестоУчета.Установить(Неопределено);

			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.АналитикаУчета = Данные;
			НоваяЗапись.ВидСчета = ТекущийВидСчета;
			НоваяЗапись.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитентуОСписании;
			НоваяЗапись.Субконто = Неопределено;
			НоваяЗапись.Организация = Справочники.Организации.ПустаяСсылка();
			НоваяЗапись.МестоУчета = Неопределено;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ТекстЗапросаИсточникиДанныхНастроекСчетовУчета()
	
	ИсточникиДанных = Новый ТаблицаЗначений();
	ИсточникиДанных.Колонки.Добавить("ИсточникДанных");
	ИсточникиДанных.Колонки.Добавить("ВидСчета");
	ИсточникиДанных.Колонки.Добавить("Организация");
	ИсточникиДанных.Колонки.Добавить("АналитикаУчета");
	ИсточникиДанных.Колонки.Добавить("МестоУчета");
	ИсточникиДанных.Колонки.Добавить("СчетУчета");
	
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "ПланВидовХарактеристик.СтатьиРасходов");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "ПланВидовХарактеристик.СтатьиДоходов");
	
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.Кассы");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.БанковскиеСчетаОрганизаций");
	ИсключаемыеВидыСчетов = Новый Массив();
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ПередачаВПереработку);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ);
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.ГруппыФинансовогоУчетаНоменклатуры",,,,ИсключаемыеВидыСчетов);
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.ГруппыФинансовогоУчетаРасчетов");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.Резервы");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.КатегорииЭксплуатации");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "Справочник.ВидыПодарочныхСертификатов");
	
	ДобавитьИсточникиДанныхРегистрыСведений(ИсточникиДанных, Истина);
	
	ШаблонТекстаЗапроса = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.#ВидСчета) КАК Видсчета,
	|	#Организация КАК Организация,
	|	#АналитикаУчета КАК АналитикаУчета,
	|	ВЫБОР КОГДА #МестоУчета = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ #МестоУчета = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ИЛИ #МестоУчета = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|		ТОГДА НЕОПРЕДЕЛЕНО ИНАЧЕ #МестоУчета КОНЕЦ КАК МестоУчета
	|ИЗ
	|	#ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.#СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	| И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
	|		ГДЕ
	|			ПорядокОтраженияНаСчетахУчета.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.#ВидСчета)
	|			И ПорядокОтраженияНаСчетахУчета.Организация = #Организация
	|			И ПорядокОтраженияНаСчетахУчета.АналитикаУчета = #АналитикаУчета
	|			И (ПорядокОтраженияНаСчетахУчета.МестоУчета = #МестоУчета
	|				ИЛИ #МестоУчета = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				ИЛИ #МестоУчета = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ИЛИ #МестоУчета = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)))
	|";
	
	ТекстыЗапросов = Новый Массив();
	
	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		ТекстЗапроса = ШаблонТекстаЗапроса;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВидСчета",				ИсточникДанных.ВидСчета);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Организация",			ИсточникДанных.Организация);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#АналитикаУчета",			ИсточникДанных.АналитикаУчета);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МестоУчета",				ИсточникДанных.МестоУчета);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИсточникДанных", 		ИсточникДанных.ИсточникДанных);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#СчетУчета",				ИсточникДанных.СчетУчета);
		ТекстыЗапросов.Добавить(ТекстЗапроса);
	КонецЦикла;
	
	СписокСчетовНоменклатурыПринятой = СписокСчетовУчетаПоРазделуУчета("НоменклатураПринятая");
	Для каждого СчетУчета Из СписокСчетовНоменклатурыПринятой Цикл
		
		ТекстЗапроса = ШаблонТекстаЗапроса;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВидСчета", СчетУчета.Значение);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Организация", "ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#АналитикаУчета", "ИсточникДанных.Ссылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МестоУчета", "НЕОПРЕДЕЛЕНО");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИсточникДанных", "Справочник.ГруппыФинансовогоУчетаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИсточникДанных.#СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)", "ИСТИНА");
		
		ТекстыЗапросов.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЗ
	|	#Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ИсточникДанных", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИсточникДанных.Ссылка", "ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)");
		
		ТекстыЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже) КАК ВидСчета,
	|	ПорядокОтраженияНаСчетахУчета.Организация КАК Организация,
	|	ПорядокОтраженияНаСчетахУчета.АналитикаУчета КАК АналитикаУчета,
	|	ПорядокОтраженияНаСчетахУчета.МестоУчета КАК МестоУчета
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже
	|		ПО ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|			И ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже.Организация = ПорядокОтраженияНаСчетахУчета.Организация
	|			И ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже.АналитикаУчета = ПорядокОтраженияНаСчетахУчета.АналитикаУчета
	|			И ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже.МестоУчета = ПорядокОтраженияНаСчетахУчета.МестоУчета
	|ГДЕ
	|	ПорядокОтраженияНаСчетахУчета.АналитикаУчета ССЫЛКА Справочник.ГруппыФинансовогоУчетаРасчетов
	|	И ЕСТЬNULL(ПорядокОтраженияНаСчетахУчета.АналитикаУчета.РасчетыСКомитентами, ИСТИНА)
	|	И ПорядокОтраженияНаСчетахУчета.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ПорядокОтраженияНаСчетахУчетаТоварыКомитентовКОформлениюПоПродаже.СчетУчета ЕСТЬ NULL";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапросов = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Возврат ТекстЗапросов;
	
КонецФункции

Функция ТекстЗапросаИсточникиДанныхНастроекРегистрыСведений()
	
	ИсточникиДанных = Новый ТаблицаЗначений();
	ИсточникиДанных.Колонки.Добавить("ИсточникДанных");
	ИсточникиДанных.Колонки.Добавить("АналитикаУчета");
	ИсточникиДанных.Колонки.Добавить("МестоУчета");
	
	ДобавитьИсточникиДанныхРегистрыСведений(ИсточникиДанных);
	
	ШаблонТекстаЗапроса = "
	|
	|ВЫБРАТЬ
	|	ИсточникДанных.Организация КАК Организация,
	|	#МестоУчетаИмяИзмерения КАК ИмяИзмеренияМестоУчета,
	|	#МестоУчета КАК МестоУчета,
	|	#АналитикаУчетаИмяИзмерения КАК ИмяИзмеренияАналитикаУчета,
	|	#АналитикаУчета КАК АналитикаУчета,
	|	""#ИсточникДанных"" КАК ТипМетаданных
	|ИЗ
	|	#ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|			ГДЕ
	|				ДанныеДляОбработки.Организация = ИсточникДанных.Организация
	|				И (ДанныеДляОбработки.МестоУчета = #МестоУчета ИЛИ ДанныеДляОбработки.МестоУчета = НЕОПРЕДЕЛЕНО)
	|				И ДанныеДляОбработки.АналитикаУчета = #АналитикаУчета
	|				И ДанныеДляОбработки.ВидСчета <> ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже))
	|
	|";
	
	ТекстыЗапросов = Новый Массив();
	
	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		ТекстЗапроса = ШаблонТекстаЗапроса;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#АналитикаУчетаИмяИзмерения", ?(ИсточникДанных.АналитикаУчета = "НЕОПРЕДЕЛЕНО", "НЕОПРЕДЕЛЕНО", """" + ИсточникДанных.АналитикаУчета + """" ));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#АналитикаУчета",				?(ИсточникДанных.АналитикаУчета = "НЕОПРЕДЕЛЕНО", "НЕОПРЕДЕЛЕНО", "ИсточникДанных." + ИсточникДанных.АналитикаУчета));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МестоУчетаИмяИзмерения",		?(ИсточникДанных.МестоУчета = "НЕОПРЕДЕЛЕНО", "НЕОПРЕДЕЛЕНО", """" + ИсточникДанных.МестоУчета + """" ));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МестоУчета",					?(ИсточникДанных.МестоУчета = "НЕОПРЕДЕЛЕНО", "НЕОПРЕДЕЛЕНО", "ИсточникДанных." + ИсточникДанных.МестоУчета));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИсточникДанных", 			ИсточникДанных.ИсточникДанных);
		ТекстыЗапросов.Добавить(ТекстЗапроса);
	КонецЦикла;
	
	ТекстЗапросов = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Возврат ТекстЗапросов;
	
КонецФункции

Функция ТекстЗапросаПолученияДанныхИзРегистраСведений(ИсточникиДанных, ИмяРегистраСведений)
	
	ШаблонТекстаЗапроса = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.#ВидСчета) КАК Видсчета,
	|	ИсточникДанных.Организация КАК Организация,
	|	#АналитикаУчета КАК АналитикаУчета,
	|	#МестоУчета КАК МестоУчета,
	|	#Субконто КАК Субконто,
	|	ИсточникДанных.#СчетУчета КАК СчетУчета
	|ИЗ
	|	#ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.#СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	| И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|		ГДЕ
	|			ДанныеДляОбработки.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.#ВидСчета)
	|			И ДанныеДляОбработки.Организация = ИсточникДанных.Организация
	|			И ДанныеДляОбработки.АналитикаУчета = #АналитикаУчета
	|			И (ДанныеДляОбработки.МестоУчета = #МестоУчета ИЛИ ДанныеДляОбработки.МестоУчета = НЕОПРЕДЕЛЕНО))
	|
	|";

	ТекстыЗапросов = Новый Массив();
	
	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		Если ИсточникДанных.ИсточникДанных = ИмяРегистраСведений Тогда
			ТекстЗапроса = ШаблонТекстаЗапроса;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВидСчета",			ИсточникДанных.ВидСчета);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#АналитикаУчета",		ИсточникДанных.АналитикаУчета);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МестоУчета",			ИсточникДанных.МестоУчета);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИсточникДанных", 	ИсточникДанных.ИсточникДанных);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#СчетУчета",			ИсточникДанных.СчетУчета);
			
			ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИсточникДанных.ВидСчета);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Субконто", ?(ЗначениеЗаполнено(ИмяСубконто), "ИсточникДанных." + ИмяСубконто + "Регл", "НЕОПРЕДЕЛЕНО"));
			
			ТекстыЗапросов.Добавить(ТекстЗапроса);
		КонецЕсли;
	КонецЦикла;
	
	Если ИмяРегистраСведений = "РегистрСведений.УдалитьПорядокОтраженияНоменклатуры" Тогда
		ШаблонЗапроса = "
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.#ВидСчета) КАК Видсчета,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчета,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
		|	НЕОПРЕДЕЛЕНО КАК Субконто,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.#ИмяСчета) КАК СчетУчета
		|ИЗ
		|	ВТДанныеДляОбработки КАК ДанныеДляОбработки
		|ГДЕ
		|	ДанныеДляОбработки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И ДанныеДляОбработки.АналитикаУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
		|	И ДанныеДляОбработки.МестоУчета = НЕОПРЕДЕЛЕНО
		|
		|";
		СписокСчетовНоменклатурыПринятой = СписокСчетовУчетаПоРазделуУчета("НоменклатураПринятая");
		Для каждого СчетУчета Из СписокСчетовНоменклатурыПринятой Цикл
			
			ТекстЗапроса = ШаблонЗапроса;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВидСчета", СчетУчета.Значение);
			ИмяСчетаУчетаНоменклатурыПринятой = СтрЗаменить(СчетУчета.Значение, "ТМЦ", "МатериальныеЦенностиНаОтветственномХранении");
			Если СчетУчета.Значение = "ТоварыНаСкладе" Тогда
				ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыПринятыеНаКомиссию";
			ИначеЕсли  СчетУчета.Значение = "ТоварыПереданныеНаКомиссию" Тогда
				ИмяСчетаУчетаНоменклатурыПринятой = "ТоварыПринятыеНаКомиссиюНаСубкомиссии";
			КонецЕсли;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяСчета", ИмяСчетаУчетаНоменклатурыПринятой);
			
			ТекстыЗапросов.Добавить(ТекстЗапроса);
		КонецЦикла;
	КонецЕсли;
	
	Если ИмяРегистраСведений = "РегистрСведений.УдалитьПорядокОтраженияРасчетовСПартнерами" Тогда
		ТекстЗапроса = "
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже) КАК ВидСчета,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК АналитикаУчета,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
		|	НЕОПРЕДЕЛЕНО КАК Субконто,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитентуОСписании) КАК СчетУчета
		|ИЗ
		|	ВТДанныеДляОбработки КАК ДанныеДляОбработки
		|ГДЕ
		|	ДанныеДляОбработки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И ДанныеДляОбработки.АналитикаУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)
		|	И ДанныеДляОбработки.МестоУчета = НЕОПРЕДЕЛЕНО
		|
		|";
		ТекстыЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	ТекстЗапросов = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Возврат ТекстЗапросов;
	
КонецФункции

Процедура ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, ИмяИсточникаДанных, РегистрацияИзменений = Истина, АналитикаУчета = "", МестоУчета = "", ИсключаемыеВидыСчетов = Неопределено)
	
	ТипИмяОбъекта = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяИсточникаДанных, ".");
	Типобъекта = ТипИмяОбъекта[0];
	ИмяОбъектаМетаданных = ТипИмяОбъекта[1];
	
	Если РегистрацияИзменений Тогда
		
		СчетаУчета = ПолучитьСчетаУчетаРеглПоТипуМетаданных(ИмяОбъектаМетаданных);
		
		Для Каждого СчетУчета из СчетаУчета Цикл
			Если ИсключаемыеВидыСчетов = Неопределено ИЛИ ИсключаемыеВидыСчетов.Найти(СчетУчета.Ключ) = Неопределено Тогда
				ЭтоРегистрСведений = Типобъекта = "РегистрСведений";
				НоваяСтрока = ИсточникиДанных.Добавить();
				НоваяСтрока.ИсточникДанных	= ИмяИсточникаДанных;
				НоваяСтрока.ВидСчета		= XMLСтрока(СчетУчета.Ключ);
				НоваяСтрока.Организация		= ?(ЭтоРегистрСведений, "ИсточникДанных.Организация", "ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)");
				Если ЭтоРегистрСведений Тогда
					НоваяСтрока.АналитикаУчета	= ?(АналитикаУчета = "", "НЕОПРЕДЕЛЕНО", "ИсточникДанных." + АналитикаУчета);
				Иначе
					НоваяСтрока.АналитикаУчета	= "ИсточникДанных." + ?(АналитикаУчета = "", "Ссылка", АналитикаУчета);
				КонецЕсли;
				НоваяСтрока.МестоУчета		= ?(МестоУчета = "", "НЕОПРЕДЕЛЕНО", "ИсточникДанных." + МестоУчета);
				НоваяСтрока.СчетУчета		= ?(ЭтоРегистрСведений, "", "Удалить") + СчетУчета.Значение;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		НоваяСтрока = ИсточникиДанных.Добавить();
		НоваяСтрока.ИсточникДанных	= ИмяИсточникаДанных;
		НоваяСтрока.АналитикаУчета	= ?(АналитикаУчета = "", "НЕОПРЕДЕЛЕНО", АналитикаУчета);
		НоваяСтрока.МестоУчета		= ?(МестоУчета = "", "НЕОПРЕДЕЛЕНО", МестоУчета);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьИсточникиДанныхРегистрыСведений(ИсточникиДанных, РегистрацияИзменений = Ложь)
	
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияТМЦВЭксплуатации", РегистрацияИзменений,"КатегорияЭксплуатации");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияРезервов", РегистрацияИзменений,"ВидРезервов");
	ИсключаемыеВидыСчетов = Новый Массив();
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.НеотфактурованныеПоставки);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиОсновнойДолг);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиПроценты);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиКомиссия);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиОсновнойДолг);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиПроценты);
	ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиКомиссия);
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияРасчетовСПартнерами", РегистрацияИзменений, "ГруппаФинансовогоУчета",,ИсключаемыеВидыСчетов);
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияДоходов", РегистрацияИзменений, "СтатьяДоходов", "Подразделение");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияРасходов", РегистрацияИзменений,"СтатьяРасходов", "Подразделение");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияПроизводства", РегистрацияИзменений, ,"МестоПроизводства");
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных, "РегистрСведений.УдалитьПорядокОтраженияПодарочныхСертификатов", РегистрацияИзменений, "ВидСертификата");
	
	Если РегистрацияИзменений Тогда
		ИсключаемыеВидыСчетов = Новый Массив();
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.НоменклатураПереданная);
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ПередачаВПереработку);
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ);
	КонецЕсли;
			
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных,
		"РегистрСведений.УдалитьПорядокОтраженияНоменклатуры",
		РегистрацияИзменений,
		"ГруппаФинансовогоУчета",
		"Склад",
		?(РегистрацияИзменений, ИсключаемыеВидыСчетов, Неопределено));
		
	Если РегистрацияИзменений Тогда
		ИсключаемыеВидыСчетов = Новый Массив();
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.НаСкладе);
//		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности);
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродаж);
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.ВычетыИзДоходов);
		ИсключаемыеВидыСчетов.Добавить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродаж);
	КонецЕсли;
	
	ДобавитьИсточникДанныхНастроекСчетовУчета(ИсточникиДанных,
		"РегистрСведений.УдалитьПорядокОтраженияНоменклатурыПереданной",
		РегистрацияИзменений,
		"ГруппаФинансовогоУчета",
		,
		?(РегистрацияИзменений, ИсключаемыеВидыСчетов, Неопределено));
		
КонецПроцедуры

Функция ПолучитьСчетаУчетаРеглПоТипуМетаданных(ИмяОбъектаМетаданных)
	
		Если ИмяОбъектаМетаданных = "СтатьиРасходов"
				ИЛИ  ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияРасходов" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаРасходовРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "СтатьиДоходов"
				ИЛИ  ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияДоходов" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаДоходовРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "Кассы"
				ИЛИ  ИмяОбъектаМетаданных = "БанковскиеСчетаОрганизаций" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаДенежныхСредствРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "ГруппыФинансовогоУчетаНоменклатуры" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаНоменклатурыРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияНоменклатуры" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаНоменклатурыРегл();
		
		ИначеЕсли   ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияНоменклатурыПереданной" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаНоменклатурыРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "ГруппыФинансовогоУчетаРасчетов"
				ИЛИ  ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияРасчетовСПартнерами" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаРасчетовРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "Резервы"
				ИЛИ  ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияРезервов" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаРезервовРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "КатегорииЭксплуатации"
				ИЛИ  ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияТМЦВЭксплуатации" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаТМЦВЭксплуатацииРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "ГруппыФинансовогоУчетаВнеоборотныхАктивов" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаВнеоборотныхАктивовРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияПроизводства" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаПроизводстваРегл();
			
		ИначеЕсли ИмяОбъектаМетаданных = "ВидыПодарочныхСертификатов" 
			ИЛИ ИмяОбъектаМетаданных = "УдалитьПорядокОтраженияПодарочныхСертификатов" Тогда
			СчетаУчета = НастройкаСчетовУчета.СчетаУчетаПодарочныхСертификатовРегл()
			
		КонецЕсли;
		
		Возврат СчетаУчета;
		
КонецФункции

#КонецОбласти

Процедура ПереопределитьТекстЗапросаПоРазделамУчета(ТекстЗапроса, РазделыУчета, ПутиКИзмерениям)
	
	ИспользованиеИзмерений = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
	ИспользованиеИзмерений.Организация = Ложь;
	ИспользованиеИзмерений.АналитикаУчета = Ложь;
	ИспользованиеИзмерений.МестоУчета = Ложь;
	
	ТекстыСоединений = Новый Массив;
	
	ШаблонСоединения =
	"ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК ИмяТаблицы
	|	ПО
	|		ИмяТаблицы.ВидСчета = ВидыСчетовРеглУчета.Ссылка
	|		И &Организация = ИмяТаблицы.Организация
	|		И &АналитикаУчета = ИмяТаблицы.АналитикаУчета
	|		И &МестоУчета = ИмяТаблицы.МестоУчета";
	
	Разделитель = Символы.ПС + Символы.Таб;
	
	ЗначениеСчетаУчета = "ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	ТекстыЗначенийСчетаУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначениеСчетаУчета);
	
	Для каждого РазделУчета Из РазделыУчета Цикл
		
		Если РазделУчета <> "ДенежныеСредства" Тогда
			// Для денежных средств организации используются, однако связь по ним не актуальна, так как они являются общими НСИ.
			ИспользованиеИзмерений.Организация = ИспользованиеИзмерений.Организация Или НастройкаСчетовУчета.НастройкиОрганизацииПоРазделуУчета(РазделУчета).Используется;
		КонецЕсли;
		ИспользованиеИзмерений.АналитикаУчета = ИспользованиеИзмерений.АналитикаУчета Или НастройкаСчетовУчета.НастройкиАналитикиУчетаПоРазделуУчета(РазделУчета).Используется;
		ИспользованиеИзмерений.МестоУчета = ИспользованиеИзмерений.МестоУчета Или НастройкаСчетовУчета.НастройкиМестаУчетаПоРазделуУчета(РазделУчета).Используется;
		
	КонецЦикла;
	
	Если ИспользованиеИзмерений.АналитикаУчета Тогда
		ТекстСоединения = ШаблонСоединения;
		УсловиеОрганизация = ?(ИспользованиеИзмерений.Организация, "ИмяТаблицы.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)", "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&Организация = ИмяТаблицы.Организация", УсловиеОрганизация);
		УсловиеМестоУчета = ?(ИспользованиеИзмерений.МестоУчета, "ИмяТаблицы.МестоУчета = НЕОПРЕДЕЛЕНО", "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&МестоУчета = ИмяТаблицы.МестоУчета", УсловиеМестоУчета);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&АналитикаУчета = ИмяТаблицы.АналитикаУчета", "ИмяТаблицы.АналитикаУчета = " + ПутиКИзмерениям.АналитикаУчета);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "ИмяТаблицы", "НастройкиПоАналитикеУчета");
		ТекстыСоединений.Вставить(0, ТекстСоединения);
		ТекстыЗначенийСчетаУчета.Вставить(0, "ЕСТЬNULL(НастройкиПоАналитикеУчета.СчетУчета, ");
		ТекстыЗначенийСчетаУчета.Добавить(")");
	КонецЕсли;
	Если ИспользованиеИзмерений.Организация Тогда
		ТекстСоединения = ШаблонСоединения;
		УсловиеАналитикаУчета = ?(ИспользованиеИзмерений.АналитикаУчета, "ИмяТаблицы.АналитикаУчета = " + ПутиКИзмерениям.АналитикаУчета, "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&АналитикаУчета = ИмяТаблицы.АналитикаУчета", УсловиеАналитикаУчета);
		УсловиеМестоУчета = ?(ИспользованиеИзмерений.МестоУчета, "ИмяТаблицы.МестоУчета = НЕОПРЕДЕЛЕНО", "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&МестоУчета = ИмяТаблицы.МестоУчета", УсловиеМестоУчета);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&Организация = ИмяТаблицы.Организация", "ИмяТаблицы.Организация = " + ПутиКИзмерениям.Организация);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "ИмяТаблицы", "НастройкиПоОрганизации");
		ТекстыСоединений.Вставить(0, ТекстСоединения);
		ТекстыЗначенийСчетаУчета.Вставить(0, "ЕСТЬNULL(НастройкиПоОрганизации.СчетУчета, ");
		ТекстыЗначенийСчетаУчета.Добавить(")");
	КонецЕсли;
	Если ИспользованиеИзмерений.МестоУчета Тогда
		ТекстСоединения = ШаблонСоединения;
		УсловиеОрганизация = ?(ИспользованиеИзмерений.Организация, "ИмяТаблицы.Организация = " + ПутиКИзмерениям.Организация, "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&Организация = ИмяТаблицы.Организация", УсловиеОрганизация);
		УсловиеАналитикаУчета = ?(ИспользованиеИзмерений.АналитикаУчета, "ИмяТаблицы.АналитикаУчета = " + ПутиКИзмерениям.АналитикаУчета, "ИСТИНА");
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&АналитикаУчета = ИмяТаблицы.АналитикаУчета", УсловиеАналитикаУчета);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "&МестоУчета = ИмяТаблицы.МестоУчета", "ИмяТаблицы.МестоУчета = " + ПутиКИзмерениям.МестоУчета);
		ТекстСоединения = СтрЗаменить(ТекстСоединения, "ИмяТаблицы", "НастройкиПоМестуУчета");
		ТекстыСоединений.Вставить(0, ТекстСоединения);
		ТекстыЗначенийСчетаУчета.Вставить(0, "ЕСТЬNULL(НастройкиПоМестуУчета.СчетУчета, ");
		ТекстыЗначенийСчетаУчета.Добавить(")");
	КонецЕсли;
	
	ПодстрокаЗамены = "ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый КАК ПорядокОтраженияНаСчетахУчетаПереопределяемый
	|	ПО
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый.ВидСчета = ВидыСчетовРеглУчета.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаЗамены, СтрСоединить(ТекстыСоединений, Разделитель));
	ПодстрокаЗамены = "ПорядокОтраженияНаСчетахУчетаПереопределяемый.СчетУчета";
	СтрокаСчетаУчета = СтрСоединить(ТекстыЗначенийСчетаУчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаЗамены, СтрокаСчетаУчета);
	ПодстрокаЗамены = "ПорядокОтраженияНаСчетахУчетаПереопределяемый.Субконто";
	СтрокаСубконто = СтрЗаменить(СтрокаСчетаУчета, "СчетУчета", "Субконто");
	СтрокаСубконто = СтрЗаменить(СтрокаСчетаУчета, "ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)", "НЕОПРЕДЕЛЕНО");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаЗамены, СтрокаСубконто);
	
КонецПроцедуры

Функция ЗначениеРавенстваИзмеренияПорядкаОтражения(ТекстЗапроса, ИмяИзмерения, ПозицияОпределенияПорядкаОтраженияВЗапросе)
	
	ШаблонИзмеренияВЗапросе = "ПорядокОтраженияНаСчетахУчетаПереопределяемый.%1 = ";
	ТекстИзмерения = СтрШаблон(ШаблонИзмеренияВЗапросе, ИмяИзмерения);
	ПозицияНачалаОпределенияИзмеренияВЗапросе = СтрНайти(ТекстЗапроса, ТекстИзмерения, , ПозицияОпределенияПорядкаОтраженияВЗапросе);
	
	Если ПозицияНачалаОпределенияИзмеренияВЗапросе = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПозицияНачалаЗначенияРавенстваИзмерения = ПозицияНачалаОпределенияИзмеренияВЗапросе + СтрДлина(ТекстИзмерения);
	ПозицияОкончанияОпределенияИзмеренияВЗапросе = СтрНайти(ТекстЗапроса, Символы.ПС, , ПозицияНачалаЗначенияРавенстваИзмерения);
	
	Возврат Сред(ТекстЗапроса, ПозицияНачалаЗначенияРавенстваИзмерения, ПозицияОкончанияОпределенияИзмеренияВЗапросе - ПозицияНачалаЗначенияРавенстваИзмерения);
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#КонецОбласти
