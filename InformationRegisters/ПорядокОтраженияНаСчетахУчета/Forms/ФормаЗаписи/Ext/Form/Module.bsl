#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);
	
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	КэшДанныхМеханизмов = Новый Структура;
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	РедактируемыеРазделы = КэшДанныхМеханизмов.НастройкаСчетовУчета.СписокРазделовУчета;
	РедактируемыеРазделы = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(РедактируемыеРазделы);
	Оповестить("ЗаконченаНастройкаСчетовУчета", РедактируемыеРазделы);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Необходимо указать вид счета!';uk='Необхідно вказати вид рахунку!'"), Запись.ИсходныйКлючЗаписи,, "Запись.ВидСчета", Отказ);
	КонецЕсли;
	РазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоНастройкам(КэшДанныхМеханизмов.НастройкаСчетовУчета);
	НастройкаРаздела = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(РазделУчета);
	ШаблонОшибки = НСтр("ru='Для учета %1 по виду счета ""%2"" необходимо указать %3!';uk='Для обліку %1 по виду рахунку ""%2"" необхідно вказати %3!'");
	
	Если ЗначениеЗаполнено(Запись.ВидСчета) И Не ЗначениеЗаполнено(Запись.СчетУчета) Тогда
		ТекстОшибки = СтрШаблон(ШаблонОшибки, НастройкаРаздела.Представление, Запись.ВидСчета, НСтр("ru='значение счета';uk='значення рахунку'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Запись.ИсходныйКлючЗаписи,, "Запись.СчетУчета", Отказ);
	КонецЕсли;
	
	РазделыУчетаСОбязательнойАналитикой = НастройкаСчетовУчетаКлиентСервер.РазделыУчетаСОбязательнойАналитикой();
	Если РазделыУчетаСОбязательнойАналитикой.Найти(РазделУчета) <> Неопределено И Не ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		ТекстОшибки = СтрШаблон(ШаблонОшибки, НастройкаРаздела.Представление, Запись.ВидСчета, НастройкаРаздела.СвойстваАналитики.Заголовок);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Запись.ИсходныйКлючЗаписи,, "Запись.АналитикаУчета", Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Организация) Тогда
		МестоУчетаОбязательно = НастройкаРаздела.СвойстваМестаУчета.Используется;
		Если МестоУчетаОбязательно И Не ЗначениеЗаполнено(Запись.МестоУчета) Тогда
			ТекстОшибки = СтрШаблон(ШаблонОшибки, НастройкаРаздела.Представление, Запись.ВидСчета, НастройкаРаздела.СвойстваМестаУчета.Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Запись.ИсходныйКлючЗаписи,, "Запись.МестоУчета", Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АналитикаУчетаПриИзменении(Элемент)
	
	АналитикаУчетаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСчетаПриИзменении(Элемент)
	
	ВидСчетаПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АналитикаУчетаПриИзмененииСервер()
	
	СтарыеРазделыУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(КэшДанныхМеханизмов.НастройкаСчетовУчета.СписокРазделовУчета);
	
	//Считаем, что если аналитика очищается, то при незаполненном виде счета, сбрасываются все настройки по разделам и разделы можно менять снова:
	Если Не ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		Если Не ЗначениеЗаполнено(Запись.ВидСчета) И ВозвратКоВсемРазделам Тогда
			Запись.АналитикаУчета = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	НовыеРазделыУчета = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.РазделыУчетаПоАналитикеУчета(Запись.АналитикаУчета);
	
	Если НовыеРазделыУчета = Неопределено Или Не ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(НовыеРазделыУчета, СтарыеРазделыУчета) Тогда
		// Изменились разделы учета:
		ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(НовыеРазделыУчета);
		ОбщегоНазначенияУТ.СохранитьДанныеМеханизмаВКэшФормы(ЭтотОбъект, "НастройкаСчетовУчета", ПараметрыНастройкиСчетовУчета);
		УточнитьПараметрыНастройкиСчетовДляТекущейФормы(ПараметрыНастройкиСчетовУчета);
		УстановитьСвойстваИзмеренийНаФорме();
		Запись.ВидСчета = Неопределено;
		Запись.Организация = Неопределено;
		Запись.МестоУчета = Неопределено;
		
	Иначе
		ПараметрыНастройкиСчетовУчета = КэшДанныхМеханизмов.НастройкаСчетовУчета;
	КонецЕсли;
	
	ОбновитьРеквизитыАналитики(ПараметрыНастройкиСчетовУчета, Запись);
	Если ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		НастройкаСчетовУчета.ЗаполнитьРеквизитыНастройкиСчетовУчетаПоДаннымАналитики(ПараметрыНастройкиСчетовУчета, Запись.АналитикаУчета);
	КонецЕсли;
	
	МассивДоступныхВидовСчетов = ПараметрыВыбораВидаСчета(ПараметрыНастройкиСчетовУчета.СписокРазделовУчета);
	Элементы.ВидСчета.ПараметрыВыбора = МассивДоступныхВидовСчетов;
	Если МассивДоступныхВидовСчетов.Получить(0).Значение.Количество() = 1 Тогда
		Запись.ВидСчета = МассивДоступныхВидовСчетов.Получить(0).Значение.Получить(0);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		УстановитьИспользуемыеСчетаУчета(ПараметрыНастройкиСчетовУчета, Запись.ВидСчета);
	КонецЕсли;
	
	ТекущийРазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоНастройкам(КэшДанныхМеханизмов.НастройкаСчетовУчета);
	НастройкаАналитики = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ТекущийРазделУчета).СвойстваАналитики;
	
	ВозвратКоВсемРазделам = (Не ВозвратКоВсемРазделам Или Не ЗначениеЗаполнено(НастройкаАналитики.ПредставлениеПустого)) И Не ЗначениеЗаполнено(Запись.АналитикаУчета);
	Элементы.АналитикаУчета.ОтметкаНезаполненного = Ложь;
	
	УстановитьВидимостьСчетаИСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ВидСчетаПриИзмененииСервер()
	
	Если Не ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		
		СтарыеРазделыУчета = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(КэшДанныхМеханизмов.НастройкаСчетовУчета.СписокРазделовУчета);
		Если ЗначениеЗаполнено(Запись.ВидСчета) Тогда
			ИмяВидаСчета = XMLСтрока(Запись.ВидСчета);
			НовыйРазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоИмениСчета(ИмяВидаСчета);
			НовыеРазделыУчета = НастройкаСчетовУчетаКлиентСервер.ПодразделыОбщегоРаздела(НовыйРазделУчета);
		Иначе
			НовыеРазделыУчета = Неопределено;
		КонецЕсли;
		
		Если (НовыеРазделыУчета = Неопределено Или Не ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(НовыеРазделыУчета, СтарыеРазделыУчета)) Тогда
			// Изменились разделы учета:
			ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(НовыеРазделыУчета);
			ОбщегоНазначенияУТ.СохранитьДанныеМеханизмаВКэшФормы(ЭтотОбъект, "НастройкаСчетовУчета", ПараметрыНастройкиСчетовУчета);
			УточнитьПараметрыНастройкиСчетовДляТекущейФормы(ПараметрыНастройкиСчетовУчета);
			УстановитьСвойстваИзмеренийНаФорме();
			ОбновитьРеквизитыАналитики(ПараметрыНастройкиСчетовУчета, Запись);
			Запись.Организация = Неопределено;
			Запись.МестоУчета = Неопределено;
			
		Иначе
			ПараметрыНастройкиСчетовУчета = КэшДанныхМеханизмов.НастройкаСчетовУчета;
		КонецЕсли;
		
		МассивДоступныхВидовСчетов = ПараметрыВыбораВидаСчета(ПараметрыНастройкиСчетовУчета.СписокРазделовУчета);
		Элементы.ВидСчета.ПараметрыВыбора = МассивДоступныхВидовСчетов;
		Если МассивДоступныхВидовСчетов.Получить(0).Значение.Количество() = 1 Тогда
			Запись.ВидСчета = МассивДоступныхВидовСчетов.Получить(0).Значение.Получить(0);
		КонецЕсли;
		
	Иначе
		
		ПараметрыНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(ЭтотОбъект, "НастройкаСчетовУчета");
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		УстановитьИспользуемыеСчетаУчета(ПараметрыНастройкиСчетовУчета, Запись.ВидСчета);
	КонецЕсли;
	
	УстановитьВидимостьСчетаИСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		ИмяВидаСчета = XMLСтрока(Запись.ВидСчета);
		РазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоИмениСчета(ИмяВидаСчета);
		РазделыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РазделУчета);
	ИначеЕсли ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		РазделыУчета = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.РазделыУчетаПоАналитикеУчета(Запись.АналитикаУчета);
	Иначе
		РазделыУчета = Неопределено;
	КонецЕсли;
	
	ПараметрыНастройкиСчетовУчета = НастройкаСчетовУчета.ПараметрыНастройкиСчетовУчета(РазделыУчета);
	ОбщегоНазначенияУТ.СохранитьДанныеМеханизмаВКэшФормы(ЭтотОбъект, "НастройкаСчетовУчета", ПараметрыНастройкиСчетовУчета);
	УточнитьПараметрыНастройкиСчетовДляТекущейФормы(ПараметрыНастройкиСчетовУчета);
	УстановитьСвойстваИзмеренийНаФорме();
	ОбновитьРеквизитыАналитики(ПараметрыНастройкиСчетовУчета, Запись);
	Если ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		НастройкаСчетовУчета.ЗаполнитьРеквизитыНастройкиСчетовУчетаПоДаннымАналитики(ПараметрыНастройкиСчетовУчета, Запись.АналитикаУчета);
	КонецЕсли;
	
	ТекущийРазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоНастройкам(КэшДанныхМеханизмов.НастройкаСчетовУчета);
	НастройкаАналитики = КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов.Получить(ТекущийРазделУчета).СвойстваАналитики;
	ВозвратКоВсемРазделам = РазделыУчета = Неопределено Или Не ЗначениеЗаполнено(НастройкаАналитики.ПредставлениеПустого);
	
	МассивДоступныхВидовСчетов = ПараметрыВыбораВидаСчета(ПараметрыНастройкиСчетовУчета.СписокРазделовУчета);
	Элементы.ВидСчета.ПараметрыВыбора = МассивДоступныхВидовСчетов;
	Если МассивДоступныхВидовСчетов.Получить(0).Значение.Количество() = 1 Тогда
		Запись.ВидСчета = МассивДоступныхВидовСчетов.Получить(0).Значение.Получить(0);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		УстановитьИспользуемыеСчетаУчета(ПараметрыНастройкиСчетовУчета, Запись.ВидСчета);
	КонецЕсли;
	
	УстановитьВидимостьСчетаИСубконто();
	
КонецПроцедуры

&НаСервере
Процедура УточнитьПараметрыНастройкиСчетовДляТекущейФормы(ПараметрыНастройки)
	
	ПараметрыНастройки.ИмяГруппыНастроекСчетовУчета = "ГруппаНастроекСчетов";
	ПараметрыНастройки.ПрефиксПутиКДаннымРеквизитов = "Запись.АналитикаУчета.";
	ПараметрыНастройки.ПрефиксПутиКДанным = "";
	ПараметрыНастройки.РазбитьПоРазделамЭлементы = ПараметрыНастройки.СписокРазделовУчета.Количество() > 1;
	ПараметрыНастройки.Вставить("МенятьОбщие");
	ПараметрыНастройки.СоздаватьВСлучаеОтсутствия = Ложь;
	ПараметрыНастройки.Вставить("НастройкаПоОдному");
	ПараметрыНастройки.РазбитьПоРазделамЭлементы = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВыбораВидаСчета(СписокРазделов)
	
	МассивРазделов = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокРазделов);
	МассивВидовСчетов = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.НастраиваемыеВидыСчетов(МассивРазделов);
	МассивПараметровВыбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый ПараметрВыбора("Отбор.Ссылка", МассивВидовСчетов));
	Возврат Новый ФиксированныйМассив(МассивПараметровВыбора);
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьИспользуемыеСчетаУчета(ПараметрыНастройки, ВидСчета)
	
	ИмяВидаСчета = "";
	ИмяСубконто = "";
	Если ЗначениеЗаполнено(ВидСчета) Тогда
		ИмяВидаСчета = XMLСтрока(ВидСчета);
		ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИмяВидаСчета);
	КонецЕсли;
	Для каждого НастройкаРаздела Из ПараметрыНастройки.НастройкиРазделов Цикл
		Для каждого СчетУчета Из НастройкаРаздела.Значение.СчетаУчета Цикл
			СчетУчета.Пометка = СчетУчета.Значение = ИмяВидаСчета Или ИмяВидаСчета = "";
		КонецЦикла;
		Для каждого Субконто Из НастройкаРаздела.Значение.Субконто Цикл
			Субконто.Пометка = Субконто.Значение = ИмяСубконто Или ИмяСубконто = "";
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьРеквизитыАналитики(ПараметрыНастройки, Запись)
	
	Если ЗначениеЗаполнено(Запись.АналитикаУчета) Тогда
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Запись.АналитикаУчета);
		МенеджерОбъекта.ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(ПараметрыНастройки.ПутиКРеквизитамАналитики);
	Иначе
		Для каждого НастройкаРаздела Из ПараметрыНастройки.НастройкиРазделов Цикл
			ОписаниеТиповАналитикиУчета = НастройкаРаздела.Значение.СвойстваАналитики.ТипЗначения;
			Если ОписаниеТиповАналитикиУчета = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Для каждого Тип Из ОписаниеТиповАналитикиУчета.Типы() Цикл
				МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
				ИмяМетаданных = МетаданныеТипа.ПолноеИмя();
				МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМетаданных);
				МенеджерОбъекта.ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(ПараметрыНастройки.ПутиКРеквизитамАналитики);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	СчетаУчетаДляПолучения = Новый Массив;
	Для каждого Реквизит Из ПараметрыНастройки.РеквизитыАналитики Цикл
		ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Реквизит.Ключ);
		Если СтрНачинаетсяС(Реквизит.Ключ, "СчетУчета") Тогда
			СчетаУчетаДляПолучения.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЦикла;
	
	Если СчетаУчетаДляПолучения.Количество() Тогда
		Раздел = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоНастройкам(ПараметрыНастройки);
		СтруктураИзмерений = НастройкаСчетовУчетаКлиентСервер.ИнициализироватьСтруктуруИзмеренийРегистра();
		ЗаполнитьЗначенияСвойств(СтруктураИзмерений, Запись);
		НастройкиСчетов = РегистрыСведений.ПорядокОтраженияНаСчетахУчета.НастройкаОтраженияРаздела(Раздел, СтруктураИзмерений, СчетаУчетаДляПолучения);
		Для каждого Реквизит Из ПараметрыНастройки.РеквизитыАналитики Цикл
			ИмяЭлементаНастройки = НастройкаСчетовУчетаКлиентСервер.ИмяЭлементаНастройкиПоИмениЭлементаФормы(Реквизит.Ключ);
			Если НастройкиСчетов.Свойство(ИмяЭлементаНастройки) Тогда
				ПараметрыНастройки.РеквизитыАналитики.Вставить(Реквизит.Ключ, НастройкиСчетов[ИмяЭлементаНастройки]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваИзмеренийНаФорме()
	
	НастройкаСчетовУчетаКлиентСервер.УстановитьСвойстваИзмеренийНаФорме(ЭтотОбъект);
	Для каждого НастройкаРаздела Из КэшДанныхМеханизмов.НастройкаСчетовУчета.НастройкиРазделов Цикл
		НастройкаСчетовУчетаКлиентСервер.УстановитьСвойстваИзмеренийНаФорме(ЭтотОбъект, НастройкаРаздела.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСчетаИСубконто()
	
	Если Не ЗначениеЗаполнено(Запись.ВидСчета) Тогда
		Элементы.СчетУчета.Видимость = Ложь;
		Элементы.Субконто.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.СчетУчета.Видимость = Истина;
	Элементы.СчетУчета.Доступность = Истина;
	
	ИмяВидаСчета = XMLСтрока(Запись.ВидСчета);
	
	Элементы.СчетУчета.ПараметрыВыбора = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСчетаУчета(ИмяВидаСчета, КэшДанныхМеханизмов.НастройкаСчетовУчета);
	НастройкаСчетовУчетаКлиентСервер.УстановитьСвойстваСчетовНаФорме(ЭтотОбъект, ИмяВидаСчета);
	ИмяСубконто = НастройкаСчетовУчетаКлиентСервер.ИмяСубконтоСчета(ИмяВидаСчета);
	Если ИмяСубконто = Неопределено Тогда
		Элементы.Субконто.Видимость = Ложь;
	Иначе
		НастройкаСчетовУчетаКлиентСервер.УстановитьСвойстваСчетовНаФорме(ЭтотОбъект, ИмяСубконто);
		Элементы.Субконто.ОграничениеТипа = НастройкаСчетовУчетаКлиентСервер.ТипСубконтоПоИмени(ИмяСубконто);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти