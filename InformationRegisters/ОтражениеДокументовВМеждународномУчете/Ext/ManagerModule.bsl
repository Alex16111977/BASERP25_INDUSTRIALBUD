#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыСведений.ОтражениеДокументовВМеждународномУчете.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("961c6e08-e8af-404b-8e18-1095473b1d5e");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ОтражениеДокументовВМеждународномУчете.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	Обработчик.БлокируемыеОбъекты = "";
	Обработчик.Комментарий = НСтр("ru='Обновляет записи регистра сведений ""Отражение документов в международном учете"" - заполняет пустые поля ""Дата отражения"" и ""Учетная политика"".';uk='Оновлює записи регістру відомостей ""Відображення документів у міжнародному обліку"" - заповнює порожні поля ""Дата відображення"" та ""Облікова політика"".'");
	
КонецПроцедуры

// Регистрирует к последующей обработке документы, отраженные в международном учете для заполнения даты отражения или очистки движений, в случае неактивных движений.
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВМеждународномУчете.Регистратор
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
	|ГДЕ
	|	(ОтражениеДокументовВМеждународномУчете.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ НЕ ОтражениеДокументовВМеждународномУчете.Активность)
	|	И НЕ ТИПЗНАЧЕНИЯ(ОтражениеДокументовВМеждународномУчете.Регистратор) В (
	|		ТИП(Документ.ВнесениеДенежныхСредствВКассуККМ),
	|		ТИП(Документ.ВыемкаДенежныхСредствИзКассыККМ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВМеждународномУчете.Регистратор
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ОтражениеДокументовВМеждународномУчете.Регистратор) В (
	|		ТИП(Документ.ВнесениеДенежныхСредствВКассуККМ),
	|		ТИП(Документ.ВыемкаДенежныхСредствИзКассыККМ))";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), 
		ДополнительныеПараметры
	);
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ДатаОтражения = ДАТАВРЕМЯ(1,1,1)
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"), ДополнительныеПараметры);
	
КонецПроцедуры

// Заполняется дату отражения в РС "ОтражениеДокументовВМеждународномУчете" документов или очищает движения (если текущие движения не активны).
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	
	ОчиститьДвижения = Новый Соответствие;
	ОчиститьДвижения.Вставить(Тип("ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ"), Истина);
	ОчиститьДвижения.Вставить(Тип("ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ"), Истина);
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(
		Параметры.Очередь, 
		Неопределено, 
		ПолноеИмяРегистра
	);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(УчетнаяПолитикаМУ.Период) КАК Период,
	|	Движения.Организация КАК Организация
	|ПОМЕСТИТЬ втПериоды
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК Движения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМУ
	|		ПО Движения.Организация = УчетнаяПолитикаМУ.Организация
	|			И Движения.Период >= УчетнаяПолитикаМУ.Период
	|ГДЕ
	|	Движения.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	Движения.Организация
	|;
	|
	|//////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Период КАК Период,
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.НомерСтроки КАК НомерСтроки,
	|	Движения.Активность КАК Активность,
	|	Движения.Организация КАК Организация,
	|	Движения.Период КАК ДатаОтражения,
	|	Движения.Статус КАК Статус,
	|	УчетнаяПолитикаМУ.УчетнаяПолитика КАК УчетнаяПолитика,
	|	Движения.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК Движения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериоды КАК ПериодыУчетнойПолитики
	|		ПО Движения.Организация = ПериодыУчетнойПолитики.Организация
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМУ
	|		ПО Движения.Организация = УчетнаяПолитикаМУ.Организация
	|			И ПериодыУчетнойПолитики.Период = УчетнаяПолитикаМУ.Период
	|ГДЕ
	|	Движения.Регистратор = &Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Движения.НомерСтроки");
	
	Пока Выборка.Следующий() Цикл
		
		Регистратор = Выборка.Регистратор;
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

			Блокировка.Заблокировать();
			
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			
			Набор = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Регистратор);
			
			ТипРегистратора = ТипЗнч(Регистратор);
			Если ОчиститьДвижения[ТипРегистратора] = Неопределено Тогда
				Запрос.УстановитьПараметр("Регистратор", Регистратор);
				Результат = Запрос.Выполнить().Выгрузить();
				Если Результат.Количество() = 0 Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор, ДополнительныеПараметры);
				Иначе
					Набор.Загрузить(Результат);
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				КонецЕсли;
			Иначе
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось обработать документ: %Регистратор% по причине: %Причина%';uk='Не вдалося обробити документ: %Регистратор% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Регистратор%", Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Регистратор.Метаданные(), 
				ТекстСообщения
			);
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли