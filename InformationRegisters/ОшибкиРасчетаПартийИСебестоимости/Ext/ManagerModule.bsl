#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает массив идентификаторов протоколов расчета указанных организаций.
//
Функция ПолучитьПротоколыРасчета(ПериодРасчета, МассивОрганизаций) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВариантыРасчета = Новый Массив;
	
	Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ПериодРасчета)) Тогда
		ВариантыРасчета.Добавить(Перечисления.ВариантыРасчетаПартийИСебестоимости.ПартииИСебестоимость);
		ВариантыРасчета.Добавить(Перечисления.ВариантыРасчетаПартийИСебестоимости.ФактическаяСебестоимость);
	Иначе
		ВариантыРасчета.Добавить(Перечисления.ВариантыРасчетаПартийИСебестоимости.ФактическаяСебестоимость);
	КонецЕсли;
	
	ИдентификаторыПротоколов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ИдентификаторПротокола КАК ИдентификаторПротокола,
	|	Т.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ОшибкиРасчетаПартийИСебестоимости КАК Т
	|ГДЕ
	|	Т.Период = &Период
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.ВариантРасчета В (&ВариантыРасчета)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИдентификаторПротокола,
	|	Организация
	|ИТОГИ ПО
	|	ИдентификаторПротокола";
	
	Запрос.УстановитьПараметр("Период", 		   НачалоМесяца(ПериодРасчета));
	Запрос.УстановитьПараметр("МассивОрганизаций", ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций));
	Запрос.УстановитьПараметр("ВариантыРасчета",   ВариантыРасчета);
	
	ВыборкаИдентификаторов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИдентификаторов.Следующий() Цикл
		
		ОрганизацииПротокола = Новый Массив;
		Выборка = ВыборкаИдентификаторов.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ОрганизацииПротокола.Добавить(Выборка.Организация);
		КонецЦикла;
		
		ИдентификаторыПротоколов.Вставить(ВыборкаИдентификаторов.ИдентификаторПротокола, ОрганизацииПротокола);
		
	КонецЦикла;
	
	Возврат ИдентификаторыПротоколов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыСведений.ОшибкиРасчетаПартийИСебестоимости.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("33387b88-0faf-4e8e-ba59-3da530c9f50d");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ОшибкиРасчетаПартийИСебестоимости.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ЧитаемыеОбъекты = "Справочник.ПроверкиСостоянияСистемы,"
		+ "РегистрСведений.ОшибкиРасчетаПартийИСебестоимости,"
		+ "РегистрСведений.ПроблемыСостоянияСистемы";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ВыполнениеПроверокСостоянияСистемы,"
		+ "РегистрСведений.ПроблемыСостоянияСистемы";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ВыполнениеПроверокСостоянияСистемы,"
		+ "РегистрСведений.ПроблемыСостоянияСистемы";
	Обработчик.Комментарий = НСтр("ru='Переносит информацию об ошибках расчета партий и себестоимости в новый регистр универсального механизма проверок.';uk='Переносить інформацію про помилки розрахунку партій та собівартості у новий регістр універсального механізму перевірок.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ПроверкиСостоянияСистемы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

Функция ПолноеИмяРегистра()
	
	Возврат "РегистрСведений.ОшибкиРасчетаПартийИСебестоимости";
	
КонецФункции

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра();
	
	Проверка = ЗакрытиеМесяцаСервер.СлужебнаяПроверкаЭтапа(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Проверка", Проверка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроблемыСостоянияСистемы.Организация КАК Организация
	|ПОМЕСТИТЬ ВТЗарегистрированныеПроблемы
	|ИЗ
	|	РегистрСведений.ПроблемыСостоянияСистемы КАК ПроблемыСостоянияСистемы
	|ГДЕ
	|	ПроблемыСостоянияСистемы.Проверка = &Проверка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Период,
	|	Т.Организация
	|ИЗ
	|	РегистрСведений.ОшибкиРасчетаПартийИСебестоимости КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗарегистрированныеПроблемы КАК Проблемы
	|		ПО Т.Организация = Проблемы.Организация
	|ГДЕ
	|	Проблемы.Организация ЕСТЬ NULL
	|";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	// Подготовим выборку данных.
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТРегистрацияОшибок";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра(),
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Период КАК Период,
	|	Т.Организация
	|ИЗ
	|	ВТРегистрацияОшибок КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Период";
	
	ВыборкаПериоды = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПериоды.Следующий() Цикл
		
		МассивОрганизаций = Новый Массив;
		
		ВыборкаОрганизации = ВыборкаПериоды.Выбрать();
		
		НачатьТранзакцию();
		
		Попытка
			
			Пока ВыборкаОрганизации.Следующий() Цикл
				
				МассивОрганизаций.Добавить(ВыборкаОрганизации.Организация);
				
				// Отметим запись в текущем регистре как отработанную.
				НаборЗаписей = РегистрыСведений.ОшибкиРасчетаПартийИСебестоимости.СоздатьНаборЗаписей();
				
				НаборЗаписей.Отбор.Период.Установить(ВыборкаПериоды.Период);
				НаборЗаписей.Отбор.Организация.Установить(ВыборкаОрганизации.Организация);
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				
			КонецЦикла;
			
			// Создадим запись в новом универсальном регистре результатов проверок состояния учета.
			ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемыРасчета(
				Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости,
				МассивОрганизаций,
				ВыборкаПериоды.Период);
			
			ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРегистрации,
				НСтр("ru='При выполнении расчета по организации ""%1"" за период %2 были диагностированы ошибки.
                    |Подробнее см. протокол расчета.'
                    |;uk='При виконанні розрахунку по організації ""%1"" за період %2 були діагностовані помилки.
                    |Докладніше див. протокол розрахунку.'"),
				Перечисления.ВариантыВажностиПроблемыСостоянияСистемы.Ошибка);
				
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра());
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли
