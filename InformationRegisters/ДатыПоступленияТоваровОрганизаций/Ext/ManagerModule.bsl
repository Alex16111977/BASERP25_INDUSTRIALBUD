#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2ef2008f-3c71-4f18-86f3-e7bf215cec0d");
	Обработчик.Процедура = "РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.ДатыПоступленияТоваровОрганизаций,"
		+ "Справочник.Назначения,"
		+ "Справочник.ВидыЗапасов";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ДатыПоступленияТоваровОрганизаций";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ДатыПоступленияТоваровОрганизаций";
	Обработчик.Комментарий = НСтр("ru='Заполняет новое измерение Назначение. Пока работа обработчика обновления не завершена - подбор видов запасов будет некорректным.';uk='Заповнює новий вимір Назначение. Поки робота обробника оновлення не завершена - підбір видів запасів буде некоректним.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";    
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Партнеры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.СгенерироватьДокументыДляПереброскиОстатковСПустогоНазначенияПоДавальческойСхеме";
	НоваяСтрока.Порядок = "До";
	
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыОбработатьДанныеДляГенерацииНазначений(
		Обработчик.ПриоритетыВыполнения,
		"После"
	);
	
КонецПроцедуры

// Обработчик обновления УТ 3.5.4
// В движениях регистра заполняет новый реквизит Назначение
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ДатыПоступленияТоваровОрганизаций");
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Даты.ВидЗапасов,
	|	Даты.Номенклатура,
	|	Даты.Характеристика,
	|	Даты.Серия,
	|	Даты.НомерГТД
	|ИЗ
	|	РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК Даты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|	ПО Даты.ВидЗапасов = СпрВидыЗапасов.Ссылка
	|ГДЕ
	|	Даты.Назначение <> СпрВидыЗапасов.УстарелоНазначение
	|	И СпрВидыЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Даты.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|");
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления УТ 3.5.4
// В движениях регистра заполняет новый реквизит Назначение
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	ДанныеКОбработке = ОбновлениеИнформационнойБазы.ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь, 
		"РегистрСведений.ДатыПоступленияТоваровОрганизаций", 
		ДополнительныеПараметры
	);
	
	Пока ДанныеКОбработке.Следующий() Цикл
		
		Попытка
			
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДатыПоступленияТоваровОрганизаций");
			ЭлементБлокировки.УстановитьЗначение("ВидЗапасов", ДанныеКОбработке.ВидЗапасов);
			ЭлементБлокировки.УстановитьЗначение("Номенклатура", ДанныеКОбработке.Номенклатура);
			ЭлементБлокировки.УстановитьЗначение("Характеристика", ДанныеКОбработке.Характеристика);
			ЭлементБлокировки.УстановитьЗначение("Серия", ДанныеКОбработке.Серия);
			ЭлементБлокировки.УстановитьЗначение("НомерГТД", ДанныеКОбработке.НомерГТД);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			Запрос = Новый Запрос("
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Даты.ВидЗапасов,
			|	Даты.Номенклатура,
			|	Даты.Характеристика,
			|	Даты.Серия,
			|	ВЫБОР КОГДА Даты.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И Даты.Назначение <> Даты.ВидЗапасов.УстарелоНазначение
			|		И Даты.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И Даты.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|		ТОГДА Даты.ВидЗапасов.УстарелоНазначение
			|		ИНАЧЕ Даты.Назначение
			|	КОНЕЦ КАК Назначение,
			|	Даты.НомерГТД,
			|	МАКСИМУМ(Даты.ДатаПоступления) КАК ДатаПоступления
			|ИЗ
			|	РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК Даты
			|ГДЕ
			|	Даты.ВидЗапасов = &ВидЗапасов
			|	И Даты.Номенклатура = &Номенклатура
			|	И Даты.Характеристика = &Характеристика
			|	И Даты.Серия = &Серия
			|	И Даты.НомерГТД = &НомерГТД
			|
			|СГРУППИРОВАТЬ ПО
			|	Даты.ВидЗапасов,
			|	Даты.Номенклатура,
			|	Даты.Характеристика,
			|	Даты.Серия,
			|	ВЫБОР КОГДА Даты.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И Даты.Назначение <> Даты.ВидЗапасов.УстарелоНазначение
			|		И Даты.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И Даты.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|		ТОГДА Даты.ВидЗапасов.УстарелоНазначение
			|		ИНАЧЕ Даты.Назначение
			|	КОНЕЦ,
			|	Даты.НомерГТД
			|");
			
			НаборЗаписей = РегистрыСведений.ДатыПоступленияТоваровОрганизаций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВидЗапасов.Установить(ДанныеКОбработке.ВидЗапасов);
			НаборЗаписей.Отбор.Номенклатура.Установить(ДанныеКОбработке.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(ДанныеКОбработке.Характеристика);
			НаборЗаписей.Отбор.Серия.Установить(ДанныеКОбработке.Серия);
			НаборЗаписей.Отбор.НомерГТД.Установить(ДанныеКОбработке.НомерГТД);
			
			Запрос.УстановитьПараметр("ВидЗапасов", ДанныеКОбработке.ВидЗапасов);
			Запрос.УстановитьПараметр("Номенклатура", ДанныеКОбработке.Номенклатура);
			Запрос.УстановитьПараметр("Характеристика", ДанныеКОбработке.Характеристика);
			Запрос.УстановитьПараметр("Серия", ДанныеКОбработке.Серия);
			Запрос.УстановитьПараметр("НомерГТД", ДанныеКОбработке.НомерГТД);
			
			НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
			ЗафиксироватьТранзакцию();
		
		Исключение
		
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", "РегистрыСведений.ДатыПоступленияТоваровОрганизаций");
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, "РегистрСведений.ДатыПоступленияТоваровОрганизаций");
	
КонецПроцедуры

#КонецОбласти

	
#КонецЕсли
