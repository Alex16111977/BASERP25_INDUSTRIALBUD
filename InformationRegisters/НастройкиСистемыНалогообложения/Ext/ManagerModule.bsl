#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	СтруктураЗначений = Новый Структура;
    СтруктураЗначений.Вставить("СистемаНалогообложения", Перечисления.СистемыНалогообложения.НалогНаПрибыльИНДС);
    СтруктураЗначений.Вставить("ПлательщикНалогаНаПрибыль", Истина);
    СтруктураЗначений.Вставить("ПлательщикНДС", Истина);
    СтруктураЗначений.Вставить("ПлательщикЕН", Ложь);
    СтруктураЗначений.Вставить("ИнформацияОСтатусеПлательщикаНалогов", "");
	СтруктураЗначений.Вставить("РаздельныйУчетТоваровПоНалогообложениюНДС", Истина);
	СтруктураЗначений.Вставить("РаздельныйУчетВНАПоНалогообложениюНДС", Истина);
	СтруктураЗначений.Вставить("РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС", Истина);
	Возврат СтруктураЗначений
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.Организация КАК Организация,
	|	ТаблицаСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
    |	ТаблицаСрезПоследних.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
    |	ТаблицаСрезПоследних.ПлательщикНДС КАК ПлательщикНДС,
	|	ЕСТЬNULL(СтатусыПлательщиковЕдиногоНалогаСрезПоследних.ПлательщикЕН, ЛОЖЬ) КАК ПлательщикЕН,
    |	ТаблицаСрезПоследних.ИнформацияОСтатусеПлательщикаНалогов КАК ИнформацияОСтатусеПлательщикаНалогов,
	|	ТаблицаСрезПоследних.РаздельныйУчетТоваровПоНалогообложениюНДС КАК РаздельныйУчетТоваровПоНалогообложениюНДС,
	|	ТаблицаСрезПоследних.РаздельныйУчетТоваровПоНалогообложениюНДС КАК РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС,
    |	ТаблицаСрезПоследних.РаздельныйУчетВНАПоНалогообложениюНДС КАК РаздельныйУчетВНАПоНалогообложениюНДС,
	|	ВЫБОР 
    |       КОГДА ТаблицаСрезПоследних.ПлательщикНДС 
    |           ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая) 
    |       ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность) 
    |       КОНЕЦ КАК НалоговоеНазначениеПоУмолчанию
	|//ПОМЕСТИТЬ
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПлательщиковЕдиногоНалога.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК СтатусыПлательщиковЕдиногоНалогаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = СтатусыПлательщиковЕдиногоНалогаСрезПоследних.Организация
	|//ИНДЕКСИРОВАТЬ
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
			"НастройкиСистемыНалогообложения",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.';uk='Не задані параметри.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон, 
		НСтр("ru='Система налогообложения';uk='Система оподаткування'"),
		ДействующиеНастройки.СистемаНалогообложения);
        
    
	
	//++ НЕ УТ
    Если ДействующиеНастройки.РаздельныйУчетВНАПоНалогообложениюНДС Тогда
	  	СтрокаОписанияНастроек = СтрокаОписанияНастроек
	  		+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Используется раздельный учет необоротных активов по налоговым назначениям';uk='Використовується окремий облік необоротних активів по податковим призначенням'"));    
    КонецЕсли;
	//-- НЕ УТ
	
    Если ДействующиеНастройки.РаздельныйУчетТоваровПоНалогообложениюНДС Тогда
	  	СтрокаОписанияНастроек = СтрокаОписанияНастроек
	  		+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Используется раздельный учет запасов по налоговым назначениям НДС';uk='Використовується окремий облік запасів по податковим призначенням ПДВ'"));    
    КонецЕсли;
	
	Возврат СтрокаОписанияНастроек
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.НастройкиСистемыНалогообложения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4c97907e-382c-47c2-8399-54fd7b198ae4");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.НастройкиСистемыНалогообложения.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр НастройкиСистемыНалогообложения из настроек учетных политик организаций.';uk='Заповнює регістр НастройкиСистемыНалогообложения з настройок облікових політик організацій.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьУчетнаяПолитикаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.УдалитьУчетныеПолитикиОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.НастройкиСистемыНалогообложения";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК Настройки
	|		ПО (Настройки.Период = ДанныеРегистра.Период)
	|			И (Настройки.Организация = ДанныеРегистра.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УдалитьУчетныеПолитикиОрганизаций КАК Политики
	|		ПО (ДанныеРегистра.УчетнаяПолитика = Политики.Ссылка)
	|ГДЕ
	|	Настройки.Организация ЕСТЬ NULL
	|	И НЕ Политики.Ссылка ЕСТЬ NULL";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	СформироватьТаблицуУчетныхПолитикДляИзмененияРаздельногоУчетаНДС(МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.УчетнаяПолитика.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ВЫБОР
	|		КОГДА РаздельныйУчетНДС.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РаздельныйУчетТоваровПоНалогообложениюНДС,
	|	ВЫБОР
	|		КОГДА РаздельныйУчетНДС.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РаздельныйУчетВНАПоНалогообложениюНДС,
	|	ДанныеРегистра.ПлательщикЕН КАК ПлательщикЕН,
	|	ДанныеРегистра.ПлательщикНДС КАК ПлательщикНДС,
	|	ДанныеРегистра.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|		ПО ДанныеДляОбработки.Период = ДанныеРегистра.Период
	|		И ДанныеДляОбработки.Организация = ДанныеРегистра.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиДляВключенияРаздельногоУчетаНДС КАК РаздельныйУчетНДС
	|		ПО ДанныеРегистра.УчетнаяПолитика = РаздельныйУчетНДС.Ссылка
	|";
	
	ВыборкаУчетнаяПолитика = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаУчетнаяПолитика.Следующий() Цикл

		НачатьТранзакцию();
	
		Попытка
	
			НаборЗаписей = РегистрыСведений.НастройкиСистемыНалогообложения.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(ВыборкаУчетнаяПолитика.Период);
			НаборЗаписей.Отбор.Организация.Установить(ВыборкаУчетнаяПолитика.Организация);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаУчетнаяПолитика);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

			ЗафиксироватьТранзакцию();
					
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


Процедура СформироватьТаблицуУчетныхПолитикДляИзмененияРаздельногоУчетаНДС(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьСменаНалогообложения
	|ПОМЕСТИТЬ ВТСменаНалогообложения
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.КорНалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И Т.НалоговоеНазначение21 <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И Т.НалоговоеНазначение21 <> Т.КорНалоговоеНазначение
	|	И Т.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетныеПолитикиОрганизаций.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТУчетныеПолитикиДляВключенияРаздельногоУчетаНДС
	|ИЗ
	|	Справочник.УдалитьУчетныеПолитикиОрганизаций КАК УчетныеПолитикиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСменаНалогообложения КАК ВТСменаНалогообложения
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	(УчетныеПолитикиОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.НалогНаПрибыльИНДС) ИЛИ УчетныеПолитикиОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ЕдиныйНалогИНДС))
	|	И (Константы.УдалитьИспользоватьРаздельныйУчетПоНалогообложению
	|			ИЛИ ЕСТЬNULL(ВТСменаНалогообложения.ЕстьСменаНалогообложения, ЛОЖЬ))
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры



#КонецОбласти

#КонецЕсли
