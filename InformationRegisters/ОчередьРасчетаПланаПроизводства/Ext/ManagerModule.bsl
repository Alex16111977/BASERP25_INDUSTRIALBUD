#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет записи в регистр заданий.
//
// Параметры:
//  ПланПроизводства	 - ДокументСсылка.ПланПроизводства	 - документ, для которого необходимо добавить задание.
//  Автозапуск		 - Булево							 - автоматический запуск расчет фоновым заданием.
//
Процедура ДобавитьЗадание(Сценарий, ПланПроизводства, Автозапуск = Истина) Экспорт
	
	Задания = Новый Массив();
	
	НовоеЗадание = Новый Структура();
	НовоеЗадание.Вставить("Состояние", Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяРасчет);
	НовоеЗадание.Вставить("Сценарий", Сценарий);
	НовоеЗадание.Вставить("ПланПроизводства", ПланПроизводства);
	Задания.Добавить(НовоеЗадание);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБюджетирование") Тогда
		НовоеЗадание = Новый Структура();
		НовоеЗадание.Вставить("Состояние", Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяОтражениеВБюджетировании);
		НовоеЗадание.Вставить("Сценарий", Сценарий);
		НовоеЗадание.Вставить("ПланПроизводства", ПланПроизводства);
		Задания.Добавить(НовоеЗадание);
	КонецЕсли;
	
	Идентификаторы = ПроизводствоСервер.ДобавитьЗаданиеВОчередь("ОчередьРасчетаПланаПроизводства", Задания);
	
	Если Автозапуск Тогда
		Приоритет = Новый Структура("ПланПроизводства", ПланПроизводства);
		ПроизводствоСервер.ЗапуститьЗаданиеОбработкиОчереди("ОчередьРасчетаПланаПроизводства", Приоритет, Идентификаторы);
	КонецЕсли;
	
КонецПроцедуры

// Запускает в фоновом режиме планирование производства
//
Процедура ЗапуститьЗадание() Экспорт
	
	ПроизводствоСервер.ЗапуститьЗаданиеОбработкиОчереди("ОчередьРасчетаПланаПроизводства");
	
КонецПроцедуры

// Проверяет активность расчета плана производства
// 
// Возвращаемое значение:
//  Булево - Истина, если обработка очереди заданий выполняется
//
Функция ВыполняетсяОбработкаОчереди() Экспорт
	
	Результат = ПроизводствоСервер.ВыполняетсяОбработкаОчереди("ОчередьРасчетаПланаПроизводства");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыОбработки() Экспорт
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("Обработчик", "РегистрыСведений.ОчередьРасчетаПланаПроизводства.ОбработатьЗадания");
	ПараметрыОбработки.Вставить("НеЗапускатьВФоне", ОбщегоНазначения.ИнформационнаяБазаФайловая());
	ПараметрыОбработки.Вставить("ДополнительныеДанные", Новый Структура);
	Возврат ПараметрыОбработки;
	
КонецФункции

Функция ОчередьЗаданий(Отбор = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОчередьРасчета.Состояние КАК Состояние,
	|	ОчередьРасчета.ПланПроизводства КАК ПланПроизводства,
	|	ОчередьРасчета.Сценарий КАК Сценарий
	|ПОМЕСТИТЬ ОчередьРасчета
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчета
	|ГДЕ
	|	ОчередьРасчета.Состояние В (
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияРасчетаПланаПроизводства.ТребуетсяРасчет),
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияРасчетаПланаПроизводства.ТребуетсяОтражениеВБюджетировании)
	|		)
	|	//&И ОчередьРасчета.ПланПроизводства = &ПланПроизводства
	|	И НЕ ОчередьРасчета.ЕстьОшибки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданияКРасчету.Состояние        КАК Состояние,
	|	ЗаданияКРасчету.ПланПроизводства КАК ПланПроизводства,
	|	ЗаданияКРасчету.Сценарий КАК Сценарий,
	|	ЗаданияКРасчету.Идентификатор    КАК Идентификатор,
	|	ЗаданияКРасчету.ДеньРегистрации  КАК ДеньРегистрации,
	|	ЗаданияКРасчету.ЕстьОшибки       КАК ЕстьОшибки
	|ПОМЕСТИТЬ ОбрабатываемыеЗадания
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ЗаданияКРасчету
	|ГДЕ
	|	(ЗаданияКРасчету.Состояние, ЗаданияКРасчету.ПланПроизводства) В
	|			(ВЫБРАТЬ
	|				ОчередьРасчета.Состояние,
	|				ОчередьРасчета.ПланПроизводства
	|			ИЗ
	|				ОчередьРасчета КАК ОчередьРасчета)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияРасчетаПланаПроизводства.ТребуетсяОчисткаНеиспользуемыхДанных),
	|	ОчередьРасчета.ПланПроизводства,
	|	ОчередьРасчета.Сценарий,
	|	&ПустойИдентификатор,
	|	ДАТАВРЕМЯ(1,1,1),
	|	ЛОЖЬ
	|ИЗ
	|	ОчередьРасчета КАК ОчередьРасчета
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОчередьРасчета
	|");
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Отбор <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//&", "");
		Запрос.УстановитьПараметр("ПланПроизводства", Отбор.ПланПроизводства);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если МассивРезультатов[1].Выгрузить()[0].Количество = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВыбратьИзОчереди(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОчередьРасчета.Состояние               КАК Состояние,
	|	ОчередьРасчета.ПланПроизводства        КАК ПланПроизводства,
	|	ОчередьРасчета.Сценарий                КАК Сценарий,
	|	МАКСИМУМ(ОчередьРасчета.Идентификатор) КАК Идентификатор
	|ИЗ
	|	ОбрабатываемыеЗадания КАК ОчередьРасчета
	|ГДЕ 
	|	НЕ ОчередьРасчета.ЕстьОшибки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОчередьРасчета.Сценарий,
	|	ОчередьРасчета.ПланПроизводства,
	|	ОчередьРасчета.Состояние
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПланПроизводства,
	|	Состояние");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Массив();
	Пока Выборка.Следующий() Цикл
		Задание = Новый Структура("ПланПроизводства,Сценарий,Состояние,Идентификатор");
		ЗаполнитьЗначенияСвойств(Задание, Выборка);
		Результат.Добавить(Задание);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьЗадания(Задание, ПропуститьЗадание = Ложь, ДополнительныеДанные = Неопределено) Экспорт
	
	НачалоРасчета = ЗаписатьНачалоОкончаниеРасчета(Задание.ПланПроизводства);
	
	Если Задание.Состояние = Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяРасчет Тогда
		
		ИмяОперации = НСтр("ru='Расчет (корректировка) плана производства';uk='Розрахунок (коригування) плану виробництва'");
		РасчетПланаПроизводства.Рассчитать(Задание.ПланПроизводства);
		
	ИначеЕсли Задание.Состояние = Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяОтражениеВБюджетировании Тогда
		
		ИмяОперации = НСтр("ru='Отражение в бюджетировании';uk='Відображення в бюджетуванні'");
		Документы.ПланПроизводства.ОтразитьВБюджетировании(Задание.ПланПроизводства);
		
	ИначеЕсли Задание.Состояние = Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяОчисткаНеиспользуемыхДанных Тогда
		
		ИмяОперации = НСтр("ru='Очистка неиспользуемых данных';uk='Очищення невикористовуваних даних'");
		РасчетПланаПроизводства.ОчиститьНеиспользуемыеДанные(Задание.ПланПроизводства);
		
	КонецЕсли;
	
	ОкончаниеРасчета = ЗаписатьНачалоОкончаниеРасчета(Задание.ПланПроизводства, Истина);
	
	ЗаписатьПротоколРасчетаВЖурналРегистрации(Задание.ПланПроизводства, ИмяОперации, НачалоРасчета, ОкончаниеРасчета);
	
КонецПроцедуры

Процедура ЗаписатьПротоколРасчетаВЖурналРегистрации(ПланПроизводства, ИмяОперации, НачалоРасчета, ОкончаниеРасчета)
	
	ИмяСобытия = РасчетПланаПроизводства.СобытиеЖурналаРегистрации();
	
	ВремяВСекундах = ОкончаниеРасчета - НачалоРасчета;
	
	Часы = Цел(ВремяВСекундах/3600);
	Минуты = Цел((ВремяВСекундах - Часы * 3600) / 60);
	Секунды = Макс(ВремяВСекундах - Часы * 3600 - Минуты * 60, 1);
	
	ПродолжительностьЧасы = ?(Часы = 0, "", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 час';uk=' %1 година'"), Часы));
	ПродолжительностьМинуты = ?(Минуты = 0, "", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 мин';uk=' %1 хв'"), Минуты));
	ПродолжительностьСекунды = ?(Секунды = 0, "", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 сек';uk=' %1 сек'"), Секунды));
	ПродолжительностьРасчета = ПродолжительностьЧасы + " " + ПродолжительностьМинуты + " " + ПродолжительностьСекунды;
	ПродолжительностьРасчета = СокрЛП(ПродолжительностьРасчета);
	
	ТекстСообщения = СтрШаблон(НСтр("ru='Завершено выполнение операции ""%1"", продолжительность %2';uk='Завершено виконання операції ""%1"", тривалість %2'"), ИмяОперации, ПродолжительностьРасчета);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ОчередьРасчетаПланаПроизводства,
		ПланПроизводства,
		ТекстСообщения);

КонецПроцедуры

Процедура УдалитьЗадание(Задание, МенеджерВременныхТаблиц) Экспорт
	
	Если ЭтоВиртуальноеЗадание(Задание) Тогда
		Возврат;
	КонецЕсли;
	
	ПроизводствоСервер.УдалитьЗадание("ОчередьРасчетаПланаПроизводства", Задание, МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ЗаписатьОшибку(Задание, ПропуститьЗадание, ИнформацияОбОшибке) Экспорт
	
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	
	Если Не ЭтоВиртуальноеЗадание(Задание) Тогда
		
		Попытка
			
			Набор = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
			
			Набор.Отбор.Состояние.Установить(Задание.Состояние);
			Набор.Отбор.ПланПроизводства.Установить(Задание.ПланПроизводства);
			Набор.Отбор.Идентификатор.Установить(Задание.Идентификатор);
			
			Набор.Прочитать();
			
			Для каждого Запись Из Набор Цикл
				Запись.ЕстьОшибки = Истина;
				Запись.ТекстОшибки = ТекстОшибки;
			КонецЦикла;
			
			УстановитьПривилегированныйРежим(Истина);
			Набор.Записать(Истина);
			УстановитьПривилегированныйРежим(Ложь);
			
			ПропуститьЗадание = Истина;
			
		Исключение
			// дополнительных действий не требуется, сообщение об ошибке будет записано в журнал регистрации
		КонецПопытки;
		
	Иначе
		ПропуститьЗадание = Истина;
	КонецЕсли;
	
	ПроизводствоСервер.ЗаписатьОшибкуОбработкиОчередиЗаданий("ОчередьРасчетаПланаПроизводства", ТекстОшибки, Задание, Задание.ПланПроизводства);
	
КонецПроцедуры

Функция ЗаписатьНачалоОкончаниеРасчета(ПланПроизводства, РасчетЗавершен = Ложь)
	
	Набор = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
	Набор.Отбор.Состояние.Установить(Перечисления.СостоянияРасчетаПланаПроизводства.Рассчитывается);
	
	Если НЕ РасчетЗавершен Тогда
		Запись = Набор.Добавить();
		Запись.Состояние = Перечисления.СостоянияРасчетаПланаПроизводства.Рассчитывается;
		Запись.ПланПроизводства = ПланПроизводства;
	КонецЕсли;
	
	Набор.ОбменДанными.Загрузка = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	Набор.Записать(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

Функция ЭтоВиртуальноеЗадание(Задание)
	
	Возврат (Задание.Состояние = Перечисления.СостоянияРасчетаПланаПроизводства.ТребуетсяОчисткаНеиспользуемыхДанных); // задание в базу данных не записывается
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ОчередьРасчетаПланаПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("6ce5ff30-dd7f-4ca7-a2df-54850d83314b");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ОчередьРасчетаПланаПроизводства.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Очередь расчета плана производства"":
                                   |Ресурс ""Сценарий"" регистра ""Очередь расчета плана производства"" переносится в измерение.'
                                   |;uk='Поновлює регістр ""Черга розрахунку плану виробництва"":
                                   |Ресурс ""Сценарій"" регістру ""Черга розрахунку плану виробництва"" переноситься у вимір.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПланПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ОчередьРасчетаПланаПроизводства.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ОчередьРасчетаПланаПроизводства.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ОчередьРасчетаПланаПроизводства.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПланПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт 
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ОчередьРасчетаПланаПроизводства");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОчередьРасчетаПланаПроизводства.ПланПроизводства КАК ПланПроизводства,
	|	ОчередьРасчетаПланаПроизводства.Идентификатор КАК Идентификатор,
	|	ОчередьРасчетаПланаПроизводства.ДеньРегистрации КАК ДеньРегистрации,
	|	ОчередьРасчетаПланаПроизводства.Состояние КАК Состояние,
	|	ОчередьРасчетаПланаПроизводства.Сценарий КАК Сценарий
	|
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПланаПроизводства
	|ГДЕ     
	|	(ОчередьРасчетаПланаПроизводства.Сценарий = &ПустойСценарий
	|	И ОчередьРасчетаПланаПроизводства.УдалитьСценарий <> &ПустойСценарий)
	|	ИЛИ ОчередьРасчетаПланаПроизводства.УдалитьСценарий = &ПустойСценарий
	|";
	
	Запрос.УстановитьПараметр("ПустойСценарий", Справочники.СценарииТоварногоПланирования.ПустаяСсылка());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
			
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОчередьРасчетаПланаПроизводства;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаОбъектыДляОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяРегистра,
		МенеджерВременныхТаблиц
	);
	
	Если НЕ ТаблицаОбъектыДляОбработки.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ТаблицаОбъектыДляОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыДляОбработки.ПланПроизводства,
	|	ОбъектыДляОбработки.Идентификатор,
	|	ОбъектыДляОбработки.ДеньРегистрации,
	|	ОбъектыДляОбработки.Состояние,
	|	ЕСТЬNULL(ОчередьРасчетаПланаПроизводства.УдалитьСценарий, ОбъектыДляОбработки.ПланПроизводства.Сценарий) КАК Сценарий
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПланаПроизводства
	|		ПО ОбъектыДляОбработки.ПланПроизводства = ОчередьРасчетаПланаПроизводства.ПланПроизводства
	|			И ОбъектыДляОбработки.Идентификатор = ОчередьРасчетаПланаПроизводства.Идентификатор
	|			И ОбъектыДляОбработки.ДеньРегистрации = ОчередьРасчетаПланаПроизводства.ДеньРегистрации
	|			И ОбъектыДляОбработки.Состояние = ОчередьРасчетаПланаПроизводства.Состояние
	|			И ОбъектыДляОбработки.Сценарий = ОчередьРасчетаПланаПроизводства.Сценарий
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", ТаблицаОбъектыДляОбработки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПустойСценарий", Справочники.СценарииТоварногоПланирования.ПустаяСсылка());
	
	Измерения = "ПланПроизводства,Идентификатор,ДеньРегистрации,Состояние,Сценарий";
	Измерения = СтрРазделить(Измерения, ",");
	
	ДополнительныеПараметрыОтметкиОбработки = ДополнительныеПараметрыОтметкиОбработки();
	
	ТаблицаИзмерения = Новый ТаблицаЗначений;
	Для каждого Измерение Из Измерения Цикл
		ТаблицаИзмерения.Колонки.Добавить(Измерение);
	КонецЦикла;
	ТаблицаИзмерения.Добавить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ПланПроизводства", Выборка.ПланПроизводства);
			
			Блокировка.Заблокировать();
			
			Если ЗначениеЗаполнено(Выборка.Сценарий) Тогда
				
				Набор = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
				
				Для каждого Измерение Из Измерения Цикл
					
					Если Измерение = "Сценарий" Тогда
						Продолжить;
					КонецЕсли;
					
					Набор.Отбор[Измерение].Установить(Выборка[Измерение]);
					
				КонецЦикла;
				
				ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
			КонецЕсли;
				
			ЗаполнитьЗначенияСвойств(ТаблицаИзмерения[0], Выборка);
			ТаблицаИзмерения[0].Сценарий = Справочники.СценарииТоварногоПланирования.ПустаяСсылка();
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(
				ТаблицаИзмерения, 
				ДополнительныеПараметрыОтметкиОбработки, 
				Параметры.Очередь
			);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Набор = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
			
			ТекстСообщения = НСтр("ru='Не удалось обновить данные плана: %ПланПроизводства% по причине: %Причина%';uk='Не вдалося оновити дані плану: %ПланПроизводства% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПланПроизводства%", Выборка.ПланПроизводства);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Набор.Метаданные(),
				,
				ТекстСообщения
			);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра()
	);
	
КонецПроцедуры

Функция ДополнительныеПараметрыОтметкиОбработки()
	
	Результат = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	Результат.ЭтоНезависимыйРегистрСведений = Истина;
	Результат.ПолноеИмяРегистра = ПолноеИмяРегистра();
	
	Возврат Результат;
	
КонецФункции

Функция ПолноеИмяРегистра()
	
	Возврат "РегистрСведений.ОчередьРасчетаПланаПроизводства";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
