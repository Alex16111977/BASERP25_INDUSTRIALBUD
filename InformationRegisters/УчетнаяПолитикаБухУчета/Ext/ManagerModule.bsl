#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	СтруктураЗначений = Новый Структура;
    
	СтруктураЗначений.Вставить("ПроводкиПоРаботникам", Ложь);
    СтруктураЗначений.Вставить("ИспользуемыеКлассыСчетовРасходов", Перечисления.КлассыСчетовРасходов.Класс8и9);
    СтруктураЗначений.Вставить("ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли", Ложь);
	СтруктураЗначений.Вставить("СборкаТоваровЧерезСчет23", Ложь);
	Возврат СтруктураЗначений
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.Организация КАК Организация,
	|	ТаблицаСрезПоследних.ИспользуемыеКлассыСчетовРасходов КАК ИспользуемыеКлассыСчетовРасходов,
	|	ТаблицаСрезПоследних.ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли КАК ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли,
	|	ТаблицаСрезПоследних.СборкаТоваровЧерезСчет23 КАК СборкаТоваровЧерезСчет23,
	|	ТаблицаСрезПоследних.ПроводкиПоРаботникам КАК ПроводкиПоРаботникам
	|//ПОМЕСТИТЬ
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|//ИНДЕКСИРОВАТЬ
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
			"УчетнаяПолитикаБухУчета",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.';uk='Не задані параметри.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = "";
    
    СтрокаОписанияНастроек = СтрокаОписанияНастроек
        + СтрШаблон(СтрокаШаблон,
            НСтр("ru='Используемые классы счетов расходов';uk='Використовувані класи рахунків витрат'"),
            ДействующиеНастройки.ИспользуемыеКлассыСчетовРасходов);
	  
	Если ДействующиеНастройки.СборкаТоваровЧерезСчет23 Тогда
	  	СтрокаОписанияНастроек = СтрокаОписанияНастроек
	  		+ СтрШаблон(СтрокаШаблонБулево,
	  			НСтр("ru='Сборка товаров через счет 23';uk='Збирання товарів через рахунок 23'"));
	КонецЕсли;
            
	Если ДействующиеНастройки.ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Сумма дооценок ОС пропорционально начислению амортизации включается в состав нераспределенной прибыли с уменьшением дополнительного капитала';uk='Сума дооцінок ОЗ пропорційно нарахуванню амортизації включається до складу нерозподіленого прибутку зі зменшенням додаткового капіталу'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПроводкиПоРаботникам Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
                НСтр("ru='Ведется учет расходов на оплату труда по счету 66 ""Оплата труда"" с детализацией по работникам организации';uk='Ведеться облік витрат на оплату праці по рахунку 66 ""Оплата праці"" з деталізацією по працівниках організації'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаОписанияНастроек) Тогда
		СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.';uk='Не задані параметри.'");
	КонецЕсли;
	Возврат СтрокаОписанияНастроек
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.УчетнаяПолитикаБухУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("76f742fc-f502-48bb-ac5e-eaaf81131c02");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.УчетнаяПолитикаБухУчета.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр УчетнаяПолитикаБухУчета из настроек учетных политик организаций.';uk='Заповнює регістр УчетнаяПолітікаБухУчета з настройок облікових політик організацій.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьУчетнаяПолитикаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.УдалитьУчетныеПолитикиОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.УчетнаяПолитикаБухУчета";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|		ПО (УчетнаяПолитикаБухУчета.Период = ДанныеРегистра.Период)
	|			И (УчетнаяПолитикаБухУчета.Организация = ДанныеРегистра.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УдалитьУчетныеПолитикиОрганизаций КАК Политики
	|		ПО (ДанныеРегистра.УчетнаяПолитика = Политики.Ссылка)
	|ГДЕ
	|	УчетнаяПолитикаБухУчета.Организация ЕСТЬ NULL
	|	И НЕ Политики.Ссылка ЕСТЬ NULL
	|
	|";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Перенос учетной политики из устаревшего справочника
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|		ПО ДанныеДляОбработки.Период = ДанныеРегистра.Период
	|		И ДанныеДляОбработки.Организация = ДанныеРегистра.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|		ПО (УчетнаяПолитикаБухУчета.Период = ДанныеРегистра.Период)
	|			И (УчетнаяПолитикаБухУчета.Организация = ДанныеРегистра.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УдалитьУчетныеПолитикиОрганизаций КАК Политики
	|		ПО (ДанныеРегистра.УчетнаяПолитика = Политики.Ссылка)
	|ГДЕ
	|	УчетнаяПолитикаБухУчета.Организация ЕСТЬ NULL
	|	И НЕ Политики.Ссылка ЕСТЬ NULL
	|	
	|ИТОГИ
	|ПО
	|	УчетнаяПолитика";
	
	ВыборкаУчетнаяПолитика = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "УчетнаяПолитика");
	
	Пока ВыборкаУчетнаяПолитика.Следующий() Цикл

		НачатьТранзакцию();
	
		Попытка
	
			БлокировкаУчетнаяПолитика = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаУчетнаяПолитика.Добавить("Справочник.УдалитьУчетныеПолитикиОрганизаций");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаУчетнаяПолитика.УчетнаяПолитика);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			БлокировкаУчетнаяПолитика.Заблокировать();
			
			ЗапросУчетнаяПолитика = Новый Запрос();
			ЗапросУчетнаяПолитика.Текст = "
			|ВЫБРАТЬ
			|	ИСТИНА КАК СборкаТоваровЧерезСчет23,
			|	УчетнаяПолитика.ПроводкиПоРаботникам,
			|	УчетнаяПолитика.ИспользуемыеКлассыСчетовРасходов,
			|	УчетнаяПолитика.ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли
			|ИЗ
			|	Справочник.УдалитьУчетныеПолитикиОрганизаций КАК УчетнаяПолитика
			|ГДЕ
			|	УчетнаяПолитика.Ссылка = &УчетнаяПолитика";
			
			ЗапросУчетнаяПолитика.УстановитьПараметр("УчетнаяПолитика", ВыборкаУчетнаяПолитика.УчетнаяПолитика);
	
			РезультатЗапросаУчетнаяПолитика = ЗапросУчетнаяПолитика.Выполнить();
			
			Если РезультатЗапросаУчетнаяПолитика.Пустой() Тогда
				ТекстОшибки = СтрШаблон(НСтр("ru='Ошибка получения настроек учетной политики ""%1"".';uk='Помилка отримання налаштувань облікової політики ""%1"".'",ОбщегоНазначения.КодОсновногоЯзыка()), 
					ВыборкаУчетнаяПолитика.УчетнаяПолитика);
				ВызватьИсключение ТекстОшибки;
			Иначе
				НастройкаУчетнойПолитики = РезультатЗапросаУчетнаяПолитика.Выгрузить()[0];
			КонецЕсли;
			
			Выборка = ВыборкаУчетнаяПолитика.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
			Пока Выборка.Следующий() Цикл
					
					НаборЗаписей = РегистрыСведений.УчетнаяПолитикаБухУчета.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
					НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
					
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					ЗаполнитьЗначенияСвойств(НоваяЗапись, НастройкаУчетнойПолитики);
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
					
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ЗаписатьОшибкуВЛог(МетаданныеОбъекта, ПолноеИмяОбъекта, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	

	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВЛог(МетаданныеОбъекта, ПолноеИмяОбъекта, ПредставлениеОшибки)
	
	ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%",     ПредставлениеОшибки);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
	
	ЗаписьЖурналаРегистрации(
		ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Предупреждение,
		МетаданныеОбъекта, 
		Неопределено, 
		ТекстСообщения
	);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
