#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - ФиксированнаяСтруктура - свойства документа (См. ПроведениеДокументов.СвойстваДокумента).
//
// Возвращаемое значение:
//  Структура - параметры учетного механизма (См. ПроведениеДокументов.ПараметрыУчетногоМеханизма()).
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение
		Или Свойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Параметры.НезависимыеРегистры.Добавить(Метаданные.РегистрыСведений.ГрафикПлатежей);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

// Для записи графика накладывает блокировку на таблицу, от которой зависит график
// 
// Параметры:
//    ТаблицаОбъектовОплаты - ТаблицаЗначений - таблица с колонками:
//        * ОбъектОплаты - ОпределяемыйТип.ОбъектОплаты - оплачиваемый объект
//        * ОбъектРасчетов - ОпределяемыйТип.ОбъектРасчетов - объект, в разрезе которого ведутся расчеты
//    ИмяТаблицы - Строка - блокируемая таблица
//    ИмяПоля - Строка - поле блокируемой таблицы
//    ИмяКолонки - Строка - поле таблицы объектов оплаты, используемое для блокировки
//
Процедура УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, ИмяТаблицы, ИмяПоля, ИмяКолонки = "ОбъектОплаты") Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектовОплаты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ИмяПоля, ИмяКолонки);
	Блокировка.Заблокировать();
	
КонецПроцедуры

// Производит расчет и запись графика по расчетам с клиентами
//
// Параметры:
//    ТаблицаОбъектовОплаты - ТаблицаЗначений - таблица с колонками:
//        * ОбъектОплаты - ОпределяемыйТип.ОбъектОплаты - оплачиваемый объект
//        * ОбъектРасчетов - ОпределяемыйТип.ОбъектРасчетов - объект, в разрезе которого ведутся расчеты
//    Очередь - Число - номер очереди при обновлении данных
//
Процедура РассчитатьГрафикПлатежейПоРасчетамСКлиентами(ТаблицаОбъектовОплаты, Очередь = Неопределено) Экспорт
	
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	
	НеОбрабатываемые = НеобрабатываемыеДокументы();
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		Если НеОбрабатываемые.Найти(ТипЗнч(СтрокаТаблицы.ОбъектОплаты)) <> Неопределено Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТаблицы Из СтрокиКУдалению Цикл
		ТаблицаОбъектовОплаты.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	СтрокиОплата = Новый Массив;
	СтрокиВозврат = Новый Массив;
	
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		Если ТипЗнч(СтрокаТаблицы.ОбъектОплаты) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
			Или ТипЗнч(СтрокаТаблицы.ОбъектОплаты) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
			СтрокиВозврат.Добавить(СтрокаТаблицы);
		Иначе
			СтрокиОплата.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиОплата.Количество() Тогда
		
		Если НоваяАрхитектураВзаиморасчетов Тогда
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентамиНоваяАрхитектура();
		Иначе
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентами();
		КонецЕсли;
		
		ЗаписатьГрафикПлатежейПоРасчетам(
			ТаблицаОбъектовОплаты.Скопировать(СтрокиОплата),
			ТекстЗапроса,
			Перечисления.ТипыДвиженияДенежныхСредств.Поступление,
			Перечисления.ОбластиПланированияПлатежей.РасчетыСКлиентами,
			Очередь);
	КонецЕсли;
	
	Если СтрокиВозврат.Количество() Тогда
		
		Если НоваяАрхитектураВзаиморасчетов Тогда
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоВозвратамКлиентамНоваяАрхитектура();
		Иначе
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоВозвратамКлиентам();
		КонецЕсли;
		
		ЗаписатьГрафикПлатежейПоРасчетам(
			ТаблицаОбъектовОплаты.Скопировать(СтрокиВозврат),
			ТекстЗапроса,
			Перечисления.ТипыДвиженияДенежныхСредств.Списание,
			Перечисления.ОбластиПланированияПлатежей.ВозвратыКлиентам,
			Очередь);
	КонецЕсли;
	
КонецПроцедуры

// Производит расчет и запись графика по расчетам с поставщиками
//
// Параметры:
//    ТаблицаОбъектовОплаты - ТаблицаЗначений - таблица с колонками:
//        * ОбъектОплаты - ОпределяемыйТип.ОбъектОплаты - оплачиваемый объект
//        * ОбъектРасчетов - ОпределяемыйТип.ОбъектРасчетов - объект, в разрезе которого ведутся расчеты
//    Очередь - Число - номер очереди при обновлении данных
//
Процедура РассчитатьГрафикПлатежейПоРасчетамСПоставщиками(ТаблицаОбъектовОплаты, Очередь = Неопределено) Экспорт
	
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	
	НеОбрабатываемые = НеобрабатываемыеДокументы();
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		Если НеОбрабатываемые.Найти(ТипЗнч(СтрокаТаблицы.ОбъектОплаты)) <> Неопределено Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТаблицы Из СтрокиКУдалению Цикл
		ТаблицаОбъектовОплаты.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	СтрокиОплата = Новый Массив;
	СтрокиВозврат = Новый Массив;
	
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		Если ТипЗнч(СтрокаТаблицы.ОбъектОплаты) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
			СтрокиВозврат.Добавить(СтрокаТаблицы);
		Иначе
			СтрокиОплата.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиОплата.Количество() Тогда
		
		Если НоваяАрхитектураВзаиморасчетов Тогда
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщикамиНоваяАрхитектура();
		Иначе
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщиками();
		КонецЕсли;
		
		ЗаписатьГрафикПлатежейПоРасчетам(
			ТаблицаОбъектовОплаты.Скопировать(СтрокиОплата),
			ТекстЗапроса,
			Перечисления.ТипыДвиженияДенежныхСредств.Списание,
			Перечисления.ОбластиПланированияПлатежей.РасчетыСПоставщиками,
			Очередь);
	КонецЕсли;
	
	Если СтрокиВозврат.Количество() Тогда
		
		Если НоваяАрхитектураВзаиморасчетов Тогда
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиковНоваяАрхитектура();
		Иначе
			ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиков();
		КонецЕсли;
		
		ЗаписатьГрафикПлатежейПоРасчетам(
			ТаблицаОбъектовОплаты.Скопировать(СтрокиВозврат),
			ТекстЗапроса,
			Перечисления.ТипыДвиженияДенежныхСредств.Поступление,
			Перечисления.ОбластиПланированияПлатежей.ВозвратыОтПоставщиков,
			Очередь);
	КонецЕсли;
	
КонецПроцедуры

// Производит расчет и запись графика по денежным средствам к выплате
//
// Параметры:
//    ОбъектыОплаты - Массив из ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств - заявки на расходование денежных средств
//    Очередь - Число - номер очереди при обновлении данных
//
Процедура РассчитатьГрафикПлатежейПоДенежнымСредствамКВыплате(ОбъектыОплаты, Очередь = Неопределено) Экспорт
	
	Заявки = Новый Массив;
	Распоряжения = Новый Массив;
	//++ Локализация
	//-- Локализация
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		Если ТипЗнч(ОбъектОплаты) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
			Заявки.Добавить(ОбъектОплаты);
		ИначеЕсли ТипЗнч(ОбъектОплаты) = Тип("ДокументСсылка.РаспоряжениеНаПеремещениеДенежныхСредств") Тогда
			Распоряжения.Добавить(ОбъектОплаты);
		//++ Локализация
		//-- Локализация
		КонецЕсли;
	КонецЦикла;
	
	Если Заявки.Количество() Тогда
		ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ЗаявкаНаРасходованиеДенежныхСредств");
		ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоЗаявкам();
		ЗаписатьГрафикПлатежейПоДенежнымСредствамКВыплате(Заявки, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь);
	КонецЕсли;
	
	Если Распоряжения.Количество() Тогда
		ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РаспоряжениеНаПеремещениеДенежныхСредств");
		ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоРаспоряжениямНаОплату();
		ЗаписатьГрафикПлатежейПоДенежнымСредствамКВыплате(Распоряжения, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь);
	КонецЕсли;
	
	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

// Производит расчет и запись графика по денежным средствам в пути
//
// Параметры:
//    ТаблицаОбъектовОплаты - ТаблицаЗначений - таблица с колонками:
//        * ОбъектОплаты - ОпределяемыйТип.ОбъектОплаты - оплачиваемый объект
//        * БанковскийСчетКасса - СправочникСсылка.БанковскиеСчетаОрганизаций, СправочникСсылка.Кассы - банковский счет или касса, в которую ожидается поступление денежных средств
//    Очередь - Число - номер очереди при обновлении данных
//
Процедура РассчитатьГрафикПлатежейПоДенежнымСредствамВПути(ТаблицаОбъектовОплаты, Очередь = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДенежныеСредства.Отправитель КАК ОбъектОплаты,
	|	ДенежныеСредства.Получатель КАК БанковскийСчетКасса,
	|	ВЫБОР ДенежныеСредства.ВидПереводаДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияВБанк) ТОГДА
	|			ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, ЕСТЬNULL(ВЫРАЗИТЬ(ДенежныеСредства.Отправитель КАК Справочник.Кассы).СрокИнкассации, 0))
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияИзБанка) ТОГДА
	|			ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, ЕСТЬNULL(ВЫРАЗИТЬ(ДенежныеСредства.Получатель КАК Справочник.Кассы).СрокИнкассации, 0))
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеНаДругойСчет) ТОГДА
	|			ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, 1)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеремещениеВДругуюКассу) ТОГДА
	|			ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, ЕСТЬNULL(ВЫРАЗИТЬ(ДенежныеСредства.Отправитель КАК Справочник.Кассы).СрокИнкассации, 0))
	|	КОНЕЦ КАК ДатаПлатежа,
	|	ДенежныеСредства.Организация КАК ПлательщикПолучатель,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ТипСуммы,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	ДенежныеСредства.СуммаОстаток КАК Сумма,
	|	ДенежныеСредства.Организация КАК Организация,
	|	ДенежныеСредства.Получатель.Подразделение КАК Подразделение,
	|	ДенежныеСредства.Получатель.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	
	|	ВЫБОР ДенежныеСредства.ВидПереводаДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияВБанк) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияИзБанка) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствИзБанка)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеНаДругойСчет) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеремещениеВДругуюКассу) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР ДенежныеСредства.ВидПереводаДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияВБанк) ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.СдачаДенежныхСредствВБанк)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияИзБанка) ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПоступлениеДенежныхСредствИзБанка)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеНаДругойСчет) ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПеречислениеДенежныхСредствНаДругойСчет)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеремещениеВДругуюКассу) ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ВыдачаДенежныхСредствВДругуюКассу)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.Кассы)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДенежныеСредства.Отправитель) = ТИП(Справочник.КассыККМ)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.КассыККМ) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|	КОНЕЦ КАК ФормаОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ДенежныеСредстваВПути) КАК ОбластьПланирования,
	|	ДенежныеСредства.СуммаОстаток КАК СуммаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК КорВалюта
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(,
	|		ВидПереводаДенежныхСредств В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияВБанк),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияИзБанка),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеНаДругойСчет),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеремещениеВДругуюКассу)
	|		)
	|		И Получатель В (&БанковскийСчетКасса)
	|		И Отправитель В (&ОбъектОплаты)
	|	) КАК ДенежныеСредства
	|	
	|ГДЕ
	|	ДенежныеСредства.СуммаОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДенежныеСредства.Контрагент КАК ОбъектОплаты,
	|	ДенежныеСредства.Получатель КАК БанковскийСчетКасса,
	|	ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, 1) КАК ДатаПлатежа,
	|	ДенежныеСредства.Контрагент КАК ПлательщикПолучатель,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ТипСуммы,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	ДенежныеСредства.СуммаОстаток КАК Сумма,
	|	ДенежныеСредства.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ДенежныеСредства.Получатель КАК Справочник.БанковскиеСчетаОрганизаций).Подразделение,
	|	ВЫРАЗИТЬ(ДенежныеСредства.Получатель КАК Справочник.БанковскиеСчетаОрганизаций).НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте),
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПоступлениеОплатыОтКлиента),
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.Кассы) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|	КОНЕЦ КАК ФормаОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ДенежныеСредстваВПути) КАК ОбластьПланирования,
	|	ДенежныеСредства.СуммаОстаток КАК СуммаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК КорВалюта
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(,
	|		ВидПереводаДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)
	|		И Получатель В (&БанковскийСчетКасса)
	|		И Контрагент В (&ОбъектОплаты)
	|	) КАК ДенежныеСредства
	|	
	|ГДЕ
	|	ДенежныеСредства.СуммаОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДенежныеСредства.Контрагент КАК ОбъектОплаты,
	|	ДенежныеСредства.Получатель КАК БанковскийСчетКасса,
	|	ДОБАВИТЬКДАТЕ(&ДатаРасчета, ДЕНЬ, 1) КАК ДатаПлатежа,
	|	ДенежныеСредства.Контрагент КАК ПлательщикПолучатель,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ТипСуммы,
	|	ДенежныеСредства.Получатель.ВалютаДенежныхСредств КАК Валюта,
	|	
	|	СУММА(ВЫБОР КОГДА КурсВалютыОтправителя.Кратность <> 0 И КурсВалютыПолучателя.Курс <> 0 ТОГДА
	|		ДенежныеСредства.СуммаОстаток *
	|			КурсВалютыОтправителя.Курс * КурсВалютыПолучателя.Кратность / (КурсВалютыОтправителя.Кратность * КурсВалютыПолучателя.Курс)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ) КАК Сумма,
	|	
	|	ДенежныеСредства.Организация КАК Организация,
	|	ДенежныеСредства.Получатель.Подразделение,
	|	ДенежныеСредства.Получатель.НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.Кассы) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|	КОНЕЦ КАК ФормаОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ДенежныеСредстваВПути) КАК ОбластьПланирования,
	|	СУММА(ДенежныеСредства.СуммаОстаток) КАК СуммаДокумента,
	|	ДенежныеСредства.Валюта КАК КорВалюта
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(,
	|		ВидПереводаДенежныхСредств В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПриобретениеВалюты),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.РеализацияВалюты)
	|		)
	|		И Получатель В (&БанковскийСчетКасса)
	|		И Контрагент В (&ОбъектОплаты)
	|	) КАК ДенежныеСредства
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета) КАК КурсВалютыОтправителя
	|	ПО КурсВалютыОтправителя.Валюта = ДенежныеСредства.Валюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРасчета) КАК КурсВалютыПолучателя
	|	ПО КурсВалютыПолучателя.Валюта = ДенежныеСредства.Получатель.ВалютаДенежныхСредств
	|	
	|ГДЕ
	|	ДенежныеСредства.СуммаОстаток > 0
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДенежныеСредства.Контрагент,
	|	ДенежныеСредства.Получатель,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств,
	|	ДенежныеСредства.Валюта
	|";
	
	Запрос.УстановитьПараметр("ОбъектОплаты", ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"));
	Запрос.УстановитьПараметр("БанковскийСчетКасса", ТаблицаОбъектовОплаты.ВыгрузитьКолонку("БанковскийСчетКасса"));
	Запрос.УстановитьПараметр("ДатаРасчета", КонецДня(ТекущаяДатаСеанса()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для каждого СтрокаОбъектОплаты Из ТаблицаОбъектовОплаты Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ОбъектОплаты", СтрокаОбъектОплаты.ОбъектОплаты);
		СтруктураПоиска.Вставить("БанковскийСчетКасса", СтрокаОбъектОплаты.БанковскийСчетКасса);
		
		НаборЗаписей = РегистрыСведений.ГрафикПлатежей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(СтрокаОбъектОплаты.ОбъектОплаты);
		НаборЗаписей.Отбор.БанковскийСчетКасса.Установить(СтрокаОбъектОплаты.БанковскийСчетКасса);
		НаборЗаписей.Очистить();
		
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.ТипОбъектаОплаты = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.ОбъектОплаты));
			
			Если Не ЗначениеЗаполнено(Запись.СтатьяДвиженияДенежныхСредств) Тогда
				Запись.СтатьяДвиженияДенежныхСредств =
					Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(Запись.ХозяйственнаяОперация);
			КонецЕсли;
		КонецЦикла;
		Выборка.Сбросить();
		
		НачатьТранзакцию();
		Попытка
			Если Очередь <> Неопределено Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				НаборЗаписей.Записать();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось записать график платежей по: %1 по причине: %2';uk='Не вдалося записати графік платежів по: %1 з причини: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения, СтрокаОбъектОплаты.ОбъектОплаты, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,
				СтрокаОбъектОплаты.ОбъектОплаты, ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Производит расчет и запись графика по эквайрингу
//
Процедура РассчитатьГрафикПлатежейПоПоступлениямОтБанкаПоЭквайрингу() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст ="
	|ВЫБРАТЬ
	|	Таблица.ОбъектОплаты,
	|	Таблица.БанковскийСчетКасса
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеРегистра.Контрагент КАК ОбъектОплаты,
	|		ДанныеРегистра.Получатель КАК БанковскийСчетКасса,
	|		ДанныеРегистра.СуммаОстаток КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваВПути.Остатки(,
	|			ВидПереводаДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)) КАК ДанныеРегистра
	|	ГДЕ
	|		ДанныеРегистра.СуммаОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеРегистра.ОбъектОплаты КАК ОбъектОплаты,
	|		ДанныеРегистра.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|		-ДанныеРегистра.Сумма КАК Сумма
	|	ИЗ
	|		РегистрСведений.ГрафикПлатежей КАК ДанныеРегистра
	|	ГДЕ	
	|		ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)
	|		И ДанныеРегистра.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
	|	) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ОбъектОплаты,
	|	Таблица.БанковскийСчетКасса
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Сумма) <> 0
	|";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоДенежнымСредствамВПути(Результат.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

// Производит расчет и запись графика по расчетам по финансовым инструментам
//
// Параметры:
//    ОбъектыОплаты - Массив из СправочникСсылка.ДоговорыКредитовДепозитов, СправочникСсылка.ДоговорыЛизинга - договоры, по которым планируется оплата
//    Очередь - Число - номер очереди при обновлении данных
//
Процедура РассчитатьГрафикПлатежейПоФинансовымИнструментам(ОбъектыОплаты, Очередь = Неопределено) Экспорт
	
	ДоговорыКредитовДепозитов = Новый Массив;
	//++ НЕ УТ
	ДоговорыЛизинга = Новый Массив;
	//-- НЕ УТ
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		Если ТипЗнч(ОбъектОплаты) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") Тогда
			ДоговорыКредитовДепозитов.Добавить(ОбъектОплаты);
		//++ НЕ УТ
		ИначеЕсли ТипЗнч(ОбъектОплаты) = Тип("СправочникСсылка.ДоговорыЛизинга") Тогда
			ДоговорыЛизинга.Добавить(ОбъектОплаты);
		//-- НЕ УТ
		КонецЕсли;
	КонецЦикла;
	
	Если ДоговорыКредитовДепозитов.Количество() Тогда
		ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ДоговорыКредитовИДепозитов");
		ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоДоговорамКредитовИДепозитов();
		ЗаписатьГрафикПлатежейПоФинансовымИнструментам(ДоговорыКредитовДепозитов, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь);
	КонецЕсли;
	
	//++ НЕ УТ
	Если ДоговорыЛизинга.Количество() Тогда
		ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ДоговорыЛизинга");
		ТекстЗапроса = ТекстЗапросаГрафикПлатежейПоДоговорамЛизинга();
		ЗаписатьГрафикПлатежейПоФинансовымИнструментам(ДоговорыЛизинга, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь);
	КонецЕсли;
	//-- НЕ УТ
	
КонецПроцедуры

Процедура РассчитатьГрафикПлатежейПоОжидаемымПоступлениямДенежныхСредств(ДатыРасчета, НедействительныеДокументыОплаты = Неопределено, Очередь = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(НедействительныеДокументыОплаты) Тогда
		Для каждого НедействительныйДокументОплаты Из НедействительныеДокументыОплаты Цикл
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ОбъектОплаты.Установить(НедействительныйДокументОплаты);
			ЗаписатьНабор(НаборЗаписей, НедействительныйДокументОплаты, Очередь);
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                    КАК ОбъектОплаты,
	|	ДанныеДокумента.КассаБанковскийСчет                                       КАК БанковскийСчетКасса,
	|	ДанныеДокумента.ДатаПлатежа                                               КАК ДатаПлатежа,
	|	ДанныеДокумента.Организация                                               КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)            КАК ПоступлениеСписание,
	|	ДанныеДокумента.Валюта                                                    КАК Валюта,
	|	
	|	ДанныеДокумента.СуммаДокумента                                            КАК Сумма,
	|	
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств                             КАК СтатьяДвиженияДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ОжидаемыеПоступления)   КАК ОбластьПланирования,
	|	ДанныеДокумента.Подразделение                                             КАК Подразделение,
	|	ДанныеДокумента.Ответственный                                             КАК Ответственный,
	|	ДанныеДокумента.Номер                                                     КАК Номер,
	|	ДанныеДокумента.Дата                                                      КАК Дата,
	|
	|	ДанныеДокумента.СуммаДокумента                                            КАК СуммаДокумента,
	|	ДанныеДокумента.ФормаОплаты                                               КАК ФормаОплаты,
	|	ДанныеДокумента.ДатаПлатежа                                               КАК ДатаАктуальностиГрафика,
	|	КОНЕЦПЕРИОДА(ДанныеДокумента.ДатаПлатежа, ДЕНЬ)                           КАК КонецДатыПлатежа
	|ПОМЕСТИТЬ ОжидаемыеПоступления
	|ИЗ
	|	Документ.ОжидаемоеПоступлениеДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.ДатаПлатежа В (&ДатыРасчета)
	|	И НЕ ДанныеДокумента.Ссылка В (&НедействительныеДокументыОплаты)
	|";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатыРасчета", ДатыРасчета);
	Запрос.УстановитьПараметр("НедействительныеДокументыОплаты", НедействительныеДокументыОплаты);
	
	Результат = Запрос.ВыполнитьПакетСПромежуточнымиДанными()[0];
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДенежныеСредства.Период, ДЕНЬ) КАК ДатаПлатежа,
	|	ДенежныеСредства.Организация КАК Организация,
	|	ДенежныеСредства.Касса.ВалютаДенежныхСредств КАК Валюта,
	|	ДенежныеСредства.Касса КАК БанковскийСчетКасса,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная) КАК ФормаОплаты,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СУММА(ДенежныеСредства.Сумма) КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДенежныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ОжидаемыеПоступления
	|		ПО
	|			ОжидаемыеПоступления.Организация = ДенежныеСредства.Организация
	|			И ОжидаемыеПоступления.СтатьяДвиженияДенежныхСредств = ДенежныеСредства.СтатьяДвиженияДенежныхСредств
	|			И ОжидаемыеПоступления.Валюта = ДенежныеСредства.Касса.ВалютаДенежныхСредств
	|			И ДенежныеСредства.Период МЕЖДУ ОжидаемыеПоступления.ДатаПлатежа И ОжидаемыеПоступления.КонецДатыПлатежа
	|ГДЕ
	|	ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ОжидаемыеПоступления.ФормаОплаты В (
	|		ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная),
	|		ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка)
	|	)
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДенежныеСредства.Период, ДЕНЬ),
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Касса,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДенежныеСредства.Период, ДЕНЬ) КАК ДатаПлатежа,
	|	ДенежныеСредства.Организация КАК Организация,
	|	ДенежныеСредства.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|	ДенежныеСредства.БанковскийСчет КАК БанковскийСчетКасса,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная) КАК ФормаОплаты,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СУММА(ДенежныеСредства.Сумма) КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные КАК ДенежныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ОжидаемыеПоступления
	|		ПО
	|			ОжидаемыеПоступления.Организация = ДенежныеСредства.Организация
	|			И ОжидаемыеПоступления.СтатьяДвиженияДенежныхСредств = ДенежныеСредства.СтатьяДвиженияДенежныхСредств
	|			И ОжидаемыеПоступления.Валюта = ДенежныеСредства.БанковскийСчет.ВалютаДенежныхСредств
	|			И ДенежныеСредства.Период МЕЖДУ ОжидаемыеПоступления.ДатаПлатежа И ОжидаемыеПоступления.КонецДатыПлатежа
	|ГДЕ
	|	ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ОжидаемыеПоступления.ФормаОплаты В (
	|		ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная),
	|		ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка)
	|	)
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДенежныеСредства.Период, ДЕНЬ),
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.БанковскийСчет,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	ФактическиеПоступления = Запрос.Выполнить().Выгрузить();

	ИдентификаторОМ = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ОжидаемоеПоступлениеДенежныхСредств");
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СуммаПоступления = Выборка.Сумма;
		
		СтруктураПоиска = Новый Структура("ДатаПлатежа, Организация, Валюта, СтатьяДвиженияДенежныхСредств");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		Если ЗначениеЗаполнено(Выборка.ФормаОплаты) Тогда
			СтруктураПоиска.Вставить("ФормаОплаты", Выборка.ФормаОплаты);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.БанковскийСчетКасса) Тогда
			СтруктураПоиска.Вставить("БанковскийСчетКасса", Выборка.БанковскийСчетКасса);
		КонецЕсли;
		
		СтрокиКУдалению = Новый Массив;
		
		СтрокиФакт = ФактическиеПоступления.НайтиСтроки(СтруктураПоиска);
		Если СтрокиФакт.Количество() Тогда
			Для каждого СтрокаФакт Из СтрокиФакт Цикл
				
				Если СтрокаФакт.Сумма >= СуммаПоступления Тогда
					СтрокаФакт.Сумма = СтрокаФакт.Сумма - СуммаПоступления;
					СуммаПоступления = 0;
				Иначе
					СуммаПоступления = СуммаПоступления - СтрокаФакт.Сумма;
					СтрокаФакт.Сумма = 0;
				КонецЕсли;
				
				Если СтрокаФакт.Сумма = 0 Тогда
					СтрокиКУдалению.Добавить(СтрокаФакт);
				КонецЕсли;
				
				Если СуммаПоступления = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ФактическиеПоступления.Удалить(СтрокаКУдалению);
		КонецЦикла;
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(Выборка.ОбъектОплаты);
		
		Если СуммаПоступления <> 0 Тогда
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Сумма = СуммаПоступления;
			Запись.ТипОбъектаОплаты = ИдентификаторОМ;
		КонецЕсли;
		
		ЗаписатьНабор(НаборЗаписей, Выборка.ОбъектОплаты, Очередь);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьГрафикПлатежейПоРасчетам(ТаблицаОбъектовОплаты, ТекстЗапроса, ПоступлениеСписание, ОбластьПланирования, Очередь)
	
	Если Не ТаблицаОбъектовОплаты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПоляГрафика = ИнициализироватьПоляГрафика();
	
	ЗапросыПоТипам = Новый Соответствие;
	ВыборкиПоТипам = Новый Соответствие;
	
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ОбъектОплаты) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипОбъектаОплаты = ОбщегоНазначения.ИмяТаблицыПоСсылке(СтрокаТаблицы.ОбъектОплаты);
		Если ЗапросыПоТипам.Получить(ТипОбъектаОплаты) = Неопределено Тогда
			ЗапросыПоТипам.Вставить(ТипОбъектаОплаты,
				ДенежныеСредстваПовтИсп.ТекстЗапросаКэшРеквизитовПлатежа(ТипОбъектаОплаты, ПоляГрафика));
		КонецЕсли;
	КонецЦикла;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	Для каждого ЗапросПоТипу Из ЗапросыПоТипам Цикл
		НовыйЗапрос = СхемаЗапроса.ПакетЗапросов.Добавить();
		НовыйЗапрос.УстановитьТекстЗапроса(ЗапросПоТипу.Значение);
	КонецЦикла;
	Запрос = Новый Запрос(СхемаЗапроса.ПолучитьТекстЗапроса());
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		Запрос.УстановитьПараметр("Ссылка", СтрокаТаблицы.ОбъектОплаты);
		Результат = Запрос.ВыполнитьПакет();
		инд = 0;
		Для каждого ЗапросПоТипу Из ЗапросыПоТипам Цикл
			ВыборкиПоТипам.Вставить(ЗапросПоТипу.Ключ, Результат[инд].Выбрать());
			инд = инд + 1;
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ОбъектыРасчетов", ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектРасчетов"));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для каждого СтрокаТаблицы Из ТаблицаОбъектовОплаты Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ОбъектОплаты) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Ссылка", СтрокаТаблицы.ОбъектОплаты);
		
		НаборЗаписей = РегистрыСведений.ГрафикПлатежей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(СтрокаТаблицы.ОбъектОплаты);
		НаборЗаписей.Отбор.ПоступлениеСписание.Установить(ПоступлениеСписание);
		
		ТипОбъектаОплаты = ОбщегоНазначения.ИмяТаблицыПоСсылке(СтрокаТаблицы.ОбъектОплаты);
		
		ВыборкаРеквизитов = ВыборкиПоТипам.Получить(ТипОбъектаОплаты);
		
		Если ВыборкаРеквизитов.НайтиСледующий(СтруктураПоиска) Тогда
			ПоляГрафика = ИнициализироватьПоляГрафика();
			ЗаполнитьЗначенияСвойств(ПоляГрафика, ВыборкаРеквизитов);
		КонецЕсли;
		
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			ЗаполнитьЗначенияСвойств(Запись, ПоляГрафика);
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.ОбъектОплаты = Выборка.Ссылка;
			Запись.ПоступлениеСписание = ПоступлениеСписание;
			Запись.ОбластьПланирования = ОбластьПланирования;
			
			Запись.БанковскийСчетКасса = Неопределено;
			Если ЗначениеЗаполнено(ПоляГрафика.ФормаОплаты) Тогда
				Если ПоляГрафика.ФормаОплаты = Перечисления.ФормыОплаты.Безналичная Тогда
					Если ЗначениеЗаполнено(ПоляГрафика.БанковскийСчет) Тогда
						Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчет;
					ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.БанковскийСчетОрганизации) Тогда
						Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчетОрганизации;
					КонецЕсли;
				ИначеЕсли ПоляГрафика.ФормаОплаты = Перечисления.ФормыОплаты.Наличная
					И ЗначениеЗаполнено(ПоляГрафика.Касса) Тогда
					Запись.БанковскийСчетКасса = ПоляГрафика.Касса;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ПоляГрафика.Касса) Тогда
					Запись.БанковскийСчетКасса = ПоляГрафика.Касса;
				ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.БанковскийСчет) Тогда
					Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчет;
				ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.БанковскийСчетОрганизации) Тогда
					Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчетОрганизации;
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ПоляГрафика.СтатьяДвиженияДенежныхСредств) Тогда
				Если ЗначениеЗаполнено(ПоляГрафика.ДоговорСтатьяДвиженияДенежныхСредств) Тогда
					Запись.СтатьяДвиженияДенежныхСредств = ПоляГрафика.ДоговорСтатьяДвиженияДенежныхСредств;
				ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.СоглашениеСтатьяДвиженияДенежныхСредств) Тогда
					Запись.СтатьяДвиженияДенежныхСредств = ПоляГрафика.СоглашениеСтатьяДвиженияДенежныхСредств;
				Иначе
					Запись.СтатьяДвиженияДенежныхСредств =
						Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ПоляГрафика.ХозяйственнаяОперация);
				КонецЕсли;
			КонецЕсли;
			
			Запись.Ответственный = ПоляГрафика.Менеджер;
			Запись.ТипОбъектаОплаты = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипОбъектаОплаты);
		КонецЦикла;
		Выборка.Сбросить();
		
		НачатьТранзакцию();
		Попытка
			Если Очередь <> Неопределено Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				НаборЗаписей.Записать();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось записать график платежей по: %Ссылка% по причине: %Причина%';uk='Не вдалося записати графік платежів по: %Ссылка% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", СтрокаТаблицы.ОбъектОплаты);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,,
				СтрокаТаблицы.ОбъектОплаты, ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьГрафикПлатежейПоДенежнымСредствамКВыплате(ОбъектыОплаты, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ОбъектыОплаты", ОбъектыОплаты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		
		СтруктураПоиска = Новый Структура("ОбъектОплаты", ОбъектОплаты);
		
		НаборЗаписей = РегистрыСведений.ГрафикПлатежей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(ОбъектОплаты);
		
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.ТипОбъектаОплаты = ИдентификаторОбъектаМетаданных;
		КонецЦикла;
		Выборка.Сбросить();
		
		ЗаписатьНабор(НаборЗаписей, ОбъектОплаты, Очередь);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьГрафикПлатежейПоФинансовымИнструментам(ОбъектыОплаты, ТекстЗапроса, ИдентификаторОбъектаМетаданных, Очередь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ОбъектыОплаты", ОбъектыОплаты);
	
	Результат = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Выборка = Результат[3].Выбрать();
	ВыборкаРеквизитов = Результат[4].Выбрать();
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		
		Если Не ЗначениеЗаполнено(ОбъектОплаты) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("ОбъектОплаты", ОбъектОплаты);
		
		НаборЗаписей = РегистрыСведений.ГрафикПлатежей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(ОбъектОплаты);
		
		ВыборкаРеквизитов.НайтиСледующий(СтруктураПоиска);
		
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаРеквизитов);
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.БанковскийСчетКасса = Неопределено;
			Если ЗначениеЗаполнено(ВыборкаРеквизитов.ФормаОплаты) Тогда
				Если ВыборкаРеквизитов.ФормаОплаты = Перечисления.ФормыОплаты.Безналичная Тогда
					Если ЗначениеЗаполнено(ВыборкаРеквизитов.БанковскийСчет) Тогда
						Запись.БанковскийСчетКасса = ВыборкаРеквизитов.БанковскийСчет;
					КонецЕсли;
				ИначеЕсли ВыборкаРеквизитов.ФормаОплаты = Перечисления.ФормыОплаты.Наличная
					И ЗначениеЗаполнено(ВыборкаРеквизитов.Касса) Тогда
					Запись.БанковскийСчетКасса = ВыборкаРеквизитов.Касса;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ВыборкаРеквизитов.Касса) Тогда
					Запись.БанковскийСчетКасса = ВыборкаРеквизитов.Касса;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаРеквизитов.БанковскийСчет) Тогда
					Запись.БанковскийСчетКасса = ВыборкаРеквизитов.БанковскийСчет;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗнч(ОбъектОплаты) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") Тогда
				Если Выборка.ПоступлениеСписание = Перечисления.ТипыДвиженияДенежныхСредств.Списание
					И ВыборкаРеквизитов.ХарактерДоговора = Перечисления.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм
					Или Выборка.ПоступлениеСписание = Перечисления.ТипыДвиженияДенежныхСредств.Поступление
					И (ВыборкаРеквизитов.ХарактерДоговора = Перечисления.ХарактерыДоговоровФинансовыхИнструментов.Депозит
						Или ВыборкаРеквизитов.ХарактерДоговора = Перечисления.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) Тогда
					
					Если Выборка.ТипСуммы = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг Тогда
						Запись.СтатьяДвиженияДенежныхСредств = ВыборкаРеквизитов.СтатьяДДСОсновногоДолга;
					ИначеЕсли Выборка.ТипСуммы = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.Проценты Тогда
						Запись.СтатьяДвиженияДенежныхСредств = ВыборкаРеквизитов.СтатьяДДСПроцентов;
					ИначеЕсли Выборка.ТипСуммы = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.Комиссия Тогда
						Запись.СтатьяДвиженияДенежныхСредств = ВыборкаРеквизитов.СтатьяДДСКомиссии;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Запись.ТипОбъектаОплаты = ИдентификаторОбъектаМетаданных;
		КонецЦикла;
		Выборка.Сбросить();
		
		ЗаписатьНабор(НаборЗаписей, ОбъектОплаты, Очередь);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьНабор(НаборЗаписей, ОбъектОплаты, Очередь)
	
	НачатьТранзакцию();
	Попытка
		Если Очередь <> Неопределено Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Иначе
			НаборЗаписей.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось записать график платежей по: %Ссылка% по причине: %Причина%';uk='Не вдалося записати графік платежів по: %Ссылка% через: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ОбъектОплаты);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,
			ОбъектОплаты, ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентами()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеПриход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(,,
	|			День,
	|			(ОбъектРасчетов, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ОбъектРасчетов,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамКлиентам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеРасход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(,,
	|			День,
	|			(ОбъектРасчетов, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ОбъектРасчетов,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентамиНоваяАрхитектура()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланОплат.ДатаПлановогоПогашения КАК Период,
	|	ПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта КАК Валюта,
	|	СУММА(ПланОплат.КОплатеКонечныйОстаток) КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат.ОстаткиИОбороты(,,,, ОбъектРасчетов В (&ОбъектыРасчетов)) КАК ПланОплат
	|ГДЕ
	|	ПланОплат.КОплатеКонечныйОстаток <> 0
	|СГРУППИРОВАТЬ ПО
	|	ПланОплат.ДатаПлановогоПогашения,
	|	ПланОплат.ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамКлиентамНоваяАрхитектура()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланОплат.ДатаПлановогоПогашения КАК Период,
	|	ПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта КАК Валюта,
	|	СУММА(ПланОплат.КОплатеКонечныйОстаток) КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат.ОстаткиИОбороты(,,,, ОбъектРасчетов В (&ОбъектыРасчетов)) КАК ПланОплат
	|ГДЕ
	|	ПланОплат.КОплатеКонечныйОстаток <> 0
	|СГРУППИРОВАТЬ ПО
	|	ПланОплат.ДатаПлановогоПогашения,
	|	ПланОплат.ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщиками()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеРасход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(,,
	|			День,
	|			(ОбъектРасчетов, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ОбъектРасчетов,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиков()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеПриход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(,,
	|			День,
	|			(ОбъектРасчетов, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ОбъектРасчетов,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщикамиНоваяАрхитектура()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланОплат.ДатаПлановогоПогашения КАК Период,
	|	ПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта КАК Валюта,
	|	СУММА(ПланОплат.КОплатеКонечныйОстаток) КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПланОплат.ОстаткиИОбороты(,,,, ОбъектРасчетов В (&ОбъектыРасчетов)) КАК ПланОплат
	|ГДЕ
	|	ПланОплат.КОплатеКонечныйОстаток <> 0
	|СГРУППИРОВАТЬ ПО
	|	ПланОплат.ДатаПлановогоПогашения,
	|	ПланОплат.ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиковНоваяАрхитектура()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			ОбъектРасчетов В (&ОбъектыРасчетов)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланОплат.ДатаПлановогоПогашения КАК Период,
	|	ПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта КАК Валюта,
	|	СУММА(ПланОплат.КОплатеКонечныйОстаток) КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПланОплат.ОстаткиИОбороты(,,,, ОбъектРасчетов В (&ОбъектыРасчетов)) КАК ПланОплат
	|ГДЕ
	|	ПланОплат.КОплатеКонечныйОстаток <> 0
	|СГРУППИРОВАТЬ ПО
	|	ПланОплат.ДатаПлановогоПогашения,
	|	ПланОплат.ОбъектРасчетов,
	|	ПланОплат.АналитикаУчетаПоПартнерам,
	|	ПланОплат.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ОбъектРасчетов = ГрафикОплаты.ОбъектРасчетов
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов.Объект КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.ОбъектРасчетов,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоЗаявкам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка,
	|	ДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ДенежныеСредства.СуммаОстаток КАК КВыплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(,
	|			ЗаявкаНаРасходованиеДенежныхСредств В (&ОбъектыОплаты)
	|		) КАК ДенежныеСредства
	|
	|ГДЕ
	|	ДенежныеСредства.СуммаОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаРасходованиеДенежныхСредств,
	|	БанковскийСчетКасса
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка,
	|	ДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ДенежныеСредства.СуммаПриход КАК КВыплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате.Обороты(,,
	|			День,
	|			(ЗаявкаНаРасходованиеДенежныхСредств, БанковскийСчетКасса) В
	|			(ВЫБРАТЬ
	|				Остатки.Заявка,
	|				Остатки.БанковскийСчетКасса
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК ДенежныеСредства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаРасходованиеДенежныхСредств,
	|	БанковскийСчетКасса
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.Заявка КАК Ссылка,
	|	ГрафикОплаты.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КВыплате) - МАКСИМУМ(ГрафикОплаты.КВыплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КВыплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КВыплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КВыплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КВыплате, 0))
	|	КОНЕЦ КАК Сумма
	|
	|ПОМЕСТИТЬ ГрафикПлатежейИтог
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.Заявка = ГрафикОплаты.Заявка
	|		И ГрафикОплатыИтог.БанковскийСчетКасса = ГрафикОплаты.БанковскийСчетКасса
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.Заявка = ГрафикОплаты.Заявка
	|		И Остатки.БанковскийСчетКасса = ГрафикОплаты.БанковскийСчетКасса
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.Заявка,
	|	ГрафикОплаты.БанковскийСчетКасса
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КВыплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КВыплате, 0)) > 0
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПлатежей.Ссылка КАК ОбъектОплаты,
	|	ГрафикПлатежей.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ГрафикПлатежей.ДатаПлатежа КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймаСотруднику),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
	|			)
	|		ТОГДА
	|			Заявка.ПодотчетноеЛицо
	|		КОГДА Заявка.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|			)
	|		ТОГДА
	|			Заявка.ОрганизацияПолучатель
	|		ИНАЧЕ
	|			Заявка.Контрагент
	|	КОНЕЦ КАК ПлательщикПолучатель,
	|	Заявка.Валюта КАК Валюта,
	|	
	|	ГрафикПлатежей.Сумма КАК Сумма,
	|	
	|	Заявка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Заявка.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Заявка.Подразделение КАК Подразделение,
	|	
	|	ВЫБОР КОГДА ГрафикПлатежей.БанковскийСчетКасса ССЫЛКА Справочник.Кассы ТОГДА
	|		ВЫРАЗИТЬ(ГрафикПлатежей.БанковскийСчетКасса КАК Справочник.Кассы).НаправлениеДеятельности
	|	КОГДА ГрафикПлатежей.БанковскийСчетКасса ССЫЛКА Справочник.БанковскиеСчетаОрганизаций ТОГДА
	|		ВЫРАЗИТЬ(ГрафикПлатежей.БанковскийСчетКасса КАК Справочник.БанковскиеСчетаОрганизаций).НаправлениеДеятельности
	|	КОГДА ГрафикПлатежей.БанковскийСчетКасса = НЕОПРЕДЕЛЕНО ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	
	|	Заявка.ПриоритетОплаты КАК Приоритет,
	|	Заявка.КтоЗаявил КАК Ответственный,
	|	Заявка.Номер КАК Номер,
	|	Заявка.Дата КАК Дата,
	|	Заявка.Организация КАК Организация,
	|	Заявка.СуммаДокумента КАК СуммаДокумента,
	|	
	|	ВЫБОР
	|		КОГДА Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.НеСогласована) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ЗаявкиНаРасходованиеДенежныхСредствНеСогласованные)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ЗаявкиНаРасходованиеДенежныхСредств)
	|	КОНЕЦ КАК ОбластьПланирования,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПлатежей.БанковскийСчетКасса) = ТИП(Справочник.БанковскиеСчетаОрганизаций) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПлатежей.БанковскийСчетКасса) = ТИП(Справочник.Кассы) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		ИНАЧЕ
	|			Заявка.ФормаОплатыЗаявки
	|	КОНЕЦ КАК ФормаОплаты
	|	
	|ИЗ
	|	ГрафикПлатежейИтог КАК ГрафикПлатежей
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|	ПО Заявка.Ссылка = ГрафикПлатежей.Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ГрафикПлатежей.Ссылка КАК ОбъектОплаты,
	|	ВЫБОР
	|		КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная) ТОГДА
	|			Заявка.КассаПолучатель
	|		КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная) ТОГДА
	|			Заявка.БанковскийСчетПолучатель
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК БанковскийСчетКасса,
	
	|	ГрафикПлатежей.ДатаПлатежа КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	
	|	ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|			)
	|		ТОГДА
	|			Заявка.Организация
	|		ИНАЧЕ
	|			Заявка.Контрагент
	|	КОНЕЦ КАК ПлательщикПолучатель,
	
	|	ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты) ТОГДА
	|			Заявка.ВалютаКонвертации
	|		ИНАЧЕ
	|			Заявка.Валюта
	|	КОНЕЦ КАК Валюта,
	|	
	|	СУММА(ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты) ТОГДА
	|			ВЫБОР
	|				КОГДА Заявка.ВалютаКонвертации = Константы.ВалютаРегламентированногоУчета ТОГДА
	|					ВЫБОР КОГДА Заявка.КратностьКурсаКонвертации <> 0 ТОГДА
	|						ГрафикПлатежей.Сумма * Заявка.КурсКонвертации / Заявка.КратностьКурсаКонвертации
	|					ИНАЧЕ
	|						0
	|					КОНЕЦ
	|				ИНАЧЕ
	|					ВЫБОР КОГДА Заявка.КурсКонвертации <> 0 ТОГДА
	|						ГрафикПлатежей.Сумма / Заявка.КурсКонвертации * Заявка.КратностьКурсаКонвертации
	|					ИНАЧЕ
	|						0
	|					КОНЕЦ
	|				КОНЕЦ
	|			ИНАЧЕ
	|				ГрафикПлатежей.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	
	|	Заявка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Заявка.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Заявка.Подразделение КАК Подразделение,
	|	
	|	ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|			) ТОГДА
	|				ВЫБОР КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная) ТОГДА
	|					Заявка.КассаПолучатель.НаправлениеДеятельности
	|				ИНАЧЕ
	|					Заявка.БанковскийСчетПолучатель.НаправлениеДеятельности
	|				КОНЕЦ
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	
	|	Заявка.ПриоритетОплаты КАК Приоритет,
	|	Заявка.КтоЗаявил КАК Ответственный,
	|	Заявка.Номер КАК Номер,
	|	Заявка.Дата КАК Дата,
	|	
	|	ВЫБОР
	|		КОГДА Заявка.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|			) ТОГДА
	|				Заявка.ОрганизацияПолучатель
	|		ИНАЧЕ
	|			Заявка.Организация
	|	КОНЕЦ КАК Организация,
	|	
	|	Заявка.СуммаДокумента КАК СуммаДокумента,
	|	
	|	ВЫБОР
	|		КОГДА Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.НеСогласована) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ЗаявкиНаРасходованиеДенежныхСредствНеСогласованные)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ЗаявкиНаРасходованиеДенежныхСредств)
	|	КОНЕЦ КАК ОбластьПланирования,
	|	Заявка.ФормаОплатыЗаявки КАК ФормаОплаты
	|	
	|ИЗ
	|	ГрафикПлатежейИтог КАК ГрафикПлатежей
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|	ПО Заявка.Ссылка = ГрафикПлатежей.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы
	|	ПО ИСТИНА
	|	
	|ГДЕ
	|	Заявка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикПлатежей.Ссылка,
	|	Заявка.Ссылка,
	|	ГрафикПлатежей.ДатаПлатежа
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоРаспоряжениямНаОплату()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств КАК Ссылка,
	|	ДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ДенежныеСредства.Получатель КАК Получатель,
	|	ДенежныеСредства.Организация КАК Организация,
	|	ДенежныеСредства.СуммаОстаток КАК Сумма
	|	
	|ПОМЕСТИТЬ ДенежныеСредства
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(, ЗаявкаНаРасходованиеДенежныхСредств В (&ОбъектыОплаты)) КАК ДенежныеСредства
	|	
	|ГДЕ
	|	ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств ССЫЛКА Документ.РаспоряжениеНаПеремещениеДенежныхСредств
	|	И ДенежныеСредства.СуммаОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредства.Ссылка КАК ОбъектОплаты,
	|	ДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	Распоряжение.ДатаПлатежа КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ДенежныеСредства.Получатель КАК ПлательщикПолучатель,
	|	Распоряжение.Валюта КАК Валюта,
	|	
	|	ДенежныеСредства.Сумма КАК Сумма,
	|	
	|	Распоряжение.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Распоряжение.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Распоряжение.Подразделение КАК Подразделение,
	|	Распоряжение.КтоРешил КАК Ответственный,
	|	
	|	ВЫБОР Распоряжение.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.Касса.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.Касса.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу) ТОГДА
	|			Распоряжение.Касса.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет) ТОГДА
	|			Распоряжение.БанковскийСчет.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка) ТОГДА
	|			Распоряжение.БанковскийСчет.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств) ТОГДА
	|			Распоряжение.БанковскийСчет.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	Распоряжение.Номер КАК Номер,
	|	Распоряжение.Дата КАК Дата,
	|	Распоряжение.Организация КАК Организация,
	|	Распоряжение.СуммаДокумента КАК СуммаДокумента,
	|
	|	ВЫБОР
	|		КОГДА Распоряжение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРаспоряженийНаПеремещениеДенежныхСредств.НеСогласовано) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.РаспоряженияНаПеремещениеДенежныхСредствНесогласованные)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.РаспоряженияНаПеремещениеДенежныхСредств)
	|	КОНЕЦ КАК ОбластьПланирования,
	|	ВЫБОР
	|		КОГДА Распоряжение.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		КОГДА Распоряжение.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|	КОНЕЦ КАК ФормаОплаты
	|	
	|ИЗ
	|	ДенежныеСредства КАК ДенежныеСредства
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспоряжениеНаПеремещениеДенежныхСредств КАК Распоряжение
	|	ПО
	|		Распоряжение.Ссылка = ДенежныеСредства.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДенежныеСредства.Ссылка КАК ОбъектОплаты,
	|	
	|	ВЫБОР Распоряжение.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу) ТОГДА
	|			Распоряжение.КассаПолучатель
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка) ТОГДА
	|			Распоряжение.КассаПолучатель
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств) ТОГДА
	|			Распоряжение.КассаПолучатель
	|	КОНЕЦ КАК БанковскийСчетКасса,
	|	
	|	Распоряжение.ДатаПлатежа КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ДенежныеСредства.Получатель КАК ПлательщикПолучатель,
	|	Распоряжение.Валюта КАК Валюта,
	|	
	|	ДенежныеСредства.Сумма КАК Сумма,
	|	
	|	Распоряжение.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Распоряжение.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Распоряжение.Подразделение КАК Подразделение,
	|	Распоряжение.КтоРешил КАК Ответственный,
	|	
	|	ВЫБОР Распоряжение.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу) ТОГДА
	|			Распоряжение.КассаПолучатель.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет) ТОГДА
	|			Распоряжение.БанковскийСчетПолучатель.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка) ТОГДА
	|			Распоряжение.КассаПолучатель.НаправлениеДеятельности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств) ТОГДА
	|			Распоряжение.КассаПолучатель.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	
	|	Распоряжение.Номер КАК Номер,
	|	Распоряжение.Дата КАК Дата,
	|	Распоряжение.Организация КАК Организация,
	|	Распоряжение.СуммаДокумента КАК СуммаДокумента,
	|
	|	ВЫБОР КОГДА Распоряжение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРаспоряженийНаПеремещениеДенежныхСредств.НеСогласовано) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.РаспоряженияНаПеремещениеДенежныхСредствНесогласованные)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.РаспоряженияНаПеремещениеДенежныхСредств)
	|	КОНЕЦ КАК ОбластьПланирования,
	|	ВЫБОР
	|		КОГДА Распоряжение.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|		КОГДА Распоряжение.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|	КОНЕЦ КАК ФормаОплаты
	|
	|ИЗ
	|	ДенежныеСредства КАК ДенежныеСредства
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспоряжениеНаПеремещениеДенежныхСредств КАК Распоряжение
	|	ПО
	|		Распоряжение.Ссылка = ДенежныеСредства.Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ Локализация
//-- Локализация

Функция ТекстЗапросаГрафикПлатежейПоДоговорамКредитовИДепозитов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка                            КАК ОбъектОплаты,
	|	ДанныеСправочника.ХарактерДоговора                  КАК ХарактерДоговора,
	|	ДанныеСправочника.ВалютаВзаиморасчетов              КАК Валюта,
	|	ДанныеСправочника.БанковскийСчет                    КАК БанковскийСчет,
	|	ДанныеСправочника.Касса                             КАК Касса,
	|	ДанныеСправочника.БанковскийСчетПроцентов           КАК БанковскийСчетПроцентов,
	|	ДанныеСправочника.БанковскийСчетКомиссии            КАК БанковскийСчетКомиссии,
	|	ДанныеСправочника.Контрагент                        КАК ПлательщикПолучатель,
	|	ДанныеСправочника.Организация                       КАК Организация,
	|	ДанныеСправочника.СтатьяДДСПоступленияВыдачи        КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеСправочника.СтатьяДДСОсновногоДолга           КАК СтатьяДДСОсновногоДолга,
	|	ДанныеСправочника.СтатьяДДСПроцентов                КАК СтатьяДДСПроцентов,
	|	ДанныеСправочника.СтатьяДДСКомиссии                 КАК СтатьяДДСКомиссии,
	|	ДанныеСправочника.Подразделение                     КАК Подразделение,
	|	ДанныеСправочника.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ДанныеСправочника.Ответственный                     КАК Ответственный,
	|	ДанныеСправочника.Номер                             КАК Номер,
	|	ДанныеСправочника.Дата                              КАК Дата,
	|	ДанныеСправочника.СуммаТраншей                      КАК СуммаДокумента,
	|	ДанныеСправочника.ФормаОплаты                       КАК ФормаОплаты,
	|	ВЫБОР ДанныеСправочника.ХарактерДоговора
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.КредитыИлиЗаймыПолученные)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.Депозиты)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.ЗаймыВыданные)
	|	КОНЕЦ КАК ОбластьПланирования
	|ПОМЕСТИТЬ Договоры
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка В (&ОбъектыОплаты)
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектОплаты,
	|	ХарактерДоговора
	|;
	|
	|ВЫБРАТЬ
	|	Расчеты.Договор КАК Договор,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.ТипСуммы КАК ТипСуммы,
	|	Расчеты.СуммаРасход КАК Сумма
	|ПОМЕСТИТЬ ПоступлениеСредств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,,,
	|		Договор В (&ОбъектыОплаты)
	|	) КАК Расчеты
	|ГДЕ
	|	Расчеты.СуммаРасход > 0
	|;
	|
	|ВЫБРАТЬ
	|	Расчеты.Договор КАК Договор,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.ТипСуммы КАК ТипСуммы,
	|	Расчеты.СуммаПриход - Расчеты.ОплачиваетсяПриход + Расчеты.ОплачиваетсяРасход КАК Сумма
	|ПОМЕСТИТЬ СписаниеСредств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,,,
	|		Договор В (&ОбъектыОплаты)
	|	) КАК Расчеты
	|ГДЕ
	|	Расчеты.СуммаПриход - Расчеты.ОплачиваетсяПриход + Расчеты.ОплачиваетсяРасход > 0
	|;
	|
	|ВЫБРАТЬ // Транши полученного кредита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.СуммаИтог - График.Сумма - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) < 0 ТОГДА
	|			График.СуммаИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Сумма
	|	КОНЕЦ КАК Сумма
	|	
	|ИЗ
	|	РегистрСведений.ГрафикТраншейКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСредств
	|		ПО ПоступлениеСредств.Договор = Договоры.ОбъектОплаты
	|			И ПоступлениеСредств.Валюта = Договоры.Валюта
	|			И ПоступлениеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|ГДЕ
	|	Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Сумма > 0
	|	И График.СуммаИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Транши выданного кредита или депозита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ТипСуммы,
	|	ВЫБОР Договоры.ХарактерДоговора
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеНаДепозиты)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.СуммаИтог - График.Сумма - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.СуммаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Сумма
	|	КОНЕЦ КАК Сумма
	|	
	|ИЗ
	|	РегистрСведений.ГрафикТраншейКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|ГДЕ
	|	Договоры.ХарактерДоговора В (
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит),
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный))
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Сумма > 0
	|	И График.СуммаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата полученного кредита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.СуммаИтог - График.Сумма - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.СуммаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Сумма
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Сумма > 0
	|	И График.СуммаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата процентов полученного кредита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.ПроцентыИтог - График.Проценты - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.ПроцентыИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Проценты
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Проценты > 0
	|	И График.ПроцентыИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата комиссии полученного кредита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.КомиссияИтог - График.Комиссия - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.КомиссияИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Комиссия
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Комиссия > 0
	|	И График.КомиссияИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата выданного кредита, депозита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ТипСуммы,
	|	ВЫБОР Договоры.ХарактерДоговора
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.СуммаИтог - График.Сумма - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) < 0 ТОГДА
	|			График.СуммаИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Сумма
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСредств
	|		ПО ПоступлениеСредств.Договор = Договоры.ОбъектОплаты
	|			И ПоступлениеСредств.Валюта = Договоры.Валюта
	|			И ПоступлениеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора В (
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит),
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный))
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Сумма > 0
	|	И График.СуммаИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата процентов выданного кредита, депозита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты) КАК ТипСуммы,
	|	ВЫБОР Договоры.ХарактерДоговора
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеПроцентовПоЗаймамВыданным)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.ПроцентыИтог - График.Проценты - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) < 0 ТОГДА
	|			График.ПроцентыИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Проценты
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСредств
	|		ПО ПоступлениеСредств.Договор = Договоры.ОбъектОплаты
	|			И ПоступлениеСредств.Валюта = Договоры.Валюта
	|			И ПоступлениеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора В (
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит),
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный))
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Проценты > 0
	|	И График.ПроцентыИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Оплата комиссии выданного кредита, депозита
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия) КАК ТипСуммы,
	|	ВЫБОР Договоры.ХарактерДоговора
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.КомиссияИтог - График.Комиссия - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) < 0 ТОГДА
	|			График.КомиссияИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.Комиссия
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСредств
	|		ПО ПоступлениеСредств.Договор = Договоры.ОбъектОплаты
	|			И ПоступлениеСредств.Валюта = Договоры.Валюта
	|			И ПоступлениеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|	
	|ГДЕ
	|	Договоры.ХарактерДоговора В (
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит),
	|		ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный))
	|	И График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.Комиссия > 0
	|	И График.КомиссияИтог - ЕСТЬNULL(ПоступлениеСредств.Сумма, 0) > 0
	|;
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	Договоры
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТ
Функция ТекстЗапросаГрафикПлатежейПоДоговорамЛизинга()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка                            КАК ОбъектОплаты,
	|	ДанныеСправочника.ВалютаВзаиморасчетов              КАК Валюта,
	|	ДанныеСправочника.БанковскийСчет                    КАК БанковскийСчет,
	|	ДанныеСправочника.Контрагент                        КАК ПлательщикПолучатель,
	|	ДанныеСправочника.Организация                       КАК Организация,
	|	ДанныеСправочника.СтатьяДвиженияДенежныхСредств     КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеСправочника.Подразделение                     КАК Подразделение,
	|	ДанныеСправочника.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ДанныеСправочника.Ответственный                     КАК Ответственный,
	|	ДанныеСправочника.Номер                             КАК Номер,
	|	ДанныеСправочника.Дата                              КАК Дата,
	|	ДанныеСправочника.СуммаУслугПоЛизингу               КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)      КАК ФормаОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ОбластиПланированияПлатежей.Лизинг) КАК ОбластьПланирования
	|ПОМЕСТИТЬ Договоры
	|ИЗ
	|	Справочник.ДоговорыЛизинга КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка В (&ОбъектыОплаты)
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектОплаты
	|;
	|
	|ВЫБРАТЬ 1
	|;
	|
	|ВЫБРАТЬ
	|	Расчеты.Договор КАК Договор,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.ТипСуммы КАК ТипСуммы,
	|	Расчеты.СуммаПриход - Расчеты.ОплачиваетсяПриход + Расчеты.ОплачиваетсяРасход КАК Сумма
	|ПОМЕСТИТЬ СписаниеСредств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,,,
	|		Договор В (&ОбъектыОплаты)
	|	) КАК Расчеты
	|ГДЕ
	|	Расчеты.СуммаПриход - Расчеты.ОплачиваетсяПриход + Расчеты.ОплачиваетсяРасход > 0
	|;
	|
	|ВЫБРАТЬ // Оплата услуг по лизингу
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ЛизинговыйПлатеж) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаЛизингодателю) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.УслугаПоЛизингуИтог - График.УслугаПоЛизингу - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.УслугаПоЛизингуИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.УслугаПоЛизингу
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатЛизинга КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ЛизинговыйПлатеж)
	|	
	|ГДЕ
	|	График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.УслугаПоЛизингу > 0
	|	И График.УслугаПоЛизингуИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Обеспечительный платеж
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаЛизингодателю) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.ЗачетОбеспечительногоПлатежаИтог - График.ЗачетОбеспечительногоПлатежа - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.ЗачетОбеспечительногоПлатежаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.ЗачетОбеспечительногоПлатежа
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатЛизинга КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж)
	|	
	|ГДЕ
	|	График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.ЗачетОбеспечительногоПлатежа > 0
	|	И График.ЗачетОбеспечительногоПлатежаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Выкуп предмета лизинга
	|	Договоры.ОбъектОплаты КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ВыкупПредметаЛизинга) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаЛизингодателю) КАК ХозяйственнаяОперация,
	|	График.Период КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА График.ВыкупПредметаЛизингаИтог - График.ВыкупПредметаЛизинга - ЕСТЬNULL(СписаниеСредств.Сумма, 0) < 0 ТОГДА
	|			График.ВыкупПредметаЛизингаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0)
	|		ИНАЧЕ
	|			График.ВыкупПредметаЛизинга
	|	КОНЕЦ КАК Сумма
	|
	|ИЗ
	|	РегистрСведений.ГрафикОплатЛизинга КАК График
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Договоры
	|		ПО Договоры.ОбъектОплаты = График.ВариантГрафика.Владелец
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеСредств
	|		ПО СписаниеСредств.Договор = Договоры.ОбъектОплаты
	|			И СписаниеСредств.Валюта = Договоры.Валюта
	|			И СписаниеСредств.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ВыкупПредметаЛизинга)
	|	
	|ГДЕ
	|	График.ВариантГрафика.Используется
	|	И НЕ График.ВариантГрафика.ПометкаУдаления
	|	И График.ВыкупПредметаЛизинга > 0
	|	И График.ВыкупПредметаЛизингаИтог - ЕСТЬNULL(СписаниеСредств.Сумма, 0) > 0
	|;
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	Договоры
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ

Функция ИнициализироватьПоляГрафика()
	
	ПоляГрафика = Новый Структура("Ссылка, БанковскийСчет, БанковскийСчетОрганизации, Касса, СтатьяДвиженияДенежныхСредств, НаправлениеДеятельности,
		|ХозяйственнаяОперация, Подразделение, Приоритет, Менеджер, Номер, Дата, СуммаДокумента, ФормаОплаты,
		|ДоговорСтатьяДвиженияДенежныхСредств, СоглашениеСтатьяДвиженияДенежныхСредств");
		
	ПоляГрафика.Вставить("Сумма", "СуммаДокумента");
	
	Возврат ПоляГрафика;
	
КонецФункции

Функция НеобрабатываемыеДокументы()
	
	НеОбрабатываемые = Новый Массив;
	НеОбрабатываемые.Добавить(Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств"));
	НеОбрабатываемые.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	НеОбрабатываемые.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	НеОбрабатываемые.Добавить(Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств"));
	НеОбрабатываемые.Добавить(Тип("ДокументСсылка.ОперацияПоПлатежнойКарте"));
	//++ Локализация
	//-- Локализация
	
	Возврат НеОбрабатываемые;
	
КонецФункции

#КонецОбласти

#Область Обновление


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("983a001d-8510-416c-b860-6ac2c93752fa");
	Обработчик.Многопоточный = Истина;
	Обработчик.Процедура = "РегистрыСведений.ГрафикПлатежей.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ГрафикПлатежей.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрНакопления.РасчетыСКлиентами,"
		+ "РегистрНакопления.РасчетыСПоставщиками,"
		+ "РегистрНакопления.ДенежныеСредстваКВыплате,"
		+ "Справочник.СтатьиДвиженияДенежныхСредств,"
		+ "Справочник.СоглашенияСКлиентами,"
		+ "РегистрСведений.ГрафикТраншейКредитовИДепозитов,"
		+ "РегистрСведений.ГрафикОплатКредитовИДепозитов,"
		//++ НЕ УТ
		+ "РегистрСведений.ГрафикОплатЛизинга,"
		//-- НЕ УТ
		+ "РегистрНакопления.ДенежныеСредстваВПути,"
		+ "Справочник.ДоговорыКонтрагентов,"
		+ "Справочник.СоглашенияСПоставщиками";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ГрафикПлатежей";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ГрафикПлатежей";
	Обработчик.Комментарий = НСтр("ru='Обновляет график платежей в регистре ""График платежей"". До завершения данные графика платежей не корректны.';uk='Оновлює графік платежів у регістрі ""Графік платежів"". До завершення дані графіка платежів не є коректними.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеКорректировокРегистровДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";                 
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеКорректировокРегистровДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";                 

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДенежныеСредстваВПути.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ДенежныеСредстваКВыплате.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.СоглашенияСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию2";
	НоваяСтрока.Порядок = "После";
	
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ГрафикОплатКредитовИДепозитов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ГрафикТраншейКредитовИДепозитов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам";
	НоваяСтрока.Порядок = "После";       
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";       
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ГрафикПлатежей";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ГрафикПлатежей");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказКлиента КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки КАК Расчеты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Расчеты.УдалитьЗаказКлиента
	|		И ГрафикПлатежей.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(Расчеты.УдалитьЗаказКлиента) НЕ В (
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтКлиента),
	|		ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
	|	)
	|	И Расчеты.УдалитьЗаказКлиента <> НЕОПРЕДЕЛЕНО
	|	И Расчеты.УдалитьЗаказКлиента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказКлиента КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки КАК Расчеты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Расчеты.УдалитьЗаказКлиента
	|		И ГрафикПлатежей.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(Расчеты.УдалитьЗаказКлиента) В (
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтКлиента),
	|		ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
	|	)
	|	И Расчеты.УдалитьЗаказКлиента <> НЕОПРЕДЕЛЕНО
	|	И Расчеты.УдалитьЗаказКлиента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказПоставщику КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание) КАК ПоступлениеСписание
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки КАК Расчеты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Расчеты.УдалитьЗаказПоставщику
	|		И ГрафикПлатежей.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(Расчеты.УдалитьЗаказПоставщику) НЕ В (
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику)
	|	)
	|	И Расчеты.УдалитьЗаказПоставщику <> НЕОПРЕДЕЛЕНО
	|	И Расчеты.УдалитьЗаказПоставщику <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказПоставщику КАК ОбъектОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление) КАК ПоступлениеСписание
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки КАК Расчеты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Расчеты.УдалитьЗаказПоставщику
	|		И ГрафикПлатежей.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(Расчеты.УдалитьЗаказПоставщику) В (
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику)
	|	)
	|	И Расчеты.УдалитьЗаказПоставщику <> НЕОПРЕДЕЛЕНО
	|	И Расчеты.УдалитьЗаказПоставщику <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств КАК ОбъектОплаты
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДенежныеСредства
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДенежныеСредства.ЗаявкаНаРасходованиеДенежныхСредств) В (
	|		ТИП(Документ.ЗаявкаНаРасходованиеДенежныхСредств),
	|		ТИП(Документ.РаспоряжениеНаПеремещениеДенежныхСредств))
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДенежныеСредства.Отправитель КАК ОбъектОплаты,
	|	ДенежныеСредства.Получатель КАК БанковскийСчетКасса
	|	
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки КАК ДенежныеСредства
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = ДенежныеСредства.Отправитель
	|		И ГрафикПлатежей.БанковскийСчетКасса = ДенежныеСредства.Получатель
	|ГДЕ
	|	ДенежныеСредства.СуммаОстаток > 0
	|	И ДенежныеСредства.Отправитель <> НЕОПРЕДЕЛЕНО
	|	И ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|";
	
	ТаблицаОбъектовРасчетов = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовРасчетов, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Договоры.Ссылка КАК ОбъектОплаты
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК Договоры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Договоры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикТраншейКредитовИДепозитов КАК ГрафикТраншейКредитовИДепозитов
	|	ПО
	|		ГрафикТраншейКредитовИДепозитов.ВариантГрафика.Владелец = Договоры.Ссылка
	|		И ГрафикТраншейКредитовИДепозитов.ВариантГрафика.Используется
	|ГДЕ
	|	ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И Договоры.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|	И НЕ ГрафикТраншейКредитовИДепозитов.ВариантГрафика ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Договоры.Ссылка КАК ОбъектОплаты
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК Договоры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|	ПО
	|		ГрафикПлатежей.ОбъектОплаты = Договоры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|	ПО
	|		ГрафикОплатКредитовИДепозитов.ВариантГрафика.Владелец = Договоры.Ссылка
	|		И ГрафикОплатКредитовИДепозитов.ВариантГрафика.Используется
	|ГДЕ
	|	ГрафикПлатежей.ОбъектОплаты ЕСТЬ NULL
	|	И Договоры.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|	И НЕ ГрафикОплатКредитовИДепозитов.ВариантГрафика ЕСТЬ NULL
	|";
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаОбъектовОплаты, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "РегистрНакопления.РасчетыСКлиентами")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "РегистрНакопления.РасчетыСПоставщиками")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "РегистрНакопления.ДенежныеСредстваКВыплате")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "РегистрНакопления.ДенежныеСредстваВПути")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "Справочник.ДоговорыКонтрагентов")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "Справочник.СоглашенияСКлиентами")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "Справочник.СоглашенияСПоставщиками")
		Или ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Неопределено, "Справочник.СтатьиДвиженияДенежныхСредств") Тогда
		
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь,
			"РегистрСведений.ГрафикПлатежей"
		);
		
		Возврат;
		
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	ОбновляемыеДанные.ОбъектОплаты КАК ОбъектОплаты,
	|	ОбновляемыеДанные.ПоступлениеСписание КАК ПоступлениеСписание,
	|	ОбновляемыеДанные.БанковскийСчетКасса КАК БанковскийСчетКасса
	|
	|ПОМЕСТИТЬ ВТДляОбработкиСсылка
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	СсылкиДляОбработки.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
	|	И НЕ ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Справочник.Кассы),
	|		ТИП(Справочник.КассыККМ),
	|		ТИП(Справочник.БанковскиеСчетаОрганизаций),
	|		ТИП(Справочник.ДоговорыКредитовИДепозитов),
	|		ТИП(Справочник.Контрагенты)
	|	)
	|";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыСКлиентами", "УдалитьЗаказКлиента");
		РассчитатьГрафикПлатежейПоРасчетамСКлиентами22(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	СсылкиДляОбработки.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
	|	И НЕ ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Документ.ЗаявкаНаРасходованиеДенежныхСредств),
	|		ТИП(Документ.РаспоряжениеНаПеремещениеДенежныхСредств),
	|		ТИП(Справочник.Кассы),
	|		ТИП(Справочник.КассыККМ),
	|		ТИП(Справочник.БанковскиеСчетаОрганизаций),
	|		ТИП(Справочник.ДоговорыКредитовИДепозитов),
	|		ТИП(Справочник.Контрагенты)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыСПоставщиками", "УдалитьЗаказПоставщику");
		РассчитатьГрафикПлатежейПоРасчетамСПоставщиками22(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	СсылкиДляОбработки.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
	|	И ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтКлиента),
	|		ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыСКлиентами", "УдалитьЗаказКлиента");
		РассчитатьГрафикПлатежейПоРасчетамСКлиентами22(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	СсылкиДляОбработки.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
	|	И ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Документ.ВозвратТоваровПоставщику)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыСПоставщиками", "УдалитьЗаказПоставщику");
		РассчитатьГрафикПлатежейПоРасчетамСПоставщиками22(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Документ.ЗаявкаНаРасходованиеДенежныхСредств),
	|		ТИП(Документ.РаспоряжениеНаПеремещениеДенежныхСредств)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.ДенежныеСредстваКВыплате", "ЗаявкаНаРасходованиеДенежныхСредств");
		РассчитатьГрафикПлатежейПоДенежнымСредствамКВыплате(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты,
	|	СсылкиДляОбработки.БанковскийСчетКасса
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Справочник.Кассы),
	|		ТИП(Справочник.БанковскиеСчетаОрганизаций)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектыОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.ДенежныеСредстваВПути", "Отправитель");
		РассчитатьГрафикПлатежейПоДенежнымСредствамВПути(ТаблицаОбъектыОплаты, Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты,
	|	СсылкиДляОбработки.БанковскийСчетКасса
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Справочник.КассыККМ)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектыОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.ДенежныеСредстваВКассахККМ", "КассаККМ");
		РассчитатьГрафикПлатежейПоДенежнымСредствамВПути(ТаблицаОбъектыОплаты, Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты,
	|	СсылкиДляОбработки.БанковскийСчетКасса
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Справочник.Контрагенты)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектыОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.ДенежныеСредстваВПути", "Контрагент");
		РассчитатьГрафикПлатежейПоДенежнымСредствамВПути(ТаблицаОбъектыОплаты, Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СсылкиДляОбработки.ОбъектОплаты) В (
	|		ТИП(Справочник.ДоговорыКредитовИДепозитов)
	|	)
	|";
	
	НачатьТранзакцию();
	
	Попытка
		ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
		УстановитьБлокировкиДанныхДляРасчетаГрафика(ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыПоФинансовымИнструментам", "Договор");
		РассчитатьГрафикПлатежейПоФинансовымИнструментам(ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"), Параметры.Очередь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось заполнить график платежей по причине: %Причина%';uk='Не вдалося заповнити графік платежів по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстСообщения
		);
	КонецПопытки;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ОбъектОплаты
	|ИЗ
	|	ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|";
	
	ОбъектыОплаты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	Если ОбъектыОплаты.Количество() > 0 Тогда
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбъектыОплаты,, Параметры.Очередь);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"РегистрСведений.ГрафикПлатежей"
	);
		
КонецПроцедуры

Процедура РассчитатьГрафикПлатежейПоРасчетамСКлиентами22(ОбъектыОплаты, Очередь = Неопределено) Экспорт
	
	ОбъектыРасчетов = Новый Массив;
	Возвраты = Новый Массив;
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		Если ТипЗнч(ОбъектОплаты) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
			Или ТипЗнч(ОбъектОплаты) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
			Возвраты.Добавить(ОбъектОплаты);
		Иначе
			ОбъектыРасчетов.Добавить(ОбъектОплаты);
		КонецЕсли;
	КонецЦикла;
	
	Если ОбъектыРасчетов.Количество() Тогда
		ЗаписатьГрафикПлатежейПоРасчетам22(
			ОбъектыРасчетов,
			ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентами22(),
			Перечисления.ТипыДвиженияДенежныхСредств.Поступление,
			Перечисления.ОбластиПланированияПлатежей.РасчетыСКлиентами,
			Очередь);
	КонецЕсли;
	
	Если Возвраты.Количество() Тогда
		ЗаписатьГрафикПлатежейПоРасчетам22(
			Возвраты,
			ТекстЗапросаГрафикПлатежейПоВозвратамКлиентам22(),
			Перечисления.ТипыДвиженияДенежныхСредств.Списание,
			Перечисления.ОбластиПланированияПлатежей.ВозвратыКлиентам,
			Очередь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьГрафикПлатежейПоРасчетам22(ОбъектыОплаты, ТекстЗапроса, ПоступлениеСписание, ОбластьПланирования, Очередь)
	
	Если Не ОбъектыОплаты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПоляГрафика = ИнициализироватьПоляГрафика();
	
	ЗапросыПоТипам = Новый Соответствие;
	ВыборкиПоТипам = Новый Соответствие;
	
	Для каждого ОбъектОплаты из ОбъектыОплаты Цикл
		
		Если Не ЗначениеЗаполнено(ОбъектОплаты) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипОбъектаОплаты = ОбщегоНазначения.ИмяТаблицыПоСсылке(ОбъектОплаты);
		Если ЗапросыПоТипам.Получить(ТипОбъектаОплаты) = Неопределено Тогда
			ЗапросыПоТипам.Вставить(ТипОбъектаОплаты,
				ДенежныеСредстваПовтИсп.ТекстЗапросаКэшРеквизитовПлатежа(ТипОбъектаОплаты, ПоляГрафика));
		КонецЕсли;
	КонецЦикла;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	Для каждого ЗапросПоТипу Из ЗапросыПоТипам Цикл
		НовыйЗапрос = СхемаЗапроса.ПакетЗапросов.Добавить();
		НовыйЗапрос.УстановитьТекстЗапроса(ЗапросПоТипу.Значение);
	КонецЦикла;
	Запрос = Новый Запрос(СхемаЗапроса.ПолучитьТекстЗапроса());
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		Запрос.УстановитьПараметр("Ссылка", ОбъектыОплаты);
		Результат = Запрос.ВыполнитьПакет();
		инд = 0;
		Для каждого ЗапросПоТипу Из ЗапросыПоТипам Цикл
			ВыборкиПоТипам.Вставить(ЗапросПоТипу.Ключ, Результат[инд].Выбрать());
			инд = инд + 1;
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ОбъектыОплаты", ОбъектыОплаты);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для каждого ОбъектОплаты из ОбъектыОплаты Цикл
		
		Если Не ЗначениеЗаполнено(ОбъектОплаты) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Ссылка", ОбъектОплаты);
		
		НаборЗаписей = РегистрыСведений.ГрафикПлатежей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектОплаты.Установить(ОбъектОплаты);
		НаборЗаписей.Отбор.ПоступлениеСписание.Установить(ПоступлениеСписание);
		
		ТипОбъектаОплаты = ОбщегоНазначения.ИмяТаблицыПоСсылке(ОбъектОплаты);
		
		ВыборкаРеквизитов = ВыборкиПоТипам.Получить(ТипОбъектаОплаты);
		
		Если ВыборкаРеквизитов.НайтиСледующий(СтруктураПоиска) Тогда
			ПоляГрафика = ИнициализироватьПоляГрафика();
			ЗаполнитьЗначенияСвойств(ПоляГрафика, ВыборкаРеквизитов);
		КонецЕсли;
		
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			ЗаполнитьЗначенияСвойств(Запись, ПоляГрафика);
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.ОбъектОплаты = Выборка.Ссылка;
			Запись.ПоступлениеСписание = ПоступлениеСписание;
			Запись.ОбластьПланирования = ОбластьПланирования;
			
			Если ЗначениеЗаполнено(ПоляГрафика.Касса) Тогда
				Запись.БанковскийСчетКасса = ПоляГрафика.Касса;
			ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.БанковскийСчет) Тогда
				Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчет;
			ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.БанковскийСчетОрганизации) Тогда
				Запись.БанковскийСчетКасса = ПоляГрафика.БанковскийСчетОрганизации;
			Иначе
				Запись.БанковскийСчетКасса = Неопределено;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ПоляГрафика.СтатьяДвиженияДенежныхСредств) Тогда
				Если ЗначениеЗаполнено(ПоляГрафика.ДоговорСтатьяДвиженияДенежныхСредств) Тогда
					Запись.СтатьяДвиженияДенежныхСредств = ПоляГрафика.ДоговорСтатьяДвиженияДенежныхСредств;
				ИначеЕсли ЗначениеЗаполнено(ПоляГрафика.СоглашениеСтатьяДвиженияДенежныхСредств) Тогда
					Запись.СтатьяДвиженияДенежныхСредств = ПоляГрафика.СоглашениеСтатьяДвиженияДенежныхСредств;
				Иначе
					Запись.СтатьяДвиженияДенежныхСредств =
						Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ПоляГрафика.ХозяйственнаяОперация);
				КонецЕсли;
			КонецЕсли;
			
			Запись.Ответственный = ПоляГрафика.Менеджер;
			Запись.ТипОбъектаОплаты = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипОбъектаОплаты);
		КонецЦикла;
		Выборка.Сбросить();
		
		Попытка
			Если Очередь <> Неопределено Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				НаборЗаписей.Записать();
			КонецЕсли;
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось записать график платежей по: %Ссылка% по причине: %Причина%';uk='Не вдалося записати графік платежів по: %Ссылка% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ОбъектОплаты);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,
				ОбъектОплаты, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСКлиентами22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказКлиента КАК ЗаказКлиента,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			УдалитьЗаказКлиента В (&ОбъектыОплаты)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.УдалитьЗаказКлиента КАК ЗаказКлиента,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеПриход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(,,
	|			День,
	|			(УдалитьЗаказКлиента, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ЗаказКлиента,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ЗаказКлиента КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ЗаказКлиента = ГрафикОплаты.ЗаказКлиента
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ЗаказКлиента = ГрафикОплаты.ЗаказКлиента
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ЗаказКлиента,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.Ссылка КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамКлиентам22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказКлиента КАК ЗаказКлиента,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(,
	|			УдалитьЗаказКлиента В (&ОбъектыОплаты)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.УдалитьЗаказКлиента КАК ЗаказКлиента,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеРасход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(,,
	|			День,
	|			(УдалитьЗаказКлиента, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ЗаказКлиента,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ЗаказКлиента КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ЗаказКлиента = ГрафикОплаты.ЗаказКлиента
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ЗаказКлиента = ГрафикОплаты.ЗаказКлиента
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ЗаказКлиента,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.Ссылка КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура РассчитатьГрафикПлатежейПоРасчетамСПоставщиками22(ОбъектыОплаты, Очередь = Неопределено) Экспорт
	
	ОбъектыРасчетов = Новый Массив;
	Возвраты = Новый Массив;
	
	Для каждого ОбъектОплаты Из ОбъектыОплаты Цикл
		Если ТипЗнч(ОбъектОплаты) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
			Возвраты.Добавить(ОбъектОплаты);
		Иначе
			ОбъектыРасчетов.Добавить(ОбъектОплаты);
		КонецЕсли;
	КонецЦикла;
	
	Если ОбъектыРасчетов.Количество() Тогда
		ЗаписатьГрафикПлатежейПоРасчетам22(
			ОбъектыОплаты,
			ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщиками22(),
			Перечисления.ТипыДвиженияДенежныхСредств.Списание,
			Перечисления.ОбластиПланированияПлатежей.РасчетыСПоставщиками,
			Очередь);
	КонецЕсли;
	
	Если Возвраты.Количество() Тогда
		ЗаписатьГрафикПлатежейПоРасчетам22(
			Возвраты,
			ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиков22(),
			Перечисления.ТипыДвиженияДенежныхСредств.Поступление,
			Перечисления.ОбластиПланированияПлатежей.ВозвратыОтПоставщиков,
			Очередь);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаГрафикПлатежейПоРасчетамСПоставщиками22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказПоставщику КАК ЗаказПоставщику,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	-(Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток) КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			УдалитьЗаказПоставщику В (&ОбъектыОплаты)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказПоставщику,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.УдалитьЗаказПоставщику КАК ЗаказПоставщику,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеРасход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(,,
	|			День,
	|			(УдалитьЗаказПоставщику, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ЗаказПоставщику,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказПоставщику,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ЗаказПоставщику КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ЗаказПоставщику = ГрафикОплаты.ЗаказПоставщику
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ЗаказПоставщику = ГрафикОплаты.ЗаказПоставщику
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ЗаказПоставщику,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.Ссылка КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаГрафикПлатежейПоВозвратамОтПоставщиков22()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.УдалитьЗаказПоставщику КАК ЗаказПоставщику,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток КАК КОплате
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(,
	|			УдалитьЗаказПоставщику В (&ОбъектыОплаты)
	|		) КАК Расчеты
	|
	|ГДЕ
	|	Расчеты.КОплатеОстаток - Расчеты.ОплачиваетсяОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказПоставщику,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.УдалитьЗаказПоставщику КАК ЗаказПоставщику,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.КОплатеПриход КАК КОплате
	|ПОМЕСТИТЬ ГрафикОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(,,
	|			День,
	|			(УдалитьЗаказПоставщику, АналитикаУчетаПоПартнерам, Валюта) В 
	|			(ВЫБРАТЬ
	|				Остатки.ЗаказПоставщику,
	|				Остатки.АналитикаУчетаПоПартнерам,
	|				Остатки.Валюта
	|			ИЗ
	|				Остатки КАК Остатки)
	|		) КАК Расчеты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказПоставщику,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.Период КАК ДатаПлатежа,
	|	ГрафикОплаты.ЗаказПоставщику КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(Остатки.КОплате) - МАКСИМУМ(ГрафикОплаты.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|		ТОГДА
	|			МАКСИМУМ(ГрафикОплаты.КОплате)
	|		ИНАЧЕ
	|			МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0))
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ГрафикОплатыРассчитанный
	|ИЗ
	|	ГрафикОплаты КАК ГрафикОплаты
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ГрафикОплаты КАК ГрафикОплатыИтог
	|	ПО
	|		ГрафикОплатыИтог.ЗаказПоставщику = ГрафикОплаты.ЗаказПоставщику
	|		И ГрафикОплатыИтог.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И ГрафикОплатыИтог.Валюта = ГрафикОплаты.Валюта
	|		И ГрафикОплатыИтог.Период > ГрафикОплаты.Период
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Остатки КАК Остатки
	|	ПО
	|		Остатки.ЗаказПоставщику = ГрафикОплаты.ЗаказПоставщику
	|		И Остатки.АналитикаУчетаПоПартнерам = ГрафикОплаты.АналитикаУчетаПоПартнерам
	|		И Остатки.Валюта = ГрафикОплаты.Валюта
	|		
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.Период,
	|	ГрафикОплаты.ЗаказПоставщику,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам,
	|	ГрафикОплаты.Валюта
	|	
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Остатки.КОплате) - СУММА(ЕСТЬNULL(ГрафикОплатыИтог.КОплате, 0)) > 0
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ГрафикОплаты.Ссылка КАК Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент КАК ПлательщикПолучатель,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ГрафикОплаты.Валюта КАК Валюта,
	|	СУММА(ГрафикОплаты.Сумма) КАК Сумма
	|ИЗ
	|	ГрафикОплатыРассчитанный КАК ГрафикОплаты
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплаты.ДатаПлатежа,
	|	ГрафикОплаты.Ссылка,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ГрафикОплаты.АналитикаУчетаПоПартнерам.Организация,
	|	ГрафикОплаты.Валюта
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти

#КонецЕсли