#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Операция.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
		|			ТОГДА Операция.РабочийЦентр.Участок
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка)
		|	КОНЕЦ КАК Участок,
		|	ВЫБОР
		|		КОГДА Операция.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ""оп.""
		|		ИНАЧЕ Операция.ЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	Операция.Описание КАК Описание
		|ИЗ
		|	Справочник.ТехнологическиеОперации КАК Операция
		|ГДЕ
		|	Операция.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Запись.Операция);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		
		Участок = Результат.Участок;
		
		Элементы.Участок.Видимость = ЗначениеЗаполнено(Участок);
		
		ЕдиницаИзмеренияПредставление = Результат.ЕдиницаИзмерения;
		
		Элементы.ОперацияОписание.Видимость = ЗначениеЗаполнено(Результат.Описание);
		
	КонецЕсли;
	
	ВремяОбщее = Запись.Количество * Запись.ВремяШтучное + Запись.ВремяПЗ;
	
	Файлы = Новый Массив;
	
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Запись.Операция, Файлы);
	
	Элементы.ПрисоединенныеФайлы.Видимость = Файлы.Количество()>0;
	Элементы.ПрисоединенныеФайлы.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru='Дополнительные материалы (%1)';uk='Додаткові матеріали (%1)'"),
												Файлы.Количество());
КонецПроцедуры

#КонецОбласти
