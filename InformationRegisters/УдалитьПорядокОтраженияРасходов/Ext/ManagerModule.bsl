#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.300";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Процедура = "РегистрыСведений.УдалитьПорядокОтраженияРасходов.ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("774b86bf-8722-49e3-91fd-dc3ba751ce81");
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.УдалитьПорядокОтраженияРасходов";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.УдалитьПорядокОтраженияПроизводства,"
		+ "РегистрСведений.УдалитьПорядокОтраженияРасходов";
	Обработчик.БлокируемыеОбъекты = "";
	Обработчик.Комментарий = НСтр("ru='Монопольно переносит данные по настройке счетов производства из регистра УдалитьПорядокОтраженияРасходов в регистр УдалитьПорядокОтраженияПроизводства.
                                   |В дальнейшем данные из УдалитьПорядокОтраженияПроизводства отложено переносятся в регистр ПорядокОтраженияНаСчетахУчета.'
                                   |;uk='Монопольно переносить дані по настройці рахунків виробництва з регістру УдалитьПорядокОтраженияРасходов в регістр УдалитьПорядокОтраженияПроизводства.
                                   |Надалі дані з УдалитьПорядокОтраженияПроизводства відкладено переносяться в регістр ПорядокОтраженияНаСчетахУчета.'");
	
КонецПроцедуры

// Монопольно переносит данные по настройке счетов производства из регистра УдалитьПорядокОтраженияРасходов в регистр УдалитьПорядокОтраженияПроизводства
// В дальнейшем данные из УдалитьПорядокОтраженияПроизводства отложено переносятся в регистр ПорядокОтраженияНаСчетахУчета 
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Организация 		КАК Организация,
	|	Данные.Подразделение 	КАК Подразделение,
	|	Данные.Подразделение 	КАК МестоПроизводства,
	|	Данные.СчетУчета 		КАК СчетУчета, 
	|	Данные.СтатьяРасходов 	КАК СтатьяРасходов
	|ИЗ
	|	РегистрСведений.УдалитьПорядокОтраженияРасходов КАК Данные
	|ГДЕ
	|	Данные.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И Данные.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|";
		
	МетаданныеРегистра = Метаданные.РегистрыСведений.УдалитьПорядокОтраженияРасходов;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписейПорядокОтраженияПроизводства = РегистрыСведений.УдалитьПорядокОтраженияПроизводства.СоздатьНаборЗаписей();
			НаборЗаписейПорядокОтраженияПроизводства.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписейПорядокОтраженияПроизводства.Отбор.МестоПроизводства.Установить(Выборка.МестоПроизводства);
			СтрокаПорядокОтраженияПроизводства = НаборЗаписейПорядокОтраженияПроизводства.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПорядокОтраженияПроизводства, Выборка);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейПорядокОтраженияПроизводства);
			
			НаборЗаписейПорядокОтраженияРасходов = РегистрыСведений.УдалитьПорядокОтраженияРасходов.СоздатьНаборЗаписей();
			НаборЗаписейПорядокОтраженияРасходов.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписейПорядокОтраженияРасходов.Отбор.Подразделение.Установить(Выборка.Подразделение);
			НаборЗаписейПорядокОтраженияРасходов.Отбор.СтатьяРасходов.Установить(Выборка.СтатьяРасходов);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейПорядокОтраженияРасходов);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра%, по организации %Организация% и месту производства %МестоПроизводства%, по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра%, по організації %Организация% та місцю виробництва %МестоПроизводства%, по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Организация%", Выборка.Организация);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%МестоПроизводства%", Выборка.МестоПроизводства);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", "РегистрСведений.УдалитьПорядокОтраженияПроизводства");
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеРегистра,
				,
				ТекстСообщения
			);
			
		КонецПопытки;
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
