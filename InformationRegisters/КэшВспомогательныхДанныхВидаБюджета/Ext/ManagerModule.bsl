#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Кэширует настройки вида бюджета путем записи в регистр сведений
// 
// Параметры:
//	ВидБюджета - СправочникСсылка.ВидыБюджетов - вид бюджета, настройки которого требуется записать в кэш
//	ВидКэша - ПеречислениеСсылка.ВидыКэшаБюджета - вид кэша бюджета
//	Значение - Булево, ХранилищеЗначения - для видов кэша РезультатПроверки и ИспользуетГраницуФактическихДанных принимает значение Булево
//	РежимФормирования (опционально) - ПеречислениеСсылка.РежимыФормированияБюджетныхОтчетов - режимы для бюджетных отчетов.
//
Процедура УстановитьЗначениеКэша(ВидБюджета, ВидКэша, Значение, РежимФормирования = Неопределено) Экспорт
	
	Запись = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьМенеджерЗаписи();
	
	Запись.ВидБюджета = ВидБюджета;
	Запись.ВидКэша = ВидКэша;
	Запись.РежимФормирования = РежимФормирования;
	
	Если ВидыКэшаТипБулево().Найти(ВидКэша) = Неопределено Тогда
		Запись.ЗначениеДвоичныеДанные = Новый ХранилищеЗначения(Значение);
	Иначе
		Запись.ЗначениеБулево = Значение;
	КонецЕсли;
	
	Запись.Записать();
	
КонецПроцедуры

// Возвращает сохраненные в кэше настройки
// 
// Параметры:
//	ВидБюджета - СправочникСсылка.ВидыБюджетов - вид бюджета, настройки которого требуется записать в кэш
//	ВидКэша - ПеречислениеСсылка.ВидыКэшаБюджета - вид кэша бюджета
//	РежимФормирования - ПеречислениеСсылка.РежимыФормированияБюджетныхОтчетов - (опционально) режимы для бюджетных отчетов.
//
// Возвращаемое значение:
//	Булево, Структура, Произвольный - для видов кэша РезультатПроверки и ИспользуетГраницуФактическихДанных принимает значение Булево.
// 
Функция ПолучитьЗначениеКэша(ВидБюджета, ВидКэша, РежимФормирования = Неопределено) Экспорт
	
	Если ВидыКэшаТипБулево().Найти(ВидКэша) = Неопределено Тогда
		ИмяРеквизита = "ЗначениеДвоичныеДанные";
	Иначе
		ИмяРеквизита = "ЗначениеБулево";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КэшВспомогательныхДанныхВидаБюджета." + ИмяРеквизита + " КАК Значение
		|ИЗ
		|	РегистрСведений.КэшВспомогательныхДанныхВидаБюджета КАК КэшВспомогательныхДанныхВидаБюджета
		|ГДЕ
		|	КэшВспомогательныхДанныхВидаБюджета.ВидБюджета = &ВидБюджета
		|	И КэшВспомогательныхДанныхВидаБюджета.ВидКэша = &ВидКэша";
		
	Если РежимФормирования <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И КэшВспомогательныхДанныхВидаБюджета.РежимФормирования = &РежимФормирования";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидБюджета", ВидБюджета);
	Запрос.УстановитьПараметр("ВидКэша", ВидКэша);
	Запрос.УстановитьПараметр("РежимФормирования", РежимФормирования);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Если ИмяРеквизита = "ЗначениеБулево" Тогда
		Возврат ВыборкаДетальныеЗаписи.Значение;
	Иначе
		Возврат ВыборкаДетальныеЗаписи.Значение.Получить();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВидыКэшаТипБулево()
	
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ВидыКэшаБюджета.РезультатПроверки);
	Массив.Добавить(Перечисления.ВидыКэшаБюджета.ИспользуетГраницуФактическихДанных);
	
	Возврат Массив;
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.ОчиститьКэшВспомогательныхДанныхВидаБюджета";
	Обработчик.Версия = "2.5.8.4";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("833e8233-8f72-4d22-824d-fc6d3a6a0b0f");
	Обработчик.Комментарий = НСтр("ru='Очищает регистр сведений ""Кэш вспомогательных данных вида бюджета"".';uk='Очищає регістр відомостей ""Кеш допоміжних даних виду бюджету"".'");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры     

Процедура ОчиститьКэшВспомогательныхДанныхВидаБюджета() Экспорт
	
	Набор = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
	Набор.Записать();	

КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#КонецЕсли