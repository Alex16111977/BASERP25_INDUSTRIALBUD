#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Увеличивает номер задания в константе.
// 
// Возвращаемое значение:
//  Число - Предыдущий номер задания.
//
Функция УвеличитьНомерЗадания() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НомерДоРасчета = 0;
	
	НачатьТранзакцию();
	Попытка
		
		НомерДоРасчета = ТекущийНомерЗадания();
		Константы.НомерЗаданияКРасчетуАмортизацииОС.Установить(НомерДоРасчета + 1);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) + " " + НСтр("ru='Значение константы';uk='Значення константи'") + " = " + НомерДоРасчета;
		ЗаписьЖурналаРегистрации(НСтр("ru='ЗакрытиеМесяца';uk='ЗакрытиеМесяца'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,ИнформацияОбОшибке);
		ВызватьИсключение;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НомерДоРасчета;
	
КонецФункции

// Возвращает период с которого требуется расчет амортизации.
//
// Параметры:
//  СписокОрганизаций	 - Массив	 - Массив организаций.
// 
// Возвращаемое значение:
//  Дата - Начало месяца на который требуется расчет амортизации.
//
Функция НачалоРасчета(СписокОрганизаций) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(Задания.Месяц), ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоРасчета
	|ИЗ
	|	РегистрСведений.ЗаданияКРасчетуАмортизацииОС КАК Задания
	|ГДЕ
	|	Задания.НомерЗадания <= &НомерЗадания
	|	И Задания.Организация В (&СписокОрганизаций)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("НомерЗадания", ТекущийНомерЗадания());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.НачалоРасчета;
	
КонецФункции

// Считывает записи регистра во временную таблицу за указанный период по отборам.
//
// Параметры:
//  НачалоПериода	 - Дата	- Начало периода выборки данных.
//  ОкончаниеПериода - Дата	- Конец периода выборки данных.
//  НомерЗадания	 - Число	- Номер задания.
//  Организации		 - Массив, СправочникСсылка.Организации	 - Фильтр по организации.
// 
// Возвращаемое значение:
//  Структура - Содержит свойства
//  * СписокПараметровРасчета - Массив - Содержит параметры расчета.
//  * ВременныеТаблицы - МенеджерВременныхТаблиц - Содержит в себе временную таблицу КэшГраниц.
//
Функция ЗаданияКРасчетуЗаМесяц(НачалоПериода, ОкончаниеПериода, НомерЗадания, Организации) Экспорт

	НачатьТранзакцию();
	Попытка
	
		ЗаблокироватьРегистрЗаданий(НомерЗадания, Организации);
	
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.Месяц,
		|	Задания.НомерПакета,
		|	Задания.Организация,
		|	Задания.НомерЗадания,
		|	Задания.Документ
		|ПОМЕСТИТЬ КэшГраниц
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуАмортизацииОС КАК Задания
		|ГДЕ
		|	Задания.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Задания.НомерЗадания <= &НомерЗадания
		|	И Задания.Организация В (&Организации)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КэшГраниц.Организация,
		|	КэшГраниц.НомерПакета
		|ИЗ
		|	КэшГраниц КАК КэшГраниц";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
		Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
		Запрос.УстановитьПараметр("Организации", Организации);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Результат = Запрос.Выполнить();
		
		ИсходныеДанные = Новый Структура();
		ИсходныеДанные.Вставить("ПакетыАмортизации", Результат.Выгрузить());
		ИсходныеДанные.Вставить("ВременныеТаблицы", Запрос.МенеджерВременныхТаблиц);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
	Возврат ИсходныеДанные;
	
КонецФункции

// Определяет необходимость расчета в заданном периоде.
//
// Параметры:
//  СписокОрганизаций	 - Массив	 - Список организаций.
//  Период				 - Дата		 - Проверяемый период.
// 
// Возвращаемое значение:
//  Булево - Истина, если требуется расчет.
//
Функция ТребуетсяРасчет(СписокОрганизаций, Период) Экспорт

	Если НЕ ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ВнеоборотныеАктивыЛокализация.ТекстЗапросаТребуетсяРасчетАмортизацииОС(Период);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&Период, Организация В (&Организация)) КАК НачислениеАмортизации
		|ГДЕ
		|	НачислениеАмортизации.НачислятьАмортизациюУУ";
	
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", СписокОрганизаций);
	Если Константы.ПорядокУчетаВНАВУпрУчете.Получить() = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Иначе	
		Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ РезультатЗапроса.Пустой();

КонецФункции
 
// Фиксирует удачный пересчет данных.
//
// Параметры:
//  Организация		 - СправочникСсылка.Организации, Массив	 - Организация для которой выполнялся расчет.
//  НачалоРасчета	 - Дата									 - Начало периода в котором выполнялся расчет.
//  НомерЗадания	 - Число								 - Номер задания, полученный до начала расчета.
//  ЗаданияКРасчету	 - Структура							 - Формируется в методе ЗаданияКРасчетуЗаМесяц().
//
Процедура ЗафиксироватьРасчет(Организация, НачалоРасчета, НомерЗадания, ВыполненныеЗадания) Экспорт

	НачатьТранзакцию();
	
	Попытка
	
		ЗаблокироватьРегистрЗаданий(НомерЗадания, Организация);
	
		УстановитьПривилегированныйРежим(Истина);
		
		// Очистка записей по которым был выполнен расчет.
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задания.Месяц КАК Месяц,
		|	Задания.НомерПакета КАК НомерПакета,
		|	Задания.Организация КАК Организация,
		|	Задания.НомерЗадания КАК НомерЗадания
		|ИЗ
		|	КэшГраниц КАК КэшГраниц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуАмортизацииОС КАК Задания
		|		ПО (Задания.Месяц = КэшГраниц.Месяц)
		|			И (Задания.Организация = КэшГраниц.Организация)
		|			И (Задания.НомерЗадания <= &НомерЗадания)";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ВыполненныеЗадания.ВременныеТаблицы;
		Запрос.УстановитьПараметр("НомерЗадания", ТекущийНомерЗадания());
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Набор = РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.СоздатьНаборЗаписей();
			Набор.Отбор.Месяц.Установить(Выборка.Месяц);
			Набор.Отбор.НомерПакета.Установить(Выборка.НомерПакета);
			Набор.Отбор.Организация.Установить(Выборка.Организация);
			Набор.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
			Набор.Записать(); 
		КонецЦикла;
		
		// Добавление записей на следующий период
		ЗаданияНаСледующийПериод = ЗаданияНаСледующийПериод(Организация, КонецМесяца(НачалоРасчета) + 1);
		Если ЗаданияНаСледующийПериод <> Неопределено Тогда
			Для каждого Выборка Из ЗаданияНаСледующийПериод Цикл
				НоваяЗапись = РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.НомерЗадания = НомерЗадания;
				НоваяЗапись.Записать(Истина);
			КонецЦикла;
		КонецЕсли; 
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки; 
	
КонецПроцедуры

// Метод создает записи регистра с параметрами, полученными запросом.
//
// Параметры:
//	Выборка - ВыборкаИзРезультатаЗапроса - выборка, содержащая данные для формирования записей.
//  НомерЗадания - Число - номер задания; если не задано, то будет установлено значение из соответствующей константы.
//
Процедура СоздатьЗаписиРегистраПоДаннымВыборки(Выборка, НомерЗадания = Неопределено) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		СтруктураПолей = Новый Структура("Месяц, Организация, НомерПакета, Документ");
		
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ТекущийНомерЗадания();
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПолей, Выборка);
			
			СоздатьЗаписьРегистра(
				СтруктураПолей.Месяц, 
				СтруктураПолей.Документ, 
				СтруктураПолей.Организация, 
				СтруктураПолей.НомерПакета, 
				НомерЗадания);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки    = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось сформировать задание к расчету амортизации ОС за %1 в организации %2 по причине: %3';uk='Не вдалося сформувати завдання до розрахунку амортизації ОЗ за %1 в організації %2 по причині: %3'"),
			Выборка.Месяц,
			Выборка.Организация,
			ТекстОшибки);
			
		ЗаписьЖурналаРегистрации(НСтр("ru='Закрытие месяца';uk='Закриття місяця'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

// Добавляет новое задание.
//
// Параметры:
//  ПериодЗадания	 - Дата - Месяц в котором добавляется задание.
//  ДокументЗадания	 - ДокументСсылка - Документ, который изменение которого привело к созданию задания.
//  Организация		 - СправочникСсылка.Организации - Организация, для которой добавляется задание.
//  НомерПакета		 - Число - Номер пакета основных средств.
//  НомерЗадания	 - Число - Номер задания.
//
Процедура СоздатьЗаписьРегистра(ПериодЗадания, ДокументЗадания, Организация, НомерПакета, НомерЗадания = Неопределено) Экспорт
				
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	// Запишем задания
	НачатьТранзакцию();
	Попытка
	
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ТекущийНомерЗадания();
		КонецЕсли; 
		
		НаборЗаписей = РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.СоздатьМенеджерЗаписи();
		НаборЗаписей.Месяц        = НачалоМесяца(ПериодЗадания);
		НаборЗаписей.НомерПакета  = НомерПакета;
		НаборЗаписей.Организация  = Организация;
		НаборЗаписей.Документ     = ДокументЗадания;
		НаборЗаписей.НомерЗадания = НомерЗадания;
		НаборЗаписей.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрСведений.НаработкиОбъектовЭксплуатации", "Период", "РегламентныеОперации");
	
	ВнеоборотныеАктивыЛокализация.ДополнитьОписаниеРегистровОСДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекущийНомерЗадания()
	УстановитьПривилегированныйРежим(Истина);
	ТекущийНомерЗадания = Константы.НомерЗаданияКРасчетуАмортизацииОС.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат ТекущийНомерЗадания;
КонецФункции

Процедура ЗаблокироватьРегистрЗаданий(НомерЗаданияДоРасчета, Организации)
	
	ВнеоборотныеАктивы.ЗаблокироватьРегистрЗаданий(
		НомерЗаданияДоРасчета, Организации, "РегистрСведений.ЗаданияКРасчетуАмортизацииОС");
	
КонецПроцедуры

Функция ЗаданияНаСледующийПериод(СписокОрганизаций, НачалоПериода)

	ТекстЗапроса = ВнеоборотныеАктивыЛокализация.ТекстЗапросаЗаданияКРасчетуАмортизацииОСНаСледующийПериод(НачалоПериода);
	
	Если ТекстЗапроса = Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НачислениеАмортизации.Организация КАК Организация,
		|	НачислениеАмортизации.НомерПакета КАК НомерПакета,
		|	ЕСТЬNULL(МИНИМУМ(НачислениеАмортизации.Период), ДАТАВРЕМЯ(1, 1, 1)) КАК Месяц
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачислениеАмортизации.Организация КАК Организация,
		|		ЕСТЬNULL(ПакетыАмортизации.НомерПакета, 0) КАК НомерПакета,
		|		&Период КАК Период
		|	ИЗ
		|		РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&Период, Организация В (&Организация)) КАК НачислениеАмортизации
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
		|			ПО (ПакетыАмортизации.Организация = НачислениеАмортизации.Организация)
		|				И (ПакетыАмортизации.ОсновноеСредство = НачислениеАмортизации.ОсновноеСредство)
		|	ГДЕ
		|		НачислениеАмортизации.НачислятьАмортизациюУУ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НачислениеАмортизации.Организация,
		|		ЕСТЬNULL(ПакетыАмортизации.НомерПакета, 0),
		|		ЕСТЬNULL(МИНИМУМ(ВЫБОР ПорядокУчетаВНАВУпрУчете.Значение
		|					КОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
		|						ТОГДА НачислениеАмортизации.Период
		|					ИНАЧЕ ДОБАВИТЬКДАТЕ(НачислениеАмортизации.Период, МЕСЯЦ, 1)
		|				КОНЕЦ), НЕОПРЕДЕЛЕНО)
		|	ИЗ
		|		РегистрСведений.ПорядокУчетаОСУУ КАК НачислениеАмортизации
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
		|			ПО (ПакетыАмортизации.Организация = НачислениеАмортизации.Организация)
		|				И (ПакетыАмортизации.ОсновноеСредство = НачислениеАмортизации.ОсновноеСредство)
		|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
		|			ПО (ИСТИНА)
		|	ГДЕ
		|		НачислениеАмортизации.НачислятьАмортизациюУУ
		|		И НачислениеАмортизации.Организация В(&Организация)
		|		И НачислениеАмортизации.Период >= &Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НачислениеАмортизации.Организация,
		|		ЕСТЬNULL(ПакетыАмортизации.НомерПакета, 0)) КАК НачислениеАмортизации
		|
		|СГРУППИРОВАТЬ ПО
		|	НачислениеАмортизации.Организация,
		|	НачислениеАмортизации.НомерПакета";
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", НачалоПериода);
	Запрос.УстановитьПараметр("Организация", СписокОрганизаций);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции


//++ НЕ УТ

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("128e7cd7-406c-4dbd-b49b-4594a72212bf");
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.УдалитьНачислениеАмортизацииОСБухгалтерскийУчет,"
		+ "Документ.АмортизацияОС"; 
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ЗаданияКРасчетуАмортизацииОС";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ЗаданияКРасчетуАмортизацииОС";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр ""Задания к расчету амортизации ОС"":
                                   |- добавляет задания к расчету амортизации ОС.
                                   |Пока обработчик не завершен состояние выполнения операций закрытия месяца может быть некорректным.'
                                   |;uk='Заповнює регістр ""Завдання для розрахунку амортизації ОЗ"":
                                   |- додає завдання для розрахунку амортизації ОЗ.
                                   |Поки обробник не завершено, стан виконання операцій закриття місяця може бути некоректним.'");
	
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Возврат; // Регистрация не требуется
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ЕстьОшибки = Ложь;
	
	#Область АмортизацияОС
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(АмортизацияОС.Дата, МЕСЯЦ, 1), МЕСЯЦ)) КАК Дата,
	|	АмортизацияОС.Ссылка, 
	|	АмортизацияОС.Организация
	|ИЗ
	|	Документ.АмортизацияОС КАК АмортизацияОС
	|ГДЕ
	|	АмортизацияОС.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	АмортизацияОС.Ссылка, АмортизацияОС.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ТребуетсяАмортизацияОС21(Выборка.Организация, Выборка.Дата) Тогда	
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = РегистрыСведений.ЗаданияКРасчетуАмортизацииОС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			
			НоваяЗапись.Месяц 			= Выборка.Дата;
			НоваяЗапись.НомерПакета 	= 0;
			НоваяЗапись.Организация 	= Выборка.Организация;
			НоваяЗапись.Документ     	= Выборка.Ссылка;
			НоваяЗапись.НомерЗадания 	= ТекущийНомерЗадания();
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось сформировать задание к расчету амортизации ОС по причине: %Причина%';uk='Не вдалося сформувати завдання до розрахунку амортизації ОЗ по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЗаданияКЗакрытиюМесяца,
				,
				ТекстСообщения
			);
			
			ЕстьОшибки = Истина;
			
		КонецПопытки;
			
	КонецЦикла;
	
	#КонецОбласти
		
	Параметры.ОбработкаЗавершена = НЕ ЕстьОшибки;
	
КонецПроцедуры

// Определяет необходимость расчета амортизации ОС в указанном периоде по данным еще не перенесенных из 2.1
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации, Массив - Организация для которой требуется определить необходимость расчета амортизации
//  Период		 - Дата - Период в котором нужно проверить необходимость расчета
// 
// Возвращаемое значение:
//  Булево - Истина, если требуется расчет амортизации ОС
//
Функция ТребуетсяАмортизацияОС21(Организация, Период) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	РегистрСведений.УдалитьНачислениеАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				ИЛИ &ПоВсемОрганизациям) КАК НачислениеАмортизации
	|ГДЕ
	|	НачислениеАмортизации.НачислятьАмортизацию";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ Результат.Пустой()

КонецФункции

#КонецОбласти

//-- НЕ УТ


#КонецОбласти

#КонецЕсли