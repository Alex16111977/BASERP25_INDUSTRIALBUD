#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		// Текущее состояние набора помещается во временную таблицу,
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор,
		|	СуммыДокументовВВалютахУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДС КАК СуммаБезНДС,
		|	СуммыДокументовВВалютахУчета.СуммаНДС КАК СуммаНДС,
		|	СуммыДокументовВВалютахУчета.СуммаНДСПропорционально КАК СуммаНДСПропорционально,
		|	СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	НАЧАЛОПЕРИОДА(СуммыДокументовВВалютахУчета.Период, МЕСЯЦ) КАК Месяц,
		|	СуммыДокументовВВалютахУчета.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаПередЗаписью
		|ИЗ
		|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|ГДЕ
		|	СуммыДокументовВВалютахУчета.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|	И НЕ &ОбменДанными
		|");
		
		Запрос.УстановитьПараметр("Регистратор",  Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ЭтоНовый",     ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
		Запрос.УстановитьПараметр("ОбменДанными", ОбменДанными.Загрузка);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе

		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор,
		|	СуммыДокументовВВалютахУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СуммыДокументовВВалютахУчета.ОбъектРасчетов КАК ОбъектРасчетов,
		|	СуммыДокументовВВалютахУчета.СуммаНДСПропорционально КАК СуммаНДСПропорционально,
		|	НАЧАЛОПЕРИОДА(СуммыДокументовВВалютахУчета.Период, МЕСЯЦ) КАК Месяц,
		|	СуммыДокументовВВалютахУчета.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаПередЗаписьюОфлайн
		|ИЗ
		|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|ГДЕ
		|	СуммыДокументовВВалютахУчета.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|	И НЕ &ОбменДанными
		|");
		
		Запрос.УстановитьПараметр("Регистратор",  Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ЭтоНовый",     ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
		Запрос.УстановитьПараметр("ОбменДанными", ОбменДанными.Загрузка);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		
		МассивТекстовЗапроса = Новый Массив;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Месяц КАК Месяц,
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.СтавкаНДС КАК СтавкаНДС,
		|	ДанныеПервичныхДокументов.Организация КАК Организация
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаИзменения
		|ИЗ
		|	(ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаБезНДС КАК СуммаБезНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДС КАК СуммаНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСПропорционально КАК СуммаНДСПропорционально,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Месяц КАК Месяц,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СтавкаНДС КАК СтавкаНДС
		|	ИЗ
		|		СуммыДокументовВВалютахУчетаПередЗаписью КАК СуммыДокументовВВалютахУчетаПередЗаписью
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчета.Регистратор,
		|		СуммыДокументовВВалютахУчета.ИдентификаторСтроки,
		|		-СуммыДокументовВВалютахУчета.СуммаБезНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСПропорционально,
		|		-СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов,
		|		НАЧАЛОПЕРИОДА(СуммыДокументовВВалютахУчета.Период, МЕСЯЦ),
		|		СуммыДокументовВВалютахУчета.СтавкаНДС
		|	ИЗ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|	ГДЕ
		|		СуммыДокументовВВалютахУчета.Регистратор = &Регистратор) КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО Таблица.Регистратор = ДанныеПервичныхДокументов.Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ИдентификаторСтроки,
		|	Таблица.Регистратор,
		|	ДанныеПервичныхДокументов.Организация,
		|	Таблица.Месяц,
		|	Таблица.СтавкаНДС
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаБезНДС) <> 0 ИЛИ
		|	СУММА(Таблица.СуммаНДС) <> 0 ИЛИ
		|	СУММА(Таблица.СуммаНДСПропорционально) <> 0 ИЛИ
		|	СУММА(Таблица.СуммаВзаиморасчетов) <> 0";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СуммыДокументовВВалютахУчетаПередЗаписью");
		
		Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		Запрос.ВыполнитьПакет();
		
	Иначе
		
		Запрос = Новый Запрос;
		
		МассивТекстовЗапроса = Новый Массив;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Месяц КАК Месяц,
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.СтавкаНДС КАК СтавкаНДС, 
		|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов, 
		|	ДанныеПервичныхДокументов.Организация КАК Организация
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаИзменения
		|ИЗ
		|	(ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ОбъектРасчетов КАК ОбъектРасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСПропорционально КАК СуммаНДСПропорционально,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Месяц КАК Месяц,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СтавкаНДС КАК СтавкаНДС
		|	ИЗ
		|		СуммыДокументовВВалютахУчетаПередЗаписьюОфлайн КАК СуммыДокументовВВалютахУчетаПередЗаписью
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчета.Регистратор,
		|		СуммыДокументовВВалютахУчета.ИдентификаторСтроки,
		|		СуммыДокументовВВалютахУчета.ОбъектРасчетов КАК ОбъектРасчетов,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСПропорционально,
		|		НАЧАЛОПЕРИОДА(СуммыДокументовВВалютахУчета.Период, МЕСЯЦ),
		|		СуммыДокументовВВалютахУчета.СтавкаНДС
		|	ИЗ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|	ГДЕ
		|		СуммыДокументовВВалютахУчета.Регистратор = &Регистратор) КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО Таблица.Регистратор = ДанныеПервичныхДокументов.Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ИдентификаторСтроки,
		|	Таблица.Регистратор,
		|	ДанныеПервичныхДокументов.Организация,
		|	Таблица.Месяц,  
		|	Таблица.ОбъектРасчетов,  
		|	Таблица.СтавкаНДС
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаНДСПропорционально) <> 0
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СуммыДокументовВВалютахУчетаПередЗаписьюОфлайн");
		
		Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		Запрос.ВыполнитьПакет(); 
		
		ТекстЗапросаЗаданий = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Месяц 						КАК Месяц,
		|	ЕСТЬNULL(АналитикаРасчетов.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
		|	Таблица.ОбъектРасчетов              КАК ОбъектРасчетов,
		|	Таблица.Документ					КАК Документ,
		|	Таблица.Организация 				КАК Организация
		|ИЗ СуммыДокументовВВалютахУчетаИзменения КАК Таблица
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаРасчетов
		|	ПО
		|		Таблица.Организация = АналитикаРасчетов.Организация
		|		И Таблица.Документ.Контрагент = АналитикаРасчетов.Контрагент
		|		И Таблица.Документ.Партнер = АналитикаРасчетов.Партнер
		|		И Таблица.Документ.Договор = АналитикаРасчетов.Договор
		|		И Таблица.ОбъектРасчетов.НаправлениеДеятельности = АналитикаРасчетов.НаправлениеДеятельности
		|";
		
		ЗапросЗаданий = Новый Запрос; 
		ЗапросЗаданий.Текст = ТекстЗапросаЗаданий;
		ЗапросЗаданий.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;

		ВыборкаЗаданий = ЗапросЗаданий.Выполнить().Выбрать();
	
		Пока ВыборкаЗаданий.Следующий() Цикл
			Задания = РегистрыСведений["ЗаданияКРаспределениюРасчетовСПоставщиками"].СоздатьМенеджерЗаписи(); 
			ЗаполнитьЗначенияСвойств(Задания, ВыборкаЗаданий);
			Задания.НомерЗадания = РегистрыСведений["ЗаданияКРаспределениюРасчетовСПоставщиками"].ПолучитьНомерЗадания();
			Задания.Записать();
		КонецЦикла;     
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
