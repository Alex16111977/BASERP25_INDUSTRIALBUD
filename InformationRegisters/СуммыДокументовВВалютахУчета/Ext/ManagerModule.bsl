#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует таблицу сумм документов в валютах регламентированного и управленческого учета.
// Сохраняет пересчитанные данные документа в регистр СуммыДокументовВВалютахУчета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос отражения документа.
// 	ТекстыЗапроса - СписокЗначений - Список текстов запроса отражения документа.
// 	Регистры - Неопределено, Структура, Строка - Список регистров для отражения.
// 	ТекстЗапросаДанныхДокументов - Строка - Запрос источников данных документов, которые используются для проведения и нуждаются в пересчете
//      * ИсточникДанных - Строка - Имя источника данных - является гранулой пересчета данных. Например, "Товары".
//      * РаспределятьОбщуюСумму - Булево - Если Истина, то выполняется пересчет общей суммы источника в валюты учета и распределение ее на строки.
//                                          Если Ложь, то выполняется построчный пересчет сумм по курсу.
//      
//      * Ссылка - ДокументСсылка - Ссылка на документ
//      * Дата - Дата - Дата документа.
//      * ВалютаДокумента - СправочникСсылка.Валюты - Валюта документа.
//      * ВалютаВзаиморасчетов - СправочникСсылка.Валюты - Валюта взаиморасчетов с клиентом или поставщиком. Обязательно указывается, если ТипРасчетов заполнен.
//      * ПериодБазыНДС - Дата - Дата, на которую выполняется определение базы начисления НДС.
//      
//      * НомерСтроки - Число - Номер строки табличной части. Если данные получаются по шапке, то 0.
//      * ИдентификаторСтроки - Строка - Уникальные идентификатор данных документа (может указываться как для строки табличной части, так и для "шапки" документа).
//      
//      * СуммаБезНДС - Число - Сумма НДС по строке в валюте документа.
//      * СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - Ставка НДС в строке табличной части или в "шапке" документа.
//      * СуммаНДС - Число - Сумма НДС по строке в валюте документа.
//      * СуммаВзаиморасчетов - Число - Сумма в валюте взаиморасчетов.
//      * СуммаНДСВзаиморасчетов - Число - Сумам НДС в валюте взаиморасчетов.
//      
//      * НеОтраженаВРасчетах - Булево - Признак того, что запись влияет на взаиморасчеты с партнерами.
//      * ОбъектРасчетов - НЕОПРЕДЕЛЕНО, СправочникСсылка.ОбъектыРасчетов - 
//                      Объект расчетов указывается, если требуется пересчет сумм в валюте регламентированного и управленческого учета с учетом курсов зачтенных авансов.
//
// Шаблон текста запроса
// 	"ВЫБРАТЬ
//	|	""ДанныеДокумента"" КАК ИсточникДанных,
//	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
//	|	ТаблицаДокумента.Ссылка КАК Ссылка,
//	|	ТаблицаДокумента.Дата КАК Дата,
//	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
//	|	ТаблицаДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
//	|	ТаблицаДокумента.Дата КАК ПериодБазыНДС,
//	|
//	|	0 КАК НомерСтроки,
//	|	"""" КАК ИдентификаторСтроки,
//	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
//	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
//	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
//	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
//	|	ТаблицаДокумента.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
//	|	
//	|	ЛОЖЬ КАК БеззалоговаяВозвратнаяТара,
//	|	ТаблицаДокумента.ОбъектРасчетов КАК ОбъектРасчетов
//	|ИЗ
//	|	<ПолноеИмяДокумента> КАК ТаблицаДокумента
//	|
//	|ГДЕ
//	|	ТаблицаДокумента.Ссылка В (&Ссылка)
//	|"
// 
Процедура СформироватьПоДаннымДокумента(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанныхДокументов) Экспорт

	Если Запрос.МенеджерВременныхТаблиц = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	КонецЕсли;
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСуммыДокументовВВалютахУчета", ТекстыЗапроса) 
		ИЛИ Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ВтСуммыДокументовВВалютахУчета") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.ИсточникДанных              КАК ИсточникДанных,
	|	ДанныеДокумента.РаспределятьОбщуюСумму      КАК РаспределятьОбщуюСумму,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Дата                        КАК Период,
	|	ДанныеДокумента.ВалютаДокумента             КАК Валюта,
	|	ДанныеДокумента.ВалютаВзаиморасчетов        КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.ПериодБазыНДС               КАК ПериодБазыНДС,
	|
	|	ДанныеДокумента.НомерСтроки                 КАК НомерСтроки,
	|	ДанныеДокумента.ИдентификаторСтроки         КАК ИдентификаторСтроки,
	|	ДанныеДокумента.СуммаБезНДС                 КАК СуммаБезНДС,
	|	ДанныеДокумента.СтавкаНДС                   КАК СтавкаНДС,
	|	ДанныеДокумента.СуммаНДС                    КАК СуммаНДС,
	|	ДанныеДокумента.СуммаВзаиморасчетов         КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.СуммаНДСВзаиморасчетов      КАК СуммаНДСВзаиморасчетов,
    |	&СуммаНДСПропорционально                    КАК СуммаНДСПропорционально,
	|	
	|	ДанныеДокумента.БеззалоговаяВозвратнаяТара  КАК БеззалоговаяВозвратнаяТара,
	|	ДанныеДокумента.ОбъектРасчетов              КАК ОбъектРасчетов
	|	
	|ПОМЕСТИТЬ ДанныеДокументаПредварительная
	|ИЗ
	|	(&ТекстЗапросаДанныхДокументов) КАК ДанныеДокумента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаДанныхДокументов", ТекстЗапросаДанныхДокументов);	
    Если СтрНайти(ВРЕГ(ТекстЗапросаДанныхДокументов), ВРЕГ("СуммаНДСПропорционально")) > 0 Тогда
        СтрокаСуммаНДСПропорционально = "ДанныеДокумента.СуммаНДСПропорционально";
    Иначе    
        СтрокаСуммаНДСПропорционально = "0";
    КонецЕсли; 
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНДСПропорционально", СтрокаСуммаНДСПропорционально);	
	ЗапросИнициализации = Новый Запрос;
	ЗапросИнициализации.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросИнициализации.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	ЗапросИнициализации.УстановитьПараметр("ВалютаУправленческогоУчета",     ВалютаУправленческогоУчета);
	Для каждого Параметр Из Запрос.Параметры Цикл
		ЗапросИнициализации.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	ЗапросИнициализации.Текст = ТекстЗапроса;
	ЗапросИнициализации.Выполнить();
	
	ВалютыДляПересчета = Новый Структура;
	ВалютыДляПересчета.Вставить("Регл", ВалютаРегламентированногоУчета);
	ВалютыДляПересчета.Вставить("Упр",  ВалютаУправленческогоУчета);
	ПересчитатьТаблицуВВалютыУчета(ЗапросИнициализации.МенеджерВременныхТаблиц, ВалютыДляПересчета);
    
	
	ЗапросИнициализации.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументов.ИсточникДанных                          КАК ИсточникДанных,
	|	ДанныеДокументов.РаспределятьОбщуюСумму                  КАК РаспределятьОбщуюСумму,
	|	ДанныеДокументов.Регистратор                             КАК Ссылка,
	|	ДанныеДокументов.Период                                  КАК Дата,
	|	ДанныеДокументов.Валюта                                  КАК ВалютаДокумента,
	|	ДанныеДокументов.ВалютаВзаиморасчетов                    КАК ВалютаВзаиморасчетов,
	|	ДанныеДокументов.ПериодБазыНДС                           КАК ПериодБазыНДС,
	|
	|	ДанныеДокументов.ИдентификаторСтроки                     КАК ИдентификаторСтроки,
	|	ДанныеДокументов.НомерСтроки                             КАК НомерСтроки,
	|	ДанныеДокументов.СтавкаНДС                               КАК СтавкаНДС,
	|	ДанныеДокументов.СуммаБезНДС + ДанныеДокументов.СуммаНДС КАК СуммаСНДС,
	|	ДанныеДокументов.СуммаНДС                                КАК СуммаНДС,
	|	ДанныеДокументов.СуммаБезНДС                             КАК СуммаБезНДС,
	|	ДанныеДокументов.СуммаВзаиморасчетов                     КАК СуммаВзаиморасчетов,
	|	ДанныеДокументов.СуммаНДСВзаиморасчетов                  КАК СуммаНДСВзаиморасчетов,
	|	ДанныеДокументов.СуммаНДСПропорционально                 КАК СуммаНДСПропорционально,
	|	
	|	ДанныеДокументов.БеззалоговаяВозвратнаяТара               КАК БеззалоговаяВозвратнаяТара,
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.БеззалоговаяВозвратнаяТара
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДанныеДокументов.ОбъектРасчетов
	|	КОНЕЦ КАК ОбъектРасчетов,
	|	
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета 
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|				ТОГДА ДанныеДокументов.СуммаВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСУпр + ДанныеДокументов.СуммаНДСУпр
	|		КОНЕЦ) КАК СуммаСНДСУпр,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|		ВЫБОР 
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаВзаиморасчетов - ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСУпр
	|		КОНЕЦ) КАК СуммаБезНДСУпр,
	|
	|	ЕСТЬNULL(Суммы.СуммаНДСУпр,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета 
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаНДСУпр
	|		КОНЕЦ) КАК СуммаНДСУпр,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаВзаиморасчетов - ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСРегл 
	|		КОНЕЦ 
	|	) КАК СуммаБезНДСРегл,
	|
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета 
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаНДСРегл
	|		КОНЕЦ
	|	) КАК СуммаНДСРегл,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСРегл + ДанныеДокументов.СуммаНДСРегл
	|		КОНЕЦ
	|	) КАК СуммаСНДСРегл,
	|
	|	ЕСТЬNULL(Суммы.БазаНДСРегл,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.СтавкаНДС В (ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|				ТОГДА 0
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|			ТОГДА ДанныеДокументов.СуммаВзаиморасчетов - ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСРегл
	|		КОНЕЦ 
	|	) КАК БазаНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпрМУ,
	|		ВЫБОР 
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаВзаиморасчетов - ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаБезНДСУпрМУ
	|		КОНЕЦ) КАК СуммаБезНДСУпрМУ,
	|
	|	ЕСТЬNULL(Суммы.СуммаНДСУпрМУ,
	|		ВЫБОР
	|			КОГДА ДанныеДокументов.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета 
	|			      И ДанныеДокументов.Валюта <> ДанныеДокументов.ВалютаВзаиморасчетов
	|			      И ДанныеДокументов.СуммаНДСВзаиморасчетов <> 0
	|				ТОГДА ДанныеДокументов.СуммаНДСВзаиморасчетов
	|			ИНАЧЕ ДанныеДокументов.СуммаНДСУпрМУ
	|		КОНЕЦ) КАК СуммаНДСУпрМУ,
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИзРегистра
	|
	|ПОМЕСТИТЬ ВтСуммыДокументовВВалютахУчета
	|ИЗ
	|	СуммыДокументовВВалютахУчета КАК ДанныеДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		ДанныеДокументов.Регистратор = Суммы.Регистратор
	|		И ДанныеДокументов.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|		И ДанныеДокументов.ВалютаВзаиморасчетов = Суммы.ВалютаВзаиморасчетов
	|		И ДанныеДокументов.Валюта = Суммы.Валюта
	|		И ДанныеДокументов.СуммаВзаиморасчетов = Суммы.СуммаВзаиморасчетов
	|		И ДанныеДокументов.СуммаБезНДС = Суммы.СуммаБезНДС
	|		И ДанныеДокументов.СуммаНДС = Суммы.СуммаНДС
	|		И ДанныеДокументов.СуммаНДСПропорционально = Суммы.СуммаНДСПропорционально
	|		И ДанныеДокументов.Валюта = Суммы.Валюта
	|		И ДанныеДокументов.ВалютаВзаиморасчетов = Суммы.ВалютаВзаиморасчетов
	|		И ДанныеДокументов.ПериодБазыНДС = Суммы.ПериодБазыНДС
	|		И ДанныеДокументов.ОбъектРасчетов = Суммы.ОбъектРасчетов
	|		И ДанныеДокументов.БеззалоговаяВозвратнаяТара = Суммы.БеззалоговаяВозвратнаяТара
	|		И Суммы.СуммаБезНДСРегл <> 0
	|		И Суммы.СуммаБезНДСУпр <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СуммыДокументовВВалютахУчета
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	ЗапросИнициализации.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
    
	
	ИмяРегистра = "СуммыДокументовВВалютахУчета";
	
	Если ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		
		Если НЕ Запрос.Параметры.Свойство("ВалютаРегламентированногоУчета") Тогда
			Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		КонецЕсли;
		Если НЕ Запрос.Параметры.Свойство("ВалютаУправленческогоУчета") Тогда
			Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     ВалютаУправленческогоУчета);
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Суммы.Ссылка                    КАК Регистратор,
		|	Суммы.Дата                      КАК Период,
		|	
		|	Суммы.ИдентификаторСтроки       КАК ИдентификаторСтроки,
		|	
		|	Суммы.СуммаБезНДС               КАК СуммаБезНДС,
		|	Суммы.СуммаНДС                  КАК СуммаНДС,
		|	
		|	Суммы.СуммаБезНДСРегл           КАК СуммаБезНДСРегл,
		|	Суммы.БазаНДСРегл               КАК БазаНДСРегл,
		|	Суммы.СуммаНДСРегл              КАК СуммаНДСРегл,
		|	Суммы.СуммаБезНДСУпр            КАК СуммаБезНДСУпр,
		|	Суммы.СуммаНДСУпр               КАК СуммаНДСУпр,
		|	Суммы.СуммаБезНДСУпрМУ          КАК СуммаБезНДСУпрМУ,
		|	Суммы.СуммаНДСУпрМУ             КАК СуммаНДСУпрМУ,
		|	Суммы.СуммаВзаиморасчетов       КАК СуммаВзаиморасчетов,
		|	Суммы.СуммаНДСПропорционально	КАК СуммаНДСПропорционально,
		|	
		|	Суммы.ВалютаДокумента           КАК Валюта,
		|	Суммы.ВалютаВзаиморасчетов      КАК ВалютаВзаиморасчетов,
		|	Суммы.СтавкаНДС                 КАК СтавкаНДС,
		|	
		|	Суммы.БеззалоговаяВозвратнаяТара  КАК БеззалоговаяВозвратнаяТара,
		|	ЕСТЬNULL(Суммы.ОбъектРасчетов.ТипРасчетов, НЕОПРЕДЕЛЕНО) КАК ТипРасчетов,
		|	Суммы.ОбъектРасчетов             КАК ОбъектРасчетов,
		|	Суммы.ПериодБазыНДС              КАК ПериодБазыНДС
		|
		|ИЗ
		|	ВтСуммыДокументовВВалютахУчета КАК Суммы
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|	ПО
		|		Суммы.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Суммы.ИсточникДанных,
		|	Суммы.НомерСтроки";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	КонецЕсли;
	
КонецПроцедуры


// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - ФиксированнаяСтруктура - свойства документа (См. ПроведениеДокументов.СвойстваДокумента).
//
// Возвращаемое значение:
//  Структура - параметры учетного механизма (См. ПроведениеДокументов.ПараметрыУчетногоМеханизма()).
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета);
		
	КонецЕсли;      
	
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета);
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по регистру.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "СуммыДокументовВВалютахУчета");
	
КонецПроцедуры

#КонецОбласти

// Синхронизация регл.сумм в учетных регистрах по данным СуммыДокументовВВалютахУчета
// Параметры:
// 	РегистраторыСумм - ДокументСсылка, Массив - одиночный ДокументСсылка или массив типов ДокументСсылка.
Процедура ОбновитьДвиженияДокументов(РегистраторыСумм) Экспорт
	
	СоставОбновления = Новый Структура();
    
	
	// регистры партий к синхронизации регл.сумм
	СоставОбновления.Вставить("ПрочиеДоходы", РегистрыНакопления.ПрочиеДоходы);
	СоставОбновления.Вставить("ПрочиеРасходы", РегистрыНакопления.ПрочиеРасходы);
	СоставОбновления.Вставить("ПартииПрочихРасходов", РегистрыНакопления.ПартииПрочихРасходов);
	СоставОбновления.Вставить("ПрочаяВыручка", РегистрыНакопления.ПрочаяВыручка);
	// регистры себестоимости к синхронизации регл.сумм
	СоставОбновления.Вставить("СебестоимостьТоваров", РегистрыНакопления.СебестоимостьТоваров);
	СоставОбновления.Вставить("ВыручкаИСебестоимостьПродаж", РегистрыНакопления.ВыручкаИСебестоимостьПродаж);
	СоставОбновления.Вставить("Закупки", РегистрыНакопления.Закупки);
	СоставОбновления.Вставить("ДвиженияКонтрагентДоходыРасходы", РегистрыНакопления.ДвиженияКонтрагентДоходыРасходы);
	СоставОбновления.Вставить("ДвиженияКонтрагентКонтрагент", РегистрыНакопления.ДвиженияКонтрагентКонтрагент);
	
	СоставОбновления.Вставить("ТоварыКОформлениюОтчетовКомитенту", РегистрыНакопления.ТоварыКОформлениюОтчетовКомитенту);
	
	РегистрыПартий = РасчетСебестоимостиЛокализация.РегистрыУчетаПартий();
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СоставОбновления, РегистрыПартий);
	
	Регистраторы = ОбщегоНазначенияУТКлиентСервер.Массив(РегистраторыСумм);
	Для Каждого Ссылка Из Регистраторы Цикл
		ОбновитьДвиженияДокумента(Ссылка, СоставОбновления);
	КонецЦикла;
	
	СоставОбновления.Удалить("ПрочиеДоходы");
	СоставОбновления.Удалить("ПрочиеРасходы");
	СоставОбновления.Удалить("ПрочаяВыручка");
	СоставОбновления.Удалить("ПартииПрочихРасходов");
	СоставОбновления.Удалить("СебестоимостьТоваров");
	СоставОбновления.Удалить("ВыручкаИСебестоимостьПродаж");
	СоставОбновления.Удалить("Закупки");
	СоставОбновления.Удалить("ДвиженияКонтрагентДоходыРасходы");
	СоставОбновления.Удалить("ДвиженияКонтрагентКонтрагент");
	
	СоставОбновления.Удалить("ТоварыКОформлениюОтчетовКомитенту");
	
	Для Каждого РегистрПартий Из РегистрыПартий Цикл
		СоставОбновления.Удалить(РегистрПартий.Ключ);
	КонецЦикла;
	
	
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Обработки.ДвиженияАктивовПассивов.ОтразитьДокументыВУправленческомБалансе(Регистраторы);
		
	КонецЕсли;// Необходимо формировать упр. баланс
	
КонецПроцедуры

// Запускает распределение сумм документов валютах регламентированного и управленческого учета, если это нужно.
// Условия пересчета:
// 1. В регистре СуммыДокументовВВалютахУчета для пересчитываемого регистратора должен быть заполнен реквизит ТипРасчетов.
// 2. Сумма строк в валюте регламентированного или управленческого учета должна отличаться от суммы движений по регистрам расчетов.
// Параметры:
//		МассивДокументов - Массив из ДокументСсылка - Массив документов, по которым нужно проверить и запустить пересчет сумм.
Процедура РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Менеджер = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = Менеджер;

	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("ЭтоПроверка", Ложь);
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.УстановитьПараметр("ПоВсемДокументам", Ложь);
	
	МассивТекстовЗапроса = Новый Массив();
	// Запрос документов, по которым суммы в валютах учета не совпадают с данными взаиморасчетов.
	МассивТекстовЗапроса.Добавить(ТекстЗапросаДокументовДляПересчета());
	// Запрос документов, по которым суммы в валютах учета вообще еще не рассчитаны.
	МассивТекстовЗапроса.Добавить(ТекстЗапросаНерасчитанныхДокументов());
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = МассивРезультатов.Количество();
	
	ТаблицаДокументовДляПересчета = МассивРезультатов[КоличествоРезультатов-2].Выгрузить();
	МассивДокументовСКлиентами = ТаблицаДокументовДляПересчета.Скопировать(
		Новый Структура("ТипРасчетов",Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом)).ВыгрузитьКолонку("РасчетныйДокумент");
	МассивДокументовСПоставщиками = ТаблицаДокументовДляПересчета.Скопировать(
		Новый Структура("ТипРасчетов",Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)).ВыгрузитьКолонку("РасчетныйДокумент");
		
	ТаблицаНерасчитанныхДокументов = МассивРезультатов[КоличествоРезультатов-1].Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		МассивДокументовСКлиентами,
		ТаблицаНерасчитанныхДокументов.Скопировать(
			Новый Структура("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом)).ВыгрузитьКолонку("Регистратор"));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		МассивДокументовСПоставщиками,
		ТаблицаНерасчитанныхДокументов.Скопировать(
			Новый Структура("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)).ВыгрузитьКолонку("Регистратор"));
			
	Если МассивДокументовСКлиентами.Количество() > 0 Тогда
		РассчитатьПоДаннымВзаиморасчетов("РасчетыСКлиентами", МассивДокументовСКлиентами);
	КонецЕсли;
	Если МассивДокументовСПоставщиками.Количество() > 0 Тогда
		РассчитатьПоДаннымВзаиморасчетов("РасчетыСПоставщиками", МассивДокументовСПоставщиками);
	КонецЕсли;
	
	ОбновитьДвиженияПоОборотнымРегистрам(МассивДокументовСКлиентами, МассивДокументовСПоставщиками);
	
КонецПроцедуры

// Обновление движений по регистрам "Движения Контрагент - Доходы/Расходы", 
// "Движения Денежные средства - Контрагент" по клиентам и по поставщикам" и "Активы и пассивы" 
//
// Параметры:
// 	МассивДокументов - Неопределено, Массив - Документы, по которым нужно обновить движения.
Процедура ОбновитьДвиженияПоОборотнымРегистрамНепересчитываемыхДокументов(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СуммыДокументовВВалютахУчета.Регистратор КАК РасчетныйДокумент
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	СуммыДокументовВВалютахУчета.Регистратор В (&МассивДокументов)
	|	И СуммыДокументовВВалютахУчета.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)";
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	МассивДокументовКПересчету =  Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("РасчетныйДокумент");
	
	ОбновитьДвиженияПоОборотнымРегистрам(МассивДокументовКПересчету, МассивДокументовКПересчету);
	
КонецПроцедуры

// Выполняет распределение сумм документов валютах регламентированного и управленческого учета по данным взаиморасчетов.
// 
// Параметры:
// 	ТипРасчетов - Строка - Тип расчетов, по данным которых необходимо рассчитать суммы.
// 	                       Может принимать значения "РасчетыСКлиентами" или "РасчетыСПоставшиками".
// 	МассивДокументов - Неопределено, Массив - Документы, по которым нужно рассчитать суммы.
// 	                                          Если Неопределено, то расчет выполняется по всем документам.
//
Процедура РассчитатьПоДаннымВзаиморасчетов(ТипРасчетов, МассивДокументов) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ТипРасчетов = &ТипРасчетов
	|	И (ДанныеРегистра.Валюта <> &ВалютаРегламентированногоУчета 
	|		ИЛИ ДанныеРегистра.Валюта <> &ВалютаУправленческогоУчета
	|		ИЛИ ДанныеРегистра.Валюта <> ДанныеРегистра.ВалютаВзаиморасчетов)
	|	И ДанныеРегистра.Регистратор В (&МассивДокументов)
	|;
	|
	|///////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка,
	|	ДанныеРегистра.Активность КАК Активность,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	ДанныеРегистра.СуммаБезНДС          КАК СуммаБезНДС,
	|	ДанныеРегистра.СуммаНДС             КАК СуммаНДС,
	|	ДанныеРегистра.СуммаБезНДСРегл      КАК СуммаБезНДСРегл,
	|	ДанныеРегистра.СуммаНДСРегл         КАК СуммаНДСРегл,
	|	ДанныеРегистра.БазаНДСРегл          КАК БазаНДСРегл,
	|	ДанныеРегистра.СуммаБезНДСУпр       КАК СуммаБезНДСУпр,
	|	ДанныеРегистра.СуммаНДСУпр          КАК СуммаНДСУпр,
	|	ДанныеРегистра.СуммаНДСПропорционально  КАК СуммаНДСПропорционально,
	|	ДанныеРегистра.СуммаБезНДСУпрМУ         КАК СуммаБезНДСУпрМУ,
	|	ДанныеРегистра.СуммаНДСУпрМУ            КАК СуммаНДСУпрМУ,
	|	ДанныеРегистра.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|
	|	ДанныеРегистра.Валюта                     КАК Валюта,
	|	ДанныеРегистра.ВалютаВзаиморасчетов       КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СтавкаНДС                  КАК СтавкаНДС,
	|	ДанныеРегистра.ТипРасчетов                КАК ТипРасчетов,
	|	ДанныеРегистра.ОбъектРасчетов             КАК ОбъектРасчетов,
	|	ДанныеРегистра.ПериодБазыНДС              КАК ПериодБазыНДС,
	|	ДанныеРегистра.БеззалоговаяВозвратнаяТара  КАК БеззалоговаяВозвратнаяТара,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ТипРасчетов = &ТипРасчетов
	|			И (ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС) <> 0
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПересчитатьСуммы,
	|
	|	ВЫБОР КОГДА ДанныеРегистра.ТипРасчетов = &ТипРасчетов
	|		ТОГДА ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР КОГДА ДанныеРегистра.ТипРасчетов = &ТипРасчетов
	|		ТОГДА ДанныеРегистра.СуммаВзаиморасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВзаиморасчетовДляПересчета,
	|
	|	ВЫБОР КОГДА ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС < 0
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСторно
	|
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеРегистра.Регистратор = ТаблицаДокументов.Регистратор
	|	
	|ИТОГИ
	|	МАКСИМУМ(Период),
	|	СУММА(СуммаСНДС),
	|	СУММА(СуммаБезНДСРегл),
	|	СУММА(СуммаНДСРегл),
	|	СУММА(СуммаБезНДСУпр),
	|	СУММА(СуммаНДСУпр),
	|	СУММА(СуммаНДСПропорционально),
	|	СУММА(СуммаБезНДСУпрМУ),
	|	СУММА(СуммаНДСУпрМУ),
	|	СУММА(СуммаВзаиморасчетовДляПересчета),
	|	МАКСИМУМ(ВалютаВзаиморасчетов),
	|	МАКСИМУМ(Валюта),
	|	МАКСИМУМ(ТипРасчетов),
	|	МАКСИМУМ(ПериодБазыНДС)
	|ПО
	|	ДанныеРегистра.Регистратор КАК Ссылка,
	|	ДанныеРегистра.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВЫБОР КОГДА ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС < 0
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСторно
	|;
	|
	|///////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Регистратор
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|";
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Валюты = Новый Структура("ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета, КэшКурсовВалют",
		Константы.ВалютаРегламентированногоУчета.Получить(),
		Константы.ВалютаУправленческогоУчета.Получить(),
		РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Валюты.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Валюты.ВалютаУправленческогоУчета);
	
	Запрос.УстановитьПараметр("ТипРасчетов", 
		?(ТипРасчетов = "РасчетыСКлиентами",
		Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом,
		Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатПоДокументам = МассивРезультатов[1]; // РезультатЗапроса - 
	ДокументыКПересчету   = МассивРезультатов[2].Выгрузить().ВыгрузитьКолонку("Регистратор");
	ТаблицаСуммДляРаспределения = СуммыДокументовДляПострочногоРаспределения(ДокументыКПересчету);
	ТаблицаСуммДляРаспределения.Индексы.Добавить("Регистратор, ОбъектРасчетов, ТипРасчетов");
	
	ТаблицаСуммыДокумента = РегистрыСведений.СуммыДокументовВВалютахУчета.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаСуммыДокумента.Колонки.Добавить("СуммаСНДСРегл", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	ТаблицаСуммыДокумента.Колонки.Добавить("СуммаСНДСУпр", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	ТаблицаСуммыДокумента.Колонки.Добавить("СуммаДляБазыНДС", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	
	ВыборкаПоДокументу = РезультатПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоДокументу.Следующий() Цикл
		
		Период = НачалоДня(ВыборкаПоДокументу.Период);
		ПериодБазыНДС = ?(ЗначениеЗаполнено(ВыборкаПоДокументу.ПериодБазыНДС), ВыборкаПоДокументу.ПериодБазыНДС, Период);
		
		КурсВалютыДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ВыборкаПоДокументу.Валюта, Период, Валюты.КэшКурсовВалют);
		КурсВалютыВзаиморасчетов = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ВыборкаПоДокументу.ВалютаВзаиморасчетов, Период, Валюты.КэшКурсовВалют);
		КурсВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(Валюты.ВалютаУправленческогоУчета, Период, Валюты.КэшКурсовВалют);
		
		КурсВалютыДокументаДляБазыНДС = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ВыборкаПоДокументу.Валюта, ПериодБазыНДС, Валюты.КэшКурсовВалют);
		КурсВалютыВзаиморасчетовДляБазыНДС = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ВыборкаПоДокументу.ВалютаВзаиморасчетов, ПериодБазыНДС, Валюты.КэшКурсовВалют);
				
		ВыборкаПоОбъектуРасчетов = ВыборкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоОбъектуРасчетов.Следующий() Цикл
				
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Регистратор",  ВыборкаПоДокументу.Ссылка);
			СтруктураПоиска.Вставить("ТипРасчетов",  ВыборкаПоОбъектуРасчетов.ТипРасчетов);
			СтруктураПоиска.Вставить("ОбъектРасчетов",  ВыборкаПоОбъектуРасчетов.ОбъектРасчетов);

			СуммыПоДокументу = ТаблицаСуммДляРаспределения.НайтиСтроки(СтруктураПоиска);
			
			ВыборкаПоТипуЗаписи = ВыборкаПоОбъектуРасчетов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоТипуЗаписи.Следующий() Цикл
				
				СуммаДокумента      = ВыборкаПоТипуЗаписи.СуммаСНДС;
				СуммаВзаиморасчетов = ВыборкаПоТипуЗаписи.СуммаВзаиморасчетовДляПересчета;
				
				СуммаДокументаРегл  = 0;
				СуммаДокументаУпр   = 0;
				
				Предоплата         = 0;
				ПредоплатаРегл     = 0; 
				ПредоплатаУпр      = 0;
				Долг               = 0;
				ДолгРегл           = 0;
				ДолгУпр            = 0;
				
				ОплатаВВалюте      = Ложь;
				
				Если СуммыПоДокументу.Количество() = 1 Тогда
					Если ВыборкаПоТипуЗаписи.ЭтоСторно Тогда
						ПредоплатаРегл = ?(СуммыПоДокументу[0].Предоплата <= 0, СуммыПоДокументу[0].ПредоплатаРегл, 0);
						ПредоплатаУпр  = ?(СуммыПоДокументу[0].Предоплата <= 0, СуммыПоДокументу[0].ПредоплатаУпр, 0);
						ДолгРегл       = ?(СуммыПоДокументу[0].Долг <= 0, СуммыПоДокументу[0].ДолгРегл, 0);
						ДолгУпр        = ?(СуммыПоДокументу[0].Долг <= 0, СуммыПоДокументу[0].ДолгУпр, 0);
						Предоплата     = ?(СуммыПоДокументу[0].Предоплата <= 0, СуммыПоДокументу[0].Предоплата, 0);
						Долг           = ?(СуммыПоДокументу[0].Долг <= 0, СуммыПоДокументу[0].Долг, 0);
					Иначе
						ПредоплатаРегл = ?(СуммыПоДокументу[0].Предоплата >= 0, СуммыПоДокументу[0].ПредоплатаРегл, 0);
						ПредоплатаУпр  = ?(СуммыПоДокументу[0].Предоплата >= 0, СуммыПоДокументу[0].ПредоплатаУпр, 0);
						ДолгРегл       = ?(СуммыПоДокументу[0].Долг >= 0, СуммыПоДокументу[0].ДолгРегл, 0);
						ДолгУпр        = ?(СуммыПоДокументу[0].Долг >= 0, СуммыПоДокументу[0].ДолгУпр, 0);
						Предоплата     = ?(СуммыПоДокументу[0].Предоплата >= 0, СуммыПоДокументу[0].Предоплата, 0);
						Долг           = ?(СуммыПоДокументу[0].Долг >= 0, СуммыПоДокументу[0].Долг, 0);
					КонецЕсли;
					ОплатаВВалюте  = ?(СуммыПоДокументу[0].ОплатаВВалюте = NULL, Ложь, СуммыПоДокументу[0].ОплатаВВалюте);
				КонецЕсли;
				
				Если Предоплата + Долг <> СуммаВзаиморасчетов Тогда
					СуммаДокументаРегл = ПредоплатаРегл
						+ (СуммаВзаиморасчетов - Предоплата) * КурсВалютыВзаиморасчетов;
					СуммаДокументаУпр = ПредоплатаУпр 
						+ (СуммаВзаиморасчетов - Предоплата) * КурсВалютыВзаиморасчетов / КурсВалютыУпр;
				Иначе
					СуммаДокументаРегл = ПредоплатаРегл + ДолгРегл;
					СуммаДокументаУпр = ПредоплатаУпр + ДолгУпр;
				КонецЕсли;
				
				Если ВыборкаПоТипуЗаписи.ЭтоСторно Тогда
					БазаСНДСРеглПоДокументу = ПредоплатаРегл + ДолгРегл;
				Иначе
					БазаСНДСРеглПоДокументу = ПредоплатаРегл + (СуммаВзаиморасчетов - Предоплата) * КурсВалютыВзаиморасчетовДляБазыНДС;
				КонецЕсли;
				
				УчтеноБазыРаспределения = 0;
				УжеРаспределеноРегл = 0;
				УжеРаспределеноУпр = 0;
				УжеРаспределеноСуммаДляБазыНДС = 0;
				
				КурсВалютыУпрСУчетомЗачтенныхАвансов = КурсВалютыУпр;
				Если СуммаДокументаУпр <> 0 Тогда
					КурсВалютыУпрСУчетомЗачтенныхАвансов = СуммаДокументаРегл / СуммаДокументаУпр;
				КонецЕсли;
				
				Выборка = ВыборкаПоТипуЗаписи.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					Запись = ТаблицаСуммыДокумента.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, Выборка);
					
					Если НЕ Выборка.ПересчитатьСуммы Тогда
						Продолжить;
					КонецЕсли;
					
					СуммаСНДС = Выборка.СуммаБезНДС + Выборка.СуммаНДС;
					
					Запись.СуммаСНДСРегл = Окр(СуммаДокументаРегл * (УчтеноБазыРаспределения + СуммаСНДС) / СуммаДокумента, 2) - УжеРаспределеноРегл;
					Запись.СуммаСНДСУпр  = Окр(СуммаДокументаУпр * (УчтеноБазыРаспределения + СуммаСНДС) / СуммаДокумента, 2) - УжеРаспределеноУпр;
					Запись.СуммаДляБазыНДС  = Окр(БазаСНДСРеглПоДокументу * (УчтеноБазыРаспределения + СуммаСНДС) / СуммаДокумента, 2) - УжеРаспределеноСуммаДляБазыНДС;
					
					УчтеноБазыРаспределения = УчтеноБазыРаспределения + СуммаСНДС;
					УжеРаспределеноРегл     = УжеРаспределеноРегл + Запись.СуммаСНДСРегл;
					УжеРаспределеноУпр      = УжеРаспределеноУпр + Запись.СуммаСНДСУпр;
					УжеРаспределеноСуммаДляБазыНДС = УжеРаспределеноСуммаДляБазыНДС + Запись.СуммаДляБазыНДС; 
					
					Если (ВыборкаПоДокументу.ВалютаВзаиморасчетов = Валюты.ВалютаРегламентированногоУчета
						 ИЛИ ВыборкаПоДокументу.Валюта = Валюты.ВалютаРегламентированногоУчета)
						 И ((ВыборкаПоТипуЗаписи.СуммаБезНДСРегл + ВыборкаПоТипуЗаписи.СуммаНДСРегл) = СуммаДокументаРегл) Тогда
						ПересчитыватьРегл = Ложь;
					Иначе
						ПересчитыватьРегл = Истина;
					КонецЕсли;
					
					Если (ВыборкаПоДокументу.ВалютаВзаиморасчетов = Валюты.ВалютаУправленческогоУчета
						 ИЛИ ВыборкаПоДокументу.Валюта = Валюты.ВалютаУправленческогоУчета)
						 И ((ВыборкаПоТипуЗаписи.СуммаБезНДСУпр + ВыборкаПоТипуЗаписи.СуммаНДСУпр) = СуммаДокументаУпр) Тогда
						ПересчитыватьУпр = Ложь;
					Иначе
						ПересчитыватьУпр = Истина;
					КонецЕсли;
					
                    Если ПересчитыватьРегл Тогда
						Если ЗначениеЗаполнено(Запись.СтавкаНДС) Тогда
							// Рассчитаем НДС и суммы без НДС в валюте регл. и упр. учета
							// Расчет базы НДС по данным взаиморасчетов (с учетом зачтенных предоплат)
							Если Запись.СуммаНДС = 0 Тогда 
								Запись.СуммаНДСРегл = 0;
							Иначе
								Запись.СуммаНДСРегл = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(Запись.СуммаДляБазыНДС, Запись.СтавкаНДС);
							КонецЕсли;
							Запись.БазаНДСРегл = Запись.СуммаДляБазыНДС - Запись.СуммаНДСРегл;
						КонецЕсли;
						Запись.СуммаБезНДСРегл = Запись.СуммаСНДСРегл - Запись.СуммаНДСРегл;
					КонецЕсли;
					
					Если ПересчитыватьУпр ИЛИ ПересчитыватьРегл Тогда
						Запись.СуммаНДСУпр = ?(
							КурсВалютыУпрСУчетомЗачтенныхАвансов = 0, 
								0, 
								(Запись.СуммаНДСРегл + Запись.СуммаНДСПропорционально) / КурсВалютыУпрСУчетомЗачтенныхАвансов
						);
                        Запись.СуммаБезНДСУпр  = Запись.СуммаСНДСУпр - Запись.СуммаНДСУпр;
						Запись.СуммаНДСУпрМУ = ?(
							КурсВалютыУпрСУчетомЗачтенныхАвансов = 0, 
								0, 
								Запись.СуммаНДСРегл / КурсВалютыУпрСУчетомЗачтенныхАвансов
						);
                        Запись.СуммаБезНДСУпрМУ  = Запись.СуммаСНДСУпр - Запись.СуммаНДСУпрМУ;
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		НаборЗаписей = РегистрыСведений.СуммыДокументовВВалютахУчета.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументу.Ссылка);
		НаборЗаписей.Загрузить(ТаблицаСуммыДокумента);
		НаборЗаписей.Записать();
		
		ТаблицаСуммыДокумента.Очистить();
		
	КонецЦикла;
	
	ОбновитьДвиженияДокументов(ДокументыКПересчету);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаНерасчитанныхДокументов()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ТипРасчетов КАК ТипРасчетов,
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК ДанныеРегистра
	|
	|ГДЕ
	|	(ДанныеРегистра.Валюта <> &ВалютаРеглУчета ИЛИ ДанныеРегистра.Валюта <> &ВалютаУпрУчета)
	|	И (ДанныеРегистра.Регистратор В (&МассивДокументов) ИЛИ &ПоВсемДокументам)
	|	И ((ДанныеРегистра.СуммаБезНДС <> 0 И (ДанныеРегистра.СуммаБезНДСРегл = 0 ИЛИ ДанныеРегистра.СуммаБезНДСУпр = 0))
	|			ИЛИ (ДанныеРегистра.СуммаНДС <> 0 И (ДанныеРегистра.СуммаНДСРегл = 0 ИЛИ ДанныеРегистра.СуммаНДСУпр = 0)))
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДокументовДляПересчета() Экспорт
	
	ТекстСуммыВзаиморасчетов = ТекстЗапросаСуммВзаиморасчетов("ВтСуммыВзаиморасчетов");
	
	ТекстСравнения = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ДокументРегистратор               КАК РасчетныйДокумент,
	|	ВложенныйЗапрос.ТипРасчетов                       КАК ТипРасчетов,
	|	ВложенныйЗапрос.ОбъектРасчетов                    КАК ОбъектРасчетов,
	|	СУММА(ВложенныйЗапрос.СуммаУвеличенияРеглРасчеты) КАК СуммаУвеличенияРеглРасчеты,
	|	СУММА(ВложенныйЗапрос.СуммаУвеличенияУпрРасчеты)  КАК СуммаУвеличенияУпрРасчеты,
	|	СУММА(ВложенныйЗапрос.СуммаУменьшенияРеглРасчеты) КАК СуммаУменьшенияРеглРасчеты,
	|	СУММА(ВложенныйЗапрос.СуммаУменьшенияУпрРасчеты)  КАК СуммаУменьшенияУпрРасчеты,
	|	СУММА(ВложенныйЗапрос.СуммаУвеличенияРегл)        КАК СуммаУвеличенияРегл,
	|	СУММА(ВложенныйЗапрос.СуммаУвеличенияУпр)         КАК СуммаУвеличенияУпр,
	|	СУММА(ВложенныйЗапрос.СуммаУменьшенияРегл)        КАК СуммаУменьшенияРегл,
	|	СУММА(ВложенныйЗапрос.СуммаУменьшенияУпр)         КАК СуммаУменьшенияУпр
	|ИЗ
	|	(ВЫБРАТЬ
	|		СуммыВзаиморасчетов.ТипРасчетов                    КАК ТипРасчетов,
	|		СуммыВзаиморасчетов.Регистратор                    КАК ДокументРегистратор,
	|		СуммыВзаиморасчетов.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|		ВЫБОР 
	|			КОГДА НЕ СуммыВзаиморасчетов.ВозможныРазвернутыеДвижения И СуммыВзаиморасчетов.ПредоплатаРегл + СуммыВзаиморасчетов.ДолгРегл > 0
	|				ТОГДА ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаРегл + СуммыВзаиморасчетов.ДолгРегл > 0
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаРегл + СуммыВзаиморасчетов.ДолгРегл
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаРегл > 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаРегл 
	|						ИНАЧЕ 0 
	|					КОНЕЦ 
	|				+ ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ДолгРегл > 0
	|							ТОГДА СуммыВзаиморасчетов.ДолгРегл 
	|						ИНАЧЕ 0 
	|					КОНЕЦ
	|		КОНЕЦ                                             КАК СуммаУвеличенияРеглРасчеты,
	|		ВЫБОР 
	|			КОГДА НЕ СуммыВзаиморасчетов.ВозможныРазвернутыеДвижения
	|				ТОГДА ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаУпр + СуммыВзаиморасчетов.ДолгУпр > 0
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаУпр + СуммыВзаиморасчетов.ДолгУпр
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаУпр > 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаУпр 
	|						ИНАЧЕ 0 
	|					КОНЕЦ 
	|				+ ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ДолгУпр > 0
	|							ТОГДА СуммыВзаиморасчетов.ДолгУпр 
	|						ИНАЧЕ 0 
	|					КОНЕЦ
	|		КОНЕЦ                                             КАК СуммаУвеличенияУпрРасчеты,
	|		ВЫБОР 
	|			КОГДА НЕ СуммыВзаиморасчетов.ВозможныРазвернутыеДвижения
	|				ТОГДА ВЫБОР 
	|						КОГДА  СуммыВзаиморасчетов.ПредоплатаРегл + СуммыВзаиморасчетов.ДолгРегл < 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаРегл + СуммыВзаиморасчетов.ДолгРегл
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаРегл < 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаРегл 
	|						ИНАЧЕ 0 
	|					КОНЕЦ 
	|				+ ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ДолгРегл < 0
	|							ТОГДА СуммыВзаиморасчетов.ДолгРегл 
	|						ИНАЧЕ 0 
	|					КОНЕЦ
	|		КОНЕЦ                                             КАК СуммаУменьшенияРеглРасчеты,
	|		ВЫБОР 
	|			КОГДА НЕ СуммыВзаиморасчетов.ВозможныРазвернутыеДвижения 
	|				ТОГДА ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаУпр + СуммыВзаиморасчетов.ДолгУпр < 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаУпр + СуммыВзаиморасчетов.ДолгУпр
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА СуммыВзаиморасчетов.ПредоплатаУпр < 0 
	|							ТОГДА СуммыВзаиморасчетов.ПредоплатаУпр 
	|						ИНАЧЕ 0 
	|					КОНЕЦ 
	|				+ ВЫБОР 
	|						КОГДА СуммыВзаиморасчетов.ДолгУпр < 0
	|							ТОГДА СуммыВзаиморасчетов.ДолгУпр 
	|						ИНАЧЕ 0 
	|					КОНЕЦ
	|		КОНЕЦ                                             КАК СуммаУменьшенияУпрРасчеты,
	|		ЕСТЬNULL(СуммыДокументов.СуммаУвеличенияРегл,0)   КАК СуммаУвеличенияРегл,
	|		ЕСТЬNULL(СуммыДокументов.СуммаУвеличенияУпр,0)    КАК СуммаУвеличенияУпр,
	|		ЕСТЬNULL(СуммыДокументов.СуммаУменьшенияРегл,0)   КАК СуммаУменьшенияРегл,
	|		ЕСТЬNULL(СуммыДокументов.СуммаУменьшенияУпр,0)    КАК СуммаУменьшенияУпр
	|	ИЗ
	|		ВтСуммыВзаиморасчетов КАК СуммыВзаиморасчетов
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыВзаиморасчетов.Регистратор = СуммыДокументов.Регистратор
	|					И СуммыВзаиморасчетов.ТипРасчетов = СуммыДокументов.ТипРасчетов
	|					И СуммыВзаиморасчетов.ОбъектРасчетов = СуммыДокументов.ОбъектРасчетов) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ &ЭтоПроверка 
	|	ИЛИ ВЫБОР 
	|			КОГДА ВложенныйЗапрос.ДокументРегистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.ДокументРегистратор КАК Документ.РеализацияТоваровУслуг).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|				КОГДА ВложенныйЗапрос.ДокументРегистратор ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|				ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.ДокументРегистратор КАК Документ.РеализацияУслугПрочихАктивов).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.ДокументРегистратор
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВложенныйЗапрос.СуммаУвеличенияРеглРасчеты) - СУММА(ВложенныйЗапрос.СуммаУвеличенияРегл) >= 0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУвеличенияУпрРасчеты) - СУММА(ВложенныйЗапрос.СуммаУвеличенияУпр) >= 0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУвеличенияРеглРасчеты) - СУММА(ВложенныйЗапрос.СуммаУвеличенияРегл) <= -0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУвеличенияУпрРасчеты) - СУММА(ВложенныйЗапрос.СуммаУвеличенияУпр) <= -0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУменьшенияРеглРасчеты) - СУММА(ВложенныйЗапрос.СуммаУменьшенияРегл) >= 0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУменьшенияУпрРасчеты) - СУММА(ВложенныйЗапрос.СуммаУменьшенияУпр) >= 0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУменьшенияРеглРасчеты) - СУММА(ВложенныйЗапрос.СуммаУменьшенияРегл) <= -0.01
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаУменьшенияУпрРасчеты) - СУММА(ВложенныйЗапрос.СуммаУменьшенияУпр) <= -0.01)
	|";
		
	Возврат ТекстСуммыВзаиморасчетов + ТекстСравнения;
	
КонецФункции

Процедура ОбновитьДвиженияДокумента(Ссылка, СоставОбновления)
	Перем Таблица;
	
	ИмяТекущегоРегистра = "";
	НачатьТранзакцию();
	Попытка
		ОписаниеКлючаОбновления = "";
		Таблицы = ПроведениеДокументов.ДанныеДокументаДляПроведения(Ссылка, СоставОбновления);
		
		Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Дата");
		Документ = ПроведениеДокументов.ЭмуляцияДокумента(Ссылка, Дата);
		МенеджерВТ = Новый МенеджерВременныхТаблиц();
		
		Для Каждого ОписаниеОбновления Из СоставОбновления Цикл
			ИмяТекущегоРегистра = ОписаниеОбновления.Ключ;
			Если Таблицы.Свойство("Таблица" + ОписаниеОбновления.Ключ, Таблица) И ЗначениеЗаполнено(Таблица) Тогда
				ОписаниеКлючаОбновления = ОписаниеОбновления.Ключ;
				Набор = ОписаниеОбновления.Значение.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(Ссылка);
				Набор.Загрузить(Таблица);
				ПроведениеДокументов.УстановитьДопСвойстваРегистра(Набор, Документ, МенеджерВТ);
				Набор.Записать(Истина);
			КонецЕсли;
		КонецЦикла;
		
		ПроведениеДокументов.СформироватьЗаданияНаОтложенныеДвижения(Документ, МенеджерВТ);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось записать движения %Обновление% по причине %Причина%';uk='Не вдалося записати рухи %Обновление% через %Причина%'",ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Обновление%", ИмяТекущегоРегистра);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОписаниеОшибки());
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Частичное обновление движений документа';uk='Часткове оновлення рухів документа'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки
КонецПроцедуры

Процедура ОбновитьДвиженияПоОборотнымРегистрам(МассивДокументовСКлиентами, МассивДокументовСПоставщиками)
	
	ВсеДокументы = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеДокументы, МассивДокументовСКлиентами, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеДокументы, МассивДокументовСПоставщиками, Истина);
		
	Если МассивДокументовСКлиентами.Количество() > 0 Тогда
		УправленческийУчетПроведениеСервер.СформироватьДвиженияДенежныеСредстваКонтрагентПоКлиентам(МассивДокументовСКлиентами);
	КонецЕсли;
	Если МассивДокументовСПоставщиками.Количество() > 0 Тогда
		УправленческийУчетПроведениеСервер.СформироватьДвиженияДенежныеСредстваКонтрагентПоПоставщикам(МассивДокументовСПоставщиками);
	КонецЕсли;
	
	Если ВсеДокументы.Количество() > 0 Тогда
		УправленческийУчетПроведениеСервер.СформироватьДвиженияКонтрагентКонтрагент(ВсеДокументы);
		УправленческийУчетПроведениеСервер.СформироватьДвиженияКонтрагентДоходыРасходы(ВсеДокументы);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаСуммВзаиморасчетов(ИмяВТ = "")
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	СуммыДокументовВВалютахУчета.ТипРасчетов               КАК ТипРасчетов,
	|	СуммыДокументовВВалютахУчета.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	СуммыДокументовВВалютахУчета.Регистратор               КАК Регистратор,
	|	СУММА(ВЫБОР КОГДА (СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл) >= 0 
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ)                                               КАК СуммаУвеличенияРегл,
	|	СУММА(ВЫБОР КОГДА (СуммыДокументовВВалютахУчета.СуммаБезНДСУпр  + СуммыДокументовВВалютахУчета.СуммаНДСУпр) >= 0 
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСУпр + СуммыДокументовВВалютахУчета.СуммаНДСУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ)                                               КАК СуммаУвеличенияУпр,
	|	СУММА(ВЫБОР КОГДА (СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл) < 0 
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ)                                               КАК СуммаУменьшенияРегл,
	|	СУММА(ВЫБОР КОГДА (СуммыДокументовВВалютахУчета.СуммаБезНДСУпр + СуммыДокументовВВалютахУчета.СуммаНДСУпр) < 0 
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСУпр + СуммыДокументовВВалютахУчета.СуммаНДСУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ)                                               КАК СуммаУменьшенияУпр
	|ПОМЕСТИТЬ ВТСуммыДокументов
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	(СуммыДокументовВВалютахУчета.Регистратор В (&МассивДокументов) ИЛИ &ПоВсемДокументам)
	|	И СуммыДокументовВВалютахУчета.ТипРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)
	|	И (СуммыДокументовВВалютахУчета.Валюта <> &ВалютаРеглУчета ИЛИ СуммыДокументовВВалютахУчета.Валюта <> &ВалютаУпрУчета)
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыДокументовВВалютахУчета.Регистратор,
	|	СуммыДокументовВВалютахУчета.ОбъектРасчетов,
	|	СуммыДокументовВВалютахУчета.ТипРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.Регистратор                                   КАК Регистратор,
	|	РасчетыСКлиентами.ОбъектРасчетов                                КАК ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта                                        КАК Валюта,
	|	РасчетыСКлиентами.СвязанныйДокумент                             КАК СвязанныйДокумент,
	|	ВЫБОР КОГДА ЕСТЬNULL(РасчетыСКлиентами.СвязанныйДокумент.ДатаПереходаПраваСобственности,ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА РасчетыСКлиентами.СвязанныйДокумент.Дата
	|		ИНАЧЕ ЕСТЬNULL(РасчетыСКлиентами.СвязанныйДокумент.ДатаПереходаПраваСобственности,ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ                                                           КАК Дата,
	|	РасчетыСКлиентами.ОбъектРасчетов.ОплатаВВалюте                    КАК ОплатаВВалюте,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|	СУММА(ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		ТОГДА РасчетыСКлиентами.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                          КАК СуммаУвеличение,
	|	СУММА(ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|		ТОГДА РасчетыСКлиентами.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                          КАК СуммаУменьшение
	|ПОМЕСТИТЬ ВтСвязанныеДокументыКорректировок
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|			ПО СуммыДокументов.Регистратор = РасчетыСКлиентами.Регистратор
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.Регистратор,
	|	РасчетыСКлиентами.ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта,
	|	РасчетыСКлиентами.СвязанныйДокумент,
	|	ВЫБОР КОГДА ЕСТЬNULL(РасчетыСКлиентами.СвязанныйДокумент.ДатаПереходаПраваСобственности,ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА РасчетыСКлиентами.СвязанныйДокумент.Дата
	|		ИНАЧЕ ЕСТЬNULL(РасчетыСКлиентами.СвязанныйДокумент.ДатаПереходаПраваСобственности,ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ,
    |	РасчетыСКлиентами.ОбъектРасчетов.ОплатаВВалюте
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.Регистратор                                   КАК Регистратор,
	|	РасчетыСПоставщиками.ОбъектРасчетов                                КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта                                        КАК Валюта,
	|	РасчетыСПоставщиками.СвязанныйДокумент                             КАК СвязанныйДокумент,
	|	РасчетыСПоставщиками.СвязанныйДокумент.Дата                        КАК Дата,
	|	РасчетыСПоставщиками.ОбъектРасчетов.ОплатаВВалюте                 КАК ОплатаВВалюте,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|	СУММА(ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА РасчетыСПоставщиками.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                             КАК СуммаУвеличение,
	|	СУММА(ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА РасчетыСПоставщиками.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                             КАК СуммаУменьшение
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|			ПО СуммыДокументов.Регистратор = РасчетыСПоставщиками.Регистратор
	|ГДЕ
	|	РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.Регистратор,
	|	РасчетыСПоставщиками.ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта,
	|	РасчетыСПоставщиками.СвязанныйДокумент,
    |	РасчетыСПоставщиками.ОбъектРасчетов.ОплатаВВалюте
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабДокументы.СвязанныйДокумент              КАК СвязанныйДокумент,
	|	ТабДокументы.Валюта                         КАК Валюта,
	|	МАКСИМУМ(ТабДокументы.ПериодВалютыРасчетов) КАК ПериодВалютыРасчетов,
	|	МАКСИМУМ(ТабДокументы.ПериодВалютыУпр)      КАК ПериодВалютыУпр
	|ПОМЕСТИТЬ ВтПериодыКурсовСвязанныхДокументов
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТабДокументы.СвязанныйДокумент       КАК СвязанныйДокумент,
	|		ТабДокументы.Валюта                  КАК Валюта,
	|		КурсыВалютыРасчетов.Период           КАК ПериодВалютыРасчетов,
	|		ДАТАВРЕМЯ(1,1,1)                     КАК ПериодВалютыУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК ТабДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютыРасчетов
	|				ПО КурсыВалютыРасчетов.Валюта = ТабДокументы.Валюта
	|					И ТабДокументы.Дата >= КурсыВалютыРасчетов.Период
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТабДокументы.СвязанныйДокумент       КАК СвязанныйДокумент,
	|		ТабДокументы.Валюта                  КАК Валюта,
	|		ДАТАВРЕМЯ(1,1,1)                     КАК ПериодВалютыРасчетов,
	|		КурсыВалютыУпр.Период                КАК ПериодВалютыУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК ТабДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютыУпр
	|			ПО КурсыВалютыУпр.Валюта = &ВалютаУпрУчета
	|				И ТабДокументы.Дата >= КурсыВалютыУпр.Период) КАК ТабДокументы
	|СГРУППИРОВАТЬ ПО
	|		ТабДокументы.СвязанныйДокумент,
	|		ТабДокументы.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсов.СвязанныйДокумент                          КАК СвязанныйДокумент,
	|	КурсыВалютРасчетов.Курс / КурсыВалютРасчетов.Кратность   КАК КоэффициентРегл,
	|	(КурсыВалютРасчетов.Курс / КурсыВалютРасчетов.Кратность)
	|		/ (КурсыВалютУпр.Курс / КурсыВалютУпр.Кратность)     КАК КоэффициентУпр
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	ВтПериодыКурсовСвязанныхДокументов КАК ПериодыКурсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютРасчетов
	|			ПО ПериодыКурсов.ПериодВалютыРасчетов = КурсыВалютРасчетов.Период
	|				И ПериодыКурсов.Валюта = КурсыВалютРасчетов.Валюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютУпр
	|			ПО ПериодыКурсов.ПериодВалютыУпр = КурсыВалютУпр.Период
	|				И &ВалютаУпрУчета = КурсыВалютУпр.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Регистратор                    КАК Регистратор,
	|	Расчеты.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|	Расчеты.ОплатаВВалюте                  КАК ОплатаВВалюте,
	|	Расчеты.СвязанныйДокумент              КАК СвязанныйДокумент,
	|	Расчеты.ТипРасчетов                    КАК ТипРасчетов,
	|	Расчеты.СуммаУменьшение                КАК СуммаУменьшение,
	|	Расчеты.СуммаУвеличение                КАК СуммаУвеличение,
	|	ВЫБОР КОГДА Расчеты.СуммаУвеличение > Расчеты.СуммаУменьшение
	|			ТОГДА Расчеты.СуммаУменьшение
	|		ИНАЧЕ Расчеты.СуммаУвеличение
	|	КОНЕЦ                                  КАК СуммаОбщая,
	|	ВЫБОР КОГДА Расчеты.СуммаУменьшение > Расчеты.СуммаУвеличение Тогда
	|		Расчеты.СуммаУвеличение
	|	ИНАЧЕ
	|		-Расчеты.СуммаУменьшение
	|	КОНЕЦ                              КАК СуммаОбщаяПредоплата,
	|	ВЫБОР КОГДА Расчеты.СуммаУменьшение > Расчеты.СуммаУвеличение Тогда
	|		-Расчеты.СуммаУвеличение
	|	ИНАЧЕ
	|		Расчеты.СуммаУменьшение
	|	КОНЕЦ                              КАК СуммаОбщаяДолг,
	|	СУММА(Расчеты.СуммаВзаиморасчетов)     КАК СуммаВзаиморасчетов,
	|	СУММА(Расчеты.СуммаВзаиморасчетовРегл) КАК СуммаВзаиморасчетовРегл,
	|	СУММА(Расчеты.СуммаВзаиморасчетовУпр)  КАК СуммаВзаиморасчетовУпр
	|ПОМЕСТИТЬ ВтСуммыВзаиморасчетовКорректировок
	|ИЗ (
	|	ВЫБРАТЬ
	|		СвязанныеДокументы.Регистратор                 КАК Регистратор,
	|		СвязанныеДокументы.ОбъектРасчетов              КАК ОбъектРасчетов,
	|		СвязанныеДокументы.ОплатаВВалюте               КАК ОплатаВВалюте,
	|		СвязанныеДокументы.СвязанныйДокумент           КАК СвязанныйДокумент,
	|		СвязанныеДокументы.ТипРасчетов                 КАК ТипРасчетов,
	|		СвязанныеДокументы.СуммаУменьшение             КАК СуммаУменьшение,
	|		СвязанныеДокументы.СуммаУвеличение             КАК СуммаУвеличение,
	|		ЕСТЬNULL(РасчетыСКлиентамиПоСрокам.Долг,0)     КАК СуммаВзаиморасчетов,
	|		ЕСТЬNULL(РасчетыСКлиентамиПоСрокам.ДолгРегл,0) КАК СуммаВзаиморасчетовРегл,
	|		ЕСТЬNULL(РасчетыСКлиентамиПоСрокам.ДолгУпр,0)  КАК СуммаВзаиморасчетовУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК СвязанныеДокументы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|				ПО СвязанныеДокументы.СвязанныйДокумент = РасчетыСКлиентамиПоСрокам.ДокументРегистратор
	|					И СвязанныеДокументы.ОбъектРасчетов = РасчетыСКлиентамиПоСрокам.ОбъектРасчетов
	|					И РасчетыСКлиентамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И РасчетыСКлиентамиПоСрокам.Долг > 0 
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов
	|		И СвязанныеДокументы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СвязанныеДокументы.Регистратор                    КАК Регистратор,
	|		СвязанныеДокументы.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|		СвязанныеДокументы.ОплатаВВалюте                  КАК ОплатаВВалюте,
	|		СвязанныеДокументы.СвязанныйДокумент              КАК СвязанныйДокумент,
	|		СвязанныеДокументы.ТипРасчетов                    КАК ТипРасчетов,
	|		СвязанныеДокументы.СуммаУменьшение                КАК СуммаУменьшение,
	|		СвязанныеДокументы.СуммаУвеличение                КАК СуммаУвеличение,
	|		ЕСТЬNULL(РасчетыСПоставщикамиПоСрокам.Долг,0)     КАК СуммаВзаиморасчетов,
	|		ЕСТЬNULL(РасчетыСПоставщикамиПоСрокам.ДолгРегл,0) КАК СуммаВзаиморасчетовРегл,
	|		ЕСТЬNULL(РасчетыСПоставщикамиПоСрокам.ДолгУпр,0)  КАК СуммаВзаиморасчетовУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК СвязанныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|			ПО СвязанныеДокументы.СвязанныйДокумент = РасчетыСПоставщикамиПоСрокам.ДокументРегистратор
	|				И СвязанныеДокументы.ОбъектРасчетов = РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов
	|				И РасчетыСПоставщикамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И РасчетыСПоставщикамиПоСрокам.Долг > 0
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов
	|		И СвязанныеДокументы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СвязанныеДокументы.Регистратор                                                   КАК Регистратор,
	|		СвязанныеДокументы.ОбъектРасчетов                                                КАК ОбъектРасчетов,
	|		СвязанныеДокументы.ОплатаВВалюте                                                 КАК ОплатаВВалюте,
	|		СвязанныеДокументы.СвязанныйДокумент                                             КАК СвязанныйДокумент,
	|		СвязанныеДокументы.ТипРасчетов                                                   КАК ТипРасчетов,
	|		СвязанныеДокументы.СуммаУменьшение                                               КАК СуммаУменьшение,
	|		СвязанныеДокументы.СуммаУвеличение                                               КАК СуммаУвеличение,
	|		ВЫБОР КОГДА ЕСТЬNULL(РасчетыСКлиентами.ВидДвижения,Неопределено) = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЕСТЬNULL(РасчетыСКлиентами.Долг,0) + ЕСТЬNULL(РасчетыСКлиентами.Предоплата,0)
	|			ИНАЧЕ -ЕСТЬNULL(РасчетыСКлиентами.Долг,0) - ЕСТЬNULL(РасчетыСКлиентами.Предоплата,0)
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетов,
	|		ВЫБОР КОГДА ЕСТЬNULL(РасчетыСКлиентами.ВидДвижения,Неопределено) = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЕСТЬNULL(РасчетыСКлиентами.ДолгРегл,0) + ЕСТЬNULL(РасчетыСКлиентами.ПредоплатаРегл,0)
	|			ИНАЧЕ -ЕСТЬNULL(РасчетыСКлиентами.ДолгРегл,0) - ЕСТЬNULL(РасчетыСКлиентами.ПредоплатаРегл,0)
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетовРегл,
	|		ВЫБОР КОГДА ЕСТЬNULL(РасчетыСКлиентами.ВидДвижения,Неопределено) = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЕСТЬNULL(РасчетыСКлиентами.ДолгУпр,0) + ЕСТЬNULL(РасчетыСКлиентами.ПредоплатаУпр,0)
	|			ИНАЧЕ -ЕСТЬNULL(РасчетыСКлиентами.ДолгУпр,0) - ЕСТЬNULL(РасчетыСКлиентами.ПредоплатаУпр,0)
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетовУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК СвязанныеДокументы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентами
	|				ПО СвязанныеДокументы.СвязанныйДокумент = РасчетыСКлиентами.Регистратор
	|					И СвязанныеДокументы.ОбъектРасчетов = РасчетыСКлиентами.ЗаказКлиента
	|	ГДЕ
	|		НЕ &НоваяАрхитектураВзаиморасчетов
	|		И СвязанныеДокументы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СвязанныеДокументы.Регистратор                                                   КАК Регистратор,
	|		СвязанныеДокументы.ОбъектРасчетов                                                КАК ОбъектРасчетов,
	|		СвязанныеДокументы.ОплатаВВалюте                                                 КАК ОплатаВВалюте,
	|		СвязанныеДокументы.СвязанныйДокумент                                             КАК СвязанныйДокумент,
	|		СвязанныеДокументы.ТипРасчетов                                                   КАК ТипРасчетов,
	|		СвязанныеДокументы.СуммаУменьшение                                               КАК СуммаУменьшение,
	|		СвязанныеДокументы.СуммаУвеличение                                               КАК СуммаУвеличение,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА РасчетыСПоставщиками.Долг + РасчетыСПоставщиками.Предоплата
	|			ИНАЧЕ -РасчетыСПоставщиками.Долг - РасчетыСПоставщиками.Предоплата
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетов,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА РасчетыСПоставщиками.ДолгРегл + РасчетыСПоставщиками.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыСПоставщиками.ДолгРегл - РасчетыСПоставщиками.ПредоплатаРегл
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетовРегл,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА РасчетыСПоставщиками.ДолгУпр + РасчетыСПоставщиками.ПредоплатаУпр
	|			ИНАЧЕ -РасчетыСПоставщиками.ДолгУпр - РасчетыСПоставщиками.ПредоплатаУпр
	|		КОНЕЦ                                                                            КАК СуммаВзаиморасчетовУпр
	|	ИЗ
	|		ВтСвязанныеДокументыКорректировок КАК СвязанныеДокументы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщиками
	|				ПО СвязанныеДокументы.СвязанныйДокумент = РасчетыСПоставщиками.Регистратор
	|					И СвязанныеДокументы.ОбъектРасчетов = РасчетыСПоставщиками.ЗаказПоставщику
	|	ГДЕ
	|		НЕ &НоваяАрхитектураВзаиморасчетов
	|		И СвязанныеДокументы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)) КАК Расчеты
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.ТипРасчетов,
	|	Расчеты.Регистратор,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.ОплатаВВалюте,
	|	Расчеты.СвязанныйДокумент,
	|	ВЫБОР КОГДА Расчеты.СуммаУвеличение > Расчеты.СуммаУменьшение 
	|			ТОГДА Расчеты.СуммаУменьшение
	|		ИНАЧЕ Расчеты.СуммаУвеличение
	|	КОНЕЦ,
	|	Расчеты.СуммаУменьшение,
	|	Расчеты.СуммаУвеличение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.ТипРасчетов                  КАК ТипРасчетов,
	|	ДанныеРегистра.Регистратор                  КАК Регистратор,
	|	ДанныеРегистра.ОбъектРасчетов               КАК ОбъектРасчетов,
	|	ДанныеРегистра.ВозможныРазвернутыеДвижения  КАК ВозможныРазвернутыеДвижения,
	|	МАКСИМУМ(ДанныеРегистра.ОплатаВВалюте)      КАК ОплатаВВалюте,
	|	СУММА(ДанныеРегистра.СуммаПредоплата)       КАК Предоплата,
	|	СУММА(ДанныеРегистра.СуммаДолг)             КАК Долг,
	|	СУММА(ДанныеРегистра.ПредоплатаРегл)        КАК ПредоплатаРегл,
	|	СУММА(ДанныеРегистра.ПредоплатаУпр)         КАК ПредоплатаУпр,
	|	СУММА(ДанныеРегистра.ДолгРегл)              КАК ДолгРегл,
	|	СУММА(ДанныеРегистра.ДолгУпр)               КАК ДолгУпр,
	|	МАКСИМУМ(ДанныеРегистра.КоэффициентРегл)    КАК КоэффициентРегл,
	|	МАКСИМУМ(ДанныеРегистра.КоэффициентУпр)     КАК КоэффициентУпр
	|//ТекстПоместить
	|ИЗ
	|	(
	|	//Суммы корректировок для распределения
	|	ВЫБРАТЬ
	|		СуммыРасчетов.ТипРасчетов                                              КАК ТипРасчетов,
	|		СуммыРасчетов.Регистратор                                              КАК Регистратор,
	|		СуммыРасчетов.ОбъектРасчетов                                           КАК ОбъектРасчетов,
	|		СуммыРасчетов.ОплатаВВалюте                                            КАК ОплатаВВалюте,
	|		СуммыРасчетов.СуммаОбщаяПредоплата                                     КАК СуммаПредоплата,
	|		СуммыРасчетов.СуммаОбщаяДолг                                           КАК СуммаДолг,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0
	|			ТОГДА КурсыВалют.КоэффициентРегл
	|			ИНАЧЕ СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов
	|		КОНЕЦ * СуммыРасчетов.СуммаОбщаяПредоплата                             КАК ПредоплатаРегл,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0 ИЛИ СуммыРасчетов.СуммаВзаиморасчетовУпр = 0
	|			ТОГДА КурсыВалют.КоэффициентУпр
	|			ИНАЧЕ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов)
	|					/ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетовУпр)
	|		КОНЕЦ * СуммыРасчетов.СуммаОбщаяПредоплата                              КАК ПредоплатаУпр,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0
	|			ТОГДА КурсыВалют.КоэффициентРегл
	|			ИНАЧЕ СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов
	|		КОНЕЦ * СуммыРасчетов.СуммаОбщаяДолг                              КАК ДолгРегл,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0 ИЛИ СуммыРасчетов.СуммаВзаиморасчетовУпр = 0
	|			ТОГДА КурсыВалют.КоэффициентУпр
	|			ИНАЧЕ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов)
	|					/ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетовУпр)
	|		КОНЕЦ * СуммыРасчетов.СуммаОбщаяДолг                              КАК ДолгУпр,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0
	|			ТОГДА КурсыВалют.КоэффициентРегл
	|			ИНАЧЕ СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов
	|		КОНЕЦ                                                             КАК КоэффициентРегл,
	|		ВЫБОР КОГДА СуммыРасчетов.СуммаВзаиморасчетов = 0 ИЛИ СуммыРасчетов.СуммаВзаиморасчетовУпр = 0
	|			ТОГДА КурсыВалют.КоэффициентУпр
	|			ИНАЧЕ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетов)
	|					/ (СуммыРасчетов.СуммаВзаиморасчетовРегл / СуммыРасчетов.СуммаВзаиморасчетовУпр)
	|		КОНЕЦ                                                             КАК КоэффициентУпр,
	|		ИСТИНА                                                            КАК ВозможныРазвернутыеДвижения
	|	ИЗ
	|		ВтСуммыВзаиморасчетовКорректировок КАК СуммыРасчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|				ПО КурсыВалют.СвязанныйДокумент = СуммыРасчетов.СвязанныйДокумент
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Суммы остальных документов с клиентами
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		ДанныеРегистра.Регистратор                                      КАК Регистратор,
	|		ДанныеРегистра.ЗаказКлиента                                     КАК ОбъектРасчетов,
	|		ДанныеРегистра.ЗаказКлиента.ОплатаВВалюте                       КАК ОплатаВВалюте,
	|	
	|		ВЫБОР
	|				КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					ТОГДА -ДанныеРегистра.Предоплата
	|				ИНАЧЕ ДанныеРегистра.Предоплата
	|		КОНЕЦ                                                           КАК СуммаПредоплата,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					ТОГДА ДанныеРегистра.Долг
	|			КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					ТОГДА -ДанныеРегистра.Долг
	|				ИНАЧЕ 0
	|		КОНЕЦ                                                           КАК СуммаДолг,
	|		ВЫБОР
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ДанныеРегистра.ПредоплатаРегл
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ДанныеРегистра.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                           КАК ПредоплатаРегл,
	|		
	|		ВЫБОР 
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ДанныеРегистра.ПредоплатаУпр
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ДанныеРегистра.ПредоплатаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                           КАК ПредоплатаУпр,
	|	
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ДанныеРегистра.ДолгРегл
	|			КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					ТОГДА -ДанныеРегистра.ДолгРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                           КАК ДолгРегл,
	|	
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ДанныеРегистра.ДолгУпр
	|			КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					ТОГДА -ДанныеРегистра.ДолгУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                           КАК ДолгУпр,
	|		0                                                               КАК КоэффициентРегл,
	|		0                                                               КАК КоэффициентУпр,
	|		ДанныеРегистра.Регистратор ССЫЛКА Документ.КорректировкаРеализации КАК ВозможныРазвернутыеДвижения
	|	
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.Регистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ЗаказКлиента
	|	ГДЕ
	|		НЕ &НоваяАрхитектураВзаиморасчетов
	|		И ДанныеРегистра.Регистратор В (&МассивДокументов)
	|		И НЕ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента)
	|		И НЕ (ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|				И ДанныеРегистра.ЗаказКлиента.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Реализации в статусе ""В пути""
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		ДанныеРегистра.Регистратор                                      КАК Регистратор,
	|		ДанныеРегистра.ЗаказКлиента                                     КАК ОбъектРасчетов,
	|		ДанныеРегистра.Регистратор.ОплатаВВалюте                        КАК ОплатаВВалюте,
	|
	|		ДанныеРегистра.Предоплата                                       КАК СуммаПредоплата,
	|		0                                                               КАК СуммаДолг,
	|		ДанныеРегистра.ПредоплатаРегл                                   КАК ПредоплатаРегл,
	|		ДанныеРегистра.ПредоплатаУпр                                    КАК ПредоплатаУпр,
	|		0                                                               КАК ДолгРегл,
	|		0                                                               КАК ДолгУпр,
	|		0                                                               КАК КоэффициентРегл,
	|		0                                                               КАК КоэффициентУпр,
	|		ДанныеРегистра.Регистратор ССЫЛКА Документ.КорректировкаРеализации КАК ВозможныРазвернутыеДвижения
	|	
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.Регистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ЗаказКлиента
	|	ГДЕ
	|		НЕ &НоваяАрхитектураВзаиморасчетов
	|		И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента)
	|		И ВЫБОР 
	|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.РеализацияТоваровУслуг).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|				КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.РеализацияУслугПрочихАктивов).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		ДанныеРегистра.ДокументРегистратор                              КАК Регистратор,
	|		ДанныеРегистра.ОбъектРасчетов                                   КАК ОбъектРасчетов,
	|		ДанныеРегистра.ОбъектРасчетов.ОплатаВВалюте                     КАК ОплатаВВалюте,
	|		ВЫБОР
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.Предоплата
	|			ИНАЧЕ -ДанныеРегистра.Предоплата
	|		КОНЕЦ                                                          КАК СуммаПредоплата,
	|		ВЫБОР 
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ДанныеРегистра.Долг
	|			ИНАЧЕ ДанныеРегистра.Долг
	|		КОНЕЦ                                                           КАК СуммаДолг,
	|		ВЫБОР
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.ПредоплатаРегл
	|			ИНАЧЕ -ДанныеРегистра.ПредоплатаРегл
	|		КОНЕЦ                                                           КАК ПредоплатаРегл,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					ТОГДА ДанныеРегистра.ПредоплатаУпр
	|			ИНАЧЕ -ДанныеРегистра.ПредоплатаУпр
	|		КОНЕЦ                                                           КАК ПредоплатаУпр,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -ДанныеРегистра.ДолгРегл
	|			ИНАЧЕ ДанныеРегистра.ДолгРегл
	|		КОНЕЦ                                                           КАК ДолгРегл,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -ДанныеРегистра.ДолгУпр
	|			ИНАЧЕ ДанныеРегистра.ДолгУпр
	|		КОНЕЦ                                                           КАК ДолгУпр,
	|		0                                                               КАК КоэффициентРегл,
	|		0                                                               КАК КоэффициентУпр,
	|		ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаРеализации КАК ВозможныРазвернутыеДвижения
	|	
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.ДокументРегистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ОбъектРасчетов
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов
	|		И НЕ ДанныеРегистра.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса))
	|		И НЕ (ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|				И ДанныеРегистра.ОбъектРасчетов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	//Реализации в статусе ""В пути""
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		ДанныеРегистра.ДокументРегистратор                              КАК Регистратор,
	|		ДанныеРегистра.ОбъектРасчетов                                   КАК ОбъектРасчетов,
	|		ДанныеРегистра.ДокументРегистратор.ОплатаВВалюте                КАК ОплатаВВалюте,
	|		ДанныеРегистра.Предоплата                                       КАК СуммаПредоплата,
	|		0                                                               КАК СуммаДолг,
	|		ДанныеРегистра.ПредоплатаРегл                                   КАК ПредоплатаРегл,
	|		ДанныеРегистра.ПредоплатаУпр                                    КАК ПредоплатаУпр,
	|		0                                                               КАК ДолгРегл,
	|		0                                                               КАК ДолгУпр,
	|		0                                                               КАК КоэффициентРегл,
	|		0                                                               КАК КоэффициентУпр,
	|		ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаРеализации КАК ВозможныРазвернутыеДвижения
	|	
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.ДокументРегистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ОбъектРасчетов
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов
	|		И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента)
	|		И ВЫБОР 
	|			КОГДА ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.ДокументРегистратор КАК Документ.РеализацияТоваровУслуг).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|				КОГДА ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|				ТОГДА ВЫРАЗИТЬ(ДанныеРегистра.ДокументРегистратор КАК Документ.РеализацияУслугПрочихАктивов).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Суммы остальных документов с поставщиками
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|		ДанныеРегистра.Регистратор                                         КАК Регистратор,
	|		ДанныеРегистра.ЗаказПоставщику                                     КАК АналитикаУчетаПоПартнерам,
	|		ДанныеРегистра.ЗаказПоставщику.ОплатаВВалюте                       КАК ОплатаВВалюте,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.Предоплата
	|			ИНАЧЕ -ДанныеРегистра.Предоплата
	|		КОНЕЦ                                                              КАК СуммаПредоплата,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.Долг
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				ТОГДА -ДанныеРегистра.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                              КАК СуммаДолг,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.ПредоплатаРегл
	|			ИНАЧЕ -ДанныеРегистра.ПредоплатаРегл
	|		КОНЕЦ                                                              КАК ПредоплатаРегл,
	|		
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.ПредоплатаУпр
	|			ИНАЧЕ -ДанныеРегистра.ПредоплатаУпр
	|		КОНЕЦ                                                              КАК ПредоплатаУпр,
	|		
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.ДолгРегл
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|				И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				ТОГДА -ДанныеРегистра.ДолгРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                              КАК ДолгРегл,
	|		
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДанныеРегистра.ДолгУпр
	|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				ТОГДА -ДанныеРегистра.ДолгУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ                                                              КАК ДолгУпр,
	|		0                                                                  КАК КоэффициентРегл,
	|		0                                                                  КАК КоэффициентУпр,
	|		ДанныеРегистра.Регистратор ССЫЛКА Документ.КорректировкаПриобретения КАК ВозможныРазвернутыеДвижения
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.Регистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ЗаказПоставщику
	|	ГДЕ
	|		НЕ &НоваяАрхитектураВзаиморасчетов
	|		И НЕ (ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|				И ДанныеРегистра.ЗаказПоставщику.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|		ДанныеРегистра.ДокументРегистратор                                 КАК Регистратор,
	|		ДанныеРегистра.ОбъектРасчетов                                      КАК ОбъектРасчетов,
	|		ДанныеРегистра.ОбъектРасчетов.ОплатаВВалюте                        КАК ОплатаВВалюте,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ДанныеРегистра.Предоплата
	|			ИНАЧЕ ДанныеРегистра.Предоплата
	|		КОНЕЦ                                                              КАК СуммаПредоплата,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|			ТОГДА -ДанныеРегистра.Долг
	|			ИНАЧЕ ДанныеРегистра.Долг
	|		КОНЕЦ                                                              КАК СуммаДолг,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ДанныеРегистра.ПредоплатаРегл
	|			ИНАЧЕ ДанныеРегистра.ПредоплатаРегл
	|		КОНЕЦ                                                              КАК ПредоплатаРегл,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|			ТОГДА -ДанныеРегистра.ПредоплатаУпр
	|			ИНАЧЕ ДанныеРегистра.ПредоплатаУпр
	|		КОНЕЦ                                                              КАК ПредоплатаУпр,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|			ТОГДА -ДанныеРегистра.ДолгРегл
	|			ИНАЧЕ ДанныеРегистра.ДолгРегл
	|		КОНЕЦ                                                              КАК ДолгРегл,
	|		ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|			ТОГДА -ДанныеРегистра.ДолгУпр
	|			ИНАЧЕ ДанныеРегистра.ДолгУпр
	|		КОНЕЦ                                                              КАК ДолгУпр,
	|		0                                                                  КАК КоэффициентРегл,
	|		0                                                                  КАК КоэффициентУпр,
	|		ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаПриобретения КАК ВозможныРазвернутыеДвижения
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДанныеРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыДокументов КАК СуммыДокументов
	|				ПО СуммыДокументов.Регистратор = ДанныеРегистра.ДокументРегистратор
	|					И СуммыДокументов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|					И СуммыДокументов.ОбъектРасчетов = ДанныеРегистра.ОбъектРасчетов
	|
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов
	|		И НЕ (ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|				И ДанныеРегистра.ОбъектРасчетов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|		И НЕ ДанныеРегистра.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса))) КАК ДанныеРегистра
	|СГРУППИРОВАТЬ ПО 
	|	ДанныеРегистра.ТипРасчетов,
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.ОбъектРасчетов,
	|	ДанныеРегистра.ВозможныРазвернутыеДвижения
	|";
	
	Если ИмяВТ <> "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ТекстПоместить","ПОМЕСТИТЬ " + ИмяВТ);
		ТекстЗапроса = ТекстЗапроса + "
		|;";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает для каждого регистратора суммы расчетов в валютах регламентированного и управленческого учета для распределения.
// Параметры:
//		МассивРегистраторов - Массив - Массив документов, по которым нужно расчитать суммы.
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица сумм и коэффициентов.
//	 *	ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - тип расчетов регистратора.
//	 *	Регистратор - ДокументСсылка - Документ, для которого произведен расчет.
//	 *	АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета расчетов по партнерам.
//	 *	ОплатаВВалюте - Булево - Признак оплаты в иностранной валюте.
//	 *	СуммаВзаиморасчетов - Число - Сумма взаиморасчетов документа, всегда положительная.
//	 *	ПредоплатаРегл - Число - Сумма зачтенной/возвращенной предоплаты в валюте регламентированного учета.
//	 *	ПредоплатаУпр - Число - Сумма зачтенной/возвращенной предоплаты в валюте управленческого учета.
//	 *	ДолгРегл - Число - Сумма погашенного/возникшего долга в валюте регламентированного учета.
//	 *	ДолгУпр - Число - Сумма погашенного/возникшего долга в валюте управленческого учета.
//	 *	КоэффициентРегл - Число - Коэффициент пересчета суммы расчетов в валюту регламентированного учета оснований корректировок.
//	 *	КоэффициентУпр - Число - Коэффициент пересчета суммы расчетов в валюту управленческого учета оснований корректировок.
//
Функция СуммыДокументовДляПострочногоРаспределения(МассивРегистраторов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСуммВзаиморасчетов();
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивРегистраторов);
	Запрос.УстановитьПараметр("ПоВсемДокументам", НЕ ЗначениеЗаполнено(МассивРегистраторов));
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", Константы.НоваяАрхитектураВзаиморасчетов.Получить());
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

// Служебная процедура.
//
Процедура ПересчитатьТаблицуВВалютыУчета(МенеджерВременныхТаблиц, ВалютыДляПересчета)
	
	ПоказателиДляПересчета = "СуммаБезНДС, СуммаНДС";

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ДанныеДокументаПредварительная КАК ТаблицаТоваров 
	|
	|ИТОГИ
	|	СУММА(ТаблицаТоваров.СуммаБезНДС),
    |	СУММА(ТаблицаТоваров.СуммаНДСПропорционально),
	|	СУММА(ТаблицаТоваров.СуммаНДС)
	|ПО
	|	ТаблицаТоваров.Регистратор,
	|	ТаблицаТоваров.Валюта,
	|	ТаблицаТоваров.Период,
	|	ТаблицаТоваров.РаспределятьОбщуюСумму,
	|	ТаблицаТоваров.ИсточникДанных
	|";

	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗаписейСуммыДокументов = РегистрыСведений.СуммыДокументовВВалютахУчета.СоздатьНаборЗаписей();
	СуммыДокументовВВалютахУчета = НаборЗаписейСуммыДокументов.Выгрузить();
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Если СуммыДокументовВВалютахУчета.Колонки.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Колонка.ТипЗначения <> Новый ОписаниеТипов("Неопределено") Тогда
			ОписаниеТиповКолонки = Колонка.ТипЗначения;
		Иначе
			ОписаниеТиповКолонки = ОбщегоНазначения.ОписаниеТипаВсеСсылки();
		КонецЕсли;
		СуммыДокументовВВалютахУчета.Колонки.Добавить(Колонка.Имя, ОписаниеТиповКолонки);
	КонецЦикла;
	СуммыДокументовВВалютахУчета.Колонки.Удалить("Активность");
	СуммыДокументовВВалютахУчета.Колонки.Удалить("МоментВремени");
	
	КэшКурсовВалют = РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют();
	СтруктураВалют = Новый Структура;
	Для каждого ВалютаДляПересчета Из ВалютыДляПересчета Цикл
		Если ТипЗнч(ВалютаДляПересчета.Значение) = Тип("СправочникСсылка.Валюты") Тогда
			ДанныеПоВалюте = Новый Структура("Валюта, ОбщаяСумма, Распределено", ВалютаДляПересчета.Значение, 0, 0);
			СтруктураВалют.Вставить(ВалютаДляПересчета.Ключ, ДанныеПоВалюте);
		КонецЕсли;
	КонецЦикла;
	
	ВыборкаПоДокументу = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументу.Следующий() Цикл
		
		ВыборкаПоВалюте = ВыборкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоВалюте.Следующий() Цикл
			
			ВыборкаПоДате = ВыборкаПоВалюте.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоДате.Следующий() Цикл
			
				КурсВалютыДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ВыборкаПоДате.Валюта, ВыборкаПоДате.Период, КэшКурсовВалют);
				
				ВыборкаПоТипуПересчета = ВыборкаПоДате.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоТипуПересчета.Следующий() Цикл
					
					ВыборкаПоИсточникуДанных = ВыборкаПоТипуПересчета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если ВыборкаПоТипуПересчета.РаспределятьОбщуюСумму Тогда
						#Область РаспределятьОбщуюСумму
						
						Пока ВыборкаПоИсточникуДанных.Следующий() Цикл
							
							ОбщаяСумма          = ВыборкаПоИсточникуДанных.СуммаБезНДС + ВыборкаПоИсточникуДанных.СуммаНДС;
							Распределено        = 0;
							Для каждого ТекущаяВалютаДляПересчета Из СтруктураВалют Цикл
								Валюта = ТекущаяВалютаДляПересчета.Значение.Валюта;
								Если Валюта <> ВыборкаПоДате.Валюта Тогда
									КурсВалюты = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(Валюта, ВыборкаПоДате.Период, КэшКурсовВалют);
									ТекущаяВалютаДляПересчета.Значение.ОбщаяСумма = ОбщаяСумма * КурсВалютыДокумента / КурсВалюты;
									ТекущаяВалютаДляПересчета.Значение.Распределено = 0;
								КонецЕсли;
							КонецЦикла;
							
							Выборка = ВыборкаПоИсточникуДанных.Выбрать();
							Пока Выборка.Следующий() Цикл
								
								Сумма = Выборка.СуммаБезНДС + Выборка.СуммаНДС;
								
								НоваяЗапись = СуммыДокументовВВалютахУчета.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
								
								Если ОбщаяСумма = 0 Тогда
									
									Для каждого ТекущаяВалютаДляПересчета Из СтруктураВалют Цикл
										НоваяЗапись["СуммаНДС" + ТекущаяВалютаДляПересчета.Ключ] = 0;
										НоваяЗапись["СуммаБезНДС" + ТекущаяВалютаДляПересчета.Ключ] = 0;
									КонецЦикла;
									
									Продолжить;
									
								КонецЕсли;
								
								Для каждого ТекущаяВалютаДляПересчета Из СтруктураВалют Цикл
									
									ДанныеВалюты = ТекущаяВалютаДляПересчета.Значение;
									
									Если ДанныеВалюты.Валюта = Выборка.Валюта Тогда
										ВалютнаяСумма = Сумма;
										ВалютнаяСуммаНДС = Выборка.СуммаНДС;
									Иначе
										ВалютнаяСумма = Окр(ДанныеВалюты.ОбщаяСумма * (Распределено + Сумма) / ОбщаяСумма, 2) - ДанныеВалюты.Распределено;
									
										ДанныеВалюты.Распределено = ДанныеВалюты.Распределено + ВалютнаяСумма;
										ВалютнаяСуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ВалютнаяСумма, Выборка.СтавкаНДС);
									КонецЕсли;
									
									НоваяЗапись["СуммаНДС" + ТекущаяВалютаДляПересчета.Ключ] = ВалютнаяСуммаНДС;
									НоваяЗапись["СуммаБезНДС" + ТекущаяВалютаДляПересчета.Ключ] = ВалютнаяСумма - ВалютнаяСуммаНДС;
                                    Если ВРЕГ(ТекущаяВалютаДляПересчета.Ключ) = ВРЕГ("Регл") Тогда
									    НоваяЗапись["СуммаНДСРегл"] = НоваяЗапись["СуммаНДСРегл"] - Выборка.СуммаНДСПропорционально;
									    НоваяЗапись["СуммаБезНДСРегл"] = НоваяЗапись["СуммаБезНДСРегл"] + Выборка.СуммаНДСПропорционально;
                                    ИначеЕсли ВРЕГ(ТекущаяВалютаДляПересчета.Ключ) = ВРЕГ("Упр") Тогда
                                        КурсВалюты = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ДанныеВалюты.Валюта, ВыборкаПоДате.Период, КэшКурсовВалют);
                                        СуммаНДСПропорциональноУпр = Выборка.СуммаНДСПропорционально * КурсВалютыДокумента / КурсВалюты;
                                        НоваяЗапись["СуммаНДСУпрМУ"] = НоваяЗапись["СуммаНДСУпр"] - СуммаНДСПропорциональноУпр;
                                        НоваяЗапись["СуммаБезНДСУпрМУ"] = НоваяЗапись["СуммаБезНДСУпр"] + СуммаНДСПропорциональноУпр;
                                    КонецЕсли; 
									
								КонецЦикла;
								
								Распределено = Распределено + Сумма;
								
							КонецЦикла;
						КонецЦикла;
						#КонецОбласти
					Иначе
						#Область ПересчетПоКурсу
						Пока ВыборкаПоИсточникуДанных.Следующий() Цикл
							Выборка = ВыборкаПоИсточникуДанных.Выбрать();
							Пока Выборка.Следующий() Цикл
								НоваяЗапись = СуммыДокументовВВалютахУчета.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
								
								Для каждого ТекущаяВалютаДляПересчета Из СтруктураВалют Цикл
									ДанныеВалюты = ТекущаяВалютаДляПересчета.Значение;
									КурсВалюты = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(ДанныеВалюты.Валюта, ВыборкаПоДате.Период, КэшКурсовВалют);
									
									СуммаСНДСВВалюте = Окр((Выборка.СуммаБезНДС + Выборка.СуммаНДС) * КурсВалютыДокумента / КурсВалюты, 2);
									СуммаНДСВВалюте = Окр(Выборка.СуммаНДС * КурсВалютыДокумента / КурсВалюты, 2);
									
                                    НоваяЗапись["СуммаНДС" + ТекущаяВалютаДляПересчета.Ключ] =  СуммаНДСВВалюте;
                                    НоваяЗапись["СуммаБезНДС" + ТекущаяВалютаДляПересчета.Ключ] = СуммаСНДСВВалюте - СуммаНДСВВалюте;
                                    
                                    СуммаНДСПропорциональноВВалюте = Окр(Выборка.СуммаНДСПропорционально * КурсВалютыДокумента / КурсВалюты, 2);
                                    Если ВРЕГ(ТекущаяВалютаДляПересчета.Ключ) = ВРЕГ("Регл") Тогда
									    НоваяЗапись["СуммаНДСРегл"] = НоваяЗапись["СуммаНДСРегл"] - СуммаНДСПропорциональноВВалюте;
									    НоваяЗапись["СуммаБезНДСРегл"] = НоваяЗапись["СуммаБезНДСРегл"] + СуммаНДСПропорциональноВВалюте;
                                    ИначеЕсли ВРЕГ(ТекущаяВалютаДляПересчета.Ключ) = ВРЕГ("Упр") Тогда
                                        НоваяЗапись["СуммаНДСУпрМУ"] = НоваяЗапись["СуммаНДСУпр"] - СуммаНДСПропорциональноВВалюте;
                                        НоваяЗапись["СуммаБезНДСУпрМУ"] = НоваяЗапись["СуммаБезНДСУпр"] + СуммаНДСПропорциональноВВалюте;
                                    КонецЕсли; 
										
								КонецЦикла;
							КонецЦикла;
						КонецЦикла;
						#КонецОбласти
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Поместим результат во временную таблицу
	МассивПолейВыборки = Новый Массив;
	Для Каждого Колонка Из СуммыДокументовВВалютахУчета.Колонки Цикл
		МассивПолейВыборки.Добавить(Колонка.Имя);
	КонецЦикла;
	ТекстПолейВыборки = СтрСоединить(МассивПолейВыборки, ",");
	
	ТекстЗапроса = "
	|УНИЧТОЖИТЬ ДанныеДокументаПредварительная
	|;
	|ВЫБРАТЬ
	|	&ПоляВыборки
	|ПОМЕСТИТЬ СуммыДокументовВВалютахУчета
	|ИЗ 
	|	&Таблица КАК Таблица";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВыборки", ТекстПолейВыборки);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Таблица", СуммыДокументовВВалютахУчета);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.СуммыДокументовВВалютахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("54837c79-4dbc-4ec6-b6ff-538ac5c8a06c");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СуммыДокументовВВалютахУчета.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет ресурс БазаНДСРегл.
                                   |Заполняет новые ресурсы ""СуммаБезНДСУпр"", ""СуммаНДСУпр"", ""СуммаБезНДСУпрМУ"", ""СуммаНДСУпрМУ"" и ""СуммаВзаиморасчетов"". 
                                   |Добавляет новые записи для гривневых документов.
                                   |Заполняет реквизит СтавкаНДС с типом СправочникСсылка.СтавкиНДС.'
                                   |;uk='Заповнює ресурс БазаНДСРегл.
                                   |Заповнює нові ресурси ""СумаБезНДСУпр"", ""СуммаНДСУпр"", ""СумаБезНДСУпрМУ"", ""СуммаНДСУпрМУ"" та ""СуммаВзаиморасчетов"".
                                   |Додає нові записи для гривневих документів.
                                   |Заповнює реквізит СтавкаНДС з типом СправочникСсылка.СтавкиНДС.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.АвансовыйОтчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.АктВыполненныхРабот.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВзаимозачетЗадолженности.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВнесениеДенежныхСредствВКассуККМ.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВозвратТоваровМеждуОрганизациями.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВозвратТоваровОтКлиента.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВозвратТоваровПоставщику.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВыемкаДенежныхСредствИзКассыККМ.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВыкупВозвратнойТарыКлиентом.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВыкупВозвратнойТарыУПоставщика.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаказКлиента.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаказПоставщику.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИнвентаризацияНаличныхДенежныхСредств.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.КорректировкаРегистров.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОперацияПоПлатежнойКарте.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОприходованиеИзлишковТоваров.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтражениеРасхожденийПриИнкассацииДенежныхСредств.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетБанкаПоОперациямЭквайринга.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетКомиссионера.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетКомиссионераОСписании.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетКомитенту.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетКомитентуОСписании.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетОРозничныхПродажах.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетПоКомиссииМеждуОрганизациями.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетПоКомиссииМеждуОрганизациямиОСписании.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПередачаТоваровМеждуОрганизациями.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПриобретениеТоваровУслуг.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПриобретениеУслугПрочихАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПриходныйКассовыйОрдер.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПрочееОприходованиеТоваров.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.РасходныйКассовыйОрдер.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеБезналичныхДенежныхСредств.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеЗадолженности.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоДокументам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	//++ Локализация
	//-- Локализация
	//++ НЕ УТ
	//++ Локализация
	Читаемые.Добавить(Метаданные.Документы.ВыбытиеДенежныхДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеДенежныхДокументов.ПолноеИмя());
	//-- Локализация
	//-- НЕ УТ
	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.Документы.ВозвратСырьяОтПереработчика.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаказПереработчику.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетПереработчика.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПередачаСырьяПереработчику.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеОтПереработчика.ПолноеИмя());
	//++ НЕ УТКА
	Читаемые.Добавить(Метаданные.Документы.ВозвратСырьяДавальцу.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаказДавальца.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетДавальцу.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеСырьяОтДавальца.ПолноеИмя());
	//-- НЕ УТКА
	//-- НЕ УТ
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.АвансовыйОтчет.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.АктВыполненныхРабот.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВзаимозачетЗадолженности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровОтКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратТоваровПоставщику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВыкупВозвратнойТарыКлиентом.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВыкупВозвратнойТарыУПоставщика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказПоставщику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаявкаНаВозвратТоваровОтКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.КорректировкаРегистров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОперацияПоПлатежнойКарте.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОприходованиеИзлишковТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетКомиссионера.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетКомиссионераОСписании.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетКомитенту.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетКомитентуОСписании.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетОРозничныхПродажах.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетПоКомиссииМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетПоКомиссииМеждуОрганизациямиОСписании.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаТоваровМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";              
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриобретениеТоваровУслуг.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриобретениеУслугПрочихАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПрочееОприходованиеТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РасходныйКассовыйОрдер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияТоваровУслуг.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияУслугПрочихАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеЗадолженности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	//++ Локализация
	//-- Локализация
	
	//++ НЕ УТ
	//++ Локализация
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВыбытиеДенежныхДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеДенежныхДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	//-- Локализация
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВозвратСырьяОтПереработчика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказПереработчику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетПереработчика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";        
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаСырьяПереработчику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";              
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеОтПереработчика.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	//-- НЕ УТ
	
	//++ НЕ УТКА
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказДавальца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетДавальцу.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";      
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПоступлениеСырьяОтДавальца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	//-- НЕ УТКА                      
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";            
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам";
	НоваяСтрока.Порядок = "После";       
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентамиПоДокументам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СуммыДокументовВВалютахУчета";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ТипыДокументовКОбработке();
	ПараметрыВыборки.ПолныеИменаРегистров = Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Регистратор.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Регистратор");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	ОбновлениеИнформационнойБазы.УстановитьИсточникДанных(
		ПараметрыВыборки.ДополнительныеИсточникиДанных,
		"Договор",
		"ПриобретениеТоваровУслуг",
		"СуммыДокументовВВалютахУчета"
	);
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПараметрыВыборки.ПолныеИменаРегистров;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК Данные
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Данные.Регистратор) В (&Типы)
	|	И Данные.БазаНДСРегл = 0
	|	И Данные.СуммаБезНДСРегл <> 0";
		
	ТипыДокументовКОбработке = СтрРазделить(ТипыДокументовКОбработке(), ",");
	ОтборПоТипам = Новый Массив;
	Для каждого ТипДокумента Из ТипыДокументовКОбработке Цикл
		ОтборПоТипам.Добавить(СтрШаблон("ТИП(%1)", СокрЛП(ТипДокумента)));
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Типы", СтрСоединить(ОтборПоТипам, ","));
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	
	// ЭТАП 1 Для записей уже присутствующих в регистре заполняем три новых ресурса
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументовВВалютеРегл.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютеРегл
	|ГДЕ
	|	((СуммыДокументовВВалютеРегл.СуммаБезНДСРегл <> 0
	|		И СуммыДокументовВВалютеРегл.СуммаБезНДСУпр = 0)
	|	ИЛИ (СуммыДокументовВВалютеРегл.ТипРасчетов В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком))
	|		И СуммыДокументовВВалютеРегл.СуммаВзаиморасчетов = 0)
	|	ИЛИ (СуммыДокументовВВалютеРегл.Валюта = &ВалютаРегламентированногоУчета
	|		И ((СуммыДокументовВВалютеРегл.СуммаБезНДС+СуммыДокументовВВалютеРегл.СуммаНДСПропорционально) <> СуммыДокументовВВалютеРегл.СуммаБезНДСРегл
	|			ИЛИ (СуммыДокументовВВалютеРегл.СуммаНДС-СуммыДокументовВВалютеРегл.СуммаНДСПропорционально) <> СуммыДокументовВВалютеРегл.СуммаНДСРегл))
	|	ИЛИ (СуммыДокументовВВалютеРегл.Валюта = &ВалютаУправленческогоУчета
	|		И (СуммыДокументовВВалютеРегл.СуммаБезНДС <> СуммыДокументовВВалютеРегл.СуммаБезНДСУпр 
	|			ИЛИ СуммыДокументовВВалютеРегл.СуммаНДС <> СуммыДокументовВВалютеРегл.СуммаНДСУпр)))
	|	И ТИПЗНАЧЕНИЯ(СуммыДокументовВВалютеРегл.Регистратор) В (&ОтборПоТипам)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументовВВалютеРегл.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютеРегл
	|ГДЕ
	|	СуммыДокументовВВалютеРегл.ТипРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)
	|	И (СуммыДокументовВВалютеРегл.Регистратор ССЫЛКА Документ.ОтчетКомитенту
	|			И ВЫРАЗИТЬ(СуммыДокументовВВалютеРегл.Регистратор КАК Документ.ОтчетКомитенту).УдержатьВознаграждение
	|		ИЛИ СуммыДокументовВВалютеРегл.Регистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			И ВЫРАЗИТЬ(СуммыДокументовВВалютеРегл.Регистратор КАК Документ.ОтчетКомиссионера).УдержатьВознаграждение
	|		ИЛИ СуммыДокументовВВалютеРегл.Регистратор ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	|			И (ВЫРАЗИТЬ(СуммыДокументовВВалютеРегл.Регистратор КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).УдержатьВознаграждение
	|				ИЛИ СуммыДокументовВВалютеРегл.ИдентификаторСтроки <> """"))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументовВВалютеРегл.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютеРегл
	|ГДЕ
	|	ВЫРАЗИТЬ(СуммыДокументовВВалютеРегл.Регистратор КАК Документ.СписаниеЗадолженности).ХозяйственнаяОперация
	|		= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности)
	|		И СуммыДокументовВВалютеРегл.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	ИЛИ ВЫРАЗИТЬ(СуммыДокументовВВалютеРегл.Регистратор КАК Документ.СписаниеЗадолженности).ХозяйственнаяОперация
	|		= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности)
	|		И СуммыДокументовВВалютеРегл.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	//++ НЕ УТ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументовВВалютеРегл.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютеРегл
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Услуги КАК УслугиПереработчика
	|		ПО СуммыДокументовВВалютеРегл.Регистратор = УслугиПереработчика.Ссылка
	|			И СуммыДокументовВВалютеРегл.ИдентификаторСтроки = УслугиПереработчика.ИдентификаторСтроки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СуммыДокументовВВалютеРегл.Регистратор) = ТИП(Документ.ОтчетПереработчика)
	|	И УслугиПереработчика.СуммаВзаиморасчетов <> СуммыДокументовВВалютеРегл.СуммаВзаиморасчетов
	//-- НЕ УТ
	|";                        
	
	ТипыДокументовКОбработке = СтрРазделить(ТипыДокументовКОбработке(), ",");
	ОтборПоТипам = Новый Массив;
	Для каждого ТипДокумента Из ТипыДокументовКОбработке Цикл
		ОтборПоТипам.Добавить(СтрШаблон("ТИП(%1)", СокрЛП(ТипДокумента)));
	КонецЦикла;
	ОтборПоТипамСтрокой = 
		СтрСоединить(ОтборПоТипам, 
			",
			|");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипам", СтрСоединить(ОтборПоТипам, ","));
	
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",    Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",Константы.ВалютаРегламентированногоУчета.Получить());
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	// ЭТАП 2 Для отсутствующих записей проводим документы по одному регистру
	
	Запрос.Текст = ТекстЗапросаРегистрацииДанныхКОбработке24();
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПартионныйУчетВерсии22",Константы.ПартионныйУчетВерсии22.Получить());
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	СуммыДокументовВВалютахУчета.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|	И НЕ СуммыДокументовВВалютахУчета.УдалитьСтавкаНДС = &ПустаяСтавкаНДС
	|	И ТИПЗНАЧЕНИЯ(СуммыДокументовВВалютахУчета.Регистратор) В (&ОтборПоТипам)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	СуммыДокументовВВалютахУчета.ТипРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)
	|	И СуммыДокументовВВалютахУчета.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|	И НЕ СуммыДокументовВВалютахУчета.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И ТИПЗНАЧЕНИЯ(СуммыДокументовВВалютахУчета.Регистратор) В (&ОтборПоТипам)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	(СуммыДокументовВВалютахУчета.УдалитьСтавкаНДС <>  &ПустаяСтавкаНДС
	|		ИЛИ СуммыДокументовВВалютахУчета.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяССылка))
	|	И СуммыДокументовВВалютахУчета.ПериодБазыНДС = ДАТАВРЕМЯ(1,1,1)
	|	И ТИПЗНАЧЕНИЯ(СуммыДокументовВВалютахУчета.Регистратор) В (&ОтборПоТипам)
	|
	//++ НЕ УТКА
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаказДавальца.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЗаказДавальца КАК ЗаказДавальца
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ЗаказДавальца.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ЗаказДавальца.Проведен
	|	И СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL
	//-- НЕ УТКА	
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ЗаказКлиента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ЗаказКлиента.Проведен
	|	И СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL
	|
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаказПереработчику.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЗаказПереработчику КАК ЗаказПереработчику
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ЗаказПереработчику.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ЗаказПереработчику.Проведен
	|	И СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL
	//-- НЕ УТ
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ЗаказПоставщику.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ЗаказПоставщику.Проведен
	|	И СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаявкаНаВозвратТоваровОтКлиента.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ЗаявкаНаВозвратТоваровОтКлиента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ЗаявкаНаВозвратТоваровОтКлиента.Проведен
	|	И СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ТипыДокументовКОбработке = СтрРазделить(ТипыДокументовКОбработке(), ",");
	ОтборПоТипам = Новый Массив;
	Для каждого ТипДокумента Из ТипыДокументовКОбработке Цикл
		ОтборПоТипам.Добавить(СтрШаблон("ТИП(%1)", СокрЛП(ТипДокумента)));
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипам", СтрСоединить(ОтборПоТипам, ","));
	
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы,
		ПолноеИмяРегистра
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() > 0 Тогда
		
		ПолноеИмяДокумента = ОбновляемыеДанные[0].Регистратор.Метаданные().ПолноеИмя();
		
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();

		Валюты = Новый Структура(
			"ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета, КэшКурсовВалют",
			ВалютаРегламентированногоУчета,
			ВалютаУправленческогоУчета,
			РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют()
		);
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОбновляемыеДанные.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ СсылкиДляОбработки
		|ИЗ
		|	&ОбновляемыеДанные КАК ОбновляемыеДанные
		|;
		|
		|ВЫБРАТЬ
		|	СсылкиДляОбработки.Регистратор                                   КАК Регистратор,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").Проведен КАК Проведен,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").ВерсияДанных КАК ВерсияДанных,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").Дата КАК Дата,
		|	МИНИМУМ(НЕ Суммы.ИдентификаторСтроки ЕСТЬ NULL)                  КАК ЕстьЗаписи,
		|	ЕСТЬNULL(Суммы.Период, ДАТАВРЕМЯ(1,1,1))                         КАК Период,
		|	ЕСТЬNULL(Суммы.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
		|	ЕСТЬNULL(Суммы.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаВзаиморасчетов,
		|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДС, 0))                            КАК СуммаБезНДС,
		|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0))                        КАК СуммаБезНДСРегл,
		|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0))                         КАК СуммаБезНДСУпр,
		|	СУММА(ЕСТЬNULL(Суммы.СуммаВзаиморасчетов, 0))                    КАК СуммаВзаиморасчетов
		|
		|ИЗ
		|	СсылкиДляОбработки КАК СсылкиДляОбработки
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
		|	ПО
		|		СсылкиДляОбработки.Регистратор = Суммы.Регистратор
		|СГРУППИРОВАТЬ ПО
		|	СсылкиДляОбработки.Регистратор,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").Проведен,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").ВерсияДанных,
		|	ВЫРАЗИТЬ(СсылкиДляОбработки.Регистратор КАК " + ПолноеИмяДокумента + ").Дата,
		|	ЕСТЬNULL(Суммы.Период, ДАТАВРЕМЯ(1,1,1)),
		|	ЕСТЬNULL(Суммы.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)),
		|	ЕСТЬNULL(Суммы.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
		|";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ИнвентаризацияНаличныхДенежныхСредств") 
				ИЛИ ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
				Продолжить;
			КонецЕсли;
			
			МетаданныеДокумента = Выборка.Регистратор.Метаданные();
			
			НачатьТранзакцию();
			
			Попытка
			
				Попытка
					
					Блокировка = Новый БлокировкаДанных;
					
					ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
					ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					
					Блокировка.Заблокировать();
					
				Исключение
					
					ОтменитьТранзакцию();
					ТекстСообщения = НСтр("ru='Не удалось заблокировать документ и его движения: %Ссылка% по причине: %Причина%';uk='Не вдалося заблокувати документ та його рухи: %Ссылка% через: %Причина%'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(
						ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Предупреждение,
						МетаданныеРегистра,
						Выборка.Регистратор,
						ТекстСообщения
					);
					Продолжить;
					
				КонецПопытки;
				
				// Был проведен при регистрации до обработки распровели
				Если НЕ Выборка.Проведен Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
					ЗафиксироватьТранзакцию();
					Продолжить;
				КонецЕсли;
				
				Если Выборка.Валюта = Справочники.Валюты.ПустаяСсылка() Тогда
					ВалютаДокумента = ВалютаРегламентированногоУчета;
				Иначе
					ВалютаДокумента = Выборка.Валюта;
				КонецЕсли;
				
				НаборЗаписей = РегистрыСведений.СуммыДокументовВВалютахУчета.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
				НаборЗаписей.Прочитать();
				
			    ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(Выборка.Регистратор, МетаданныеРегистра.Имя);
			    НовыйНаборЗаписей = ТаблицыДляДвижений["Таблица" + МетаданныеРегистра.Имя];
				НовыйНаборЗаписей.Индексы.Добавить("ИдентификаторСтроки");
				// Для нового набора записей - обход некорректного заполнения идентификаторов строк в документе
				Для каждого Запись Из НовыйНаборЗаписей Цикл
					Если ПустаяСтрока(Запись.ИдентификаторСтроки) 
						И Запись.ИдентификаторСтроки <> "" Тогда
						Запись.ИдентификаторСтроки = "";
					КонецЕсли;
				КонецЦикла;
				
				ОбъектИзменен = Ложь;

				// Существующий набор записей
				// Обход некорретного заполнения идентификаторов строк в документе
				// Заполнение ресурса БазаНДСРегл 
				// Заполнение нового СтавкаНДС по УдалитьСтавкаНДС
				// Заполнение ОбъектРасчетов, ПериодБазыНДС
				Для каждого Запись Из НаборЗаписей Цикл
					Если ПустаяСтрока(Запись.ИдентификаторСтроки) 
						И Запись.ИдентификаторСтроки <> "" Тогда
						Запись.ИдентификаторСтроки = "";
						ОбъектИзменен = Истина;
					КонецЕсли;     
					Если Запись.БазаНДСРегл = 0 Тогда
						Запись.БазаНДСРегл = Запись.СуммаБезНДСРегл;
						ОбъектИзменен = Истина;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(Запись.СтавкаНДС) тогда
						Запись.СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Запись.УдалитьСтавкаНДС);
						Запись.УдалитьСтавкаНДС = Неопределено;
						ОбъектИзменен = Истина;
					КонецЕсли;
					Если НовыйНаборЗаписей = Неопределено Тогда
						СтрокаТаблицы = Неопределено;
					Иначе
						СтрокаТаблицы = НовыйНаборЗаписей.Найти(Запись.ИдентификаторСтроки, "ИдентификаторСтроки");
					КонецЕсли;
					Если СтрокаТаблицы <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы, "ОбъектРасчетов, ПериодБазыНДС");
						ОбъектИзменен = Истина;
					КонецЕсли;
				КонецЦикла;
				
				ДокументБезБазыНДСУпр = ДокументБезБазыНДСУпр24(Выборка.Регистратор);
				ДокументССуммойВзаиморасчетов = ДокументССуммойВзаиморасчетов24(Выборка.Регистратор);
				
				МассивИдентификаторов = НовыйНаборЗаписей.Скопировать();
				МассивИдентификаторов.Свернуть("ИдентификаторСтроки");
				МассивИдентификаторов = МассивИдентификаторов.ВыгрузитьКолонку("ИдентификаторСтроки");
				
				ОбъектИзменен = Ложь;
				СуммыЗаполнены = Ложь;
				
				Для Каждого Идентификатор Из МассивИдентификаторов Цикл 
					
					СтрокиНовогоНабора = НовыйНаборЗаписей.НайтиСтроки(Новый Структура("ИдентификаторСтроки",Идентификатор));
					
					Пока СтрокиНовогоНабора.Количество() > 1 Цикл
						СтрокиНовогоНабора[0].СуммаБезНДС     = СтрокиНовогоНабора[0].СуммаБезНДС + СтрокиНовогоНабора[1].СуммаБезНДС;
						СтрокиНовогоНабора[0].СуммаНДС        = СтрокиНовогоНабора[0].СуммаНДС + СтрокиНовогоНабора[1].СуммаНДС;
						СтрокиНовогоНабора[0].СуммаБезНДСРегл = СтрокиНовогоНабора[0].СуммаБезНДСРегл + СтрокиНовогоНабора[1].СуммаБезНДСРегл;
						СтрокиНовогоНабора[0].СуммаНДСРегл    = СтрокиНовогоНабора[0].СуммаНДСРегл + СтрокиНовогоНабора[1].СуммаНДСРегл;
						СтрокиНовогоНабора[0].СуммаБезНДСУпр  = СтрокиНовогоНабора[0].СуммаБезНДСУпр + СтрокиНовогоНабора[1].СуммаБезНДСУпр;
						СтрокиНовогоНабора[0].СуммаНДСУпр     = СтрокиНовогоНабора[0].СуммаНДСУпр + СтрокиНовогоНабора[1].СуммаНДСУпр;
						СтрокиНовогоНабора[0].СуммаБезНДСУпрМУ  = СтрокиНовогоНабора[0].СуммаБезНДСУпрМУ + СтрокиНовогоНабора[1].СуммаБезНДСУпрМУ;
						СтрокиНовогоНабора[0].СуммаНДСУпрМУ     = СтрокиНовогоНабора[0].СуммаНДСУпрМУ + СтрокиНовогоНабора[1].СуммаНДСУпрМУ;
						СтрокиНовогоНабора[0].СуммаНДСПропорционально = СтрокиНовогоНабора[0].СуммаНДСПропорционально + СтрокиНовогоНабора[1].СуммаНДСПропорционально;
						Если НЕ ДокументБезБазыНДСУпр Тогда
							СтрокиНовогоНабора[0].БазаНДСРегл     = СтрокиНовогоНабора[0].БазаНДСРегл + СтрокиНовогоНабора[1].БазаНДСРегл;
						КонецЕсли;
						Если ДокументССуммойВзаиморасчетов Тогда
							СтрокиНовогоНабора[0].СуммаВзаиморасчетов  = СтрокиНовогоНабора[0].СуммаВзаиморасчетов + СтрокиНовогоНабора[1].СуммаВзаиморасчетов;
						КонецЕсли;
						НовыйНаборЗаписей.Удалить(НовыйНаборЗаписей.Индекс(СтрокиНовогоНабора[1]));
						СтрокиНовогоНабора.Удалить(1);
						ОбъектИзменен = Истина;
					КонецЦикла;
					
				КонецЦикла;
				
				Если Выборка.Проведен И НаборЗаписей.Количество() = 0 И НовыйНаборЗаписей.Количество() <> 0 Тогда
					НаборЗаписей.Загрузить(НовыйНаборЗаписей);
					ТипРасчетов = НаборЗаписей[0].ТипРасчетов;
					ОбъектИзменен = Истина;
				Иначе
					
					
					СуммыЗаполнены = Ложь;
					
					Копия = НаборЗаписей.Выгрузить();
					Для Каждого ЗаписьНовогоНабора Из НовыйНаборЗаписей Цикл
						Если НовыйНаборЗаписей.Колонки.Найти("ИдентификаторСтроки") <> Неопределено Тогда
							ИмеющаясяЗаписьКопии = Копия.Найти(ЗаписьНовогоНабора.ИдентификаторСтроки, "ИдентификаторСтроки");
						ИначеЕсли Копия.Количество() > 0 Тогда
							ИмеющаясяЗаписьКопии = Копия[0];
						Иначе
							Продолжить;
						КонецЕсли;
						
						Если ИмеющаясяЗаписьКопии <> Неопределено Тогда
							ИмеющаясяЗапись = НаборЗаписей[Копия.Индекс(ИмеющаясяЗаписьКопии)];
							Если ЗаписьНовогоНабора.СуммаБезНДСУпр <> 0 И НЕ СуммыЗаполнены Тогда
								ИмеющаясяЗапись.СуммаБезНДСУпр = ЗаписьНовогоНабора.СуммаБезНДСУпр;
								ОбъектИзменен = Истина;
							КонецЕсли;
							Если ЗаписьНовогоНабора.СуммаНДСУпр <> 0 И НЕ СуммыЗаполнены Тогда
								ИмеющаясяЗапись.СуммаНДСУпр = ЗаписьНовогоНабора.СуммаНДСУпр;
								ОбъектИзменен = Истина;
							КонецЕсли;      
							Если ЗаписьНовогоНабора.СуммаБезНДСУпрМУ <> 0 И НЕ СуммыЗаполнены Тогда
								ИмеющаясяЗапись.СуммаБезНДСУпрМУ = ЗаписьНовогоНабора.СуммаБезНДСУпрМУ;
								ОбъектИзменен = Истина;
							КонецЕсли;
							Если ЗаписьНовогоНабора.СуммаНДСУпрМУ <> 0 И НЕ СуммыЗаполнены Тогда
								ИмеющаясяЗапись.СуммаНДСУпрМУ = ЗаписьНовогоНабора.СуммаНДСУпрМУ;
								ОбъектИзменен = Истина;
							КонецЕсли;      
							Если ЗаписьНовогоНабора.СуммаНДСПропорционально <> 0 И НЕ СуммыЗаполнены Тогда
								ИмеющаясяЗапись.СуммаНДСПропорционально = ЗаписьНовогоНабора.СуммаНДСПропорционально;
								ОбъектИзменен = Истина;
							КонецЕсли;      
							Если (ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.СписаниеЗадолженности")
									ИЛИ ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями"))
								И ИмеющаясяЗаписьКопии.ТипРасчетов <> ЗаписьНовогоНабора.ТипРасчетов Тогда
								ИмеющаясяЗапись.ТипРасчетов = ЗаписьНовогоНабора.ТипРасчетов;
								ОбъектИзменен = Истина;
							КонецЕсли;
						Иначе           
							ИмеющаясяЗапись = НаборЗаписей.Добавить();
							ЗаполнитьЗначенияСвойств(ИмеющаясяЗапись, ЗаписьНовогоНабора);
							ОбъектИзменен = Истина;
						КонецЕсли;
						
						
					КонецЦикла;
				КонецЕсли;
				
				
				Если ТребуетсяРаспределение24(Выборка.Регистратор) И НЕ СуммыЗаполнены Тогда
					РассчитатьСуммыДокументовВВалютеУпр24(Выборка.Регистратор, НаборЗаписей, Выборка.Дата, Валюты, ВалютаДокумента);
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ДокументССуммойВзаиморасчетов Тогда
					Для каждого Запись Из НаборЗаписей Цикл
						Отбор = Новый Структура("ИдентификаторСтроки",Запись.ИдентификаторСтроки);
						СтрокиНовогоНабора = НовыйНаборЗаписей.НайтиСтроки(Отбор);
						Если СтрокиНовогоНабора.Количество() > 0 Тогда
							Запись.СуммаВзаиморасчетов = СтрокиНовогоНабора[0].СуммаВзаиморасчетов;
							Запись.ВалютаВзаиморасчетов = СтрокиНовогоНабора[0].ВалютаВзаиморасчетов;
							ОбъектИзменен = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				// Удалим записи, которых нет в новом наборе (для отчетов комитентам).
				Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ОтчетКомитенту")
					ИЛИ ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ОтчетКомиссионера")
					ИЛИ ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
					Сч = 0;
					Пока Сч < НаборЗаписей.Количество() Цикл
						Если НовыйНаборЗаписей.Найти(НаборЗаписей[сч].ИдентификаторСтроки, "ИдентификаторСтроки") = Неопределено Тогда
							НаборЗаписей.Удалить(сч);
							ОбъектИзменен = Истина;
						Иначе
							Сч = Сч +1;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Если ОбъектИзменен Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
				КонецЕсли;
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ТекстСообщения = НСтр("ru='Не удалось обработать документ: %Ссылка% по причине: %Причина%';uk='Не вдалося обробити документ: %Ссылка% з причини: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,
					МетаданныеРегистра,
					Выборка.Регистратор,
					ТекстСообщения
				);
				ВызватьИсключение;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

Функция ТипыДокументовКОбработке()
	
	Возврат
		"Документ.АвансовыйОтчет,
		|Документ.АктВыполненныхРабот,
		|Документ.ВзаимозачетЗадолженности,
		|Документ.ВнесениеДенежныхСредствВКассуККМ,
		|Документ.ВозвратТоваровМеждуОрганизациями,
		|Документ.ВозвратТоваровОтКлиента,
		|Документ.ВозвратТоваровПоставщику,
		|Документ.ВыкупВозвратнойТарыКлиентом,
		|Документ.ВыкупВозвратнойТарыУПоставщика,
		|Документ.ЗаказКлиента,
		|Документ.ЗаказПоставщику,
		//++ Локализация
		//-- Локализация
		|Документ.ИнвентаризацияНаличныхДенежныхСредств,
		|Документ.КорректировкаРегистров,
		|Документ.ЗаявкаНаВозвратТоваровОтКлиента,
		|Документ.ОперацияПоПлатежнойКарте,
		|Документ.ОприходованиеИзлишковТоваров,
		|Документ.ОтчетБанкаПоОперациямЭквайринга,
		|Документ.ОтчетКомиссионера,
		|Документ.ОтчетКомиссионераОСписании,
		|Документ.ОтчетКомитенту,
		|Документ.ОтчетКомитентуОСписании,
		|Документ.ОтчетОРозничныхПродажах,
		|Документ.ОтчетПоКомиссииМеждуОрганизациями,
		|Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании,
		|Документ.ПередачаТоваровМеждуОрганизациями,
		|Документ.ПоступлениеБезналичныхДенежныхСредств,
		|Документ.ПриобретениеТоваровУслуг,
		|Документ.ПриобретениеУслугПрочихАктивов,
		|Документ.ПриходныйКассовыйОрдер,
		|Документ.ПрочееОприходованиеТоваров,
		|Документ.РасходныйКассовыйОрдер,
		|Документ.РеализацияТоваровУслуг,
		|Документ.РеализацияУслугПрочихАктивов,
		|Документ.СписаниеБезналичныхДенежныхСредств,
		|Документ.ВыемкаДенежныхСредствИзКассыККМ,
		//++ НЕ УТ
		//++ Локализация
		|Документ.ВыбытиеДенежныхДокументов,
		|Документ.ПоступлениеДенежныхДокументов,
		//-- Локализация
		|Документ.ВозвратСырьяОтПереработчика,
		|Документ.ОтражениеРасхожденийПриИнкассацииДенежныхСредств,
		|Документ.ОтчетПереработчика,
		|Документ.ЗаказПереработчику,
		|Документ.ПередачаСырьяПереработчику,
		|Документ.ПоступлениеОтПереработчика,
		//-- НЕ УТ
		//++ НЕ УТКА
		|Документ.ВозвратСырьяДавальцу,
		|Документ.ЗаказДавальца,
		|Документ.ОтчетДавальцу,
		|Документ.ПоступлениеСырьяОтДавальца,
		//-- НЕ УТКА
		|Документ.СписаниеЗадолженности";
	
КонецФункции


Функция ТекстЗапросаРегистрацииДанныхКОбработке24() Экспорт
	
	МассивТекстов = Новый Массив;
	
	#Область АвансовыйОтчет
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка КАК Регистратор,
	|	ТаблицаПрочиеРасходы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет.ПрочиеРасходы КАК ТаблицаПрочиеРасходы
	|			ПО ТаблицаПрочиеРасходы.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ВЫБОР КОГДА ТаблицаДокумента.Мультивалютный
	|		ТОГДА ТаблицаПрочиеРасходы.Валюта = &ВалютаРегламентированногоУчета
	|		ИНАЧЕ ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	  КОНЕЦ
	|	И ТаблицаДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаКонвертацияВалюты.ИдентификаторСтроки
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет.КонвертацияВалюты КАК ТаблицаКонвертацияВалюты
	|			ПО ТаблицаКонвертацияВалюты.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Мультивалютный
	|	И (ТаблицаКонвертацияВалюты.Валюта = &ВалютаРегламентированногоУчета
	|		ИЛИ ТаблицаКонвертацияВалюты.ВалютаКонвертации = &ВалютаРегламентированногоУчета)
	|	И ТаблицаДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАвансовогоОтчета.Утвержден)");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область АктВыполненныхРабот
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.АктВыполненныхРабот КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот.Услуги КАК ТаблицаУслуги
	|			ПО ТаблицаУслуги.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВзаимозачетЗадолженности
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДебиторскаяЗадолженность.ИдентификаторСтроки
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТаблицаДебиторскаяЗадолженность
	|			ПО ТаблицаДебиторскаяЗадолженность.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДебиторскаяЗадолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаКредиторскаяЗадолженность.ИдентификаторСтроки
	|ИЗ
	|	Документ.ВзаимозачетЗадолженности КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности.КредиторскаяЗадолженность КАК ТаблицаКредиторскаяЗадолженность
	|			ПО ТаблицаКредиторскаяЗадолженность.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаКредиторскаяЗадолженность.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВозвратТоваровМеждуОрганизациями
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И НЕ ТаблицаДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями)
	|	И ТаблицаДокумента.Проведен");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВозвратТоваровОтКлиента
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И НЕ ТаблицаДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВозвратТоваровПоставщику
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВыкупВозвратнойТарыКлиентом
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыКлиентом КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыкупВозвратнойТарыКлиентом.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВыкупВозвратнойТарыУПоставщика
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыУПоставщика КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыкупВозвратнойТарыУПоставщика.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	

	
	#Область ОперацияПоПлатежнойКарте
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	
	#Область ОприходованиеИзлишковТоваров
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеИзлишковТоваров.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И &ВалютаУправленческогоУчета = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетБанкаПоОперациямЭквайринга
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетБанкаПоОперациямЭквайринга КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетКомиссионера
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионера.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионера.Товары КАК ТЧТовары
	|			ПО ТЧТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТЧТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета
	|	И Документ.СуммаВознаграждения <> 0");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетКомиссионераОСписании
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомиссионераОСписании КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОСписании.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетКомитенту
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомитенту КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетКомитентуОСписании
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетКомитентуОСписании КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомитентуОСписании.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетОРозничныхПродажах
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ОплатаПлатежнымиКартами КАК ТЧОплата
	|			ПО ТЧОплата.Ссылка = ТаблицаДокумента.Ссылка
	|
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры НЕ В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ПодарочныеСертификаты КАК ТаблицаПодарочныеСертификаты
	|			ПО ТаблицаПодарочныеСертификаты.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетПоКомиссииМеждуОрганизациями
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПоКомиссииМеждуОрганизациями.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И ТаблицаВидыЗапасов.ВидЗапасов.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПоКомиссииМеждуОрганизациями.Товары КАК Услуги
	|			ПО Услуги.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И Услуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|	И Услуги.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета
	|	И Документ.СуммаВознаграждения <> 0");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетПоКомиссииМеждуОрганизациямиОСписании
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаДокумента.Проведен
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПередачаТоваровМеждуОрганизациями
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	
	
	#Область ПоступлениеБезналичныхДенежныхСредств
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета
	|	И Документ.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаДокумента.ХозяйственнаяОперация НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета))");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПриобретениеТоваровУслуг
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|	)");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПриобретениеУслугПрочихАктивов
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПриходныйКассовыйОрдер
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПрочееОприходованиеТоваров
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И &ВалютаУправленческогоУчета = &ВалютаРегламентированногоУчета
	|	И ТаблицаДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область РасходныйКассовыйОрдер
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область РеализацияТоваровУслуг
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ТЧВидыЗапасов
	|			ПО ТЧВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область РеализацияУслугПрочихАктивов
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|			ПО ТаблицаДоходы.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область СписаниеБезналичныхДенежныхСредств
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета
	|	И Документ.ХозяйственнаяОперация НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеНаДепозиты),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймаСотруднику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаЛизингодателю),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеТаможне),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
	|			ПО ТЧРасшифровкаПлатежа.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаДокумента.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеНаДепозиты),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймаСотруднику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаЛизингодателю),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеТаможне),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств))");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
//++ НЕ УТ
	#Область ВозвратСырьяОтПереработчика
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратСырьяОтПереработчика КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратСырьяОтПереработчика.ВидыЗапасов КАК ТЧВидыЗапасов
	|			ПО ТЧВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ВыбытиеДенежныхДокументов
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДД.ИдентификаторСтроки
	|ИЗ
	|	Документ.ВыбытиеДенежныхДокументов КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыбытиеДенежныхДокументов.ДенежныеДокументы КАК ТаблицаДД
	|			ПО ТаблицаДД.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДД.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	
	#Область ОтчетПереработчика
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Услуги КАК Строки
	|			ПО Строки.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И Строки.Сумма <> 0");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПередачаСырьяПереработчику
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПередачаСырьяПереработчику КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаСырьяПереработчику.ВидыЗапасов КАК ТЧВидыЗапасов
	|			ПО ТЧВидыЗапасов.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПоступлениеДенежныхДокументов
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДД.ИдентификаторСтроки
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеДенежныхДокументов.ДенежныеДокументы КАК ТаблицаДД
	|			ПО ТаблицаДД.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПоступлениеОтПереработчика
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПоступлениеОтПереработчика КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеОтПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И НЕ &ПартионныйУчетВерсии22");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	
	
	#Область ИнвентаризацияНаличныхДенежныхСредств
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаКассы.ИдентификаторСтроки
	|ИЗ
	|	Документ.ИнвентаризацияНаличныхДенежныхСредств КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНаличныхДенежныхСредств.Кассы КАК ТаблицаКассы
	|			ПО ТаблицаКассы.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаКассы.СуммаРасхождения <> 0
	|	И ТаблицаКассы.Касса.ВалютаДенежныхСредств = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтражениеРасхожденийПриИнкассацииДенежныхСредств
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтражениеРасхожденийПриИнкассацииДенежныхСредств КАК Документ
	|ГДЕ
	|	Документ.Проведен
	|	И Документ.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
//-- НЕ УТ
	
//++ НЕ УТКА
	#Область ВозвратСырьяДавальцу
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратСырьяДавальцу КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратСырьяДавальцу.ВидыЗапасов КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаТовары.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ВозвратСырьяДавальцу КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратСырьяДавальцу.Товары КАК ТаблицаТовары
	|			ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ТаблицаТовары.Сумма <> 0");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ОтчетДавальцу
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ОтчетДавальцу КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
	
	#Область ПоступлениеСырьяОтДавальца
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	NULL
	|ИЗ
	|	Документ.ПоступлениеСырьяОтДавальца КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеСырьяОтДавальца.Товары КАК ДанныеТЧ
	|			ПО ДанныеТЧ.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Валюта = &ВалютаРегламентированногоУчета
	|	И ДанныеТЧ.Сумма <> 0");
	МассивТекстов.Добавить(ТекстОбъединить());
	
	#КонецОбласти
//-- НЕ УТКА
	
	#Область СписаниеЗадолженности
	
	МассивТекстов.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка,
	|	ДанныеТЧ.ИдентификаторСтроки
	|ИЗ
	|	Документ.СписаниеЗадолженности КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеЗадолженности.Задолженность КАК ДанныеТЧ
	|			ПО ДанныеТЧ.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ДанныеТЧ.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета");
	
	#КонецОбласти
	
	МассивТекстов.Добавить("
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокументов.Регистратор КАК Регистратор
	|ИЗ 
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО	
	|		ТаблицаДокументов.Регистратор = Суммы.Регистратор
	|		И (ТаблицаДокументов.ИдентификаторСтроки ЕСТЬ NULL ИЛИ ЕСТЬNULL(ТаблицаДокументов.ИдентификаторСтроки,0) = Суммы.ИдентификаторСтроки)
	|ГДЕ
	|	Суммы.Регистратор ЕСТЬ NULL");
	
	Возврат СтрСоединить(МассивТекстов,"");
	
КонецФункции

Функция ТекстОбъединить()
	Возврат "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|";
КонецФункции

Функция ДокументБезБазыНДСУпр24(ДокументСсылка)
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если ТипДокумента = Тип("ДокументСсылка.АвансовыйОтчет")
		//++ НЕ УТ
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыбытиеДенежныхДокументов")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов")
		//-- НЕ УТ
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ИнвентаризацияНаличныхДенежныхСредств")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетБанкаПоОперациямЭквайринга")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") 
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПересортицаТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.НачисленияКредитовИДепозитов") 
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПрочееОприходованиеТоваров") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ДокументССуммойВзаиморасчетов24(ДокументСсылка)
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если ТипДокумента = Тип("ДокументСсылка.АктВыполненныхРабот")
		//++ НЕ УТ
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетПереработчика")
		//-- НЕ УТ
		//++ НЕ УТКА
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцу")
		//-- НЕ УТКА
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетКомиссионера")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетКомитенту")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.СписаниеЗадолженности") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ТребуетсяРаспределение24(ДокументСсылка)
	
	СписокТипов = Новый СписокЗначений;
	
	СписокТипов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ВыкупВозвратнойТарыУПоставщика"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));
	СписокТипов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокТипов.Добавить(Тип("ДокументСсылка.РеализацияУслугПрочихАктивов"));
	
	//++ НЕ УТ
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетПереработчика"));
	//-- НЕ УТ
	
	//++ НЕ УТКА
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетДавальцу"));
	//-- НЕ УТКА
	
	Если СписокТипов.НайтиПоЗначению(ТипЗнч(ДокументСсылка)) = Неопределено Тогда
		Возврат ЛОЖЬ;
	Иначе
		Возврат ИСТИНА;
	КонецЕсли;
	
КонецФункции

Процедура РассчитатьСуммыДокументовВВалютеУпр24(ДокументСсылка, НаборЗаписей, Дата, Валюты, ВалютаДокумента)
	
	Для СчетчикТипРасчетов = 0 По 1 Цикл
		
		Если СчетчикТипРасчетов = 0 И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
			Продолжить;
		КонецЕсли;
		
		ТипРасчетов =?(СчетчикТипРасчетов = 0,Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
		
		Если ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком Тогда
			ТекстРасчета = СуммыПоставщиковВВалютеРегл24();
		Иначе
			ТекстРасчета = СуммыКлиентовВВалютеРегл24();
		КонецЕсли;
		
		Запрос = Новый Запрос();
		Запрос.Текст = ТекстРасчета;
		
		Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
		Запрос.УстановитьПараметр("ХозОперацииЗакупкаУПоставщика", ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика));
		Запрос.УстановитьПараметр("ХозОперацииЗакупкаПоИмпорту", ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту));
		
		РезультатПоРасчетам = Запрос.Выполнить();
		ВыборкаПоРасчетам = РезультатПоРасчетам.Выбрать();
		
		КорректировкаЗадолженности = Перечисления.ХозяйственныеОперации.КорректировкаЗадолженности;
		
		Для СчетчикВидДвижения = 0 По 1 Цикл
			
			ВыборкаПоРасчетам.Сбросить();
			
			СуммаДокументаУпр = 0;
			СуммаДокумента = 0;
			Для Каждого СтрокаНабораЗаписей Из НаборЗаписей Цикл
				Если (СчетчикВидДвижения = 0 И СтрокаНабораЗаписей.СуммаБезНДС < 0) 
					ИЛИ (СчетчикВидДвижения = 1 И СтрокаНабораЗаписей.СуммаБезНДС > 0) Тогда
					Если СтрокаНабораЗаписей.ТипРасчетов = ТипРасчетов Тогда
						СуммаДокумента = СуммаДокумента + СтрокаНабораЗаписей.СуммаНДС + СтрокаНабораЗаписей.СуммаБезНДС;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			КурсВалютыУпр      = 0;

			Если ВыборкаПоРасчетам.Количество() = 0 Тогда
				
				КурсВалютыДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					ВалютаДокумента,
					Дата,
					Валюты.КэшКурсовВалют
				);
					
				КурсВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					Валюты.ВалютаУправленческогоУчета,
					Дата,
					Валюты.КэшКурсовВалют
				);
				
				Если КурсВалютыУпр = 0 Тогда
					ВызватьИсключение(СтрЗаменить(НСтр("ru='Нулевой курс валюты управленческого учета на %ДатаКурса%';uk='Нульовий курс валюти управлінського обліку на %ДатаКурса%'"),"%ДатаКурса%", Формат(Дата,"ДЛФ=DD")));
				ИначеЕсли КурсВалютыДокумента = 0 Тогда
					ВызватьИсключение(СтрЗаменить(НСтр("ru='Нулевой курс валюты документа на %ДатаКурса%';uk='Нульовий курс валюти документа на %ДатаКурса%'"),"%ДатаКурса%", Формат(Дата,"ДЛФ=DD")));
				КонецЕсли;
					
				СуммаДокументаУпр = Окр(СуммаДокумента * КурсВалютыДокумента / КурсВалютыУпр,2);
				
			КонецЕсли;
			
			Пока ВыборкаПоРасчетам.Следующий() Цикл
				
				КурсВалютыВзаиморасчетов = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					ВыборкаПоРасчетам.Валюта,
					Дата,
					Валюты.КэшКурсовВалют
				);                
				
				КурсВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					Валюты.ВалютаУправленческогоУчета,
					Дата,
					Валюты.КэшКурсовВалют
				);
				
				Если КурсВалютыУпр = 0 Тогда
					ВызватьИсключение(СтрЗаменить(НСтр("ru='Нулевой курс валюты управленческого учета на %ДатаКурса%';uk='Нульовий курс валюти управлінського обліку на %ДатаКурса%'"),"%ДатаКурса%", Формат(Дата,"ДЛФ=DD")));
				ИначеЕсли КурсВалютыВзаиморасчетов = 0 Тогда
					ВызватьИсключение(СтрЗаменить(НСтр("ru='Нулевой курс валюты взаиморасчетов на %ДатаКурса%';uk='Нульовий курс валюти взаєморозрахунків на %ДатаКурса%'"),"%ДатаКурса%", Формат(Дата,"ДЛФ=DD")));
				КонецЕсли;
				
				ДолгУпр  = ВыборкаПоРасчетам.Долг * КурсВалютыВзаиморасчетов / КурсВалютыУпр;
				Если (ВыборкаПоРасчетам.ХозяйственнаяОперация = КорректировкаЗадолженности И СчетчикВидДвижения = 0)
					ИЛИ (НЕ ВыборкаПоРасчетам.ХозяйственнаяОперация = КорректировкаЗадолженности  И СчетчикВидДвижения = 1) Тогда
					СуммаДокументаУпр  = СуммаДокументаУпр + ВыборкаПоРасчетам.ПредоплатаУпр + ДолгУпр;
				КонецЕсли;
				
				УдалитьПорядокОплаты = ВыборкаПоРасчетам.УдалитьПорядокОплаты;
			КонецЦикла;

			УчтеноБазыРаспределения = 0;
			УжеРаспределеноУпр = 0;

			Для Каждого Запись Из НаборЗаписей Цикл
				
				Если (СчетчикВидДвижения = 1 И Запись.СуммаБезНДС < 0) 
					ИЛИ (СчетчикВидДвижения = 0 И Запись.СуммаБезНДС > 0) 
					ИЛИ СуммаДокумента = 0
					ИЛИ ТипРасчетов <> Запись.ТипРасчетов Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаСНДС = Запись.СуммаБезНДС + Запись.СуммаНДС;
				
				СуммаСНДСУпр  = Окр(СуммаДокументаУпр * (УчтеноБазыРаспределения + СуммаСНДС) / СуммаДокумента, 2) - УжеРаспределеноУпр;
				
				УчтеноБазыРаспределения = УчтеноБазыРаспределения + СуммаСНДС;
				УжеРаспределеноУпр      = УжеРаспределеноУпр + СуммаСНДСУпр;
				
				КурсВалютыДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					Запись.Валюта,
					Дата,
					Валюты.КэшКурсовВалют
				);
				
				Если КурсВалютыДокумента = 0 Тогда
					ВызватьИсключение(СтрЗаменить(НСтр("ru='Нулевой курс валюты документа на %ДатаКурса%';uk='Нульовий курс валюти документа на %ДатаКурса%'"),"%ДатаКурса%", Формат(Дата,"ДЛФ=DD")));
				КонецЕсли;
				
                СтавкаНДС = ?(ЗначениеЗаполнено(Запись.СтавкаНДС),
					Запись.СтавкаНДС, 
					НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Запись.УдалитьСтавкаНДС)
				);
					
				Если УдалитьПорядокОплаты = Перечисления.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте Тогда
					
					
					Запись.СуммаНДСУпр = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
						Запись.СуммаБезНДС * КурсВалютыДокумента / КурсВалютыУпр, 
						СтавкаНДС,
						Ложь
					);
					Запись.СуммаБезНДСУпр   = СуммаСНДСУпр - Запись.СуммаНДСУпр;
					Запись.СуммаНДСУпрМУ    = Запись.СуммаНДСУпр;
					Запись.СуммаБезНДСУпрМУ = Запись.СуммаБезНДСУпр;
					
				Иначе
					
					Запись.СуммаНДСУпр = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
						СуммаСНДСУпр, 
						СтавкаНДС
					);
					
					Запись.СуммаБезНДСУпр      = СуммаСНДСУпр - Запись.СуммаНДСУпр;
					СуммаНДСПропорциональноУпр = Запись.СуммаНДСПропорционально * КурсВалютыДокумента / КурсВалютыУпр; 
					Запись.СуммаБезНДСУпрМУ    = Запись.СуммаБезНДСУпр + СуммаНДСПропорциональноУпр;
					Запись.СуммаНДСУпрМУ       = Запись.СуммаНДСУпр    - СуммаНДСПропорциональноУпр;
					
				КонецЕсли;
				
				Если Запись.СуммаБезНДС + Запись.СуммаНДС < 0 Тогда
					Запись.СуммаНДСУпр = -Запись.СуммаНДСУпр;
					Запись.СуммаБезНДСУпр = -Запись.СуммаБезНДСУпр;                 
					Запись.СуммаНДСУпрМУ = -Запись.СуммаНДСУпрМУ;
					Запись.СуммаБезНДСУпрМУ = -Запись.СуммаБезНДСУпрМУ;                 
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СуммыКлиентовВВалютеРегл24()
	
	Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Возврат "
		|ВЫБРАТЬ
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ДанныеРегистра.Валюта КАК Валюта,
		|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ДанныеРегистра.УдалитьЗаказКлиента.УдалитьПорядокОплаты) КАК УдалитьПорядокОплаты,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ДанныеРегистра.ПредоплатаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ДанныеРегистра.ПредоплатаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ДанныеРегистра.Долг
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Долг
		|
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
		|
		|ГДЕ ДанныеРегистра.Регистратор = &Регистратор
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Регистратор,
		|	ДанныеРегистра.ХозяйственнаяОперация,
		|	ДанныеРегистра.Валюта
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
	Иначе
		Возврат "
		|ВЫБРАТЬ
		|	ДанныеРегистра.ДокументРегистратор КАК Регистратор,
		|	ДанныеРегистра.Валюта КАК Валюта,
		|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ДанныеРегистра.УдалитьОбъектРасчетов.УдалитьПорядокОплаты) КАК УдалитьПорядокОплаты,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|				ТОГДА ДанныеРегистра.ПредоплатаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|					ТОГДА ДанныеРегистра.ПредоплатаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|				ТОГДА ДанныеРегистра.Долг
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Долг
		|
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ДанныеРегистра
		|
		|ГДЕ ДанныеРегистра.ДокументРегистратор = &Регистратор
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.ДокументРегистратор,
		|	ДанныеРегистра.ХозяйственнаяОперация,
		|	ДанныеРегистра.Валюта
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
	КонецЕсли;
КонецФункции

Функция СуммыПоставщиковВВалютеРегл24()
	
	Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Возврат "
		|ВЫБРАТЬ
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ДанныеРегистра.Валюта КАК Валюта,
		|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ДанныеРегистра.УдалитьЗаказПоставщику.УдалитьПорядокОплаты) КАК УдалитьПорядокОплаты,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|		 		И Поступление.Организация = Аналитика.Организация 
		|			ТОГДА 
		|				ВЫБОР КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|				ТОГДА ДанныеРегистра.ПредоплатаРегл
		|				ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА ДанныеРегистра.ПредоплатаРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|		 		И Поступление.Организация = Аналитика.Организация 
		|			ТОГДА 
		|				ВЫБОР КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
		|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|				ТОГДА ДанныеРегистра.ПредоплатаУпр
		|				ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА ДанныеРегистра.ПредоплатаУпр
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			И Поступление.Организация = Аналитика.Организация
		|			ТОГДА
		|				ВЫБОР КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|			 		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
		|			 		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|				ТОГДА ДанныеРегистра.Долг
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА ДанныеРегистра.Долг
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК Долг
		|
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|	ПО Аналитика.Ссылка = ДанныеРегистра.АналитикаУчетаПоПартнерам
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Поступление
		|	ПО Поступление.Ссылка = ДанныеРегистра.Регистратор
		|
		|ГДЕ
		|	ДанныеРегистра.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Регистратор,
		|	ДанныеРегистра.ХозяйственнаяОперация,
		|	ДанныеРегистра.Валюта
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
	Иначе
		Возврат "
		|ВЫБРАТЬ
		|	ДанныеРегистра.ДокументРегистратор КАК Регистратор,
		|	ДанныеРегистра.Валюта КАК Валюта,
		|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ДанныеРегистра.УдалитьОбъектРасчетов.УдалитьПорядокОплаты) КАК УдалитьПорядокОплаты,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|				И Поступление.Организация = Аналитика.Организация 
		|			ТОГДА 
		|				ВЫБОР КОГДА Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаУПоставщика)
		|					ИЛИ Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
		|					ИЛИ Поступление.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|						ТОГДА ДанныеРегистра.ПредоплатаРегл
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ДанныеРегистра.ПредоплатаРегл
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаПриобретения
		|			ТОГДА ДанныеРегистра.ПредоплатаРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|		 		И Поступление.Организация = Аналитика.Организация 
		|			ТОГДА 
		|				ВЫБОР КОГДА Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаУПоставщика)
		|					ИЛИ Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
		|					ИЛИ Поступление.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|				ТОГДА -ДанныеРегистра.ПредоплатаУпр
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ДанныеРегистра.ПредоплатаУпр
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаПриобретения
		|			ТОГДА ДанныеРегистра.ПредоплатаУпр
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|	
		|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|			И Поступление.Организация = Аналитика.Организация
		|			ТОГДА
		|				ВЫБОР КОГДА Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаУПоставщика)
		|					ИЛИ Поступление.ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
		|			 		ИЛИ Поступление.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|				ТОГДА ДанныеРегистра.Долг
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДанныеРегистра.Долг
		|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДанныеРегистра.ДокументРегистратор ССЫЛКА Документ.КорректировкаПриобретения
		|			ТОГДА ДанныеРегистра.Долг
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|	) КАК Долг
		|
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДанныеРегистра
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|	ПО Аналитика.Ссылка = ДанныеРегистра.АналитикаУчетаПоПартнерам
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Поступление
		|	ПО Поступление.Ссылка = ДанныеРегистра.ДокументРегистратор
		|
		|ГДЕ
		|	ДанныеРегистра.ДокументРегистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.ДокументРегистратор,
		|	ДанныеРегистра.ХозяйственнаяОперация,
		|	ДанныеРегистра.Валюта
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
	КонецЕсли;
	
КонецФункции


#КонецОбласти

#КонецОбласти

#КонецЕсли
