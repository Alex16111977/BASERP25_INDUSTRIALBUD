
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПорядокУчетаОСБУ.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("21f0f7e1-1194-4506-94d6-38b6087b7253");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПорядокУчетаОСБУ.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Порядок учета ОС (бухгалтерский и налоговый учет)"": 
                                   |- заполняет по данным учета ОС версии 2.1'
                                   |;uk='Оновлює регістр ""Порядок обліку ОЗ (бухгалтерський та податковий облік)"":
                                   |- Заповнює за даними обліку ОЗ версії 2.1'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеСостоянияОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПеремещениеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОСБУ.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОСБУ.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОСБУ.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПодготовкаКПередачеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ОбъектыЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ПорядокУчетаОСБУ";
	
	
	
	ПолноеИмяОбъекта = "РегистрСведений.ПорядокУчетаОСБУ";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьНачислениеАмортизацииОСБухгалтерскийУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьНачислениеАмортизацииОСНалоговыйУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьСостоянияОСОрганизаций КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьСпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|
	|	) Регистраторы";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ВнеоборотныеАктивы.СформироватьДвиженияПриОбновленииИБ("ПорядокУчетаОСБУ", Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
