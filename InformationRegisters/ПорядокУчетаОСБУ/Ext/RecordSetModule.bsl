#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Состояние КАК Состояние,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ТаблицаПередЗаписью.СтатьяРасходовБУ КАК СтатьяРасходовБУ,
	|	ТаблицаПередЗаписью.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	ТаблицаПередЗаписью.СтатьяРасходовНУ КАК СтатьяРасходовНУ,
	|	ТаблицаПередЗаписью.АналитикаРасходовНУ КАК АналитикаРасходовНУ,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходовБУ КАК СпособОтраженияРасходовБУ,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходовНУ КАК СпособОтраженияРасходовНУ,
	|	ТаблицаПередЗаписью.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаПередЗаписью.НалоговаяГруппаОС КАК НалоговаяГруппаОС
	
	|ПОМЕСТИТЬ ПорядокУчетаОСБУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПорядокУчетаОСБУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовБУ <> ТаблицаПередЗаписью.СтатьяРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовБУ <> ТаблицаПередЗаписью.АналитикаРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовНУ <> ТаблицаПередЗаписью.СтатьяРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовНУ <> ТаблицаПередЗаписью.АналитикаРасходовНУ 
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовБУ <> ТаблицаПередЗаписью.СпособОтраженияРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовНУ <> ТаблицаПередЗаписью.СпособОтраженияРасходовНУ 
	|				ИЛИ ТаблицаПриЗаписи.НалоговоеНазначение <> ТаблицаПередЗаписью.НалоговоеНазначение
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяГруппаОС <> ТаблицаПередЗаписью.НалоговаяГруппаОС
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовБУ <> ТаблицаПередЗаписью.СтатьяРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовБУ <> ТаблицаПередЗаписью.АналитикаРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовНУ <> ТаблицаПередЗаписью.СтатьяРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовНУ <> ТаблицаПередЗаписью.АналитикаРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовБУ <> ТаблицаПередЗаписью.СпособОтраженияРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовНУ <> ТаблицаПередЗаписью.СпособОтраженияРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.НалоговоеНазначение <> ТаблицаПередЗаписью.НалоговоеНазначение
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяГруппаОС <> ТаблицаПередЗаписью.НалоговаяГруппаОС
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПередЗаписью.Организация       КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|ПОМЕСТИТЬ ПорядокУчетаОСБУ_НачислятьАмортизацию
	|ИЗ
	|	ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ ИЛИ ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПриЗаписи.Организация,
	|	ТаблицаПриЗаписи.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.Регистратор = &Регистратор
	|	И (ТаблицаПриЗаписи.НачислятьАмортизациюБУ
	|		ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОСБУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПорядокУчетаОСБУИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

