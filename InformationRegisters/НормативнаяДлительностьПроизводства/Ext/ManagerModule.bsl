#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Рассчитывает нормативную длительность производства
//
// Параметры:
//  Задание				 - Структура - описание задания к расчету.
//  ПропуститьЗадание	 - Булево	 - признак, требуется пропустить выполнение задания (возвращаемое значение).
//  ДополнительныеДанные - Структура - дополнительные данные, используемые при обработке очереди
//
Процедура ВыполнитьРасчет(Задание, ПропуститьЗадание = Ложь, ДополнительныеДанные = Неопределено) Экспорт
	
	ЗамерВремени = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РегистрСведений.НормативнаяДлительностьПроизводства.МодульМенеджера.Рассчитать");
	
	Рассчитать(Задание.ОбъектРасчета, Задание.ПолныйРасчет, ДополнительныеДанные);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ЗамерВремени, 1);
	
КонецПроцедуры

// Возвращает длительность производства по спецификации
//
// Параметры:
//  Спецификация - СправочникСсылка.РесурсныеСпецификация, Массив - спецификации.
// 
// Возвращаемое значение:
//   - Структура, Соответствие - длительность производства.
//
Функция ПолучитьДлительность(Спецификация) Экспорт
	
	ТекстЗапроса = ТекстЗапросаДлительностиПроизводстваПоСпецификации();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивСсылок", ПроизводствоСервер.МассивЗначений(Спецификация));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТипЗнч(Спецификация) <> Тип("Массив") Тогда
		
		Результат = СтруктураДанныхЗаписиРегистра();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЕсли;
		
	Иначе
		
		Результат = Новый Соответствие;
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Данные = СтруктураДанныхЗаписиРегистра();
				ЗаполнитьЗначенияСвойств(Данные, Выборка);
				Результат.Вставить(Выборка.Спецификация, Данные);
			КонецЦикла;
		КонецЕсли;
		
		Для каждого Элемент Из Спецификация Цикл
			Если Результат.Получить(Элемент) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Данные = СтруктураДанныхЗаписиРегистра();
			Результат.Вставить(Элемент, Данные);
		КонецЦикла;

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетДлительности

Процедура Рассчитать(ОбъектРасчета, ПолныйРасчет = Ложь, ДополнительныеДанные = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьОшибки = Ложь;
	ЕстьИзменения = Ложь;
	
	// расчет нормативной длительности производства
	
	РасчетПоСпецификации = ( ТипЗнч(ОбъектРасчета) = Тип("СправочникСсылка.РесурсныеСпецификации") );
	Если РасчетПоСпецификации Тогда
		
		Спецификация = ОбъектРасчета;
		
		Если ПолныйРасчет Тогда
			ЕстьОшибки = ЕстьОшибки ИЛИ Не РегистрыСведений.НормативнаяДлительностьЭтаповПроизводства.Рассчитать(Спецификация);
		КонецЕсли;
		
		Если Не ЕстьОшибки Тогда
			
			РезультатРасчета = ВычислитьДлительность(Спецификация);
			
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НормативнаяДлительностьПроизводства");
				ЭлементБлокировки.УстановитьЗначение("Спецификация", Спецификация);
				
				Блокировка.Заблокировать();
				
				ДлительностьДоИзменения = ПолучитьДлительность(Спецификация);
				
				ЕстьИзменения = Не ДлительностиРавны(РезультатРасчета, ДлительностьДоИзменения);
				Если ЕстьИзменения Тогда
					ЗаписатьРезультатРасчета(Спецификация, РезультатРасчета);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				СобытиеЖурналаРегистрации = ПроизводствоСервер.СобытиеРасчетНормативнойДлительности();
				
				ТекстСообщения = СтрШаблон(НСтр("ru='Не удалось обработать спецификацию: %1 по причине: %2';uk='Не вдалося обробити специфікацію: %1 з причини: %2'"),
									Спецификация,
									ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Спецификация.Метаданные(), Спецификация, ТекстСообщения);
				
				ЕстьОшибки = Истина;
				ЕстьИзменения = Ложь;
			
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// добавление в очередь расчета зависимых объектов
	
	Если ЕстьИзменения Или Не РасчетПоСпецификации Тогда
		
		ДлительностьЗависимых = Новый Соответствие;
		
		ЗависимыеОбъекты = ЗависимыеОбъектыРасчета(ОбъектРасчета);
		
		Если ЗначениеЗаполнено(ЗависимыеОбъекты) Тогда
			
			Если РасчетПоСпецификации Тогда
				
				ДлительностьЗависимых = ПолучитьДлительность(ЗависимыеОбъекты);
				Для каждого КлючИЗначение Из ДлительностьЗависимых Цикл
					Если КлючИЗначение.Значение.Предел  >  Макс(РезультатРасчета.ПлановаяДлительность,
																ДлительностьДоИзменения.ПлановаяДлительность)
						И ( КлючИЗначение.Значение.ПовтИсп = РезультатРасчета.ПовтИсп
							ИЛИ РезультатРасчета.ПовтИсп = ДлительностьДоИзменения.ПовтИсп ) Тогда
						ЗависимыеОбъекты.Удалить(ЗависимыеОбъекты.Найти(КлючИЗначение.Ключ));
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			//++ НЕ УТКА
			ПроизвестиКонтрольЗацикленностиРасчета(ОбъектРасчета, ЗависимыеОбъекты, ДополнительныеДанные);
			//-- НЕ УТКА
			
			Для каждого ЗависимыйОбъект Из ЗависимыеОбъекты Цикл
				ПолныйРасчет = Не РасчетПоСпецификации ИЛИ (ДлительностьЗависимых[ЗависимыйОбъект] <> Неопределено
															И ДлительностьЗависимых[ЗависимыйОбъект].ЕстьВложенныеСпецификации);
				РегистрыСведений.ЗаданияКРасчетуДлительностиПроизводства.ДобавитьЗадание(ЗависимыйОбъект, ПолныйРасчет, Ложь);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ТекстЗапросаДлительностиПроизводстваПоСпецификации()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДлительностьПроизводства.Спецификация КАК Спецификация,
	|
	|	ДлительностьПроизводства.Длительность             КАК Длительность,
	|	ДлительностьПроизводства.ПлановаяДлительность     КАК ПлановаяДлительность,
	|	ДлительностьПроизводства.МаксимальнаяДлительность КАК МаксимальнаяДлительность,
	|
	|	ДлительностьПроизводства.Предел                    КАК Предел,
	|	ДлительностьПроизводства.ЕстьВложенныеСпецификации КАК ЕстьВложенныеСпецификации,
	|
	|	ДлительностьПроизводства.ПовтИсп КАК ПовтИсп
	|ИЗ
	|	РегистрСведений.НормативнаяДлительностьПроизводства КАК ДлительностьПроизводства
	|ГДЕ
	|	ДлительностьПроизводства.Спецификация В (&МассивСсылок)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетСвязанныхСпецификаций(Отбор = Неопределено)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Этап   КАК Этап,
	|
	|	Таблица.Спецификация   КАК Спецификация,
	|	Таблица.Альтернативная КАК Альтернативная,
	|
	|	МАКСИМУМ(Таблица.ВходящаяСпецификация)  КАК ВходящаяСпецификация,
	|	МАКСИМУМ(Таблица.ИсходящаяСпецификация) КАК ИсходящаяСпецификация,
	|	МАКСИМУМ(Таблица.ВложеннаяСпецификация) КАК ВложеннаяСпецификация
	|
	|ПОМЕСТИТЬ ВтСвязанныеСпецификации
	|ИЗ
	|(
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Этап КАК Этап,
	|	Таблица.Спецификация КАК Спецификация,
	|	ЛОЖЬ КАК Альтернативная,
	|	ЛОЖЬ КАК ВходящаяСпецификация,
	|	ИСТИНА КАК ИсходящаяСпецификация,
	|	ЛОЖЬ КАК ВложеннаяСпецификация
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.ОбработатьПоСпецификации
	|	И Таблица.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Этап,
	|	Таблица.Спецификация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.ОбработатьПоСпецификации
	|	И Таблица.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Этап,
	|	Таблица.ИсточникПолученияПолуфабриката,
	|	Таблица.Альтернативный,
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.ПроизводитсяВПроцессе
	|	И Таблица.СпособПолученияМатериала = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации)
	|	И Таблица.ИсточникПолученияПолуфабриката <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Этап,
	|	Таблица.СпецификацияРемонта,
	|	Таблица.Альтернативный,
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.ПроизводитсяВПроцессе
	|	И Таблица.СпецификацияРемонта <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Этап,
	|	СпецификацииИзделий.Спецификация,
	|	Таблица.Альтернативный,
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК Таблица
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииИзделий КАК СпецификацииИзделий
	|		ПО Таблица.Номенклатура.ВидНоменклатуры = СпецификацииИзделий.ВидНоменклатуры
	|			И СпецификацииИзделий.Номенклатура В (Таблица.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И СпецификацииИзделий.Характеристика В (ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка), Таблица.Характеристика)
	|			И СпецификацииИзделий.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
	|			И СпецификацииИзделий.ПобочныйВыход = ЛОЖЬ
	|
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И Таблица.ПроизводитсяВПроцессе
	|	И Таблица.СпособПолученияМатериала = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации)
	|	И Таблица.ИсточникПолученияПолуфабриката = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Ссылка,
	|	Таблица.Этап,
	|	Таблица.Спецификация,
	|	Таблица.Альтернативная
	|";
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		
		Если Отбор.Свойство("Спецификация") Тогда
			
			ТекстОтбора =
				""
				+ "Таблица.Ссылка В "
				+ "("
				+ Отбор.Спецификация
				+ ")";
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица.Ссылка = &Спецификация", ТекстОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетДлительностиПроизводстваПоСпецификации()
	
	ТекстЗапроса = ТекстЗапросаРасчетСвязанныхСпецификаций()
						+ ОбщегоНазначения.РазделительПакетаЗапросов();
		
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Этап   КАК Этап,
	|
	|	Таблица.Спецификация   КАК Спецификация,
	|	Таблица.Альтернативная КАК Альтернативная,
	|
	|	Таблица.ВходящаяСпецификация  КАК ВходящаяСпецификация,
	|	Таблица.ИсходящаяСпецификация КАК ИсходящаяСпецификация,
	|	Таблица.ВложеннаяСпецификация КАК ВложеннаяСпецификация,
	|
	|	ISNULL(ДлительностьСпецификаций.ПлановаяДлительность, 0)     КАК ПлановаяДлительность,
	|	ISNULL(ДлительностьСпецификаций.МаксимальнаяДлительность, 0) КАК МаксимальнаяДлительность,
	|
	|	ISNULL(ДлительностьСпецификаций.ПовтИсп, ЛОЖЬ) КАК ПовтИсп
	|
	|ПОМЕСТИТЬ ВтДлительностьСпецификаций
	|ИЗ
	|	ВтСвязанныеСпецификации КАК Таблица
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативнаяДлительностьПроизводства КАК ДлительностьСпецификаций
	|		ПО Таблица.Спецификация = ДлительностьСпецификаций.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	Таблица.Спецификация КАК Спецификация,
	|
	|	Таблица.Длительность КАК Длительность,
	|	Таблица.ПлановаяДлительность КАК ПлановаяДлительность,
	|	Таблица.МаксимальнаяДлительность КАК МаксимальнаяДлительность,
	|
	|	Таблица.Предел КАК Предел,
	|	Таблица.ЕстьВложенныеСпецификации КАК ЕстьВложенныеСпецификации,
	|
	|	Таблица.ПовтИсп И
	|	 НЕ ИСТИНА В
	|		(ВЫБРАТЬ
	|			ИСТИНА
	|		ИЗ
	|			Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ГДЕ
	|			Таблица.Спецификация = РесурсныеСпецификации.Ссылка
	|			И ( РесурсныеСпецификации.ОптимальнаяПартияВыпуска <> 0
	|				ИЛИ РесурсныеСпецификации.МинимальнаяПартияВыпуска <> 0
	|				ИЛИ РесурсныеСпецификации.ЕстьПараметризацияРесурсов
	|				ИЛИ РесурсныеСпецификации.ЕстьАвтовыборСпецификацийВПроцессе
	|				ИЛИ РесурсныеСпецификации.ЕстьРасчетВероятности
	|				ИЛИ РесурсныеСпецификации.ЕстьНекратныйВыпуск
	|				ИЛИ РесурсныеСпецификации.ЕстьНекратныеНормативыВРЦ)
	|	) КАК ПовтИсп
	|
	|ИЗ (
	|
	|ВЫБРАТЬ
	|	ПроизводственныйПроцесс.Спецификация КАК Спецификация,
	|
	|	МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончания) КАК Длительность,
	|
	|	МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончания + ВЫБОР
	|			КОГДА ЕСТЬNULL(ДлительностьСпецификаций.ВходящаяСпецификация И НЕ ДлительностьСпецификаций.Альтернативная, ЛОЖЬ)
	|				ТОГДА ЕСТЬNULL(ДлительностьСпецификаций.ПлановаяДлительность, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ)
	|		+ ВЫБОР КОГДА МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|							КОГДА ДлительностьСпецификаций.ИсходящаяСпецификация И НЕ ДлительностьСпецификаций.Альтернативная
	|							ТОГДА ДлительностьСпецификаций.ПлановаяДлительность
	|							ИНАЧЕ 0
	|						КОНЕЦ + ПроизводственныйПроцесс.ДнейДоОкончания + ПроизводственныйПроцесс.ДнейОтНачала, 0)) - МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончания) > 0
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|							КОГДА ДлительностьСпецификаций.ИсходящаяСпецификация И НЕ ДлительностьСпецификаций.Альтернативная
	|							ТОГДА ДлительностьСпецификаций.ПлановаяДлительность
	|							ИНАЧЕ 0
	|						КОНЕЦ + ПроизводственныйПроцесс.ДнейДоОкончания + ПроизводственныйПроцесс.ДнейОтНачала, 0)) - МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончания)
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|			КАК ПлановаяДлительность,
	|
	|	МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончанияМакс + ВЫБОР
	|			КОГДА ЕСТЬNULL(ДлительностьСпецификаций.ВходящаяСпецификация, ЛОЖЬ)
	|				ТОГДА ЕСТЬNULL(ДлительностьСпецификаций.МаксимальнаяДлительность, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ)
	|		+ ВЫБОР КОГДА МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|						КОГДА ДлительностьСпецификаций.ИсходящаяСпецификация
	|							ТОГДА ДлительностьСпецификаций.МаксимальнаяДлительность
	|						ИНАЧЕ 0
	|					КОНЕЦ + ПроизводственныйПроцесс.ДнейДоОкончанияМакс + ПроизводственныйПроцесс.ДнейОтНачалаМакс, 0)) - МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончанияМакс) > 0
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|							КОГДА ДлительностьСпецификаций.ИсходящаяСпецификация
	|								ТОГДА ДлительностьСпецификаций.МаксимальнаяДлительность
	|							ИНАЧЕ 0
	|						КОНЕЦ + ПроизводственныйПроцесс.ДнейДоОкончанияМакс + ПроизводственныйПроцесс.ДнейОтНачалаМакс, 0)) - МАКСИМУМ(ПроизводственныйПроцесс.ДнейДоОкончанияМакс)
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|			КАК МаксимальнаяДлительность,
	|
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПроизводственныйПроцесс.Этап) > 1
	|			ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ЕСТЬNULL(ВЫБОР
	|						КОГДА НЕ ДлительностьСпецификаций.Альтернативная
	|							ТОГДА ДлительностьСпецификаций.ПлановаяДлительность
	|						ИНАЧЕ 0
	|					КОНЕЦ, 0))
	|	КОНЕЦ                                                                    КАК Предел,
	|	МАКСИМУМ(ЕСТЬNULL(ДлительностьСпецификаций.ВложеннаяСпецификация, ЛОЖЬ)) КАК ЕстьВложенныеСпецификации,
	|
	|	МИНИМУМ(ЕСТЬNULL(ДлительностьСпецификаций.ПовтИсп, ИСТИНА)) КАК ПовтИсп
	|
	|ИЗ
	|	РегистрСведений.НормативнаяДлительностьЭтаповПроизводства КАК ПроизводственныйПроцесс
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДлительностьСпецификаций КАК ДлительностьСпецификаций
	|
	|			ПО ПроизводственныйПроцесс.Спецификация = ДлительностьСпецификаций.Ссылка
	|				И ПроизводственныйПроцесс.Этап      = ДлительностьСпецификаций.Этап
	|ГДЕ
	|	ПроизводственныйПроцесс.Спецификация = &Спецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроизводственныйПроцесс.Спецификация
	|
	|) КАК Таблица
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВычислитьДлительность(Спецификация)
	
	ТекстЗапроса = ТекстЗапросаРасчетДлительностиПроизводстваПоСпецификации();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Результат = СтруктураДанныхЗаписиРегистра();
		
	Иначе
		
		Результат = СтруктураДанныхЗаписиРегистра();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗаписатьРезультатРасчета(Спецификация, РезультатРасчета, ВызовИзОбработчикаОбновления = Ложь)
	
	Набор = РегистрыСведений.НормативнаяДлительностьПроизводства.СоздатьНаборЗаписей();
	
	Набор.Отбор.Спецификация.Установить(Спецификация);
	
	НоваяЗапись = Набор.Добавить();
	
	ЗаполнитьЗначенияСвойств(НоваяЗапись, РезультатРасчета);
	НоваяЗапись.Спецификация = Спецификация;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВызовИзОбработчикаОбновления Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
	Иначе
		Набор.Записать(Истина);
	КонецЕсли;
	
КонецФункции

//++ НЕ УТКА
Процедура ПроизвестиКонтрольЗацикленностиРасчета(ОбъектРасчета, ЗависимыеОбъекты, ДополнительныеДанные)
	
	Если НЕ ЗначениеЗаполнено(ЗависимыеОбъекты) Тогда
		Возврат;
	КонецЕсли;
	
	Граф = Неопределено;
	Если НЕ ДополнительныеДанные.Свойство("ГрафКонтроляЗацикленности", Граф) Тогда
		Граф = УправлениеПроизводством.Граф();
	КонецЕсли;
	
	РасчетЗациклен = Ложь;
	СписокОшибок   = Новый Массив(2);
	
	ВершинаОбъектРасчета = УправлениеПроизводством.ПолучитьВершинуГрафаПоСсылке(Граф, ОбъектРасчета);
	Для каждого ЗависимыйОбъект Из ЗависимыеОбъекты Цикл
		Если ОбъектРасчета = ЗависимыйОбъект Тогда
			РасчетЗациклен  = Истина;
			СписокОшибок[0] = ОбъектРасчета;
		Иначе
			УправлениеПроизводством.ДобавитьСмежнуюВершинуВСписокСмежности(ВершинаОбъектРасчета, ЗависимыйОбъект);
			УправлениеПроизводством.ПолучитьВершинуГрафаПоСсылке(Граф, ЗависимыйОбъект);
		КонецЕсли;
	КонецЦикла;
	
	РасчетЗациклен = РасчетЗациклен ИЛИ УправлениеПроизводством.ЕстьЦиклыВГрафе(Граф, СписокОшибок[0], СписокОшибок[1]);
	Если РасчетЗациклен Тогда
		
		НачатьТранзакцию();
		Попытка
			
			Для каждого Спецификация Из СписокОшибок Цикл
				
				Если НЕ ЗначениеЗаполнено(Спецификация) Тогда
					Продолжить;
				КонецЕсли;
				
				Блокировка = Новый БлокировкаДанных;
				Блокировка.Добавить("РегистрСведений.НормативнаяДлительностьПроизводства").УстановитьЗначение("Спецификация", Спецификация);
				Блокировка.Заблокировать();
				
				ЗаписатьРезультатРасчета(Спецификация, СтруктураДанныхЗаписиРегистра(), Истина);
				
				Индекс = ЗависимыеОбъекты.Найти(Спецификация);
				Если Индекс <> Неопределено Тогда
					ЗависимыеОбъекты.Удалить(Индекс);
				КонецЕсли;
				
				ТекстСообщения = СтрШаблон(НСтр("ru='Не удалось рассчитать длительность спецификации %1 из-за зацикливания расчета.';uk='Не вдалося розрахувати тривалість специфікації %1 з причини зациклення розрахунку.'"),
					Спецификация);
				
				ЗаписьЖурналаРегистрации(
					ПроизводствоСервер.СобытиеРасчетНормативнойДлительности(),
					УровеньЖурналаРегистрации.Ошибка,
					Спецификация.Метаданные(),
					Спецификация,
					ТекстСообщения);
				
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ОбъектРасчета);
		КонецПопытки;
	
	КонецЕсли;
	
	ДополнительныеДанные.Вставить("ГрафКонтроляЗацикленности", Граф);
	
КонецПроцедуры
//-- НЕ УТКА

#КонецОбласти

#Область ЗависимыеОбъектыРасчета

Функция ЗависимыеОбъектыРасчета(ОбъектРасчета)
	
	ТипИсточника = ТипЗнч(ОбъектРасчета);
	
	Если ТипИсточника <> Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Владелец КАК Спецификация
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК Таблица
		|	//ТекстСоединение
		|ГДЕ
		|	Таблица.Владелец.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
		|	И НЕ Таблица.ПометкаУдаления
		|	И НЕ Таблица.Владелец.ПометкаУдаления
		|	И //ТекстОтбор";
		
		ТекстСоединение = "";
		Если ТипИсточника = Тип("СправочникСсылка.Календари") Тогда
			ТекстСоединение = "
			|ЛЕВОЕ СОЕДИНЕНИЕ Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарьПредприятия
			|ПО ИСТИНА";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстСоединение", ТекстСоединение);
		
		ТекстОтбор = "ЛОЖЬ";
		Если ТипИсточника = Тип("СправочникСсылка.Календари") Тогда
			ТекстОтбор = "
			|ВЫБОР
			|	КОГДА Таблица.Подразделение.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
			|		ТОГДА ОсновнойКалендарьПредприятия.Значение
			|	ИНАЧЕ Таблица.Подразделение.ГрафикРаботы
			|КОНЕЦ = &ОбъектРасчета";
		ИначеЕсли ТипИсточника = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ТекстОтбор = "Таблица.Подразделение = &ОбъектРасчета";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстОтбор", ТекстОтбор);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ОбъектРасчета", ОбъектРасчета);
		
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
		
	Иначе
		
		Возврат Справочники.РесурсныеСпецификации.ЗависимыеСпецификации(ОбъектРасчета);
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция СтруктураДанныхЗаписиРегистра()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Длительность", 0);
	
	Результат.Вставить("ПлановаяДлительность", 0);
	Результат.Вставить("МаксимальнаяДлительность", 0);

	Результат.Вставить("Предел", -1);
	Результат.Вставить("ЕстьВложенныеСпецификации", Ложь);
	
	Результат.Вставить("ПовтИсп", Ложь);
	
	Результат.Вставить("Версия", Версия());
	
	Возврат Результат;
	
КонецФункции

Функция Версия()
	
	Возврат "2.0";
	
КонецФункции

Функция ДлительностиРавны(Длительность1, Длительность2)
	
	Для Каждого Свойство Из Длительность1 Цикл
		
		Если Длительность1[Свойство.Ключ] <> Длительность2[Свойство.Ключ] Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.НормативнаяДлительностьПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b9b7d5cd-a46a-47e3-8349-f870bdcba6a9");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.НормативнаяДлительностьПроизводства.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет служебный регистр ""Нормативная длительность производства"".';uk='Заповнює службовий регістр ""Нормативна тривалість виробництва"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.НормативнаяДлительностьПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.РесурсныеСпецификации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.НормативнаяДлительностьПроизводства.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.НормативнаяДлительностьПроизводства.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";      
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию22";
	НоваяСтрока.Порядок = "После";       
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию24";
	НоваяСтрока.Порядок = "После";          

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.НормативнаяДлительностьПроизводства.ПолноеИмя();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Спецификация КАК Спецификация
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		РесурсныеСпецификации.Ссылка КАК Спецификация
	|	ИЗ
	|		Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативнаяДлительностьПроизводства КАК НормативнаяДлительностьПроизводства
	|			ПО НормативнаяДлительностьПроизводства.Спецификация = РесурсныеСпецификации.Ссылка
	|		
	|	ГДЕ
	|		НормативнаяДлительностьПроизводства.Спецификация ЕСТЬ NULL
	|		И РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
	|	
	|	) КАК Таблица
	|");
	
	Запрос.УстановитьПараметр("Версия", Версия());
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.НормативнаяДлительностьПроизводства";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ИмяВременнойТаблицы = "ВТДляОбработки";
	ДопПараметры.ВыбиратьПорциями = Истина;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра,
		МенеджерВременныхТаблиц,
		ДопПараметры);
		
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТаблицыДляЧтения = Новый Массив;
	ТаблицыДляЧтения.Добавить("Справочник.РесурсныеСпецификации");
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
		Параметры.Очередь,
		ТаблицыДляЧтения,
		МенеджерВременныхТаблиц);
	
	СтруктураОтбора = Новый Структура(
		"Спецификация",
		"ВЫБРАТЬ ВТДляОбработки.Спецификация ИЗ ВТДляОбработки КАК ВТДляОбработки");
	
	ТекстЗапроса = ТекстЗапросаРасчетСвязанныхСпецификаций(СтруктураОтбора)
						+ ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтСвязанныеСпецификации.Ссылка КАК Спецификация
	|ПОМЕСТИТЬ ВТНеобработанныеСтроки
	|ИЗ
	|	ВтСвязанныеСпецификации КАК ВтСвязанныеСпецификации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДляОбработки КАК ВТДляОбработки
	|		ПО (ВТДляОбработки.Спецификация = ВтСвязанныеСпецификации.Спецификация)
	|ГДЕ
	|	НЕ ВТДляОбработки.Спецификация ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДляОбработки.Спецификация КАК Спецификация
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ВТДляОбработки.Спецификация
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					ВТНеобработанныеСтроки КАК ВТНеобработанныеСтроки
	|				ГДЕ
	|					ВТНеобработанныеСтроки.Спецификация = ВТДляОбработки.Спецификация)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборИзменен = Ложь;
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
				ЭлементБлокировки.УстановитьЗначение("Спецификация", Выборка.Спецификация);
				Блокировка.Заблокировать();
				
				РезультатРасчета = ВычислитьДлительность(Выборка.Спецификация);
				ЗаписатьРезультатРасчета(Выборка.Спецификация, РезультатРасчета, Истина);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Спецификация);
				Продолжить;
			КонецПопытки;
			
		КонецЦикла;
		
//++ НЕ УТКА
	Иначе
		
		СписокОшибок = Новый Массив(2);
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ * ИЗ
		|(
		|ВЫБРАТЬ
		|	ВтСвязанныеСпецификации.Ссылка       КАК Вершина,
		|	ВтСвязанныеСпецификации.Спецификация КАК СмежнаяВершина
		|ИЗ
		|	ВтСвязанныеСпецификации КАК ВтСвязанныеСпецификации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДляОбработки КАК ВТДляОбработки
		|		ПО (ВТДляОбработки.Спецификация = ВтСвязанныеСпецификации.Спецификация)
		|ГДЕ
		|	НЕ ВТДляОбработки.Спецификация ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВтСвязанныеСпецификации.Спецификация,
		|	ВтСвязанныеСпецификации.Ссылка
		|ИЗ
		|	ВтСвязанныеСпецификации КАК ВтСвязанныеСпецификации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДляОбработки КАК ВТДляОбработки
		|		ПО (ВТДляОбработки.Спецификация = ВтСвязанныеСпецификации.Спецификация)
		|ГДЕ
		|	НЕ ВТДляОбработки.Спецификация ЕСТЬ NULL
		|
		|) КАК Таблица
		|
		|УПОРЯДОЧИТЬ ПО
		|	Вершина
		|");
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Граф = УправлениеПроизводством.Граф(Запрос.Выполнить());
		ЕстьЦиклыВГрафе = УправлениеПроизводством.ЕстьЦиклыВГрафе(Граф, СписокОшибок[0], СписокОшибок[1]);
		
		Если ЕстьЦиклыВГрафе Тогда
		
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				Блокировка.Добавить(ПолноеИмяРегистра).УстановитьЗначение("Спецификация", СписокОшибок[0]);
				Блокировка.Добавить(ПолноеИмяРегистра).УстановитьЗначение("Спецификация", СписокОшибок[1]);
				Блокировка.Заблокировать();
				
				ЗаписатьРезультатРасчета(СписокОшибок[0], СтруктураДанныхЗаписиРегистра(), Истина);
				ЗаписатьРезультатРасчета(СписокОшибок[1], СтруктураДанныхЗаписиРегистра(), Истина);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), СписокОшибок[0]);
			КонецПопытки;
			
		КонецЕсли;
		
//-- НЕ УТКА
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
