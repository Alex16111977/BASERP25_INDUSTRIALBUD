#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыСведений.СостоянияИРеквизитыДоставки.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b432a2ef-93c9-4d20-ae05-ba890fa67a9a");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СостоянияИРеквизитыДоставки.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.СостоянияИРеквизитыДоставки";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.СостоянияИРеквизитыДоставки";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.СостоянияИРеквизитыДоставки";
	Обработчик.Комментарий = НСтр("ru='Исправляет значение служебного реквизита ""Адрес значения полей"".
    |Пока обработчик не выполнен, возможны ошибки при создании новых заданий на перевозку.'
    |;uk='Виправляє значення службового реквізиту ""Адреса значення полів"".
    |Поки обробник не виконаний, можливі помилки під час створення нових завдань перевезення.'");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт 
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.СостоянияИРеквизитыДоставки");
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписьРегистра.Распоряжение          КАК Распоряжение,
	|	ЗаписьРегистра.Склад                 КАК Склад,
	|	ЗаписьРегистра.ПолучательОтправитель КАК ПолучательОтправитель,
	|	ЗаписьРегистра.Перевозчик            КАК Перевозчик,
	|	ЗаписьРегистра.СпособДоставки        КАК СпособДоставки,
	|	ЗаписьРегистра.Зона                  КАК Зона
	|ИЗ
	|	РегистрСведений.СостоянияИРеквизитыДоставки КАК ЗаписьРегистра
	|ГДЕ
	|	(ЗаписьРегистра.Адрес <> """"
	|		И (
	|			(ВЫРАЗИТЬ(ЗаписьРегистра.АдресЗначенияПолей КАК СТРОКА(21))) = ""<КонтактнаяИнформация""
	|				И НЕ ЗаписьРегистра.АдресЗначенияПолей ПОДОБНО ""%</КонтактнаяИнформация>""
	|			ИЛИ (ВЫРАЗИТЬ(ЗаписьРегистра.АдресЗначенияПолей КАК СТРОКА(21))) <> ""<КонтактнаяИнформация""
	|				И ЗаписьРегистра.АдресЗначенияПолей ПОДОБНО &ДлинаНевалидногоJSON
	|			)
	|	)";
	
	ДлинаНевалидногоJSON = "";
	Для Счетчик = 1 По 1000 Цикл
		ДлинаНевалидногоJSON = ДлинаНевалидногоJSON + "_";
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ДлинаНевалидногоJSON", ДлинаНевалидногоJSON);
	
	ЗаписиКРегистрации = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ЗаписиКРегистрации, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	ДанныеКОбработке = ОбновлениеИнформационнойБазы.ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра(),
		ДополнительныеПараметры);
	
	Пока ДанныеКОбработке.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра());
			ЭлементБлокировки.УстановитьЗначение("Распоряжение",          ДанныеКОбработке.Распоряжение);
			ЭлементБлокировки.УстановитьЗначение("Склад",                 ДанныеКОбработке.Склад);
			ЭлементБлокировки.УстановитьЗначение("ПолучательОтправитель", ДанныеКОбработке.ПолучательОтправитель);
			ЭлементБлокировки.УстановитьЗначение("Перевозчик",            ДанныеКОбработке.Перевозчик);
			ЭлементБлокировки.УстановитьЗначение("СпособДоставки",        ДанныеКОбработке.СпособДоставки);
			ЭлементБлокировки.УстановитьЗначение("Зона",                  ДанныеКОбработке.Зона);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СостоянияИРеквизитыДоставки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Распоряжение.Установить(ДанныеКОбработке.Распоряжение);
			НаборЗаписей.Отбор.Склад.Установить(ДанныеКОбработке.Склад);
			НаборЗаписей.Отбор.ПолучательОтправитель.Установить(ДанныеКОбработке.ПолучательОтправитель);
			НаборЗаписей.Отбор.Перевозчик.Установить(ДанныеКОбработке.Перевозчик);
			НаборЗаписей.Отбор.СпособДоставки.Установить(ДанныеКОбработке.СпособДоставки);
			НаборЗаписей.Отбор.Зона.Установить(ДанныеКОбработке.Зона);
			
			НаборЗаписей.Прочитать();
			
			ОбъектИзменен = Ложь;
			
			Для Каждого ЗаписьРегистра Из НаборЗаписей Цикл
				Если Не ПустаяСтрока(ЗаписьРегистра.АдресЗначенияПолей) Тогда
					НовыеЗначенияПолей = "";
					ОбщегоНазначенияУТ.ЗаполнитьЗначенияПолейКИПоПредставлению(ЗаписьРегистра.Адрес, НовыеЗначенияПолей);
					ЗаписьРегистра.АдресЗначенияПолей = НовыеЗначенияПолей;
					ОбъектИзменен = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ДанныеКОбработке.Распоряжение);
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра());
	
КонецПроцедуры

Функция ПолноеИмяРегистра()
	
	Возврат "РегистрСведений.СостоянияИРеквизитыДоставки";
	
КонецФункции


#КонецОбласти

#КонецОбласти

#КонецЕсли