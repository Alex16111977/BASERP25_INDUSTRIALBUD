#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Метод возвращает значение константы "Номер задания",
//считанной при разделяемой блокировке
Функция ПолучитьНомерЗадания() Экспорт
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Константа.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Блокировка.Заблокировать();
	НомерЗадания = Константы.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов.Получить();
	
	ЗафиксироватьТранзакцию();
	
	Возврат НомерЗадания;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоличествоНеактуальныхДокументов(НачалоРасчета, КонецРасчета, АналитикиРасчета = Неопределено) Экспорт
	Запрос = Новый Запрос("
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		КОЛИЧЕСТВО(Расчеты.Регистратор) КАК Количество
	|	ИЗ
	|		РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК Расчеты
	|	ГДЕ
	|		Расчеты.Период МЕЖДУ &НачалоРасчета И &КонецРасчета
	|		И (Расчеты.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|			ИЛИ Расчеты.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|			ИЛИ &ПоВсемАналитикам)
	|");
	
	Запрос.УстановитьПараметр("НачалоРасчета", НачалоРасчета);
	Запрос.УстановитьПараметр("КонецРасчета", КонецРасчета);
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", НЕ Значениезаполнено(АналитикиРасчета));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		КоличествоДокументов = 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоДокументов = Выборка.Количество;
	КонецЕсли;
	Возврат КоличествоДокументов;
КонецФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("537c11bb-837e-46a6-b2e3-ece72de4732d");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ОбъектРасчетов';uk='Заповнює реквізит ОбъектРасчетов'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры = Неопределено) Экспорт
	
	ПолноеИмяРегистра = СоздатьНаборЗаписей().Метаданные().ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов КАК ЗаданияКФормированиюИсходящихНалоговыхДокументов
	|ГДЕ
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить(), 
		ДополнительныеПараметры
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда
			Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	1 КАК Приоритет,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов КАК ЗаданияКФормированиюИсходящихНалоговыхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Организация = ОбъектыРасчетов.Организация
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Партнер = ОбъектыРасчетов.Партнер
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Контрагент = ОбъектыРасчетов.Контрагент
	|		И ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов = ОбъектыРасчетов.Объект
	|ГДЕ
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День = &День
	|	И НЕ ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	2 КАК Приоритет,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов КАК ЗаданияКФормированиюИсходящихНалоговыхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов = ОбъектыРасчетов.Объект
	|ГДЕ
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День = &День
	|	И НЕ ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	3 КАК Приоритет,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов КАК ЗаданияКФормированиюИсходящихНалоговыхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Организация = ОбъектыРасчетов.Организация
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|		И ОбъектыРасчетов.Объект = НЕОПРЕДЕЛЕНО
	|		И ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Контрагент = ОбъектыРасчетов.Контрагент
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Партнер = ОбъектыРасчетов.Партнер
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Договор = ОбъектыРасчетов.Договор
	|		И ЗаданияКФормированиюИсходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.НаправлениеДеятельности = ОбъектыРасчетов.НаправлениеДеятельности
	|ГДЕ
	|	ЗаданияКФормированиюИсходящихНалоговыхДокументов.День = &День
	|	И ЗаданияКФормированиюИсходящихНалоговыхДокументов.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектовРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	
	Для Каждого ПорцияДанных Из ОбновляемыеДанные Цикл
	
		НачатьТранзакцию();
		
		Попытка
		
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("День", ПорцияДанных.День);
			Блокировка.Заблокировать();
			
			Запрос.УстановитьПараметр("День", ПорцияДанных.День);
			ОбъектыРасчетов = Запрос.Выполнить().Выгрузить();
			ОбъектыРасчетов.Индексы.Добавить("АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов");
			
			НаборЗаписей = РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.День.Установить(ПорцияДанных.День);
			НаборЗаписей.Прочитать();
			
			Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если ЗаписьНабора.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
					СтруктураПоиска = Новый Структура(
						"АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов, Приоритет",
						ЗаписьНабора.АналитикаУчетаПоПартнерам,
						ЗаписьНабора.УдалитьОбъектРасчетов,
						1
					);
					НайденныйОбъектРасчетов = ОбъектыРасчетов.НайтиСтроки(СтруктураПоиска);
					
					Если НайденныйОбъектРасчетов.Количество() = 1 Тогда 
						ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
					Иначе
						СтруктураПоиска = Новый Структура(
							"АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов",
							ЗаписьНабора.АналитикаУчетаПоПартнерам,
							ЗаписьНабора.УдалитьОбъектРасчетов
						);               
						
						НайденныйОбъектРасчетов = ОбъектыРасчетов.НайтиСтроки(СтруктураПоиска);
						
						НайденоНесколькоОбъектовРасчетов = Ложь;
					
						Если НайденныйОбъектРасчетов.Количество() = 0 И ЗначениеЗаполнено(ЗаписьНабора.УдалитьОбъектРасчетов) Тогда
							ВызватьИсключение(
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не найден объект расчетов для источника данных: %1';uk='Не знайдений об''єкт розрахунків для джерела даних: %1'"),
									ЗаписьНабора.УдалитьОбъектРасчетов
								)
							);
						ИначеЕсли НайденныйОбъектРасчетов.Количество() = 1 Тогда 
							ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
						ИначеЕсли НайденныйОбъектРасчетов.Количество() = 2 Тогда
							Если НайденныйОбъектРасчетов[0].Приоритет <> НайденныйОбъектРасчетов[1].Приоритет Тогда
								Если ЗначениеЗаполнено(НайденныйОбъектРасчетов[0].ОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
								ИначеЕсли ЗначениеЗаполнено(НайденныйОбъектРасчетов[1].ОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[1].ОбъектРасчетов;
								Иначе
									НайденоНесколькоОбъектовРасчетов = Истина;
								КонецЕсли;
							Иначе
								Если Не ЗначениеЗаполнено(ЗаписьНабора.УдалитьОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
								Иначе
									НайденоНесколькоОбъектовРасчетов = Истина;
								КонецЕсли;
							КонецЕсли;
						Иначе
							НайденоНесколькоОбъектовРасчетов = Истина;
						КонецЕсли;
						
						Если НайденоНесколькоОбъектовРасчетов Тогда
							ВызватьИсключение(
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Для источника данных: %1 найдено несколько объектов расчетов. Заполнение невозможно.';uk='Для джерела даних %1 знайдено кілька об''єктів розрахунків. Заповнення неможливе.'"),
									ЗаписьНабора.УдалитьОбъектРасчетов
								)
							);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
		
			ОтменитьТранзакцию();
			
			Шаблон = НСтр("ru='Не удалось записать данные в регистр %1 , по причине: %2';uk='Не вдалося записати дані в регістр %1, з причини: %2'");
			ТекстСообщения = СтрШаблон(
				Шаблон,
				ПолноеИмяРегистра,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеРегистра,
				,
				ТекстСообщения
			);
		
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли