
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("d2414f29-0b46-4c0a-83dd-d0417fbb46c0");
	Обработчик.Многопоточный = Истина;
	Обработчик.Процедура = "РегистрыСведений.ВариантыПереупаковки.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ВариантыПереупаковки.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.ВариантыПереупаковки,"
		+ "Справочник.УпаковкиЕдиницыИзмерения";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ВариантыПереупаковки";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ВариантыПереупаковки";
	Обработчик.Комментарий = НСтр("ru='Переформирует записи в регистре """"Варианты переупаковки""""';uk='Переформує записи в регістрі """"Варіанти перепакування""""'");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ВариантыПереупаковки";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ВариантыПереупаковки");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УпаковкиНоменклатуры.Ссылка КАК МаксимальнаяУпаковкаВВетви
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.Владелец <> &БазовыеЕдиницыИзмерения
	|	И Не УпаковкиНоменклатуры.ПометкаУдаления";
	Запрос.УстановитьПараметр("БазовыеЕдиницыИзмерения", Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения);
	
	Данные = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления УТ 3.5.4
// Процедура перезаполняет варианты переупаковки. 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ВариантыПереупаковки";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	ОбновляемыеДанные.МаксимальнаяУпаковкаВВетви КАК МаксимальнаяУпаковкаВВетви
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ВТДляОбработки.МаксимальнаяУпаковкаВВетви КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Ссылка
	|ПОМЕСТИТЬ УпаковкиДляОбработки
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УпаковкиДляОбработки.Ссылка.Владелец КАК Владелец
	|ПОМЕСТИТЬ ВладельцыУпаковок
	|ИЗ
	|	УпаковкиДляОбработки КАК УпаковкиДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпаковкиЕдиницыИзмерения.Ссылка КАК МаксимальнаяУпаковкаВВетви,
	|	ВладельцыУпаковок.Владелец КАК Владелец
	|ИЗ
	|	ВладельцыУпаковок КАК ВладельцыУпаковок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО ВладельцыУпаковок.Владелец = УпаковкиЕдиницыИзмерения.Владелец
	|ИТОГИ ПО
	|	Владелец";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	ВыборкаПоВладельцу = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоВладельцу.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			ТаблицаВариантовПереупаковки = Справочники.УпаковкиЕдиницыИзмерения.СформироватьТаблицуВариантовПереупаковкиНаСервере(ВыборкаПоВладельцу.Владелец);
			
			ТЗМаксимальныеУпаковкиВВетви = ТаблицаВариантовПереупаковки.Скопировать(,"МаксимальнаяУпаковкаВВетви");
			ТЗМаксимальныеУпаковкиВВетви.Свернуть("МаксимальнаяУпаковкаВВетви");
			МаксимальныеУпаковкиВВетви = ТЗМаксимальныеУпаковкиВВетви.ВыгрузитьКолонку("МаксимальнаяУпаковкаВВетви");
			
			Для Каждого МаксимальнаяУпаковкаВВетви Из МаксимальныеУпаковкиВВетви Цикл
				Отбор = Новый Структура("МаксимальнаяУпаковкаВВетви", МаксимальнаяУпаковкаВВетви);
				ВариантыПереупаковки = ТаблицаВариантовПереупаковки.Скопировать(Отбор);
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
				ЭлементБлокировки.УстановитьЗначение("МаксимальнаяУпаковкаВВетви", МаксимальнаяУпаковкаВВетви);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				Набор = РегистрыСведений.ВариантыПереупаковки.СоздатьНаборЗаписей();
				Набор.Отбор.МаксимальнаяУпаковкаВВетви.Установить(МаксимальнаяУпаковкаВВетви);
				Набор.Прочитать();
				Набор.Загрузить(ВариантыПереупаковки);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			КонецЦикла;
			
			Выборка = ВыборкаПоВладельцу.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если МаксимальныеУпаковкиВВетви.Найти(Выборка.МаксимальнаяУпаковкаВВетви) = Неопределено Тогда
					НаборЗаписей = РегистрыСведений.ВариантыПереупаковки.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.МаксимальнаяУпаковкаВВетви.Установить(Выборка.МаксимальнаяУпаковкаВВетви);
					НаборЗаписей.Очистить();
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
				КонецЕсли;
			КонецЦикла;
			
			//Очистить записи в регистре по максимальным упаковкам в ветви более неиспользуемым
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВариантыПереупаковки.МаксимальнаяУпаковкаВВетви КАК МаксимальнаяУпаковкаВВетви
			|ИЗ
			|	РегистрСведений.ВариантыПереупаковки КАК ВариантыПереупаковки
			|ГДЕ
			|	НЕ ВариантыПереупаковки.МаксимальнаяУпаковкаВВетви В (&МаксимальнаяУпаковкаВВетви)
			|	И ВариантыПереупаковки.МаксимальнаяУпаковкаВВетви.Владелец = &Владелец";
			Запрос.УстановитьПараметр("МаксимальнаяУпаковкаВВетви", МаксимальныеУпаковкиВВетви);
			Запрос.УстановитьПараметр("Владелец", ВыборкаПоВладельцу.Владелец);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.ВариантыПереупаковки.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.МаксимальнаяУпаковкаВВетви.Установить(Выборка.МаксимальнаяУпаковкаВВетви);
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать();
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось рассчитать переупаковку для упаковок по владельцу %Владелец% по причине: %Причина%';uk='Не вдалося розрахувати переупаковку для упаковок за власником %Владелец% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Владелец%", ВыборкаПоВладельцу.Владелец);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Метаданные.РегистрыСведений.ВариантыПереупаковки,
									ВыборкаПоВладельцу.Владелец,
									ТекстСообщения);
									
			Продолжить;
			
		КонецПопытки;
			
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);

КонецПроцедуры

#КонецОбласти

	
#КонецЕсли