#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПервоначальныеСведенияНМА.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ee1529fa-c98f-4a32-bd0e-3b5a116a7831");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПервоначальныеСведенияНМА.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Первоначальные сведения НМА"":
|- заполняет по данным учета НМА версии 2.1'
|;uk='Оновлює регістр ""Початкові відомості НМА"":
|- заповнює за даними обліку НМА версії 2.1'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПереоценкаНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.МодернизацияНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровНМА.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПервоначальныеСведенияНМАБухгалтерскийУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПервоначальныеСведенияНМАНалоговыйУчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ПервоначальныеСведенияНМА";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрИсточник.Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПервоначальныеСведенияНМАБухгалтерскийУчет КАК РегистрИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = РегистрИсточник.Регистратор
	|	ГДЕ
	|		РегистрПриемник.Регистратор ЕСТЬ NULL
	|		И НЕ РегистрИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РегистрИсточник.Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПервоначальныеСведенияНМАНалоговыйУчет КАК РегистрИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = РегистрИсточник.Регистратор
	|	ГДЕ
	|		РегистрПриемник.Регистратор ЕСТЬ NULL
	|		И НЕ РегистрИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	) Регистраторы";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы, 
		ПолноеИмяОбъекта
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ПервоначальныеСведенияНМА";

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;

	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Документ.ВводОстатковВнеоборотныхАктивов");
	ЧитаемыеДанные.Добавить("Документ.ПереоценкаНМА");
	ЧитаемыеДанные.Добавить("Документ.ПодготовкаКПередачеНМА");
	ЧитаемыеДанные.Добавить("Документ.ПринятиеКУчетуНМА");
	ЧитаемыеДанные.Добавить("Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций");
	ЧитаемыеДанные.Добавить("Документ.МодернизацияНМА");
	ЧитаемыеДанные.Добавить("Документ.ИзменениеПараметровНМА");
	ЧитаемыеДанные.Добавить("Документ.СписаниеНМА");
								
	Если ЧитаемыеДанные.Количество() <> 0 
			И ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, ЧитаемыеДанные) 
		ИЛИ ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "РегистрСведений.РеестрДокументов") Тогда
		// Обновление не начнется пока есть необработанные документы, т.к. нужно обрабатывать записи в хронологическом порядке.
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ВыбиратьПорциями = Ложь;
	
	ПараметрыОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
		Параметры.Очередь,
		Неопределено, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц,
		ДопПараметры
	);
	
	Если НЕ ПараметрыОбработки.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ ПараметрыОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
	|	СсылкиДляОбработки.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора) КАК Дата,
	|	ЕСТЬNULL(РеестрДокументов.Организация, ДанныеПервичныхДокументов.Организация) КАК Организация
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО РеестрДокументов.Ссылка = СсылкиДляОбработки.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ДанныеПервичныхДокументов.Документ = СсылкиДляОбработки.Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДляОбработки", ПараметрыОбработки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;

	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияНМА.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМА.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);

			Если НаборЗаписей.Количество() = 0 Тогда
				ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка);
			КонецЕсли;			
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
			КонецЕсли; 
			
			ОбъектовОбработано = ОбъектовОбработано + 1;

			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ТекстСообщения = НСтр("ru='Не удалось обработать записи регистра ""Первоначальные сведения НМА"": %Ссылка% по причине: %Причина%';uk='Не вдалося обробити записи регістру ""Початкові відомості НМА"": %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА,
				Выборка.Регистратор,
				ТекстСообщения
			);
			
		КонецПопытки;
		
	КонецЦикла;
	
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА.ПолноеИмя()
	);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка)

	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Проведен") <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистра = Неопределено;
	
	ТипДокумента = ТипЗнч(Выборка.Регистратор);	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуНМА") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.НематериальныйАктив КАК НематериальныйАктив,
		|	ИсходныйРегистрБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимостьБУ,
		|	ИсходныйРегистрНУ.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
		|	ИсходныйРегистрБУ.СпособНачисленияАмортизации КАК МетодНачисленияАмортизацииБУ,
		|	ИсходныйРегистрНУ.СпособНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
		|	ДанныеДокумента.Дата КАК ДатаПринятияКУчетуБУ,
		|	ДанныеДокумента.Ссылка КАК ДокументПринятияКУчетуБУ
		|ИЗ
		|	Документ.ПринятиеКУчетуНМА КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияНМАБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = ДанныеДокумента.Ссылка
		|			И ИсходныйРегистрБУ.НематериальныйАктив = ДанныеДокумента.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияНМАНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = ДанныеДокумента.Ссылка
		|			И ИсходныйРегистрНУ.НематериальныйАктив = ДанныеДокумента.НематериальныйАктив
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Регистратор";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПереоценкаНМА")
	  ИЛИ ТипДокумента = Тип("ДокументСсылка.МодернизацияНМА")
	  ИЛИ ТипДокумента = Тип("ДокументСсылка.СписаниеНМА")
	  ИЛИ ТипДокумента = Тип("ДокументСсылка.ИзменениеПараметровНМА")
	  ИЛИ ТипДокумента = Тип("ДокументСсылка.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций") Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив
		|ПОМЕСТИТЬ втТаблицаНМА
		|ИЗ
		|	РегистрСведений.УдалитьПервоначальныеСведенияНМАБухгалтерскийУчет КАК ТаблицаНМА
		|ГДЕ
		|	ТаблицаНМА.Регистратор = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Дата КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ИсходныйРегистрБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимостьБУ,  
		|	ИсходныйРегистрНУ.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
		|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
		|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
		|	ПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
		|	ПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания
		|ИЗ
		|	втТаблицаНМА КАК ТаблицаНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И НематериальныйАктив В
		|						(ВЫБРАТЬ
		|							втТаблицаНМА.НематериальныйАктив
		|						ИЗ
		|							втТаблицаНМА КАК втТаблицаНМА)) КАК ПервоначальныеСведенияНМА
		|		ПО ПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|			И ПервоначальныеСведенияНМА.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияНМАБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияНМАНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрНУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив";

		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.Регистратор);
		Если ТипДокумента = Тип("ДокументСсылка.ПереоценкаНМА") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИсходныйРегистрНУ.ПервоначальнаяСтоимостьНУ", "ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ");
		КонецЕсли;
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	Иначе
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор);
		ТаблицыДляДвижений = МенеджерДокумента.ДанныеДокументаДляПроведения(Выборка.Регистратор, "ПервоначальныеСведенияНМА");
		ТаблицыДляДвижений.Свойство("ТаблицаПервоначальныеСведенияНМА", ТаблицаРегистра);
	КонецЕсли;

	Если ТаблицаРегистра <> Неопределено Тогда
		НаборЗаписей.Загрузить(ТаблицаРегистра);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
