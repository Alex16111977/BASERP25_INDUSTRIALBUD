#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПервоначальныеСведенияОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("18a37d35-aab4-4772-8fc6-df649d629050");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПервоначальныеСведенияОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Первоначальные сведения ОС"":
|- заполняет ресурс ""Первоначальная стоимость НУ""
|- заполняет по данным учета ОС версии 2.1'
|;uk='Оновлює регістр ""Початкові відомості ОЗ"":
|- заповнює ресурс ""Первісна вартість ПО""
|- заповнює за даними обліку ОЗ версії 2.1'");
	
	Читаемые = Новый Массив;
    Читаемые.Добавить(Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ДанныеПервичныхДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыЛизинга.ПолноеИмя());
	//++ Локализация
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПеремещениеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеПредметовЛизинга.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьУчетнаяПолитикаОрганизаций.ПолноеИмя());
	//-- Локализация
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПодготовкаКПередачеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ВнеоборотныеАктивыЛокализация.ЗарегистрироватьДанныеКОбработкеРегистраПервоначальныеСведенияОСДляПереходаНаНовуюВерсию(Параметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ПервоначальныеСведенияОС";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
	
	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Документ.ВводОстатковВнеоборотныхАктивов");
							
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, ЧитаемыеДанные) 
		ИЛИ ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "РегистрСведений.РеестрДокументов") Тогда
		// Обновление не начнется пока есть необработанные документы, т.к. нужно обрабатывать записи в хронологическом порядке.
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ВыбиратьПорциями = Ложь;
	
	ПараметрыОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
		Параметры.Очередь,
		Неопределено, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц,
		ДопПараметры);
	
	Если НЕ ПараметрыОбработки.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ ПараметрыОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
	|	СсылкиДляОбработки.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора) КАК Дата,
	|	ЕСТЬNULL(РеестрДокументов.Организация, ДанныеПервичныхДокументов.Организация) КАК Организация
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО РеестрДокументов.Ссылка = СсылкиДляОбработки.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ДанныеПервичныхДокументов.Документ = СсылкиДляОбработки.Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДляОбработки", ПараметрыОбработки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;

	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияОС.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияОС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка);
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
			КонецЕсли; 
			
			ОбъектовОбработано = ОбъектовОбработано + 1;

			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ТекстСообщения = НСтр("ru='Не удалось обработать записи регистра ""Первоначальные сведения ОС"": %Ссылка% по причине: %Причина%';uk='Не вдалося обробити записи регістру ""Початкові відомості ОЗ"": %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ПервоначальныеСведенияОС,
				Выборка.Регистратор,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		Метаданные.РегистрыСведений.ПервоначальныеСведенияОС.ПолноеИмя()
	);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка)

	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Проведен") <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистра = Неопределено;
	
	ТипДокумента = ТипЗнч(Выборка.Регистратор);	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СпособНачисленияАмортизации КАК МетодНачисленияАмортизацииБУ,
		|	ИсходныйРегистрНУ.СпособНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимостьБУ,
		|	ИсходныйРегистрНУ.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
		|	ДанныеДокумента.Дата КАК ДатаВводаВЭксплуатациюБУ,
		|	ДанныеДокумента.Ссылка КАК ДокументВводаВЭксплуатациюБУ,
		|	ИсходныйРегистрБУ.ДоговорЛизинга КАК Договор
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияОСНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Регистратор";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций") Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Регистратор = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Дата КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимостьБУ,
		|	ИсходныйРегистрНУ.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДокументСнятияСУчета КАК ДокументСнятияСУчета,
		|	ПервоначальныеСведенияОС.Контрагент КАК Контрагент,
		|	ПервоначальныеСведенияОС.Договор КАК Договор
		|ИЗ
		|	втТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПервоначальныеСведенияОСНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство";

		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	Иначе
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор);
		ТаблицыДляДвижений = МенеджерДокумента.ДанныеДокументаДляПроведения(Выборка.Регистратор, "ПервоначальныеСведенияОС");
		ТаблицыДляДвижений.Свойство("ТаблицаПервоначальныеСведенияОС", ТаблицаРегистра);
	КонецЕсли;

	Если ТаблицаРегистра <> Неопределено Тогда
		НаборЗаписей.Загрузить(ТаблицаРегистра);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
