#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ТаблицаПередЗаписью.Договор КАК Договор
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Регистратор               КАК Документ,
	|	Таблица.Период             КАК Период,
	|	Таблица.Организация        КАК Организация,
	|	Таблица.ОсновноеСредство   КАК ОсновноеСредство,
	|	Таблица.Договор            КАК Договор,
	|	МАКСИМУМ(Таблица.ОтражатьВРеглУчете) КАК ОтражатьВРеглУчете,
	|	МАКСИМУМ(Таблица.ОтражатьВУпрУчете)  КАК ОтражатьВУпрУчете
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.Договор           КАК Договор,
	|
	|		ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|			ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|			ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ КАК ОтражатьВРеглУчете,
	|
	|		ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|			ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|			ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, ДЕНЬ)
	|			ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		ПервоначальныеСведенияОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL 
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииНУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ ТаблицаПриЗаписи.Договор <> ТаблицаПередЗаписью.Договор
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, ДЕНЬ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство,
	|		ТаблицаПриЗаписи.Договор,
	|
	|		ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|			ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|			ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ КАК ОтражатьВРеглУчете,
	|
	|		ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|			ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|			ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, ДЕНЬ)
	|			ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииНУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ ТаблицаПриЗаписи.Договор <> ТаблицаПередЗаписью.Договор
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ <> ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.ОсновноеСредство,
	|	Таблица.Договор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "УНИЧТОЖИТЬ " + ИмяРегистра + "ПередЗаписью";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПервоначальныеСведенияОСИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
