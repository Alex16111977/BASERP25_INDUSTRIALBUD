#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытийФормы

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ВидыСчетовЗаписываемыеВРегистрТребующихсяНастроек = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.НастраиваемыеВидыСчетов();
	МассивЗаписейДляУдаления = Новый Массив;
	КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонСообщения = НСтр("ru='При записи счетов требующих настроек, ошибка: для вида счета ""%1""';uk='При записі рахунків що вимагають настройок, помилка: для виду рахунку ""%1""'",КодОсновногоЯзыка);
	ОбъектМетаданных = Метаданные.РегистрыСведений.СчетаРеглУчетаТребующиеНастройки;
	ОтражаемыйДокумент = Неопределено;
	ДополнительныеСвойства.Свойство("Документ", ОтражаемыйДокумент);
	
	Для каждого СтрокаРегистра Из ЭтотОбъект Цикл
		
		Если ВидыСчетовЗаписываемыеВРегистрТребующихсяНастроек.Найти(СтрокаРегистра.ВидСчета) = Неопределено Тогда
			// Данный вид счета не настраивается в рабочем месте, поэтому его запись в данный регистр не имеет смысла.
			МассивЗаписейДляУдаления.Добавить(СтрокаРегистра);
			Продолжить;
		КонецЕсли;
		
		ОбязательныеИзмеренияРегистра = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.СписокИзмеренийРегистраПоВидуСчета(СтрокаРегистра.ВидСчета, Истина);
		ОбязательныеИзмеренияРегистра.Добавить("ВидСчета");
		
		Для каждого ИзмерениеРегистра Из ОбязательныеИзмеренияРегистра Цикл
			Если Не ЗначениеЗаполнено(СтрокаРегистра[ИзмерениеРегистра]) Тогда
				ТекстСообщения = ШаблонСообщения + " " + НСтр("ru='не заполнено измерение регистра ""%2"".';uk='не заповнено вимір регістра ""%2"".'",КодОсновногоЯзыка);
				ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаРегистра.ВидСчета, ИзмерениеРегистра);
				ЗаписьЖурналаРегистрации(НСтр("ru='Отражение в регламентированном учете';uk='Відображення в регламентованому обліку'",КодОсновногоЯзыка), 
					УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, ОтражаемыйДокумент, ТекстСообщения);
				МассивЗаписейДляУдаления.Добавить(СтрокаРегистра);
			КонецЕсли;
		КонецЦикла;
		
		РазделУчета = НастройкаСчетовУчетаКлиентСервер.РазделУчетаПоИмениСчета(XMLСтрока(СтрокаРегистра.ВидСчета));
		ОграничениеТипаМестоУчета = НастройкаСчетовУчета.НастройкиМестаУчетаПоРазделуУчета(РазделУчета).ТипЗначения;
		Если ОграничениеТипаМестоУчета <> Неопределено И Не ОграничениеТипаМестоУчета.СодержитТип(ТипЗнч(СтрокаРегистра.МестоУчета)) И Не РазделУчета = "Номенклатура" Тогда
			ТекстСообщения = ШаблонСообщения + " " + НСтр("ru='тип значения измерения ""Место учета"" (%2) не соответствует требуемому.';uk='тип значення вимірювання ""Місце обліку"" (%2) не відповідає необхідному.'",КодОсновногоЯзыка);
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаРегистра.ВидСчета, ТипЗнч(СтрокаРегистра.МестоУчета));
			ЗаписьЖурналаРегистрации(НСтр("ru='Отражение в регламентированном учете';uk='Відображення в регламентованому обліку'",КодОсновногоЯзыка), 
				УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, ОтражаемыйДокумент, ТекстСообщения);
			МассивЗаписейДляУдаления.Добавить(СтрокаРегистра);
		КонецЕсли;
		
		ОграничениеТипаАналитикаУчета = НастройкаСчетовУчета.НастройкиАналитикиУчетаПоРазделуУчета(РазделУчета).ТипЗначения;
		Если ОграничениеТипаАналитикаУчета <> Неопределено И Не ОграничениеТипаАналитикаУчета.СодержитТип(ТипЗнч(СтрокаРегистра.АналитикаУчета)) Тогда
			ТекстСообщения = ШаблонСообщения + " " + НСтр("ru='тип значения измерения ""Аналитика учета"" (%2) не соответствует требуемому.';uk='тип значення вимірювання ""Аналітика обліку"" (%2) не відповідає необхідному.'",КодОсновногоЯзыка);
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаРегистра.ВидСчета, ТипЗнч(СтрокаРегистра.АналитикаУчета));
			ЗаписьЖурналаРегистрации(НСтр("ru='Отражение в регламентированном учете';uk='Відображення в регламентованому обліку'",КодОсновногоЯзыка), 
				УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, ОтражаемыйДокумент, ТекстСообщения);
			МассивЗаписейДляУдаления.Добавить(СтрокаРегистра);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивЗаписейДляУдаления.Количество() Тогда
		Для каждого ЗаписьДляУдаления Из МассивЗаписейДляУдаления Цикл
			ЭтотОбъект.Удалить(ЗаписьДляУдаления);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли