
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПорядокУчетаОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("5bf1ad42-f6df-4036-8aeb-36fadeeaf6db");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПорядокУчетаОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Порядок учета ОС"":
|- заполняет новый реквизит ""Организация""
|- заполняет по данным учета ОС версии 2.1'
|;uk='Оновлює регістр ""Порядок обліку ОЗ"":
| - заповнює новий реквізит ""Організація""
| - заповнює за даними обліку ОЗ версії 2.1'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


КонецПроцедуры
 
// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ПорядокУчетаОС";
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаписиРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС КАК ЗаписиРегистра
	|ГДЕ
	|	ЗаписиРегистра.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ЗаписиРегистра.Регистратор ССЫЛКА Документ.КорректировкаРегистров";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
	ПолноеИмяОбъекта = "РегистрСведений.ПорядокУчетаОС";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПеремещениеОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.СписаниеОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.МодернизацияОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПереоценкаОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПеремещениеОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТаблицаИсточник.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьСчетаБухгалтерскогоУчетаОС КАК ТаблицаИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ТаблицаПолучатель
	|			ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|	ГДЕ
	|		ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПеремещениеОС
	|		И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	) Регистраторы";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ВнеоборотныеАктивы.СформироватьДвиженияПриОбновленииИБ("ПорядокУчетаОС", Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
