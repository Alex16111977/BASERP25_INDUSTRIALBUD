#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Метод выводит в табличный документ предупреждение,
// если отчет формируетя по неактуальным данным
// Параметры:
//	Макет - ТабличныйДокумент - Макет, в который выводится предупреждение
//	ПараметрыРасчета - Структура - Свойства, в которых указаны данные о границах расчета.
Процедура ВывестиАктуальностьРасчета(Макет, ПараметрыРасчета) Экспорт
	Если ЗначениеЗаполнено(ПараметрыРасчета) И ПараметрыРасчета.Свойство("ГраницаВзаиморасчетов") Тогда
		Если ЗначениеЗаполнено(ПараметрыРасчета.ГраницаВзаиморасчетов) Тогда
			ТаблицаПредупреждение = Новый ТабличныйДокумент;
			ОбластьПредупреждение = ТаблицаПредупреждение.Область(1,1,1,1);
			Если Константы.АктуализироватьВзаиморасчетыПриФормированииОтчетов.Получить() Тогда
				ТекстПредупреждения = НСтр("ru='Распределение расчетов выполнено до %ДатаАктуальности%. 
|Запущено задание по распределению расчетов с %ДатаНачалаРаспределения% (требуется распределить расчеты для %КоличествоДокументов%). 
|После распределения Вам будет предложено переформировать отчет.'
|;uk='Розподіл розрахунків виконано до %ДатаАктуальности%.
|Запущено завдання з розподілу розрахунків з %ДатаНачалаРаспределения% (потрібно розподілити розрахунки для %КоличествоДокументов%). 
|Після розподілу Вам буде запропоновано переформувати звіт.'");
			Иначе
				ТекстПредупреждения = НСтр("ru='Распределение расчетов выполнено до %ДатаАктуальности%. 
|Необходимо восстановить взаиморасчеты из формы закрытия месяца (пункт ""Формирование движений по расчетам с партнерами (контрагентами)"",
|либо запустить регламентное задание ""Выполнение отложенных движений по расчетам с клиентами\поставщиками"".'
|;uk='Розподіл розрахунків виконано до %ДатаАктуальности%.
|Необхідно відновити взаєморозрахунки з форми закриття місяця (пункт ""Формування рухів за розрахунками з партнерами (контрагентами)"",
|або запустити регламентне завдання ""Виконання відкладених рухів за розрахунками з клієнтами \ постачальниками"".'");
			КонецЕсли;
			ДатаАктуальности = КонецМесяца(ПараметрыРасчета.ГраницаВзаиморасчетов - 1);
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%ДатаАктуальности%", Формат(ДатаАктуальности, "ДЛФ=D"));
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%ДатаНачалаРаспределения%", Формат(ПараметрыРасчета.ГраницаВзаиморасчетов, "ДЛФ=D"));
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%КоличествоДокументов%", ОбщегоНазначенияУТ.ЧислоДокументовПрописью(ПараметрыРасчета.КРасчету));
			ОбластьПредупреждение.Текст = ТекстПредупреждения;
			ОбластьПредупреждение.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			Макет.ВставитьОбласть(ОбластьПредупреждение, Макет.Область(1,1,1,1), ТипСмещенияТабличногоДокумента.ПоВертикали);
		Иначе
			ПараметрыРасчета.Удалить("ГраницаВзаиморасчетов");
			ПараметрыРасчета.Удалить("НомерЗадания");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Метод возвращает значение константы "Номер задания",
// считанной при разделяемой блокировке.
//
// Возвращаемое значение:
//	Число - Номер текущего задания из константы "Номер задания к распределению расчетов с клиентами".
Функция ПолучитьНомерЗадания() Экспорт
	Возврат Константы.НомерЗаданияКРаспределениюРасчетовСКлиентами.Получить();
КонецФункции

// Метод создает запись регистра на указанный период по всем аналитикам за месяц.
//
// Параметры:
//	ПериодЗадания   - Дата - Начало периода, для которого необходимо зарегистрировать задание к расчету.
//
Процедура СоздатьЗаписьРегистра(ПериодЗадания) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КРасчету.Организация КАК Организация,
	|	КРасчету.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Расчеты.АналитикаУчетаПоПартнерам,
	|		Ключи.Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Ключи.Ссылка
	|	ГДЕ
	|		Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Расчеты.Активность
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыОфлайн.АналитикаУчетаПоПартнерам,
	|		Ключи.Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыОфлайн
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|		ПО РасчетыОфлайн.АналитикаУчетаПоПартнерам = Ключи.Ссылка
	|	ГДЕ
	|		РасчетыОфлайн.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И РасчетыОфлайн.Активность
	|	) КАК КРасчету
	|");
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодЗадания));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодЗадания));
	ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
	
	// Запишем задания
	НачатьТранзакцию();
	Попытка
		НомерЗадания = Константы.НомерЗаданияКРаспределениюРасчетовСКлиентами.Получить();
		Для Каждого ТекущаяАналитика Из ТаблицаАналитик Цикл
			НаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.СоздатьМенеджерЗаписи();
			НаборЗаписей.Месяц = НачалоМесяца(ПериодЗадания);
			НаборЗаписей.Организация = ТекущаяАналитика.Организация;
			НаборЗаписей.АналитикаУчетаПоПартнерам = ТекущаяАналитика.АналитикаУчетаПоПартнерам;
			НаборЗаписей.НомерЗадания = НомерЗадания;
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Не удалось записать задания к распределению расчетов с клиентами';uk='Не вдалося записати завдання до розподілу розрахунків з клієнтами'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка);
		ВызватьИсключение;
	КонецПопытки
	
КонецПроцедуры

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.РасчетыСКлиентами", "Период", "РегламентныеОперации");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.РасчетыСКлиентамиПоДокументам", "Период", "РегламентныеОперации");
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоличествоНеактуальныхДокументов(НачалоРасчета, КонецРасчета, АналитикиРасчета = Неопределено) Экспорт
	Запрос = Новый Запрос("
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		КОЛИЧЕСТВО(Расчеты.Регистратор) КАК Количество
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|	ГДЕ
	|		Расчеты.Период МЕЖДУ &НачалоРасчета И &КонецРасчета
	|		И (Расчеты.АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерам)
	|			ИЛИ Расчеты.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|			ИЛИ &ПоВсемАналитикам)
	|		И Расчеты.Активность
	|");
	
	Запрос.УстановитьПараметр("НачалоРасчета", НачалоРасчета);
	Запрос.УстановитьПараметр("КонецРасчета", КонецРасчета);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", АналитикиРасчета.АналитикиУчетаПоПартнерам);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", НЕ Значениезаполнено(АналитикиРасчета.АналитикиУчетаПоПартнерам));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		КоличествоДокументов = 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоДокументов = Выборка.Количество;
	КонецЕсли;
	Возврат КоличествоДокументов;
КонецФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно";
	Обработчик.Версия = "3.5.4.349";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("438afe04-f391-44c6-941c-fe5fb65f7a12");
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами,"
		+ "Справочник.КлючиАналитикиУчетаПоПартнерам,"
		+ "РегистрНакопления.РасчетыСКлиентами";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами";
	Обработчик.Комментарий = НСтр("ru='Обработчик обновления заполняет новый реквизит ""Организация""';uk='Обробник оновлення заповнює новий реквізит ""Організація""'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("7274a65c-9163-4640-8c55-b7f89ca78da9");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ОбъектРасчетов';uk='Заповнює реквізит ОбъектРасчетов'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();



	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);

КонецПроцедуры


// Обработчик обновления УТ 3.5.4
// В движениях регистра заполняет новый реквизит Организация.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Задания.Месяц,
	|	Задания.НомерЗадания,
	|	Задания.АналитикаУчетаПоПартнерам,
	|	Задания.УдалитьОбъектРасчетов,
	|	Задания.Документ
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК Задания
	|ГДЕ
	|	Задания.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	ИЛИ Задания.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|");
	
	ДанныеКОбработке = Запрос.Выполнить().Выбрать();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КЗаписи.Месяц,
	|	КЗаписи.НомерЗадания,
	|	КЗаписи.Организация,
	|	КЗаписи.АналитикаУчетаПоПартнерам,
	|	КЗаписи.УдалитьОбъектРасчетов,
	|	КЗаписи.Документ
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Задания.Месяц,
	|		Задания.НомерЗадания,
	|		Ключи.Организация,
	|		Задания.АналитикаУчетаПоПартнерам,
	|		Задания.УдалитьОбъектРасчетов,
	|		Задания.Документ
	|	ИЗ
	|		РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК Задания
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|		ПО Задания.АналитикаУчетаПоПартнерам = Ключи.Ссылка
	|	
	|	ГДЕ
	|		Задания.Месяц = &Месяц
	|		И Задания.НомерЗадания = &НомерЗадания
	|		И Задания.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|		И Задания.УдалитьОбъектРасчетов = &УдалитьОбъектРасчетов
	|		И Задания.Документ = &Документ
	|		И Задания.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Задания.Месяц,
	|		Задания.НомерЗадания,
	|		Ключи.Организация,
	|		Расчеты.АналитикаУчетаПоПартнерам,
	|		Задания.УдалитьОбъектРасчетов,
	|		Задания.Документ
	|	ИЗ
	|		РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК Задания
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		ПО Расчеты.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Ключи.Ссылка
	|	
	|	ГДЕ
	|		Задания.Месяц = &Месяц
	|		И Задания.НомерЗадания = &НомерЗадания
	|		И Задания.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|		И Задания.УдалитьОбъектРасчетов = &УдалитьОбъектРасчетов
	|		И Задания.Документ = &Документ
	|		И Задания.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|	) КАК КЗаписи
	|
	|");
	
	Пока ДанныеКОбработке.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Месяц.Установить(ДанныеКОбработке.Месяц);
		НаборЗаписей.Отбор.НомерЗадания.Установить(ДанныеКОбработке.НомерЗадания);
		НаборЗаписей.Отбор.УдалитьОбъектРасчетов.Установить(ДанныеКОбработке.УдалитьОбъектРасчетов);
		НаборЗаписей.Отбор.Документ.Установить(ДанныеКОбработке.Документ);
		Если ЗначениеЗаполнено(ДанныеКОбработке.АналитикаУчетаПоПартнерам) Тогда
			НаборЗаписей.Отбор.АналитикаУчетаПоПартнерам.Установить(ДанныеКОбработке.АналитикаУчетаПоПартнерам);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Месяц", ДанныеКОбработке.Месяц);
		Запрос.УстановитьПараметр("НомерЗадания", ДанныеКОбработке.НомерЗадания);
		Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", ДанныеКОбработке.АналитикаУчетаПоПартнерам);
		Запрос.УстановитьПараметр("УдалитьОбъектРасчетов", ДанныеКОбработке.УдалитьОбъектРасчетов);
		Запрос.УстановитьПараметр("Документ", ДанныеКОбработке.Документ);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ДанныеКОбработке.Месяц));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ДанныеКОбработке.Месяц));
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры


// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры = Неопределено) Экспорт
	
	ПолноеИмяРегистра = СоздатьНаборЗаписей().Метаданные().ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|ГДЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0
		Или Не ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь) Тогда
			Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	1 КАК Приоритет,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация КАК Организация,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Организация = ОбъектыРасчетов.Организация
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Партнер = ОбъектыРасчетов.Партнер
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Контрагент = ОбъектыРасчетов.Контрагент
	|		И ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов = ОбъектыРасчетов.Объект
	|ГДЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация = &Организация
	|	И ЗаданияКРаспределениюРасчетовСКлиентами.Месяц = &Месяц
	|	И НЕ ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	2 КАК Приоритет,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация КАК Организация,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов = ОбъектыРасчетов.Объект
	|ГДЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация = &Организация
	|	И ЗаданияКРаспределениюРасчетовСКлиентами.Месяц = &Месяц
	|	И НЕ ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	3 КАК Приоритет,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация КАК Организация,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ЗаданияКРаспределениюРасчетовСКлиентами.Организация = ОбъектыРасчетов.Организация
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|		И ОбъектыРасчетов.Объект = НЕОПРЕДЕЛЕНО
	|		И ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) = ОбъектыРасчетов.ТипРасчетов
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Контрагент = ОбъектыРасчетов.Контрагент
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Партнер = ОбъектыРасчетов.Партнер
	|		И ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.Договор = ОбъектыРасчетов.Договор
	|		И
	|			ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам.НаправлениеДеятельности = ОбъектыРасчетов.НаправлениеДеятельности
	|ГДЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Организация = &Организация
	|	И ЗаданияКРаспределениюРасчетовСКлиентами.Месяц = &Месяц
	|	И ЗаданияКРаспределениюРасчетовСКлиентами.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектовРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	
	Для Каждого ПорцияДанных Из ОбновляемыеДанные Цикл
	
		НачатьТранзакцию();
		
		Попытка
		
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Организация", ПорцияДанных.Организация);
			ЭлементБлокировки.УстановитьЗначение("Месяц", ПорцияДанных.Месяц);
			Блокировка.Заблокировать();
			
			Запрос.УстановитьПараметр("Организация", ПорцияДанных.Организация);
			Запрос.УстановитьПараметр("Месяц", ПорцияДанных.Месяц);
			ОбъектыРасчетов = Запрос.Выполнить().Выгрузить();
			ОбъектыРасчетов.Индексы.Добавить("АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов");
			
			НаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(ПорцияДанных.Организация);
			НаборЗаписей.Отбор.Месяц.Установить(ПорцияДанных.Месяц);
			НаборЗаписей.Прочитать();
			
			Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если ЗаписьНабора.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
					СтруктураПоиска = Новый Структура("АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов, Приоритет",
						ЗаписьНабора.АналитикаУчетаПоПартнерам,
						ЗаписьНабора.УдалитьОбъектРасчетов,
						1);
					НайденныйОбъектРасчетов = ОбъектыРасчетов.НайтиСтроки(СтруктураПоиска);
					
					Если НайденныйОбъектРасчетов.Количество() = 1 Тогда 
						ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
					Иначе
						СтруктураПоиска = Новый Структура("АналитикаУчетаПоПартнерам, ИсточникОбъектаРасчетов",
						ЗаписьНабора.АналитикаУчетаПоПартнерам,
						ЗаписьНабора.УдалитьОбъектРасчетов);
						НайденныйОбъектРасчетов = ОбъектыРасчетов.НайтиСтроки(СтруктураПоиска);
						
						НайденоНесколькоОбъектовРасчетов = Ложь;
					
						Если НайденныйОбъектРасчетов.Количество() = 0 И ЗначениеЗаполнено(ЗаписьНабора.УдалитьОбъектРасчетов) Тогда
							ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Не найден объект расчетов для источника данных: %1';uk='Не знайдений об''єкт розрахунків для джерела даних: %1'"),
								ЗаписьНабора.УдалитьОбъектРасчетов));
						ИначеЕсли НайденныйОбъектРасчетов.Количество() = 1 Тогда 
							ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
						ИначеЕсли НайденныйОбъектРасчетов.Количество() = 2 Тогда
							Если НайденныйОбъектРасчетов[0].Приоритет <> НайденныйОбъектРасчетов[1].Приоритет Тогда
								Если ЗначениеЗаполнено(НайденныйОбъектРасчетов[0].ОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
								ИначеЕсли ЗначениеЗаполнено(НайденныйОбъектРасчетов[1].ОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[1].ОбъектРасчетов;
								Иначе
									НайденоНесколькоОбъектовРасчетов = Истина;
								КонецЕсли;
							Иначе
								Если Не ЗначениеЗаполнено(ЗаписьНабора.УдалитьОбъектРасчетов) Тогда
									ЗаписьНабора.ОбъектРасчетов = НайденныйОбъектРасчетов[0].ОбъектРасчетов;
								Иначе
									НайденоНесколькоОбъектовРасчетов = Истина;
								КонецЕсли;
							КонецЕсли;
						Иначе
							НайденоНесколькоОбъектовРасчетов = Истина;
						КонецЕсли;
						
						Если НайденоНесколькоОбъектовРасчетов Тогда
							ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Для источника данных: %1 найдено несколько объектов расчетов. Заполнение невозможно.';uk='Для джерела даних %1 знайдено кілька об''єктів розрахунків. Заповнення неможливе.'"),
								ЗаписьНабора.УдалитьОбъектРасчетов));
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
		
			ОтменитьТранзакцию();
			
			Шаблон = НСтр("ru='Не удалось записать данные в регистр %1 , по причине: %2';uk='Не вдалося записати дані в регістр %1, з причини: %2'");
			ТекстСообщения = СтрШаблон(Шаблон,
				ПолноеИмяРегистра,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеРегистра,
				,
				ТекстСообщения);
		
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
