#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.300";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Процедура = "РегистрыСведений.УдалитьПорядокОтраженияНоменклатурыПереданной.ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("047a5837-7fae-4af8-904e-cefa320b0318");
	Обработчик.ЧитаемыеОбъекты = ""
		+ "РегистрСведений.УдалитьПорядокОтраженияНоменклатурыПереданной"
	;
	Обработчик.ИзменяемыеОбъекты = ""
		+ "РегистрСведений.УдалитьПорядокОтраженияНоменклатурыПереданной"
	;
	;
	Обработчик.Комментарий = НСтр("ru='Заполняет счет передачи в переработку регистра сведений ""УдалитьПорядокОтраженияНоменклатурыПереданной""';uk='Заповнює рахунок передачі в переробку регістру відомостей ""УдалитьПорядокОтраженияНоменклатурыПереданной""'");

КонецПроцедуры

// Обработчик обновления КА 2.5.4,
// заполняет счет передачи в переработку регистра сведений УдалитьПорядокОтраженияНоменклатурыПереданной
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно() Экспорт
	
	ИмяРегистра = "РегистрСведений.ПорядокОтраженияНоменклатурыПереданной";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ДанныеРегистра.СчетУчетаПередачиНаКомиссию,
	|	ДанныеРегистра.СчетУчетаВыручкиОтПродаж,
	|	ДанныеРегистра.СчетУчетаСебестоимостиПродаж,
	|	ДанныеРегистра.СтатьяДоходовРегл,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МатериалыПереданныеВПереработку) КАК СчетУчетаПередачиВПереработку,
	|	ДанныеРегистра.СчетУчетаВычетовИзДоходов,
	|	ДанныеРегистра.СтатьяЗатратРегл КАК СтатьяЗатратРегл
	|ИЗ
	|	РегистрСведений.УдалитьПорядокОтраженияНоменклатурыПереданной КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.СчетУчетаПередачиВПереработку = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = РегистрыСведений.УдалитьПорядокОтраженияНоменклатурыПереданной.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.ГруппаФинансовогоУчета.Установить(Выборка.ГруппаФинансовогоУчета);
			
			СтрокаРегистра = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРегистра, Выборка);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", "РегистрСведений.ПорядокОтраженияНоменклатурыПереданной");
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.УдалитьПорядокОтраженияНоменклатурыПереданной, 
				Неопределено, 
				ТекстСообщения
			);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
