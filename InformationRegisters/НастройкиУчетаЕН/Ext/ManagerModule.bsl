#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	СтруктураЗначений = Новый Структура;
    
    СтруктураЗначений.Вставить("ГруппаПлательщикаЕдиногоНалога", Перечисления.ГруппыПлательщиковЕдиногоНалога.ВтораяГруппа);
    
	
	Возврат СтруктураЗначений
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.Период КАК Период,
    |	ГоловныеОрганизации.Организация КАК Организация,
    |	ТаблицаСрезПоследних.ГруппаПлательщикаЕдиногоНалога КАК ГруппаПлательщикаЕдиногоНалога
	|//ПОМЕСТИТЬ
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЕН.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|//ИНДЕКСИРОВАТЬ
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
            "НастройкиУчетаЕН",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
    
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
	  	СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.';uk='Не задані параметри.'");
	  	Возврат СтрокаОписанияНастроек;
    КонецЕсли;
    
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон,
			НСтр("ru='Группа плательщика единого налога';uk='Група платника єдиного податку'"),
			ДействующиеНастройки.ГруппаПлательщикаЕдиногоНалога);
	
	Возврат СтрокаОписанияНастроек
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.НастройкиУчетаЕН.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("574c382b-12ec-4faa-97d5-7bcc719cf6b0");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.НастройкиУчетаЕН.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр НастройкиУчетаЕН из настроек учетных политик организаций.';uk='Заповнює регістр Настройки УчетаЕН з настройокоблікових політик організацій.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьУчетнаяПолитикаОрганизаций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.УдалитьУчетныеПолитикиОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.НастройкиУчетаЕН.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.НастройкиУчетаЕН.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.НастройкиУчетаЕН";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ДатаИзмененияУчетнойПолитики = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДанныеРегистра.Период
	|		ИНАЧЕ ДанныеРегистра.ДатаИзмененияУчетнойПолитики
	|	КОНЕЦ КАК Период,
	|	ДанныеРегистра.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЕН КАК Настройки
	|		ПО (Настройки.Период = ВЫБОР
	|				КОГДА ДанныеРегистра.ДатаИзмененияУчетнойПолитики = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ДанныеРегистра.Период
	|				ИНАЧЕ ДанныеРегистра.ДатаИзмененияУчетнойПолитики
	|			КОНЕЦ)
	|			И (Настройки.Организация = ДанныеРегистра.Организация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УдалитьУчетныеПолитикиОрганизаций КАК Политики
	|		ПО ДанныеРегистра.УчетнаяПолитика = Политики.Ссылка
	|ГДЕ
	|	Настройки.Организация ЕСТЬ NULL
	|	И Политики.СистемаНалогообложения В (ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ЕдиныйНалог), ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ЕдиныйНалогИНДС))";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.НастройкиУчетаЕН;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДляОбработки КАК ДанныеДляОбработки 
	|		ПО (ДанныеДляОбработки.Период = ВЫБОР
	|				КОГДА ДанныеРегистра.ДатаИзмененияУчетнойПолитики = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ДанныеРегистра.Период
	|				ИНАЧЕ ДанныеРегистра.ДатаИзмененияУчетнойПолитики
	|			КОНЕЦ)
	|		И ДанныеДляОбработки.Организация = ДанныеРегистра.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЕН КАК Настройки
	|		ПО (Настройки.Период = ДанныеДляОбработки.Период)
	|			И (Настройки.Организация = ДанныеДляОбработки.Организация)
	|ГДЕ
	|	Настройки.Организация ЕСТЬ NULL
	|ИТОГИ
	|ПО
	|	УчетнаяПолитика";
	
	ВыборкаУчетнаяПолитика = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "УчетнаяПолитика");
	
	Пока ВыборкаУчетнаяПолитика.Следующий() Цикл

		НачатьТранзакцию();
	
		Попытка
	
			БлокировкаУчетнаяПолитика = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаУчетнаяПолитика.Добавить("Справочник.УдалитьУчетныеПолитикиОрганизаций");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаУчетнаяПолитика.УчетнаяПолитика);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			БлокировкаУчетнаяПолитика.Заблокировать();
			
			ЗапросУчетнаяПолитика = Новый Запрос();
			ЗапросУчетнаяПолитика.Текст = "ВЫБРАТЬ
			|	УчетнаяПолитика.ГруппаПлательщикаЕдиногоНалога КАК ГруппаПлательщикаЕдиногоНалога
			|ИЗ
			|	Справочник.УдалитьУчетныеПолитикиОрганизаций КАК УчетнаяПолитика
			|ГДЕ
			|	УчетнаяПолитика.Ссылка = &УчетнаяПолитика";
			
			ЗапросУчетнаяПолитика.УстановитьПараметр("УчетнаяПолитика", ВыборкаУчетнаяПолитика.УчетнаяПолитика);
	
			РезультатЗапросаУчетнаяПолитика = ЗапросУчетнаяПолитика.Выполнить();
			
			Если РезультатЗапросаУчетнаяПолитика.Пустой() Тогда
				ТекстОшибки = СтрШаблон(НСтр("ru='Ошибка получения настроек учетной политики ""%1"".';uk='Помилка отримання налаштувань облікової політики ""%1"".'",ОбщегоНазначения.КодОсновногоЯзыка()), 
					ВыборкаУчетнаяПолитика.УчетнаяПолитика);
				ВызватьИсключение ТекстОшибки;
			Иначе
				НастройкаУчетнойПолитики = РезультатЗапросаУчетнаяПолитика.Выгрузить()[0];
			КонецЕсли;
			
			Выборка = ВыборкаУчетнаяПолитика.Выбрать(ОбходРезультатаЗапроса.Прямой);
	
			Пока Выборка.Следующий() Цикл
					НаборЗаписей = РегистрыСведений.НастройкиУчетаЕН.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
					НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
					
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					ЗаполнитьЗначенияСвойств(НоваяЗапись, НастройкаУчетнойПолитики);
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
					
			КонецЦикла;
	
			ЗафиксироватьТранзакцию();
				
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%';uk='Не вдалося записати дані в регістр %ИмяРегистра% через: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, Неопределено, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли