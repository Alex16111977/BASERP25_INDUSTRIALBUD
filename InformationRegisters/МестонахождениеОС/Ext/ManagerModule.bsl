
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.МестонахождениеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e74b0eac-e050-4049-a3c5-216ba09f983d");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.МестонахождениеОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр ""Местонахождение ОС"" по данным учета ОС версии 2.1';uk='Заповнює регістр ""Місцезнаходження ОЗ"" за даними обліку ОЗ версії 2.1'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПеремещениеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьМестонахождениеОСБухгалтерскийУчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.МестонахождениеОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.МестонахождениеОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "РегистрСведений.МестонахождениеОС";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяОбъекта;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Регистратор.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Регистратор");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаИсточник.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.УдалитьМестонахождениеОСБухгалтерскийУчет КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ТаблицаПолучатель
	|		ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|ГДЕ
	|	ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|	И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Регистраторы, 
		ДополнительныеПараметры
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "РегистрСведений.МестонахождениеОС";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДанных.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ТаблицаОбновляемыхДанных КАК ТаблицаДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДляОбработки.Регистратор КАК Регистратор
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки";

	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Документ.ВводОстатковВнеоборотныхАктивов");
	ДополнительныеПараметрыПроцедуры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыПроцедуры.ИмяВременнойТаблицы = "ВТЗаблокированныеДанные";
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
		Параметры.Очередь, 
		ЧитаемыеДанные, 
		МенеджерВременныхТаблиц, 
		ДополнительныеПараметрыПроцедуры
	);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДанных.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ТаблицаОбновляемыхДанных КАК ТаблицаДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДляОбработки.Регистратор КАК Регистратор
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки
	|ГДЕ
	|	НЕ ВТДляОбработки.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТЗаблокированныеДанные.Ссылка
	|			ИЗ
	|				ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОбновляемыхДанных", ОбновляемыеДанные);
	
	Выборка = Запрос.Выполнить().Выбрать();

	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;

	Пока Выборка.Следующий() Цикл
		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 
 			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.МестонахождениеОС.НаборЗаписей");
 			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);

 			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.МестонахождениеОС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			
			ОбработатьДанныеРегистра(Выборка, НаборЗаписей);
			
			Если НаборЗаписей.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
			КонецЕсли; 
	
			ОбъектовОбработано = ОбъектовОбработано + 1;

			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ТекстСообщения = НСтр("ru='Не удалось обработать данные регистра ""Местонахождение ОС"": %Ссылка% по причине: %Причина%';uk='Не вдалося обробити дані регістра ""Місцезнаходження ОЗ"": %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Регистратор.Метаданные(),
				Выборка.Регистратор,
				ТекстСообщения
			);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		Метаданные.РегистрыСведений.МестонахождениеОС.ПолноеИмя()
	);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ОбработатьДанныеРегистра(Выборка, НаборЗаписей)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Дата ЕСТЬ NULL
	|			ТОГДА ВводОстатковВнеоборотныхАктивов.Дата
	|		ИНАЧЕ ТаблицаИсточник.Период 
	|	КОНЕЦ КАК Период,
	|	ТаблицаИсточник.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.МОЛ КАК МОЛ,
	|	ТаблицаИсточник.Местонахождение КАК Местонахождение,
	|	ТаблицаИсточник.АдресМестонахождения КАК АдресМестонахождения
	|ИЗ
	|	РегистрСведений.УдалитьМестонахождениеОСБухгалтерскийУчет КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ТаблицаПолучатель
	|		ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатковВнеоборотныхАктивов
	|		ПО ВводОстатковВнеоборотныхАктивов.Ссылка = ТаблицаИсточник.Регистратор
	|ГДЕ
	|	ТаблицаИсточник.Регистратор = &Регистратор
	|	И ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|	И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПередачаОСАрендатору
	|	И НЕ ТаблицаИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И (ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL
	|			ИЛИ ВводОстатковВнеоборотныхАктивов.Организация = ТаблицаИсточник.Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаИсточник.Период КАК Период,
	|	ТаблицаИсточник.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.МОЛ КАК МОЛ,
	|	ТаблицаИсточник.Местонахождение КАК Местонахождение,
	|	ТаблицаИсточник.АдресМестонахождения КАК АдресМестонахождения
	|ИЗ
	|	РегистрСведений.УдалитьМестонахождениеОСБухгалтерскийУчет КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ТаблицаПолучатель
	|		ПО ТаблицаПолучатель.Регистратор = ТаблицаИсточник.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаОСАрендатору КАК ПередачаОСАрендатору
	|		ПО ПередачаОСАрендатору.Ссылка = ТаблицаИсточник.Регистратор
	|ГДЕ
	|	ТаблицаИсточник.Регистратор = &Регистратор
	|	И ТаблицаПолучатель.Регистратор ЕСТЬ NULL
	|	И ТаблицаИсточник.Регистратор ССЫЛКА Документ.ПередачаОСАрендатору
	|	И ПередачаОСАрендатору.Организация = ТаблицаИсточник.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
	Результат = Запрос.Выполнить();
	ВыборкаИсходныеДанные = Результат.Выбрать();
	
	Пока ВыборкаИсходныеДанные.Следующий() Цикл
		
		ЗаписьРегистра = НаборЗаписей.Добавить();
		
		ЗаписьРегистра.Период               = ВыборкаИсходныеДанные.Период;
		ЗаписьРегистра.ОсновноеСредство     = ВыборкаИсходныеДанные.ОсновноеСредство;
		ЗаписьРегистра.Организация          = ВыборкаИсходныеДанные.Организация;
		ЗаписьРегистра.МОЛ                  = ВыборкаИсходныеДанные.МОЛ;
		ЗаписьРегистра.Местонахождение      = ВыборкаИсходныеДанные.Местонахождение;
		ЗаписьРегистра.АдресМестонахождения = ВыборкаИсходныеДанные.АдресМестонахождения;
		
		Если ЗначениеЗаполнено(ЗаписьРегистра.АдресМестонахождения)  
			И НЕ ЗначениеЗаполнено(ЗаписьРегистра.АдресМестонахожденияЗначение) Тогда
			
			ЗаписьРегистра.АдресМестонахожденияЗначение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
				ЗаписьРегистра.АдресМестонахождения,
				Перечисления.ТипыКонтактнойИнформации.Адрес
			);
			
		КонецЕсли;
				
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
