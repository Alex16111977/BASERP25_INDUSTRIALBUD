#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура АктуализироватьДанные(НаборЗаписейУчетнойПолитики) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтборПоОрганизации = Неопределено;
	Если НаборЗаписейУчетнойПолитики.Отбор.Найти("Организация") <> Неопределено И НаборЗаписейУчетнойПолитики.Отбор.Организация.Использование Тогда
		ОтборПоОрганизации = НаборЗаписейУчетнойПолитики.Отбор.Организация.Значение;
	КонецЕсли;
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ВЗ.ПлательщикНДС КАК ПлательщикНДС,
	|	ВЗ.ПлательщикЕН КАК ПлательщикЕН,
	|	ВЗ.Период КАК Период,
	|	ВЗ.ГруппаПлательщикаЕдиногоНалога КАК ГруппаПлательщикаЕдиногоНалога
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НастройкиСистемыНалогообложения.Организация КАК Организация,
	|			НастройкиСистемыНалогообложения.ПлательщикНДС КАК ПлательщикНДС,
	|			НастройкиСистемыНалогообложения.ПлательщикЕН КАК ПлательщикЕН,
	|			НастройкиСистемыНалогообложения.Период КАК Период,
	|			NULL КАК ГруппаПлательщикаЕдиногоНалога,
	|			0 КАК Приоритет
	|		ИЗ
	|			РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		{ГДЕ
	|			НастройкиСистемыНалогообложения.Организация.*}
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			НастройкиУчетаЕН.Организация,
	|			NULL,
	|			ИСТИНА,
	|			НастройкиУчетаЕН.Период,
	|			НастройкиУчетаЕН.ГруппаПлательщикаЕдиногоНалога,
	|			1
	|		ИЗ
	|			РегистрСведений.НастройкиУчетаЕН КАК НастройкиУчетаЕН
	|		{ГДЕ
	|			НастройкиУчетаЕН.Организация.*}) КАК ВЗ
	|		ПО Организации.Ссылка = ВЗ.Организация
	|{ГДЕ
	|	Организации.Ссылка.* КАК Организация}
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ВЗ.Приоритет
	|ИТОГИ ПО
	|	Организация";
	
	Если ЗначениеЗаполнено(ОтборПоОрганизации) Тогда
		Запрос.Отбор.Добавить("Организация").Установить(ОтборПоОрганизации);
	КонецЕсли;
	
	Запрос.Выполнить();
	ВыборкаОрганизация = Запрос.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизация.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СтатусыПлательщиковЕдиногоНалога.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(ВыборкаОрганизация.Организация);
		
		ТекущиеЗначения = Новый Структура("ПлательщикЕН, ПлательщикНДС, ГруппаПлательщикаЕдиногоНалога");
		ДатаПоследнегоИзменения = Неопределено;
		
		Выборка = ВыборкаОрганизация.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.Период) Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьОтличия = Ложь;
			Для каждого ЭлементСтруктуры Из ТекущиеЗначения Цикл
				ЗначениеВыборки = Выборка[ЭлементСтруктуры.Ключ];
				Если ЗначениеВыборки <> NULL И ЭлементСтруктуры.Значение <> ЗначениеВыборки Тогда
					ЕстьОтличия = Истина;
					ТекущиеЗначения[ЭлементСтруктуры.Ключ] = ЗначениеВыборки;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьОтличия Тогда
				
				Если Выборка.Период <> ДатаПоследнегоИзменения Тогда
					НоваяЗапись = НаборЗаписей.Добавить();              
					НоваяЗапись.Период = Выборка.Период;
					НоваяЗапись.Организация = ВыборкаОрганизация.Организация;
				КонецЕсли;
			
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущиеЗначения);
				
				Если НЕ НоваяЗапись.ПлательщикЕН Тогда
					НоваяЗапись.ГруппаПлательщикаЕдиногоНалога = Неопределено;
				КонецЕсли;
				
			КонецЕсли;
			
			ДатаПоследнегоИзменения = Выборка.Период;	
			
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом



#КонецОбласти

#КонецОбласти

#КонецЕсли        

