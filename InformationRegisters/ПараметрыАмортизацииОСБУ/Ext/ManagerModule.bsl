
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПараметрыАмортизацииОСБУ.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("330403b8-abad-495c-b00b-d63402397b92");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПараметрыАмортизацииОСБУ.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Обновляет регистр ""Параметры амортизации ОС (бухгалтерский и налоговый учет)""
|- заполняет по данным учета ОС версии 2.1'
|;uk='Оновлює регістр ""Параметри амортизації ОЗ (бухгалтерський і податковий облік)""
|- заповнює за даними обліку ОЗ версії 2.1'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.МодернизацияОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПеремещениеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПереоценкаОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.КорректировкаНалоговогоНазначенияОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииОСБУ.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьГрафикиАмортизацииОСБухгалтерскийУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииОСБУ.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииОСБУ.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.КорректировкаНалоговогоНазначенияОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";


КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ПараметрыАмортизацииОСБУ";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрИсточник.Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьГрафикиАмортизацииОСБухгалтерскийУчет КАК РегистрИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = РегистрИсточник.Регистратор
	|	ГДЕ
	|		(РегистрПриемник.Регистратор ЕСТЬ NULL
	|			ИЛИ РегистрПриемник.ГрафикАмортизации <> РегистрИсточник.ГрафикАмортизации)
	|		И НЕ РегистрИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РегистрИсточник.Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК РегистрИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = РегистрИсточник.Регистратор
	|	ГДЕ
	|		РегистрПриемник.Регистратор ЕСТЬ NULL
	|		И НЕ РегистрИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РегистрИсточник.Регистратор
	|	ИЗ
	|		РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет КАК РегистрИсточник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = РегистрИсточник.Регистратор
	|	ГДЕ
	|		РегистрПриемник.Регистратор ЕСТЬ NULL
	|		И НЕ РегистрИсточник.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка
	|	ИЗ
	|		Документ.МодернизацияОС КАК ДанныеДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК РегистрПриемник
	|			ПО РегистрПриемник.Регистратор = ДанныеДокумента.Ссылка
	|	ГДЕ
	|		РегистрПриемник.Регистратор ЕСТЬ NULL
	|
	|	) Регистраторы";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ПараметрыАмортизацииОСБУ";

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;

	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Документ.ВводОстатковВнеоборотныхАктивов");
	ЧитаемыеДанные.Добавить("Документ.ИзменениеПараметровОС");
	ЧитаемыеДанные.Добавить("Документ.МодернизацияОС");
	ЧитаемыеДанные.Добавить("Документ.ПеремещениеОС");
	ЧитаемыеДанные.Добавить("Документ.ПереоценкаОС");
	ЧитаемыеДанные.Добавить("Документ.ПринятиеКУчетуОС");
	ЧитаемыеДанные.Добавить("Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций");
	ЧитаемыеДанные.Добавить("Документ.КорректировкаНалоговогоНазначенияОС");
	ЧитаемыеДанные.Добавить("Документ.СписаниеОС");
							
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, ЧитаемыеДанные) 
		ИЛИ ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "РегистрСведений.РеестрДокументов") Тогда
		// Обновление не начнется пока есть необработанные документы, т.к. нужно обрабатывать записи в хронологическом порядке.
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ВыбиратьПорциями = Ложь;
	
	ПараметрыОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
		Параметры.Очередь,
		Неопределено, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц,
		ДопПараметры
		);
	
	Если НЕ ПараметрыОбработки.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ ПараметрыОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
	|	СсылкиДляОбработки.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора) КАК Дата,
	|	ЕСТЬNULL(РеестрДокументов.Организация, ДанныеПервичныхДокументов.Организация) КАК Организация
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО РеестрДокументов.Ссылка = СсылкиДляОбработки.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ДанныеПервичныхДокументов.Документ = СсылкиДляОбработки.Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеПервичныхДокументов.ДатаРегистратора)
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДляОбработки", ПараметрыОбработки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыАмортизацииОСБУ.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПараметрыАмортизацииОСБУ.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);

			Если НаборЗаписей.Количество() = 0 Тогда
				ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка);
			КонецЕсли;			
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
			КонецЕсли; 
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось обработать записи регистра ""Параметры амортизации ОС (бухгалтерский и налоговый учет)"": %Ссылка% по причине: %Причина%';uk='Не вдалося обробити записи регістру ""Параметри амортизації ОЗ (бухгалтерський і податковий облік)"": %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ПараметрыАмортизацииОСБУ,
				Выборка.Регистратор,
				ТекстСообщения
			);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(НаборЗаписей, Выборка)

	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Проведен") <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистра = Неопределено;
	
	ТипДокумента = ТипЗнч(Выборка.Регистратор);	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ИсходныйРегистрНУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияНУ,
		|	ИсходныйРегистрБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ИсходныйРегистрБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрГрафикАмортизации.ГрафикАмортизации КАК ГрафикАмортизации,
		|	ДанныеДокумента.Дата КАК ДатаПоследнегоИзменения
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьГрафикиАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрГрафикАмортизации
		|		ПО ИсходныйРегистрГрафикАмортизации.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрГрафикАмортизации.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Регистратор";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.МодернизацияОС") Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	Документ.ПереоценкаОС.ОС КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ИсходныйРегистрНУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияНУ,
		|	ИсходныйРегистрБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ИсходныйРегистрБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
		|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации КАК ГрафикАмортизации,
		|	ДанныеДокумента.Дата КАК ДатаПоследнегоИзменения
		|ИЗ
		|	Документ.ПереоценкаОС КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС.ОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Регистратор";

		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.Регистратор);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ПереоценкаОС", ИмяТаблицы);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();

	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПереоценкаОС") Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	Документ.ПереоценкаОС.ОС КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ИсходныйРегистрНУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияНУ,
		|	ИсходныйРегистрБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ИсходныйРегистрБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
		|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации КАК ГрафикАмортизации,
		|	ДанныеДокумента.Дата КАК ДатаПоследнегоИзменения
		|ИЗ
		|	Документ.ПереоценкаОС КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС.ОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = ТаблицаОС.Ссылка
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет.СрезПоследних(&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ИсходныйРегистрНУ.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Регистратор";

		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.Регистратор);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ПереоценкаОС", ИмяТаблицы);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеОС") Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	Документ.ПереоценкаОС.ОС КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Дата КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
		|	ИсходныйРегистрБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ИсходныйРегистрБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
		|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации КАК ГрафикАмортизации,
		|	&Дата КАК ДатаПоследнегоИзменения
		|ИЗ
		|	втТаблицаОС КАК ТаблицаОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство";

		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.Регистратор);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ПереоценкаОС", ИмяТаблицы);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций") 
			  ИЛИ ТипДокумента = Тип("ДокументСсылка.КорректировкаНалоговогоНазначенияОС") 
			  
			  Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Регистратор = &Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Дата КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсходныйРегистрБУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ИсходныйРегистрНУ.СрокПолезногоИспользования КАК СрокПолезногоИспользованияНУ,
		|	ИсходныйРегистрБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
		|	ИсходныйРегистрБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ИсходныйРегистрБУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
		|	ИсходныйРегистрНУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
		|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации КАК ГрафикАмортизации,
		|	&Дата КАК ДатаПоследнегоИзменения
		|ИЗ
		|	втТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|				&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСНалоговыйУчет.СрезПоследних(&Дата,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							втТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							втТаблицаОС КАК втТаблицаОС)) КАК ИсходныйРегистрНУ
		|		ПО ИсходныйРегистрНУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|			И ИсходныйРегистрНУ.Организация = &Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьПараметрыАмортизацииОСБухгалтерскийУчет КАК ИсходныйРегистрБУ
		|		ПО ИсходныйРегистрБУ.Регистратор = &Регистратор
		|			И ИсходныйРегистрБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство";

		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Выборка.Дата);
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
	Иначе
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор);
		ТаблицыДляДвижений = МенеджерДокумента.ДанныеДокументаДляПроведения(Выборка.Регистратор, "ПараметрыАмортизацииОСБУ");
		ТаблицыДляДвижений.Свойство("ТаблицаПараметрыАмортизацииОСБУ", ТаблицаРегистра);
	КонецЕсли;

	Если ТаблицаРегистра <> Неопределено Тогда
		НаборЗаписей.Загрузить(ТаблицаРегистра);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
