
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает данные об изделиях спецификации в регистр
//
// Параметры:
//  Спецификация  - СправочникСсылка.РесурсныеСпецификации - спецификация, для которой записываются изделия.
//  ТолькоОчистка - Булево - определяет необходимость записи данных, либо только очистку уже имеющихся в регистре
//  ВызовИзОбработчикаОбновления - Булево - режим вызова из обработчика обновления
//
Процедура ЗаписатьИзделияПоСпецификации(Спецификация, ТолькоОчистка = Ложь, ВызовИзОбработчикаОбновления = Ложь) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СпецификацииИзделий");
	ЭлементБлокировки.УстановитьЗначение("Спецификация", Спецификация);
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.СпецификацииИзделий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Спецификация.Установить(Спецификация);
	
	Если НЕ ТолькоОчистка Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаИзделияСпецификации();
		Запрос.УстановитьПараметр("Спецификация", Спецификация);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если ВызовИзОбработчикаОбновления Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	Иначе
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ТекстЗапросаИзделияСпецификации()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Т.Ссылка            КАК Спецификация,
	|	Т.Номенклатура      КАК Номенклатура,
	|	Т.Характеристика    КАК Характеристика,
	|	Т.Этап              КАК Этап,
	|	СУММА(ВЫРАЗИТЬ(Т.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3))) КАК Количество
	|ПОМЕСТИТЬ ВтВозвратныеОтходы
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК Т
	|ГДЕ
	|	Т.Ссылка = &Спецификация
	|	И Т.СпособАвтовыбораНоменклатуры = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ)
	|	И Т.СпособАвтовыбораХарактеристики = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ)
	|	И (ВЫРАЗИТЬ(Т.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Ссылка,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Номенклатура                   КАК Номенклатура,
	|	Т.Характеристика                 КАК Характеристика,
	|	Т.ИсточникПолученияПолуфабриката КАК Этап,
	|	СУММА(ВЫРАЗИТЬ(Т.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3))) КАК Количество
	|ПОМЕСТИТЬ ВтМатериалы
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК Т
	|ГДЕ
	|	Т.Ссылка = &Спецификация
	|	И Т.СпособПолученияМатериала = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе)
	|	И Т.СпособАвтовыбораНоменклатуры = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ)
	|	И Т.СпособАвтовыбораХарактеристики = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ)
	|	И (ВЫРАЗИТЬ(Т.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.ИсточникПолученияПолуфабриката
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтВозвратныеОтходы.Номенклатура.ВидНоменклатуры              КАК ВидНоменклатуры,
	|	ВтВозвратныеОтходы.Номенклатура                              КАК Номенклатура,
	|	ВтВозвратныеОтходы.Характеристика                            КАК Характеристика,
	|	ВтВозвратныеОтходы.Спецификация                              КАК Спецификация,
	|	ВтВозвратныеОтходы.Спецификация.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	ВтВозвратныеОтходы.Спецификация.Статус                       КАК Статус,
	|	ИСТИНА                                                       КАК ПобочныйВыход
	|ПОМЕСТИТЬ ВтИзделия
	|ИЗ
	|	ВтВозвратныеОтходы КАК ВтВозвратныеОтходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтМатериалы КАК ВтМатериалы
	|		ПО ВтВозвратныеОтходы.Номенклатура = ВтМатериалы.Номенклатура
	|			И ВтВозвратныеОтходы.Характеристика = ВтМатериалы.Характеристика
	|			И ВтВозвратныеОтходы.Этап = ВтМатериалы.Этап
	|ГДЕ
	|	ВтВозвратныеОтходы.Спецификация.Статус В (
	|										ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует),
	|										ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке))
	|	И ВтВозвратныеОтходы.Количество - ЕСТЬNULL(ВтМатериалы.Количество, 0) > 0
	|	И ЕСТЬNULL(ВтВозвратныеОтходы.Номенклатура.ВидНоменклатуры,
	|		ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА Т.ОсновноеИзделиеВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка) ТОГДА
	|			ЕСТЬNULL(Т.ОсновноеИзделиеНоменклатура.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ Т.ОсновноеИзделиеВидНоменклатуры
	|	КОНЕЦ,
	|	Т.ОсновноеИзделиеНоменклатура,
	|	Т.ОсновноеИзделиеХарактеристика,
	|	Т.Ссылка,
	|	Т.ТипПроизводственногоПроцесса,
	|	Т.Статус,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК Т
	|ГДЕ
	|	Т.Ссылка = &Спецификация
	|	И Т.Статус В (
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке))
	|	И Т.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
	|	И ВЫБОР
	|		КОГДА Т.ОсновноеИзделиеВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка) ТОГДА
	|				ЕСТЬNULL(Т.ОсновноеИзделиеНоменклатура.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка))
	|			ИНАЧЕ Т.ОсновноеИзделиеВидНоменклатуры
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Т.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка) ТОГДА
	|			ЕСТЬNULL(Т.Номенклатура.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ Т.ВидНоменклатуры
	|	КОНЕЦ,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Ссылка,
	|	Т.Ссылка.ТипПроизводственногоПроцесса,
	|	Т.Ссылка.Статус,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Т
	|ГДЕ
	|	Т.Ссылка = &Спецификация
	|	И Т.Ссылка.Статус В (
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке))
	|	И Т.Ссылка.ТипПроизводственногоПроцесса <> ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
	|	И Т.СпособАвтовыбораНоменклатуры = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ)
	|	И Т.СпособАвтовыбораХарактеристики = ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ)
	|	И (ВЫРАЗИТЬ(Т.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
	|	И ВЫБОР
	|		КОГДА Т.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка) ТОГДА
	|				ЕСТЬNULL(Т.Номенклатура.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка))
	|			ИНАЧЕ Т.ВидНоменклатуры
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ВидНоменклатуры              КАК ВидНоменклатуры,
	|	Т.Номенклатура                 КАК Номенклатура,
	|	Т.Характеристика               КАК Характеристика,
	|	Т.Спецификация                 КАК Спецификация,
	|	Т.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	Т.Статус                       КАК Статус,
	|	МИНИМУМ(Т.ПобочныйВыход)       КАК ПобочныйВыход
	|ИЗ
	|	ВтИзделия КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.ВидНоменклатуры,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Спецификация,
	|	Т.ТипПроизводственногоПроцесса,
	|	Т.Статус
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Т.Упаковка",
			"Т.Номенклатура"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.СпецификацииИзделий.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("43414af7-dddf-40f1-86c0-4c14f143823e");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СпецификацииИзделий.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет регистр изделий по спецификациям';uk='Заповнює регістр виробів за специфікаціями'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.РесурсныеСпецификации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.СпецификацииИзделий.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.СпецификацииИзделий.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";            
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию22";
	НоваяСтрока.Порядок = "После";          
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.РесурсныеСпецификации.ОбработатьДанныеДляПереходаНаНовуюВерсию24";
	НоваяСтрока.Порядок = "После";          

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СпецификацииИзделий";
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Спецификация КАК Спецификация
	|ИЗ
	|	(ВЫБРАТЬ
	|		Реквизиты.Ссылка КАК Спецификация
	|	ИЗ
	|		Справочник.РесурсныеСпецификации КАК Реквизиты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииИзделий КАК СпецификацииИзделий
	|			ПО (Реквизиты.Ссылка = СпецификацииИзделий.Спецификация)
	|	ГДЕ
	|		Реквизиты.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке))
	|		И СпецификацииИзделий.Спецификация ЕСТЬ NULL
	|
	|) КАК Таблица");
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра             = ПолноеИмяРегистра;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СпецификацииИзделий";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ИмяВременнойТаблицы = "ВТДляОбработки";
	ДопПараметры.ВыбиратьПорциями = Истина;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра,
		МенеджерВременныхТаблиц,
		ДопПараметры);
		
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВТДляОбработки.Спецификация КАК Спецификация
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			Попытка
				
				ЗаписатьИзделияПоСпецификации(Выборка.Спецификация,,Истина);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Спецификация);
				Продолжить;
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
