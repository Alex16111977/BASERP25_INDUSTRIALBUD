
Процедура ОбменДаннымиСВнешнимиБазамиПриЗаписиПриЗаписи(Источник, Отказ) Экспорт
	
	РегистрироватьОбъект=Истина;
	Если ТипЗнч(Источник)=Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее") Тогда
		РегистрироватьОбъект=Не Источник.НеОтправлятьВБухгалтерию;
	КонецЕсли;
	
	Если РегистрироватьОбъект  Тогда
		ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("КазначействоБухгалтерияУкраины", Источник, Отказ); 
		ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("КазначействоBASБухгалтерияУкраины", Источник, Отказ);
	КонецЕсли;
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("КазначействоBASERP", Источник, Отказ);
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("КазначействоУправлениеТорговлейДляУкраины", Источник, Отказ);

	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("КазначействоПродажиНедвижимости", Источник, Отказ);

	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("ОбменСМобильнымиУстройствами", Источник, Отказ);
	
КонецПроцедуры

Процедура АХ_КонтрольДублейКонтрагентаПередЗаписью(Источник, Отказ) Экспорт
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ЕГРПОУ", Источник.ЕГРПОУ);
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Контрагенты.ЕГРПОУ,
	               |	Контрагенты.Ссылка
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.ЕГРПОУ = &ЕГРПОУ
	               |	И Контрагенты.Ссылка <> &Ссылка";
				   
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		//Дубль найден
		Если Константы.АХ_ВариантыДействияПриНахожденииДубляКонтрагента.Получить() = Перечисления.ВариантыДействийПриНахожденииДубля.НеРазрешатьЗаписывать Тогда
			Отказ = Истина;
		КонецЕсли;

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Контрагент с данным ИНН уже существует.'"),
			РезультатЗапроса.Ссылка,
			,
			,
			Отказ);
		
	КонецЕсли;					   

КонецПроцедуры

Процедура АХ_КонтрольДлиныОКПО(Источник, Отказ)
	
	ОКПО = СокрЛП(Источник.ЕГРПОУ);
	
	//НекорректнаяДлинна	= (Источник.ФизЮрЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо И СтрДлина(ОКПО)<>10) Или (Источник.ФизЮрЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо И СтрДлина(ОКПО)<>8);
	
	//**Убираем проверку на юр\физ, не всегда пишут ФОП в наименовании
	НекорректнаяДлинна	= НЕ(СтрДлина(ОКПО)=10 ИЛИ СтрДлина(ОКПО)=8);
	
	
	Если НекорректнаяДлинна Тогда
		Если Константы.АХ_ВариантыДействияПриНахожденииДубляКонтрагента.Получить() = Перечисления.ВариантыДействийПриНахожденииДубля.НеРазрешатьЗаписывать Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Некорректный ЕГРПОУ.'"),
			,
			,
			);
	КонецЕсли;
	
КонецПроцедуры

Процедура АХ_ПроверкиПередЗаписью(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Контрагенты") Тогда
		Если Источник.ОбменДанными.Загрузка  Тогда
			Возврат;
		КонецЕсли;
		Если НЕ Источник.ЭтоГруппа Тогда
			АХ_КонтрольДублейКонтрагентаПередЗаписью(Источник, Отказ);
			АХ_КонтрольДлиныОКПО(Источник, Отказ);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция РазборСтрокиПоРегулярномуВыражению(Строка, Шаблон = Неопределено, Параметры = Неопределено, ОбъектRegExp = Неопределено, МассивНайденныхСтрок = Неопределено) Экспорт
	
	Если Параметры = Неопределено И ОбъектRegExp = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОбъектRegExp = Неопределено Тогда
		ОбъектRegExp = СоздатьИНастроитьОбъектRegExp(Параметры)
	КонецЕсли;
	
	Если Не Шаблон = Неопределено Тогда
		ОбъектRegExp.Pattern	= Шаблон;
	КонецЕсли;
	
	Если МассивНайденныхСтрок = Неопределено Тогда
		МассивНайденныхСтрок = Новый Массив;
	КонецЕсли;
	
	Matches = ОбъектRegExp.Execute(Строка);
	Для Счетчик = 0 По Matches.Count - 1 Цикл 
		Match = Matches.Item(Счетчик);
		МассивНайденныхСтрок.Добавить(Match.Value);
	КонецЦикла;
	
	Возврат МассивНайденныхСтрок;
	
КонецФункции

Функция СоздатьИНастроитьОбъектRegExp(Параметры) Экспорт
	
	RegExp = Новый COMОбъект("VBScript.RegExp");
	
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Ключ = "Global" Тогда
			RegExp.Global = Параметр.Значение;
		ИначеЕсли Параметр.Ключ = "Pattern" Тогда
			RegExp.Pattern  = Параметр.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат RegExp;
	
КонецФункции

Функция ПолучитьСписокМасокДляПоискаДанныхВСтроке(ТипМаски) Экспорт
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ТипМаски", ТипМаски);
	
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	МаскиДляПоискаДанныхВСтроке.Маска
	            	  |ИЗ
	            	  |	РегистрСведений.МаскиДляПоискаДанныхВСтроке КАК МаскиДляПоискаДанныхВСтроке
	            	  |ГДЕ
	            	  |	МаскиДляПоискаДанныхВСтроке.ТипМаски = &ТипМаски";
					  
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Маска");					  
		
КонецФункции

Функция ВыслатьУведомленияОВизированииЗаявок(ТолькоНовыеСчета) Экспорт
	
	ТаблицаДокументовРассылки	= ПолучитьСписокСчетовДляРассылки(ТолькоНовыеСчета);
	ПолучателиРассылки	= ТаблицаДокументовРассылки.Скопировать(,"Пользователь");
	ПолучателиРассылки.Свернуть("Пользователь");
	
	Для Каждого ПолучательРассылки Из ПолучателиРассылки Цикл
		АдресEmail	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПолучательРассылки.Пользователь, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		Если ЗначениеЗаполнено(АдресEmail) Тогда
			ВыслатьОтчетОСчетахПользователю(ПолучательРассылки.Пользователь, ТолькоНовыеСчета, АдресEmail);
			ЗаписатьФактРассылкиВРегистр(ПолучательРассылки.Пользователь, ТаблицаДокументовРассылки);
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция ПолучитьСписокСчетовДляРассылки(ТолькоНовыеСчета)
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ТолькоНовыеСчета", ТолькоНовыеСчета);
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	СчетНаОплатуПоставщикаТекущиеВизирующие.Пользователь КАК Пользователь,
	            	  |	СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка,
	            	  |	ВЫБОР
	            	  |		КОГДА ФактОтправкиУведомленийОСчетахНаВизировании.СчетНаОплатуПоставщика ЕСТЬ NULL
	            	  |			ТОГДА ИСТИНА
	            	  |		ИНАЧЕ ЛОЖЬ
	            	  |	КОНЕЦ КАК НовыйДокумент,
	            	  |	СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка.Виза_ТекущаяРоль
	            	  |ИЗ
	            	  |	Документ.СчетНаОплатуПоставщика.ТекущиеВизирующие КАК СчетНаОплатуПоставщикаТекущиеВизирующие
	            	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФактОтправкиУведомленийОСчетахНаВизировании КАК ФактОтправкиУведомленийОСчетахНаВизировании
	            	  |		ПО СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка = ФактОтправкиУведомленийОСчетахНаВизировании.СчетНаОплатуПоставщика
	            	  |			И СчетНаОплатуПоставщикаТекущиеВизирующие.Пользователь = ФактОтправкиУведомленийОСчетахНаВизировании.Пользователь
	            	  |			И СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка.Виза_ТекущаяРоль = ФактОтправкиУведомленийОСчетахНаВизировании.РольВизирования
	            	  |ГДЕ
	            	  |	ВЫБОР
	            	  |			КОГДА &ТолькоНовыеСчета
	            	  |				ТОГДА ФактОтправкиУведомленийОСчетахНаВизировании.СчетНаОплатуПоставщика ЕСТЬ NULL
	            	  |			ИНАЧЕ ИСТИНА
	            	  |		КОНЕЦ
	            	  |	И СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка.Виза_Состояние = ЗНАЧЕНИЕ(Перечисление.Визы_Состояния.НаВизировании)
	            	  |	И СчетНаОплатуПоставщикаТекущиеВизирующие.Ссылка.Дата >= ДАТАВРЕМЯ(2018, 11, 1)";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ВыслатьОтчетОСчетахПользователю(Пользователь, ТолькоНовыеСчета, АдресEmail) Экспорт
	
	СхемаКомпоновкиДанных		= ПолучитьОбщийМакет("РассылкаУведомленийОСчетахНаВизировании");

	КомпоновщикНастроек	= Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Пользователь"));
	Если Не Параметр = Неопределено Тогда
		Параметр.Значение = Пользователь;
	КонецЕсли;
	
	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТолькоНовыеСчета"));
	Если Не Параметр = Неопределено Тогда
		Параметр.Значение = ТолькоНовыеСчета;
	КонецЕсли;
		
	ДанныеРасшифровки	= Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета	= Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки			= КомпоновщикНастроек.Настройки;
	
	МакетКомпоновки		= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
		
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);
	
	Результат	= Новый ТабличныйДокумент;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	//ПроцессорВывода.УстановитьДокумент(Результат);
	
	ИмяТемпФайла	= ПолучитьИмяВременногоФайла("html"); 
	НужнаОтправка	= Ложь;
	//Выводит поэлементно.
	//Логика следующая - если отчет пустой, то вывод осуществляться не будет, и флаг отправки не взведется 
	ПроцессорВывода.НачатьВывод();
    ЭлементРезультатаКД =  ПроцессорКомпоновкиДанных.Следующий();
	Пока ЭлементРезультатаКД <> Неопределено Цикл
        ПроцессорВывода.ВывестиЭлемент(ЭлементРезультатаКД);
		
		// Определить не пустой результат.
		Для Каждого ЗначениеПараметраМакетаКД Из ЭлементРезультатаКД.ЗначенияПараметров Цикл
			Попытка
				ЗначениеЗаполнено = ЗначениеЗаполнено(ЗначениеПараметраМакетаКД.Значение);
			Исключение
				ЗначениеЗаполнено = Ложь; // Линия, Рамка, Цвет и другие объекты КД, которые могут фигурировать при выводе.
			КонецПопытки;
			Если ЗначениеЗаполнено Тогда
				НужнаОтправка = Истина;
			КонецЕсли;
		КонецЦикла;

		ЭлементРезультатаКД = ПроцессорКомпоновкиДанных.Следующий();
    КонецЦикла;
    Результат	= ПроцессорВывода.ЗакончитьВывод();
	
	Если Не НужнаОтправка Тогда
		Возврат;
	КонецЕсли;
		
	Результат.Записать(ИмяТемпФайла, ТипФайлаТабличногоДокумента.HTML);
	
	ТекстHТМЛ = Новый ТекстовыйДокумент;
    ТекстHТМЛ.Прочитать(ИмяТемпФайла);
    ТекстHТМЛ = ТекстHТМЛ.ПолучитьТекст();
	
	ПараметрыПисьма	= Новый Структура;
	ПараметрыПисьма.Вставить("Кому", АдресEmail);
	ПараметрыПисьма.Вставить("Тема", "Уведомление системы визирования");
	ПараметрыПисьма.Вставить("ТипТекста", Перечисления.ТипыТекстовЭлектронныхПисем.HTML);
	ПараметрыПисьма.Вставить("Тело", ТекстHТМЛ);	
	
	Попытка
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(Константы.УчетнаяЗаписьДляУведомлений.Получить(),ПараметрыПисьма);	
	Исключение
		ТекстОшибки	= ОписаниеОшибки();
	КонецПопытки;
	                                     	
КонецПроцедуры

Процедура ЗаписатьФактРассылкиВРегистр(ПолучательРассылки, ТаблицаДокументовРассылки)
	
	СтруктураОтбора		= Новый Структура("Пользователь, НовыйДокумент", ПолучательРассылки, Истина);
	СписокДокументов	= ТаблицаДокументовРассылки.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого ДанныеДокумента Из СписокДокументов Цикл
		МенеджерЗаписи		= РегистрыСведений.ФактОтправкиУведомленийОСчетахНаВизировании.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь				= ПолучательРассылки;
		МенеджерЗаписи.СчетНаОплатуПоставщика	= ДанныеДокумента.Ссылка;
		МенеджерЗаписи.РольВизирования			= ДанныеДокумента.Виза_ТекущаяРоль;
		МенеджерЗаписи.Записать();
	КонецЦикла;	
КонецПроцедуры

Процедура ОчиститьРегистрФактОтправкиУведомленийОСчетахНаВизировании(Ссылка) Экспорт
	
	НаборЗаписей	= РегистрыСведений.ФактОтправкиУведомленийОСчетахНаВизировании.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СчетНаОплатуПоставщика.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ОтправитьУведомлениеАвторуОбОтменеСчета(Ссылка) Экспорт
	
	АвторДокумента	= Ссылка.Автор;
	АдресEmail		= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(АвторДокумента, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		
	ТекстУведомления	= Строка(Ссылка) + " был отменен";
	
	ПараметрыПисьма	= Новый Структура;
	ПараметрыПисьма.Вставить("Кому", АдресEmail);
	ПараметрыПисьма.Вставить("Тема", "Уведомление системы визирования");
	ПараметрыПисьма.Вставить("Тело", ТекстУведомления);	
	
	Попытка
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(Константы.УчетнаяЗаписьДляУведомлений.Получить(),ПараметрыПисьма);	
	Исключение
	КонецПопытки;
		
КонецПроцедуры

Процедура РазослатьУведомленияОВизированииНовыеСчета() Экспорт
	
	АХ_ОбщегоНазначения.ВыслатьУведомленияОВизированииЗаявок(Истина);
	
КонецПроцедуры

Процедура РазослатьУведомленияОВизированииНовыеСчета1() Экспорт

	АХ_ОбщегоНазначения.ВыслатьУведомленияОВизированииЗаявок(Ложь);
	
КонецПроцедуры

Процедура АХ_ПроверкаПередЗаписьюДокументаСПодразделениемПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Павел Чистяков 06.09.2016 13:20:24 №230
	Если Источник.ОбменДанными.Загрузка
		ИЛИ Источник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.Подразделение.НеИспользовать Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Подразделение " + Источник.Подразделение + " имеет признак ""Не использовать"". Укажите другое поразделение.",
			Источник.Ссылка, "Подразделение", "", Отказ);
	КонецЕсли; 
	
КонецПроцедуры

Процедура КонтрольЗаполненияОбязательныхРеквизитовШапкиОбъекта(Объект, МассивОбязательныхРеквизитов, Отказ) Экспорт
		
	Для Каждого ОбязательныйРеквизит Из МассивОбязательныхРеквизитов Цикл
		Если Не ЗначениеЗаполнено(Объект[ОбязательныйРеквизит]) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не заполнен реквизит %1, проведение невозможно", ОбязательныйРеквизит);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			Объект.Ссылка,
			,
			,
			Отказ);
		КонецЕсли;
	КонецЦикла;

	
КонецПроцедуры

Функция ПолучитьСтатьюДДСПоУсловию(Подразделение, Контрагент, Сумма, ТипДокумента, Секция) Экспорт
	
	//*СтруктураСДДС	= РегистрыСведений.УсловияЗаполненияСтатьиДДС.Получить(Новый Структура("Подразделение, Контрагент, Сумма, ТипДокумента, Секция",Подразделение, Контрагент, Сумма, ТипДокумента, Секция));
	
	//Упрощаем поиск, только по виду документа и сумме
	СтруктураСДДС	= РегистрыСведений.УсловияЗаполненияСтатьиДДС.Получить(Новый Структура("Подразделение, Контрагент, Сумма, ТипДокумента, Секция",Справочники.Подразделения.ПустаяСсылка(), Справочники.Контрагенты.ПустаяСсылка(), Сумма, ТипДокумента, Справочники.Секции.ПустаяСсылка()));
	
	Возврат СтруктураСДДС["СДДС"];
	
КонецФункции

Функция ПользовательИмеетДоступКПодразделению(Пользователь, Подразделение) Экспорт
	
	Если Пользователи.РолиДоступны("ПолныеПрава, БанкОтделПродаж, ДоступностьСчетовПоОрганизациям", Пользователь) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение"	, Подразделение);
	Запрос.УстановитьПараметр("Пользователь"	, Пользователь);
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	ГруппыПользователейСостав.Ссылка
	            	  |ПОМЕСТИТЬ Группы
	            	  |ИЗ
	            	  |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	            	  |ГДЕ
	            	  |	ГруппыПользователейСостав.Пользователь = &Пользователь
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ
	            	  |	НастройкиДоступа.Чтение
	            	  |ИЗ
	            	  |	РегистрСведений.НастройкиДоступа КАК НастройкиДоступа
	            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Группы КАК Группы
	            	  |		ПО НастройкиДоступа.Пользователь = Группы.Ссылка
	            	  |			И (НастройкиДоступа.ВидОбъектаДоступа = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовДоступа.Подразделения))
	            	  |			И (НастройкиДоступа.ОбъектДоступа = &Подразделение)
	            	  |			И (НастройкиДоступа.Чтение)";
					  
	  Возврат Не Запрос.Выполнить().Пустой();
					  	
КонецФункции

Процедура КонтрольЗаполненияОбязательныхРеквизитовТачбличнойЧастиОбъекта(Объект, ТабличнаяЧасть, МассивОбязательныхРеквизитов, Отказ) Экспорт
	
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		Для Каждого ОбязательныйРеквизит Из МассивОбязательныхРеквизитов Цикл
			Если Не ЗначениеЗаполнено(Строка[ОбязательныйРеквизит]) Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не заполнен реквизит %1 в строке %2, проведение невозможно", ОбязательныйРеквизит, Строка.НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				,
				,
				Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура АХ_ОтменаРегистрацииППИПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура ЗагрузкаКурсовВалютНБУ() Экспорт
	ДатаДляКурса = ТекущаяДата(); 
	Обр= Обработки.ЗагрузкаКурсовВалютНБУ.Создать();
	
	Обр.ЗагрузитьКурсыССайтаНБУ(ДатаДляКурса,ДатаДляКурса);
КонецПроцедуры

//Тютюн отправка уведомлений пользователям о необходимости согласования счета
Процедура ОтправитьУведомлениеОНеобходимостиВизированияСчета(Ссылка,МассивПолучателей,РольВизирования) Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая()Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Получатель Из МассивПолучателей Цикл
	
	АдресEmail		= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
	Если ЗначениеЗаполнено(АдресEmail) Тогда	
	ТекстУведомления	=  "Для ролі '"+Строка(РольВизирования)+"' додано новий документ на візування: "+Строка(Ссылка);
	
	ПараметрыПисьма	= Новый Структура;
	ПараметрыПисьма.Вставить("Кому", АдресEmail); 
	ПараметрыПисьма.Вставить("Тема", "Візування документа: " +Строка(Ссылка));
	ПараметрыПисьма.Вставить("Тело", ТекстУведомления);	
	
	Попытка
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(Константы.УчетнаяЗаписьДляУведомлений.Получить(),ПараметрыПисьма);
		МенеджерЗаписи		= РегистрыСведений.ФактОтправкиУведомленийОСчетахНаВизировании.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь				= Получатель;
		МенеджерЗаписи.СчетНаОплатуПоставщика	= Ссылка.Ссылка;
		МенеджерЗаписи.РольВизирования			= РольВизирования;
		МенеджерЗаписи.Записать();

	Исключение
	КонецПопытки; 
	КонецЕсли;
	
	КонецЦикла;
		
КонецПроцедуры    
//КонецИзменений


//КонецИзменений

