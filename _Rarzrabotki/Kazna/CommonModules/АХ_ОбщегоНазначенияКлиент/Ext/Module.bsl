Процедура ДобавитьИзФайловойСистемыБезРасширения(ПараметрыВыполнения) Экспорт
	
	// Загрузить из файловой системы без расширения работы с файлами (веб-клиент).
	Обработчик = Новый ОписаниеОповещения("ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла", ЭтотОбъект, ПараметрыВыполнения);
	НачатьПомещениеФайлов(Обработчик, , , , ПараметрыВыполнения.ФормаВладелец.УникальныйИдентификатор);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
Процедура ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла(ПомещаемыеФайлы, ПараметрыВыполнения) Экспорт
		
	Результат = Новый Структура;
	Результат.Вставить("ФайлДобавлен", Ложь);
	Результат.Вставить("ФайлСсылка",   Неопределено);
	Результат.Вставить("ТекстОшибки",  "");
	
	Если  ПомещаемыеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеФайла Из ПомещаемыеФайлы Цикл
		ВыбранноеИмяФайла	= ОписаниеФайла.Имя;
		СтруктураПути = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
		Если ПустаяСтрока(СтруктураПути.Расширение) Тогда
			ТекстВопроса = НСтр("ru = 'Необходимо указать файл с расширением.'");
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.Повторить, НСтр("ru = 'Выбрать другой файл'"));
			Кнопки.Добавить(КодВозвратаДиалога.Отмена);
			Обработчик = Новый ОписаниеОповещения("ДобавитьИзФайловойСистемыБезРасширенияПослеОтветаНаВопросПовторить", ЭтотОбъект, ПараметрыВыполнения);
			ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
			Возврат;
		КонецЕсли;
		
		// Создание карточки файла в БД.
		Попытка
			СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
			СведенияОФайле.АдресВременногоХранилищаФайла = ОписаниеФайла.Хранение;
			СведенияОФайле.ИмяБезРасширения = СтруктураПути.ИмяБезРасширения;
			СведенияОФайле.РасширениеБезТочки = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураПути.Расширение);
			Результат.ФайлСсылка = РаботаСФайламиСлужебныйВызовСервера.СоздатьФайлСВерсией(ПараметрыВыполнения.ВладелецФайла, СведенияОФайле);
			Результат.ФайлДобавлен = Истина;
		Исключение
			Результат.ТекстОшибки = ФайловыеФункцииСлужебныйКлиентСервер.ОшибкаСозданияНовогоФайла(ИнформацияОбОшибке());
		КонецПопытки;
		Если Результат.ТекстОшибки <> "" Тогда
			РаботаСФайламиСлужебныйКлиент.ВернутьРезультатПослеПоказаПредупреждения(ПараметрыВыполнения.ОбработчикРезультата, Результат.ТекстОшибки, Неопределено);
			Возврат;
		КонецЕсли;
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Владелец", ПараметрыВыполнения.ВладелецФайла);
		ПараметрыОповещения.Вставить("Файл", Результат.ФайлСсылка);
		ПараметрыОповещения.Вставить("Событие", "СозданФайл");
		Оповестить("Запись_Файл", ПараметрыОповещения);
		
		ПоказатьОповещениеПользователя(
		НСтр("ru = 'Создание:'"),
		ПолучитьНавигационнуюСсылку(Результат.ФайлСсылка),
		Результат.ФайлСсылка,
		БиблиотекаКартинок.Информация32);
		
		
		РаботаСФайламиСлужебныйКлиент.ВернутьРезультат(ПараметрыВыполнения.ОбработчикРезультата, Результат);
	КонецЦикла;
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
Процедура ДобавитьИзФайловойСистемыБезРасширенияПослеОтветаНаВопросПовторить(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Повторить Тогда
		ДобавитьИзФайловойСистемыБезРасширения(ПараметрыВыполнения);
	Иначе
		РаботаСФайламиСлужебныйКлиент.ВернутьРезультат(ПараметрыВыполнения.ОбработчикРезультата, Неопределено);
	КонецЕсли;
	
КонецПроцедуры
