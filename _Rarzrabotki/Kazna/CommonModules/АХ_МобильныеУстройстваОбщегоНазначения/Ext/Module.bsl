Функция ПолучитьМобильноеУстройствоПоИдентификатору(ИдентификаторКлиента) Экспорт
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторКлиента"	,ИдентификаторКлиента);
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	МобильныеУстройства.Ссылка
	            	  |ИЗ
	            	  |	Справочник.МобильныеУстройства КАК МобильныеУстройства
	            	  |ГДЕ
	            	  |	МобильныеУстройства.ИдентификаторКлиента = &ИдентификаторКлиента";
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.Ссылка;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьУзелПоМобильномуУстройству(МобильноеУстройство) Экспорт
	
	Возврат ПланыОбмена.ОбменСМобильнымиУстройствами.НайтиПоРеквизиту("МобильноеУстройство", МобильноеУстройство)
		
КонецФункции

Функция СоздатьПакетДанныхДляМобильногоУстройства(МобильноеУстройство) Экспорт
	
	Пользователь	= МобильноеУстройство.Пользователь; 
	Узел			= ПолучитьУзелПоМобильномуУстройству(МобильноеУстройство);
			
	Если ЗначениеЗаполнено(Узел) Тогда 
		ЗаписьСообщения		= Неопределено;
		ЗаписьXML			= ПолучитьЗаписьXMLДляСообщенияОбмена(Узел, ЗаписьСообщения);
		ВыборкаИзменений	= ПланыОбмена.ВыбратьИзменения(Узел, ЗаписьСообщения.НомерСообщения);
		
		ВозвращаемыйСписок	= СоздатьОбъектXDTO("Objects");
		
		
		Если Не ВыборкаИзменений = Неопределено Тогда	
			Пока ВыборкаИзменений.Следующий() Цикл
				Данные = ВыборкаИзменений.Получить();
				ЗаписатьДанные(ВозвращаемыйСписок, Данные, МобильноеУстройство, Пользователь);
			КонецЦикла;
		КонецЕсли;
		
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ВозвращаемыйСписок);
		ЗаписьСообщения.ЗакончитьЗапись();
		СообщениеОбмена = Новый ХранилищеЗначения(ЗаписьXML.Закрыть(), Новый СжатиеДанных(9));
		
		Возврат СообщениеОбмена;
	КонецЕсли;

	
КонецФункции

Функция ЗарегистрироватьМобильноеУстройство(УидКлиента, ИмяКлиента = "") Экспорт
	
	НовоеМобильноеУстройство	= Справочники.МобильныеУстройства.СоздатьЭлемент();
	
	НовоеМобильноеУстройство.Наименование			= ИмяКлиента;
	НовоеМобильноеУстройство.ИдентификаторКлиента	= УидКлиента;
	
	Попытка 
		НовоеМобильноеУстройство.Записать();
	    Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
		
КонецФункции

Функция ЗарегистрироватьПриемДанныхОтМобильногоУстройства(МобильноеУстройство, НомерПринятогоСообщения) Экспорт
	
	Узел		= ПолучитьУзелПоМобильномуУстройству(МобильноеУстройство);

	Если ЗначениеЗаполнено(Узел) Тогда
		Попытка
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, НомерПринятогоСообщения); 
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#Область ФормированиеФайлДанных

// Функция создает объект переданного типа.
//
Функция СоздатьОбъектXDTO(ТипОбъекта) 
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ah-mobile-app.org", ТипОбъекта));
	
КонецФункции // СоздатьОбъектXDTO()

Функция ПолучитьЗаписьXMLДляСообщенияОбмена(УзелОбмена, ЗаписьСообщения)
	
	ЗаписьXML = Новый ЗаписьXML;
	
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелОбмена);
	
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8",  "http://v8.1c.ru/data/core");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("msg",  "http://www.ah-mobile-app.org");
	
	Возврат ЗаписьXML;
	
КонецФункции

Процедура ЗаписатьДанные(ВозвращаемыйСписок, Данные, МобильноеУстройство = Неопределено, Пользователь = Неопределено) 
	
	ОбъектXDTO = ПолучитьОбъектXDTO(Данные, МобильноеУстройство, Пользователь);
	Если ОбъектXDTO <> Неопределено Тогда
		ВозвращаемыйСписок.objects.Добавить(ОбъектXDTO);
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьДанные()

Функция ПолучитьОбъектXDTO(Данные, МобильноеУстройство = Неопределено, Пользователь = Неопределено)
	
	ПередаваемыйОбъект = Неопределено;
	
	Если ТипЗнч(Данные) = Тип("ДокументОбъект.СчетНаОплатуПоставщика")
		ИЛИ ТипЗнч(Данные) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
	
		ПередаваемыйОбъект = СоздатьОбъектXDTO("DocBill");
		ПередаваемыйОбъект.Id			= Строка(Данные.Ссылка.УникальныйИдентификатор());
		ПередаваемыйОбъект.DeletionMark = Данные.ПометкаУдаления;
		ПередаваемыйОбъект.Posted		= Данные.Проведен;
		ПередаваемыйОбъект.Name			= Данные.Номер;
		ПередаваемыйОбъект.Date			= Данные.Дата;
		ПередаваемыйОбъект.Comment 		= Данные.Комментарий;
		
		ПередаваемыйОбъект.Division				= Строка(Данные.Подразделение);
		ПередаваемыйОбъект.Item 				= Строка(Данные.СтатьяДвиженияДенежныхСредств);
		ПередаваемыйОбъект.Contractor 			= Строка(Данные.Контрагент);
		ПередаваемыйОбъект.ConrtactorAccount	= Строка(Данные.СчетКонтрагента);
		ПередаваемыйОбъект.Organisation 		= Строка(Данные.Организация);
		ПередаваемыйОбъект.Summ 				= Данные.СуммаДокумента;
		ПередаваемыйОбъект.PaymentDate 			= Данные.ПлановаяДатаОплаты;
		ПередаваемыйОбъект.Currency 			= Строка(Данные.ВалютаДокумента);
		ПередаваемыйОбъект.Responsible 			= Строка(Данные.Ответственный);
		ПередаваемыйОбъект.PaymentType 			= ПолучитьОбъектXDTO(Данные.ВидОплаты);
		ПередаваемыйОбъект.StateOfVisa 			= ПолучитьОбъектXDTO(Данные.Виза_Состояние);
		ПередаваемыйОбъект.Mine 				= Не Данные.ТекущиеВизирующие.Найти(Пользователь) = Неопределено;
	ИначеЕсли ТипЗнч(Данные) = Тип("ПеречислениеСсылка.Визы_Состояния") Тогда
		
		Если Данные = Перечисления.Визы_Состояния.Анулировано Тогда
			ПередаваемыйОбъект = "Cancelled";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.Отклонено Тогда
			ПередаваемыйОбъект = "Declined";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.Черновик Тогда
			ПередаваемыйОбъект = "Draft";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.НаВизировании Тогда
			ПередаваемыйОбъект = "AwaitsVisa";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.Утвержден Тогда
			ПередаваемыйОбъект = "Approved";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.ТребуетсяЗаявка Тогда
			ПередаваемыйОбъект = "RequestRequired";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.ТребуетсяОплата Тогда
			ПередаваемыйОбъект = "PaymentRequired";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.ВыгруженаВКБ Тогда
			ПередаваемыйОбъект = "Exported";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.Оплачено Тогда
			ПередаваемыйОбъект = "Paid";
		ИначеЕсли Данные = Перечисления.Визы_Состояния.ОплаченоЧастично Тогда
			ПередаваемыйОбъект = "PartiallyPaid";
		КонецЕсли;
	ИначеЕсли ТипЗнч(Данные) = Тип("ПеречислениеСсылка.ВидыДенежныхСредств") Тогда
		Если Данные = Перечисления.ВидыДенежныхСредств.Наличные Тогда
			ПередаваемыйОбъект = "Cash";
		Иначе
			ПередаваемыйОбъект = "Cashless";
		КонецЕсли;		
	КонецЕсли;
	
	Возврат ПередаваемыйОбъект;
	
КонецФункции // ПолучитьОбъектXDTO()

#КонецОбласти

Функция ИзменитьСостояниеВизы(МобильноеУстройство, УИДДокумента, НовоеСостояние) Экспорт
	
	УИД	= Новый УникальныйИдентификатор(УИДДокумента);
	ДокументСсылка	= Документы.СчетНаОплатуПоставщика.ПолучитьСсылку(УИД);
	
	ДокументОбъект  = ДокументСсылка.ПолучитьОбъект();
	Если НовоеСостояние = "Approved" Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ТекущийПользователь", МобильноеУстройство.Пользователь);
		ДополнительныеПараметры.Вставить("РольДоступна_ПолныеПрава", Истина );
		Попытка
			ДокументОбъект.Виза_УстановитьСостояние(Перечисления.Визы_Состояния.Утвержден, ДополнительныеПараметры); 
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	ИначеЕсли НовоеСостояние = "Declined" Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ТекущийПользователь", МобильноеУстройство.Пользователь);
		ДополнительныеПараметры.Вставить("РольДоступна_ПолныеПрава", Истина );
		Попытка
			ДокументОбъект.Виза_УстановитьСостояние(Перечисления.Визы_Состояния.Отклонено, ДополнительныеПараметры); 
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСтруктуруСправочника(ИмяСправочника, РодительскийЭлемент) Экспорт
	
	МенеджерСправочника	= Метаданные.Справочники[ИмяСправочника];
	Иерархический		= МенеджерСправочника.Иерархический;
	ИерархияГрупп		= МенеджерСправочника.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов;
	Родитель			= Справочники[ИмяСправочника].ПолучитьСсылку(РодительскийЭлемент);
	
	Запрос	= Новый Запрос;
	
	ТекстЗапроса	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	            	  |	ТаблицаБД.Ссылка, 
	            	  |	ТаблицаБД.Наименование," 
					  + ?(ИерархияГрупп, "ВЫБОР	КОГДА ТаблицаБД.ЭтоГруппа ТОГДА 0 ИНАЧЕ 1 КОНЕЦ КАК Картинка, ТаблицаБД.ЭтоГруппа КАК ЭтоГруппа", " 2 КАК Картинка, Ложь КАК ЭтоГруппа")
	            	  + " ИЗ Справочник.#ИмяСправочника КАК ТаблицаБД "
	            	  + ?(Иерархический, "ГДЕ ТаблицаБД.Родитель = &Родитель", "");
					  
	Запрос.Текст	= СтрЗаменить(ТекстЗапроса, "#ИмяСправочника", ИмяСправочника);
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	МассивЭлементов	= Новый Массив();
	
	СтруктураРодителя	= Новый Структура("Картинка, Наименование, ID, ЭтоГруппа", ?(ИерархияГрупп, 1, 2), Родитель.Наименование, Строка(Родитель.УникальныйИдентификатор()),?(ИерархияГрупп, Истина, Ложь));
	МассивЭлементов.Добавить(СтруктураРодителя);
	
	Пока РезультатЗапроса.Следующий() Цикл
		СтруктураЭлемента	= Новый Структура("Картинка, Наименование, ID, ЭтоГруппа", РезультатЗапроса.Картинка, РезультатЗапроса.Наименование, Строка(РезультатЗапроса.Ссылка.УникальныйИдентификатор()), РезультатЗапроса.ЭтоГруппа);
		МассивЭлементов.Добавить(СтруктураЭлемента);		
	КонецЦикла;
	
	Возврат Новый ХранилищеЗначения(МассивЭлементов, Новый СжатиеДанных(9));
	
КонецФункции

Функция ПолучитьСтруктуруСправочникаПодразделения(МобильноеУстройство, РодительскийЭлемент) Экспорт
	
	Родитель	= Справочники.Подразделения.ПолучитьСсылку(РодительскийЭлемент);
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("ПолныеПрава"	, ПолучитьУзелПоМобильномуУстройству(МобильноеУстройство).ОбменБезОграничений);
	Запрос.УстановитьПараметр("Пользователь", МобильноеУстройство.Пользователь);
		
	Запрос.Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	            	  |	Подразделения.Ссылка,
	            	  |	Подразделения.Наименование
	            	  |ИЗ
	            	  |	Справочник.Подразделения КАК Подразделения
	            	  |ГДЕ
	            	  |	Подразделения.Родитель = &Родитель
	            	  |	И ВЫБОР
	            	  |			КОГДА &ПолныеПрава
	            	  |				ТОГДА ИСТИНА
	            	  |			ИНАЧЕ Подразделения.Ссылка В ИЕРАРХИИ
	            	  |					(ВЫБРАТЬ
	            	  |						НастройкиДоступа.ОбъектДоступа
	            	  |					ИЗ
	            	  |						РегистрСведений.НастройкиДоступа КАК НастройкиДоступа
	            	  |							ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	            	  |							ПО
	            	  |								НастройкиДоступа.Пользователь = ГруппыПользователейСостав.Ссылка
	            	  |									И ГруппыПользователейСостав.Пользователь = &Пользователь
	            	  |					ГДЕ
	            	  |						НастройкиДоступа.ВидОбъектаДоступа = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовДоступа.Подразделения))
	            	  |		КОНЕЦ";
	
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	МассивЭлементов		= Новый Массив();
	
	СтруктураРодителя	= Новый Структура("Картинка, Наименование, ID", 0, Родитель.Наименование, Строка(Родитель.УникальныйИдентификатор()));
	МассивЭлементов.Добавить(СтруктураРодителя);
	
	Пока РезультатЗапроса.Следующий() Цикл
		СтруктураЭлемента	= Новый Структура("Картинка, Наименование, ID", 0, РезультатЗапроса.Наименование, Строка(РезультатЗапроса.Ссылка.УникальныйИдентификатор()));
		МассивЭлементов.Добавить(СтруктураЭлемента);		
	КонецЦикла;
	
	Возврат Новый ХранилищеЗначения(МассивЭлементов, Новый СжатиеДанных(9));
	
КонецФункции

Функция СформироватьОтчетБДДС(ИмяВарианта, Подразделение,НачалоПериода, КонецПериода) Экспорт
	
	ОтчетОбъект	= Отчеты.БДДС.Создать();
	
	КомпоновщикНастроек	= ОтчетОбъект.КомпоновщикНастроек;
	КомпоновщикНастроек.ЗагрузитьНастройки(ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек[ИмяВарианта].Настройки);
		
	ПараметрНачалоПериода				= КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НачалоПериода");
    ПараметрНачалоПериода.Значение		= НачалоПериода;
    ПараметрНачалоПериода.Использование	= Истина;

	ПараметрКонецПериода				= КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("КонецПериода");
    ПараметрКонецПериода.Значение		= КонецПериода;
    ПараметрКонецПериода.Использование	= Истина;
	
	
	ОтборПодразделение	= Новый ПолеКомпоновкиДанных("Подразделение");
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = ОтборПодразделение Тогда
				ЭлементОтбора.Использование		= Истина;
				ЭлементОтбора.ПравоеЗначение	= Справочники.Подразделения.ПолучитьСсылку(Подразделение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	
	//Формируем макет, с помощью компоновщика макета
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	//Передаем в макет компоновки схему, настройки и данные расшифровки
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ОтчетОбъект.СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки());
	
	//Выполним компоновку с помощью процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	//Очищаем поле табличного документа
	Результат = Новый ТабличныйДокумент;
	
	//Выводим результат в табличный документ
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат СериализаторXDTO.ЗаписатьXDTO(Результат);
	
КонецФункции

Функция ПолучитьВариантыОтчета(ИмяОтчета) Экспорт
	
	МассивВариантов	= Новый Массив;
	
	ОтчетОбъект	= Отчеты.БДДС.Создать();
	Для Каждого Вариант Из ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
		МассивВариантов.Добавить(Новый Структура("Имя, Представление", Вариант.Имя, Вариант.Представление));
	КонецЦикла;
	
	Возврат СериализаторXDTO.ЗаписатьXDTO(МассивВариантов);
	
КонецФункции

Функция ПолучитьРодителя(ИмяСправочника, ИдентификаторЭлемента) Экспорт 
	
	СтруктураВозврата	= Новый Структура("Наименование, ID");
	
	МенеджерСправочника	= Метаданные.Справочники[ИмяСправочника];
	Иерархический		= МенеджерСправочника.Иерархический;
	
	Если Иерархический Тогда 
		Элемент	= Справочники[ИмяСправочника].ПолучитьСсылку(ИдентификаторЭлемента);
		СтруктураВозврата.Вставить("Наименование"	, Элемент.Родитель.Наименование);
		СтруктураВозврата.Вставить("ID"				, Элемент.Родитель.УникальныйИдентификатор());
	КонецЕсли;

	Возврат СериализаторXDTO.ЗаписатьXDTO(СтруктураВозврата);
	
КонецФункции

	