Функция ЗаписьИОбъектИдентичны(МетаданныеРегистра, ЗаписьРегистра, ОбъектИстории)
	
	Для Каждого Строка Из МетаданныеРегистра.Ресурсы Цикл
		Если ЗаписьРегистра[Строка.Имя]<>ОбъектИстории[Строка.Имя] Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция МожноЗаписыватьИсторию(МетаданныеРегистра, ОбъектИстории)
	
	Для Каждого Строка Из МетаданныеРегистра.Ресурсы Цикл
		// Реквизиты, которые не обязательны для заполнения не контролируются
		Если Строка.ПроверкаЗаполнения=ПроверкаЗаполнения.НеПроверять Тогда
			Продолжить;
		КонецЕсли;		
		Если НЕ ЗначениеЗаполнено(ОбъектИстории[Строка.Имя]) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ОбновитьИсторию(ИмяРегистра, ОбъектИстории) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеРегистра = Метаданные.РегистрыСведений[ИмяРегистра];
	
	Если Не МожноЗаписыватьИсторию(МетаданныеРегистра, ОбъектИстории) Тогда
		Возврат;
	КонецЕсли; 
		
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений." + ИмяРегистра + ".СрезПоследних(,Объект=&Объект)");
	Запрос.УстановитьПараметр("Объект", ОбъектИстории.Ссылка);
	ЗаписьРегистраСтарая = Запрос.Выполнить().Выбрать();
	Если НЕ ЗаписьРегистраСтарая.Следующий() Тогда
		ЗаписьРегистраСтарая = Неопределено;
	КонецЕсли;
	Если ЗаписьРегистраСтарая<>Неопределено
		И ЗаписьИОбъектИдентичны(МетаданныеРегистра, ЗаписьРегистраСтарая, ОбъектИстории) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьРегистраНовая = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
	ЗаписьРегистраНовая.Период = ТекущаяДата();
	ЗаписьРегистраНовая.Объект = ОбъектИстории.Ссылка;
	Для Каждого Строка Из МетаданныеРегистра.Ресурсы Цикл
		ЗаписьРегистраНовая[Строка.Имя] = ОбъектИстории[Строка.Имя];
	КонецЦикла;
	Если МетаданныеРегистра.Реквизиты.Найти("АвторИзменения")<>Неопределено Тогда
		ЗаписьРегистраНовая.АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	Если МетаданныеРегистра.Реквизиты.Найти("ПерваяЗапись")<>Неопределено Тогда
		Если ЗаписьРегистраСтарая=Неопределено
			ИЛИ (ЗаписьРегистраСтарая.Период=ЗаписьРегистраНовая.Период И ЗаписьРегистраСтарая.ПерваяЗапись) Тогда
			ЗаписьРегистраНовая.ПерваяЗапись = Истина;
		КонецЕсли;
	КонецЕсли;
	Если МетаданныеРегистра.Реквизиты.Найти("ПериодОкончание")<>Неопределено Тогда
		Если ЗаписьРегистраСтарая<>Неопределено Тогда
			ЗаписьРегистра = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ЗаписьРегистра, ЗаписьРегистраСтарая);
			ЗаписьРегистра.ПериодОкончание = ЗаписьРегистраНовая.Период-1;
			ЗаписьРегистра.Записать();
		КонецЕсли; 
		ЗаписьРегистраНовая.ПериодОкончание = Дата(3000,1,1);
	КонецЕсли;
	ЗаписьРегистраНовая.Записать();
			
КонецПроцедуры