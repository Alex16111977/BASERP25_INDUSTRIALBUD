Процедура ВыполнитьДвижениеОбщая(Объект, ВидОплаты, ЭтоРасход, Сторнировать, ПланФакт, БратьИзЗаявки)
	
	МетаданныеОбъекта = Объект.Метаданные();
	НоваяСтрока = Добавить();
	
	Если МетаданныеОбъекта.Реквизиты.Найти("ДатаОплаты")<>Неопределено
		И ЗначениеЗаполнено(Объект.ДатаОплаты) Тогда
		НоваяСтрока.Период = Объект.ДатаОплаты;
	Иначе
		НоваяСтрока.Период = Объект.Дата;
	КонецЕсли;
	Если МетаданныеОбъекта.Реквизиты.Найти("Подразделение")<>Неопределено Тогда
		НоваяСтрока.Подразделение = Объект.Подразделение;
	КонецЕсли;
	Если МетаданныеОбъекта.Реквизиты.Найти("Секция")<>Неопределено Тогда
		НоваяСтрока.Секция = Объект.Секция;
	КонецЕсли;
	Если МетаданныеОбъекта.Реквизиты.Найти("Этаж")<>Неопределено Тогда
		НоваяСтрока.Этаж = Объект.Этаж;
	КонецЕсли;
	
	Если МетаданныеОбъекта.Реквизиты.Найти("ДокументОснование")<>Неопределено Тогда
		НоваяСтрока.ДокументОснование = Объект.ДокументОснование;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(НоваяСтрока.Этаж)
		И ЗначениеЗаполнено(НоваяСтрока.ДокументОснование)
		И НоваяСтрока.ДокументОснование.Метаданные().Реквизиты.Найти("СчетНаОплатуПоставщика")<>Неопределено Тогда
		НоваяСтрока.Этаж = НоваяСтрока.ДокументОснование.СчетНаОплатуПоставщика.Этаж;
	КонецЕсли;
	
	Если МетаданныеОбъекта.Реквизиты.Найти("СтатьяДвиженияДенежныхСредств")<>Неопределено Тогда
		НоваяСтрока.СтатьяДвиженияДенежныхСредств = Объект.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
	Если БратьИзЗаявки
		И ТипЗнч(НоваяСтрока.ДокументОснование)=Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств")
		И ЗначениеЗаполнено(НоваяСтрока.ДокументОснование) Тогда
		НоваяСтрока.Подразделение = НоваяСтрока.ДокументОснование.Подразделение;
		НоваяСтрока.СтатьяДвиженияДенежныхСредств = НоваяСтрока.ДокументОснование.СтатьяДвиженияДенежныхСредств;
	КонецЕсли; 
	
	НоваяСтрока.ВидОплаты = ВидОплаты;
	НоваяСтрока.ПланФакт = ПланФакт;
	
	НоваяСтрока.Сумма = Объект.СуммаДокумента;
	Если ЭтоРасход Тогда
		//++ Бахтишаев А.Э. , 13/10/16 10:48, 311
		НоваяСтрока.НаправлениеДДС	= Перечисления.НаправленияДвиженийДенежныхСредств.Расход;
		//-- Бахтишаев А.Э. , 13/10/16 10:48
		НоваяСтрока.СуммаРасход = НоваяСтрока.Сумма;
	Иначе
		//++ Бахтишаев А.Э. , 13/10/16 10:48, 311
		НоваяСтрока.НаправлениеДДС	= Перечисления.НаправленияДвиженийДенежныхСредств.Приход;
		//-- Бахтишаев А.Э. , 13/10/16 10:48
		НоваяСтрока.СуммаПриход = НоваяСтрока.Сумма;
	КонецЕсли;
	
	Если Сторнировать Тогда
		НоваяСтрока.СуммаРасход = -НоваяСтрока.СуммаРасход;
		НоваяСтрока.СуммаПриход = -НоваяСтрока.СуммаПриход;
	КонецЕсли; 
	
	Записывать = Истина;
	
КонецПроцедуры

Процедура ВыполнитьДвижение(Объект) Экспорт
	
	ТипОбъекта = ТипЗнч(Объект);
	
	ВидОплаты = Перечисления.ВидыДенежныхСредств.Безналичные;
	Если ТипОбъекта=Тип("ДокументОбъект.РасходныйКассовыйОрдер")
		ИЛИ ТипОбъекта=Тип("ДокументОбъект.ПриходныйКассовыйОрдер") Тогда
		ВидОплаты = Перечисления.ВидыДенежныхСредств.Наличные;
	КонецЕсли; 
	
	ЭтоРасход = Ложь;
	Сторнировать = Ложь;
	БратьИзЗаявки = Ложь;
	
	Если ТипОбъекта=Тип("ДокументОбъект.ПриходныйКассовыйОрдер")
		ИЛИ ТипОбъекта=Тип("ДокументОбъект.ПлатежноеПоручениеВходящее") Тогда
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПодотчетником 
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПоставщиком тогда
			ЭтоРасход = Истина;
			Сторнировать = Истина;
		КонецЕсли;
	ИначеЕсли ТипОбъекта=Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее") Тогда
		ЭтоРасход = Истина;
	ИначеЕсли ТипОбъекта=Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		БратьИзЗаявки = Истина;
		ЭтоРасход = Истина;
	КонецЕсли; 
	
	ВыполнитьДвижениеОбщая(Объект, ВидОплаты, ЭтоРасход, Сторнировать, Ложь, БратьИзЗаявки);
	
КонецПроцедуры