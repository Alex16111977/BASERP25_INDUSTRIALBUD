Функция ПолучитьСоответствие(ИмяСправочника, Подразделение) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиЗагрузкиСчетов.Ключ,
	|	НастройкиЗагрузкиСчетов.Значение
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиСчетов КАК НастройкиЗагрузкиСчетов
	|ГДЕ
	|	НастройкиЗагрузкиСчетов.Справочник = &Справочник
	|	И НастройкиЗагрузкиСчетов.Подразделение = &Подразделение"
	);
	Запрос.УстановитьПараметр("Справочник", ИмяСправочника);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Результат = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Ключ, Выборка.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеПоКлючу(ИмяСправочника, Подразделение, Ключ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиЗагрузкиСчетов.Ключ,
	|	НастройкиЗагрузкиСчетов.Значение
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиСчетов КАК НастройкиЗагрузкиСчетов
	|ГДЕ
	|	НастройкиЗагрузкиСчетов.Справочник = &Справочник
	|	И НастройкиЗагрузкиСчетов.Ключ = &Ключ
	|	И НастройкиЗагрузкиСчетов.Подразделение = &Подразделение"
	);	
	Запрос.УстановитьПараметр("Справочник", ИмяСправочника);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Ключ", Ключ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли; 
	
	Возврат Неопределено;
	
КонецФункции

Процедура СохранитьЗначениеПоКлючу(ИмяСправочника, Подразделение, Ключ, Значение) Экспорт
	
	Запись = РегистрыСведений.НастройкиЗагрузкиСчетов.СоздатьМенеджерЗаписи();
	Запись.Подразделение = Подразделение;
	Запись.Справочник = ИмяСправочника;
	Запись.Ключ = Ключ;
	Запись.Значение = Значение;
	Запись.Записать();
	
КонецПроцедуры