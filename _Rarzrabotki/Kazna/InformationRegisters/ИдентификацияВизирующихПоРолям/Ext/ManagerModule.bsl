
Функция ПолучитьМассивПользователей( пПараметры ) Экспорт 
	
	лРоль			= Неопределено;
	лДата			= Неопределено;
	лПодразделение	= Неопределено;
	лОрганизация    = Неопределено;
	лВидОплаты      = Неопределено;
	лСтатьяДДС		= Неопределено; 
	лНаправление		= Неопределено; 
	
	
	пПараметры.Свойство("Подразделение", 	лПодразделение);
	пПараметры.Свойство("Организация", 		лОрганизация);
	пПараметры.Свойство("ВидОплаты", 		лВидОплаты);
	пПараметры.Свойство("Роль", 			лРоль);
	пПараметры.Свойство("Дата", 			лДата);
	пПараметры.Свойство("Направление", 		лНаправление);
	                                                           
	Если НЕ пПараметры.Свойство("СтатьяДДС", лСтатьяДДС) Тогда 
		пПараметры.Свойство("СтатьяДвиженияДенежныхСредств", лСтатьяДДС);
	КонецЕсли;
	                                                     
	лПодразделение = ?(Не ЗначениеЗаполнено(лПодразделение), Справочники.Подразделения.ПустаяСсылка(), лПодразделение);
	лПодразделения = Новый Массив();
	лПодразделения.Добавить(Неопределено);
	лПодразделения.Добавить(Справочники.Подразделения.ПустаяСсылка());
	Пока ЗначениеЗаполнено(лПодразделение) Цикл 
		лПодразделения.Добавить(лПодразделение);
		лПодразделение = лПодразделение.Родитель;
	КонецЦикла;
	
	лНаправления = Новый Массив();
	Если ЗначениеЗаполнено(лНаправление) Тогда
		лНаправления.Добавить(лНаправление);
	Иначе	
		лНаправления.Добавить(Справочники.Направления.ПустаяСсылка());
	КонецЕсли;

	
	лОрганизации  = Новый Массив();
	лОрганизации.Добавить(лОрганизация);
	лОрганизации.Добавить(Справочники.Организации.ПустаяСсылка());
	лОрганизации.Добавить(Неопределено);
	
	лДата 			=?(лДата = Неопределено, ТекущаяДата(), лДата);
	
	лЗапрос 		= Новый Запрос();
	
	лЗапрос.Текст 	= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	  |	Т.Пользователь КАК Пользователь1,
	              	  |	ВЫБОР
	              	  |		КОГДА Т.Переадресация = ИСТИНА
	              	  |				И (Т.ПереадресацияДо >= &Дата
	              	  |					ИЛИ Т.ПереадресацияДо = &Дата0)
	              	  |				И (Т.ПереадресацияС <= &Дата
	              	  |					ИЛИ Т.ПереадресацияС = &Дата0)
	              	  |			ТОГДА Т.Переадресант
	              	  |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	              	  |	КОНЕЦ КАК Пользователь2
	              	  |ПОМЕСТИТЬ Т_Данные
	              	  |ИЗ
	              	  |	РегистрСведений.ИдентификацияВизирующихПоРолям КАК Т
	              	  |ГДЕ
	              	  |	Т.Роль = &Роль
	              	  |	И (Т.Роль.ВидАдресации = ЗНАЧЕНИЕ(Перечисление.ВидыАдресацийРолейВизирования.ПоОрганизации)
	              	  |				И Т.Объект В (&Организации)
	              	  |			ИЛИ Т.Роль.ВидАдресации = ЗНАЧЕНИЕ(Перечисление.ВидыАдресацийРолейВизирования.ПоПодразделению)
	              	  |				И Т.Объект В (&Подразделения)
	              	  |			ИЛИ Т.Роль.ВидАдресации = ЗНАЧЕНИЕ(Перечисление.ВидыАдресацийРолейВизирования.ПоНаправлению)
	              	  |				И Т.Объект В (&Направления))
	              	  |;
	              	  |
	              	  |////////////////////////////////////////////////////////////////////////////////
	              	  |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	  |	Т.Пользователь1 КАК Пользователь
	              	  |ИЗ
	              	  |	(ВЫБРАТЬ
	              	  |		Т_Данные.Пользователь1 КАК Пользователь1
	              	  |	ИЗ
	              	  |		Т_Данные КАК Т_Данные
	              	  |	
	              	  |	ОБЪЕДИНИТЬ ВСЕ
	              	  |	
	              	  |	ВЫБРАТЬ
	              	  |		Т_Данные.Пользователь2
	              	  |	ИЗ
	              	  |		Т_Данные КАК Т_Данные) КАК Т
	              	  |ГДЕ
	              	  |	Т.Пользователь1 <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
					  
 	лЗапрос.УстановитьПараметр("Подразделения", лПодразделения);
	лЗапрос.УстановитьПараметр("Организации", 	лОрганизации);
	лЗапрос.УстановитьПараметр("Направления", 	лНаправления);
	лЗапрос.УстановитьПараметр("Роль",			лРоль);
	лЗапрос.УстановитьПараметр("Дата",			лДата);
	лЗапрос.УстановитьПараметр("Дата0", 		Дата("00010101"));

	лТЗ 	= лЗапрос.Выполнить().Выгрузить();
	Возврат лТЗ.ВыгрузитьКолонку("Пользователь");

КонецФункции
