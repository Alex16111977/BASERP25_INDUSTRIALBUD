

Функция ПолучитьМассивВизерей( пПараметры ) Экспорт 
	
	лПодразделение	= Неопределено;
	лОрганизация    = Неопределено;
	лВидОплаты      = Неопределено;
	лСтатьяДДС		= Неопределено;
	лСумма          = Неопределено;
	
	пПараметры.Свойство("Подразделение", 	лПодразделение);
	пПараметры.Свойство("Организация", 		лОрганизация);
	пПараметры.Свойство("ВидОплаты", 		лВидОплаты);
	пПараметры.Свойство("Сумма", 			лСумма);
	Если НЕ пПараметры.Свойство("СтатьяДДС", лСтатьяДДС) Тогда 
		пПараметры.Свойство("СтатьяДвиженияДенежныхСредств", лСтатьяДДС);
	КонецЕсли;
	
	лПодразделение = ?(Не ЗначениеЗаполнено(лПодразделение), Справочники.Подразделения.ПустаяСсылка(), лПодразделение);
	лПодразделения = Новый Массив();
	Пока ЗначениеЗаполнено(лПодразделение) Цикл 
		лПодразделения.Добавить(лПодразделение);
		лПодразделение = лПодразделение.Родитель;
	КонецЦикла;
	
	лСтатьиДДС 		= Новый Массив();
	Пока ЗначениеЗаполнено(лСтатьяДДС) Цикл 
		лСтатьиДДС.Добавить(лСтатьяДДС);
		лСтатьяДДС 		= лСтатьяДДС.Родитель;
	КонецЦикла;
	
	лСумма	=?(лСумма = Неопределено, -1, 	лСумма);
	
	лЗапрос 		= Новый Запрос();
	
	лЗапрос.Текст 	= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	  |	Т.Роль,
	              	  |	Т.Роль.Приоритет КАК Приоритет
	              	  |ИЗ
	              	  |	РегистрСведений.ОпределениеВизирующих КАК Т
	              	  |ГДЕ
	              	  |	(Т.Подразделение В (&Подразделения)
	              	  |			ИЛИ Т.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	              	  |	И (Т.Организация = &Организация
	              	  |			ИЛИ Т.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	              	  |	И (Т.ВидОплаты = &ВидОплаты
	              	  |			ИЛИ Т.ВидОплаты = ЗНАЧЕНИЕ(перечисление.ВидыДенежныхСредств.ПустаяСсылка))
	              	  |	И (Т.СтатьяДвиженияДенежныхСредств В (&СтатьиДДС)
	              	  |			ИЛИ Т.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка))
	              	  |	И (Т.Сумма <= &Сумма
	              	  |			ИЛИ Т.Сумма = 0)
	              	  |
	              	  |УПОРЯДОЧИТЬ ПО
	              	  |	Приоритет УБЫВ";
					  
 	лЗапрос.УстановитьПараметр("Подразделения", лПодразделения);
 	лЗапрос.УстановитьПараметр("Организация", 	лОрганизация);
 	лЗапрос.УстановитьПараметр("ВидОплаты", 	лВидОплаты);
 	лЗапрос.УстановитьПараметр("СтатьиДДС",		лСтатьиДДС);
	лЗапрос.УстановитьПараметр("Сумма",			лСумма);
	
	лТЗ = лЗапрос.Выполнить().Выгрузить();
	Возврат лТЗ.ВыгрузитьКолонку("Роль");

КонецФункции
