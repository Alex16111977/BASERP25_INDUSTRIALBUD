Функция ПолучитьМассивВизерейПоСхеме(СхемаВизирования) Экспорт
	
	Если Не ЗначениеЗаполнено(СхемаВизирования) Тогда 
		Возврат Новый Массив();
	Иначе
		лЗапрос = Новый Запрос();
		лЗапрос.Текст = "ВЫБРАТЬ
		                |	Т.Ссылка,
		                |	Т.НомерСтроки КАК НомерСтроки,
		                |	Т.Роль
		                |ИЗ
		                |	Справочник.СхемыВизирования.Роли КАК Т
		                |ГДЕ
		                |	Т.Ссылка = &Ссылка
		                |
		                |УПОРЯДОЧИТЬ ПО
		                |	НомерСтроки";
		лЗапрос.УстановитьПараметр("Ссылка", СхемаВизирования);
		Возврат лЗапрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль"); 
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСхемуВизирования(пПараметры) Экспорт 
	
	лПодразделение	= Неопределено;
	лОрганизация    = Неопределено;
	лВидОплаты      = Неопределено;
	лСтатьяДДС		= Неопределено;
	лСумма          = Неопределено;
	лВидЗатрат		= Неопределено;  
	лНаправление		= Неопределено;  
	лДолжность		= Неопределено;  
	
	
	пПараметры.Свойство("Подразделение", 	лПодразделение);
	пПараметры.Свойство("Организация", 		лОрганизация);
	пПараметры.Свойство("ВидОплаты", 		лВидОплаты);
	пПараметры.Свойство("Сумма", 			лСумма);
	пПараметры.Свойство("ВидЗатрат", 		лВидЗатрат);
	пПараметры.Свойство("СтатьяДвиженияДенежныхСредств", лСтатьяДДС);
	пПараметры.Свойство("Направление", лНаправление);
	пПараметры.Свойство("Должность", лДолжность);
	
	лНаправления = Новый Массив();
	Если ЗначениеЗаполнено(лНаправление) Тогда
		лНаправления.Добавить(лНаправление);	
	Иначе	
		лНаправления.Добавить(Справочники.Направления.ПустаяСсылка());	
	КонецЕсли;                                                          
	
	лДолжности = Новый Массив();
	Если ЗначениеЗаполнено(лДолжность) Тогда
		лДолжности.Добавить(лДолжность);
		лДолжности.Добавить(Справочники.Должности.ПустаяСсылка());	
	Иначе	    
		лДолжность= Справочники.Должности.ПустаяСсылка();
		лДолжности.Добавить(Справочники.Должности.ПустаяСсылка());	
	КонецЕсли;                    
	
	лПодразделения = Новый Массив();
	Пока ЗначениеЗаполнено(лПодразделение) Цикл 
		лПодразделения.Добавить(лПодразделение);
		лПодразделение = лПодразделение.Родитель;
	КонецЦикла;
	лПодразделения.Добавить(Справочники.Подразделения.ПустаяСсылка()); 
	
	лСтатьиДДС 		= Новый Массив();
	Пока ЗначениеЗаполнено(лСтатьяДДС) Цикл 
		лСтатьиДДС.Добавить(лСтатьяДДС);
		лСтатьяДДС 		= лСтатьяДДС.Родитель;
	КонецЦикла;
	лСтатьиДДС.Добавить( Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка() ); 
	
	лСумма	=?(лСумма = Неопределено, 0, 	лСумма);
	
	лЗапрос 		= Новый Запрос();
	
	лЗапрос.Текст 	= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	  |	Т.СхемаВизирования,
	              	  |	Т.Подразделение,
	              	  |	Т.Организация,
	              	  |	Т.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
	              	  |	Т.ВидЗатрат,
	              	  |	Т.ВидОплаты,
	              	  |	ВЫБОР
	              	  |		КОГДА Т.Должность = &лДолжность
	              	  |				И Т.СтатьяДвиженияДенежныхСредств = &лСтатьяДДС
	              	  |			ТОГДА 1
	              	  |		КОГДА Т.Должность = &лДолжность
	              	  |				И Т.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	              	  |			ТОГДА 3
	              	  |		КОГДА Т.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
	              	  |				И Т.СтатьяДвиженияДенежныхСредств = &лСтатьяДДС
	              	  |			ТОГДА 2
	              	  |		ИНАЧЕ 9
	              	  |	КОНЕЦ КАК Приоритет,
	              	  |	Т.Должность,
	              	  |	Т.Приоритет КАК ПриоритетСхемы
	              	  |ИЗ
	              	  |	РегистрСведений.ОпределениеСхемыВизированияПоСчетам КАК Т
	              	  |ГДЕ
	              	  |	Т.Подразделение В(&Подразделения)
	              	  |	И Т.Организация В(&Организации)
	              	  |	И Т.Должность В(&Должности)
	              	  |	И Т.Направление В(&Направления)
	              	  |	И Т.ВидОплаты В(&ВидыОплат)
	              	  |	И Т.СтатьяДвиженияДенежныхСредств В(&СтатьиДДС)
	              	  |	И Т.ВидЗатрат В(&ВидыЗатрат)
	              	  |	И Т.Сумма <= &Сумма
	              	  |
	              	  |УПОРЯДОЧИТЬ ПО
	              	  |	Приоритет,ПриоритетСхемы";
	
	пПараметры.Свойство("СтатьяДвиженияДенежныхСредств", лСтатьяДДС);
	лЗапрос.УстановитьПараметр("лСтатьяДДС", лСтатьяДДС);
	лЗапрос.УстановитьПараметр("лДолжность", лДолжность);
 	лЗапрос.УстановитьПараметр("Подразделения", лПодразделения);
	лЗапрос.УстановитьПараметр("Должности", лДолжности);
	лЗапрос.УстановитьПараметр("Направления", лНаправления);
 	лЗапрос.УстановитьПараметр("СтатьиДДС",		лСтатьиДДС);
	лЗапрос.УстановитьПараметр("Организации", 	ПолучитьМассивИзЗначения(лОрганизация, Справочники.Организации.ПустаяСсылка() ) );
 	лЗапрос.УстановитьПараметр("ВидыОплат", 	ПолучитьМассивИзЗначения(лВидОплаты, Перечисления.ВидыДенежныхСредств.ПустаяСсылка() ) );
	лЗапрос.УстановитьПараметр("ВидыЗатрат",	ПолучитьМассивИзЗначения(лВидЗатрат, Перечисления.ВидыЗатрат.ПустаяСсылка()) );
	лЗапрос.УстановитьПараметр("Сумма",			лСумма);
	
	лТЗ = лЗапрос.Выполнить().Выгрузить();
	лТЗ.Колонки.Добавить("Глубина");
	
	Для Каждого лСтрока Из лТЗ  Цикл
		лСтрока.Глубина = 1000+?(лСтрока.СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка(), 0, СтрДлина(лСтрока.СтатьяДДС.ПолныйКод()) )
						+?(лСтрока.Подразделение = Справочники.Подразделения.ПустаяСсылка(), 0, СтрДлина(лСтрока.Подразделение.ПолныйКод()) ) 
	КонецЦикла;
	
	лТЗ.Сортировать("Приоритет,ПриоритетСхемы, Глубина Убыв, Подразделение Убыв, СтатьяДДС Убыв, Организация Убыв,  ВидЗатрат Убыв, ВидОплаты Убыв");
	
	Возврат ?(лТЗ.Количество() = 0, Справочники.СхемыВизирования.ПустаяСсылка(), лТЗ[0].СхемаВизирования);
		
КонецФункции

Функция ПолучитьМассивВизерей(пПараметры, пЭтоТестСхемы = Неопределено) Экспорт 
	
	СхемаВизирования = ПолучитьСхемуВизирования(пПараметры);
	Если пЭтоТестСхемы=Истина Тогда
		Возврат СхемаВизирования;
	КонецЕсли;
	Возврат ПолучитьМассивВизерейПоСхеме(СхемаВизирования);
	
КонецФункции

Функция ПолучитьМассивИзЗначения(Знач пЗначение, пЗначение2 = Неопределено)
	лРезултат = Новый Массив();
	лРезултат.Добавить( пЗначение ) ;
	
	Если НЕ пЗначение2 = Неопределено Тогда 
		лРезултат.Добавить( пЗначение2 ) ;
	КонецЕсли;
	
	Возврат лРезултат;
КонецФункции
