Функция НайтиСправочник(Знач Значение, ИмяСправочника, ДополнительныеПараметры, ДанныеСтроки) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Справочники[ИмяСправочника].ПустаяСсылка();
	КонецЕсли; 
	
	МетаданныеСправочника = Метаданные.Справочники[ИмяСправочника];
	Значение = Лев(СокрЛП(Значение), МетаданныеСправочника.ДлинаНаименования);	
	
	ИмяКэша = "Ссылки" + ИмяСправочника;
	Если ДополнительныеПараметры.Свойство(ИмяКэша) Тогда
		Результат = ДополнительныеПараметры[ИмяКэша][Значение];
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить(ИмяКэша, Новый Соответствие);
	КонецЕсли;
	
	ИмяНеНайдено = "НеНайдено" + ИмяСправочника;
	Если ДополнительныеПараметры.Свойство(ИмяНеНайдено) Тогда
		Результат = ДополнительныеПараметры[ИмяНеНайдено][Значение];
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить(ИмяНеНайдено, Новый Соответствие);
	КонецЕсли;
		
	МассивПоиска = Неопределено;
	Если ДополнительныеПараметры.Свойство("Поиск"+ИмяСправочника, МассивПоиска) Тогда
		// Ищем по произвольным реквизитам
		Для Каждого РеквизитПоиска Из МассивПоиска Цикл
			Результат = Справочники[ИмяСправочника].НайтиПоРеквизиту(РеквизитПоиска.Ключ, Значение, РеквизитПоиска.Значение);
			Если ЗначениеЗаполнено(Результат) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат) Тогда
		// Ищем по наименованию
		Результат = Справочники[ИмяСправочника].НайтиПоНаименованию(Значение, Истина);
		Если Не ЗначениеЗаполнено(Результат) Тогда
			// Ищем по коду
			Результат = Справочники[ИмяСправочника].НайтиПоКоду(Значение);
		КонецЕсли;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Если ДополнительныеПараметры.Свойство("Создавать"+ИмяСправочника) Тогда
			ОбъектСправочника = Справочники[ИмяСправочника].СоздатьЭлемент();
			ОбъектСправочника.Наименование = Значение;
			ДополнительныеПараметрыНового = Неопределено;
			Если ДополнительныеПараметры.Свойство("Новые"+ИмяСправочника, ДополнительныеПараметрыНового) Тогда
				Для Каждого Строка Из ДополнительныеПараметрыНового Цикл
					ОбъектСправочника[Строка.Ключ] = ДанныеСтроки[Строка.Значение];
				КонецЦикла; 
			КонецЕсли;
			Если ДополнительныеПараметры.Свойство("Реквизиты"+ИмяСправочника, ДополнительныеПараметрыНового) Тогда
				ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДополнительныеПараметрыНового);
			КонецЕсли;		
			ОбъектСправочника.ОбменДанными.Загрузка = Истина;
			ОбъектСправочника.Записать();
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = ОбъектСправочника.Ссылка;
			СообщениеПользователю.Текст = "Создан " + ОбъектСправочника.Ссылка;
			СообщениеПользователю.Сообщить();
			Результат = ОбъектСправочника.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ДополнительныеПараметры[ИмяКэша].Вставить(Значение, Результат);
		Возврат Результат;
	КонецЕсли;
	
	ДополнительныеПараметры[ИмяНеНайдено].Вставить(Значение, Истина);
	СообщениеПользователю = Новый СообщениеПользователю;
	СообщениеПользователю.Текст = "" + Значение + " не найден " + МетаданныеСправочника;
	СообщениеПользователю.Сообщить();			
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, ИндексКолонки)
	
	Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
		Если ДополнительныеПараметры.ADODBRecordset.Fields.Count>(ИндексКолонки - 1) Тогда
			Поле = ДополнительныеПараметры.ADODBRecordset.Fields.Item(ИндексКолонки - 1);
			Если Поле.ActualSize = 0 Тогда // Пустое поле EXCEL.
	        	Возврат "";
			КонецЕсли;
			Возврат СокрЛП(Поле.Value);
		Иначе
			Возврат "";
		КонецЕсли; 
		//лВремПерем = Поле.Value;
		//лВремПерем = ?(ТипЗнч(лВремПерем) = Тип("Число"), Формат(лВремПерем,"ЧДЦ=; ЧГ=" ), Строка(лВремПерем) ); 
		//Возврат СокрЛП(лВремПерем);
	ИначеЕсли ДополнительныеПараметры.Свойство("ТабличныйДокумент") Тогда
		Возврат СокрЛП(ДополнительныеПараметры.ТабличныйДокумент.Область(ИндексСтроки, ИндексКолонки).Текст);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДату(Знач Значение) Экспорт
	
	ЧислоМесяц = 1;
	ЧислоДень = 1;
	ЧислоГод = 1;
	ЧислоЧас = 0;
	ЧислоМинута = 0;
	ЧислоСекунда = 0;
	
	МассивДатаВремя = СтрРазделить(Значение, " ");
	
	Если МассивДатаВремя.Количество()>0 Тогда
		ЧастьДаты = МассивДатаВремя[0];
		ЧастьДаты = СтрЗаменить(ЧастьДаты, "-", ".");
		ЧастьДаты = СтрЗаменить(ЧастьДаты, "/", ".");
		ЧастьДаты = СтрЗаменить(ЧастьДаты, "\", ".");
		ЧастьДаты = СтрЗаменить(ЧастьДаты, ",", ".");
		МассивДеньМесяцГод = СтрРазделить(ЧастьДаты, ".", Истина);
		Если МассивДеньМесяцГод.Количество()>0 Тогда
			ЧислоДень = Число(МассивДеньМесяцГод[0]);
		КонецЕсли; 
		Если МассивДеньМесяцГод.Количество()>1 Тогда
			ЧислоМесяц = Число(МассивДеньМесяцГод[1]);
		КонецЕсли; 
		Если МассивДеньМесяцГод.Количество()>2 Тогда
			ЧислоГод = Число(МассивДеньМесяцГод[2]);
		КонецЕсли; 
	КонецЕсли; 
	
	Если МассивДатаВремя.Количество()>1 Тогда
		ЧастьДаты = МассивДатаВремя[1];
		Если КодСимвола(ЧастьДаты, 1)>=48 И КодСимвола(ЧастьДаты, 1)<=57 Тогда
			МассивЧасМинутаСекунда = СтрРазделить(ЧастьДаты, ":", Истина);
			ЧислоЧас = Число(МассивЧасМинутаСекунда[0]);
			Если МассивЧасМинутаСекунда.Количество()>1 И ЗначениеЗаполнено(МассивЧасМинутаСекунда[1]) Тогда
				ЧислоМинута = Число(МассивЧасМинутаСекунда[1]);
			КонецЕсли; 
			Если МассивЧасМинутаСекунда.Количество()>2 И ЗначениеЗаполнено(МассивЧасМинутаСекунда[2]) Тогда
				ЧислоСекунда = Число(МассивЧасМинутаСекунда[2]);
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
	Если ЧислоЧас>=24 Тогда
		ЧислоЧас = 0;
	КонецЕсли; 
	Если ЧислоМинута>=60 Тогда
		ЧислоМинута = 0;
	КонецЕсли; 
	Если ЧислоСекунда>=60 Тогда
		ЧислоСекунда = 0;
	КонецЕсли; 
	Если ЧислоГод>=2200 ИЛИ ЧислоГод<100 Тогда
		ЧислоГод = Год(ТекущаяДата());
	КонецЕсли;
	Если ЧислоМесяц>12 Тогда
		ЧислоМесяц = 12;
	КонецЕсли;
	Если ЧислоМесяц=0 Тогда
		ЧислоМесяц = 1;
	КонецЕсли;
	Если ЧислоДень=0 Тогда
		Возврат Дата(1,1,1);
	КонецЕсли;	
	
	Возврат Дата(ЧислоГод, ЧислоМесяц, ЧислоДень, ЧислоЧас, ЧислоМинута, ЧислоСекунда);
	
КонецФункции

Функция ПолучитьКомпоненту(ИмяКомпоненты) Экспорт
	
	Попытка
    	Соединение = Новый COMОбъект(ИмяКомпоненты); // Инициализация ADODB.
	Исключение
		Информация = ИнформацияОбОшибке();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "На компьютере " + ИмяКомпьютера() + " не найдена компонента " + ИмяКомпоненты + Символы.ПС
			+ Информация.Причина.Описание;
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

Функция ОткрытьСоединениеADO(ПутьКФайлу) Экспорт
	
	Если ПутьКФайлу=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Соединение = ПолучитьКомпоненту("ADODB.Connection");
	Если Соединение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СonnectionString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source= "
		+ СокрЛП(ПутьКФайлу)
		+ ";Extended Properties=""Excel 12.0;HDR=YES;IMEX=1;""";
		
	Попытка
        Соединение.Open(СonnectionString);                                      		
	Исключение
		Информация = ИнформацияОбОшибке();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "На компьютере " + ИмяКомпьютера() + " не удается открыть ADODB.Connection со строкой:" + Символы.ПС
			+ "'" + СonnectionString + "'" + Символы.ПС
			+ Информация.Причина.Описание;
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

Процедура ЗакрытьСоединениеADO(Соединение) Экспорт
	Соединение.Close();
    Соединение = Неопределено;	
КонецПроцедуры

Функция ПолучитьИмяФайла(ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("ИмяВременногоФайла") Тогда
		Возврат ДополнительныеПараметры.ИмяВременногоФайла;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("АдресФайлаВХранилище") Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(ДополнительныеПараметры.РасширениеФайлаВХранилище);
		Данные = ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресФайлаВХранилище);
		Данные.Записать(ИмяВременногоФайла);
		ДополнительныеПараметры.Вставить("ИмяВременногоФайла", ИмяВременногоФайла);
		Возврат ИмяВременногоФайла;
		
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "В структуре дополнительных свойств нет свойства ""АдресФайлаВХранилище""";
	Сообщение.Сообщить();
	Возврат Неопределено;	
		
КонецФункции

Функция ПолучитьСписокЛистов(ДополнительныеПараметры) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяФайла(ДополнительныеПараметры);
	Соединение = ОткрытьСоединениеADO(ИмяВременногоФайла);
	Если Соединение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Каталог = Новый COMОбъект("ADOX.Catalog");
	Каталог.ActiveConnection = Соединение;
    МассивЛистов = Каталог.Tables;
	СписокЛистов = Новый СписокЗначений;
            
    Для Каждого ЛистМассива ИЗ МассивЛистов Цикл
    	ИмяЛиста = ЛистМассива.Name;
        Если ИмяЛиста = "Excel_BuiltIn_Database" Тогда
        	Продолжить;
		КонецЕсли;
        // Добавляем в список имя листа без знака $. Исключительно для удобвства восписятия при выборе листа из списка.
		ПредставлениеЛиста = СокрЛП(ИмяЛиста);
		ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "#", "_");
		ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "'", "");
		ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "$", "");
        СписокЛистов.Добавить(ИмяЛиста, ПредставлениеЛиста);
	КонецЦикла;
	
	СписокЛистов.СортироватьПоПредставлению();
	ЗакрытьСоединениеADO(Соединение);
	
	Возврат СписокЛистов;
		
КонецФункции

Функция СоздатьТалицуРезультата(МассивКолонок) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Для Каждого Строка Из МассивКолонок Цикл
		Таблица.Колонки.Добавить(Строка.Ключ, Новый ОписаниеТипов("Строка"));
	КонецЦикла;
	Возврат Таблица;
	
КонецФункции

Функция ПолучитьМассивКолонокИзТаблицыНастроек(ТаблицаНастроек) Экспорт
	
	Возврат ТаблицаНастроек.НайтиСтроки(Новый Структура("Использовать", Истина));
	
КонецФункции

Процедура ЗагрузитьВТаблицу(ТаблицаРезультат, МассивКолонок, НачальнаяСтрока, ДополнительныеПараметры) Экспорт
	
	ТипСправочников = Справочники.ТипВсеСсылки();
	КоличествоПустыхСтрокПодряд = 0;
	
	ИндексСтроки = 0;
	Пока Истина Цикл
		
		ИндексСтроки = ИндексСтроки + 1;
 		Если ИндексСтроки < НачальнаяСтрока Тогда
			Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
				ДополнительныеПараметры.ADODBRecordset.MoveNext();
			КонецЕсли;		
            Продолжить;
        КонецЕсли;		
		
		Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
			Если ДополнительныеПараметры.ADODBRecordset.EOF()<>0 Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;		
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		
		Если ДополнительныеПараметры.Свойство("ЗначенияНовойСтроки") Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДополнительныеПараметры.ЗначенияНовойСтроки);
		КонецЕсли; 
		
		СтрокаЗаполнена = Ложь;
		
		Для Каждого Строка Из МассивКолонок Цикл
			
			ИмяРеквизита = Строка.Ключ;
			ЗначениеКолонки = ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, Строка.Значение);
			
			ТипРеквизита = ТипЗнч(НоваяСтрока[Строка.Ключ]);
			Если Не ЗначениеЗаполнено(ЗначениеКолонки) Тогда
				НоваяСтрока[ИмяРеквизита] = Неопределено;
				Продолжить;
			КонецЕсли;
			
			СтрокаЗаполнена = Истина;
			Если ТипСправочников.СодержитТип(ТипРеквизита) Тогда
				МетаданныеСправочника = НоваяСтрока[ИмяРеквизита].Метаданные();
				НоваяСтрока[ИмяРеквизита] = НайтиСправочник(ЗначениеКолонки, МетаданныеСправочника.Имя, ДополнительныеПараметры, НоваяСтрока);
			ИначеЕсли ТипРеквизита=Тип("Число") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, ",", ".");
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, " ", "");
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, Символ(160), "");
				НоваяСтрока[ИмяРеквизита] = Число(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Дата") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, "/", ".");
				НоваяСтрока[ИмяРеквизита] = ПолучитьДату(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Строка") Тогда
				Если ЛЕВ(ЗначениеКолонки, 2)="0," ИЛИ ЛЕВ(ЗначениеКолонки, 2)="0." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 3);
				ИначеЕсли ЛЕВ(ЗначениеКолонки, 1)="." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 2);
				КонецЕсли; 
				НоваяСтрока[ИмяРеквизита] = ЗначениеКолонки;
			Иначе
				НоваяСтрока[ИмяРеквизита] = ЗначениеКолонки;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не СтрокаЗаполнена Тогда
			ТаблицаРезультат.Удалить(НоваяСтрока);
			КоличествоПустыхСтрокПодряд = КоличествоПустыхСтрокПодряд + 1;
		Иначе
			КоличествоПустыхСтрокПодряд = 0;
		КонецЕсли;
		
		Если КоличествоПустыхСтрокПодряд>=10 Тогда
			Прервать;
		КонецЕсли; 
		
		Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
			ДополнительныеПараметры.ADODBRecordset.MoveNext();
		КонецЕсли;		
		
	КонецЦикла;
		
КонецПроцедуры