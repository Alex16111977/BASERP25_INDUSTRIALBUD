&НаСервере
Процедура ЗагрузитьДокументНаСервере(Адресс, Расширение) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	Данные = ПолучитьИзВременногоХранилища(Адресс);
	Данные.Записать(ИмяВременногоФайла);
	ТабДокумент.Прочитать(ИмяВременногоФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокументПродолжение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат=Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранноеИмяФайла);
	ЗагрузитьДокументНаСервере(Адрес, Файл.Расширение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьДокументПродолжение", ЭтотОбъект)
		,,,Истина);
КонецПроцедуры

&НаСервере
Процедура ЗапланироватьЗадачу(Контакт, ВидЗадачи, ДатаЗадачи, ТемаЗадачи)
	
	НовыйОбъект = Задачи.ЗадачаПоКонтактам.СоздатьЗадачу();
	НовыйОбъект.Контакт = Контакт;
	НовыйОбъект.Вид = ВидЗадачи;
	НовыйОбъект.Дата = ДатаЗадачи;
	НовыйОбъект.Тема = ТемаЗадачи;
	НовыйОбъект.Исполнитель = Контакт.ОсновнойМенеджер;
	НовыйОбъект.Записать();
	
	//Сообщение = Новый СообщениеПользователю;
	//Сообщение.КлючДанных = НовыйОбъект.Ссылка;
	//Сообщение.Текст = "Создана " + НовыйОбъект.Ссылка;
	//Сообщение.Сообщить();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТелефон(Знач Значение)
	
	Значение = СтрЗаменить(Значение, " ", "");
	Значение = СтрЗаменить(Значение, "-", "");
	Значение = СтрЗаменить(Значение, "(", "");
	Значение = СтрЗаменить(Значение, ")", "");
	Значение = СтрЗаменить(Значение, "3,8", "");
	Значение = СтрЗаменить(Значение, "e+", "");
	Значение = СтрЗаменить(Значение, ""+Символы.ПС+"", "");
	Значение = СтрЗаменить(Значение, ""+Символы.ПФ+"", "");
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат "";
	КонецЕсли;
		
	Если ЛЕВ(Значение, 2)="38" Тогда
		Значение = Сред(Значение, 3);
	КонецЕсли; 
		
	Значение = Лев(Значение, 10);
	Если СтрДлина(Значение)=9 Тогда
		Значение = "0"+Значение;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Процедура ОбработатьДанныеНаСервере()
	
	МассивКолонок = Обработки.ЗаполнениеДокументов.ПолучитьМассивКолонокИзТаблицыНастроек(ТаблицаНастроек);
	ТаблицаРезультата = Обработки.ЗаполнениеДокументов.СоздатьТалицуРезультата(МассивКолонок);
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТабличныйДокумент", ТабДокумент);
		
	Обработки.ЗаполнениеДокументов.ЗагрузитьВТаблицу(ТаблицаРезультата, МассивКолонок, НачальнаяСтрока, ДополнительныеПараметры);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Док.Ссылка
	|ИЗ
	|	Справочник.СтроковыеКонтактыВзаимодействий.Телефоны КАК Док
	|ГДЕ
	|	Док.Телефон = &Телефон
	|	И Док.Ссылка.КонтактПредставление = &Имя"
	);	
	
	ИндексМенеджера = 0;
	КоличествоМенеджеров = СписокМенеджеров.Количество();
	СозданоКонтактов = 0;
	
	Для Каждого Строка Из ТаблицаРезультата Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Телефон) Тогда
			Продолжить;
		КонецЕсли; 
		
		СтрокаТелефон = ПолучитьТелефон(Строка.Телефон);
		Запрос.УстановитьПараметр("Имя", Строка.Имя);
		Запрос.УстановитьПараметр("Телефон", СтрокаТелефон);
		
		Выборка = Запрос.Выполнить().Выбрать();
		НайденнаяСсылка = Справочники.СтроковыеКонтактыВзаимодействий.ПустаяСсылка();
		
		Если Выборка.Следующий() Тогда
			НайденнаяСсылка = Выборка.Ссылка;
		Иначе
			НовыйОбъект = Справочники.СтроковыеКонтактыВзаимодействий.СоздатьЭлемент();
			НовыйОбъект.КонтактПредставление = Строка.Имя;
			НоваяСтрока = НовыйОбъект.Телефоны.Добавить();
			НоваяСтрока.Телефон = СтрокаТелефон;
			НовыйОбъект.EMail = Строка.Почта;
			НовыйОбъект.ИсточникОбращения = Источник;
			НовыйОбъект.Статус = Справочники.СтатусыКонтактов.Новый;
			Если КоличествоМенеджеров>0 Тогда
				НовыйОбъект.ОсновнойМенеджер = СписокМенеджеров.Получить(ИндексМенеджера%КоличествоМенеджеров).Значение;
				ИндексМенеджера = ИндексМенеджера + 1;
			КонецЕсли;
			НовыйОбъект.Записать();
			НайденнаяСсылка = НовыйОбъект.Ссылка;
			
			//Сообщение = Новый СообщениеПользователю;
			//Сообщение.КлючДанных = НовыйОбъект.Ссылка;
			//Сообщение.Текст = "Создан " + НовыйОбъект.Ссылка;
			//Сообщение.Сообщить();
			СозданоКонтактов = СозданоКонтактов + 1;
			
		КонецЕсли;
		
		Если ПланироватьЗадчу Тогда
			ЗапланироватьЗадачу(НайденнаяСсылка, ЗадачаВид, ЗадачаДата, ЗадачаТема);
		КонецЕсли; 
		
	КонецЦикла; 
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Создано " + СозданоКонтактов + " контактов";
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДанные(Команда)
	
	МассивДляУдаления = Новый Массив;
	Для Каждого Строка Из СписокМенеджеров Цикл
		Если Не ЗначениеЗаполнено(Строка.Значение) Тогда
			МассивДляУдаления.Добавить(Строка);
		КонецЕсли; 
	КонецЦикла;
	Для Каждого Строка Из МассивДляУдаления Цикл
		СписокМенеджеров.Удалить(Строка);
	КонецЦикла; 
	
	Если СписокМенеджеров.Количество()=0 Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОбработатьДанныеПродолжение", ЭтотОбъект),
			"Если список менеджер не указан Ответственным менеджером у контактов станете вы. Продолжить?",
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ОбработатьДанныеПродолжение(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли; 
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДанныеПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса<>КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработатьДанныеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВТаблицуНастроек(ИмяРеквизита, Картинка = Неопределено)
	
	Если Картинка=Неопределено Тогда
		Картинка = БиблиотекаКартинок.Реквизит;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаНастроек.Добавить();
	НоваяСтрока.Использовать = Истина;
	НоваяСтрока.Ключ = ИмяРеквизита;
	НоваяСтрока.Значение = ТаблицаНастроек.Количество();
	НоваяСтрока.Картинка = Картинка;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДобавитьВТаблицуНастроек("Имя");
	ДобавитьВТаблицуНастроек("Телефон");
	ДобавитьВТаблицуНастроек("Почта");
	РегистрыСведений.ВариантыВыбора.УстановитьВыбор(Элементы.ЗадачаТема, "ТемаЗадачи");
	НачальнаяСтрока = 2;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланироватьЗадчуПриИзменении(Элемент)
	Элементы.ПараметрыЗадачи.Видимость = ПланироватьЗадчу;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПланироватьЗадчуПриИзменении(Неопределено);
КонецПроцедуры