&НаСервере
Процедура ПараметрыЗапросаНаСервере(НомерСтроки)
	
	СтрокаДереваЗначений = ДеревоЗапросов.НайтиПоИдентификатору(НомерСтроки);
	ПараметрыЗапроса = СтрокаДереваЗначений.ПараметрыЗапроса;
	Запрос = Новый Запрос(СтрокаДереваЗначений.ТекстЗапроса);
	Попытка
		Таблица = Запрос.НайтиПараметры();
	Исключение
		Ошибка = ИнформацияОбОшибке();
		Пока Ошибка.Причина<>Неопределено Цикл
			Ошибка = Ошибка.Причина;
		КонецЦикла;
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = Ошибка.Описание;
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецПопытки;	
	
	Если Таблица.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Таблица Цикл
		
		НайденнаяСтрока = ПараметрыЗапроса.НайтиСтроки(Новый Структура("ИмяПараметра", Строка.Имя));
		Если НайденнаяСтрока.Количество()=0 Тогда
			НоваяСтрокаПараметра = ПараметрыЗапроса.Добавить();
		Иначе
			НоваяСтрокаПараметра = НайденнаяСтрока[0];
		КонецЕсли;
		
		НоваяСтрокаПараметра.ИмяПараметра = Строка.Имя;
		НоваяСтрокаПараметра.ТипЗначения = Строка.ТипЗначения;
		НоваяСтрокаПараметра.Значение = Строка.ТипЗначения.ПривестиЗначение(НоваяСтрокаПараметра.Значение);
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗапроса(Команда)
	
	Если Элементы.ДеревоЗапросов.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыЗапросаНаСервере(Элементы.ДеревоЗапросов.ТекущаяСтрока);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтоговуюСтроку()
	
	ТекстИнформации = "В таблице " + ТаблицаРезультата.Количество() + " строк";
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуРезультата(Таблица, ТипыКолонок = Неопределено)
	
	ТипЗначенияСтрокиПоУмолчанию = Новый ОписаниеТипов("Строка");
	МассивУдаляемых = Новый Массив;	
	СтараяТаблицаРезультат = РеквизитФормыВЗначение("ТаблицаРезультата");
	Для Каждого Строка Из СтараяТаблицаРезультат.Колонки Цикл
		МассивУдаляемых.Добавить("ТаблицаРезультата." + Строка.Имя);
	КонецЦикла;	
	
	МассивДляУдаленияСтрок = Новый Массив;
	Для Каждого Строка Из Элементы.ТаблицаРезультата.ПодчиненныеЭлементы Цикл
		МассивДляУдаленияСтрок.Добавить(Строка);
	КонецЦикла;
	Для Каждого Строка Из МассивДляУдаленияСтрок Цикл
		Элементы.Удалить(Строка);
	КонецЦикла; 
			
	МассивДобавляемых = Новый Массив;
	Для Каждого Строка Из Таблица.Колонки Цикл
		ТипЗначенияСтроки = Строка.ТипЗначения;
		Если Строка.ТипЗначения.СодержитТип(Тип("МоментВремени"))
			ИЛИ Строка.ТипЗначения.СодержитТип(Тип("ОбъектМетаданных")) Тогда
			ТипЗначенияСтроки = ТипЗначенияСтрокиПоУмолчанию;
		КонецЕсли;
		Если ТипыКолонок<>Неопределено Тогда
			ТипИзСоответствия = ТипыКолонок[Строка.Имя];
			Если ТипИзСоответствия<>Неопределено Тогда
				ТипЗначенияСтроки = ТипИзСоответствия;
			КонецЕсли; 
		КонецЕсли; 
		Реквизит = Новый РеквизитФормы(Строка.Имя, ТипЗначенияСтроки, "ТаблицаРезультата", Строка.Заголовок);
		МассивДобавляемых.Добавить(Реквизит);
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивДобавляемых, МассивУдаляемых);
	
	Для Каждого Строка Из Таблица.Колонки Цикл
		ЭлементРеквизита = Элементы.Добавить("ТаблицаРезультата"+Строка.Имя, Тип("ПолеФормы"), Элементы.ТаблицаРезультата);
		ЭлементРеквизита.ПутьКДанным = "ТаблицаРезультата." + Строка.Имя;
	КонецЦикла;	
	
	ТаблицаРезультата.Загрузить(Таблица);
		
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросНаСервере(НомерСтроки)
	
	СтрокаДереваЗначений = ДеревоЗапросов.НайтиПоИдентификатору(НомерСтроки);
	ПараметрыЗапроса = СтрокаДереваЗначений.ПараметрыЗапроса;
	Запрос = Новый Запрос(СтрокаДереваЗначений.ТекстЗапроса);	
	Попытка
		ЗапросПараметры = Запрос.НайтиПараметры();
		Для Каждого Строка Из ЗапросПараметры Цикл
			НайденныеСтроки = ПараметрыЗапроса.НайтиСтроки(Новый Структура("ИмяПараметра", Строка.Имя));
			Если НайденныеСтроки.Количество()>0 Тогда
				Запрос.УстановитьПараметр(Строка.Имя, НайденныеСтроки[0].Значение);
			КонецЕсли; 
		КонецЦикла; 
		Таблица = Запрос.Выполнить().Выгрузить();
	Исключение
		Ошибка = ИнформацияОбОшибке();
		Пока Ошибка.Причина<>Неопределено Цикл
			Ошибка = Ошибка.Причина;
		КонецЦикла;
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = Ошибка.Описание;
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецПопытки;
	
	МассивУдаляемых = Новый Массив;	
	СтараяТаблицаРезультат = РеквизитФормыВЗначение("ТаблицаРезультата");
	Для Каждого Строка Из СтараяТаблицаРезультат.Колонки Цикл
		МассивУдаляемых.Добавить("ТаблицаРезультата." + Строка.Имя);
	КонецЦикла;	
	
	МассивДляУдаленияСтрок = Новый Массив;
	Для Каждого Строка Из Элементы.ТаблицаРезультата.ПодчиненныеЭлементы Цикл
		МассивДляУдаленияСтрок.Добавить(Строка);
	КонецЦикла;
	Для Каждого Строка Из МассивДляУдаленияСтрок Цикл
		Элементы.Удалить(Строка);
	КонецЦикла; 
			
	МассивДобавляемых = Новый Массив;
	Для Каждого Строка Из Таблица.Колонки Цикл
		Если Строка.ТипЗначения.СодержитТип(Тип("МоментВремени")) Тогда
			Продолжить;
		КонецЕсли; 
		Реквизит = Новый РеквизитФормы(Строка.Имя, Строка.ТипЗначения, "ТаблицаРезультата", Строка.Заголовок);
		МассивДобавляемых.Добавить(Реквизит);
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивДобавляемых, МассивУдаляемых);
	
	Для Каждого Строка Из Таблица.Колонки Цикл
		Если Строка.ТипЗначения.СодержитТип(Тип("МоментВремени")) Тогда
			Продолжить;
		КонецЕсли; 
		ЭлементРеквизита = Элементы.Добавить("ТаблицаРезультата"+Строка.Имя, Тип("ПолеФормы"), Элементы.ТаблицаРезультата);
		ЭлементРеквизита.ПутьКДанным = "ТаблицаРезультата." + Строка.Имя;
	КонецЦикла;	
	
	ТаблицаРезультата.Загрузить(Таблица);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапрос(Команда)
	Если Элементы.ДеревоЗапросов.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	ВыполнитьЗапросНаСервере(Элементы.ДеревоЗапросов.ТекущаяСтрока);
	ОбновитьИтоговуюСтроку();
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапросаПродолжение(Текст, ДополнительныеПараметры) Экспорт
	
	Если Текст=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Элементы.ДеревоЗапросов.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Элементы.ДеревоЗапросов.ТекущиеДанные.ТекстЗапроса = Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапроса(Команда)
	
	Если Элементы.ДеревоЗапросов.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	ТекстЗапроса = Элементы.ДеревоЗапросов.ТекущиеДанные.ТекстЗапроса;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Конструктор = Новый КонструкторЗапроса;
	Иначе
		Конструктор = Новый КонструкторЗапроса(ТекстЗапроса);
	КонецЕсли; 
	
	Конструктор.Показать(Новый ОписаниеОповещения("КонструкторЗапросаПродолжение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗапросовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыполнитьЗапрос(Неопределено);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗапросаПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.ПараметрыЗапроса.ТекущиеДанные=Неопределено Тогда
		Элементы.ПараметрыЗапросаЗначение.ОграничениеТипа = Неопределено;
	Иначе
		ТипДанных = Элементы.ПараметрыЗапроса.ТекущиеДанные.ТипЗначения;
		Элементы.ПараметрыЗапросаЗначение.ОграничениеТипа = ТипДанных;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,Элементы.ТаблицаРезультата.ТекущиеДанные[СРЕД(Элементы.ТаблицаРезультата.ТекущийЭлемент.Имя,18)]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Добавим пустую строку
	Если ДеревоЗапросов.ПолучитьЭлементы().Количество()=0 Тогда
		НоваяСтрока = ДеревоЗапросов.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Имя = "Запросы";
	КонецЕсли; 
	ОбновитьИтоговуюСтроку();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатаПриАктивизацииСтроки(Элемент)
	ОбновитьИтоговуюСтроку();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьКодНаСервереБезКонтекста(ТаблицаРезультата, ПараметрКодДляВыполнения)
	
	Выполнить(ПараметрКодДляВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКодНаСервере(ПараметрКодДляВыполнения)
	
	ВыполнитьКодНаСервереБезКонтекста(ТаблицаРезультата.Выгрузить(), ПараметрКодДляВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКодПродолжить(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗакрытия)=Тип("Строка") Тогда
		ВыполнитьКодНаСервере(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКод(Команда)
	ОткрытьФорму("Обработка.КонсольЗапросов.Форма.ВыполнитьКод",,,,,,
		Новый ОписаниеОповещения("ВыполнитьКодПродолжить",ЭтотОбъект));
КонецПроцедуры

&НаСервере
Процедура НайтиСсылкиНаСервере(СсылкаНаОбъект)
	
	СписокСсылок = Новый Массив;
	СписокСсылок.Добавить(СсылкаНаОбъект);
	
	Таблица = НайтиПоСсылкам(СписокСсылок);
	
	ТипыКолонок = Новый Соответствие;
	ТипыКолонок.Вставить("Метаданные", Новый ОписаниеТипов("Строка"));
	
	ОбновитьТаблицуРезультата(Таблица, ТипыКолонок);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиСсылки(Команда)
	НайтиСсылкиНаСервере(Элементы.ТаблицаРезультата.ТекущиеДанные[СРЕД(Элементы.ТаблицаРезультата.ТекущийЭлемент.Имя,18)]);
КонецПроцедуры