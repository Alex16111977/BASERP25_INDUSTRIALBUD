&НаКлиенте
Процедура ПодразделенияПриИзменении(Элемент)
	ЗаполнитьПолучателей();
	ОбновитьДанные();
	ИзменитьТему();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
		
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Док.Ссылка,
	|	Док.СуммаДокумента КАК Сумма,
	|	ВЫБОР
	|		КОГДА Док.ПлановаяДатаОплаты <= &ДатаНачала
	|				И Док.Виза_Состояние = ЗНАЧЕНИЕ(Перечисление.Визы_Состояния.НаВизировании)
	|			ТОГДА ИСТИНА
	|					И &НеУтвержденныеРанее
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаУтверждении
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК Док
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &НеУтвержденныеРанее
	|				ТОГДА Док.ПлановаяДатаОплаты <= &ДатаКонца
	|						И Док.Виза_Состояние = ЗНАЧЕНИЕ(Перечисление.Визы_Состояния.НаВизировании)
	|			ИНАЧЕ Док.ПлановаяДатаОплаты МЕЖДУ &ДатаНачала И &ДатаКонца
	|		КОНЕЦ
	|	И Док.Проведен
	|	И Док.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И Док.Виза_Состояние В(&Состояние)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Док.ПлановаяДатаОплаты", "Док."+РеквизитОтбора);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ДатаОтчета));
	Запрос.УстановитьПараметр("Подразделение", Подразделения);
	Запрос.УстановитьПараметр("Состояние", Состояние);
	//++ Бахтишаев А.Э. , 19/07/16 12:34	
	Запрос.УстановитьПараметр("НеУтвержденныеРанее", НеУтвержденныеРанее);
	//-- Бахтишаев А.Э. , 19/07/16 12:34
	Объект.ТаблицаРезультат.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для Каждого Строка Из Объект.ТаблицаРезультат Цикл
		Строка.Использовать = Истина;
		Если Строка.Ссылка.ВидОплаты=Перечисления.ВидыДенежныхСредств.Безналичные Тогда
			Строка.СуммаБез = Строка.Сумма;
			Строка.СуммаНал = 0;
		Иначе
			Строка.СуммаБез = 0;
			Строка.СуммаНал = Строка.Сумма;
		КонецЕсли; 
	КонецЦикла; 
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолучателей()
	
	Для Каждого ЭлементПодразделение Из Подразделения Цикл
		Для Каждого Строка Из ЭлементПодразделение.Значение.Получатели Цикл
			Если Не ЗначениеЗаполнено(Строка.Получатель)
				ИЛИ СписокПользователей.НайтиПоЗначению(Строка.Получатель)<>Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СписокПользователей.Добавить(Строка.Получатель);
		КонецЦикла;
	КонецЦикла;
	СписокПользователей.СортироватьПоЗначению();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОтчета = ТекущаяДата();
	РеквизитОтбора = "Дата";
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистироватьСчета()
	
	ДатаОтправки = ТекущаяДата();
	Для Каждого Строка Из Объект.ТаблицаРезультат Цикл
		НовыйНабор = РегистрыСведений.ИсторияОтправкиСчетов.СоздатьМенеджерЗаписи();
		НовыйНабор.Период = ДатаОтправки;
		НовыйНабор.Объект = Строка.Ссылка;
		ЗаполнитьЗначенияСвойств(НовыйНабор, Строка.Ссылка);
		НовыйНабор.Отправитель = ПараметрыСеанса.ТекущийПользователь;
		НовыйНабор.Записать();
	КонецЦикла; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТаблицуИстории(Счета) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИсторияОтправкиСчетов.Период КАК Период,
	|	ИсторияОтправкиСчетов.Объект,
	|	ИсторияОтправкиСчетов.ПлановаяДатаОплаты,
	|	ИсторияОтправкиСчетов.Контрагент,
	|	ИсторияОтправкиСчетов.Организация,
	|	ИсторияОтправкиСчетов.Подразделение,
	|	ИсторияОтправкиСчетов.СтатьяДвиженияДенежныхСредств,
	|	ИсторияОтправкиСчетов.СуммаДокумента,
	|	ИсторияОтправкиСчетов.Отправитель
	|ИЗ
	|	РегистрСведений.ИсторияОтправкиСчетов КАК ИсторияОтправкиСчетов
	|ГДЕ
	|	ИсторияОтправкиСчетов.Объект В(&Объект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ"
	);
	Запрос.УстановитьПараметр("Объект", Счета.Выгрузить(,"Ссылка").ВыгрузитьКолонку("Ссылка"));
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьТекстИстории(СтрокиИтории, СтрокаДанных, Реквизит, Знач ТекущееЗначение, Область)
	
	ТекстИстории = "";
	
	Для Каждого СтрокаИстории Из СтрокиИтории Цикл
		Если СтрокаИстории[Реквизит]=ТекущееЗначение Тогда
			Продолжить;
		КонецЕсли;
		ТекущееЗначение = СтрокаИстории[Реквизит];
		Если Не ПустаяСтрока(ТекстИстории) Тогда
			ТекстИстории = ТекстИстории + Символы.ПС;
		КонецЕсли; 
		ТекстИстории = Формат(СтрокаИстории.Период, "ДФ=dd.MM.yyyy") + " было " + СтрокаИстории[Реквизит];
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстИстории) Тогда
		Возврат;
	КонецЕсли;
	
	Область.Области[Реквизит].Примечание.Текст = ТекстИстории;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьДанные(ИмяФайла, Таблица) Экспорт
	
	ТаблицаИстории = ПолучитьТаблицуИстории(Таблица);
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Обработки.ОтправкаСчетовНаСогласование.ПолучитьМакет("Макет");
	МакетШапки = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(МакетШапки);
	
	ИтогСуммаБез = 0;
	ИтогСуммаНал = 0;
	ИтогСуммаВсего = 0;
	
	Для Каждого Строка Из Таблица Цикл
		
		Если Не Строка.Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		МакетДетали = Макет.ПолучитьОбласть("Детали");
		МакетДетали.Параметры.НомерДокумента = Строка.Ссылка.Номер;
		МакетДетали.Параметры.Подразделение = Строка.Ссылка.Подразделение;
		МакетДетали.Параметры.СтатьяДДС = Строка.Ссылка.СтатьяДвиженияДенежныхСредств;
		МакетДетали.Параметры.Задача = Строка.Ссылка.Задача;
		МакетДетали.Параметры.СтатьяЗатрат = Строка.Ссылка.СтатьяЗатрат;
		МакетДетали.Параметры.Контрагент = СокрЛП(Строка.Ссылка.Контрагент);
		МакетДетали.Параметры.ЕДРПОУ = СокрЛП(Строка.Ссылка.Контрагент.ЕГРПОУ);
		МакетДетали.Параметры.РасчетныйСчет = Строка.Ссылка.СчетКонтрагента;
		МакетДетали.Параметры.Банк = Строка.Ссылка.СчетКонтрагента.Банк;
		МакетДетали.Параметры.МФО = Строка.Ссылка.СчетКонтрагента.Банк.Код;
		МакетДетали.Параметры.СуммаБез = Строка.СуммаБез;
		МакетДетали.Параметры.СуммаНал = Строка.СуммаНал;
		МакетДетали.Параметры.СуммаВсего = Строка.Сумма;
		МакетДетали.Параметры.НазначениеПлатежа = Строка.Ссылка.НазначениеПлатежа;
		МакетДетали.Параметры.Комментарий = Строка.Ссылка.Комментарий;
		МакетДетали.Параметры.Ответственный = Строка.Ссылка.Ответственный;
		МакетДетали.Параметры.ДатаОплаты = Строка.Ссылка.ПлановаяДатаОплаты;
		
		СтрокиИтории = ТаблицаИстории.НайтиСтроки(Новый Структура("Объект", Строка.Ссылка));
		УстановитьТекстИстории(СтрокиИтории,Строка, "СуммаДокумента", Строка.Сумма, МакетДетали);
		УстановитьТекстИстории(СтрокиИтории,Строка, "Контрагент", Строка.Ссылка.Контрагент, МакетДетали);
		УстановитьТекстИстории(СтрокиИтории,Строка, "Подразделение", Строка.Ссылка.Подразделение, МакетДетали);
		УстановитьТекстИстории(СтрокиИтории,Строка, "СтатьяДвиженияДенежныхСредств", Строка.Ссылка.СтатьяДвиженияДенежныхСредств, МакетДетали);
		
		Статус = Строка.Ссылка.Виза_Состояние;
		Если Статус=Перечисления.Визы_Состояния.Утвержден Тогда
			МакетДетали.Области.ДеталиСтроки.ЦветФона = WebЦвета.Роса;
		ИначеЕсли Статус=Перечисления.Визы_Состояния.НаВизировании Тогда
			МакетДетали.Области.ДеталиСтроки.ЦветФона = WebЦвета.Лимонный;
		КонецЕсли; 
		
		//++ Бахтишаев А.Э. , 19/07/16 03:01, Задача 119
		Если Строка.НаУтверждении Тогда
			МакетДетали.Области.СуммаБез.ЦветФона	= WebЦвета.Красный;
			МакетДетали.Области.СуммаНал.ЦветФона	= WebЦвета.Красный;
			МакетДетали.Области.СуммаДокумента.ЦветФона	= WebЦвета.Красный;
		КонецЕсли;
		//-- Бахтишаев А.Э. , 19/07/16 03:01
		
		ИтогСуммаБез = ИтогСуммаБез + Строка.СуммаБез;
		ИтогСуммаНал = ИтогСуммаНал + Строка.СуммаНал;
		ИтогСуммаВсего = ИтогСуммаВсего + Строка.Сумма;
		
		ТабДок.Вывести(МакетДетали);
	КонецЦикла;
	
	МакетДетали = Макет.ПолучитьОбласть("Итого");
	МакетДетали.Параметры.СуммаВсего = ИтогСуммаВсего;
	МакетДетали.Параметры.СуммаБез = ИтогСуммаБез;
	МакетДетали.Параметры.СуммаНал = ИтогСуммаНал;
	
	ТабДок.Вывести(МакетДетали);
	
	//ТабДок.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
	ТабДок.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLS);
	//ТабДок.Записать(ИмяФайла);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xls");
	СохранитьДанные(ИмяВременногоФайла, Объект.ТаблицаРезультат);
	ПараметрыСообщения = Новый Структура;
	СписокПолучателей = Новый Массив;
	
	Для Каждого Строка Из СписокПользователей Цикл
		АдрессПочты = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Строка.Значение, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		СписокПолучателей.Добавить(Новый Структура("Адрес, Представление", АдрессПочты, СокрЛП(Строка.Значение)));
	КонецЦикла;
	
	ИмяФайла = "Счета_"+Формат(ДатаОтчета, "ДФ=yyyyMMdd")+"_";
	Для Каждого Строка Из Подразделения Цикл
		ИмяПодразделения = ТРег(Строка.Значение.НаименованиеЭлемента);
		ИмяПодразделения = СтрЗаменить(ИмяПодразделения, " ", "");
		ИмяФайла = ИмяФайла + ИмяПодразделения;
	КонецЦикла;
	ИмяФайла = ИмяФайла + ".xls";
	
	Вложения = Новый Соответствие;
	Вложения.Вставить(ИмяФайла, Новый ДвоичныеДанные(ИмяВременногоФайла));
	
	ПараметрыСообщения.Вставить("Кому", СписокПолучателей);
	ПараметрыСообщения.Вставить("Тема", Тема);
	ПараметрыСообщения.Вставить("Тело", ТелоПисьма);
	ПараметрыСообщения.Вставить("Вложения", Вложения);
	
	РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(
		Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты,
		ПараметрыСообщения);
		
	ЗарегистироватьСчета();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИтогПоПолю(ИмяПоля)
	
	Результат = 0;
	Для Каждого Строка Из Объект.ТаблицаРезультат Цикл
		Если Не Строка.Использовать Тогда
			Продолжить;
		КонецЕсли;
		Результат = Результат + Строка[ИмяПоля];
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеИтогов()
	ИтогСумма = ПолучитьИтогПоПолю("Сумма");
	ИтогСуммаБез = ПолучитьИтогПоПолю("СуммаБез");
	ИтогСуммаНач = ПолучитьИтогПоПолю("СуммаНал");
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеНаКлиенте()
	ОбновитьДанные();
	ОбновитьДанныеИтогов();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеСчетов(Команда)
	ОбновитьДанныеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтчетаПриИзменении(Элемент)
	ОбновитьДанныеНаКлиенте();
	ИзменитьТему();
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если Подразделения.Количество()=0 Тогда
		Подразделения.Добавить(ПараметрыСеанса.ТекущийПользователь.Подразделение);
	КонецЕсли;
	Если Состояние.Количество()=0 Тогда
		Состояние.Добавить(Перечисления.Визы_Состояния.НаВизировании);
	КонецЕсли;
	ОбновитьДанные();
	ЗаполнитьПолучателей();	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	ОбновитьДанныеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТему()
	Тема = "Счета для визирования " + Формат(ДатаОтчета, "ДФ=dd.MM.yyyy") + " " + Подразделения;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИзменитьТему();
	ОбновитьДанныеИтогов();
КонецПроцедуры

&НаКлиенте
Процедура РеквизитОтбораПриИзменении(Элемент)
	ОбновитьДанныеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПолучателей1(Команда)
	ЗаполнитьПолучателей();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатИспользоватьПриИзменении(Элемент)
	ОбновитьДанныеИтогов();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсе(Команда)
	Для Каждого Строка Из Объект.ТаблицаРезультат Цикл
		Строка.Использовать = Истина;
	КонецЦикла;
	ОбновитьДанныеИтогов();
КонецПроцедуры

&НаКлиенте
Процедура УбратьВсе(Команда)
	Для Каждого Строка Из Объект.ТаблицаРезультат Цикл
		Строка.Использовать = Ложь;
	КонецЦикла; 
	ОбновитьДанныеИтогов();
КонецПроцедуры

&НаКлиенте
Процедура НеУтвержденныеРанееПриИзменении(Элемент)
	 ОбновитьДанныеНаКлиенте();
КонецПроцедуры
