&НаСервере
Функция НайтиСправочник(Знач Значение, ИмяСправочника, ДополнительныеПараметры, ДанныеСтроки) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Справочники[ИмяСправочника].ПустаяСсылка();
	КонецЕсли; 
	
	МетаданныеСправочника = Метаданные.Справочники[ИмяСправочника];
	Значение = Лев(СокрЛП(Значение), МетаданныеСправочника.ДлинаНаименования);	
	
	ИмяКэша = "Ссылки" + ИмяСправочника;
	Если ДополнительныеПараметры.Свойство(ИмяКэша) Тогда
		Результат = ДополнительныеПараметры[ИмяКэша][Значение];
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить(ИмяКэша, Новый Соответствие);
	КонецЕсли;
		
	МассивПоиска = Неопределено;
	Если ДополнительныеПараметры.Свойство("Поиск"+ИмяСправочника, МассивПоиска) Тогда
		// Ищем по произвольным реквизитам
		Для Каждого РеквизитПоиска Из МассивПоиска Цикл
			Если РеквизитПоиска.Ключ="Код" Тогда
				Результат = Справочники[ИмяСправочника].НайтиПоКоду(Значение, Ложь, Значение, РеквизитПоиска.Значение);
			Иначе
				Результат = Справочники[ИмяСправочника].НайтиПоРеквизиту(РеквизитПоиска.Ключ, Значение, РеквизитПоиска.Значение);
			КонецЕсли; 
			Если ЗначениеЗаполнено(Результат) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат) Тогда
		// Ищем по наименованию
		Результат = Справочники[ИмяСправочника].НайтиПоНаименованию(Значение, Истина);
		Если Не ЗначениеЗаполнено(Результат) Тогда
			// Ищем по коду
			Результат = Справочники[ИмяСправочника].НайтиПоКоду(Значение);
		КонецЕсли;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Если ДополнительныеПараметры.Свойство("Создавать"+ИмяСправочника) Тогда
			ОбъектСправочника = Справочники[ИмяСправочника].СоздатьЭлемент();
			ОбъектСправочника.Наименование = Значение;
			ДополнительныеПараметрыНового = Неопределено;
			Если ДополнительныеПараметры.Свойство("Новые"+ИмяСправочника, ДополнительныеПараметрыНового) Тогда
				Для Каждого Строка Из ДополнительныеПараметрыНового Цикл
					ОбъектСправочника[Строка.Ключ] = ДанныеСтроки[Строка.Значение];
				КонецЦикла; 
			КонецЕсли;
			Если ДополнительныеПараметры.Свойство("Реквизиты"+ИмяСправочника, ДополнительныеПараметрыНового) Тогда
				ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДополнительныеПараметрыНового);
			КонецЕсли;		
			ОбъектСправочника.ОбменДанными.Загрузка = Истина;
			ОбъектСправочника.Записать();
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = ОбъектСправочника.Ссылка;
			СообщениеПользователю.Текст = "Создан " + ОбъектСправочника.Ссылка;
			СообщениеПользователю.Сообщить();
			Результат = ОбъектСправочника.Ссылка;
		Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = "Не найден " + ИмяСправочника + " по значению '" + Значение + "'";
			СообщениеПользователю.Сообщить();			
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ДополнительныеПараметры[ИмяКэша].Вставить(Значение, Результат);
		Возврат Результат;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПолучитьДату(Значение)
	
	ЧислоМесяц = 1;
	ЧислоДень = 1;
	ЧислоГод = 1;
	ПоложениеТочки = Найти(Значение, ".");
	Если ПоложениеТочки>0 Тогда
		ЧислоМесяц = Число(Сред(Значение, 1, ПоложениеТочки-1));
		Значение = СокрЛП(Сред(Значение, ПоложениеТочки+1));
		ПоложениеТочки = Найти(Значение, ".");
		Если ПоложениеТочки>0 Тогда
			ЧислоДень = Число(Сред(Значение, 1, ПоложениеТочки-1));
			Значение = СокрЛП(Сред(Значение, ПоложениеТочки+1));
			ЧислоГод = Число(Значение);
		КонецЕсли;
	КонецЕсли; 
	Возврат Дата(ЧислоГод, ЧислоМесяц, ЧислоДень);
	
КонецФункции

&НаСервере
Функция ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, ИндексКолонки)
	
	Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
		Поле = ДополнительныеПараметры.ADODBRecordset.Fields.Item(ИндексКолонки - 1);
		Если Поле.ActualSize = 0 Тогда        // Пустое поле EXCEL.
        	Возврат "";
		КонецЕсли;
		Возврат СокрЛП(Поле.Value);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция НайтиБлокЗатрат(ДополнительныеПараметры)
	
	Для ИндексСтроки=1 По 200 Цикл
		Если ДополнительныеПараметры.ADODBRecordset.EOF()<>0 Тогда
			Прервать;
		КонецЕсли;
		Если ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, 1)="Витрати"
			ИЛИ ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, 2)="Витрати" Тогда
			ДополнительныеПараметры.ADODBRecordset.MoveNext();
			ДополнительныеПараметры.ADODBRecordset.MoveNext();
			Возврат Истина;
		КонецЕсли;
		ДополнительныеПараметры.ADODBRecordset.MoveNext();
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьИзТабличногоДокументаВТаблицу(ТаблицаРезультат, МассивКолонок, ИмяКлючевогоРеквизита=Неопределено, НачальнаяСтрока, КонечнаяСтрока, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры=Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	ТипСправочников = Справочники.ТипВсеСсылки();
	
	Для ИндексСтроки=1 По КонечнаяСтрока Цикл
		
 		Если ИндексСтроки < НачальнаяСтрока Тогда
			Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
				ДополнительныеПараметры.ADODBRecordset.MoveNext();
			КонецЕсли;		
            Продолжить;
        КонецЕсли;		
		
		Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
			Если ДополнительныеПараметры.ADODBRecordset.EOF()<>0 Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;		
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		Если ДополнительныеПараметры.Свойство("ЗначенияНовойСтроки") Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДополнительныеПараметры.ЗначенияНовойСтроки);
		КонецЕсли; 
		
		Для Каждого Строка Из МассивКолонок Цикл
			
			ЗначениеКолонки = ПолучитьЗначениеЯчейки(ДополнительныеПараметры, ИндексСтроки, Строка.Значение);
			ИмяРеквизита = Строка.Ключ;
			Если ИмяРеквизита=ИмяКлючевогоРеквизита
				И (Не ЗначениеЗаполнено(ЗначениеКолонки) ИЛИ ЗначениеКолонки="Всього Витрати") Тогда
				ТаблицаРезультат.Удалить(НоваяСтрока);
				Возврат;
			КонецЕсли; 
			
			ТипРеквизита = ТипЗнч(НоваяСтрока[Строка.Ключ]);
			Если Не ЗначениеЗаполнено(ЗначениеКолонки) Тогда
				НоваяСтрока[ИмяРеквизита] = Неопределено;
			ИначеЕсли ТипСправочников.СодержитТип(ТипРеквизита) Тогда
				Если ЛЕВ(ЗначениеКолонки, 2)="0," ИЛИ ЛЕВ(ЗначениеКолонки, 2)="0." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 3);
				ИначеЕсли ЛЕВ(ЗначениеКолонки, 1)="." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 2);
				КонецЕсли; 
				МетаданныеСправочника = НоваяСтрока[ИмяРеквизита].Метаданные();
				НоваяСтрока[ИмяРеквизита] = НайтиСправочник(ЗначениеКолонки, МетаданныеСправочника.Имя, ДополнительныеПараметры, НоваяСтрока);
			ИначеЕсли ТипРеквизита=Тип("Число") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, ",", ".");
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, " ", "");
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, Символ(160), "");
				НоваяСтрока[ИмяРеквизита] = Число(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Дата") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, "/", ".");
				НоваяСтрока[ИмяРеквизита] = ПолучитьДату(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Строка") Тогда
				Если ЛЕВ(ЗначениеКолонки, 2)="0," ИЛИ ЛЕВ(ЗначениеКолонки, 2)="0." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 3);
				ИначеЕсли ЛЕВ(ЗначениеКолонки, 1)="." Тогда
					ЗначениеКолонки = Сред(ЗначениеКолонки, 2);
				КонецЕсли;
				Если Строка.Ключ="МФОБанка" Тогда
					ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, " ", "");
				КонецЕсли; 
				НоваяСтрока[ИмяРеквизита] = ЗначениеКолонки;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДополнительныеПараметры.Свойство("ADODBRecordset") Тогда
			ДополнительныеПараметры.ADODBRecordset.MoveNext();
		КонецЕсли;		
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьОрганизацию(Список, Примечание) Экспорт
	Для Каждого Строка Из Список Цикл
		Если Найти(Примечание, Строка.Ключ)<>0 Тогда
			Возврат Строка.Значение;
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

&НаСервере
Функция ПолучитьОрганизацию2(Список, Примечание, Примечание2)
	Результат = ПолучитьОрганизацию(Список, Примечание);
	Если Результат=Неопределено Тогда
		Результат = ПолучитьОрганизацию(Список, Примечание2);
	КонецЕсли; 
	Если Результат=Неопределено Тогда
		Результат = Организация;
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ЗагрузитьТаблицуСчетовADO(МассивЛистов) Экспорт
	
	ТаблицаРезультат.Очистить();	
	
	СоответствиеКолонок = Новый Структура;	
	// Измерения
	СоответствиеКолонок.Вставить("Подразделение", 3);
	// Простые типы
	СоответствиеКолонок.Вставить("СуммаНал", 13);
	СоответствиеКолонок.Вставить("СуммаБез", 12);
	СоответствиеКолонок.Вставить("Задача", 5);
	СоответствиеКолонок.Вставить("Комментарий", 17);
	СоответствиеКолонок.Вставить("КодЕДРПОУ", 8);
	СоответствиеКолонок.Вставить("НаименованиеКонтрагента", 7);
	СоответствиеКолонок.Вставить("НаименованиеБанка", 10);
	СоответствиеКолонок.Вставить("МФОБанка", 11);
	СоответствиеКолонок.Вставить("НазначениеПлатежа", 16);
	СоответствиеКолонок.Вставить("РасчетныйСчет", 9);
	СоответствиеКолонок.Вставить("КомментарийДатаОплаты", 19);
	// Ссылочные типы
	СоответствиеКолонок.Вставить("Банк", 11);
	СоответствиеКолонок.Вставить("СтатьяДвиженияДенежныхСредств", 4);
	СоответствиеКолонок.Вставить("СтатьяЗатрат", 6);
	СоответствиеКолонок.Вставить("Контрагент", 8);
	СоответствиеКолонок.Вставить("СчетКонтрагента", 9);	
	
	ЗначенияНовойСтроки = Новый Структура;
	ЗначенияНовойСтроки.Вставить("Дата");
	
	ПоискКонтрагенты = Новый Структура;
	ПоискКонтрагенты.Вставить("ЕГРПОУ");
	
	ПоискБанки = Новый Структура;
	ПоискБанки.Вставить("Код");	
	
	ПоискСчета = Новый Структура;
	ПоискСчета.Вставить("НомерСчета");	
	
	ПоискПодразделения = Новый Структура;
	ПоискПодразделения.Вставить("НаименованиеЭлемента", Подразделение);		
	
	НовыеКонтрагенты = Новый Структура;
	НовыеКонтрагенты.Вставить("Наименование", "НаименованиеКонтрагента");
	НовыеКонтрагенты.Вставить("ЕГРПОУ", "КодЕДРПОУ");
	
	НовыеБанки = Новый Структура;
	НовыеБанки.Вставить("Наименование", "НаименованиеБанка");
	НовыеБанки.Вставить("Код", "МФОБанка");	
	
	РеквизитыКонтрагенты = Новый Структура;
	РеквизитыКонтрагенты.Вставить("Ответственный", ПараметрыСеанса.ТекущийПользователь);
	РеквизитыКонтрагенты.Вставить("Подразделение", Подразделение);
	РеквизитыКонтрагенты.Вставить("Комментарий", "Создан при загрузке " + ТекущаяДата());
	
	НовыеБанковскиеСчета = Новый Структура;
	НовыеБанковскиеСчета.Вставить("Владелец", "Контрагент");
	НовыеБанковскиеСчета.Вставить("Банк", "Банк");
	НовыеБанковскиеСчета.Вставить("НомерСчета", "РасчетныйСчет");
	
	СсылкиПодразделения = РегистрыСведений.НастройкиЗагрузкиСчетов.ПолучитьСоответствие("Подразделения", Подразделение);
	СсылкиОрганизаций = РегистрыСведений.НастройкиЗагрузкиСчетов.ПолучитьСоответствие("Организации", Подразделение);
	Организация = СсылкиОрганизаций.Получить("Основная организация по-умолчанию");
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СоздаватьБанки", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьКонтрагенты", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьСтатьиДвиженияДенежныхСредств", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьБанковскиеСчета", Истина);
	ДополнительныеПараметры.Вставить("РеквизитыКонтрагенты", РеквизитыКонтрагенты);
	ДополнительныеПараметры.Вставить("ПоискКонтрагенты", ПоискКонтрагенты);
	ДополнительныеПараметры.Вставить("ПоискБанки", ПоискБанки);
	ДополнительныеПараметры.Вставить("ПоискБанковскиеСчета", ПоискСчета);
	ДополнительныеПараметры.Вставить("ПоискПодразделения", ПоискПодразделения);
	ДополнительныеПараметры.Вставить("СсылкиПодразделения", СсылкиПодразделения);
	ДополнительныеПараметры.Вставить("НовыеБанковскиеСчета", НовыеБанковскиеСчета);
	ДополнительныеПараметры.Вставить("НовыеКонтрагенты", НовыеКонтрагенты);
	ДополнительныеПараметры.Вставить("НовыеБанки", НовыеБанки);
	ДополнительныеПараметры.Вставить("ЗначенияНовойСтроки", ЗначенияНовойСтроки);
	
	СonnectionString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source= "
		+ СокрЛП(ИмяВременногоФайла)
		+ ";Extended Properties=""Excel 12.0;HDR=YES;IMEX=1;""";
	Попытка
    	Соединение = Новый COMОбъект("ADODB.Connection"); // Инициализация ADODB.
        Соединение.Open(СonnectionString);                                      		
	Исключение
		Информация = ИнформацияОбОшибке();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "На компьютере " + ИмяКомпьютера() + " не удается открыть ADODB.Connection со строкой:" + Символы.ПС
			+ "'" + СonnectionString + "'" + Символы.ПС
			+ Информация.Причина.Описание;
		Сообщение.Сообщить();
	КонецПопытки;	
	
	ДополнительныеПараметры.Вставить("Сообщение", Сообщение);
	
	Для Каждого Лист Из МассивЛистов Цикл
		
		ТекстЗапроса = "SELECT * FROM [" + Лист.ИмяЛиста + "]";
		ADODBRecordset = Новый COMОбъект("ADODB.Recordset");
		Попытка
		    ADODBRecordset.Open(ТекстЗапроса, Соединение);
		Исключение
			Информация = ИнформацияОбОшибке();
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не выполнился запрос:" + Символы.ПС
				+ "'" + ТекстЗапроса + "'" + Символы.ПС
				+ Информация.Причина.Описание;
			Сообщение.Сообщить();
			Соединение.Close();
			Возврат;
		КонецПопытки;
		
		ДополнительныеПараметры.Вставить("ADODBRecordset", ADODBRecordset);
		
		ЗначенияНовойСтроки.Дата = Лист.Дата;
		
		Если НайтиБлокЗатрат(ДополнительныеПараметры) Тогда
			ЗагрузитьИзТабличногоДокументаВТаблицу(ТаблицаРезультат,
				СоответствиеКолонок, "Подразделение",
				1, 10000,
				ДополнительныеПараметры);
		КонецЕсли;
			
		ADODBRecordset.Close();
		ADODBRecordset = Неопределено;
		ДополнительныеПараметры.Вставить("ADODBRecordset", Неопределено);
		
	КонецЦикла; 
		
	Соединение.Close();
	Соединение = Неопределено;
	
	МассивДляУдаления = Новый Массив;
	Для Каждого Строка Из ТаблицаРезультат Цикл
		Строка.Организация = ПолучитьОрганизацию2(СсылкиОрганизаций, Строка.Комментарий, Строка.КомментарийДатаОплаты);
		Если Не ЗначениеЗаполнено(Строка.НазначениеПлатежа) Тогда
			Строка.НазначениеПлатежа = Строка.Комментарий;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Строка.СуммаНал) И Не ЗначениеЗаполнено(Строка.СуммаБез) Тогда
			МассивДляУдаления.Добавить(Строка);
		КонецЕсли; 
	КонецЦикла;
	
	Для Каждого Строка Из МассивДляУдаления Цикл
		ТаблицаРезультат.Удалить(Строка);
	КонецЦикла; 
	
	Элементы.Главная.ТекущаяСтраница = Элементы.РезультатЗагрузкиИзИсточника;
					
КонецПроцедуры

&НаСервере
Функция ВсеЧисла(Знач Значение)
	
	ДлиннаСтроки = СтрДлина(Значение);
	Для Индекс=1 По ДлиннаСтроки  Цикл
		Код = КодСимвола(Значение, Индекс);
		Если Код>=КодСимвола("0",1) И Код<=КодСимвола("9",1) Тогда
			Продолжить;
		КонецЕсли; 
		Возврат Ложь;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьСписокЛистов()
	
	СonnectionString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source= "
		+ СокрЛП(ИмяВременногоФайла)
		+ ";Extended Properties=""Excel 12.0;HDR=YES;IMEX=1;""";
	Попытка
    	Соединение = Новый COMОбъект("ADODB.Connection"); // Инициализация ADODB.
        Соединение.Open(СonnectionString);                                      		
	Исключение
		Информация = ИнформацияОбОшибке();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "На компьютере " + ИмяКомпьютера() + " не удается открыть ADODB.Connection со строкой:" + Символы.ПС
			+ "'" + СonnectionString + "'" + Символы.ПС
			+ Информация.Причина.Описание;
		Сообщение.Сообщить();
	КонецПопытки;
	
	Каталог = Новый COMОбъект("ADOX.Catalog");
	Каталог.ActiveConnection = Соединение;
    МассивЛистов = Каталог.Tables;
	
	ТаблицаЛистов.Очистить();
            
    Для Каждого ЛистМассива ИЗ МассивЛистов Цикл
		ИмяЛистаВременно = ЛистМассива.Name;
		Если СтрЗаканчиваетсяНа(ИмяЛистаВременно, "Database") Тогда
        	Продолжить;
		КонецЕсли;
		ДатаЗагрузки = Дата(1,1,1);
		Если СтрДлина(ИмяЛистаВременно)=8 И КодСимвола(ИмяЛистаВременно, 4)=КодСимвола("#", 1) Тогда
			ПредставлениеЛиста = Сред(ИмяЛистаВременно, 2, СтрДлина(ИмяЛистаВременно)-3);
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "#", "");
			Если ВсеЧисла(ПредставлениеЛиста) Тогда
				ДатаЗагрузки = Дата(Год(ТекущаяДата()), Число(Сред(ПредставлениеЛиста, 3, 2)), Число(Сред(ПредставлениеЛиста, 1, 2)));
			КонецЕсли; 
		ИначеЕсли СтрДлина(ИмяЛистаВременно)=9 Тогда
			ПредставлениеЛиста = СтрЗаменить(ИмяЛистаВременно, "$", "");
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "'", "");
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "#", "");
			Если ВсеЧисла(ПредставлениеЛиста) Тогда
				ПредставлениеЛиста = Сред(ПредставлениеЛиста, 1, 2) + "_" + Сред(ПредставлениеЛиста, 3, 2);
				ДатаЗагрузки = Дата(Год(ТекущаяДата()), Число(Сред(ПредставлениеЛиста, 3, 2)), Число(Сред(ПредставлениеЛиста, 1, 2)));
			КонецЕсли; 
		ИначеЕсли СтрДлина(ИмяЛистаВременно)=13 Тогда
			ПредставлениеЛиста = СтрЗаменить(ИмяЛистаВременно, "$", "");
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "'", "");
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "#", "");
			Если ВсеЧисла(ПредставлениеЛиста) Тогда
				ДатаЗагрузки = Дата(Число(Сред(ПредставлениеЛиста, 5)), Число(Сред(ПредставлениеЛиста, 3, 2)), Число(Сред(ПредставлениеЛиста, 1, 2)));
			КонецЕсли; 
		КонецЕсли;
		НоваяСтрока = ТаблицаЛистов.Добавить();
		НоваяСтрока.ИмяЛиста = ИмяЛистаВременно;
		НоваяСтрока.ДатаЗагрузки = ДатаЗагрузки;
	КонецЦикла;
	
	Элементы.ИмяЛиста.СписокВыбора.Очистить();
	Для Каждого Строка Из ТаблицаЛистов Цикл
		Если ЗначениеЗаполнено(Строка.ДатаЗагрузки) Тогда
			ПредставлениеЛиста = Формат(Строка.ДатаЗагрузки, "ДЛФ=DD");
		Иначе
			ПредставлениеЛиста = Сред(Строка.ИмяЛиста, 2, СтрДлина(Строка.ИмяЛиста)-2);
			ПредставлениеЛиста = СтрЗаменить(ПредставлениеЛиста, "$", "");
		КонецЕсли; 
		Элементы.ИмяЛиста.СписокВыбора.Добавить(Строка.ИмяЛиста, ПредставлениеЛиста);
		ИмяЛистаЗагрузки = Строка.ИмяЛиста;
	КонецЦикла;
	
	Соединение.Close();
    Соединение = Неопределено;	
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСписокЛистовНаСервере(Адресс, Расширение) Экспорт
	
	РегистрыСведений.НастройкиЗагрузкиСчетов.СохранитьЗначениеПоКлючу("Организации", Подразделение, "Основная организация по-умолчанию", Организация);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	Данные = ПолучитьИзВременногоХранилища(Адресс);
	Данные.Записать(ИмяВременногоФайла);
	ЗагрузитьСписокЛистов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокументПродолжение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат=Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ВыбранноеИмяФайла;
	Файл = Новый Файл(ВыбранноеИмяФайла);
	ЗагрузитьСписокЛистовНаСервере(Адрес, Файл.Расширение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокумент(Команда)
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьДокументПродолжение", ЭтотОбъект)
		,,,Истина);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ВыбратьЛист(Команда)
	
	СтруктураЛиста = Новый Структура;
	СтруктураЛиста.Вставить("ИмяЛиста", ИмяЛистаЗагрузки);
	СтруктураЛиста.Вставить("Дата", ДатаЗагрузки);
		
	МассивЛистов = Новый Массив;
	МассивЛистов.Добавить(СтруктураЛиста);
	ЗагрузитьТаблицуСчетовADO(МассивЛистов);
	ТаблицаРезультатОбновитьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, ИмяРеквзизита, ЗначениеРеквизита, ЕслиЗаполненоНеУстанавливать = Ложь)
	
	Если МетаданныеДокумента.Реквизиты.Найти(ИмяРеквзизита)=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕслиЗаполненоНеУстанавливать И ЗначениеЗаполнено(ОбъектДокумента[ИмяРеквзизита]) Тогда
		Возврат;
	КонецЕсли; 
	
	ОбъектДокумента[ИмяРеквзизита] = ЗначениеРеквизита;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСчетаНаСервере(ТипДокумента)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Док.Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК Док
	|ГДЕ
	|	Док.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И Док.Контрагент = &Контрагент
	|	И Док.СуммаДокумента = &СуммаДокумента";
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Для Каждого Строка Из ТаблицаРезультат Цикл
		
		ОбъектДокумента = Неопределено;
		СуммаДокумента = Строка.СуммаБез;
		ВидОплаты = Перечисления.ВидыДенежныхСредств.Безналичные;
		Контрагент = Строка.Контрагент;
		Если Не ЗначениеЗаполнено(СуммаДокумента) Тогда
			СуммаДокумента = Строка.СуммаНал;
			ВидОплаты = Перечисления.ВидыДенежныхСредств.Наличные;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			ТипДокументаРеальный = Строка(ТипДокумента);
			Если ТипДокумента="ПлатежноеПоручениеИсходящее" И ВидОплаты=Перечисления.ВидыДенежныхСредств.Наличные Тогда
				ТипДокументаРеальный = "РасходныйКассовыйОрдер";
			КонецЕсли; 
			ТекстЗапросаИзмененный = СтрЗаменить(ТекстЗапроса, "СчетНаОплатуПоставщика", ТипДокументаРеальный);
			ЗапросПоиска = Новый Запрос(ТекстЗапросаИзмененный);
			ЗапросПоиска.УстановитьПараметр("ДатаНачала", НачалоДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("ДатаКонца", КонецДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("Контрагент", Контрагент);
			ЗапросПоиска.УстановитьПараметр("СуммаДокумента", СуммаДокумента);
			Выборка = ЗапросПоиска.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			Иначе
				ОбъектДокумента = Документы[ТипДокументаРеальный].СоздатьДокумент();
			КонецЕсли; 
		Иначе
			ОбъектДокумента = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		Если ОбъектДокумента.ПометкаУдаления Тогда
			ОбъектДокумента.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;		
		
		ЗаполнитьЗначенияСвойств(ОбъектДокумента, Строка);
		ОбъектДокумента.СуммаДокумента = СуммаДокумента;
		ОбъектДокумента.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		ОбъектДокумента.ВалютаДокумента = ВалютаРегламентированногоУчета;
		
		МетаданныеДокумента = ОбъектДокумента.Метаданные();
		
		УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, "ВидОплаты", ВидОплаты);
		УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, "Автор", ПараметрыСеанса.ТекущийПользователь);
		УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, "Виза_Состояние", Перечисления.Визы_Состояния.Черновик);
		УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, "ПлановаяДатаОплаты", ОбъектДокумента.Дата);
		УстановитьРеквизитДокумента(МетаданныеДокумента, ОбъектДокумента, "СтатьяДвиженияДенежныхСредств", СтатьяДвиженияДенежныхСредств, Истина);
		
		Если МетаданныеДокумента.Реквизиты.Найти("СчетОрганизации")<>Неопределено Тогда
			ОбъектДокумента.СчетОрганизации = ОбъектДокумента.Организация.ОсновнойБанковскийСчет;
		КонецЕсли;
		
		ОбъектДокумента.Комментарий = "[" + ТекущаяДата() + "] " + Строка.Комментарий;
		
		ЭтоБылНовый = ОбъектДокумента.ЭтоНовый();		
				
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
		Если ЭтоБылНовый Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = ОбъектДокумента.Ссылка;
			СообщениеПользователю.Текст = "Создан " + ОбъектДокумента.Ссылка;
			СообщениеПользователю.Сообщить();
		КонецЕсли; 
		Строка.Ссылка = ОбъектДокумента.Ссылка;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСчета(Команда)
	СоздатьСчетаНаСервере("СчетНаОплатуПоставщика");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Не указана");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ПодразделениеПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()
	Организация = РегистрыСведений.НастройкиЗагрузкиСчетов.ПолучитьЗначениеПоКлючу("Организации", Подразделение, "Основная организация по-умолчанию");
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ПодразделениеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПлатежки(Команда)
	СоздатьСчетаНаСервере("ПлатежноеПоручениеИсходящее");
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьДокументПродолжение", ЭтотОбъект)
		,,,Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатОбновитьИтоги()
	ИтогСуммаБез = ТаблицаРезультат.Итог("СуммаБез");
	ИтогСуммаНал = ТаблицаРезультат.Итог("СуммаНал");
КонецПроцедуры

&НаКлиенте
Процедура ИмяЛистаПриИзменении(Элемент)
	
	НайденыеСтроки = ТаблицаЛистов.НайтиСтроки(Новый Структура("ИмяЛиста", ИмяЛистаЗагрузки));
	Для Каждого Строка Из НайденыеСтроки Цикл
		ДатаЗагрузки = Строка.ДатаЗагрузки;
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатПриИзменении(Элемент)
	ТаблицаРезультатОбновитьИтоги();
КонецПроцедуры
