&НаСервере
Функция НайтиСправочник(Знач Значение, ИмяСправочника, ДополнительныеПараметры, ДанныеСтроки) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Справочники[ИмяСправочника].ПустаяСсылка();
	КонецЕсли; 
	
	МетаданныеСправочника = Метаданные.Справочники[ИмяСправочника];
	Значение = Лев(СокрЛП(Значение), МетаданныеСправочника.ДлинаНаименования);	
	
	ИмяКэша = "Ссылки" + ИмяСправочника;
	Если ДополнительныеПараметры.Свойство(ИмяКэша) Тогда
		Результат = ДополнительныеПараметры[ИмяКэша][Значение];
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить(ИмяКэша, Новый Соответствие);
	КонецЕсли;
		
	// Ищем по наименованию
	Результат = Справочники[ИмяСправочника].НайтиПоНаименованию(Значение, Истина);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		// Ищем по коду
		Результат = Справочники[ИмяСправочника].НайтиПоКоду(Значение);
		МассивПоиска = Неопределено;
		Если Не ЗначениеЗаполнено(Результат)
			И ДополнительныеПараметры.Свойство("Поиск"+ИмяСправочника, МассивПоиска) Тогда
			// Ищем по произвольным реквизитам
			Для Каждого РеквизитПоиска Из МассивПоиска Цикл
				Результат = Справочники[ИмяСправочника].НайтиПоРеквизиту(РеквизитПоиска.Ключ, Значение);
				Если ЗначениеЗаполнено(Результат) Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат)
		И ДополнительныеПараметры.Свойство("Создавать"+ИмяСправочника) Тогда
		ОбъектСправочника = Справочники[ИмяСправочника].СоздатьЭлемент();
		ОбъектСправочника.Наименование = Значение;
		ДополнительныеПараметрыНового = Неопределено;
		Если ДополнительныеПараметры.Свойство("Новые"+ИмяСправочника, ДополнительныеПараметрыНового) Тогда
			Для Каждого Строка Из ДополнительныеПараметрыНового Цикл
				ОбъектСправочника[Строка.Ключ] = ДанныеСтроки[Строка.Значение];
			КонецЦикла; 
		КонецЕсли;
		ОбъектСправочника.Записать();
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.КлючДанных = ОбъектСправочника.Ссылка;
		СообщениеПользователю.Текст = "Создан " + ОбъектСправочника.Ссылка;
		СообщениеПользователю.Сообщить();
		Результат = ОбъектСправочника.Ссылка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ДополнительныеПараметры[ИмяКэша].Вставить(Значение, Результат);
		Возврат Результат;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПолучитьСчетКонтрагента(Значение) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	БанковскиеСчета.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Владелец
	|	И НЕ БанковскиеСчета.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ"
	);
	Запрос.УстановитьПараметр("Владелец", Значение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли; 
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПолучитьДату(Значение)
	ЧислоМесяц = 1;
	ЧислоДень = 1;
	ЧислоГод = 1;
	ПоложениеТочки = Найти(Значение, ".");
	Если ПоложениеТочки>0 Тогда
		ЧислоМесяц = Число(Сред(Значение, 1, ПоложениеТочки-1));
		Значение = СокрЛП(Сред(Значение, ПоложениеТочки+1));
		ПоложениеТочки = Найти(Значение, ".");
		Если ПоложениеТочки>0 Тогда
			ЧислоДень = Число(Сред(Значение, 1, ПоложениеТочки-1));
			Значение = СокрЛП(Сред(Значение, ПоложениеТочки+1));
			ЧислоГод = Число(Значение);
		КонецЕсли;
	КонецЕсли; 
	Возврат Дата(ЧислоГод, ЧислоМесяц, ЧислоДень);
КонецФункции

&НаСервере
Процедура ЗагрузитьИзТабличногоДокументаВТаблицу(ТаблицаРезультат, ТабДокумент, МассивКолонок, ИмяКлючевогоРеквизита=Неопределено, НачальнаяСтрока, КонечнаяСтрока, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры=Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	ТипСправочников = Справочники.ТипВсеСсылки();
	
	Для ИндексСтроки=НачальнаяСтрока По КонечнаяСтрока Цикл
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		
		Для Каждого Строка Из МассивКолонок Цикл
			
			ЗначениеКолонки = СокрЛП(ТабДокумент.Область(ИндексСтроки, Строка.Значение).Текст);
			ИмяРеквизита = Строка.Ключ;
			Если Не ЗначениеЗаполнено(ЗначениеКолонки)
				И ИмяРеквизита=ИмяКлючевогоРеквизита Тогда
				ТаблицаРезультат.Удалить(НоваяСтрока);
				Возврат;
			КонецЕсли; 
			
			ТипРеквизита = ТипЗнч(НоваяСтрока[Строка.Ключ]);
			Если Не ЗначениеЗаполнено(ЗначениеКолонки) Тогда
				НоваяСтрока[ИмяРеквизита] = Неопределено;
				Если ИмяРеквизита="Контрагент" Тогда
					Если ЗначениеЗаполнено(НоваяСтрока.СуммаБез) Тогда
						НоваяСтрока.Контрагент = КонтрагентБН;
					Иначе
						НоваяСтрока.Контрагент = КонтрагентФ2;
					КонецЕсли; 
				КонецЕсли; 
			ИначеЕсли ТипСправочников.СодержитТип(ТипРеквизита) Тогда
				МетаданныеСправочника = НоваяСтрока[ИмяРеквизита].Метаданные();
				НоваяСтрока[ИмяРеквизита] = НайтиСправочник(ЗначениеКолонки, МетаданныеСправочника.Имя, ДополнительныеПараметры, НоваяСтрока);
			ИначеЕсли ТипРеквизита=Тип("Число") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, ",", ".");
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, " ", "");
				НоваяСтрока[ИмяРеквизита] = Число(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Дата") Тогда
				ЗначениеКолонки = СтрЗаменить(ЗначениеКолонки, "/", ".");
				НоваяСтрока[ИмяРеквизита] = ПолучитьДату(ЗначениеКолонки);
			ИначеЕсли ТипРеквизита=Тип("Строка") Тогда
				НоваяСтрока[ИмяРеквизита] = ЗначениеКолонки;
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайла(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДокументНаСервере(Адресс, Расширение) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	Данные = ПолучитьИзВременногоХранилища(Адресс);
	Данные.Записать(ИмяВременногоФайла);
	ТабДокумент.Прочитать(ИмяВременногоФайла);
	КонечнаяСтрока = 3;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокументПродолжение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат=Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранноеИмяФайла);
	ЗагрузитьДокументНаСервере(Адрес, Файл.Расширение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокумент(Команда)
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьДокументПродолжение", ЭтотОбъект)
		,,,Истина);
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзТабличногоДокументаВТаблицуРезультата()
	
	ТаблицаРезультат.Очистить();	
	
	СоответствиеКолонок = Новый Структура;
	// Измерения
	СоответствиеКолонок.Вставить("Подразделение", 2);
	// Простые типы
	СоответствиеКолонок.Вставить("СуммаНал", 12);
	СоответствиеКолонок.Вставить("СуммаБез", 11);
	СоответствиеКолонок.Вставить("Задача", 4);
	СоответствиеКолонок.Вставить("Комментарий", 15);
	СоответствиеКолонок.Вставить("КодЕДРПОУ", 7);
	СоответствиеКолонок.Вставить("НаименованиеКонтрагента", 6);
	СоответствиеКолонок.Вставить("НазначениеПлатежа", 14);
	СоответствиеКолонок.Вставить("Дата", 17);
	СоответствиеКолонок.Вставить("РасчетныйСчет", 8);
	// Ссылочные типы
	СоответствиеКолонок.Вставить("СтатьяДвиженияДенежныхСредств", 3);
	СоответствиеКолонок.Вставить("СтатьяЗатрат", 5);
	СоответствиеКолонок.Вставить("Контрагент", 7);
	СоответствиеКолонок.Вставить("Банк", 10);
	СоответствиеКолонок.Вставить("СчетКонтрагента", 8);
	
	ПоискКонтрагенты = Новый Структура;
	ПоискКонтрагенты.Вставить("ЕГРПОУ");
	
	ПоискСчета = Новый Структура;
	ПоискСчета.Вставить("НомерСчета");	
	
	НовыеКонтрагенты = Новый Структура;
	НовыеКонтрагенты.Вставить("Наименование", "НаименованиеКонтрагента");
	НовыеКонтрагенты.Вставить("ЕГРПОУ", "КодЕДРПОУ");
	
	НовыеБанковскиеСчета = Новый Структура;
	НовыеБанковскиеСчета.Вставить("Владелец", "Контрагент");
	НовыеБанковскиеСчета.Вставить("Банк", "Банк");
	НовыеБанковскиеСчета.Вставить("НомерСчета", "РасчетныйСчет");
	
	СсылкиПодразделения = Новый Соответствие;
	СсылкиПодразделения.Вставить("земля", Справочники.Подразделения.НайтиПоКоду("00-000005"));
	СсылкиПодразделения.Вставить("Продажі", Справочники.Подразделения.НайтиПоКоду("00-000007"));
	СсылкиПодразделения.Вставить("Будівництво", Справочники.Подразделения.НайтиПоКоду("00-000002"));
	СсылкиПодразделения.Вставить("СЗ", Справочники.Подразделения.НайтиПоКоду("00-000013"));
	СсылкиПодразделения.Вставить("Адмін", Справочники.Подразделения.НайтиПоКоду("00-000012"));
	СсылкиПодразделения.Вставить("Масани", Справочники.Подразделения.НайтиПоКоду("00-000001"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СоздаватьБанки", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьКонтрагенты", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьСтатьиДвиженияДенежныхСредств", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьБанковскиеСчета", Истина);
	ДополнительныеПараметры.Вставить("ПоискКонтрагенты", ПоискКонтрагенты);
	ДополнительныеПараметры.Вставить("ПоискБанковскиеСчета", ПоискСчета);
	ДополнительныеПараметры.Вставить("СсылкиПодразделения", СсылкиПодразделения);
	ДополнительныеПараметры.Вставить("НовыеБанковскиеСчета", НовыеБанковскиеСчета);
	ДополнительныеПараметры.Вставить("НовыеКонтрагенты", НовыеКонтрагенты);
	
	ТаблицаРезультат.Очистить();
	
	ЗагрузитьИзТабличногоДокументаВТаблицу(ТаблицаРезультат, ТабДокумент,
		СоответствиеКолонок, "Подразделение",
		НачальнаяСтрока, 10000,
		ДополнительныеПараметры);
				
КонецПроцедуры
	
&НаСервере
Процедура НайтиСоздатьДокументыСчетов()
	
	ЗапросПоиска = Новый Запрос(
	"ВЫБРАТЬ
	|	СчетНаОплатуПоставщика.Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
	|ГДЕ
	|	СчетНаОплатуПоставщика.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И СчетНаОплатуПоставщика.Контрагент = &Контрагент
	|	И СчетНаОплатуПоставщика.СуммаДокумента = &СуммаДокумента"
	);
	
	Для Каждого Строка Из ТаблицаРезультат Цикл
		
		ОбъектДокумента = Неопределено;
		СуммаДокумента = Строка.СуммаБез;
		ВидОплаты = Перечисления.ВидыДенежныхСредств.Безналичные;
		Контрагент = Строка.Контрагент;
		Если Не ЗначениеЗаполнено(СуммаДокумента) Тогда
			СуммаДокумента = Строка.СуммаНал;
			ВидОплаты = Перечисления.ВидыДенежныхСредств.Наличные;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			ЗапросПоиска.УстановитьПараметр("ДатаНачала", НачалоДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("ДатаКонца", КонецДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("Контрагент", Контрагент);
			ЗапросПоиска.УстановитьПараметр("СуммаДокумента", СуммаДокумента);
			Выборка = ЗапросПоиска.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			Иначе
				ОбъектДокумента = Документы.СчетНаОплатуПоставщика.СоздатьДокумент();
			КонецЕсли; 
		Иначе
			ОбъектДокумента = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		Если ОбъектДокумента.ПометкаУдаления Тогда
			ОбъектДокумента.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;		
		
		ЗаполнитьЗначенияСвойств(ОбъектДокумента, Строка);
		ОбъектДокумента.ВидОплаты = ВидОплаты;
		ОбъектДокумента.Организация = Организация;
		Если ВидОплаты=Перечисления.ВидыДенежныхСредств.Наличные Тогда
			ОбъектДокумента.Организация = ОрганизацияНал;
		КонецЕсли;
		ОбъектДокумента.СуммаДокумента = СуммаДокумента;
		ОбъектДокумента.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		ОбъектДокумента.Автор = ПараметрыСеанса.ТекущийПользователь;
		ОбъектДокумента.Виза_Состояние = Перечисления.Визы_Состояния.Черновик;
		ОбъектДокумента.ПлановаяДатаОплаты = ОбъектДокумента.Дата;
		
		Если Не ЗначениеЗаполнено(ОбъектДокумента.СтатьяДвиженияДенежныхСредств) Тогда
			ОбъектДокумента.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
		КонецЕсли; 
		
		ЭтоБылНовый = ОбъектДокумента.ЭтоНовый();		
				
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
		Если ЭтоБылНовый Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = ОбъектДокумента.Ссылка;
			СообщениеПользователю.Текст = "Создан " + ОбъектДокумента.Ссылка;
			СообщениеПользователю.Сообщить();
		КонецЕсли; 
		Строка.Ссылка = ОбъектДокумента.Ссылка;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуСТабличногоДокумента(Команда)
	
	ЗагрузитьИзТабличногоДокументаВТаблицуРезультата();
	Элементы.Шапка.ТекущаяСтраница = Элементы.Счета;
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	НайтиСоздатьДокументыСчетов();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьОплатыНаСервере()
	
	ТаблицаОплат.Очистить();	
	
	СоответствиеКолонок = Новый Структура;
	// Измерения
	СоответствиеКолонок.Вставить("Подразделение", 1);
	// Простые типы
	СоответствиеКолонок.Вставить("Дата", 7);
	СоответствиеКолонок.Вставить("СуммаБез", 8);
	СоответствиеКолонок.Вставить("СуммаНал", 9);
	СоответствиеКолонок.Вставить("Комментарий", 11);
	СоответствиеКолонок.Вставить("Задача", 3);
	СоответствиеКолонок.Вставить("НаименованиеКонтрагента", 5);
	СоответствиеКолонок.Вставить("КодЕДРПОУ", 6);
	// Ссылочные типы
	СоответствиеКолонок.Вставить("СтатьяДвиженияДенежныхСредств", 2);
	СоответствиеКолонок.Вставить("СтатьяЗатрат", 4);
	СоответствиеКолонок.Вставить("Контрагент", 6);
	
	ПоискКонтрагенты = Новый Структура;
	ПоискКонтрагенты.Вставить("ЕГРПОУ");
	
	НовыеКонтрагенты = Новый Структура;
	НовыеКонтрагенты.Вставить("Наименование", "НаименованиеКонтрагента");
	НовыеКонтрагенты.Вставить("ЕГРПОУ", "КодЕДРПОУ");
	
	НовыеБанковскиеСчета = Новый Структура;
	НовыеБанковскиеСчета.Вставить("Владелец", "Контрагент");
	НовыеБанковскиеСчета.Вставить("Банк", "Банк");
	
	СсылкиПодразделения = Новый Соответствие;
	СсылкиПодразделения.Вставить("земля", Справочники.Подразделения.НайтиПоКоду("00-000005"));
	СсылкиПодразделения.Вставить("Продажі", Справочники.Подразделения.НайтиПоКоду("00-000007"));
	СсылкиПодразделения.Вставить("Будівництво", Справочники.Подразделения.НайтиПоКоду("00-000002"));
	СсылкиПодразделения.Вставить("СЗ", Справочники.Подразделения.НайтиПоКоду("00-000013"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СоздаватьКонтрагенты", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьСтатьиДвиженияДенежныхСредств", Истина);
	ДополнительныеПараметры.Вставить("СоздаватьСтатьиЗатрат", Истина);
	ДополнительныеПараметры.Вставить("ПоискКонтрагенты", ПоискКонтрагенты);
	ДополнительныеПараметры.Вставить("СсылкиПодразделения", СсылкиПодразделения);
	ДополнительныеПараметры.Вставить("НовыеБанковскиеСчета", НовыеБанковскиеСчета);
	ДополнительныеПараметры.Вставить("НовыеКонтрагенты", НовыеКонтрагенты);
	
	ЗагрузитьИзТабличногоДокументаВТаблицу(ТаблицаОплат, ТабДокумент,
		СоответствиеКолонок, ,
		5, 500,
		ДополнительныеПараметры);	
		
	МассивСтрок = Новый Массив;
	Для Каждого Строка Из ТаблицаОплат Цикл
		Если (Не ЗначениеЗаполнено(Строка.Дата) ИЛИ (ЗначениеЗаполнено(ДатаСчетов) И Строка.Дата<ДатаСчетов))
			ИЛИ (Не ЗначениеЗаполнено(Строка.СуммаНал) И Не ЗначениеЗаполнено(Строка.СуммаБез)) Тогда
			МассивСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла; 
	Для Каждого Строка Из МассивСтрок Цикл
		ТаблицаОплат.Удалить(Строка);
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОплаты(Команда)
	ЗагрузитьОплатыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыОплатНаСервере()
		
	Для Каждого Строка Из ТаблицаОплат Цикл
		
		ОбъектДокумента = Неопределено;
		СуммаДокумента = Строка.СуммаБез;
		ТипДокумента = "ПлатежноеПоручениеВходящее";
		Контрагент = Строка.Контрагент;
		
		Если Не ЗначениеЗаполнено(СуммаДокумента) Тогда
			СуммаДокумента = Строка.СуммаНал;
			ТипДокумента = "ПриходныйКассовыйОрдер";
			Если СуммаДокумента<0 Тогда
				ТипДокумента = "РасходныйКассовыйОрдер";
				СуммаДокумента = -СуммаДокумента;
			КонецЕсли;
		Иначе
			Если СуммаДокумента<0 Тогда
				ТипДокумента = "ПлатежноеПоручениеИсходящее";
				СуммаДокумента = -СуммаДокумента;
			КонецЕсли;			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Док.Ссылка
			|ИЗ
			|	Документ.ПриходныйКассовыйОрдер КАК Док
			|ГДЕ
			|	Док.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
			|	И Док.Контрагент = &Контрагент
			|	И Док.СуммаДокумента = &СуммаДокумента";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПриходныйКассовыйОрдер", ТипДокумента);
			ЗапросПоиска = Новый Запрос(ТекстЗапроса);
			ЗапросПоиска.УстановитьПараметр("ДатаНачала", НачалоДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("ДатаКонца", КонецДня(Строка.Дата));
			ЗапросПоиска.УстановитьПараметр("Контрагент", Контрагент);
			ЗапросПоиска.УстановитьПараметр("СуммаДокумента", СуммаДокумента);
			Выборка = ЗапросПоиска.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			Иначе
				ОбъектДокумента = Документы[ТипДокумента].СоздатьДокумент();
			КонецЕсли; 
		Иначе
			ОбъектДокумента = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли; 
		
		Если ОбъектДокумента.ПометкаУдаления Тогда
			ОбъектДокумента.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;		
		
		ЗаполнитьЗначенияСвойств(ОбъектДокумента, Строка);
		ОбъектДокумента.Организация = ОрганизацияНал;
		Если ТипДокумента="ПлатежноеПоручениеВходящее"
			ИЛИ ТипДокумента="ПлатежноеПоручениеИсходящее" Тогда
			ОбъектДокумента.Организация = Организация;
		КонецЕсли; 
		ОбъектДокумента.СуммаДокумента = СуммаДокумента;
		ОбъектДокумента.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		
		МетаданныеДокумента = ОбъектДокумента.Метаданные();
		
		Если Не ЗначениеЗаполнено(ОбъектДокумента.СтатьяДвиженияДенежныхСредств) Тогда
			ОбъектДокумента.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
		КонецЕсли; 
		
		Если МетаданныеДокумента.Реквизиты.Найти("СчетОрганизации")<>Неопределено
			И Не ЗначениеЗаполнено(ОбъектДокумента.СчетОрганизации) Тогда
			ОбъектДокумента.СчетОрганизации = ОбъектДокумента.Организация.ОсновнойБанковскийСчет;
		КонецЕсли;
		
		Если МетаданныеДокумента.Реквизиты.Найти("Касса")<>Неопределено
			И Не ЗначениеЗаполнено(ОбъектДокумента.Касса) Тогда
			ОбъектДокумента.Касса = ОбъектДокумента.Организация.ОсновнаяКасса;
		КонецЕсли;		
				
		ЭтоБылНовый = ОбъектДокумента.ЭтоНовый();
				
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
		Если ЭтоБылНовый Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = ОбъектДокумента.Ссылка;
			СообщениеПользователю.Текст = "Создан " + ОбъектДокумента.Ссылка;
			СообщениеПользователю.Сообщить();
		КонецЕсли;		
		Строка.Ссылка = ОбъектДокумента.Ссылка;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыОплат(Команда)
	СоздатьДокументыОплатНаСервере();
КонецПроцедуры

&НаСервере
Функция НайтиЗаявку(ДокументОплаты)
	
	//Запрос = Новый Запрос(
	//"ВЫБРАТЬ ПЕРВЫЕ 1
	//|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка
	//|ИЗ
	//|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	//|ГДЕ
	//|	ЗаявкаНаРасходованиеДенежныхСредств.ДокументОснование = &ДокументОснование"
	//);
	//Запрос.УстановитьПараметр("ДокументОснование", ДокументОплаты);
	//Выборка = Запрос.Выполнить().Выбрать();
	//Если Выборка.Следующий() Тогда
	//	Возврат Выборка.Ссылка;
	//КонецЕсли;
	
	Возврат ДокументОплаты.ДокументОснование;
	
КонецФункции

&НаСервере
Функция НайтиСчетНаОплатуПоставщика(ДокументОплаты)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Док.Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК Док
	|ГДЕ
	|	Док.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И Док.Контрагент=&Контрагент
	|	И Док.СуммаДокумента=&СуммаДокумента
	|	И Док.ВидОплаты=&ВидОплаты"
	//|	И Док.Подразделение=&Подразделение"
	);
	Запрос.УстановитьПараметр("ДатаНачала", ДокументОплаты.Дата-24*60*60*3);
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ДокументОплаты.Дата));
	Запрос.УстановитьПараметр("Контрагент", ДокументОплаты.Контрагент);
	Запрос.УстановитьПараметр("СуммаДокумента", ДокументОплаты.СуммаДокумента);
	Запрос.УстановитьПараметр("ВидОплаты", ?(ТипЗнч(ДокументОплаты)=Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее"), Перечисления.ВидыДенежныхСредств.Безналичные, Перечисления.ВидыДенежныхСредств.Наличные));
	//Запрос.УстановитьПараметр("Подразделение", ДокументОплаты.Подразделение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура СоздатьДокументыЗаявкиНаСервере()
	
	МассивДокументов = Новый Массив;
	// Найдем заявки
	Для Каждого Строка Из ТаблицаОплат Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Ссылка) Тогда
			Продолжить;
		КонецЕсли; 
		
		Если ТипЗнч(Строка.Ссылка)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
			ИЛИ ТипЗнч(Строка.Ссылка)=Тип("ДокументСсылка.ПлатежноеПоручениеВходящее") Тогда
			Продолжить;
		КонецЕсли; 
		
		Если Не ЗначениеЗаполнено(Строка.Заявка) Тогда
			Строка.Заявка = НайтиЗаявку(Строка.Ссылка);
		КонецЕсли;
		
		НовыйОбъект = Неопределено;
		Если Не ЗначениеЗаполнено(Строка.Заявка) Тогда
			НовыйОбъект = Документы.ЗаявкаНаРасходованиеДенежныхСредств.СоздатьДокумент();
		Иначе
			НовыйОбъект = Строка.Заявка.ПолучитьОбъект();
		КонецЕсли;
		
		Если НовыйОбъект.ПометкаУдаления Тогда
			НовыйОбъект.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;		
		
		ЗаполнитьЗначенияСвойств(НовыйОбъект, Строка.Ссылка,, "Номер, Ссылка");
		НовыйОбъект.ДокументОснование = Неопределено;
		НовыйОбъект.СчетНаОплатуПоставщика = НайтиСчетНаОплатуПоставщика(Строка.Ссылка);
		НовыйОбъект.ВидОплаты = Перечисления.ВидыДенежныхСредств.Безналичные;
		НовыйОбъект.Организация = Организация;
		Если ТипЗнч(Строка.Ссылка)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			НовыйОбъект.ВидОплаты = Перечисления.ВидыДенежныхСредств.Наличные;
			НовыйОбъект.Организация = ОрганизацияНал;
		КонецЕсли;
		ЭтоБылНовый = НовыйОбъект.ЭтоНовый();
				
		НовыйОбъект.Записать();
		Если ЭтоБылНовый Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.КлючДанных = НовыйОбъект.Ссылка;
			СообщениеПользователю.Текст = "Создан " + НовыйОбъект.Ссылка;
			СообщениеПользователю.Сообщить();
		КонецЕсли; 
		
		МетаданныеСсылки = Строка.Ссылка.Метаданные();
		Если МетаданныеСсылки.Реквизиты.Найти("ДокументОснование")<>Неопределено Тогда
			ОбъектСсылки = Строка.Ссылка.ПолучитьОбъект();
			ОбъектСсылки.ДокументОснование = НовыйОбъект.Ссылка;
			ОбъектСсылки.Записать();
		КонецЕсли; 
		
		Строка.Заявка = НовыйОбъект.Ссылка;
		Строка.ЗаявкаВсеНашла = ЗначениеЗаполнено(Строка.Заявка.СчетНаОплатуПоставщика);
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыЗаявки(Команда)
	СоздатьДокументыЗаявкиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СсылкаДокумента = Документы.ПлатежноеПоручениеИсходящее.НайтиПоНомеру("ED00-0426");
	НайденныйСчет = НайтиСчетНаОплатуПоставщика(СсылкаДокумента);
КонецПроцедуры

&НаСервере
Процедура УдалитьПомеченныеСчетаИзЗаявокНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|ГДЕ
	|	НЕ ЗаявкаНаРасходованиеДенежныхСредств.ПометкаУдаления
	|	И ЗаявкаНаРасходованиеДенежныхСредств.СчетНаОплатуПоставщика.ПометкаУдаления"
	);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектЗаявки = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектЗаявки.СчетНаОплатуПоставщика = Неопределено;
		ОбъектЗаявки.Записать();
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|ГДЕ
	|	НЕ ЗаявкаНаРасходованиеДенежныхСредств.ПометкаУдаления
	|	И ЗаявкаНаРасходованиеДенежныхСредств.СчетНаОплатуПоставщика.ПометкаУдаления"
	);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектЗаявки = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектЗаявки.СчетНаОплатуПоставщика = Неопределено;
		ОбъектЗаявки.Записать();
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПомеченныеСчетаИзЗаявок(Команда)
	УдалитьПомеченныеСчетаИзЗаявокНаСервере();
КонецПроцедуры