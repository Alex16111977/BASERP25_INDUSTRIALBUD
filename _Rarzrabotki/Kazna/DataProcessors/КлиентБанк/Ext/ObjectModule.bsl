#Область Выгрузка

// Заполняет табл. часть "Банковские счета" настройками счетов для выгрузки платежей
//
Процедура ЗаполнитьТаблицуСчетовВыгрузки() Экспорт
	
	ТаблицаСчетов = ТаблицаСчетов();
	
	БанковскиеСчета.Загрузить(ТаблицаСчетов);
	
КонецПроцедуры

// Формирует таблицу баннковских счетов с настройками обмена
//
// Параметры
//    Загрузка - Булево - Признак подготовки счетов для загрузки или для выгрузки платежей
//
Функция ТаблицаСчетов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	БанковскиеСчета.Ссылка,
	               |	ЛОЖЬ КАК Пометка,
	               |	БанковскиеСчета.Наименование,
	               |	БанковскиеСчета.Владелец КАК Организация,
	               |	БанковскиеСчета.Банк,
	               |	БанковскиеСчета.НомерСчета,
	               |	БанковскиеСчета.НастройкаОбменаСБанком
	               |ИЗ
	               |	Справочник.БанковскиеСчета КАК БанковскиеСчета
	               |ГДЕ
	               |	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
	               |	И БанковскиеСчета.Владелец.ПометкаУдаления = ЛОЖЬ
	               |	И БанковскиеСчета.ПометкаУдаления = ЛОЖЬ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	БанковскиеСчета.Владелец.Наименование";
	
	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаСчетов;
	
КонецФункции
	
// Выполняет выгрузку платежей в файл по указанному счету
//
// Параметры
//    Счет - Строка табличной части - Строка списка банковских счетов
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ВыгрузитьПлатежиПоСчету(Счет, ИдФормы = "") Экспорт
	
	Счет.Выгружен = Ложь;
	
	СтрокиКВыгрузке = ДокументыКВыгрузкеПоСчету(Счет);
	Если СтрокиКВыгрузке.Количество() Тогда
		ПараметрыВыгрузки = Новый Структура;
		ПараметрыВыгрузки.Вставить("БанковскийСчет",     Счет.Ссылка);
		ПараметрыВыгрузки.Вставить("НастройкаОбмена",    Счет.НастройкаОбмена);
		ПараметрыВыгрузки.Вставить("Кодировка",          Счет.Кодировка);
		
		ТаблицаДокументов = ДокументыКВыгрузке.Выгрузить(СтрокиКВыгрузке);
		ПараметрыВыгрузки.Вставить("ТаблицаДокументов", ТаблицаДокументов);
		
		Счет.АдресХранилищаДокументов = ПоместитьВоВременноеХранилище(ТаблицаДокументов, ИдФормы);
		
		//Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		//	ИдФормы,
		//	"Обработки.КлиентБанк.Выгрузить",
		//	ПараметрыВыгрузки,
		//	НСтр("ru='Выгрузка платежей в банк'")
		//);
		//
		//Счет.АдресХранилищаФайла = Результат.АдресХранилища;
		//
		//Если Результат.ЗаданиеВыполнено Тогда
		//	Счет.СохранитьФайл = Истина;
		//Иначе
		//	Счет.Выгружается = Истина;
		//	Счет.ИдентификаторВыгрузки = Результат.ИдентификаторЗадания;
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует Табличный документ с отчетом о выгруженных платежах
//
// Параметры
//    ПрямойОбмен - Булево - Признак печати отчета для выгрузки через прямой обмен
//
// Возвращаемое значение
//    ТабличныйДокумент - отчет о выгрузке
//
Функция ПечатьОтчетаОВыгрузке(ПрямойОбмен = Ложь) Экспорт
	
	ПолеОтчета = Неопределено;
	
	Для каждого Счет из БанковскиеСчета Цикл
		Если Счет.ПрямойОбмен И НЕ ПрямойОбмен
			Или Не Счет.ПрямойОбмен И ПрямойОбмен Тогда
			Продолжить;
		КонецЕсли;
		Если Счет.Выгружен Тогда
			Если ПолеОтчета = Неопределено Тогда
				ПолеОтчета = Новый ТабличныйДокумент;
				ПолеОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
			КонецЕсли;
			Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
				ВыгруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
				Если ВыгруженныеДокументы <> Неопределено Тогда
					ОтборПоСчету = Новый Структура("БанковскийСчет", Счет.Ссылка);
					ДокументыПоСчету = ВыгруженныеДокументы.Скопировать(ОтборПоСчету);
					
					ВывестиСекциюОтчетаОВыгрузке(ПолеОтчета, ДокументыПоСчету, Счет.Ссылка);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПолеОтчета;
	
КонецФункции

// Записывает дату выгрузки в выгруженные документы, сведения о последней выгрузке по банковскому счету
//
Процедура ЗаписатьДатуВыгрузкиПлатежей() Экспорт
	
	Для каждого Счет из БанковскиеСчета Цикл
		
		Если Счет.Выгружен Тогда
			Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
				ВыгруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
				Если ТипЗнч(ВыгруженныеДокументы) = Тип("ТаблицаЗначений") Тогда
			
					Для каждого ВыгруженныйДокумент Из ВыгруженныеДокументы Цикл
						ДокументОбъект = ВыгруженныйДокумент.Ссылка.ПолучитьОбъект();
						ДокументОбъект.ДатаВыгрузки = ТекущаяДата();
						ДокументОбъект.Записать();
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			РегистрыСведений.ПоследнийОбменСБанками.СоздатьЗапись(Счет.Ссылка, Новый Структура("ДатаВыгрузки", ТекущаяДата()));
			Счет.ДатаПоследнейВыгрузки = ТекущаяДата();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет табл. часть "Документы к выгрузке" платежными поручениями и требованиями
//
Процедура ЗаполнитьТаблицуПлатежей() Экспорт
	
	ДокументыКВыгрузке.Загрузить(ТаблицаДокументовКВыгрузке(СписокСчетов, ДатаНачалаВыгрузки, ДатаКонцаВыгрузки, ТолькоНеВыгруженные) );
	ДокументыКВыгрузке.Сортировать("Дата");
	
	ПроверитьДокументыКВыгрузке();
	
КонецПроцедуры

// Формирует таблицу документов к выгрузке в банк
//
// Параметры
//    МассивСчетов - Массив - счета для отбора платежей к выгрузке
//    ДатаНачала - Дата - Начало периода выборки документов
//    ДатаКонца - Дата - Конец периода выборки документов
//    ТолькоНевыгруженные - Булево - Признак отбора только не выгруженных ранее платежей
//
Функция ТаблицаДокументовКВыгрузке(МассивСчетов = Неопределено,
									ДатаНачала = '00010101',
									ДатаКонца = '00010101',
									ТолькоНевыгруженные = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДокументыКВыгрузке();
	
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца);
	Запрос.УстановитьПараметр("ТолькоНевыгруженные", ТолькоНевыгруженные);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

Функция ТекстЗапросаДокументыКВыгрузке()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПлатежноеПоручениеИсходящее.Ссылка,
	               |	ПлатежноеПоручениеИсходящее.Номер КАК Номер,
	               |	ПлатежноеПоручениеИсходящее.ВалютаДокумента КАК Валюта,
	               |	ПлатежноеПоручениеИсходящее.ВалютаДокумента.Код КАК ВалютаКод,
	               |	ПлатежноеПоручениеИсходящее.Контрагент,
	               |	ПлатежноеПоручениеИсходящее.Контрагент.Наименование КАК КонтрагентНаим,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента КАК БанковскийСчетКонтрагента,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента.НомерСчета КАК КонтрагентРасчСчет,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк КАК КонтрагентБанк,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.Код,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации КАК БанковскийСчет,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.НомерСчета КАК ОрганизацияРасчСчет,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк КАК ОрганизацияБанк,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.Код,
	               |	ПлатежноеПоручениеИсходящее.СуммаДокумента КАК Сумма,
	               |	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.НазначениеПлатежа КАК СТРОКА(160)) КАК НазначениеПлатежа,
	               |	ИСТИНА КАК ИсходящийДокумент,
	               |	ПлатежноеПоручениеИсходящее.Дата,
	               |	ПлатежноеПоручениеИсходящее.Организация,
	               |	ПлатежноеПоручениеИсходящее.Организация.Наименование КАК ОрганизацияНаим,
	               |	ПлатежноеПоручениеИсходящее.Контрагент.ЕГРПОУ,
	               |	ПлатежноеПоручениеИсходящее.Организация.ЕГРПОУ,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.МФО КАК ОрганизацияБанкМФО,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.МФО КАК КонтрагентБанкМФО,
	               |	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.Наименование КАК КонтрагентБанкНаим,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.Наименование КАК ОрганизацияБанкНаим,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации.НастройкаОбменаСБанком КАК НастройкаОбменаСБанком,
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации,
	               |	ПлатежноеПоручениеИсходящее.ДатаВыгрузки,
	               |	ПлатежноеПоручениеИсходящее.Оплачено
	               |ИЗ
	               |	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	               |ГДЕ
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации В(&МассивСчетов)
	               |	И (ПлатежноеПоручениеИсходящее.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	               |			ИЛИ &ДатаКонца = ДАТАВРЕМЯ(1, 1, 1))
	               |	И (ПлатежноеПоручениеИсходящее.ДатаВыгрузки = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ НЕ &ТолькоНевыгруженные)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Номер";
			
	Возврат ТекстЗапроса;
	
КонецФункции      


#КонецОбласти

#Область Загрузка

// Заполняет табл. часть "Банковские счета" настройками счетов для загрузки платежей
//
Процедура ЗаполнитьТаблицуСчетовЗагрузки() Экспорт
	
	ТаблицаСчетов = ТаблицаСчетов();
	
	БанковскиеСчета.Загрузить(ТаблицаСчетов);
	
КонецПроцедуры


// Формирует Табличный документ с отчетом о загруженных платежах
//
// Возвращаемое значение
//    ТабличныйДокумент - отчет о загрузке
//
Функция ПечатьОтчетаОЗагрузке() Экспорт
	
	ПолеОтчета = Неопределено;
	
	Для каждого Счет из БанковскиеСчета Цикл
		Если Счет.Загружен Тогда
			Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
				ЗагруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
				Если ЗагруженныеДокументы <> Неопределено
					И ТипЗнч(ЗагруженныеДокументы) = Тип("ТаблицаЗначений")
					И ЗагруженныеДокументы.Количество() Тогда
					
					Если ПолеОтчета = Неопределено Тогда
						ПолеОтчета = Новый ТабличныйДокумент;
						ПолеОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
					КонецЕсли;
					
					ВывестиСекциюОтчетаОЗагрузке(ПолеОтчета, ЗагруженныеДокументы, Счет.Ссылка);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПолеОтчета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


Процедура ПроверитьДокументыКВыгрузке()
	
	// Основные реквизиты
	Проверки = Новый Соответствие;
	Проверки.Вставить("Номер",   НСтр("ru='Номер документа'"));
	Проверки.Вставить("Дата",    НСтр("ru='Дата документа'"));
	Проверки.Вставить("Сумма",   НСтр("ru='Сумма документа'"));
	
	// Реквизиты организации
	ПроверкиОрганизацииПР = Новый Соответствие;
	ПроверкиОрганизацииПР.Вставить("ОрганизацияРасчСчет",          НСтр("ru='Банковский счет организации'"));
	ПроверкиОрганизацииПР.Вставить("ОрганизацияНаим",              НСтр("ru='Наименование организации'"));
	ПроверкиОрганизацииПР.Вставить("ОрганизацияИНН",               НСтр("ru='ИНН организации'"));
	
	
	// Реквизиты контрагента
	ПроверкиКонтрагентаПР = Новый Соответствие;
	ПроверкиКонтрагентаПР.Вставить("КонтрагентНаим",               НСтр("ru='Контрагент'"));
	ПроверкиКонтрагентаПР.Вставить("КонтрагентИНН",                НСтр("ru='ИНН контрагента'"));
	
	
	Для каждого ДокументКВыгрузке из ДокументыКВыгрузке Цикл
		
		СтрокаОшибки = "";
		Для каждого Проверка из Проверки Цикл
			Если Не ЗначениеЗаполнено(ДокументКВыгрузке[Проверка.Ключ]) Тогда
				СтрокаОшибки = СтрокаОшибки + " " + Проверка.Значение + ",";
			КонецЕсли;
		КонецЦикла;		
		
		Если ЗначениеЗаполнено(СтрокаОшибки) Тогда
			СтрокаОшибки = Лев(СтрокаОшибки, СтрДлина(СтрокаОшибки) - 1);
			ДокументКВыгрузке.ЕстьОшибка = Истина;
			ДокументКВыгрузке.Выгружать = Ложь;
			ДокументКВыгрузке.ОписаниеОшибок = НСтр("ru='Не заполнены реквизиты платежа:'") + СтрокаОшибки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументыКВыгрузкеПоСчетам(Счета)
	ДокументыКВыгрузкеПоСчетам = Новый Массив;
	
	Для Каждого Счет Из Счета Цикл 
		Отбор = Новый Структура;
		Отбор.Вставить("Выгружать", Истина);
		Отбор.Вставить("БанковскийСчет", Счет.Ссылка);
		
		НайденыеДокументы = ДокументыКВыгрузке.НайтиСтроки(Отбор);
		
		Для Каждого НайденныйДокумент Из НайденыеДокументы Цикл 
			ДокументыКВыгрузкеПоСчетам.Добавить(НайденныйДокумент);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДокументыКВыгрузкеПоСчетам;
КонецФункции

Функция ДокументыКВыгрузкеПоСчету(Счет)
	Отбор = Новый Структура;
	Отбор.Вставить("Выгружать", Истина);
	Отбор.Вставить("БанковскийСчет", Счет.Ссылка);
	
	Возврат ДокументыКВыгрузке.НайтиСтроки(Отбор);
КонецФункции

Процедура ВывестиСекциюОтчетаОВыгрузке(ПолеОтчета, Документы, БанковскийСчет)
	
	МакетОтчета = ПолучитьМакет("ОтчетОВыгрузке");
	
	Шапка    = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка   = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал   = МакетОтчета.ПолучитьОбласть("Подвал");
	НазвОрг  = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	
	НазвОрг.Параметры.НазваниеОрганизации = Строка(РеквизитыСчета.Организация);
	ПолеОтчета.Вывести(НазвОрг);
	
	Шапка.Параметры.ИмяОтчета = НСтр("ru='Отчет о выгруженных документах'");
	Шапка.Параметры.ИмяСуммыПоступления = "Поступление";
	Шапка.Параметры.ИмяСуммыСписания    = "Списание";
	
	ОбрБанковскийСчет = "";
	Индекс = 0; ИтогоСуммаП = 0; ИтогоСуммаР = 0;
	
	Документы.Сортировать("Дата");
	НачПериода = Документы[0].Дата;
	КонПериода = Документы[Документы.Количество() - 1].Дата;
	
	Валюта = ?(ЗначениеЗаполнено(РеквизитыСчета.Валюта), СокрЛП(Строка(РеквизитыСчета.Валюта)), НСтр("ru='валюта не указана'"));
	НачалоОтчетногоПериода = Формат(НачПериода, "ДЛФ=D");
	КонецОтчетногоПериода = Формат(КонПериода, "ДЛФ=D");
	
	Шапка.Параметры.ОписаниеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (%2) с %3 по %4'"),
		РеквизитыСчета.НомерСчета,
		Валюта,
		НачалоОтчетногоПериода,
		КонецОтчетногоПериода);
	ПолеОтчета.Вывести(Шапка);
	
	Для Каждого СтрокаИсточника Из Документы Цикл
		
		Валюта = ?(ЗначениеЗаполнено(СтрокаИсточника.Валюта), СокрЛП(Строка(СтрокаИсточника.Валюта)), НСтр("ru='валюта не указана'"));
		
		Если СтрокаИсточника.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование Тогда
			
			Строка.Параметры.Получатель = СтрокаИсточника.ОрганизацияНаим;
			Строка.Параметры.ПолучательСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ОрганизацияРасчСчет,
				Валюта);
			
			Строка.Параметры.Плательщик = СтрокаИсточника.Контрагент;
			Строка.Параметры.ПлательщикСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.КонтрагентРасчСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление  = СтрокаИсточника.Сумма;
			Строка.Параметры.СуммаСписание = "";
			ИтогоСуммаП = ИтогоСуммаП + СтрокаИсточника.Сумма;
		Иначе
			
			Строка.Параметры.Плательщик = СтрокаИсточника.ОрганизацияНаим;
			Строка.Параметры.ПлательщикСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ОрганизацияРасчСчет,
				Валюта);
			
			Строка.Параметры.Получатель = СтрокаИсточника.Контрагент;
			Строка.Параметры.ПолучательСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.КонтрагентРасчСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление = "";
			Строка.Параметры.СуммаСписание = СтрокаИсточника.Сумма;
			ИтогоСуммаР = ИтогоСуммаР + СтрокаИсточника.Сумма;
		КонецЕсли;
		
		Строка.Параметры.Документ = СтрокаИсточника.Ссылка;
		Индекс = Индекс + 1;
		Строка.Параметры.Индекс = Индекс;
		
		ПолеОтчета.Вывести(Строка);
		
	КонецЦикла;
	
	Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;
	Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;
	
	ПолеОтчета.Вывести(Подвал);
	
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку       = Ложь;
	ПолеОтчета.ТолькоПросмотр        = Истина;
	
КонецПроцедуры

Процедура ВывестиСекциюОтчетаОЗагрузке(ПолеОтчета, Документы, БанковскийСчет)
	
	МакетОтчета = ПолучитьМакет("ОтчетОЗагрузке");
	
	ИмеетсяСекцияРасчСчет = Ложь;
	
	Шапка   = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка  = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал  = МакетОтчета.ПолучитьОбласть("Подвал");
	Остатки = МакетОтчета.ПолучитьОбласть("Остатки");
	НазвОрг = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	
	НазвОрг.Параметры.НазваниеОрганизации = Строка(РеквизитыСчета.Организация);
	ПолеОтчета.Вывести(НазвОрг);
	
	Шапка.Параметры.ИмяОтчета = НСтр("ru='Отчет о загруженных документах'");

	ОбрБанковскийСчет = "";
	Индекс = 0; ИтогоСуммаП = 0; ИтогоСуммаР = 0;
	
	Документы.Сортировать("Дата");
	НачПериода = Документы[0].ДатаДок;
	КонПериода = Документы[Документы.Количество() - 1].ДатаДок;
	
	Валюта = ?(ЗначениеЗаполнено(РеквизитыСчета.Валюта), СокрЛП(Строка(РеквизитыСчета.Валюта)), НСтр("ru='валюта не указана'"));
	НачалоОтчетногоПериода = Формат(НачПериода, "ДЛФ=D");
	КонецОтчетногоПериода = Формат(КонПериода, "ДЛФ=D");
	
	Шапка.Параметры.ОписаниеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (%2) с %3 по %4'"),
		РеквизитыСчета.НомерСчета,
		Валюта,
		НачалоОтчетногоПериода,
		КонецОтчетногоПериода);
	ПолеОтчета.Вывести(Шапка);
	
	Для Каждого СтрокаИсточника Из Документы Цикл
		
		Валюта = ?(ЗначениеЗаполнено(СтрокаИсточника.Валюта), СокрЛП(Строка(СтрокаИсточника.Валюта)), НСтр("ru='валюта не указана'"));
		
		Если СтрокаИсточника.СуммаПоступило > 0 Тогда
			
			Строка.Параметры.Контрагент =
				?(ПустаяСтрока(СтрокаИсточника.Плательщик1), СтрокаИсточника.Плательщик, СтрокаИсточника.Плательщик1);
			Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ПлательщикСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление = СтрокаИсточника.СуммаПоступило;
			Строка.Параметры.СуммаСписание    = "";
			Строка.Параметры.Дата             = Формат(СтрокаИсточника.ДатаДок, "ДЛФ=Д");
			ИтогоСуммаП                       = ИтогоСуммаП + СтрокаИсточника.СуммаПоступило;
			
		ИначеЕсли СтрокаИсточника.СуммаСписано > 0 Тогда

			Строка.Параметры.Контрагент =
				?(ПустаяСтрока(СтрокаИсточника.Получатель1), СтрокаИсточника.Получатель, СтрокаИсточника.Получатель1);
			Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ПолучательСчет,
				Валюта);
				
			Строка.Параметры.СуммаСписание    = СтрокаИсточника.СуммаСписано;
			Строка.Параметры.СуммаПоступление = "";
			Строка.Параметры.Дата             = ?(ПустаяСтрока(СтрокаИсточника.Дата), Формат(СтрокаИсточника.ДатаДок, "ДЛФ=Д"), СтрокаИсточника.Дата);
			ИтогоСуммаР                       = ИтогоСуммаР + СтрокаИсточника.СуммаСписано;

		Иначе
			Продолжить;
		КонецЕсли;
		
		Док = СтрокаИсточника.Документ;
		Если ЗначениеЗаполнено(Док) Тогда
			Строка.Параметры.Документ     = Док;
			Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		Иначе
			Строка.Параметры.Документ     = НСтр("ru='НЕ ЗАГРУЖЕН'");
			Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		КонецЕсли;
		
		Строка.Параметры.Номер             = СтрокаИсточника.Номер;
		Строка.Параметры.НазначениеПлатежа = СтрокаИсточника.НазначениеПлатежа;
		
		Индекс = Индекс + 1;
		Строка.Параметры.Индекс = Индекс;
		
		ПолеОтчета.Вывести(Строка);
		
	КонецЦикла;
	
	Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;
	Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;
	
	ПолеОтчета.Вывести(Подвал);
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку       = Ложь;
	ПолеОтчета.ТолькоПросмотр        = Истина;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.1.3.1");
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = "0.1";
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	//ПараметрыРегистрации.Назначение.Добавить("Документ._ДемоСчетНаОплатуПокупателю");
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = НСтр("ru = 'Клиент-банк(загрузка)'");
	НоваяКоманда.Идентификатор = "КлиентБанк";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.ПоказыватьОповещение = Истина;
	НоваяКоманда.Модификатор = "КлиентБанк";
		
	Возврат ПараметрыРегистрации;
	
КонецФункции

//-Бахтишаев
