
&НаКлиенте
Перем СуммыПлатежейКВыгрузке;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьТаблицуСчетов();
	
	Если Параметры.Свойство("БанковскийСчет")
		И ЗначениеЗаполнено(Параметры.БанковскийСчет) Тогда
		
		СтрокиТаблицы = Объект.БанковскиеСчета.НайтиСтроки(Новый Структура("Ссылка", Параметры.БанковскийСчет));
		Если СтрокиТаблицы.Количество() Тогда
			СтрокиТаблицы[0].Пометка = Истина;
			Объект.СписокСчетов.Добавить(СтрокиТаблицы[0].Ссылка);
			ОбновитьСписокПлатежей();
			ИндексСтрокиСчета = Объект.БанковскиеСчета.Индекс(СтрокиТаблицы[0]);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораПериодов();
		
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Объект.ТолькоНевыгруженные = (ТолькоНевыгруженные = 1);
	
	Если Период = "Все время" Тогда
		Объект.ДатаНачалаВыгрузки  = '00010101';
		Объект.ДатаКонцаВыгрузки   = '00010101';
	ИначеЕсли Период = "ЗаДату" Тогда
		Объект.ДатаНачалаВыгрузки  = НачалоДня(ДатаОтбор);
		Объект.ДатаКонцаВыгрузки   = КонецДня(ДатаОтбор);
	ИначеЕсли ТипЗнч(Период) = Тип("Дата") Тогда
		Объект.ДатаНачалаВыгрузки  = Период;
		Объект.ДатаКонцаВыгрузки   = КонецДня(Период);
	ИначеЕсли Период = "Сегодня" Тогда
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата();
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
	ИначеЕсли Период = "Три дня" Тогда
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата() - 3 * 24 * 3600;
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
	ИначеЕсли Период = "Неделю" Тогда
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата() - 7 * 24 * 3600;
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
	КонецЕсли;
	
	ОбновитьСписокПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПодвал();
	
	Если Объект.СписокСчетов.Количество() = 1 Тогда
		БанковскийСчет = Объект.СписокСчетов[0].Значение;
		Элементы.БанковскиеСчета.ТекущаяСтрока = ИндексСтрокиСчета;
	КонецЕсли;
	
	//ВозможностьВыбораФайлов = ПодключитьРасширениеРаботыСФайлами();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСпискаБанковскихСчетов

&НаКлиенте
Процедура БанковскиеСчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	
	Если Поле.Имя = "БанковскиеСчетаСсылка" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
		
	ИначеЕсли Поле.Имя = "БанковскиеСчетаНастройкаОбмена" Тогда
		
		Если ТекущиеДанные.Выгружается Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаПометкаПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработатьПометкуСчета", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаНастройкаОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.НастройкаОбмена) Тогда
		//ФинансыКлиент.ОткрытьФайлДляПросмотра(
		//	"Объект.БанковскиеСчета[" + Строка(Элементы.БанковскиеСчета.ТекущаяСтрока) + "].НастройкаОбмена",
		//	ТекущиеДанные.НастройкаОбмена, ТекущиеДанные.Кодировка, НСтр("ru = 'Файл выгрузки'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаНастройкаОбменаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	ТекущиеДанные.ПравилоФайловогоОбменаСБанками = ТекущиеДанные.НастройкаОбмена;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуПрямойОбменНажатие(Элемент)
	
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	
	Обработчик = Новый ОписаниеОповещения("ПослеСозданияНастройкиЭДО", ЭтотОбъект);
	//ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(
	//	ТекущиеДанные.Организация,
	//	ТекущиеДанные.Банк,
	//	ТекущиеДанные.НомерСчета,
	//	Обработчик);
	//
КонецПроцедуры

&НаКлиенте
Процедура УзнатьПодробнееПрямойОбменНажатие(Элемент)
	
	//ОбщегоНазначенияУТКлиент.ЗапуститьПриложениеБезОбработкиРезультата("http://v8.1c.ru/edi/edi_app/bank/");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСпискаПлатежей

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Если Период = "Все время" Тогда
		
		Объект.ДатаНачалаВыгрузки  = '00010101';
		Объект.ДатаКонцаВыгрузки   = '00010101';
		
	ИначеЕсли Период = "ЗаДату" Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыполнитьПослеВыбораДаты", ЭтаФорма, );
		ПоказатьВводДаты(Оповещение, ТекущаяДата(), "Дата отбора платежей", ЧастиДаты.Дата);
		Возврат;
		
	ИначеЕсли ТипЗнч(Период) = Тип("Дата") Тогда
		
		Объект.ДатаНачалаВыгрузки  = Период;
		Объект.ДатаКонцаВыгрузки   = КонецДня(Период);
		
	ИначеЕсли Период = "Сегодня" Тогда
		
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата();
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
		
	ИначеЕсли Период = "Три дня" Тогда
		
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата() - 3 * 24 * 3600;
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
		
	ИначеЕсли Период = "Неделю" Тогда
		
		Объект.ДатаНачалаВыгрузки  = ТекущаяДата() - 7 * 24 * 3600;
		Объект.ДатаКонцаВыгрузки   = КонецДня(ТекущаяДата());
		
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "Дату" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ВыполнитьПослеВыбораДаты", ЭтотОбъект);
		
		ПоясняющийТекст = НСтр("ru='Выберите дату выгружаемых документов'") + " ";
		ОткрытьФорму(
			"ОбщаяФорма.ВыборДаты",
			Новый Структура("ПоясняющийТекст, НачальноеЗначение", ПоясняющийТекст),
			,,,,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) <> Тип("Дата") Тогда
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("Строка"));
		ОписаниеТиповДата = Новый ОписаниеТипов(Массив, , );
		
		Элемент.ОграничениеТипа = ОписаниеТиповДата;
		
		Элемент.КнопкаВыбора = Ложь;
		Элемент.РедактированиеТекста = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбораДаты(ВыбраннаяДата, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбраннаяДата) Тогда
		
		Объект.ДатаНачалаВыгрузки  = НачалоДня(ВыбраннаяДата);
		Объект.ДатаКонцаВыгрузки   = КонецДня(ВыбраннаяДата);
		Период = ВыбраннаяДата;
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("Дата"));
		ОписаниеТиповДата = Новый ОписаниеТипов(Массив, , );
		
		Элементы.Период.ОграничениеТипа = ОписаниеТиповДата;
		
		Элементы.Период.КнопкаВыбора = Истина;
		Элементы.Период.РедактированиеТекста = Истина;
	Иначе
		
		Если ТипЗнч(Период) = Тип("Дата") Тогда
			Возврат;
		КонецЕсли;
		
		Если Объект.ДатаНачалаВыгрузки  = '00010101' И Объект.ДатаКонцаВыгрузки = '00010101' Тогда
			Период = "Все время";
		ИначеЕсли Объект.ДатаНачалаВыгрузки = ТекущаяДата() И Объект.ДатаКонцаВыгрузки = КонецДня(ТекущаяДата()) Тогда
			Период = "Сегодня";
		ИначеЕсли Объект.ДатаНачалаВыгрузки = ТекущаяДата() - 3 * 24 * 3600 И Объект.ДатаКонцаВыгрузки = КонецДня(ТекущаяДата()) Тогда
			Период = "Три дня";
		ИначеЕсли Объект.ДатаНачалаВыгрузки = ТекущаяДата() - 7 * 24 * 3600 И Объект.ДатаКонцаВыгрузки = КонецДня(ТекущаяДата()) Тогда
			Период = "Неделю";
		КонецЕсли;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	ОбновитьСписокПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоНеВыгруженныеПриИзменении(Элемент)
	
	Объект.ТолькоНевыгруженные = (ТолькоНевыгруженные = 1);
	
	ОбновитьСписокПлатежей();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКВыгрузкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Поле.Имя = "ДокументыКВыгрузкеВыгружать" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеДокументыВыгружатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыКВыгрузке.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Знак = ?(ТекущиеДанные.Выгружать, 1, -1);
	
	КоличествоКВыгрузке = КоличествоКВыгрузке + 1 * Знак;
	
	СуммаПлатежей = СуммыПлатежейКВыгрузке.Получить(ТекущиеДанные.Валюта);
	Если СуммаПлатежей = Неопределено Тогда
		СуммыПлатежейКВыгрузке.Вставить(ТекущиеДанные.Валюта, ТекущиеДанные.Сумма);
	Иначе
		СуммыПлатежейКВыгрузке.Вставить(ТекущиеДанные.Валюта, СуммаПлатежей + ТекущиеДанные.Сумма * Знак);
	КонецЕсли;
	СформироватьСтрокуСуммыКВыгрузке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьСчета(Команда)
	
	Объект.СписокСчетов.Очистить();
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		Если Не Счет.Выгружается Тогда
			Счет.Пометка = Истина;
			Объект.СписокСчетов.Добавить(Счет.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьСписокПлатежей();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкиСчетов(Команда)
	
	Объект.СписокСчетов.Очистить();
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		Если Не Счет.Выгружается Тогда
			Счет.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Объект.ДокументыКВыгрузке.Очистить();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокСчетов(Команда)
	
	// Если есть счета, которые выгружаются, обновить не даем
	ПроизводитсяВыгрузка = Ложь;
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		Если Счет.Выгружается Тогда
			ПроизводитсяВыгрузка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПроизводитсяВыгрузка Тогда
		ОбновитьСписокСчетовНаСервере();
		ОбновитьПодвал();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСчетовНаСервере()
	
	ЗаполнитьТаблицуСчетов();
	
	// Отмеченные счета сохраняют отметку
	МассивНайденныхСчетов = Новый Массив;
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		СчетСписка = Объект.СписокСчетов.НайтиПоЗначению(Счет.Ссылка);
		Если СчетСписка <> Неопределено Тогда
			Счет.Пометка = Истина;
			МассивНайденныхСчетов.Добавить(СчетСписка);
		КонецЕсли;
	КонецЦикла;
	
	МассивНеНайденныхСчетов = Новый Массив;
	Для каждого Счет из Объект.СписокСчетов Цикл
		Если МассивНайденныхСчетов.Найти(Счет) = Неопределено Тогда
			МассивНеНайденныхСчетов.Добавить(Счет);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Счет Из МассивНеНайденныхСчетов Цикл
		Объект.СписокСчетов.Удалить(Счет);
	КонецЦикла;
	
	ОбновитьСписокПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отметить(Команда)
	
	Для каждого СтрокаДокумента Из Объект.ДокументыКВыгрузке Цикл
		СтрокаДокумента.Выгружать = Истина;
	КонецЦикла;
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для каждого СтрокаДокумента Из Объект.ДокументыКВыгрузке Цикл
		СтрокаДокумента.Выгружать = Ложь;
	КонецЦикла;
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСписокПлатежей();
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ВыгрузитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНаКлиенте()

	СтруктураОтбора	= Новый Структура("Пометка", Истина);
	МассивСчетовКВыгрузке = Объект.БанковскиеСчета.НайтиСтроки(СтруктураОтбора);
	Для Каждого СтрокаБанковскогоСчета Из МассивСчетовКВыгрузке Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаБанковскогоСчета.НастройкаОбменаСБанком) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для счета %1 не указана настройка обмена", СтрокаБанковскогоСчета.Ссылка));
			Продолжить;	
		КонецЕсли;
		
		Если ПустаяСтрока(СтрокаБанковскогоСчета.ФайлОбмена) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для счета %1 не указан путь сохранения", СтрокаБанковскогоСчета.Ссылка));
			Продолжить;	
		КонецЕсли;
			
		АдресХранилищаФайла = ВыгрузитьНаСервере(СтрокаБанковскогоСчета.Ссылка, СтрокаБанковскогоСчета.ФайлОбмена);
		
		Если Не ПустаяСтрока(АдресХранилищаФайла) Тогда
			#Если ВебКлиент Тогда
				СтруктураИмениФайла	= ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(СтрокаБанковскогоСчета.ФайлОбмена);
				Файл = ПолучитьФайл(АдресХранилищаФайла, СтруктураИмениФайла.Имя);
			#Иначе
				Файл = ПолучитьФайл(АдресХранилищаФайла, СтрокаБанковскогоСчета.ФайлОбмена, Ложь);
			#КонецЕсли
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьНаСервере(БанковскийСчет, ФайлОбмена)
	
	РеквизитОбъект	= РеквизитФормыВЗначение("Объект");
	
	СтруктураОтбораДокументов	= Новый Структура("СчетОрганизации, Выгружать", БанковскийСчет, Истина);
	МассивСтрокКВыгрузке		= РеквизитОбъект.ДокументыКВыгрузке.НайтиСтроки(СтруктураОтбораДокументов);
		
	СтруктураИмениФайла			= ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ФайлОбмена);
	СсылкаНаОбработкуЭкспорта	= ПолучитьНавигационнуюСсылку(БанковскийСчет.НастройкаОбменаСБанком, "ОбработкаЭкспорта");
	
	ИмяФайлаВнешнейОбработкиНастроек	= ВнешниеОбработки.Подключить(СсылкаНаОбработкуЭкспорта, , Ложь);
	ВнешняяОбработкаНастроек			= ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработкиНастроек);
	Возврат ВнешняяОбработкаНастроек.ВыгрузитьВФайл(СтруктураИмениФайла.Имя, МассивСтрокКВыгрузке);	
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	
	// Производится выгрузка данных по счету
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.БанковскиеСчетаКВыгрузке.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.БанковскиеСчета.Выгружается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Выгружается...'"));
	
	// Документ ранее не выгружался в банк
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДокументыКВыгрузкеДатаВыгрузки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДокументыКВыгрузке.ДатаВыгрузки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Дата("00010101");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "-");
	
	// Есть ошибка, препятствующая выгрузке документа
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДокументыКВыгрузке.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДокументыКВыгрузке.ЕстьОшибка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	// Закрытые счета серым
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("БанковскиеСчета");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.БанковскиеСчета.Закрыт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	//Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	ЕстьВозможностьПрямогоОбмена = Ложь;
	
	ЕстьОшибкиПлатежей = Ложь;
	Отбор = Новый Структура("ЕстьОшибка", Истина);
	НайденныеСтроки = Объект.ДокументыКВыгрузке.НайтиСтроки(Отбор);
	ЕстьОшибкиПлатежей = Булево(НайденныеСтроки.Количество());
	Элементы.ДокументыКВыгрузкеОписаниеОшибок.Видимость = ЕстьОшибкиПлатежей;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСчетов()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаполнитьТаблицуСчетовВыгрузки();
	ЗначениеВРеквизитФОрмы(Обработка, "Объект");
	
	МассивСчетов = Объект.БанковскиеСчета.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ДополнитьТаблицуСчетовКоличествомДокументовКВыгрузке(МассивСчетов);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуСчетовКоличествомДокументовКВыгрузке(МассивСчетов)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДатаНачала", Объект.ДатаНачалаВыгрузки);
	Запрос.УстановитьПараметр("ДатаКонца", Объект.ДатаКонцаВыгрузки);
	Запрос.УстановитьПараметр("ТолькоНеВыгруженные", Объект.ТолькоНеВыгруженные);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПлатежноеПоручениеВходящее.Ссылка) КАК Количество,
	               |	ПлатежноеПоручениеВходящее.СчетОрганизации КАК БанковскийСчет,
	               |	ИСТИНА КАК ИсходящийДокумент
	               |ИЗ
	               |	Документ.ПлатежноеПоручениеВходящее КАК ПлатежноеПоручениеВходящее
	               |ГДЕ
	               |	ПлатежноеПоручениеВходящее.СчетОрганизации В(&МассивСчетов)
	               |	И (ПлатежноеПоручениеВходящее.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	               |			ИЛИ &ДатаКонца = ДАТАВРЕМЯ(1, 1, 1))
	               |	И (ПлатежноеПоручениеВходящее.ДатаВыгрузки = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ НЕ &ТолькоНевыгруженные)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПлатежноеПоручениеВходящее.СчетОрганизации
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПлатежноеПоручениеИсходящее.Ссылка),
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	               |ГДЕ
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации В(&МассивСчетов)
	               |	И ПлатежноеПоручениеИсходящее.ДатаВыгрузки = ДАТАВРЕМЯ(1, 1, 1)
	               |	И (ПлатежноеПоручениеИсходящее.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	               |			ИЛИ &ДатаКонца = ДАТАВРЕМЯ(1, 1, 1))
	               |	И (ПлатежноеПоручениеИсходящее.ДатаВыгрузки = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ НЕ &ТолькоНевыгруженные)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПлатежноеПоручениеИсходящее.СчетОрганизации";
	
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	Результат = Запрос.Выполнить();
	//Если Результат.Пустой() Тогда
	//	Возврат;
	//КонецЕсли;
	
	Таблица = Результат.Выгрузить();
	Таблица.Свернуть("БанковскийСчет", "Количество");
	
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		СтрокаКоличества = Таблица.Найти(Счет.Ссылка, "БанковскийСчет");
		Если СтрокаКоличества <> Неопределено Тогда
			Счет.КВыгрузке = СтрокаКоличества.Количество;
		Иначе
			Счет.КВыгрузке = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокПлатежей()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаполнитьТаблицуПлатежей();
	ЗначениеВРеквизитФОрмы(Обработка, "Объект");
	
	МассивСчетов = Объект.БанковскиеСчета.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ДополнитьТаблицуСчетовКоличествомДокументовКВыгрузке(МассивСчетов);
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПериодов()
	
	СписокВыбораПериода = Элементы.Период.СписокВыбора;
	
	СписокВыбораПериода.Добавить("Все время",     НСтр("ru='Все время'"));
	СписокВыбораПериода.Добавить("Дату",          НСтр("ru='Дату...'"));
	СписокВыбораПериода.Добавить("Сегодня",       НСтр("ru='Сегодня'"));
	СписокВыбораПериода.Добавить("Три дня",       НСтр("ru='Три дня'"));
	СписокВыбораПериода.Добавить("Неделю",        НСтр("ru='Неделю'"));
	
	// Начальные установки
	Период = "Все время";
	Объект.ДатаНачалаВыгрузки = '00010101';
	Объект.ДатаКонцаВыгрузки = '00010101';
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПометкуСчета()
	
	Объект.СписокСчетов.Очистить();
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		Если Счет.Пометка Тогда
			Объект.СписокСчетов.Добавить(Счет.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьСписокПлатежей();
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодвал()
	
	СуммыПлатежейКВыгрузке.Очистить();
	
	ОтборСтрокКВыгрузке = Новый Структура("Выгружать", Истина);
	СтрокиКВыгрузке = Объект.ДокументыКВыгрузке.НайтиСтроки(ОтборСтрокКВыгрузке);
	
	КоличествоКВыгрузке = СтрокиКВыгрузке.Количество();
	
	Для каждого Строка Из СтрокиКВыгрузке Цикл
		СуммаПлатежей = СуммыПлатежейКВыгрузке.Получить(Строка.Валюта);
		Если СуммаПлатежей = Неопределено Тогда
			СуммыПлатежейКВыгрузке.Вставить(Строка.Валюта, Строка.Сумма);
		Иначе
			СуммыПлатежейКВыгрузке.Вставить(Строка.Валюта, СуммаПлатежей + Строка.Сумма);
		КонецЕсли;
	КонецЦикла;
	СформироватьСтрокуСуммыКВыгрузке();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСтрокуСуммыКВыгрузке()
	
	СуммаКВыгрузке = "";
	Если СуммыПлатежейКВыгрузке.Количество() Тогда
		Для каждого СуммаПлатежей из СуммыПлатежейКВыгрузке Цикл
			Если ЗначениеЗаполнено(СуммаПлатежей.Значение) Тогда
				СуммаКВыгрузке = СуммаКВыгрузке + Формат(СуммаПлатежей.Значение, "ЧЦ=15; ЧДЦ=2") + " " + Строка(СуммаПлатежей.Ключ) + ", ";
			КонецЕсли;
		КонецЦикла;
		СуммаКВыгрузке = Лев(СуммаКВыгрузке, СтрДлина(СуммаКВыгрузке) - 2);
		СуммаКВыгрузке = НСтр("ru = 'На сумму:'") + " " + ?(СуммаКВыгрузке = "", "-", СуммаКВыгрузке);
	Иначе
		СуммаКВыгрузке = НСтр("ru = 'На сумму: -'")
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьСтрокуФильтра(СписокРасширений = Неопределено)
	
	СтрокаФильтра = "";
	
	Для Каждого Расширение Из СписокРасширений Цикл
		БазоваяСтрока	= "%описание (*.%расширение)|*%расширение";
		БазоваяСтрока   = СтрЗаменить(БазоваяСтрока, "%описание", Расширение.Представление);
		БазоваяСтрока   = СтрЗаменить(БазоваяСтрока, "%расширение", Расширение.Значение);
		СтрокаФильтра	= СтрокаФильтра + ?(ПустаяСтрока(СтрокаФильтра), "", "|") + БазоваяСтрока;
	КонецЦикла;
	
	Возврат СтрокаФильтра;
	
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруФильтровРасширенийДляБанковскогоСчета(БанковскийСчет)
	
	СсылкаНаОбработкуИмпорта	= ПолучитьНавигационнуюСсылку(БанковскийСчет.НастройкаОбменаСБанком, "ОбработкаЭкспорта");
	
	ИмяФайлаВнешнейОбработкиНастроек	= ВнешниеОбработки.Подключить(СсылкаНаОбработкуИмпорта, , Ложь);
	ВнешняяОбработкаНастроек			= ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработкиНастроек);
	СписокРасширений					= ВнешняяОбработкаНастроек.ВернутьСписокПодерживаемыхРасширений();	

	Возврат Новый Структура("СписокРасширений, СтрокаФильтра", СписокРасширений , СоздатьСтрокуФильтра(СписокРасширений));
	
КонецФункции

#КонецОбласти

#Область РаботаСФайлами

&НаКлиенте
Процедура БанковскиеСчетаФайлОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	#Если ВебКлиент Тогда
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если Не РасширениеПодключено Тогда 
		Предупреждение("В Веб-клиенте без установленного расширения работы с файлами добавление файлов не поддерживается.");
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	СтруктураФильтра	= ПолучитьСтруктуруФильтровРасширенийДляБанковскогоСчета(Элементы.БанковскиеСчета.ТекущиеДанные.Ссылка);
	
	Если СтруктураФильтра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла	= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                  = СтруктураФильтра.СтрокаФильтра;
	ДиалогВыбораФайла.Заголовок               = "Выберите файл для выгрузки данных";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = СтруктураФильтра.СписокРасширений[0].Значение;
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла          = Элементы.БанковскиеСчета.ТекущиеДанные.ФайлОбмена;

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"БанковскиеСчетаВыборФайлаЗавершение",
		ЭтотОбъект,
		Новый Структура(""));
	
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);

КонецПроцедуры


&НаКлиенте
Процедура БанковскиеСчетаВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	Иначе
		ТекущиеДанные				= Элементы.БанковскиеСчета.ТекущиеДанные;
		ТекущиеДанные.ФайлОбмена	= ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьИнтеллектуально(Команда)
	
	Для каждого СтрокаДокумента Из Объект.ДокументыКВыгрузке Цикл
		СтрокаДокумента.Выгружать = Не СтрокаДокумента.Оплачено;
	КонецЦикла;
	
	ОбновитьПодвал();
	
КонецПроцедуры

#КонецОбласти

СуммыПлатежейКВыгрузке	= Новый Соответствие;