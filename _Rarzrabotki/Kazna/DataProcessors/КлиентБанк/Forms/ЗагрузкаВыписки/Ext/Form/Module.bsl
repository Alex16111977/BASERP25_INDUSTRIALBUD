
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуСчетов();
	
	Объект.СоздаватьКонтрагентов	= Истина;
	
	Если Параметры.Свойство("БанковскийСчет")
		И ЗначениеЗаполнено(Параметры.БанковскийСчет) Тогда
		
		СтрокиТаблицы = Объект.БанковскиеСчета.НайтиСтроки(Новый Структура("Ссылка", Параметры.БанковскийСчет));
		Если СтрокиТаблицы.Количество() Тогда
			СтрокиТаблицы[0].Пометка = Истина;
			Объект.СписокСчетов.Добавить(СтрокиТаблицы[0].Ссылка);
			ИндексСтрокиСчета = Объект.БанковскиеСчета.Индекс(СтрокиТаблицы[0]);
		КонецЕсли;
	КонецЕсли;
		
		
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.СписокСчетов.Количество() = 1 Тогда
		БанковскийСчет = Объект.СписокСчетов[0].Значение;
		Элементы.БанковскиеСчета.ТекущаяСтрока = ИндексСтрокиСчета;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура БанковскиеСчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.БанковскиеСчета.ТекущиеДанные;
	
	Если Поле.Имя = "БанковскиеСчетаСсылка" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаПометкаПриИзменении(Элемент)
	
	Объект.СписокСчетов.Очистить();
	Для каждого Счет из Объект.БанковскиеСчета Цикл
		Если Счет.Пометка Тогда
			Счет.Загружается = Ложь;
			Объект.СписокСчетов.Добавить(Счет.Ссылка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя = "СтраницаЗагруженныеДокументы" Тогда
		Если Объект.СписокСчетов.Количество() = 1 Тогда
			Если БанковскийСчетОтбор <> Объект.СписокСчетов[0].Значение Тогда
				БанковскийСчетОтбор = Объект.СписокСчетов[0].Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетОтборПриИзменении(Элемент)
	
	БанковскийСчетОтборПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура БанковскийСчетОтборПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(БанковскийСчетОтбор) Тогда
		Отбор	= Новый ФиксированнаяСтруктура("СчетОрганизации", БанковскийСчетОтбор); 
		Элементы.ДокументыКЗагрузке.ОтборСтрок	= Отбор;
	Иначе
		Элементы.ДокументыКЗагрузке.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСчетов()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаполнитьТаблицуСчетовЗагрузки();
	ЗначениеВРеквизитФОрмы(Обработка, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлНастройкиОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
		
	#Если ВебКлиент Тогда
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если Не РасширениеПодключено Тогда 
		Предупреждение("В Веб-клиенте без установленного расширения работы с файлами добавление файлов не поддерживается.");
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                  = НСтр("Файл настройки (*.epf)|*epf'");
	ДиалогВыбораФайла.Заголовок               = "Выберите файл настроек для выгрузки данных";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "epf";
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла          = ФайлНастройкиОбмена;

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыборФайлаНастроекЗавершение",
		ЭтотОбъект,
		Новый Структура(""));
	
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаНастроекЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	Иначе
		ФайлНастройкиОбмена = ВыбранныеФайлы[0];
		
		ВыборФайлаНастроекЗавершениеНаСервере = Новый ОписаниеОповещения("ВыборФайлаНастроекЗавершениеНаСервере", ЭтотОбъект);
		НачатьПомещениеФайла(ВыборФайлаНастроекЗавершениеНаСервере, АдресХранилищаФайлаНастроек, ФайлНастройкиОбмена, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыборФайлаНастроекЗавершениеНаСервере(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры)
	
	АдресХранилищаФайлаНастроек		= Адрес;
	ИмяФайлаВнешнейОбработкиНастроек= ВнешниеОбработки.Подключить(Адрес, , Ложь);
	ВнешняяОбработкаНастроек		= ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработкиНастроек);
	//СписокРасширений				= ВнешняяОбработкаНастроек.ВернутьСписокПодерживаемыхРасширений();	
		
КонецПроцедуры

&НаКлиенте
Процедура ФайлОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	#Если ВебКлиент Тогда
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если Не РасширениеПодключено Тогда 
		Предупреждение("В Веб-клиенте без установленного расширения работы с файлами добавление файлов не поддерживается.");
		Возврат;
	КонецЕсли;
	#КонецЕсли
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Заголовок               = "Выберите файл для выгрузки данных";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла          = ФайлОбмена;

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыборФайлаЗавершение",
		ЭтотОбъект,
		Новый Структура(""));
	
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	Иначе
		ФайлОбмена	= ВыбранныеФайлы[0];
		
		//ВыборФайлаЗавершениеНаСервере = Новый ОписаниеОповещения("ВыборФайлаЗавершениеНаСервере", ЭтотОбъект);
		
		ДвоичныеДанные	= Новый ДвоичныеДанные(ФайлОбмена);
		
		АдресХранилищаФайла	= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		//НачатьПомещениеФайла(ВыборФайлаЗавершениеНаСервере, , ФайлОбмена, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыборФайлаЗавершениеНаСервере(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры)
	
	АдресХранилищаФайла	= Адрес;
	
КонецПроцедуры

&НаСервере
Функция СоздатьСтрокуФильтра(СписокРасширений)
	
	СтрокаФильтра = "";
	
	Для Каждого Расширение Из СписокРасширений Цикл
		БазоваяСтрока	= "%описание (*.%расширение)|*%расширение";
		БазоваяСтрока   = СтрЗаменить(БазоваяСтрока, "%описание", Расширение.Представление);
		БазоваяСтрока   = СтрЗаменить(БазоваяСтрока, "%расширение", Расширение.Значение);
		СтрокаФильтра	= СтрокаФильтра + ?(ПустаяСтрока(СтрокаФильтра), "", "|") + БазоваяСтрока;
	КонецЦикла;
	
	Возврат СтрокаФильтра;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДокументы(Команда)
	
	Объект.ДокументыКЗагрузке.Очистить();
	
	СтруктураОтбора	= Новый Структура("Пометка", Истина);
	МассивСчетовКЗагрузке = Объект.БанковскиеСчета.НайтиСтроки(СтруктураОтбора);
	Для Каждого СтрокаБанковскогоСчета Из МассивСчетовКЗагрузке Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаБанковскогоСчета.НастройкаОбменаСБанком) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для счета %1 не указана настройка обмена", СтрокаБанковскогоСчета.Ссылка));
			Продолжить;	
		КонецЕсли;
		
		Если ПустаяСтрока(СтрокаБанковскогоСчета.ФайлОбмена) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для счета %1 не указан файл обмена", СтрокаБанковскогоСчета.Ссылка));
			Продолжить;	
		КонецЕсли;
		
		СтруктураДанныхДляЗагрузки	= Новый Структура("НомерСчета, Организация, Ссылка, Пометка");
		ЗаполнитьЗначенияСвойств(СтруктураДанныхДляЗагрузки, СтрокаБанковскогоСчета);
		
		ДвоичныеДанные	= Новый ДвоичныеДанные(СтрокаБанковскогоСчета.ФайлОбмена);
		АдресХранилищаФайлаОбмена	= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		
		ЗагрузитьДокументыНаСервере(СтруктураДанныхДляЗагрузки, АдресХранилищаФайлаОбмена);
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДокументыНаСервере(СтруктураДанныхДляЗагрузки, АдресХранилищаФайлаОбмена)
	
	
	СсылкаНаОбработкуИмпорта	= ПолучитьНавигационнуюСсылку(СтруктураДанныхДляЗагрузки.Ссылка.НастройкаОбменаСБанком, "ОбработкаИмпорта");
	
	ИмяФайлаВнешнейОбработкиНастроек	= ВнешниеОбработки.Подключить(СсылкаНаОбработкуИмпорта, , Ложь);
	ВнешняяОбработкаНастроек			= ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработкиНастроек);
	
	РеквизитОбъект						= РеквизитФормыВЗначение("Объект");
	ТаблицаДанных						= ВнешняяОбработкаНастроек.ЗагрузитьИзФайла(АдресХранилищаФайлаОбмена, СтруктураДанныхДляЗагрузки, РеквизитОбъект.ДокументыКЗагрузке.ВыгрузитьКолонки());	
	
	Если ТипЗнч(ТаблицаДанных) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТаблицаДанных);
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ТаблицаДанных Цикл
		ЗаполнитьЗначенияСвойств(Объект.ДокументыКЗагрузке.Добавить(), Строка);
	КонецЦикла;
	
	ЗагрузитьДокументыЗаполнитьРеквизиты();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗагруженныеДокументы;
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьДокументыЗаполнитьРеквизиты()
	
	Для Каждого Строка Из Объект.ДокументыКЗагрузке Цикл
		Строка.Контрагент 		= НайтиСоздатьКонтрагента(Строка.КонтрагентНаименование, Строка.КонтрагентЕГРПОУ);
		Строка.КонтрагентСчет	= НайтиСоздатьБанковскийСчетКонтрагента(Строка.КонтрагентНомерСчета, Строка.КонтрагентМФОБанка, Строка.Контрагент);
		Строка.Валюта			= Справочники.Валюты.НайтиПоКоду(Строка.ВалютаКод);
		
		Строка.Документ			= НайтиСоздатьДокумент(Строка, Строка.ОрганизацияСчет);	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиСоздатьДокумент(ДанныеДляЗаполнения, БанковскийСчет)
	
	
	Документ = НайтиДокумент(ДанныеДляЗаполнения, БанковскийСчет);
	
	Если Не Документ = Неопределено Тогда
		ДокументОбъект				= Документ.ПолучитьОбъект();
		ДокументОбъект.Оплачено 	= Истина;
		ДокументОбъект.ДатаОплаты	= ДанныеДляЗаполнения.ДатаПлатежа; 
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокументОбъект.Записать();
		КонецПопытки;
		
		Возврат ДокументОбъект.Ссылка;
		
	Иначе
		
		НовыйДокумент	= ?(ДанныеДляЗаполнения.ИсходящийДокумент, Документы.ПлатежноеПоручениеИсходящее.СоздатьДокумент(), Документы.ПлатежноеПоручениеВходящее.СоздатьДокумент());
		
		НовыйДокумент.Дата				= ДанныеДляЗаполнения.ДатаДокумента;
		НовыйДокумент.ВалютаДокумента	= ДанныеДляЗаполнения.Валюта;
		НовыйДокумент.Организация		= ДанныеДляЗаполнения.Организация;
		НовыйДокумент.СчетОрганизации	= ДанныеДляЗаполнения.ОрганизацияСчет;
		
		НовыйДокумент.Контрагент		= ДанныеДляЗаполнения.Контрагент;
		НовыйДокумент.СчетКонтрагента	= ДанныеДляЗаполнения.КонтрагентСчет;
		НовыйДокумент.ДатаЗагрузки		= ТекущаяДата();
		НовыйДокумент.НазначениеПлатежа	= ДанныеДляЗаполнения.НазначениеПлатежа;
		НовыйДокумент.СуммаДокумента	= ?(ДанныеДляЗаполнения.СуммаПлатежа < 0 , -ДанныеДляЗаполнения.СуммаПлатежа, ДанныеДляЗаполнения.СуммаПлатежа);
		НовыйДокумент.ДатаОплаты		= ДанныеДляЗаполнения.ДатаПлатежа;
		НовыйДокумент.БанковскийНомер	= СокрЛП(ДанныеДляЗаполнения.НомерДокумента);
		
		Если Не ДанныеДляЗаполнения.ИсходящийДокумент Тогда
			Сделка = ПоискСделки(ДанныеДляЗаполнения);
			Если Не Сделка = Неопределено Тогда
				НовыйДокумент.ДоговорКонтрагента = Сделка;
			КонецЕсли;
		КонецЕсли;
		
		//Слав нач
		Если НовыйДокумент.Метаданные() = Метаданные.Документы.ПлатежноеПоручениеИсходящее Тогда
			Если НовыйДокумент.Дата > Дата('20170701000000') И (Найти(НовыйДокумент.НазначениеПлатежа, "Сплата комiсiї банку") > 0)  Тогда
				
				СтатьяДДДС_GUID = Новый УникальныйИдентификатор("3d66d73a-21a0-11e6-80c4-000c29bbac23");
				СсылкаСтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьСсылку(СтатьяДДДС_GUID);
				НовыйДокумент.СтатьяДвиженияДенежныхСредств = СсылкаСтатьяДДС;
				
				Подразделение_GUID = Новый УникальныйИдентификатор("c41b2d32-f74e-11e5-80c1-000c29bbac23");
				СсылкаПодразделение = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьСсылку(Подразделение_GUID);
				НовыйДокумент.Подразделение = СсылкаПодразделение;
				
			КонецЕсли;	
		КонецЕсли;	
		//Слав кон
		
		НовыйДокумент.Записать();
		
		Если Объект.ПроводитьДокументы Тогда
			Попытка
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		Возврат НовыйДокумент.Ссылка;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПоискСделки(ДанныеПлатежа)
	
	ИНН	= СокрЛП(ДанныеПлатежа.КонтрагентЕГРПОУ);
	
	Если СтрДлина(ИНН) = 8 Тогда
		Возврат ПоискСделкиПриПлатежеЧерезКассуБанка(ДанныеПлатежа);
	ИначеЕсли СтрДлина(ИНН) = 10 Тогда
		Возврат ПоискСделкиПриПлатежеСоСвоегоСчета(ДанныеПлатежа);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПоискСделкиПриПлатежеЧерезКассуБанка(ДанныеПлатежа)
	
	Возврат ПоискСделкиПоКомментарию(ДанныеПлатежа.НазначениеПлатежа);
	
КонецФункции

&НаСервере
Функция ПоискСделкиПриПлатежеСоСвоегоСчета(ДанныеПлатежа)
	
	//1.Обрабатываем самый простой вариант - у клиента всего одна сдела
	Сделка	= ПоискСделкиПоИННКлиента(ДанныеПлатежа.КонтрагентЕГРПОУ);
	
	//Ищем номер сделки в комментарии
	Если Сделка = Неопределено Тогда
		СписокСделок	= ПоискНомераСделкиВКомментарии(ДанныеПлатежа.НазначениеПлатежа);
		Для Каждого НомерСделки Из СписокСделок Цикл
			Сделка	= ПоискСделкиПоНомеру(НомерСделки);
			Если Не Сделка = Неопределено Тогда
				Возврат Сделка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Сделка;
	
КонецФункции

&НаСервере
Функция ПоискСделкиПоКомментарию(НазначениеПлатежа)
	
	СписокИНН	= ПоискИННВКомментарии(НазначениеПлатежа);
	
	Если СписокИНН.Количество() = 0 Тогда
		СписокСделок	= ПоискНомераСделкиВКомментарии(НазначениеПлатежа);
		Для Каждого НомерСделки Из СписокСделок Цикл
			Сделка	= ПоискСделкиПоНомеру(НомерСделки);
			Если Не Сделка = Неопределено Тогда
				Возврат Сделка;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого ИНН Из СписокИНН Цикл
			Сделка	= ПоискСделкиПоИННКлиента(ИНН);
			Если Не Сделка = Неопределено Тогда
				Возврат Сделка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

&НаСервере
Функция ПоискНомераСделкиВКомментарии(НазначениеПлатежа)

	МассивНайденныхСтрок	= Новый Массив;
	СписокМасок				= АХ_ОбщегоНазначения.ПолучитьСписокМасокДляПоискаДанныхВСтроке(Перечисления.ТипыМаскиДляПоискаВСтроке.НомерДоговора);
	
	Для Каждого Маска Из СписокМасок Цикл
		ПараметрыРазбора	= Новый Структура();
		ПараметрыРазбора.Вставить("Global"	, Истина);
		ПараметрыРазбора.Вставить("Pattern", Маска);
		АХ_ОбщегоНазначения.РазборСтрокиПоРегулярномуВыражению(НазначениеПлатежа,, ПараметрыРазбора,, МассивНайденныхСтрок)
	КонецЦикла;
	
	Возврат МассивНайденныхСтрок;
	
КонецФункции

&НаСервере
Функция ПоискИННВКомментарии(НазначениеПлатежа)
	
	МассивНайденныхСтрок	= Новый Массив;
	СписокМасок				= АХ_ОбщегоНазначения.ПолучитьСписокМасокДляПоискаДанныхВСтроке(Перечисления.ТипыМаскиДляПоискаВСтроке.ИНН);
	
	Для Каждого Маска Из СписокМасок Цикл
		ПараметрыРазбора	= Новый Структура();
		ПараметрыРазбора.Вставить("Global"	, Истина);
		ПараметрыРазбора.Вставить("Pattern", Маска);
		АХ_ОбщегоНазначения.РазборСтрокиПоРегулярномуВыражению(НазначениеПлатежа,, ПараметрыРазбора,, МассивНайденныхСтрок)
	КонецЦикла;
	
	Возврат МассивНайденныхСтрок;
	
КонецФункции

&НаСервере
Функция ПоискСделкиПоИННКлиента(ИНН)
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	Запрос.Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	            	  |	ДоговорыКонтрагентов.Ссылка
	            	  |ИЗ
	            	  |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	            	  |		ПО ДоговорыКонтрагентов.Владелец = Контрагенты.Ссылка
	            	  |			И (Контрагенты.ЕГРПОУ = &ИНН)
	            	  |			И (Контрагенты.ФизЮрЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо))
	            	  |ГДЕ
	            	  |	Контрагенты.ЕГРПОУ = &ИНН
	            	  |	И Контрагенты.ФизЮрЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)";
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Количество() = 1 Тогда
		РезультатЗапроса.Следующий();
		Возврат РезультатЗапроса.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
					  
КонецФункции

&НаСервере
Функция ПоискСделкиПоНомеру(НомерСделки)
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Номер", НомерСделки);
	
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	ДоговорыКонтрагентов.Ссылка
	            	  |ИЗ
	            	  |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	            	  |ГДЕ
	            	  |	ДоговорыКонтрагентов.Номер = &Номер";
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Количество() = 1 Тогда
		РезультатЗапроса.Следующий();
		Возврат РезультатЗапроса.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
					  
КонецФункции	

&НаСервере
Функция НайтиДокумент(ПараметрыДокумента, БанковскийСчет)
	
	Возврат ?(ПараметрыДокумента.ИсходящийДокумент, ПоискаППИ(ПараметрыДокумента, БанковскийСчет), ПоискаППВ(ПараметрыДокумента, БанковскийСчет));					  
		
КонецФункции

&НаСервере
Функция ПоискаППВ(ПараметрыДокумента, БанковскийСчет)
	
	СуммаДокумента	= ?(ПараметрыДокумента.СуммаПлатежа < 0 , -ПараметрыДокумента.СуммаПлатежа, ПараметрыДокумента.СуммаПлатежа);
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Дата"			, ПараметрыДокумента.ДатаДокумента);
	Запрос.УстановитьПараметр("Организация"		, ПараметрыДокумента.Организация);
	Запрос.УстановитьПараметр("Контрагент"		, ПараметрыДокумента.Контрагент);
	Запрос.УстановитьПараметр("БанковскийНомер"	, СокрЛП(ПараметрыДокумента.НомерДокумента));
	Запрос.УстановитьПараметр("СуммаДокумента"	, СуммаДокумента);
	Запрос.УстановитьПараметр("ОплатаКомиссии"	, СуммаДокумента = БанковскийСчет.СуммаКомиссии);
	
	Запрос.Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	            	  |	Таблица.Ссылка
	            	  |ИЗ
	            	  |	Документ.ПлатежноеПоручениеВходящее КАК Таблица
	            	  |ГДЕ
	            	  |	ВЫБОР
	            	  |			КОГДА НЕ &ОплатаКомиссии
	            	  |				ТОГДА Таблица.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, -3) И КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	            	  |			ИНАЧЕ Таблица.Дата = &Дата
	            	  |					И Таблица.БанковскийНомер = &БанковскийНомер
	            	  |		КОНЕЦ
	            	  |	И Таблица.БанковскийНомер = &БанковскийНомер
	            	  |	И Таблица.СуммаДокумента = &СуммаДокумента
	            	  |	И НЕ Таблица.ПометкаУдаления
	            	  |	И Таблица.Организация = &Организация";
	
	  РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	  
	  Если РезультатЗапроса.Следующий() Тогда
		  Возврат РезультатЗапроса.Ссылка;
	  Иначе
		  Возврат Неопределено;
	  КонецЕсли;
	  	
КонецФункции

&НаСервере
Функция ПоискаППИ(ПараметрыДокумента, БанковскийСчет)
	
	СуммаДокумента	= ?(ПараметрыДокумента.СуммаПлатежа < 0 , -ПараметрыДокумента.СуммаПлатежа, ПараметрыДокумента.СуммаПлатежа);
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Дата"			, ПараметрыДокумента.ДатаДокумента);
	Запрос.УстановитьПараметр("Организация"		, ПараметрыДокумента.Организация);
	Запрос.УстановитьПараметр("Контрагент"		, ПараметрыДокумента.Контрагент);
	Запрос.УстановитьПараметр("БанковскийНомер"	, СокрЛП(ПараметрыДокумента.НомерДокумента));
	Запрос.УстановитьПараметр("СуммаДокумента"	, СуммаДокумента);
	Запрос.УстановитьПараметр("ОплатаКомиссии"	, СуммаДокумента = БанковскийСчет.СуммаКомиссии);
	
	Запрос.Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	            	  |	Таблица.Ссылка,
	            	  |	Таблица.НазначениеПлатежа
	            	  |ИЗ
	            	  |	Документ.ПлатежноеПоручениеИсходящее КАК Таблица
	            	  |ГДЕ
	            	  |	ВЫБОР
	            	  |			КОГДА НЕ &ОплатаКомиссии
	            	  |				ТОГДА Таблица.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, -3) И КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	            	  |			ИНАЧЕ Таблица.Дата = &Дата
	            	  |					И Таблица.БанковскийНомер = &БанковскийНомер
	            	  |		КОНЕЦ
	            	  |	И Таблица.Контрагент = &Контрагент
	            	  |	И Таблица.СуммаДокумента = &СуммаДокумента
	            	  |	И НЕ Таблица.ПометкаУдаления
	            	  |	И Таблица.БанковскийНомер = """"
	            	  |	И Таблица.Организация = &Организация
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	            	  |	Таблица.Ссылка
	            	  |ИЗ
	            	  |	Документ.ПлатежноеПоручениеИсходящее КАК Таблица
	            	  |ГДЕ
	            	  |	ВЫБОР
	            	  |			КОГДА НЕ &ОплатаКомиссии
	            	  |				ТОГДА Таблица.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, -3) И КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	            	  |			ИНАЧЕ Таблица.Дата = &Дата
	            	  |		КОНЕЦ
	            	  |	И Таблица.БанковскийНомер = &БанковскийНомер
	            	  |	И Таблица.Контрагент = &Контрагент
	            	  |	И Таблица.СуммаДокумента = &СуммаДокумента
	            	  |	И НЕ Таблица.ПометкаУдаления
	            	  |	И Таблица.Организация = &Организация";
	
	РезультатЗапроса	= Запрос.ВыполнитьПакет();
	ОбщийРезультат		= РезультатЗапроса[0].Выбрать();
	УточненныйРезультат	=  РезультатЗапроса[1].Выбрать();

  	Если  ОбщийРезультат.Количество() < 2 И ОбщийРезультат.Следующий() Тогда
		Возврат ОбщийРезультат.Ссылка;
	КонецЕсли;
	
	Если УточненныйРезультат.Следующий() Тогда
		Возврат УточненныйРезультат.Ссылка;
	КонецЕсли;  
	
	Пока ОбщийРезультат.Следующий() Цикл  
		НазначениеПлатежа = СтрЗаменить(СтрЗаменить(ОбщийРезультат.НазначениеПлатежа, "і", "i"), "І", "I") ;
		Если СтрЗаменить(НазначениеПлатежа,"№","N") = ПараметрыДокумента.НазначениеПлатежа Тогда 
			Возврат ОбщийРезультат.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат Неопределено;
	
КонецФункции


&НаСервере
Функция НайтиСоздатьБанковскийСчетКонтрагента(НомерСчета, МФОБанка, Контрагент)
	
	НайденныйСчет = НайтиБанковскийСчет(НомерСчета, МФОБанка, Контрагент);
	
	Если НайденныйСчет <> Неопределено Тогда
		Возврат НайденныйСчет;
	Иначе
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Возврат СоздатьБанковскийСчетКонтрагентаНаСервере(НомерСчета, МФОБанка, Контрагент);
		Иначе
			Возврат Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СоздатьБанковскийСчетКонтрагентаНаСервере(НомерСчета, МФОБанка, Контрагент)
	
	НовыйБанковскийСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
	
	НовыйБанковскийСчет.Наименование	= НомерСчета;
	НовыйБанковскийСчет.НомерСчета		= НомерСчета;
	НовыйБанковскийСчет.Владелец		= Контрагент;
	НовыйБанковскийСчет.Банк			= НайтиСоздатьБанк(МФОБанка);
	
	НовыйБанковскийСчет.Записать();
	
	Возврат НовыйБанковскийСчет.Ссылка;
	
	
КонецФункции

&НаСервере
Функция НайтиСоздатьБанк(МФОБанка)
	
	НайденныйБанк = НайтиБанк(МФОБанка);
	
	Если НайденныйБанк <> Неопределено Тогда
		Возврат НайденныйБанк;			
	Иначе
		Возврат СоздатьБанкНаСервере(МФОБанка);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СоздатьБанкНаСервере(МФО)
	
	НовыйБанк	= Справочники.Банки.СоздатьЭлемент();
	
	НовыйБанк.Наименование	= МФО;
	НовыйБанк.Код	= МФО;
	
	НовыйБанк.Записать();
	
	Возврат НовыйБанк.Ссылка;
	
КонецФункции

&НаСервере
Функция НайтиБанк(МФОБанка)
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МФО"	, МФОБанка);
	
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Банки.Ссылка
	            	  |ИЗ
	            	  |	Справочник.Банки КАК Банки
	            	  |ГДЕ
	            	  |	Банки.МФО = &МФО";
					  
	РезультатЗапроса	= Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

&НаСервере
Функция НайтиБанковскийСчет(НомерСчета, МФОБанка, Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НомерСчета"	, НомерСчета);
	Запрос.УстановитьПараметр("МФО"	, МФОБанка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.НомерСчета = &НомерСчета
	|	И БанковскиеСчета.Банк.МФО = &МФО
	|	И &СсылкаТипВладельца";
	
	//++ Павел Чистяков 18.08.2016 17:01:27 №194
	Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Организации") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СсылкаТипВладельца", "БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации");
	ИначеЕсли ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СсылкаТипВладельца", "БанковскиеСчета.Владелец ССЫЛКА Справочник.Контрагенты");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &СсылкаТипВладельца", "");
	КонецЕсли;
	//-- Павел Чистяков 18.08.2016 17:01:27 №194
					  
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

&НаСервере
Функция НайтиСоздатьКонтрагента(Наименование, КодЕГРПОУ)
	
	НайденныйКонтрагент = НайтиКонтрагента(Наименование, КодЕГРПОУ);
	
	Если НайденныйКонтрагент <> Неопределено Тогда
		Возврат НайденныйКонтрагент;			
	Иначе
		Если Объект.СоздаватьКонтрагентов Тогда
			Возврат СоздатьКонтрагента(Наименование, КодЕГРПОУ);
		Иначе
			Возврат Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

КонецФункции

&НаСервере
Функция СоздатьКонтрагента(Наименование, КодЕГРПОУ)
	
	НовыйКонтрагент	= Справочники.Контрагенты.СоздатьЭлемент();
	
	НовыйКонтрагент.Наименование	= Наименование;
	НовыйКонтрагент.ЕГРПОУ			= СокрЛП(КодЕГРПОУ);
	
	НовыйКонтрагент.Записать();
	
	Возврат НовыйКонтрагент.Ссылка;
	
КонецФункции

&НаСервере
Функция НайтиКонтрагента(НаименованиеКонтрагента, КодЕГРПОУ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование"	, НаименованиеКонтрагента);
	Запрос.УстановитьПараметр("ЕГРПОУ"			, СокрЛП(КодЕГРПОУ));
	
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Контрагенты.Ссылка
	            	  |ИЗ
	            	  |	Справочник.Контрагенты КАК Контрагенты
	            	  |ГДЕ
	            	  |	Контрагенты.ЕГРПОУ = &ЕГРПОУ
	            	  |
	            	  |ОБЪЕДИНИТЬ ВСЕ
	            	  |
	            	  |ВЫБРАТЬ
	            	  |	Контрагенты.Ссылка
	            	  |ИЗ
	            	  |	Справочник.Контрагенты КАК Контрагенты
	            	  |ГДЕ
	            	  |	Контрагенты.Наименование = &Наименование";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПровестиНаСервере()

	ТекущиеДанные	= Элементы.ДокументыКЗагрузке.ТекущиеДанные;
	
	ДокументСсылка	= ТекущиеДанные.Документ;
	
	ДокументОбъект	= ДокументСсылка.ПолучитьОбъект();
	
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	ПровестиНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьКонтрагентаНаСервере()

	ТекущиеДанные	= Элементы.ДокументыКЗагрузке.ТекущиеДанные;
	
	НовыйКонтрагент	= СоздатьКонтрагента(ТекущиеДанные.КонтрагентНаименование, ТекущиеДанные.КонтрагентЕГРПОУ); 
	
	ЗаполнениеКонтрагента(ТекущиеДанные.КонтрагентНаименование, НовыйКонтрагент);	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеКонтрагента(КонтрагентНаименование, Контрагент)
	
	ОтборСтрок = Новый Структура("КонтрагентНаименование", КонтрагентНаименование);
	МассивСтрок	= Объект.ДокументыКЗагрузке.НайтиСтроки(ОтборСтрок);
	
	Для Каждого Строка Из МассивСтрок Цикл
		Строка.Контрагент	= Контрагент;
		ДокументОбъект		= Строка.Документ.ПолучитьОбъект();
		ДокументОбъект.Контрагент	= Контрагент;
		ДокументОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтрагентаКлиент(Команда)
	СоздатьКонтрагентаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКонтрагента(Команда)
		
	ТекущиеДанные = Элементы.ЗагруженныеДокументы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Контрагент    = ТекущиеДанные.Контрагент;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"В документе %1 уже задан контрагент %2. Его можно изменить в форме документа.",
				ТекущиеДанные.Документ,
				ТекущиеДанные.Контрагент);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ТекущиеДанные.Документ,
			,
			Элементы.ДокументыКЗагрузке.Имя);
	Иначе
		
		ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", , Элементы.ДокументыКЗагрузке);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДокументыКЗагрузкеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		ТекущиеДанные = Элементы.ДокументыКЗагрузке.ТекущиеДанные;
		
		ЗаполнениеКонтрагента(ТекущиеДанные.КонтрагентНаименование, ВыбранноеЗначение);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаФайлОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	#Если ВебКлиент Тогда
		РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
		Если Не РасширениеПодключено Тогда 
			Предупреждение("В Веб-клиенте без установленного расширения работы с файлами добавление файлов не поддерживается.");
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	СтруктураФильтра	= ПолучитьСтруктуруФильтровРасширенийДляБанковскогоСчета(Элементы.БанковскиеСчета.ТекущиеДанные.Ссылка);
	
	Если СтруктураФильтра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла	= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                  = СтруктураФильтра.СтрокаФильтра;
	ДиалогВыбораФайла.Заголовок               = "Выберите файл для выгрузки данных";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = СтруктураФильтра.СписокРасширений[0].Значение;
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла          = ФайлОбмена;

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"БанковскиеСчетаВыборФайлаЗавершение",
		ЭтотОбъект,
		Новый Структура(""));
	
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);

КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруФильтровРасширенийДляБанковскогоСчета(БанковскийСчет)
	
	СсылкаНаОбработкуИмпорта	= ПолучитьНавигационнуюСсылку(БанковскийСчет.НастройкаОбменаСБанком, "ОбработкаИмпорта");
	
	ИмяФайлаВнешнейОбработкиНастроек	= ВнешниеОбработки.Подключить(СсылкаНаОбработкуИмпорта, , Ложь);
	ВнешняяОбработкаНастроек			= ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработкиНастроек);
	СписокРасширений					= ВнешняяОбработкаНастроек.ВернутьСписокПодерживаемыхРасширений();	

	Возврат Новый Структура("СписокРасширений, СтрокаФильтра", СписокРасширений , СоздатьСтрокуФильтра(СписокРасширений));
	
КонецФункции

&НаКлиенте
Процедура БанковскиеСчетаВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	Иначе
		ТекущиеДанные				= Элементы.БанковскиеСчета.ТекущиеДанные;
		ТекущиеДанные.ФайлОбмена	= ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКЗагрузкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(, Элементы.ДокументыКЗагрузке.ТекущиеДанные.Документ);
	
КонецПроцедуры

