
&НаСервере
Процедура ЗаписатьПраваНаСервере()

	ЗаписатьПраваВРегистрДоступа();	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПрава()
	
	ЗаписатьПраваНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаПравДоступаОрганизации.Загрузить(ВернутьТаблицуЗаписейРегистраПравДоступа(Перечисления.ВидыОбъектовДоступа.Организации));
	
	ТаблицаПравДоступаБанковскиеСчета.Загрузить(ВернутьТаблицуЗаписейРегистраПравДоступа(Перечисления.ВидыОбъектовДоступа.БанковскиеСчета));

	ТаблицаПравДоступаПодразделения.Загрузить(ВернутьТаблицуЗаписейРегистраПравДоступа(Перечисления.ВидыОбъектовДоступа.Подразделения));
	
	ТаблицаПравДоступаКассы.Загрузить(ВернутьТаблицуЗаписейРегистраПравДоступа(Перечисления.ВидыОбъектовДоступа.Кассы));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьПрава();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьПрава();
	
КонецПроцедуры

#Область Служебные

&НаСервере
Процедура ЗаписатьПраваВРегистрДоступа()
	
	ОчиститьЗаписиРегистраПравДоступа();
	
	Для Каждого Строка Из ТаблицаПравДоступаОрганизации Цикл
		ОбработатьСтрокуТаблицыПравДоступаПоОрганизациям(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаПравДоступаБанковскиеСчета Цикл
		ОбработатьСтрокуТаблицыПравДоступаПоБанковскомуСчету(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаПравДоступаПодразделения Цикл
		ОбработатьСтрокуТаблицыПравДоступаПоПодразделениям(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаПравДоступаКассы Цикл
		ОбработатьСтрокуТаблицыПравДоступаПоКассам(Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТаблицыПравДоступаПоОрганизациям(СтрокаТаблицыПравДоступа)
	
	//УдалитьЗаписиДляНаследников(СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Объект);
		
	ТаблицаПрав	= СоздатьТаблицуОрганизацийПоИерархииДоступа(СтрокаТаблицыПравДоступа.ОбъектДоступа, СтрокаТаблицыПравДоступа.ВидНаследованияПравДоступаИерархическихСправочников);
	
	ЗаписатьТаблицуПравВРегистрНастроекДоступа(ТаблицаПрав, СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Чтение, СтрокаТаблицыПравДоступа.Запись, Перечисления.ВидыОбъектовДоступа.Организации);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТаблицыПравДоступаПоБанковскомуСчету(СтрокаТаблицыПравДоступа)
			
	ТаблицаПрав	= Новый ТаблицаЗначений;
	ТаблицаПрав.Колонки.Добавить("ОбъектДоступа");
	ТаблицаПрав.Колонки.Добавить("ВидНаследованияПравДоступаИерархическихСправочников");
	ТаблицаПрав.Колонки.Добавить("ВладелецПравДоступа");
	
	НоваяСтрока														= ТаблицаПрав.Добавить();
	НоваяСтрока.ОбъектДоступа										= СтрокаТаблицыПравДоступа.ОбъектДоступа;
	НоваяСтрока.ВидНаследованияПравДоступаИерархическихСправочников	= Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.ТолькоДляТекущегоПрава;
	НоваяСтрока.ВладелецПравДоступа									= Справочники.БанковскиеСчета.ПустаяСсылка();
	
	ЗаписатьТаблицуПравВРегистрНастроекДоступа(ТаблицаПрав, СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Чтение, СтрокаТаблицыПравДоступа.Запись, Перечисления.ВидыОбъектовДоступа.БанковскиеСчета);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТаблицыПравДоступаПоПодразделениям(СтрокаТаблицыПравДоступа)
	
	//УдалитьЗаписиДляНаследников(СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Объект);
		
	ТаблицаПрав	= СоздатьТаблицуПодразделенийПоИерархииДоступа(СтрокаТаблицыПравДоступа.ОбъектДоступа, СтрокаТаблицыПравДоступа.ВидНаследованияПравДоступаИерархическихСправочников);
	
	ЗаписатьТаблицуПравВРегистрНастроекДоступа(ТаблицаПрав, СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Чтение, СтрокаТаблицыПравДоступа.Запись, Перечисления.ВидыОбъектовДоступа.Подразделения);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТаблицыПравДоступаПоКассам(СтрокаТаблицыПравДоступа)
	
	ТаблицаПрав	= Новый ТаблицаЗначений;
	ТаблицаПрав.Колонки.Добавить("ОбъектДоступа");
	ТаблицаПрав.Колонки.Добавить("ВидНаследованияПравДоступаИерархическихСправочников");
	ТаблицаПрав.Колонки.Добавить("ВладелецПравДоступа");
	
	НоваяСтрока														= ТаблицаПрав.Добавить();
	НоваяСтрока.ОбъектДоступа										= СтрокаТаблицыПравДоступа.ОбъектДоступа;
	НоваяСтрока.ВидНаследованияПравДоступаИерархическихСправочников	= Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.ТолькоДляТекущегоПрава;
	НоваяСтрока.ВладелецПравДоступа									= Справочники.Кассы.ПустаяСсылка();
	
	ЗаписатьТаблицуПравВРегистрНастроекДоступа(ТаблицаПрав, СтрокаТаблицыПравДоступа.Пользователь, СтрокаТаблицыПравДоступа.Чтение, СтрокаТаблицыПравДоступа.Запись, Перечисления.ВидыОбъектовДоступа.Кассы);
	
КонецПроцедуры

&НаСервере
Функция СоздатьТаблицуОрганизацийПоИерархииДоступа(Организация, ВидНаследованияПравДоступаИерархическихСправочников)
	
	Запрос			= Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Организация);
	Запрос.УстановитьПараметр("ВидНаследованияПравДоступаИерархическихСправочников", ВидНаследованияПравДоступаИерархическихСправочников);
	ТекстЗапроса	= "ВЫБРАТЬ
	            	  |	Организации.Ссылка КАК ОбъектДоступа,
	            	  |	ВЫБОР
	            	  |		КОГДА Организации.Ссылка = &Ссылка
	            	  |			ТОГДА &ВидНаследованияПравДоступаИерархическихСправочников
	            	  |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыНаследованияПравДоступаИерархическихСправочников.НаследуетсяОтРодителя)
	            	  |	КОНЕЦ КАК ВидНаследованияПравДоступаИерархическихСправочников,
	            	  |	ВЫБОР
	            	  |		КОГДА Организации.Ссылка = &Ссылка
	            	  |			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	            	  |		ИНАЧЕ &ссылка
	            	  |	КОНЕЦ КАК ВладелецПравДоступа
	            	  |ИЗ
	            	  |	Справочник.Организации КАК Организации
	            	  |ГДЕ
	            	  |	Организации.Ссылка В ИЕРАРХИИ(&Ссылка)";
	Если ВидНаследованияПравДоступаИерархическихСправочников = Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.ТолькоДляТекущегоПрава Тогда
		ТекстЗапроса	= СтрЗаменить(ТекстЗапроса, "В ИЕРАРХИИ", " В ");
	КонецЕсли;
	
	Запрос.Текст	= ТекстЗапроса;
	
	ТаблицаОбъектовНедвижимости = Запрос.Выполнить().Выгрузить();
	
	ТекущийРодитель = Организация;
	Пока Истина Цикл
		ТекущийРодитель = ТекущийРодитель.Родитель;
		Если ТекущийРодитель = Справочники.Организации.ПустаяСсылка() Тогда
			Прервать;
		Иначе
			НоваяСтрока														= ТаблицаОбъектовНедвижимости.Добавить();
			НоваяСтрока.ОбъектДоступа										= ТекущийРодитель;
			НоваяСтрока.ВидНаследованияПравДоступаИерархическихСправочников	= Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.УнаследованоОтДочернего;
			НоваяСтрока.ВладелецПравДоступа									= Организация;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаОбъектовНедвижимости;
	
КонецФункции

&НаСервере
Функция СоздатьТаблицуПодразделенийПоИерархииДоступа(Подразделение, ВидНаследованияПравДоступаИерархическихСправочников)
	
	Запрос			= Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Подразделение);
	Запрос.УстановитьПараметр("ВидНаследованияПравДоступаИерархическихСправочников", ВидНаследованияПравДоступаИерархическихСправочников);
	ТекстЗапроса	= "ВЫБРАТЬ
	            	  |	Подразделения.Ссылка КАК ОбъектДоступа,
	            	  |	ВЫБОР
	            	  |		КОГДА Подразделения.Ссылка = &Ссылка
	            	  |			ТОГДА &ВидНаследованияПравДоступаИерархическихСправочников
	            	  |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыНаследованияПравДоступаИерархическихСправочников.НаследуетсяОтРодителя)
	            	  |	КОНЕЦ КАК ВидНаследованияПравДоступаИерархическихСправочников,
	            	  |	ВЫБОР
	            	  |		КОГДА Подразделения.Ссылка = &Ссылка
	            	  |			ТОГДА ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	            	  |		ИНАЧЕ &Ссылка
	            	  |	КОНЕЦ КАК ВладелецПравДоступа
	            	  |ИЗ
	            	  |	Справочник.Подразделения КАК Подразделения
	            	  |ГДЕ
	            	  |	Подразделения.Ссылка В ИЕРАРХИИ(&Ссылка)";
	Если ВидНаследованияПравДоступаИерархическихСправочников = Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.ТолькоДляТекущегоПрава Тогда
		ТекстЗапроса	= СтрЗаменить(ТекстЗапроса, "В ИЕРАРХИИ", " В ");
	КонецЕсли;
	
	Запрос.Текст	= ТекстЗапроса;
	
	ТаблицаОбъектовНедвижимости = Запрос.Выполнить().Выгрузить();
	
	ТекущийРодитель = Подразделение;
	Пока Истина Цикл
		ТекущийРодитель = ТекущийРодитель.Родитель;
		Если ТекущийРодитель = Справочники.Подразделения.ПустаяСсылка() Тогда
			Прервать;
		Иначе
			НоваяСтрока														= ТаблицаОбъектовНедвижимости.Добавить();
			НоваяСтрока.ОбъектДоступа										= ТекущийРодитель;
			НоваяСтрока.ВидНаследованияПравДоступаИерархическихСправочников	= Перечисления.ВидыНаследованияПравДоступаИерархическихСправочников.УнаследованоОтДочернего;
			НоваяСтрока.ВладелецПравДоступа									= Подразделение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаОбъектовНедвижимости;
	
КонецФункции

&НаСервере
Процедура УдалитьЗаписиДляНаследников(Пользователь, ОбъектДоступа)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь"	, Пользователь);
	Запрос.УстановитьПараметр("ОбъектДоступа"	, ОбъектДоступа);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиДоступа.ОбъектДоступа,
	               |	НастройкиДоступа.ВладелецПравДоступа,
	               |	НастройкиДоступа.НаследованОтВсеПользователи,
	               |	НастройкиДоступа.Пользователь
	               |ИЗ
	               |	РегистрСведений.НастройкиДоступа КАК НастройкиДоступа
	               |ГДЕ
	               |	НастройкиДоступа.Пользователь = &Пользователь
	               |	И НастройкиДоступа.ОбъектДоступа = &ОбъектДоступа
	               |	И НастройкиДоступа.ВидНаследованияПравДоступаИерархическихСправочников = Значение(Перечисление.ВидыНаследованияПравДоступаИерархическихСправочников.НаследуетсяОтРодителя)";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		МенеджерЗаписи	= РегистрыСведений.НастройкиДоступа.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, РезультатЗапроса);
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьЗаписиРегистраПравДоступа()
	
	НаборЗаписей	= РегистрыСведений.НастройкиДоступа.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере
Функция ВернутьТаблицуЗаписейРегистраПравДоступа(ВидОбъектаДоступа)
	
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("ВидОбъектаДоступа"	, ВидОбъектаДоступа);
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиДоступа.ОбъектДоступа,
	               |	НастройкиДоступа.Пользователь,
	               |	НастройкиДоступа.Чтение,
	               |	НастройкиДоступа.ВидНаследованияПравДоступаИерархическихСправочников,
	               |	НастройкиДоступа.Запись
	               |ИЗ
	               |	РегистрСведений.НастройкиДоступа КАК НастройкиДоступа
	               |ГДЕ
	               |	НЕ НастройкиДоступа.ВидНаследованияПравДоступаИерархическихСправочников = ЗНАЧЕНИЕ(Перечисление.ВидыНаследованияПравДоступаИерархическихСправочников.НаследуетсяОтРодителя)
	               |	И НЕ НастройкиДоступа.ВидНаследованияПравДоступаИерархическихСправочников = ЗНАЧЕНИЕ(Перечисление.ВидыНаследованияПравДоступаИерархическихСправочников.УнаследованоОтДочернего)
	               |	И НастройкиДоступа.ВидОбъектаДоступа = &ВидОбъектаДоступа";
				   
	Возврат Запрос.Выполнить().Выгрузить();				   
				   
КонецФункции

&НаСервере
Процедура ЗаписатьТаблицуПравВРегистрНастроекДоступа(ТаблицаПрав, Пользователь, Чтение, Запись,ВидОбъектаДоступа, НаследованОтВсеПользователи = Ложь)
	
	Для Каждого Строка Из ТаблицаПрав Цикл
		МенеджерЗаписи	= РегистрыСведений.НастройкиДоступа.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
		МенеджерЗаписи.НаследованОтВсеПользователи	= НаследованОтВсеПользователи;
		МенеджерЗаписи.Пользователь					= Пользователь;
		МенеджерЗаписи.ВидОбъектаДоступа			= ВидОбъектаДоступа;
		МенеджерЗаписи.Чтение						= Чтение;
		МенеджерЗаписи.Запись						= Запись;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Интерфейс

&НаКлиенте
Процедура ТаблицаПравДоступаПодразделенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ТаблицаПравДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование, "ТаблицаПравДоступаПодразделения");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравДоступаОрганизацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТаблицаПравДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование, "ТаблицаПравДоступаОрганизации");

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравДоступаБанковскиеСчетаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТаблицаПравДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование, "ТаблицаПравДоступаБанковскиеСчета");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравДоступаКассыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТаблицаПравДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование, "ТаблицаПравДоступаКассы");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование, Таблица)
	
	Если НоваяСтрока Тогда
		ГруппаПользователей 		= Элементы.СписокГруппПользователей.ТекущаяСтрока;

		ТекущаяСтрока				= ЭтотОбъект[Таблица].НайтиПоИдентификатору(Элемент.ТекущиеДанные.ПолучитьИдентификатор());	
		ТекущаяСтрока.Пользователь	= ГруппаПользователей;
		ТекущаяСтрока.Чтение		= Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокГруппПользователейПриАктивизацииСтроки(Элемент)
	
	ГруппаПользователей = Элементы.СписокГруппПользователей.ТекущаяСтрока;
	
	СтруктураОтбора	= Новый Структура();
	СтруктураОтбора.Вставить("Пользователь", ГруппаПользователей);
	
	Отбор = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	
	Элементы.ТаблицаПравДоступаПодразделения.ОтборСтрок = Отбор;
	
	Элементы.ТаблицаПравДоступаОрганизации.ОтборСтрок = Отбор;

	Элементы.ТаблицаПравДоступаБанковскиеСчета.ОтборСтрок = Отбор;
	
	Элементы.ТаблицаПравДоступаКассы.ОтборСтрок = Отбор;

КонецПроцедуры

#КонецОбласти

