&НаСервере
Функция ДобавитьСтрокуГруппировкиИерархия(Строки, Значение, ИндексОформления = 0)
	
	Для Каждого Строка Из Строки Цикл
		Если Строка.Группировка=Значение Тогда
			Возврат Строка;
		КонецЕсли; 
	КонецЦикла; 
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.ИндексОформления = ИндексОформления;
	НоваяСтрока.Группировка = Значение;
	НоваяСтрока.СтандартнаяКартинка = БиблиотекаКартинок.Реквизит;
	Возврат НоваяСтрока;
		
КонецФункции

&НаСервере
Функция ДобавитьСтрокуИерархия(Знач РодительСтрок, СсылкаНаОбъект, ИндексОформления)
	
	Массив = Новый Массив;
	Родитель = СсылкаНаОбъект.Родитель;
	Пока ЗначениеЗаполнено(Родитель) Цикл
		Массив.Вставить(0, Родитель);
		Родитель = Родитель.Родитель;
	КонецЦикла;
	
	Для Каждого Строка Из Массив Цикл
		РодительСтрок = ДобавитьСтрокуГруппировкиИерархия(РодительСтрок, Строка, ИндексОформления).ПолучитьЭлементы();
	КонецЦикла;
	
	Возврат ДобавитьСтрокуГруппировкиИерархия(РодительСтрок, СсылкаНаОбъект, ИндексОформления);
		
КонецФункции

&НаСервере
Функция ПросуммироватьРекурсивно(ЭлементыДерева, Колонка)
	
	Если ЭлементыДерева.Количество()=0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Сумма = 0;
	Для Каждого Строка Из ЭлементыДерева Цикл
		Результат = ПросуммироватьРекурсивно(Строка.ПолучитьЭлементы(), Колонка);
		Если Результат<>Неопределено Тогда
			Строка[Колонка] = Результат;
		КонецЕсли; 
		Сумма = Сумма + Строка[Колонка];
	КонецЦикла;
	
	Возврат Сумма;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСтрокуДеталейКлиент(Строка)
	
	Лимит = Строка.Лимит;
	Сумма = Строка.Утверждено + Строка.КУтверждению + Строка.КОплате;
	Если Лимит<Сумма Тогда
		Строка.СуммаПрофицит = 0;
		Строка.СуммаДефицит = Сумма-Лимит;
	Иначе
		Строка.СуммаПрофицит = Лимит - Сумма;
		Строка.СуммаДефицит = 0;		
	КонецЕсли; 
	Строка.Отклонение = Строка.Лимит - Строка.ТекущийЛимит;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Результат.Ссылка,
	|	Результат.Организация,
	|	Результат.Подразделение,
	|	Результат.СтатьяДвиженияДенежныхСредств,
	|	Результат.КУтверждению,
	|	Результат.Утверждено,
	|	Результат.КОплате,
	|	Результат.ТекущийЛимит
	|ИЗ
	|	(ВЫБРАТЬ
	|		Док.Ссылка КАК Ссылка,
	|		Док.Организация КАК Организация,
	|		Док.Подразделение КАК Подразделение,
	|		Док.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		Док.СуммаДокумента КАК КУтверждению,
	|		0 КАК Утверждено,
	|		0 КАК КОплате,
	|		0 КАК ТекущийЛимит
	|	ИЗ
	|		Документ.СчетНаОплатуПоставщика КАК Док
	|	ГДЕ
	|		Док.Виза_Состояние = &Виза_Состояние
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Док.СчетНаОплатуПоставщика,
	|		Док.СчетНаОплатуПоставщика.Организация,
	|		Док.СчетНаОплатуПоставщика.Подразделение,
	|		Док.СчетНаОплатуПоставщика.СтатьяДвиженияДенежныхСредств,
	|		0,
	|		Док.СуммаОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.СчетаПоставщиков.Остатки КАК Док
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Док.ЗаявкаНаРасходованиеДС,
	|		Док.ЗаявкаНаРасходованиеДС.Организация,
	|		Док.ЗаявкаНаРасходованиеДС.Подразделение,
	|		Док.ЗаявкаНаРасходованиеДС.СтатьяДвиженияДенежныхСредств,
	|		0,
	|		0,
	|		Док.СуммаОстаток,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗаявкиНаРасходованиеДС.Остатки КАК Док
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		Док.Организация,
	|		Док.Подразделение,
	|		NULL,
	|		0,
	|		0,
	|		0,
	|		СУММА(Док.СуммаОстаток)
	|	ИЗ
	|		РегистрНакопления.ЛимитыДДС.Остатки КАК Док
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Док.Подразделение,
	|		Док.Организация) КАК Результат
	|ГДЕ
	|	Результат.Подразделение В ИЕРАРХИИ(&Подразделение)"
	);
	
	Если СписокПодразделений.Количество()=0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Результат.Подразделение В ИЕРАРХИИ(&Подразделение)", "Истина");
	Иначе
		Запрос.УстановитьПараметр("Подразделение", СписокПодразделений); 
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Виза_Состояние", Перечисления.Визы_Состояния.НаВизировании); 
	ТекущиеСчета = Запрос.Выполнить().Выгрузить();
		
	ДеревоДанных.ПолучитьЭлементы().Очистить();
	СтруктураГруппировки = Новый Структура(СтрокаГруппировки);
	
	Для Каждого Строка Из ТекущиеСчета Цикл
		
		СтрокаРодителя = ДеревоДанных;
		ИндексОформления = 1;
		Для Каждого Группа Из СтруктураГруппировки Цикл
			СтрокаРодителя = ДобавитьСтрокуИерархия(СтрокаРодителя.ПолучитьЭлементы(), Строка[Группа.Ключ], ИндексОформления);
			ИндексОформления = ИндексОформления + 1;
		КонецЦикла;

		СтрокаРодителя.МожноУстанавливатьЛимит = Истина;
		ЗаполнитьЗначенияСвойств(СтрокаРодителя, Строка, "Организация, Подразделение");
		
		Если Строка.ТекущийЛимит<>0 Тогда
			СтрокаРодителя.ТекущийЛимит = Строка.ТекущийЛимит;
		КонецЕсли;
		СтрокаРодителя.Лимит = СтрокаРодителя.ТекущийЛимит;
		
		Если Не ЗначениеЗаполнено(Строка.Ссылка) Тогда
			Продолжить;
		КонецЕсли; 
				
		НоваяСтрока = СтрокаРодителя.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Группировка = Строка.Ссылка;
		НоваяСтрока.СтандартнаяКартинка = БиблиотекаКартинок.Документ;
		НоваяСтрока.ЭтоДетали = Истина;
		НоваяСтрока.Лимит = НоваяСтрока.ТекущийЛимит;
		НоваяСтрока.МожноУстанавливатьЛимит = Истина;
		Лимит = НоваяСтрока.Лимит;
		Сумма = НоваяСтрока.Утверждено + НоваяСтрока.КУтверждению + НоваяСтрока.КОплате;
		Если Лимит<Сумма Тогда
			НоваяСтрока.СуммаПрофицит = 0;
			НоваяСтрока.СуммаДефицит = Сумма-Лимит;
		Иначе
			НоваяСтрока.СуммаПрофицит = Лимит - Сумма;
			НоваяСтрока.СуммаДефицит = 0;		
		КонецЕсли; 
		
	КонецЦикла;
	
	ПросуммироватьРекурсивно(ДеревоДанных.ПолучитьЭлементы(), "Лимит");
	ПросуммироватьРекурсивно(ДеревоДанных.ПолучитьЭлементы(), "КУтверждению");
	ПросуммироватьРекурсивно(ДеревоДанных.ПолучитьЭлементы(), "Утверждено");
	ПросуммироватьРекурсивно(ДеревоДанных.ПолучитьЭлементы(), "КОплате");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокаГруппировки = "Подразделение";
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПросуммироватьПодчиненные(РодительСтроки, ИмяКолонки = "Лимит")
	
	Пока РодительСтроки<>Неопределено Цикл
		Сумма = 0;
		МассивСтрок = РодительСтроки.ПолучитьЭлементы();
		Для Каждого Строка Из МассивСтрок Цикл
			Сумма = Сумма + Строка[ИмяКолонки];
		КонецЦикла;
		РодительСтроки[ИмяКолонки] = Сумма;
		РодительСтроки = РодительСтроки.ПолучитьРодителя();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхЛимитПриИзменении(Элемент)
	
	ОбновитьСтрокуДеталейКлиент(Элементы.ДеревоДанных.ТекущиеДанные);
	ПросуммироватьПодчиненные(Элементы.ДеревоДанных.ТекущиеДанные.ПолучитьРодителя());
	
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьВТаблицу(Таблица, ВходящаяСтрока)
	
	МассивСтрок = ВходящаяСтрока.ПолучитьЭлементы();
	Для Каждого Строка Из МассивСтрок Цикл
		ПреобразоватьВТаблицу(Таблица, Строка);
		Если Не Строка.МожноУстанавливатьЛимит Тогда
			Продолжить;
		КонецЕсли; 
		Если Строка.Лимит=Строка.ТекущийЛимит Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЛимитыНаСервере()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Родитель");
	Таблица.Колонки.Добавить("Подразделение");
	Таблица.Колонки.Добавить("Лимит");
	Таблица.Колонки.Добавить("ТекущийЛимит");
	ПреобразоватьВТаблицу(Таблица, ДеревоДанных);
	Для Каждого Строка Из Таблица Цикл
		Строка.Родитель = Строка.Подразделение.Родитель;
	КонецЦикла;
	
	ТаблицаГруппировки = Таблица.Скопировать(,"Родитель");
	ТаблицаГруппировки.Свернуть("Родитель");
	МассивГрупп = ТаблицаГруппировки.ВыгрузитьКолонку("Родитель");
	
	Для Каждого Группа Из МассивГрупп Цикл
		
		ЭтаДата = ТекущаяДата();
		
		ДокументОбъект = Документы.УстановкаЛимитовСтатейДвиженияДенежныхСредств.СоздатьДокумент();
		ДокументОбъект.Дата = ЭтаДата;
		ДокументОбъект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		ДокументОбъект.Лимиты.Очистить();
		
		НайденыеСтроки = Таблица.НайтиСтроки(Новый Структура("Родитель", Группа));
		Для Каждого НайденнаяСтрока Из НайденыеСтроки Цикл
			
			СуммаЛимита = НайденнаяСтрока.Лимит - НайденнаяСтрока.ТекущийЛимит;
			Если НайденнаяСтрока.Лимит=0 Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ДокументОбъект.Лимиты.Добавить();
			НоваяСтрока.Подразделение = НайденнаяСтрока.Подразделение;
			НоваяСтрока.Сумма = СуммаЛимита;
			
		КонецЦикла; 
		
		Если ДокументОбъект.Лимиты.Количество()>0 Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);		
		КонецЕсли; 
		
	КонецЦикла; 
		
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЛимиты(Команда)
	
	УстановитьЛимитыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхПередНачаломИзменения(Элемент, Отказ)
	Если НЕ Элементы.ДеревоДанных.ТекущиеДанные.МожноУстанавливатьЛимит Тогда
		Отказ = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоказать(Команда)
	Элементы.ГруппаНастройки.Видимость = Не Элементы.ГруппаНастройки.Видимость;
	Элементы.ФормаНастройкиПоказать.Пометка = Элементы.ГруппаНастройки.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеНаКлиенте(Команда)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДеревоРекурсивно(Строка, ТекущийУровень, ГраничныйУровень, НадоСвернуть = Истина)
	
	Если ТекущийУровень=ГраничныйУровень Тогда
		Если НадоСвернуть Тогда
			Элементы.ДеревоДанных.Свернуть(Строка.ПолучитьИдентификатор());
		Иначе
			Элементы.ДеревоДанных.Развернуть(Строка.ПолучитьИдентификатор());
		КонецЕсли; 
	Иначе
		МассивСтрок = Строка.ПолучитьЭлементы();
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			СвернутьДеревоРекурсивно(СтрокаМассива, ТекущийУровень+1, ГраничныйУровень, НадоСвернуть);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсеДоУровня(Команда)
	СвернутьДеревоРекурсивно(ДеревоДанных, 0, Число(Прав(Команда.Имя, 1)), Истина);
КонецПроцедуры

&НаКлиенте
Процедура СгрупироватьОрганизацияПодразделение(Команда)
	СтрокаГруппировки = "Организация, Подразделение";
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура СгрупироватьПодразделениеОрганизация(Команда)
	СтрокаГруппировки = "Подразделение, Организация";
	ОбновитьДанные();
КонецПроцедуры