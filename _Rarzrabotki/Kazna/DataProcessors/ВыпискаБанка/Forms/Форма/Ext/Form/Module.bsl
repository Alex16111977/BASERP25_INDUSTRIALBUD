
&НаКлиенте
Процедура Выделить(Команда)
	
	
	 СнятьУстановитьПометку(Истина)
	
		
КонецПроцедуры

&НаКлиенте
Процедура СнятьУстановитьПометку(СтатусПометки)
	
	Для Каждого Идентификатор Из Элементы.Документы.ВыделенныеСтроки Цикл
		ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Идентификатор);
		ТекущаяСтрока.Оплачено			= СтатусПометки;
		ТекущаяСтрока.ВнесеныИзменения	= СтатусПометки;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДокументыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущаяСтрока					= Объект.Документы.НайтиПоИдентификатору(Элемент.ТекущиеДанные.ПолучитьИдентификатор());
	
	Если Элементы.Документы.ТекущийЭлемент.Имя = "ДокументыДатаОплаты" 
		Или Элементы.Документы.ТекущийЭлемент.Имя = "ДокументыОплачено" Тогда
		ТекущаяСтрока.ВнесеныИзменения	= Истина;
	ИначеЕсли Элементы.Документы.ТекущийЭлемент.Имя = "ДокументыПодразделение"
		Или Элементы.Документы.ТекущийЭлемент.Имя = "ДокументыСекция"
		Или Элементы.Документы.ТекущийЭлемент.Имя = "ДокументыСтатьяДвиженияДенежныхСредств"Тогда
		ТекущаяСтрока.ВнесеныИзмененияПоАналитикам	= Истина;
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()

	ОбъектРеквизит				= РеквизитФормыВЗначение("Объект");
	
	СхемаКомпоновкиДанных		= ОбъектРеквизит.ПолучитьМакет("Макет");

    Результат					= ЗаполнитьСвойстваИСформироватьСКД(СхемаКомпоновкиДанных);;
	
	ОбъектРеквизит.Документы.Очистить();
	
	Для Каждого Строка Из Результат Цикл
		ЗаполнитьЗначенияСвойств(ОбъектРеквизит.Документы.Добавить(), Строка);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектРеквизит, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
	
	ПолучитьСтруктуруОстатокИДвиженийДС();

	ПересчитатьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ПроставитьПризнакОплаченоНаСервере()

	ОбъектРеквизит	= РеквизитФормыВЗначение("Объект");
	
	Отбор		= Новый Структура("Оплачено, ВнесеныИзменения", Истина, Истина);
	МассивСтрок	= ОбъектРеквизит.Документы.НайтиСтроки(Отбор);
	
	Для Каждого Документ Из МассивСтрок Цикл
		ДокументОбъект				= Документ.Документ.ПолучитьОбъект();
		ДокументОбъект.Оплачено		= Истина;
		ДокументОбъект.ДатаОплаты	= ?(ЗначениеЗаполнено(Документ.ДатаОплаты), Документ.ДатаОплаты, ДокументОбъект.Дата);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Документ.ВнесеныИзменения	= Ложь;
	КонецЦикла;
	
	Отбор		= Новый Структура("Оплачено, ВнесеныИзменения", Ложь, Истина);
	МассивСтрок	= ОбъектРеквизит.Документы.НайтиСтроки(Отбор);
	
	Для Каждого Документ Из МассивСтрок Цикл
		ДокументОбъект				= Документ.Документ.ПолучитьОбъект();
		ДокументОбъект.Оплачено		= Ложь;
		ДокументОбъект.ДатаОплаты	= Дата(1,1,1);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Документ.ВнесеныИзменения	= Ложь;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектРеквизит, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьПризнакОплачено(Команда)
	
	ПроставитьПризнакОплаченоНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтруктуруОстатокИДвиженийДС();
	
	ОбъектРеквизит				= РеквизитФормыВЗначение("Объект");
	
	СхемаКомпоновкиДанных		= ОбъектРеквизит.ПолучитьМакет("МакетОстаткиИДвижения");

	Результат = ЗаполнитьСвойстваИСформироватьСКД(СхемаКомпоновкиДанных);
	
	Если Результат.Количество() <> 0 Тогда
		//Приход					= Результат[0]["СуммаПриход"];
		//Расход					= Результат[0]["СуммаРасход"];
		//ОстатокНаКонецПериода	= Результат[0]["СуммаКонечныйОстаток"];
		ОстатокНаНачалоПериода	= Результат[0]["СуммаНачальныйОстаток"];
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСвойстваИСформироватьСКД(СхемаКомпоновкиДанных)
	
	КомпоновщикНастроек	= Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Если ЗначениеЗаполнено(Период) Тогда
		Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение	= Период;
			Параметр.Использование	= Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение	= Объект.Организация;
			Параметр.Использование	= Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("БанковскийСчет"));
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение	= Объект.БанковскийСчет;
			Параметр.Использование	= Истина;
		КонецЕсли;
	КонецЕсли;

	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТолькоОплаченные"));
	Если Не Параметр = Неопределено Тогда
		Параметр.Значение	= Объект.ТолькоОплаченные;
		Параметр.Использование	= Истина;
	КонецЕсли;

	ДанныеРасшифровки	= Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета	= Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки			= КомпоновщикНастроек.Настройки;

	МакетКомпоновки		= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;

	Возврат ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

КонецФункции

&НаСервере
Процедура ИзменитьАналитикиНаСервере()

	ОбъектРеквизит	= РеквизитФормыВЗначение("Объект");
	
	Отбор	= Новый Структура("ВнесеныИзмененияПоАналитикам", Истина);
	МассивСтрок	= ОбъектРеквизит.Документы.НайтиСтроки(Отбор);
	
	Для Каждого Документ Из МассивСтрок Цикл
		ДокументОбъект	= Документ.Документ.ПолучитьОбъект();
		ДокументОбъект.Секция = Документ.Секция;
		ДокументОбъект.Подразделение = Документ.Подразделение;
		ДокументОбъект.СтатьяДвиженияДенежныхСредств = Документ.СтатьяДвиженияДенежныхСредств;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Документ.ВнесеныИзменения	= Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьАналитики(Команда)

	ИзменитьАналитикиНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьДополнительныеАналитикиПриИзменении(Элемент)
	
	
	Элементы.ДокументыПодразделение.Видимость = ОтображатьДополнительныеАналитики;
	Элементы.ДокументыСекция.Видимость = ОтображатьДополнительныеАналитики;
	Элементы.ДокументыСтатьяДвиженияДенежныхСредств.Видимость = ОтображатьДополнительныеАналитики;
	
	Элементы.ИзменитьАналитики.Видимость = ОтображатьДополнительныеАналитики;
	
КонецПроцедуры

&НаКлиенте
Процедура Снять(Команда)
	
	СнятьУстановитьПометку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Инвертировать(Команда)
	
	Для Каждого Идентификатор Из Элементы.Документы.ВыделенныеСтроки Цикл
		ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Идентификатор);
		ТекущаяСтрока.Оплачено			= Не ТекущаяСтрока.Оплачено;
		ТекущаяСтрока.ВнесеныИзменения	= Истина;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтоги()
	
	ОбъектРеквизит	= РеквизитФормыВЗначение("Объект");
	
	Отбор			= Новый Структура("Оплачено", Истина);
	МассивСтрок		= ОбъектРеквизит.Документы.НайтиСтроки(Отбор);
	
	Приход			= 0;
	Расход			= 0;
	
	Для Каждого Документ Из МассивСтрок Цикл
		Приход = Приход + Документ.СуммаПрихода;
		Расход = Расход + Документ.СуммаРасхода;
	КонецЦикла;
	
	ОстатокНаКонецПериода	= ОстатокНаНачалоПериода + Приход - Расход;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОплаченоПриИзменении(Элемент)
	
	ПересчитатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.ТолькоПросмотр Тогда
		ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Элемент.ТекущиеДанные.ПолучитьИдентификатор());
		ОткрытьЗначение(ТекущаяСтрока.Документ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокСекций(Подразделение)
	
	СписокСекций	= Новый СписокЗначений;
	Запрос	= Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ПодразделениеРодитель", Подразделение.Родитель);
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Секции.Ссылка КАК Секция
	            	  |ИЗ
	            	  |	Справочник.Секции КАК Секции
	            	  |ГДЕ
	            	  |	(Секции.Владелец = &Подразделение
	            	  |			ИЛИ Секции.Владелец = &ПодразделениеРодитель)";
					  
					  
	РезультатЗапроса	= Запрос.Выполнить().Выгрузить();
	
	СписокСекций.ЗагрузитьЗначения(РезультатЗапроса.ВыгрузитьКолонку("Секция"));
	
	Возврат СписокСекций;
	
КонецФункции

&НаКлиенте
Процедура ДокументыСекцияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Элементы.Документы.ТекущиеДанные.ПолучитьИдентификатор());
	
	ОписаниеОповещения		= Новый ОписаниеОповещения("ДокументыСекцияНачалоВыбораЗавершение", ЭтаФорма);
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, ПолучитьСписокСекций(ТекущаяСтрока.Подразделение), Элемент);
	
КонецПроцедуры

&НаКлиенте 
Процедура ДокументыСекцияНачалоВыбораЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Элементы.Документы.ТекущиеДанные.ПолучитьИдентификатор());
	ТекущаяСтрока.Секция = Значение.Значение;

КонецПроцедуры

&НаКлиенте
Процедура ДокументыПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущаяСтрока	= Объект.Документы.НайтиПоИдентификатору(Элементы.Документы.ТекущиеДанные.ПолучитьИдентификатор());
	Если ТекущаяСтрока.Подразделение <> ВыбранноеЗначение Тогда
		ТекущаяСтрока.Секция = ПредопределенноеЗначение("Справочник.Секции.ПустаяСсылка");
	КонецЕсли;
	
	
КонецПроцедуры
