Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроверитьЗаполнениеОбязательныхРевизитов(Отказ);
	
	// регистр ДенежныеСредства Приход
	Если Оплачено Тогда
		
		лДатаОплаты = ?(ЗначениеЗаполнено(ДатаОплаты), ДатаОплаты, Дата);
		
		Движения.ДенежныеСредства.Записывать = Истина;
		Движения.БДДС.Записывать = Истина;
		Движение = Движения.ДенежныеСредства.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = лДатаОплаты;
		Движение.ВидДенежныхСредств = Перечисления.ВидыДенежныхСредств.Безналичные;
		Движение.БанковскийСчетКасса = СчетОрганизации;
		Движение.Организация = Организация;
		Движение.Сумма = СуммаДокумента;
		
		Движения.БДДС.ВыполнитьДвижение(ЭтотОбъект);
		
		Если Расшифровка.Количество()>0 Тогда
			Движения.БДДС.Очистить();
			Для каждого Стр Из Расшифровка Цикл
				Если Стр.НеОтражатьВДДС Тогда
					Продолжить;;
				КонецЕсли;
				Движение = Движения.БДДС.Добавить();
				Если ЗначениеЗаполнено(ДатаОплаты) Тогда
					Движение.Период = ДатаОплаты;
				Иначе
					Движение.Период = Дата;
				КонецЕсли;
				
				Движение.Активность=Истина;
				Движение.ВидОплаты=Перечисления.ВидыДенежныхСредств.Безналичные;
				Движение.СтатьяДвиженияДенежныхСредств=Стр.СтатьяДвиженияДенежныхСредств;
				Движение.Подразделение=Стр.Подразделение;
				Движение.НаправлениеДДС	= Перечисления.НаправленияДвиженийДенежныхСредств.Расход;
				Движение.СуммаРасход=РаботаСКурсамиВалют.ПересчитатьВВалюту(Стр.Сумма,ВалютаДокумента,Константы.ВалютаРегламентированногоУчета.Получить(),Дата);
				Движение.Сумма=Движение.СуммаРасход;
				Движение.ДокументОснование=ДокументОснование;
				Движение.Направление=Стр.Направление;
			КонецЦикла;
		КонецЕсли;
		
		
		//Сава 2016/10
		лПроект = ?(Подразделение.НеИспользовать, Подразделение, Подразделение.Родитель);//Если самый низ - берем уровень выше.
		Для Каждого лСтрока Из ДСПроектов Цикл 
			Если лСтрока.ЭтоДСПроектов Тогда 
				//Девиденды
				Движение 				= Движения.ДенежныеСредстваПроектов.Добавить();
				Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
				Движение.Период 		= лДатаОплаты;
				Движение.Активность		= Истина;
				Движение.Сумма 			= лСтрока.Сумма;
				Движение.Подразделение  = Подразделение;//лПроект;
				Движение.Организация	= Организация;
			Иначе
				Движение 				= Движения.ВзаиморасчетыПоПроектам.Добавить();
				Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
				Движение.Период 		= лДатаОплаты;
				Движение.Активность		= Истина;
				Движение.Сумма 			= лСтрока.Сумма;
				Движение.Подразделение  = лПроект;
				Движение.ПодразделениеЗайма = лСтрока.Подразделение;
			КонецЕсли;
		КонецЦикла;
		Движения.ВзаиморасчетыПоПроектам.Записывать = Истина;
		Движения.ДенежныеСредстваПроектов.Записывать = Истина;
	КонецЕсли;
	
	//Сава: 2016/07 Заявки закрываются про проведениею платежки
	Если ЗначениеЗаполнено( ДокументОснование )  и ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств" ) Тогда  
		Движения.ЗаявкиНаРасходованиеДС.Записывать = Истина;
		Движение 						= Движения.ЗаявкиНаРасходованиеДС.Добавить();
		Движение.Период 				= Дата;
		Движение.ВидДвижения			= ВидДвиженияНакопления.Расход;
		Движение.ЗаявкаНаРасходованиеДС = ДокументОснование;
		Движение.Сумма 					= СуммаДокумента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено( ДокументОснование ) 				И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств" )
		И ЗначениеЗаполнено(ДокументОснование.ДокументОснование)И ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПоставщика" ) Тогда  
		
		Движения.СчетаПоставщиков.Записывать = Истина;
		Движение 						= Движения.СчетаПоставщиков.Добавить();
		Движение.Период 				= Дата;
		Движение.ВидДвижения			= ВидДвиженияНакопления.Расход;
		Движение.СчетНаОплатуПоставщика = ДокументОснование.ДокументОснование;
		Движение.Сумма 					= 0;
		Движение.НаОплату 				= СуммаДокумента;
		Движения.СчетаПоставщиков.Записывать = Истина;
		
		Движение 						= Движения.СчетаПоставщиков.Добавить();
		Движение.Период 				= Дата;
		Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
		Движение.СчетНаОплатуПоставщика = ДокументОснование.ДокументОснование;
		Движение.Сумма 					= 0;
		Движение.НаОплату 				= 0;
		Движение.ВКБ					= СуммаДокумента;
		
		Если Оплачено Тогда 
			Движение 						= Движения.СчетаПоставщиков.Добавить();
			Движение.Период 				= ?(НЕ ЗначениеЗаполнено(ДатаОплаты) ИЛИ НачалоДня(Дата) = НачалоДня(ДатаОплаты), Дата, ДатаОплаты ) ;
			Движение.ВидДвижения			= ВидДвиженияНакопления.Расход;
			Движение.СчетНаОплатуПоставщика = ДокументОснование.ДокументОснование;
			Движение.Сумма 					= 0;
			Движение.НаОплату 				= 0;
			Движение.ВКБ					= СуммаДокумента;
		КонецЕсли;
		
	КонецЕсли;  
	
	//Тютюн 
	СтатьяФинАгенты = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("УТ-001096");
	Если СтатьяДвиженияДенежныхСредств = СтатьяФинАгенты Тогда 
		
		// Создаем набор записей для движений
		Движения.ДенежныеСредстваФинАгенты.Записывать = Истина;
		
		// Проверяем остатки с пустым ДокументПередачи
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|    ЕСТЬNULL(ДенежныеСредстваФинАгентыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|    РегистрНакопления.ДенежныеСредстваФинАгенты.Остатки(
		|            &МоментВремени,
		|            ФинАгент = &ФинАгент
		|            И Направление = &Направление
		|            И ДокументПередачи = &ПустойДокумент) КАК ДенежныеСредстваФинАгентыОстатки";
		
		Запрос.УстановитьПараметр("МоментВремени", ЭтотОбъект.МоментВремени());
		Запрос.УстановитьПараметр("ФинАгент", _ФинАгент);
		Запрос.УстановитьПараметр("Направление", Направление); // Берем значение из реквизита документа
		Запрос.УстановитьПараметр("ПустойДокумент", Документы.ПлатежноеПоручениеИсходящее.ПустаяСсылка());
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Для каждого Стр Из Расшифровка Цикл
			Если Стр.СтатьяДвиженияДенежныхСредств = СтатьяФинАгенты Тогда
				
				ОстатокПрихода = Стр.Сумма; // Сумма, которую нужно оприходовать
				
				// Если есть отрицательный остаток с пустым ДокументПередачи
				Если Выборка.Следующий() И Выборка.СуммаОстаток < 0 Тогда
					СуммаДляЗакрытия = Мин(-Выборка.СуммаОстаток, ОстатокПрихода); // Берем модуль отрицательного остатка
					
					Если СуммаДляЗакрытия > 0 Тогда
						// Формируем приход для закрытия отрицательного остатка с пустым ДокументПередачи
						Движение = Движения.ДенежныеСредстваФинАгенты.Добавить();
						Движение.Период = Дата;
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.ФинАгент = _ФинАгент;
						Движение.Направление = Направление; // Добавляем новое измерение
						Движение.Сумма = СуммаДляЗакрытия;
						// ДокументПередачи оставляем пустым
						
						ОстатокПрихода = ОстатокПрихода - СуммаДляЗакрытия;
					КонецЕсли;
				КонецЕсли;
				
				// Оставшуюся сумму оприходуем с заполненным ДокументПередачи
				Если ОстатокПрихода > 0 Тогда
					Движение = Движения.ДенежныеСредстваФинАгенты.Добавить();
					Движение.Период = Дата;
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.ДокументПередачи = Ссылка;
					Движение.ФинАгент = _ФинАгент;
					Движение.Направление = Направление; // Добавляем новое измерение
					Движение.Сумма = ОстатокПрихода;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	//КонецИзменений
	
	//++ Павел Чистяков 06.09.2016 15:23:49 №216
	//Если ЗначениеЗаполнено(ДатаОплаты)
	Если 
		 СтатьяДвиженияДенежныхСредств=Константы.СтатьяТранзитНал.Получить() Тогда
		
		Движения.ДенежныеСредстваВПути.ВыполнитьПриход(ЭтотОбъект);
	КонецЕсли; 
	
	Если НеОтражатьВДДС Тогда
		Движения.БДДС.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	
	ВидОперации= Перечисления.ВидыОперацийППИсходящее.ОплатаПоставщику;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
		// Заполнение шапки
		ВалютаДокумента 	= ДанныеЗаполнения.ВалютаДокумента;
		Комментарий	 	= ДанныеЗаполнения.Комментарий;
		Контрагент = ДанныеЗаполнения.Контрагент;
		НазначениеПлатежа = ДанныеЗаполнения.НазначениеПлатежа;
		Организация = ДанныеЗаполнения.Организация;
		//Ответственный = ДанныеЗаполнения.Ответственный;
		Подразделение = ДанныеЗаполнения.Подразделение;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		СчетОрганизации = ДанныеЗаполнения.СтруктурнаяЕдиница;
		СуммаДокумента = Документы.ЗаявкаНаРасходованиеДенежныхСредств.ПолучитьОстатокСуммыКОплате(ДанныеЗаполнения);
		СчетКонтрагента = ДанныеЗаполнения.СчетКонтрагента;
		//Тютюн 
		Если ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("УТ-001096") ТОгда 
			_ФинАгент              = ДанныеЗаполнения._ФинАгент;  
			_ПроцентФинАгента      = ДанныеЗаполнения._ПроцентФинАгента;
			_ДатаВозвратаФинАгента = ДанныеЗаполнения._ДатаВозвратаФинАгента;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПлатежноеПоручениеВходящее") Тогда
		ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
		ДатаВыгрузки = ДанныеЗаполнения.ДатаВыгрузки;
		ДатаЗагрузки = ДанныеЗаполнения.ДатаЗагрузки;
		ДатаОплаты = ТекущаяДатаСеанса();
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Контрагент = ДанныеЗаполнения.Контрагент;
		НазначениеПлатежа = ДанныеЗаполнения.НазначениеПлатежа;
		Направление = ДанныеЗаполнения.Направление;
		Оплачено = ДанныеЗаполнения.Оплачено;
		Организация = ДанныеЗаполнения.Организация; 
		ДокументОснование = ДанныеЗаполнения;
		//Ответственный = ДанныеЗаполнения.Ответственный;
		Подразделение = ДанныеЗаполнения.Подразделение;
		Секция = ДанныеЗаполнения.Секция;
		СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		СчетКонтрагента = ДанныеЗаполнения.СчетКонтрагента;
		СчетОрганизации = ДанныеЗаполнения.СчетОрганизации;
		
	КонецЕсли;
	Ответственный    = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗначениеЗаполнено(ДатаОплаты) Тогда
		Оплачено=Истина;
	КонецЕсли;
	
	Если Дата>=Дата(2016,07,01) Тогда
		Если ЗначениеЗаполнено(ДатаОплаты) Тогда
			 Если НачалоДня(ДатаОплаты)<>НачалоДня(Дата) Тогда
				Комментарий=Комментарий+" КЛБ - Сдвиг даты оплаты с "+Формат(Дата, "ДФ=dd.MM.yyyy");
				Дата =ДатаОплаты; 
			 КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтатьяДвиженияДенежныхСредств) И Не ЗначениеЗаполнено(ДокументОснование) Тогда
		СтатьяДвиженияДенежныхСредств = АХ_ОбщегоНазначения.ПолучитьСтатьюДДСПоУсловию(Подразделение, Контрагент, СуммаДокумента, "ППИ", Секция);
	КонецЕсли;
	
	
	Если Расшифровка.Количество()>0 Тогда
		Если Расшифровка.Итог("Сумма")<>СуммаДокумента Тогда
		     Сообщить("Не совпадает сумма документа и расшифровки");
			 Отказ= Истина
		КонецЕсли;
	КонецЕсли;  
	
	
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеОбязательныхРевизитов(Отказ)
	
	МассивПроверяемыхРеквизитов = Новый Массив;
	МассивПроверяемыхРеквизитов.Добавить("Организация");
	МассивПроверяемыхРеквизитов.Добавить("СчетКонтрагента");
	МассивПроверяемыхРеквизитов.Добавить("Контрагент");
	МассивПроверяемыхРеквизитов.Добавить("СчетОрганизации");    
	
	
	АХ_ОбщегоНазначения.КонтрольЗаполненияОбязательныхРеквизитовШапкиОбъекта(ЭтотОбъект, МассивПроверяемыхРеквизитов, Отказ);
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДСПроектов.Очистить();
	Ответственный    = Пользователи.ТекущийПользователь();
КонецПроцедуры
