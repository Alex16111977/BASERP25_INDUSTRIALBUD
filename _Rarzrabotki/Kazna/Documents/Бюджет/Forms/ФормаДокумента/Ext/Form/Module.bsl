
&НаКлиенте
Процедура ПериодБюджетаПриИзменении(Элемент)

	Если Не ЗначениеЗаполнено(Объект.НачалоБюджета) ИЛИ Не ЗначениеЗаполнено(Объект.КонецБюджета) Тогда
		Возврат;                                                                                   
	КонецЕсли;

	ОписаниеОповещения	= Новый ОписаниеОповещения("ПериодБюджетаПриИзмененииЗавершение", ЭтаФорма);
	
	ПоказатьВопрос(ОписаниеОповещения, "Внимание! Данные о суммах бюджета будут очищены. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
	
КонецПроцедуры

&НаКлиенте 
Процедура ПериодБюджетаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьКолонкиДат();
		ДобавитьКолонкиДат();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ДобавитьКолонкиДат();
		ЗаполнитьДеревоПоказателейНаФорме();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПоказателейНаФорме()
	
	СтруктураСоответствийКолонокДатам	= ПолучитьИзВременногоХранилища(АдресХранилищаСтруктурыСоответствийКолонокДат);
	
	Запрос	= Новый Запрос;
	
	Запрос.УстановитьПараметр("ТаблицаДанных", Объект.Показатели.Выгрузить());
	
	ТекстЗапроса	= "Выбрать * ПОМЕСТИТЬ ТаблицаДанных ИЗ &ТаблицаДанных КАК ТаблицаДанных;" + Символы.ПС;
	
	ТекстЗапроса	= ТекстЗапроса + 
						"ВЫБРАТЬ 
						|	ТаблицаДанных.Подразделение КАК Подразделение,
						|	ТаблицаДанных.Секция КАК Секция,
						|	ТаблицаДанных.СДДС КАК СДДС,
						|	ТаблицаДанных.НаправлениеДДС КАК НаправлениеДДС,
						|	1 КАК УровеньИерархии," + Символы.ПС
						+ ПолучитьДополнениеЗапросаПоМесяцам(Запрос, СтруктураСоответствийКолонокДатам) +  Символы.ПС
						+ " ИЗ ТаблицаДанных
						|	СГРУППИРОВАТЬ ПО
						|	Подразделение,
						|	Секция,
						|	СДДС,
						|	НаправлениеДДС
						|							
						|	ИТОГИ ПО
						|	Подразделение,
						|	Секция";
						Запрос.Текст	= ТекстЗапроса;
						
	Дерево	= Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗаполнитьУровниИерархии(Дерево, 1, СтруктураСоответствийКолонокДатам);
						
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПоказателей");
							
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУровниИерархии(Дерево, УровеньИерархии, СтруктураСоответствийКолонокДатам)
	

	Для Каждого Строка Из Дерево.Строки Цикл
		Если Строка.Строки.Количество() <> 0 Тогда
			Для Каждого Дата Из СтруктураСоответствийКолонокДатам Цикл
				Строка[Дата.Ключ] = 0;
			КонецЦикла;
		КонецЕсли;
		
		Строка.УровеньИерархии = УровеньИерархии;
		ЗаполнитьУровниИерархии(Строка, УровеньИерархии + 1, СтруктураСоответствийКолонокДатам);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДополнениеЗапросаПоМесяцам(Запрос, СтруктураСоответствийКолонокДатам)
	
	ДополнениеТекстаЗапроса	= "";
	СчетчикКолонок			= 1;
	
	Для Каждого Дата Из СтруктураСоответствийКолонокДатам Цикл
		ДополнениеТекстаЗапроса = ДополнениеТекстаЗапроса + " СУММА(ВЫБОР КОГДА ТаблицаДанных.Месяц = &Дата" + СчетчикКолонок +" ТОГДА Сумма ИНАЧЕ 0 КОНЕЦ) КАК " + Дата.Ключ + "," + Символы.ПС;
		Запрос.УстановитьПараметр("Дата" + СчетчикКолонок, Дата.Значение);
		СчетчикКолонок = СчетчикКолонок + 1;
	КонецЦикла;
	
	Возврат Лев(ДополнениеТекстаЗапроса, СтрДлина(ДополнениеТекстаЗапроса) - 2);
	
КонецФункции

&НаСервере
Процедура УдалитьКолонкиДат()
	
	Если Не ЗначениеЗаполнено(Объект.НачалоБюджета) ИЛИ Не ЗначениеЗаполнено(Объект.КонецБюджета) Или ПустаяСтрока(АдресХранилищаСтруктурыСоответствийКолонокДат) Тогда
		Возврат;                                                                                   
	КонецЕсли;
	
	СтруктураСоответствийКолонокДатам	= ПолучитьИзВременногоХранилища(АдресХранилищаСтруктурыСоответствийКолонокДат);
	
	ДеревоПоказателейЗначение = РеквизитФормыВЗначение("ДеревоПоказателей", Тип("ДеревоЗначений"));
	
	УсловноеОформление.Элементы.Очистить();
	
	УдаляемыеРеквизиты		= Новый Массив;
	Для Каждого КолонкаДаты Из СтруктураСоответствийКолонокДатам Цикл	
		Элементы.Удалить(Элементы.Найти("ДеревоЗначений" + КолонкаДаты.Ключ));
		УдаляемыеРеквизиты.Добавить("ДеревоЗначений" + КолонкаДаты.Ключ);
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(УдаляемыеРеквизиты);
	
	Для Каждого КолонкаДаты Из СтруктураСоответствийКолонокДатам Цикл	
		ДеревоПоказателейЗначение.Колонки.Удалить(ДеревоПоказателейЗначение.Найти(КолонкаДаты));	
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкиДат()
	
	Если Не ЗначениеЗаполнено(Объект.НачалоБюджета) ИЛИ Не ЗначениеЗаполнено(Объект.КонецБюджета) Тогда
		Возврат;                                                                                   
	КонецЕсли;
	
	СтруктураСоответствийКолонокДатам	= Новый Структура;
	
	ДеревоПоказателейЗначение = РеквизитФормыВЗначение("ДеревоПоказателей", Тип("ДеревоЗначений"));
	
	Дата	= Объект.НачалоБюджета;
	
	СчетчикКолонок 				= 1;
	ДобавляемыеРеквизиты		= Новый Массив;
	Пока Дата <= Объект.КонецБюджета Цикл
		ДеревоПоказателейЗначение.Колонки.Добавить("Колонка" + СчетчикКолонок);
		СтруктураСоответствийКолонокДатам.Вставить("Колонка" + СчетчикКолонок, Дата);
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Колонка" + СчетчикКолонок, Новый ОписаниеТипов("Число"), "ДеревоПоказателей"));
		
		Дата = НачалоМесяца(ДобавитьМесяц(Дата, 1));
		СчетчикКолонок = СчетчикКолонок + 1;
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	ЗначениеВРеквизитФормы(ДеревоПоказателейЗначение, "ДеревоПоказателей");
	
	//НовыйЭлементУФ	= УсловноеОформление.Элементы.Добавить();
	//НовыйЭлементУФ.Использование = Истина;
	//
	//ОтборЭлемента					= НовыйЭлементУФ.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//ОтборЭлемента.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеРавно;
	//ОтборЭлемента.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("УровеньИерархии");
	//ОтборЭлемента.ПравоеЗначение	= 3;
	//ОтборЭлемента.Использование		= Истина;
	//
	//НовыйЭлементУФ.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Для Счетчик = 1 По СчетчикКолонок - 1 Цикл
		НовыйЭлемент			= ЭтаФорма.Элементы.Добавить("ДеревоЗначенийКолонка" + Счетчик, Тип("ПолеФормы"), Элементы.ДеревоПоказателей);
		НовыйЭлемент.Вид		= ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок	= Формат(СтруктураСоответствийКолонокДатам["Колонка" + Счетчик], "ДФ='MMMM yy'");
		НовыйЭлемент.ПутьКДанным = "ДеревоПоказателей.Колонка" +  Счетчик;
		
		
		//ПолеЭлемента = НовыйЭлементУФ.Поля.Элементы.Добавить();
		//ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Колонка" + Счетчик);
		//ПолеЭлемента.Использование	= Истина;
	КонецЦикла;
	
	
	АдресХранилищаСтруктурыСоответствийКолонокДат = ПоместитьВоВременноеХранилище(СтруктураСоответствийКолонокДатам, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПерезаполнитьТЧПоказателей(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьТЧПоказателей(ТекущийОбъект)
	
	СтруктураСоответствийКолонокДатам	= ПолучитьИзВременногоХранилища(АдресХранилищаСтруктурыСоответствийКолонокДат);
	ДеревоПоказателейЗначение 			= РеквизитФормыВЗначение("ДеревоПоказателей", Тип("ДеревоЗначений"));
	
	ТекущийОбъект.Показатели.Очистить();
	
	Для Каждого СтрокаУровеньПодразделение Из ДеревоПоказателейЗначение.Строки Цикл
		Для Каждого СтрокаУровеньСекция Из СтрокаУровеньПодразделение.Строки Цикл
			Для Каждого ДетальныеДанные Из СтрокаУровеньСекция.Строки Цикл
				Для Каждого КолонкаДаты Из СтруктураСоответствийКолонокДатам Цикл	
					Если Не ДетальныеДанные[КолонкаДаты.Ключ] = 0 Тогда
						НоваяСтрока = ТекущийОбъект.Показатели.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ДетальныеДанные);
						НоваяСтрока.Сумма	= ДетальныеДанные[КолонкаДаты.Ключ];
						НоваяСтрока.Месяц	= КолонкаДаты.Значение;
					КонецЕсли;						
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСекцию(Команда)
	
	Если Элементы.ДеревоПоказателей.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока		= ДеревоПоказателей.НайтиПоИдентификатору(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());	
	
	ОписаниеОповещения	= Новый ОписаниеОповещения("ДобавитьСекциюЗавершение", ЭтаФорма);
	ПараметрыОткрытия	= Новый Структура("Владелец", ТекущаяСтрока.Подразделение);
	ОткрытьФорму("Справочник.Секции.Форма.ФормаВыбораПодчиненныхСекций", ПараметрыОткрытия, ЭтаФорма,,,, ОписаниеОповещения); 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСекциюЗавершение(Значение, Параметры) Экспорт
	
	Если Элементы.ДеревоПоказателей.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока				= ДеревоПоказателей.НайтиПоИдентификатору(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());	
	НоваяСтрока					= ТекущаяСтрока.ПолучитьЭлементы().Добавить();
	НоваяСтрока.Подразделение	= ТекущаяСтрока.Подразделение;
	НоваяСтрока.Секция			= Значение;
	
	Элементы.ДеревоПоказателей.Развернуть(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры
 
&НаКлиенте
Процедура ДобавитьСДДС(Команда)

	Если Элементы.ДеревоПоказателей.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока	= ДеревоПоказателей.НайтиПоИдентификатору(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());	
	Если Не ТекущаяСтрока.УровеньИерархии = 3 ИЛИ Не ТекущаяСтрока.УровеньИерархии = 2 Тогда
		Возврат
	КонецЕсли;
	
	ОписаниеОповещения	= Новый ОписаниеОповещения("ДобавитьСДДСЗавершение", ЭтаФорма);
	ПоказатьВводЗначения(ОписаниеОповещения, , , Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСДДСЗавершение(Значение, Параметры) Экспорт
	
	Если Элементы.ДеревоПоказателей.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока				= ДеревоПоказателей.НайтиПоИдентификатору(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());	
	Если ТекущаяСтрока.УровеньИерархии = 3 Тогда
		НоваяСтрока					= ТекущаяСтрока.ПолучитьРодителя().Добавить();
	ИначеЕсли ТекущаяСтрока.УровеньИерархии = 2 Тогда
		НоваяСтрока					= ТекущаяСтрока.ПолучитьЭлементы().Добавить();
	Иначе
		Возврат
	КонецЕсли;
	
	НоваяСтрока.Подразделение	= ТекущаяСтрока.Подразделение;
	НоваяСтрока.Секция			= ТекущаяСтрока.Секция;
	НоваяСтрока.СДДС			= Значение;
	НоваяСтрока.НаправлениеДДС	= ПредопределенноеЗначение("Перечисление.НаправленияДвиженийДенежныхСредств.Расход");
	
	Элементы.ДеревоПоказателей.Развернуть(Элементы.ДеревоПоказателей.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры
 
&НаКлиенте
Процедура ДобавитьПодразделение(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьПодразделениеЗавершение", ЭтаФорма);
	ПараметрыОткрытия	= Новый Структура("Родитель", Объект.Подразделение);
	ОткрытьФорму("Справочник.Подразделения.Форма.ФормаВыбораНеиерархическая", ПараметрыОткрытия, ЭтаФорма,,,, ОписаниеОповещения); 
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодразделениеЗавершение(Значение, Параметры) Экспорт
	
	НоваяСтрока					= ДеревоПоказателей.ПолучитьЭлементы().Добавить();
	НоваяСтрока.Подразделение	= Значение;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПоказателейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока Тогда
		ТекущаяСтрока					= ДеревоПоказателей.НайтиПоИдентификатору(Элемент.ТекущиеДанные.ПолучитьИдентификатор());	
		ТекущаяСтрока.НаправлениеДДС	= ПредопределенноеЗначение("Перечисление.НаправленияДвиженийДенежныхСредств.Расход");
	КонецЕсли;
	
КонецПроцедуры

