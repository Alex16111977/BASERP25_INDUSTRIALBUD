Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если Оплачено Тогда
		лДатаОплаты = ?( ЗначениеЗаполнено(ДатаОплаты), ДатаОплаты, Дата );
		
		Движения.ДенежныеСредства.Записывать = Истина;
		Движение = Движения.ДенежныеСредства.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = лДатаОплаты;
		Движение.ВидДенежныхСредств = Перечисления.ВидыДенежныхСредств.Безналичные;
		Движение.БанковскийСчетКасса = СчетОрганизации;
		Движение.Организация = Организация;
		Движение.Сумма = СуммаДокумента;
		
		Движения.БДДС.ВыполнитьДвижение(ЭтотОбъект);
		
		Если Расшифровка.Количество()>0 Тогда
			Движения.БДДС.Очистить();
			Для каждого Стр Из Расшифровка Цикл
				Движение = Движения.БДДС.Добавить();
				Если ЗначениеЗаполнено(ДатаОплаты) Тогда
					Движение.Период = ДатаОплаты;
				Иначе
					Движение.Период = Дата;
				КонецЕсли;
				
				Движение.Активность=Истина;
				Движение.ВидОплаты=Перечисления.ВидыДенежныхСредств.Безналичные;
				Движение.СтатьяДвиженияДенежныхСредств=Стр.СтатьяДвиженияДенежныхСредств;
				Движение.Подразделение=Стр.Подразделение;
				Движение.НаправлениеДДС	= Перечисления.НаправленияДвиженийДенежныхСредств.Приход;
				Движение.СуммаРасход=РаботаСКурсамиВалют.ПересчитатьВВалюту(Стр.Сумма,ВалютаДокумента,Константы.ВалютаРегламентированногоУчета.Получить(),Дата);
				Движение.Сумма=Движение.СуммаРасход;
				
				Если ЗначениеЗаполнено(Стр.Направление) Тогда
					Движение.Направление=Стр.Направление;
				Иначе	
					Движение.Направление=Направление;
				КонецЕсли;
				Движение.ДокументОснование=ДокументОснование;
			КонецЦикла;
		КонецЕсли;

		Движения.ДенежныеСредстваПроектов.ВыполнитьДвижение(ЭтотОбъект);
		
		//Сава 2016/10
		лПроект = ?(Подразделение.НеИспользовать, Подразделение, Подразделение.Родитель);//Если самый низ - берем уровень выше.
		Для Каждого лСтрока Из ДСПроектов Цикл 
			Движение 				= Движения.ВзаиморасчетыПоПроектам.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= лДатаОплаты;
			Движение.Активность		= Истина;
			Движение.Сумма 			= лСтрока.Сумма;
			Движение.Подразделение  = лПроект;
			Движение.ПодразделениеЗайма = лСтрока.Подразделение;
		КонецЦикла;
		Движения.ВзаиморасчетыПоПроектам.Записывать = Истина;
		
	КонецЕсли;
	
	
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗначениеЗаполнено(ДатаОплаты) Тогда
		Оплачено=Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтатьяДвиженияДенежныхСредств) И Не ЗначениеЗаполнено(ДокументОснование) Тогда
		СтатьяДвиженияДенежныхСредств = АХ_ОбщегоНазначения.ПолучитьСтатьюДДСПоУсловию(Подразделение, Контрагент, СуммаДокумента, "ППВ", Секция);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДСПроектов.Очистить();
	Ответственный    = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") Тогда
		// Заповнення шапки
		//БанковскийНомер = ДанныеЗаполнения.БанковскийНомер;
		ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
		ДатаВыгрузки = ДанныеЗаполнения.ДатаВыгрузки;
		ДатаЗагрузки = ДанныеЗаполнения.ДатаЗагрузки;
		ДатаОплаты = ТекущаяДатаСеанса();
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Контрагент = ДанныеЗаполнения.Контрагент;
		НазначениеПлатежа = ДанныеЗаполнения.НазначениеПлатежа;
		Направление = ДанныеЗаполнения.Направление;
		Оплачено = ДанныеЗаполнения.Оплачено;
		Организация = ДанныеЗаполнения.Организация; 
		ДокументОснование = ДанныеЗаполнения;
		//Ответственный = ДанныеЗаполнения.Ответственный;
		Подразделение = ДанныеЗаполнения.Подразделение;
		Секция = ДанныеЗаполнения.Секция;
		СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		СчетКонтрагента = ДанныеЗаполнения.СчетКонтрагента;
		СчетОрганизации = ДанныеЗаполнения.СчетОрганизации;
	КонецЕсли;    
	
	Ответственный    = Пользователи.ТекущийПользователь();
	
КонецПроцедуры
