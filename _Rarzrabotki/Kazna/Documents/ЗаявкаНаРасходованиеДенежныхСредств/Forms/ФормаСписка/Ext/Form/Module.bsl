&НаКлиенте
Процедура ВыводитьСчетаПриИзменении(Элемент)
	
	Если ВыводитьСчета = 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КОплате", 0, ВидСравненияКомпоновкиДанных.Больше,, Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КОплате", 0, ВидСравненияКомпоновкиДанных.Больше,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПлатежиНаСервере(МассивЗаявок)
	
	СозданоРКО = 0;
	СозданоППИ = 0;
	Для Каждого Строка Из МассивЗаявок Цикл
		
		//++ Павел Чистяков 20.09.2016 16:56:56 №272
		Если Строка.ВидОплаты=Перечисления.ВидыДенежныхСредств.Наличные Тогда
			//**ОбъектДокумента = Документы.ПриходныйКассовыйОрдер.СоздатьДокумент();
			ОбъектДокумента = Документы.РасходныйКассовыйОрдер.СоздатьДокумент();
		Иначе
			ОбъектДокумента = Документы.ПлатежноеПоручениеИсходящее.СоздатьДокумент();			
		КонецЕсли; 
		
		ОбъектДокумента.Заполнить(Строка);
		
		Если НЕ ЗначениеЗаполнено(ОбъектДокумента.СуммаДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Заявка " + Строка + " полностью оплачена", Строка);
			Продолжить;
		КонецЕсли; 
		
		ОбъектДокумента.Дата = ТекущаяДата();
		ОбъектДокумента.УстановитьНовыйНомер();
		
		Попытка 
			ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
			Если ТипЗнч(ОбъектДокумента)=Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
				СозданоРКО = СозданоРКО + 1;
			ИначеЕсли ТипЗнч(ОбъектДокумента)=Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее") Тогда
				СозданоППИ = СозданоППИ + 1;
			КонецЕсли; 
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Ошибка при проведении платежки: " + ОписаниеОшибки(), Строка);
		КонецПопытки;
		
	КонецЦикла;
	
	Если СозданоРКО>0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создано " + СозданоРКО + " документа Расходный Кассовый Ордер");
	КонецЕсли; 
	Если СозданоППИ>0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создано " + СозданоППИ + " документа Платежное Поручение Исходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПлатежиПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса<>КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	СоздатьПлатежиНаСервере(Новый ФиксированныйМассив(Элементы.Список.ВыделенныеСтроки));	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПлатежи(Команда)
	
	КоличествоЭлементов = Элементы.Список.ВыделенныеСтроки.Количество();
	Если КоличествоЭлементов>0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьПлатежиПродолжение", ЭтотОбъект),
			"Вы действительно хотите создать " + КоличествоЭлементов + " платежей?",
			РежимДиалогаВопрос.ДаНет);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	Результат = 0;
	ВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество()>0 Тогда
		Для Каждого Строка Из ВыделенныеСтроки Цикл
			Результат = Результат + Элементы.Список.ДанныеСтроки(Строка).КОплате;
		КонецЦикла;
	КонецЕсли; 
	Если Результат<>0 Тогда
		ТекстРезультата = "Всего выбрано к оплате: " + Формат(Результат, "ЧДЦ=2; ЧН=") + " грн.";
	Иначе
		ТекстРезультата = "";
	КонецЕсли 
КонецПроцедуры