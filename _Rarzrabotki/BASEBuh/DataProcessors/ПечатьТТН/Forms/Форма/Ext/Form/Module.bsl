////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Документ") И ЗначениеЗаполнено(Параметры.Документ) Тогда	
		Объект.Документ = Параметры.Документ;	
	КонецЕсли;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	МетаданныеОбработка = ОбработкаОбъект.Метаданные();	
	Для Каждого МетаданныеРеквизит Из МетаданныеОбработка.Реквизиты Цикл
		РеквизитыОбработки.Добавить(МетаданныеРеквизит.Имя);
	КонецЦикла;

	ДокументПриИзмененииНаСервере();         
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ВыбранноеЗначение <> Неопределено Тогда
		ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ДокументПриИзменении(Элемент)
	
	ДокументПриИзмененииНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ДокументПриИзмененииНаСервере()

	
	Если Не ВосстановитьИсториюПечати() Тогда
		
		УстановитьНомер();
		УстановитьМестоСоставления();
		УстановитьПункты();
		УстановитьТипЦен();
		
	КонецЕсли;
	УстановитьВидимостьДоступность(); 
	
	
КонецПроцедуры

&НаКлиенте
Процедура МассаГрузаПриИзменении(Элемент)
	РассчитатьОбщуюМассу(Объект);
КонецПроцедуры

&НаКлиенте
Процедура МассаТранспортаПриИзменении(Элемент)
	РассчитатьОбщуюМассу(Объект);
КонецПроцедуры  

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ЭтаФорма.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(Объект.Документ); 
	
	ПараметрыПечати = УправлениеПечатьюБПКлиент.ПолучитьЗаголовокПечатнойФормы(ПараметрКоманды);
	Для каждого Реквизит Из РеквизитыОбработки Цикл
		
		ИмяРеквизита = Реквизит.Значение;
		ПараметрыПечати.Вставить(ИмяРеквизита, Объект[ИмяРеквизита]);
		
	КонецЦикла;   

	СохранитьИсторию();
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьТТН",
		"ТТН",
		ПараметрКоманды,
		ЭтаФорма,
		ПараметрыПечати
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПараметрыДоставки(Команда)
	СохранитьПараметрыДоставкиНаСервере();
	ПоказатьОповещениеПользователя(НСтр("ru='Параметры доставки сохранены';uk= 'Параметри доставки збережено'"));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПараметрыДоставки(Команда)
	ОткрытьФорму("РегистрСведений.НастройкиЗаполненияТТН.ФормаСписка", Новый Структура("РежимВыбора", Истина), ЭтаФорма);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

&НаСервере
Процедура УстановитьНомер()
	
	Если ЗначениеЗаполнено(Объект.Документ) Тогда
		Объект.НомерТТН = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Объект.Документ.Номер, Истина, Истина);
	КонецЕсли;
	Если ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Объект.НомерТТН <> "" Тогда
		
		Объект.НомерТТН = "Р" + Объект.НомерТТН;
		
	ИначеЕсли ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") И Объект.НомерТТН <> "" Тогда
		
		Объект.НомерТТН = "П" + Объект.НомерТТН;
		
	Иначе
		
		Объект.НомерТТН = "";
	
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает тип цен.
//
&НаСервере
Процедура УстановитьТипЦен()
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда

	Иначе
		ТипЦен = Неопределено;
	КонецЕсли;

КонецПроцедуры // УстановитьТипЦен()

// Процедура устанавливает видимость и доступность реквизитов.
//
&НаСервере
Процедура УстановитьВидимостьДоступность()

	Если ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ДоступностьТипаЦен = Истина;
	Иначе
		ДоступностьТипаЦен = Ложь;
	КонецЕсли;
	
	Элементы.ТипЦен.Видимость = ДоступностьТипаЦен;

КонецПроцедуры // УстановитьВидимостьДоступность()

&НаСервере
Процедура УстановитьМестоСоставления()
	
	Если ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда

		Объект.МестоСоставленияДокумента = СокрЛП(Объект.Документ.МестоСоставленияДокумента);
		
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.МестоСоставленияДокумента) Тогда
		Объект.МестоСоставленияДокумента = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеМестоСоставленияДокумента");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьПункты()
	
	Если ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда

		Объект.ПунктПогрузки = Объект.Документ.Склад.Местонахождение;           
		Объект.ПунктРазгрузки = Объект.Документ.АдресДоставки; 

		Если Не ЗначениеЗаполнено(Объект.ПунктРазгрузки) Тогда
	 		Объект.ПунктРазгрузки = БухгалтерскийУчетПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(Объект.Документ.Контрагент, "Фактический");
		КонецЕсли;

	ИначеЕсли ТипЗнч(Объект.Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда

		Объект.ПунктПогрузки = Объект.Документ.СкладОтправитель.Местонахождение;
		Объект.ПунктРазгрузки = Объект.Документ.СкладПолучатель.Местонахождение;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПунктПогрузки) И ЗначениеЗаполнено(Объект.Документ) Тогда
		Объект.ПунктПогрузки = БухгалтерскийУчетПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(Объект.Документ.Организация, "Фактический");
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьОбщуюМассу(Объект)
	Объект.МассаОбщая = Объект.МассаГруза + Объект.МассаТранспорта;
КонецПроцедуры

&НаСервере
Функция ВосстановитьИсториюПечати()
	
	Если Не ЗначениеЗаполнено(Объект.Документ) Тогда
		Возврат Ложь;
	КонецЕсли;

	Успех = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияПечатиТТН.НастройкиПечати КАК НастройкиПечати
	|ИЗ
	|	РегистрСведений.ИсторияПечатиТТН КАК ИсторияПечатиТТН
	|ГДЕ
	|	ИсторияПечатиТТН.Документ = &Документ";
	Запрос.УстановитьПараметр("Документ", Объект.Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Попытка
			
			СтруктураПараметров = Выборка.НастройкиПечати.Получить(); 
			ЗаполнитьЗначенияСвойств(Объект, СтруктураПараметров);
			Успех = Истина;
			
		Исключение
		КонецПопытки;
		
		
	КонецЕсли;

	Возврат Успех;
	
КонецФункции

&НаСервере
Процедура СохранитьИсторию()

	Если НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ИсторияПечатиТТН) Тогда
		Возврат;
	КонецЕсли;
	
	МЗ = РегистрыСведений.ИсторияПечатиТТН.СоздатьМенеджерЗаписи();
	
	ПараметрыИстории = Новый Структура;
	Для каждого Реквизит Из РеквизитыОбработки Цикл
		
		ИмяРеквизита = Реквизит.Значение;
		Если ИмяРеквизита = "Документ" Тогда
			МЗ.Документ = Объект.Документ;
		Иначе
			ПараметрыИстории.Вставить(ИмяРеквизита, Объект[ИмяРеквизита]);
		КонецЕсли;
		
	КонецЦикла;  
	
	МЗ.НастройкиПечати = Новый ХранилищеЗначения(ПараметрыИстории);
    МЗ.Записать();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПараметрыДоставкиНаСервере()
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиЗаполненияТТН.ИД КАК ИД
	|ИЗ
	|	РегистрСведений.НастройкиЗаполненияТТН КАК НастройкиЗаполненияТТН
	|{ГДЕ
	|	НастройкиЗаполненияТТН.МаркаИГосНомерАвтомобиля,
	|	НастройкиЗаполненияТТН.ГосНомерПрицепа,
	|	НастройкиЗаполненияТТН.Водитель,
	|	НастройкиЗаполненияТТН.ВодительскоеУдостоверение,
	|	НастройкиЗаполненияТТН.УНЗРВодителя,
	|	НастройкиЗаполненияТТН.ДолжностьВодителя,
	|	НастройкиЗаполненияТТН.Перевозчик,
	|	НастройкиЗаполненияТТН.ВидПеревозки,
	|	НастройкиЗаполненияТТН.Длина,
	|	НастройкиЗаполненияТТН.Ширина,
	|	НастройкиЗаполненияТТН.Высота,
	|	НастройкиЗаполненияТТН.МассаТранспорта}";
	
	Для каждого Рекв Из Метаданные.РегистрыСведений.НастройкиЗаполненияТТН.Реквизиты Цикл
		Запрос.Отбор.Добавить(Рекв.Имя).Установить(Объект[Рекв.Имя]);
	КонецЦикла;
	
	Запрос.Выполнить();
	Если Запрос.Результат.Пустой() Тогда
		
		МЗ = РегистрыСведений.НастройкиЗаполненияТТН.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МЗ, Объект);
		МЗ.ИД = Новый УникальныйИдентификатоР;
		МЗ.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("РегистрСведенийКлючЗаписи.НастройкиЗаполненияТТН") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "Выбрать * ИЗ РегистрСведений.НастройкиЗаполненияТТН КАК НастройкиЗаполненияТТН Где НастройкиЗаполненияТТН.ИД = &ИД";
		Запрос.УстановитьПараметр("ИД", ВыбранноеЗначение.ИД);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Объект, Выборка);
			РассчитатьОбщуюМассу(Объект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
