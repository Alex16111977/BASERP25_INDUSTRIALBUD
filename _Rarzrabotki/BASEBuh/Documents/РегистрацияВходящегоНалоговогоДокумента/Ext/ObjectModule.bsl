#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Перем мВалютаРегламентированногоУчета;


/////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА


// Процедура определяет параметры регл. учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	СтруктураШапкиДокумента.Вставить("ЕстьНДС"                        , УчетнаяПолитика.ПлательщикНДС(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитикиРегл()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, МассивНепроверяемыхРеквизитов)
	
	Если  НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
		СтрокаСообщения = НСтр("ru='Фирма не является плательщиком НДС!';uk='Фірма не є платником ПДВ!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект,"Организация",, Отказ);	
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ПогашениеНалоговогоВекселя Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаНДС");	
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаВексель");		
	КонецЕсли;
	
	Если Дата < '2015-07-01' Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетНДСУсловнаяПродажа");	
	КонецЕсли;
	
	Если НЕ	 ((ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат) 
		      ИЛИ (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка))	
		 ИЛИ (Дата < '2015-07-01')Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаКорректируемогоВходящегоДокумента");		 
	КонецЕсли;
	
	
	Если НЕ СтруктураШапкиДокумента.ВедениеВзаиморасчетовНУ = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Сделка");	
	КонецЕсли;

	Если НЕ ВключаетсяВУточняющийРасчет Тогда
		МассивНепроверяемыхРеквизитов.Добавить("УточняемыйПериод");
	КонецЕсли;
	
	Если    ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки 
		ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Сделка");
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаНДС");
	Иначе	
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаКорректировкиНДСКредит");
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита 
		И НЕ (СуммаДокумента = 0 И СуммаНДСДокумента = 0) Тогда
	
		СтрокаСообщения = НСтр("ru='Для данного вида операции сумма документа и сумма НДС должна быть нулевой!';uk='Для даного виду операції сума документа та ПДВ повинна дорівнювати нулю!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект,"ВидОперации",, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблиица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДС",     ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаТоваров.Колонки.Добавить("ВидДеятельностиНДС",   Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДеятельностиНДС"));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСВал",     		ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаТоваров.Колонки.Добавить("СуммаВал",     		ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСПропорционально", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСУсловнойПродажи", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		// движения по регистру ОжидаемыйИПодтвержденныйНДСПриобретений могут быть только от резидентов
		// и только в гривне.
		СтрокаТаблицы.СуммаНДСВал = СтрокаТаблицы.СуммаНДС;
		СтрокаТаблицы.СуммаВал 	  = СтрокаТаблицы.Сумма;
		
		// Сумма налогового кредита 
		СтрокаТаблицы.ПроводкиСуммаНДС = СтрокаТаблицы.СуммаНДС;
		// Обнулим эту сумму, если отсутствует право на налоговый кредит	
		Если  НЕ (СтрокаТаблицы.ДляХозяйственнойДеятельности И СтрокаТаблицы.ДляОперацийОблагаемыхНДС) Тогда
			СтрокаТаблицы.ПроводкиСуммаНДС = 0;
		КонецЕсли;
		
		Если СтрокаТаблицы.ПропорциональныйНДС Тогда
		    СтрокаТаблицы.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая;	
			Если СтрокаТаблицы.ДляОперацийОблагаемыхНДС Тогда
				СтрокаТаблицы.СуммаНДСПропорционально =  СтрокаТаблицы.СуммаНДСВал
			КонецЕсли;
		ИначеЕсли СтрокаТаблицы.ДляОперацийОблагаемыхНДС Тогда
			СтрокаТаблицы.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.Облагаемая;	
		Иначе	
			СтрокаТаблицы.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.Необлагаемая;				
		КонецЕсли;
		
		// с 2015 года, если дата документ и дата вх. нал. документа после 07.2015 - на налоговый кредит относятся все суммы вх. НДС, однако
		// если приобретение осуществляется для не облагаемых/не хоз. операций, необходимо оформить условную продажу
		Если Дата >= '2015-07-01' Тогда
			
			Если СтрокаТаблицы.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.Необлагаемая Тогда
			
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.СуммаНДС;	
				
			ИначеЕсли  СтрокаТаблицы.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая 
				     И НЕ СтрокаТаблицы.ДляОперацийОблагаемыхНДС Тогда
					 
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.СуммаНДС;	
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(СтруктураШапкиДокумента, Отказ, МассивНепроверяемыхРеквизитов)
	
	// проверим допустимость указания пропорционального ндс
	Если Не Отказ Тогда
		
		Для каждого Строка Из Товары Цикл
			
			Префикс = "Товары[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Префикс, Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ="));
				
			Если  Строка.ПропорциональныйНДС Тогда
				
				Поле = Префикс + "СтатьяДекларацииНДСНалоговыйКредит";
				Если НЕ Строка.СтавкаНДС = Перечисления.СтавкиНДС.НДС20
				   И НЕ Строка.СтавкаНДС = Перечисления.СтавкиНДС.НДС14
				   И НЕ Строка.СтавкаНДС = Перечисления.СтавкиНДС.НДС7
				   И НЕ Строка.СтавкаНДС = Перечисления.СтавкиНДС.НДС0  Тогда
				
					Если    Строка.СтатьяДекларацииНДСНалоговыйКредит = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорционально 
						ИЛИ Строка.СтатьяДекларацииНДСНалоговыйКредит.Родитель = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорционально Тогда
						
						СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке ""%1"" указан признак пропорционального НДС для ставки НДС отличной от 20%, 14%, 7% или 0%! Статья декларации НДС(н/к) в этом случае не должна входить в группу ""%2""!.';uk='У рядку ""%1"" зазначена ознака пропорційного ПДВ для ставки ПДВ яка відмінна від 20%, 14%, 7% або 0%! Стаття декларації ПДВ(п/к) в цьому випадку не повинна входити в групу ""%2""!.'"), Строка.НомерСтроки, Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорционально);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, Поле, , Отказ);
						
					КонецЕсли;
					
				КонецЕсли;
				
				Поле = Префикс + "ДляХозяйственнойДеятельности";
				Если НЕ Строка.ДляХозяйственнойДеятельности Тогда
					
					СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке ""%1"" указан признак пропорционального НДС! В этом случае должен быть установлен признак использования в хозяйственной деятельности!.';uk='У рядку ""%1"" зазначена ознака пропорційного ПДВ! У цьому випадку повинна бути встановлена ознака використання в господарській діяльності!'"), Строка.НомерСтроки);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, Поле, , Отказ);
					
				КонецЕсли;			
				
			КонецЕсли;	
			
		КонецЦикла; 
		
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	
	// Проводки по бухгалтерскому учету НДС 
	ПроводкиБУ = Движения.Хозрасчетный;

	// ТОВАРЫ
	ТаблицаДвижений = ТаблицаПоТоварам.Скопировать();
	//ТаблицаДвижений.Свернуть("СчетУчетаНДС","ПроводкиСуммаНДС");
	ТаблицаДвижений.Свернуть("СчетУчетаНДС","ПроводкиСуммаНДС,СуммаНДСУсловнойПродажи");

	Корректировка = ((ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат) 
			     ИЛИ (ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка));
	
	СодержаниеПроводки = НСтр("ru='НДС: налоговый кредит: ';uk='ПДВ: податковий кредит: '",Локализация.КодЯзыкаИнформационнойБазы());
	Если 	 ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная Тогда
		СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='налоговая накладная';uk='податкова накладна'",Локализация.КодЯзыкаИнформационнойБазы());
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек Тогда 
		СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='подтверждение (товарный чек)';uk='підтвердження (товарний чек)'",Локализация.КодЯзыкаИнформационнойБазы());
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат Тогда
		СодержаниеПроводки = НСтр("ru='НДС: налоговые обязательства: расчет корректировки (возврат)';uk=""ПДВ: податкові зобов'язання: розрахунок коригування (повернення)""",Локализация.КодЯзыкаИнформационнойБазы());       	
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка Тогда
		СодержаниеПроводки = НСтр("ru='НДС: налоговые обязательства: расчет корректировки (изменение суммы компенсации)';uk=""ПДВ: податкові зобов'язання: розрахунок коригування (зміна суми компенсації)""",Локализация.КодЯзыкаИнформационнойБазы());       
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
		СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='работы от нерезидента';uk='роботи від нерезидента'",Локализация.КодЯзыкаИнформационнойБазы());
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.BeксeльПpoшлoгoПepиoдa Тогда		
		СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='вексель, погашенный в прошлом периоде';uk='вексель, погашений у минулому періоді'",Локализация.КодЯзыкаИнформационнойБазы());
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки Тогда		
	    Если СтруктураШапкиДокумента.ВключаетсяВУточняющийРасчет = Ложь Тогда
			СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='исправление ошибки в текущем периоде';uk='виправлення помилки у поточному періоді'");
		Иначе
			СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='корректировка через уточняющий расчет';uk='коригування через уточнюючий розрахункок'");			
		КонецЕсли;
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита Тогда		
	    Если СтруктураШапкиДокумента.ВключаетсяВУточняющийРасчет = Ложь Тогда
			СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='восстановление налогового кредита в текущем периоде';uk='відновлення податкового кредиту поточному періоді'");
		Иначе
			СодержаниеПроводки = СодержаниеПроводки+ НСтр("ru='восстановление налогового кредита через уточняющий расчет';uk='відновлення податкового кредиту через уточнюючий розрахункок'");			
		КонецЕсли;
	КонецЕсли;		
	
	Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		
		Если    СтрокаТаблицы.ПроводкиСуммаНДС <> 0 Тогда
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = СтруктураШапкиДокумента.Дата;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.ПроводкиСуммаНДС;
			Проводка.Содержание                 = СодержаниеПроводки;
			Проводка.НомерЖурнала               = "";

				
			Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ПогашениеНалоговогоВекселя Тогда
					
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетУчетаВексель;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, ВексельСубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, ВексельСубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, ВексельСубконто3);

			
				Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетНДС;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СубконтоДт1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СубконтоДт2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СубконтоДт3);
					
			Иначе	
					
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетНДС;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СубконтоДт1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СубконтоДт2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СубконтоДт3);

			
				Если    ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки
					ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита  Тогда
					
					Проводка.СчетКт= СтруктураШапкиДокумента.СчетУчетаКорректировкиНДСКредит;
					
				Иначе
				
					Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаНДС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры"   , СтруктураШапкиДокумента.ДоговорКонтрагента);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДокументыРасчетовСКонтрагентами", Документы.РегистрацияВходящегоНалоговогоДокумента.ОпределитьСделкуЛокально(ЭтотОбъект));
				
				КонецЕсли;
					
			КонецЕсли;	
					
		КонецЕсли;

	КонецЦикла;
	
	ДвиженияПоРегиструОжидаемыйИПодтвержденныйНДСПриобретений(СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок);
	
	ДвиженияПоРегиструНДСНалоговыйКредит( СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок);

	// с 2015 года, если дата документ и дата вх. нал. документа после 07.2015 - на налоговый кредит относятся все суммы вх. НДС, однако
	// если приобретение осуществляется для не облагаемых/не хоз. операций, необходимо оформить условную продажу
	Если    (НЕ Корректировка И ДатаВходящегоДокумента 				  >= '2015-07-01' И Дата >= '2015-07-01')
		ИЛИ (   Корректировка И ДатаКорректируемогоВходящегоДокумента >= '2015-07-01' И Дата >= '2015-07-01') Тогда
		
		Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
			
			Если    СтрокаТаблицы.СуммаНДСУсловнойПродажи <> 0 Тогда
				
				Проводка = ПроводкиБУ.Добавить();

				Проводка.Период                     = СтруктураШапкиДокумента.Дата;
				Проводка.Активность                 = Истина;
				Проводка.Организация                = СтруктураШапкиДокумента.Организация;
				Проводка.Сумма                      = СтрокаТаблицы.СуммаНДСУсловнойПродажи;
				Проводка.Содержание                 = НСтр("ru='Налоговый кредит по операциям не обл. НДС после 07.2015';uk='Податковий кредит за операціями не опод. ПДВ після 07.2015'", Локализация.КодЯзыкаИнформационнойБазы());
				Проводка.НомерЖурнала               = "";

					
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетНДС;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СубконтоДт1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СубконтоДт2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СубконтоДт3);

				Проводка.СчетКт= СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;
					
			КонецЕсли;

		КонецЦикла;
		
		ДвиженияПоРегиструОжидаемыйИПодтвержденныйНДСПродаж(СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок);
	
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДвиженияПоРегиструОжидаемыйИПодтвержденныйНДСПриобретений(СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок)

	Если  ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная 
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек 
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка Тогда
		
		// В этом случае движения по регистру НДСПокупок этом документом не формируются
		Возврат
		
	КонецЕсли;
	
	НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПриобретений;
	
	// Получим таблицу значений, совпадающую со струкутрой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	ТаблицаДвиженийОплата 	  = ТаблицаДвижений.Скопировать();
	ТаблицаДвиженийТара 	  = ТаблицаДвижений.Скопировать();
	
	// ТОВАРЫ
	ТаблицаКопия = ТаблицаПоТоварам.Скопировать();                                                                                           
	ТаблицаКопия.Свернуть("СтавкаНДС,ДляХозяйственнойДеятельности,ВидДеятельностиНДС,ВозвратнаяТара,Амортизируется", "СуммаВал, СуммаНДСВал, СуммаНДСПропорционально");
	
	ТаблицаКопия.Колонки.СуммаВал.Имя    = "БазаНДС";
	ТаблицаКопия.Колонки.СуммаНДСВал.Имя = "СуммаНДС";
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(Организация       , "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(ДоговорКонтрагента, "ДоговорКонтрагента");
	ТаблицаДвижений.ЗаполнитьЗначения(Документы.РегистрацияВходящегоНалоговогоДокумента.ОпределитьСделкуЛокально(ЭтотОбъект), "Сделка");
	
	Если ВидОперации =  Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат Тогда
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПриобретений.Возврат, "СобытиеНДС");
		
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПриобретений.КорректировкаНалоговыйДокумент, "КодОперации");
		
		// Инвертируем суммы
		Для Каждого Строка Из ТаблицаДвижений Цикл
			Строка.БазаНДС	   = -Строка.БазаНДС;
			Строка.СуммаНДС    = -Строка.СуммаНДС;
			Строка.СуммаНДСПропорционально = -Строка.СуммаНДСПропорционально;
		КонецЦикла;
		
	Иначе
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПриобретений.Поступление, "СобытиеНДС");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПриобретений.ПодтвержденныйНДС,  "КодОперации");
	КонецЕсли;		
	
	// ТАРА
	Если     СтруктураШапкиДокумента.СуммаВозвратнойТары <> 0 
		И НЕ ВидОперации =  Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат 
		И НЕ ВидОперации =  Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка Тогда
		
		СтрокаДвиженийПоТаре = ТаблицаДвиженийТара.Добавить();
		СтрокаДвиженийПоТаре.СтавкаНДС 						= Перечисления.СтавкиНДС.НеНДС;
		СтрокаДвиженийПоТаре.ДляХозяйственнойДеятельности 	= Истина;
		СтрокаДвиженийПоТаре.ВидДеятельностиНДС 			= Перечисления.ВидыДеятельностиНДС.Необлагаемая;
		СтрокаДвиженийПоТаре.ВозвратнаяТара 				= Истина;
		СтрокаДвиженийПоТаре.БазаНДС 						= СтруктураШапкиДокумента.СуммаВозвратнойТары;
		
		ТаблицаДвиженийТара.ЗаполнитьЗначения(Организация       , "Организация");
		ТаблицаДвиженийТара.ЗаполнитьЗначения(ДоговорКонтрагента, "ДоговорКонтрагента");
		ТаблицаДвиженийТара.ЗаполнитьЗначения(Документы.РегистрацияВходящегоНалоговогоДокумента.ОпределитьСделкуЛокально(ЭтотОбъект), "Сделка");
		ТаблицаДвиженийТара.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПриобретений.Поступление, "СобытиеНДС");
		ТаблицаДвиженийТара.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПриобретений.ПодтвержденныйНДС,  "КодОперации");
		
	КонецЕсли; 
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.ОжидаемыйИПодтвержденныйНДСПриобретений.ВыполнитьРасход();
	
	КонецЕсли;

	Если НЕ Отказ И ТаблицаДвиженийТара.Количество() > 0 Тогда
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвиженийТара;
		
		Движения.ОжидаемыйИПодтвержденныйНДСПриобретений.ВыполнитьРасход();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияПоРегиструНДСНалоговыйКредит(СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок);
  		
	НаборДвижений = Движения.НДСНалоговыйКредит;
	
	// Получим таблицу значений, совпадающую со струкутрой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	ТаблицаКопия = ТаблицаПоТоварам.Скопировать();

	ТаблицаКопия.Свернуть("СтавкаНДС, СтатьяДекларацииНДСНалоговыйКредит", "Сумма, СуммаНДС");
	
	ТаблицаКопия.Колонки.Сумма   .Имя = "БазаНДС";
	ТаблицаКопия.Колонки.СуммаНДС.Имя = "НДС";
	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(Организация       , "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(ДоговорКонтрагента, "ДоговорКонтрагента");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийНДСНалоговыйКредит.ПервичныйДокумент, "КодОперации");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.НДСНалоговыйКредит.ДобавитьДвижение();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияПоРегиструОжидаемыйИПодтвержденныйНДСПродаж(СтруктураШапкиДокумента, ТаблицаПоТоварам,  Отказ, Заголовок)

	Если  ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная 
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек 
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка
		И ВидОперации <> Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки		Тогда
		
		// В этом случае движения по регистру НДСПокупок этом документом не формируются
		Возврат
		
	КонецЕсли;
	
	НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПродаж;
	
	// Получим таблицу значений, совпадающую со струкутрой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	// ТОВАРЫ
	ТаблицаКопия = ТаблицаПоТоварам.Скопировать();                                                                                          
	Сч = 0;
	Пока Сч < ТаблицаКопия.Количество() Цикл
	
		СтрокаТоваров = ТаблицаКопия[Сч];	
		Если СтрокаТоваров.СуммаНДСУсловнойПродажи = 0 Тогда
			ТаблицаКопия.Удалить(СтрокаТоваров);                      
		Иначе
			Сч = Сч + 1;
		КонецЕсли;	
	
	КонецЦикла;
	
	ТаблицаКопия.Свернуть("СтавкаНДС", "СуммаВал, СуммаНДСУсловнойПродажи");
	
	ТаблицаКопия.Колонки.СуммаВал.Имя    			 = "БазаНДС";
	ТаблицаКопия.Колонки.СуммаНДСУсловнойПродажи.Имя = "СуммаНДС";
	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(Организация       , "Организация");
	
	Если ВидОперации =  Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат Тогда
		
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажаВозврат, "СобытиеНДС");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС, "КодОперации");
		
		// Инвертируем суммы
		Для Каждого Строка Из ТаблицаДвижений Цикл
			Строка.БазаНДС	   = -Строка.БазаНДС;
			Строка.СуммаНДС    = -Строка.СуммаНДС;
		КонецЦикла;
		
	ИначеЕсли ВидОперации =  Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки Тогда
		
		Для каждого Строка Из ТаблицаДвижений Цикл
			
			Если Строка.БазаНДС < 0 Тогда
				Строка.БазаНДС	   = -Строка.БазаНДС;
				Строка.СуммаНДС    = -Строка.СуммаНДС;
				Строка.СобытиеНДС  = Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажаВозврат;
			Иначе	
				Строка.СобытиеНДС  = Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажа;
			КонецЕсли;
		
		КонецЦикла;
		
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС, "КодОперации");
		
	Иначе
		
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажа,    "СобытиеНДС");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС,  "КодОперации");
		
	КонецЕсли;		
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.ОжидаемыйИПодтвержденныйНДСПродаж.ВыполнитьПриход();
	
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента) Экспорт
	
	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;
	
	
	СтруктураШапкиДокумента = ОбщегоНазначенияРед12.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке      = ОбщегоНазначенияРед12.СформироватьДеревоПолейЗапросаПоШапке();
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке( ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "Организация"         , "ДоговорОрганизация");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке( ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВидДоговора"         , "ВидДоговора");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВедениеВзаиморасчетов"                   , "ВедениеВзаиморасчетов");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВедениеВзаиморасчетовНУ"                 , "ВедениеВзаиморасчетовНУ");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "СложныйНалоговыйУчет"					   , "СложныйНалоговыйУчет");

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
КонецПроцедуры // ПодготовитьСтруктуруШапкиДокумента()

// Процедура формирует таблицы документа.
//
Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок) Экспорт
	
	// Получим необходимые данные для проведения и проверки заполенения по табличной части "Товары".
	СтруктураПолей = Новый Структура();
	СтруктураПростыхПолей = Новый Структура();
	СтруктураСложныхПолей = Новый Структура();
	
	СтруктураПолей.Вставить("Сумма"                     , "Сумма");
	СтруктураПолей.Вставить("СтавкаНДС"                 , "СтавкаНДС");
	СтруктураПолей.Вставить("СуммаНДС"					, "СуммаНДС");
	СтруктураПолей.Вставить("ДляХозяйственнойДеятельности", "ДляХозяйственнойДеятельности");
	СтруктураПолей.Вставить("ДляОперацийОблагаемыхНДС"	, "ДляОперацийОблагаемыхНДС");
	СтруктураПолей.Вставить("Амортизируется"			, "Амортизируется");
	СтруктураПолей.Вставить("ПропорциональныйНДС"		, "ПропорциональныйНДС");
	СтруктураПолей.Вставить("СтатьяДекларацииНДСНалоговыйКредит"  	, "СтатьяДекларацииНДСНалоговыйКредит");
	СтруктураПростыхПолей.Вставить("ВозвратнаяТара"     , Ложь);
	СтруктураПолей.Вставить("СчетУчетаНДС"    	, "Ссылка.СчетУчетаНДС");
	
	РезультатЗапросаПоТоварам = ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей, СтруктураПростыхПолей, СтруктураСложныхПолей);

	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ


// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	ИначеЕсли ДанныеЗаполнения <> Неопределено 
		И ТипДанныхЗаполнения = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("Основание") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
		
		Если ДанныеЗаполнения.Свойство("ДоговорСделка") Тогда
			ДоговорСделка = ДанныеЗаполнения.ДоговорСделка;
		Иначе	
		    ДоговорСделка = Неопределено;
		КонецЕсли; 
		ДанныеЗаполнения = ДанныеЗаполнения.Основание;
		
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения, ДоговорСделка);
	КонецЕсли;
	// Предварительно необходимо предоставить возможность пользователю выбрать договор и сделку, по которым необходимо сформировать документ
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("НесколькоДоговоровСделок") И ЭтотОбъект.ДополнительныеСвойства.НесколькоДоговоровСделок Тогда
		Возврат;
	КонецЕсли; 

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Если НЕ ЗначениеЗаполнено(ОбособленноеПодразделение) Тогда
			
			ОбособленноеПодразделение = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеОбособленноеПодразделениеОрганизации");	
			
			Если НЕ ОбособленноеПодразделение.Владелец = Организация Тогда
				ОбособленноеПодразделение = Неопределено;				
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Контрагент)
		И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьСчетаУчетаРасчетов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(Основание, ДоговорСделка = Неопределено)	
	
	Если ДоговорСделка = Неопределено Тогда
		ТаблицаДоговоров   = Неопределено;
		ДоговорСделка 	   = Документы.РегистрацияВходящегоНалоговогоДокумента.ОпределитьДоговорСделку(Основание, ТаблицаДоговоров);
		Если ТипЗнч(ТаблицаДоговоров) = Тип("ТаблицаЗначений") И ТаблицаДоговоров.Количество() > 1 Тогда
			// Если несколько договоров, то необходимо предоставить выбор договора и сделки, по которой формировать документ
			ЭтотОбъект.ДополнительныеСвойства.Вставить("НесколькоДоговоровСделок", Истина);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
	ДокументОснование  = Основание;
	
	ДоговорКонтрагента = ДоговорСделка.ДоговорКонтрагента;
	Сделка 			   = ДоговорСделка.Сделка;
	
	// в случае Авансового отчета контрагент может быть переопределен
	Контрагент = ДоговорКонтрагента.Владелец;

	СхемыНалогообложения = РегистрыСведений.СхемыНалогообложенияКонтрагентов.СрезПоследних(Дата, Новый Структура("Контрагент", Контрагент));
	Если СхемыНалогообложения.Количество() = 0 Тогда
		ПлательщикНДС = Истина;
	Иначе
		ПлательщикНДС = СхемыНалогообложения[0].СхемаНалогообложения.НДС;
	КонецЕсли;

	Если  ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаОжидаемогоИПодтвержденногоНДС")  Тогда
	  
	  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки;	
	  ВключаетсяВУточняющийРасчет = истина;
	  Возврат;
	  
  	ИначеЕсли  ТипЗнч(Основание) = Тип("ДокументСсылка.ИзменениеНалоговогоНазначенияЗапасов") 
	  	 ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ИзменениеНалоговогоНазначенияТЗР") Тогда
	  
	  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ВосстановлениеНалоговогоКредита;	
	  Возврат;
	  
	ИначеЕсли  ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет") Тогда
		// возврат
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") 
			 И (НЕ  Основание.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом) Тогда

		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат;
	
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		  ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		  
		Если ПлательщикНДС  Тогда
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		Иначе
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")  
		 И НЕ (Основание.ВалютаДокумента = мВалютаРегламентированногоУчета) Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода;
		
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоРаботамОтНерезидента(ЭтотОбъект);
		// стандартного заполнения не будет.
		Возврат;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")  
		 И НЕ (Основание.ВалютаДокумента = мВалютаРегламентированногоУчета) Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода;
		
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоУслугамКомиссииОтНерезидента(ЭтотОбъект);
		// стандартнго заполнения не будет.
		Возврат;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
			 И Основание.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда

		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоКомиссионнойТорговлеНалоговыйКодекс(ЭтотОбъект);
		// стандартнго заполнения не будет.
		Возврат;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
			 И Основание.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда

		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат;
		
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоКомиссионнойТорговлеНалоговыйКодекс(ЭтотОбъект);
		// стандартнго заполнения не будет.
		Возврат;
		
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка;
		
		Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоКомиссионнойТорговлеНалоговыйКодекс(ЭтотОбъект);
		
		Возврат	
		
	Иначе
		
		Если ПлательщикНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		Иначе
			//покупка у неплательщика НДС. Основание включения в книгу - товарний чек (п. 10.2 Порядка)
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек;
		КонецЕсли;
		
	КонецЕсли;
	
	Дата = Основание.Дата;
	
	ДатаВходящегоДокумента = Дата;
	
	Документы.РегистрацияВходящегоНалоговогоДокумента.ЗаполнитьПоДокументуОснованиюСУчетомОстатков(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента 	  = Товары.Итог("СуммаВзаиморасчетов");
	СуммаНДСДокумента = Товары.Итог("СуммаНДС");

	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Если ДоговорКонтрагента.ВалютаВзаиморасчетов <> мВалютаРегламентированногоУчета Тогда
			// Договор - внешнеэкономический
			СуммаВозвратнойТары = 0
		КонецЕсли;
	КонецЕсли;
	
	Если СчетНДСУсловнаяПродажа.Пустая() Тогда
		СчетНДСУсловнаяПродажа = ПланыСчетов.Хозрасчетный.УсловнаяПродажа;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	Ответственный = Пользователи.ТекущийПользователь();
	
	ДатаПолучения = '0001-01-01';
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Перем Заголовок, СтруктураШапкиДокумента;
	Перем ТаблицаПоТоварам;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = НСтр("ru='Проведение документа ""';uk='Проведення документа ""'") + СокрЛП(Ссылка) + """: ";

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента);
	
	// Получим данные учетной политики
	ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок);
		
    ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);

	// Движения по документу
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	КонецЕсли;
	
	Движения.Хозрасчетный.ВыполнитьДействияПередЗаписьюДвижений();
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);

КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	СтруктураШапкиДокумента		  = Новый Структура;
	
	СтруктураШапкиДокумента.Вставить("ЕстьНДС",					 УчетнаяПолитика.ПлательщикНДС(Организация, Дата));
	СтруктураШапкиДокумента.Вставить("ВедениеВзаиморасчетовНУ",  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВедениеВзаиморасчетовНУ"));
	
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, МассивНепроверяемыхРеквизитов);
	
	ПроверитьЗаполнениеТабличнойЧастиТовары(СтруктураШапкиДокумента, Отказ, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ЗаполнитьИзXML(ЗаполняемыеДанные = "", ТекстXML = Неопределено) Экспорт
	
	ЗаполнятьШапку = Истина;
	ЗаполнятьТЧ = Истина;
	ЗаполнятьКорректируемыйДокумент = Истина;
	Если НЕ ПустаяСтрока(ЗаполняемыеДанные) Тогда
		
		ЗаполняемыеДанныеВРЕГ = ВРег(ЗаполняемыеДанные);
		ЗаполнятьШапку = СтрНайти(ЗаполняемыеДанныеВРЕГ, "ШАПКА") > 0;
		ЗаполнятьТЧ = СтрНайти(ЗаполняемыеДанныеВРЕГ, "ТЧ") > 0;
		ЗаполнятьКорректируемыйДокумент = СтрНайти(ЗаполняемыеДанныеВРЕГ, "КОРРЕКТИРУЕМЫЙДОКУМЕНТ") > 0;
		
	КонецЕсли; 
	
	Если НЕ ЗаполнятьШапку И НЕ ЗаполнятьТЧ И НЕ ЗаполнятьКорректируемыйДокумент Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоказателейXML = Новый Структура();
	
	ТЧ = Новый ТаблицаЗначений;
	СтруктураПоказателейXML.Вставить("ТабличнаяЧасть", ТЧ);
	ТЧ.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	КоличествоСтрокТЧ = 0;
	
	Если ТекстXML = Неопределено Тогда
		
		ДДXML = ДанныеXML.Получить();// двоичные данные
		Если ЗначениеЗаполнено(ДДXML) Тогда
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
			ДДXML.Записать(ИмяВременногоФайла);
			Текст = Новый ТекстовыйДокумент;
			Текст.Прочитать(ИмяВременногоФайла, "windows-1251");
			ТекстXML = Текст.ПолучитьТекст();
		    УдалитьФайлы(ИмяВременногоФайла);
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ИгнорироватьПробелы = Истина;
	Попытка
		ЧтениеXML.УстановитьСтроку(ТекстXML);
	Исключение
		
		Сообщить(НСтр("ru='Данный файл содерижит информацию в закодированном виде или не является стандартым текстовым XML-файлом.
|Необходимо воспользоваться специализированным программным обеспечением для его расшифровки.';uk='Зазначений файл містить інформацію в закодованому вигляді або не являє собою стандартний текстовий XML-файл.
|Необхідно скористатися спеціалізованим програмним забезбеченням для його розшифрування.'"));
							 
		Возврат;
		
	КонецПопытки;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ТекУзел = ВРЕГ(ЧтениеXML.Имя);
			НачалоХХХХ = Найти(ТекУзел, "XXXX");
			
			Если    ТекУзел = "DECLAR"
				ИЛИ ТекУзел = "DECLARHEAD"
				ИЛИ ТекУзел = "DECLARBODY"
				ИЛИ ТекУзел = "LINKED_DOCS" Тогда
				Продолжить;
			ИначеЕсли ВРЕГ(ЧтениеXML.ЗначениеАтрибута("xsi:nil")) = "TRUE" Тогда
					Продолжить;
			ИначеЕсли НачалоХХХХ > 0 И ЗаполнятьТЧ Тогда
				
				НомерСтрокиСтрокой = ЧтениеXML.ЗначениеАтрибута("ROWNUM");
				Если Не ЗначениеЗаполнено(НомерСтрокиСтрокой) Тогда
					Продолжить;
				КонецЕсли;        

				НомерСтроки = Число(НомерСтрокиСтрокой);
				Пока НомерСтроки > КоличествоСтрокТЧ Цикл
					
					КоличествоСтрокТЧ = КоличествоСтрокТЧ + 1;

					НоваяСтрока = ТЧ.Добавить();
					НоваяСтрока.НомерСтроки = КоличествоСтрокТЧ;

				КонецЦикла;
				
				ИмяКолонки = Сред(ТекУзел, НачалоХХХХ + 4);
				Если ТЧ.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
					ТЧ.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЕсли;
				
				ЧтениеXML.Прочитать();
				ТЧ[НомерСтроки - 1][ИмяКолонки] = ЧтениеXML.Значение;
				
			Иначе
				ЧтениеXML.Прочитать();
				СтруктураПоказателейXML.Вставить(ТекУзел, ЧтениеXML.Значение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;		
	
	ЧтениеXML.Закрыть();

	// определим номер схемы документа
	C_DOC     = ""; СтруктураПоказателейXML.Свойство("C_DOC",	   C_DOC);
	C_DOC_SUB = ""; СтруктураПоказателейXML.Свойство("C_DOC_SUB", C_DOC_SUB);
	C_DOC_VER = ""; СтруктураПоказателейXML.Свойство("C_DOC_VER", C_DOC_VER);

	// найдем подходящую форму налоговой (регл. отчет)
	Если  НЕ (C_DOC     = "J12" ИЛИ C_DOC     = "F12")
		И НЕ (C_DOC_SUB = "010" ИЛИ C_DOC_SUB = "012") Тогда
		
		// не тот XML подсунули
		Сообщить(НСтр("ru='Загруженный файл не является XML образом Налоговой накладной или Приложения 2 к налоговой накладной!';uk='Завантажений файл не є XML образом Податкової накладної або Додатка 2 до податкової накладної!'"));
		Возврат;	
		
	КонецЕсли;
	
	ПоддерживаемаяВерсия = Ложь;
	
	Попытка
		ПоддерживаемаяВерсия =  Число(C_DOC_VER) >= 8;
	Исключение
	КонецПопытки;
	
	Если Не ПоддерживаемаяВерсия Тогда
		Сообщить(НСтр("ru='Загруженный файл XML имеет устаревшую версию, загрузка не поддерживается.';uk='Завантажений файл XML має застарілу версію, завантаження не підтримується.'"));
		Возврат;	
	КонецЕсли;
	
	Если ЗаполнятьШапку ИЛИ ЗаполнятьТЧ Тогда
		
		Если C_DOC_SUB = "010" Тогда
			ИсточникиСумм = 
			"R03G7, НДС20, НДС
			|R03G109, НДС7, НДС
			|R03G14, НДС14, НДС
			|R01G7, НДС20, База 
			|R01G109, НДС7, База
			|R01G14, НДС14, База
			|R01G9, НДС0, База
			|R01G8, НДС0, База
			|R01G10, БезНДС, База";
		Иначе
			ИсточникиСумм = 
			"R02G9, НДС20, НДС
			|R02G111, НДС7, НДС
			|R03G14, НДС14, НДС
			|R01G9, НДС20, База 
			|R01G111, НДС7, База
			|R01G14, НДС14, База
			|R006G03, НДС0, База
			|R007G03, НДС0, База
			|R01G11, БезНДС, База";
		КонецЕсли;
		
		ТаблицаСумм = Новый ТаблицаЗначений;
		ТаблицаСумм.Колонки.Добавить("Имя");
		ТаблицаСумм.Колонки.Добавить("СтавкаНДС");
		ТаблицаСумм.Колонки.Добавить("Показатель");
		ТаблицаСумм.Колонки.Добавить("База", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		ТаблицаСумм.Колонки.Добавить("НДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		ТаблицаСумм.Колонки.Добавить("Установлен", Новый ОписаниеТипов("Булево"));

		
		Для ж = 1 По СтрЧислоСтрок(ИсточникиСумм) Цикл
			
			НовыйИсточник = ТаблицаСумм.Добавить();
			ЧастиИсточника = СтрРазделить(СтрПолучитьСтроку(ИсточникиСумм, ж), ",", Истина);
			Для жж = 0 По 2 Цикл      
				ЧастьИсточника = СокрЛП(ЧастиИсточника[жж]);
				Если жж = 1 Тогда
					НовыйИсточник[жж] = Перечисления.СтавкиНДС[ЧастьИсточника];					
				Иначе
					НовыйИсточник[жж] = ЧастьИсточника;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ЗначениеПоказателя = "";
		Для каждого СтрокаТаблицы Из ТаблицаСумм Цикл
			
			Если СтруктураПоказателейXML.Свойство(СтрокаТаблицы.Имя, ЗначениеПоказателя) Тогда
				
				СтрокаТаблицы[СтрокаТаблицы.Показатель] = ЗначениеПоказателя;
				СтрокаТаблицы.Установлен = Истина;	
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаСумм = ТаблицаСумм.Скопировать(Новый Структура("Установлен", Истина), "СтавкаНДС, База, НДС");
		ТаблицаСумм.Свернуть("СтавкаНДС", "База, НДС");
		
	КонецЕсли;
	
	Если ЗаполнятьШапку Тогда
		
		// заполним структуру показателй последней текущей версии
		// переопределим для старых версий
		
		Если C_DOC_SUB = "010" Тогда
			НовВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная");	
		Иначе
			НовВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат");	
		КонецЕсли;
		
		Если ВидОперации <> НовВидОперации Тогда
			
			Если ЗначениеЗаполнено(ВидОперации) Тогда
				Сообщить(НСтр("ru='При заполнении изменен вид операции докумнта!';uk='При заповненні змінений вид операції документа!'"), СтатусСообщения.Важное);
			КонецЕсли;
			
			ВидОперации = НовВидОперации;
			
			ВидДокумента = "-";
			ВключаетсяВУточняющийРасчет = Ложь;	
			
		КонецЕсли;
		
		// Организация
		ОрганизацияИНН = ""; СтруктураПоказателейXML.Свойство("HKBUY", ОрганизацияИНН);
		
		СписокПоказателей = Новый СписокЗначений; 
		СписокПоказателей.Добавить("","ИНН");
		Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Дата, СписокПоказателей);
		
		Если НЕ Сведения.Свойство("ИНН") ИЛИ НЕ (ОрганизацияИНН = Сведения.ИНН) Тогда
			
			Если ЗначениеЗаполнено(Организация) Тогда
				Сообщить(НСтр("ru='ИНН Организации в документе не совпадает с ИНН Покупателя из файла!';uk='ІПН Організації в документі, не збігається з ІПН Покупця з файлу!'"), СтатусСообщения.Важное);	                                   
			КонецЕсли;
			
			ЗаполнитьОрганизациюПоИНННаСервере(ОрганизацияИНН);
			
		КонецЕсли;
		
		// контрагент
		КонтрагентИНН = ""; 		СтруктураПоказателейXML.Свойство("HKSEL", КонтрагентИНН);
		
		Если НЕ Контрагент.ИНН = КонтрагентИНН Тогда
			
			Если ЗначениеЗаполнено(Контрагент) Тогда
				Сообщить(НСтр("ru='Данные контрагента (ИНН) не соответствуют данным из файла';uk='Дані контрагента (ІПН) не відповідають даним з файлу'"), СтатусСообщения.Важное);	
			КонецЕсли;
			
			ЗаполнитьКонтрагентаПоИНННаСервере(КонтрагентИНН);		
			
		КонецЕсли;
		
		//номер вх. документа
		HNUM  = ""; СтруктураПоказателейXML.Свойство("HNUM",  HNUM);  HNUM  = СокрЛП(HNUM);
		HNUM1 = ""; СтруктураПоказателейXML.Свойство("HNUM1", HNUM1); HNUM1 = СокрЛП(HNUM1); HNUM1 = ?(HNUM1 = "0", "", HNUM1);
		HNUM2 = ""; СтруктураПоказателейXML.Свойство("HNUM2", HNUM2); HNUM2 = СокрЛП(HNUM2);
		НомерВходящегоДокумента = HNUM; 
		Если ЗначениеЗаполнено(HNUM1) Тогда
			НомерВходящегоДокумента = НомерВходящегоДокумента + "/" + HNUM1;
		КонецЕсли;
		Если ЗначениеЗаполнено(HNUM2) Тогда
			Если ЗначениеЗаполнено(HNUM1) Тогда
				Если C_DOC_VER > "04" Тогда
					// всего два слеша
					НомерВходящегоДокумента = НомерВходящегоДокумента + "/" + HNUM2;	
				Иначе	
					// всего один слеш
					НомерВходящегоДокумента = НомерВходящегоДокумента + HNUM2;	
				КонецЕсли;
			Иначе
				Если C_DOC_VER > "04" Тогда
					// два слеша
					НомерВходящегоДокумента = НомерВходящегоДокумента + "//" + HNUM2;	
				Иначе	
					// один слеш
					НомерВходящегоДокумента = НомерВходящегоДокумента + "/ " + HNUM2;	
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		HFILL = ""; СтруктураПоказателейXML.Свойство("HFILL", HFILL);
		ДатаВходящегоДокумента = '00010101';
		Если ЗначениеЗаполнено(HFILL) Тогда
			HFILL = СокрЛП(HFILL);
			ДатаВходящегоДокумента = Дата(Сред(HFILL, 5, 4), Сред(HFILL, 3, 2), Сред(HFILL, 1, 2));
		КонецЕсли;
		
		Товары.Очистить();
		
		Для каждого СтрокаСумм Из ТаблицаСумм Цикл
			
			Если СтрокаСумм.База = 0 И СтрокаСумм.НДС = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаДок = Товары.Добавить();
			
			СтрокаДок.СтавкаНДС = СтрокаСумм.СтавкаНДС;
			СтрокаДок.Сумма     = СтрокаСумм.База; 
			СтрокаДок.СуммаНДС  = СтрокаСумм.НДС;
	
		КонецЦикла;
	
		// Тара
		Тара = 0;
		Если C_DOC_SUB = "010" Тогда
			СтруктураПоказателейXML.Свойство("R02G11",  Тара);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Тара) И Число(Тара) <> 0 Тогда
			
			СуммаВозвратнойТары = СуммаВозвратнойТары + Число(Тара);
			
		КонецЕсли;
		
	КонецЕсли;  
	
	Если ЗаполнятьШапку ИЛИ ЗаполнятьКорректируемыйДокумент Тогда
		
		// для П2 дата и номер корректируемой налоговой накладной
		Если C_DOC_SUB = "012" Тогда
			HNUM  = ""; СтруктураПоказателейXML.Свойство("HPODNUM",  HNUM);  HNUM  = СокрЛП(HNUM);
			HNUM1 = ""; СтруктураПоказателейXML.Свойство("HPODNUM1", HNUM1); HNUM1 = СокрЛП(HNUM1); HNUM1 = ?(HNUM1 = "0", "", HNUM1);
			HNUM2 = ""; СтруктураПоказателейXML.Свойство("HPODNUM2", HNUM2); HNUM2 = СокрЛП(HNUM2);
			НомерКорректируемогоВходящегоДокумента = HNUM; 
			Если ЗначениеЗаполнено(HNUM1) Тогда
				НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/" + HNUM1;
			КонецЕсли;
			Если ЗначениеЗаполнено(HNUM2) Тогда
				Если ЗначениеЗаполнено(HNUM1) Тогда
					Если C_DOC_VER > "04" Тогда
						// всего два слеша
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/" + HNUM2;	
					Иначе	
						// всего один слеш
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + HNUM2;	
					КонецЕсли;
				Иначе
					Если C_DOC_VER > "04" Тогда
						// два слеша
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "//" + HNUM2;	
					Иначе	
						// один слеш
						НомерКорректируемогоВходящегоДокумента = НомерКорректируемогоВходящегоДокумента + "/ " + HNUM2;	
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			HFILL = ""; СтруктураПоказателейXML.Свойство("HPODFILL", HFILL);
			ДатаКорректируемогоВходящегоДокумента = '00010101';
			Если ЗначениеЗаполнено(HFILL) Тогда
				HFILL = СокрЛП(HFILL);
				ДатаКорректируемогоВходящегоДокумента = Дата(Сред(HFILL, 5, 4), Сред(HFILL, 3, 2), Сред(HFILL, 1, 2));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаполнятьТЧ Тогда
		
		Товары.Очистить();
		
		КэшПоискаУКТВЭД = Неопределено;
		КэшПоискаЕдиИзм = Неопределено;
		
		СоответствиеСтавокНДС = Новый Соответствие;
		СоответствиеСтавокНДС.Вставить("20", Перечисления.СтавкиНДС.НДС20);
		СоответствиеСтавокНДС.Вставить("7", Перечисления.СтавкиНДС.НДС7);
		СоответствиеСтавокНДС.Вставить("14", Перечисления.СтавкиНДС.НДС14);
		СоответствиеСтавокНДС.Вставить("901", Перечисления.СтавкиНДС.НДС0);
		СоответствиеСтавокНДС.Вставить("902", Перечисления.СтавкиНДС.НДС0);
		СоответствиеСтавокНДС.Вставить("903", Перечисления.СтавкиНДС.БезНДС);  
		
		Для каждого СтрокаФайла Из СтруктураПоказателейXML.ТабличнаяЧасть Цикл

			СтруктураИсходныхДанных = Новый Структура("G001,G21,G22,G3S,G5,G6,G7,G8,G008,G009,G010,G11_10");
			ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанных, СтрокаФайла);
			
			НоваяСтрока = Товары.Добавить();
			
			НоваяСтрока.НаименованиеТовара = СтруктураИсходныхДанных.G3S;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G5) Тогда
				НоваяСтрока.Количество = Число(СтруктураИсходныхДанных.G5);
			ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанных.G8) Тогда
				НоваяСтрока.Количество = Число(СтруктураИсходныхДанных.G8);
			КонецЕсли;

			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G6) Тогда
				НоваяСтрока.Цена = Число(СтруктураИсходныхДанных.G6);
			ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанных.G7) Тогда
				НоваяСтрока.Цена = Число(СтруктураИсходныхДанных.G7);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G008) Тогда
				НоваяСтрока.СтавкаНДС = СоответствиеСтавокНДС[СтруктураИсходныхДанных.G008];
			КонецЕсли; 

			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G010) Тогда
				НоваяСтрока.Сумма = Число(СтруктураИсходныхДанных.G010);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтруктураИсходныхДанных.G11_10) Тогда
				НоваяСтрока.СуммаНДС = Число(СтруктураИсходныхДанных.G11_10);
			КонецЕсли;
			
			НоваяСтрока.КодУКТВЭД = ПолучитьЭлементКлассификатораУКТВЭД(СтрокаФайла, КэшПоискаУКТВЭД);
			НоваяСтрока.ЕдиницаИзмерения = ПолучитьЭлементКлассификатораЕдиницИзмерения(СтрокаФайла, КэшПоискаЕдиИзм);
			
		КонецЦикла;
		
		//если итоги не совпадают с данными раздела А, распределим расхождение по строкам
		ТаблицаПогрешностей = Товары.Выгрузить(,"НомерСтроки, СтавкаНДС, СуммаНДС");
		ТаблицаПогрешностей.Колонки.Добавить("СуммаНДСМинус", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		
		Для каждого СтрокаПогрешностей Из ТаблицаПогрешностей Цикл
			
			Если СтрокаПогрешностей.СуммаНДС < 0 Тогда
				СтрокаПогрешностей.СуммаНДСМинус = СтрокаПогрешностей.СуммаНДС;
				СтрокаПогрешностей.СуммаНДС = 0;
			КонецЕсли;
			
		КонецЦикла;
		
		ИтогиПогрешностей = ТаблицаПогрешностей.Скопировать();
		ИтогиПогрешностей.Свернуть("СтавкаНДС", "СуммаНДС, СуммаНДСМинус");
		Для каждого СтрокаИтогов Из ИтогиПогрешностей Цикл
			
			СтрокаРазделаА = ТаблицаСумм.Найти(СтрокаИтогов.СтавкаНДС, "СтавкаНДС");
			Если СтрокаРазделаА = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			РасхождениеПоСтавке = СтрокаРазделаА.НДС - СтрокаИтогов.СуммаНДС - СтрокаИтогов.СуммаНДСМинус;
			
			Если РасхождениеПоСтавке = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаИтогов.СуммаНДС = 0 ИЛИ РасхождениеПоСтавке < 0 И СтрокаИтогов.СуммаНДСМинус <> 0 Тогда
				КолонкаПогрешности = "СуммаНДСМинус";
			Иначе
				КолонкаПогрешности = "СуммаНДС";
			КонецЕсли;
			
			ТаблицаПогрешностейПоСтавке = ТаблицаПогрешностей.Скопировать(Новый Структура("СтавкаНДС", СтрокаИтогов.СтавкаНДС), "НомерСтроки, " + КолонкаПогрешности);
			МассивКоэф = ТаблицаПогрешностейПоСтавке.ВыгрузитьКолонку(КолонкаПогрешности);  
			МассивПогрешностей = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РасхождениеПоСтавке, МассивКоэф, 2);
			
			Для ж = 0 По МассивПогрешностей.ВГраница() Цикл
				
				СтрокаТоваров = Товары[ТаблицаПогрешностейПоСтавке[ж].НомерСтроки -1];
				СтрокаТоваров.СуммаНДС = СтрокаТоваров.СуммаНДС + МассивПогрешностей[ж];
				
			КонецЦикла;
			
		КонецЦикла;

		Если КэшПоискаУКТВЭД <> Неопределено Тогда
			
			НовыеКодыУКТВЭД = КэшПоискаУКТВЭД.НайтиСтроки(Новый Структура("НовыйЭлемент", Истина));
			
			ПолныйКлассификаторТоваров = Неопределено;
			ПолныйКлассификаторУслуг = Неопределено;
			
			Для каждого СтрокаКэша ИЗ НовыеКодыУКТВЭД Цикл
				
				Если СтрокаКэша.Ссылка.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги Тогда
					
					Если ПолныйКлассификаторУслуг = Неопределено Тогда
						Макет = НСИССервер.ПолучитьМакет("КлассификаторДКПП", Справочники.КлассификаторУКТВЭД.ПолучитьМакет("КлассификаторДКПП"));
						КлассификаторXML = Макет.ПолучитьТекст();
						ПолныйКлассификаторУслуг = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
					КонецЕсли;
					
					СтрокаПолногоКлассификатора = ПолныйКлассификаторУслуг.Найти(СтрокаКэша.Ссылка.Код, "Code");
					Если СтрокаПолногоКлассификатора <> Неопределено Тогда 
						СправочникОбъект = СтрокаКэша.Ссылка.ПолучитьОбъект();
						СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код) + "/"+ СтрокаПолногоКлассификатора.Name;
						СправочникОбъект.НаименованиеПолное      = СтрокаПолногоКлассификатора.Name;
						СправочникОбъект.Записать();
					КонецЕсли;

				Иначе
					
					Если ПолныйКлассификаторТоваров = Неопределено Тогда
						Макет = НСИССервер.ПолучитьМакет("КлассификаторУКТВЭД", Справочники.КлассификаторУКТВЭД.ПолучитьМакет("КлассификаторУКТВЭД"));
						КлассификаторXML = Макет.ПолучитьТекст();
						ПолныйКлассификаторТоваров = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные; 
						
						ПолныйКлассификаторТоваров.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Число"));
						Для каждого СтрокаПолногоКлассификатора Из ПолныйКлассификаторТоваров Цикл
							
							Уровень = 1;    
							СтрокаНаименование = СтрокаПолногоКлассификатора.Name;
							ДлинаСтроки = СтрДлина(СтрокаНаименование);
							Для Сч = 1 По ДлинаСтроки Цикл
								
								ТекСимв = Сред(СтрокаНаименование, Сч, 1);
								
								Если ТекСимв = "-" Тогда
									Уровень = Уровень + 1;
								ИначеЕсли  ТекСимв  = " " Тогда
									Продолжить;	
								Иначе
									Прервать;
								КонецЕсли;
								
							КонецЦикла;
							СтрокаПолногоКлассификатора.Уровень = Уровень;
							
						КонецЦикла;

					КонецЕсли;
					
					СтрокаПолногоКлассификатора = ПолныйКлассификаторТоваров.Найти(СокрЛП(СтрокаКэша.Ссылка.Код), "Code");
					Если СтрокаПолногоКлассификатора <> Неопределено Тогда 
						
						ПолноеНаименование = СтрокаПолногоКлассификатора.Name;
						
						ИндексСтроки = ПолныйКлассификаторТоваров.Индекс(СтрокаПолногоКлассификатора);
						ТекУровень = СтрокаПолногоКлассификатора.Уровень;
						
						Пока Истина Цикл
							
							Если ТекУровень = 1 Тогда
								Прервать;
							КонецЕсли;
							
							ИндексСтроки = ИндексСтроки - 1;
							
							Если ИндексСтроки < 0 Тогда
								Прервать;
							КонецЕсли;
							
							ТекСтрокаИерархии = ПолныйКлассификаторТоваров.Получить(ИндексСтроки);
							
							Если ТекСтрокаИерархии.Уровень < ТекУровень Тогда
								
								ПолноеНаименование = ТекСтрокаИерархии.Name + Символы.ПС + ПолноеНаименование;
								ТекУровень = ТекСтрокаИерархии.Уровень;
								
							КонецЕсли;
							
						КонецЦикла;
						
						СправочникОбъект = СтрокаКэша.Ссылка.ПолучитьОбъект();
						СправочникОбъект.НаименованиеПолное      = ПолноеНаименование;
						СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код) + ?(СправочникОбъект.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного," X","") 
																	+ "/"+ СтрЗаменить(СправочникОбъект.НаименованиеПолное,Символы.ПС,"");
						СправочникОбъект.ВыводитьПриПечатиЧека   = СтрокаПолногоКлассификатора.Print = "1";
						СправочникОбъект.Записать(); 
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаТоваров ИЗ Товары Цикл
		
		СтрокаТоваров.СуммаВзаиморасчетов = СтрокаТоваров.Сумма + СтрокаТоваров.СуммаНДС;
		СтрокаТоваров.ДляХозяйственнойДеятельности = Истина;
		СтрокаТоваров.ДляОперацийОблагаемыхНДС = Истина;
		СтрокаТоваров.СтатьяДекларацииНДСНалоговыйКредит = Документы.РегистрацияВходящегоНалоговогоДокумента.ОпределитьСтатьюНалоговойДекларации(ВидОперации, СтрокаТоваров);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЭлементКлассификатораУКТВЭД(ИсходныеДанные, Кэш)
	
	СтруктураПоиска = Новый Структура("Код, Вид");
	Если Кэш = Неопределено Тогда
		Кэш = Новый ТаблицаЗначений; 
		Для каждого ПолеПоиска ИЗ СтруктураПоиска Цикл
			Кэш.Колонки.Добавить(ПолеПоиска.Ключ);            
		КонецЦикла;
		Кэш.Колонки.Добавить("Ссылка");
		Кэш.Колонки.Добавить("НовыйЭлемент", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	СтруктураИсходныхДанныхПоиска = Новый Структура("G4,G32,G322,G33");
	ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанныхПоиска, ИсходныеДанные);

	Если ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G4) Тогда
		
		ИсходныйКодУКТВЭД = СокрЛП(СтруктураИсходныхДанныхПоиска.G4);
		КодУКТВЭД = "";
		Для ж = 1 По СтрДлина(ИсходныйКодУКТВЭД) Цикл
			
			КодУКТВЭД = КодУКТВЭД + Сред(ИсходныйКодУКТВЭД, ж, 1);
			Если ж = 4 ИЛИ ж = 6 ИЛИ ж = 8 Тогда
				КодУКТВЭД = КодУКТВЭД + " ";
			КонецЕсли;
			
		КонецЦикла;
		
		СтруктураПоиска.Код = КодУКТВЭД;
		
		Если ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G32) Тогда
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного;
		ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G322) Тогда
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодСХПродукции;
		Иначе
			СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовара;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СтруктураИсходныхДанныхПоиска.G33) Тогда
		
		СтруктураПоиска.Код = СокрЛП(СтруктураИсходныхДанныхПоиска.G33);
		СтруктураПоиска.Вид = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;  
	
	НайденныеСтроки = Кэш.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;

	НоваяЗаписьКэша = Кэш.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗаписьКэша, СтруктураПоиска);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторУКТВЭД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторУКТВЭД КАК КлассификаторУКТВЭД
	|ГДЕ
	|	КлассификаторУКТВЭД.Код = &Код
	|	И КлассификаторУКТВЭД.Вид = &Вид
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлассификаторУКТВЭД.ПометкаУдаления УБЫВ";
	Запрос.УстановитьПараметр("Код", СтруктураПоиска.Код);
	Запрос.УстановитьПараметр("Вид", СтруктураПоиска.Вид);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НоваяЗаписьКэша.Ссылка = Выборка.Ссылка;
		
	Иначе
		
		СправочникОбъект = Справочники.КлассификаторУКТВЭД.СоздатьЭлемент();
		
		СправочникОбъект.Код                     = СтруктураПоиска.Код;
		СправочникОбъект.Вид                	 = СтруктураПоиска.Вид;
		СправочникОбъект.Наименование       	 = СокрЛП(СправочникОбъект.Код);
				
		СправочникОбъект.Записать();
		НоваяЗаписьКэша.Ссылка = СправочникОбъект.Ссылка;
		НоваяЗаписьКэша.НовыйЭлемент = Истина;
		
	КонецЕсли;

	Возврат НоваяЗаписьКэша.Ссылка;
	
КонецФункции

Функция ПолучитьЭлементКлассификатораЕдиницИзмерения(ИсходныеДанные, Кэш)
	
	СтруктураПоиска = Новый Структура("Код, Наименование");
	Если Кэш = Неопределено Тогда
		Кэш = Новый ТаблицаЗначений; 
		Для каждого ПолеПоиска ИЗ СтруктураПоиска Цикл
			Кэш.Колонки.Добавить(ПолеПоиска.Ключ);            
		КонецЦикла;
		Кэш.Колонки.Добавить("Ссылка");
		Кэш.Колонки.Добавить("НовыйЭлемент", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	СтруктураИсходныхДанныхПоиска = Новый Структура("G105_2S, G4S");
	ЗаполнитьЗначенияСвойств(СтруктураИсходныхДанныхПоиска, ИсходныеДанные);

	СтруктураПоиска.Код = СокрЛП(СтруктураИсходныхДанныхПоиска.G105_2S);
	СтруктураПоиска.Наименование = СокрЛП(СтруктураИсходныхДанныхПоиска.G4S);
	
	Если НЕ ЗначениеЗаполнено(СтруктураПоиска.Код) И НЕ ЗначениеЗаполнено(СтруктураПоиска.Наименование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НайденныеСтроки = Кэш.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;

	НоваяЗаписьКэша = Кэш.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗаписьКэша, СтруктураПоиска);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторЕдиницИзмерения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|ГДЕ
	|	КлассификаторЕдиницИзмерения.Код = &Код
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторЕдиницИзмерения.Ссылка
	|ИЗ
	|	Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|ГДЕ
	|	&Код = """"
	|	И КлассификаторЕдиницИзмерения.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Код", СтруктураПоиска.Код);
	Запрос.УстановитьПараметр("Наименование", СтруктураПоиска.Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НоваяЗаписьКэша.Ссылка = Выборка.Ссылка;
		
	Иначе
		
		СправочникОбъект = Справочники.КлассификаторЕдиницИзмерения.СоздатьЭлемент();
		
		СправочникОбъект.Код                     = СтруктураПоиска.Код;
		СправочникОбъект.Наименование       	 = СтруктураПоиска.Наименование;
		СправочникОбъект.НаименованиеПолное      = СправочникОбъект.Наименование;
				
		СправочникОбъект.Записать();
		НоваяЗаписьКэша.Ссылка = СправочникОбъект.Ссылка;
		НоваяЗаписьКэша.НовыйЭлемент = Истина;
		
	КонецЕсли;

	Возврат НоваяЗаписьКэша.Ссылка;
	
КонецФункции

Процедура ЗаполнитьОрганизациюПоИНННаСервере(ОрганизацияИНН)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
               |	Рег.Организация КАК Ссылка
               |ИЗ
               |	РегистрСведений.КодыОрганизации.СрезПоследних(&Дата) КАК Рег
               |ГДЕ
               |	Рег.ИНН 		  = &ОрганизацияИНН
               |
               |УПОРЯДОЧИТЬ ПО
               |	Рег.Организация.Код УБЫВ";
	
	Запрос.УстановитьПараметр("ОрганизацияИНН", 			ОрганизацияИНН);
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Ссылка;
		ДоговорКонтрагента = "";
		Сделка = "";
		ДокументОснование = ""
	КонецЕсли;
	

КонецПроцедуры

Процедура ЗаполнитьКонтрагентаПоИНННаСервере(КонтрагентИНН)		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
               |	Спр.Ссылка
               |ИЗ
               |	Справочник.Контрагенты КАК Спр
               |ГДЕ
               |	Спр.ИНН 		  = &КонтрагентИНН
			   //|	И Спр.КодПоЕДРПОУ = &КонтрагентКодПоЕДРПОУ
               |
               |УПОРЯДОЧИТЬ ПО
               |	Спр.Код УБЫВ";
	
	Запрос.УстановитьПараметр("КонтрагентИНН", 			КонтрагентИНН);
	//Запрос.УстановитьПараметр("КонтрагентКодПоЕДРПОУ", 	КонтрагентКодПоЕДРПОУ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Контрагент = Выборка.Ссылка;
		ДоговорКонтрагента = "";
		Сделка 			   = "";
		ДокументОснование  = "";
	КонецЕсли;
	
КонецПроцедуры

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

#КонецЕсли