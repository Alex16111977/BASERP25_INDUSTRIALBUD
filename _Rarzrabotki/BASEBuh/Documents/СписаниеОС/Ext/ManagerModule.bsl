#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Форма ОЗ-3
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОЗ3";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-3';uk='Форма ОЗ-3'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Форма Акт списания основных средств
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктСписанияОСБюжд";
	КомандаПечати.Представление = НСтр("ru='Акт списания основных средств';uk='Акт списання основних засобів'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";       

	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Списание ОС""';uk='Реєстр документів ""Списання ОЗ""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
		
КонецПроцедуры

// Функция формирует табличный документ с печатной формой ОЗ-3
// 
Функция ПечатьОЗ3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	УстановитьПривилегированныйРежим(Истина);

	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОЗ3";
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_ОЗ3");

	ПервыйДокумент = Истина;

	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;


		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка);
		Запрос.УстановитьПараметр("ТекДата",        Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеОС.Дата                           КАК ДатаАкта,
		|	СписаниеОС.Номер                          КАК НомерАкта,
		|	ВЫРАЗИТЬ(СписаниеОС.Организация.НаименованиеПолное 
		|	                    КАК СТРОКА(1000))     КАК Организация,
		|	СписаниеОС.Организация.КодПоЕДРПОУ               КАК ЕДРПОУ,
		|	СписаниеОС.ПричинаСписания                КАК ПричинаСписания,
		|	СписаниеОС.ПричинаСписания.Код            КАК КодПричины
		|ИЗ
		|	Документ.СписаниеОС                       КАК СписаниеОС
		|
		|ГДЕ
		|	СписаниеОС.Ссылка = &Ссылка";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();
		
		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка"        , Ссылка);
		Запрос.УстановитьПараметр("СписокОС"      , СписокОС);
		Запрос.УстановитьПараметр("ТекДата"       , Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.УстановитьПараметр("СостояниеВвода", Перечисления.СостоянияОС.ВведеноВЭксплуатацию);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПервоначальныеСведенияБУ.ИнвентарныйНомер        КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	СчетаБухгалтерскогоУчета.СчетУчета               КАК СчетКт,
		|	СписаниеОСОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	СписаниеОСОС.ОсновноеСредство.ЗаводскойНомер     КАК ЗаводскойНомер,
		|	СписаниеОСОС.ОсновноеСредство.ДатаВыпуска        КАК ГодВыпуска,
		|	СписаниеОСОС.Ссылка.СчетСписанияБУ               КАК СчетДт,
		|	СписаниеОСОС.СтоимостьБУ - СписаниеОСОС.АмортизацияБУ - СписаниеОСОС.АмортизацияЗаМесяцБУ КАК ОстаточнаяСтоимость,
		|	СписаниеОСОС.АмортизацияБУ + СписаниеОСОС.АмортизацияЗаМесяцБУ КАК Амортизация,
		|	МестонахождениеОС.Местонахождение                КАК СдалоПодразделение,
		|	СостояниеОС.ДатаСостояния                        КАК ДатаВвода
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних( 
		|		                    &ТекДата,
		|		                    ОсновноеСредство В (&СписокОС)
		|		                    И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияБУ
		|		ПО СписаниеОСОС.ОсновноеСредство = ПервоначальныеСведенияБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|		                    &ТекДата,
		|		                    ОсновноеСредство В (&СписокОС)
		|		                    И Организация = &ТекОрганизация) КАК СчетаБухгалтерскогоУчета
		|		ПО СписаниеОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|		                    &ТекДата,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО СписаниеОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			(ВЫБРАТЬ
		|				СостоянияОС.ОсновноеСредство КАК ОсновноеСредство,
		|				СостоянияОС.ДатаСостояния    КАК ДатаСостояния
		|			ИЗ
		|				РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОС
		|			ГДЕ
		|				СостоянияОС.Состояние = &СостояниеВвода
		|				И СостоянияОС.Организация = &ТекОрганизация
		|				И СостоянияОС.ОсновноеСредство В(&СписокОС)) КАК СостояниеОС
		|		ПО СписаниеОСОС.ОсновноеСредство = СостояниеОС.ОсновноеСредство
		|ГДЕ
		|	СписаниеОСОС.Ссылка = &Ссылка";
			
		Результат = Запрос.Выполнить();
		ВыборкаПоОС = Результат.Выбрать();
		
		СписокПодразделений = Результат.Выгрузить().ВыгрузитьКолонку("СдалоПодразделение");
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
		
		Пока ВыборкаПоОС.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("ОЗ3");
			Параметры     = ОбластьМакета.Параметры;
			Параметры.Заполнить(ВыборкаПоШапке);
			Параметры.Заполнить(ВыборкаПоКомиссии);
			Параметры.Организация = СокрП(ВыборкаПоШапке.Организация);
			Параметры.Заполнить(ВыборкаПоОС);
			Параметры.ВидОперации = "Списання";
			ТабДокумент.Вывести(ОбластьМакета);
			НеНачало = Истина;
			
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;

КонецФункции // ПечатьСписаниеТоваров()

Функция ПечатьСписанияОС_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)

	УстановитьПривилегированныйРежим(Истина);

	
	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_АктСписанияОС";
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_АктСписанияОСБюдж_2016");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
	
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка);
		Запрос.УстановитьПараметр("ТекДата",        Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеОС.Дата КАК ДатаАкта,
		|	СписаниеОС.Номер КАК НомерАкта,
		|	ВЫРАЗИТЬ(СписаниеОС.Организация.НаименованиеПолное КАК СТРОКА(1000)) КАК Организация,
		|	СписаниеОС.Организация.КодПоЕДРПОУ КАК ЕДРПОУ
		|ИЗ
		|	Документ.СписаниеОС КАК СписаниеОС
		|ГДЕ
		|	СписаниеОС.Ссылка = &Ссылка";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();
		
		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка"        , Ссылка);
		Запрос.УстановитьПараметр("СписокОС"      , СписокОС);
		Запрос.УстановитьПараметр("ТекДата"       , Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.УстановитьПараметр("СостояниеВвода", Перечисления.СостоянияОС.ВведеноВЭксплуатацию);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПервоначальныеСведенияБУ.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ПервоначальныеСведенияБУ.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
		|	СчетаБухгалтерскогоУчета.СчетУчета КАК СчетКт,
		|	СписаниеОСОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	СписаниеОСОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
		|	СписаниеОСОС.Ссылка.СчетСписанияБУ КАК СчетДт,
		|	СписаниеОСОС.АмортизацияБУ + СписаниеОСОС.АмортизацияЗаМесяцБУ КАК Амортизация,
		|	СостояниеОС.ДатаСостояния КАК ДатаВвода,
		|	МестонахождениеОС.Местонахождение КАК Подразделение,
		|	МестонахождениеОС.МОЛ КАК МОЛ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчет.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|				&ТекДата,
		|				ОсновноеСредство В (&СписокОС)
		|					И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияБУ
		|		ПО СписаниеОСОС.ОсновноеСредство = ПервоначальныеСведенияБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|				&ТекДата,
		|				ОсновноеСредство В (&СписокОС)
		|					И Организация = &ТекОрганизация) КАК СчетаБухгалтерскогоУчета
		|		ПО СписаниеОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|				&ТекДата,
		|				Организация = &ТекОрганизация
		|					И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО СписаниеОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СостоянияОС.ОсновноеСредство КАК ОсновноеСредство,
		|			СостоянияОС.ДатаСостояния КАК ДатаСостояния
		|		ИЗ
		|			РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОС
		|		ГДЕ
		|			СостоянияОС.Состояние = &СостояниеВвода
		|			И СостоянияОС.Организация = &ТекОрганизация
		|			И СостоянияОС.ОсновноеСредство В(&СписокОС)) КАК СостояниеОС
		|		ПО СписаниеОСОС.ОсновноеСредство = СостояниеОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
		|				&ТекДата,
		|				ОсновноеСредство В (&СписокОС)
		|					И Организация = &ТекОрганизация) КАК ПараметрыАмортизацииОСБухгалтерскийУчет
		|		ПО СписаниеОСОС.ОсновноеСредство = ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство
		|ГДЕ
		|	СписаниеОСОС.Ссылка = &Ссылка
		|ИТОГИ ПО
		|	МОЛ";
			
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
			ТабДокумент.Вывести(ОбластьМакета);
			
			Возврат ТабДокумент;
			
		КонецЕсли;
		СписокМОЛ = Результат.Выгрузить();
		СписокМОЛ.Свернуть("МОЛ");
		
		ВыборкаПоКомиссии 			= ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
		ОтветственныеЛица 			= ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);
		ДанныеФизЛицаОтветственный  = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Ответственный.ФизическоеЛицо, Ссылка.Дата);
		
		ВыборкаПоМОЛ = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
		НеПервая = Ложь;
		
		Пока ВыборкаПоМОЛ.Следующий() Цикл      
			
			Если НеПервая Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ОбластьМакета		  = Макет.ПолучитьОбласть("Шапка");
			Параметры = ОбластьМакета.Параметры;
			Параметры.Заполнить(ВыборкаПоШапке); 
			Параметры.Должность_Руководитель = ОтветственныеЛица.РуководительДолжность;
			Параметры.ФИО_Руководитель		 = ОтветственныеЛица.РуководительПредставление;
			Параметры.ДатаАктаПрописью		 = Формат(ВыборкаПоШапке.ДатаАкта, "ДЛФ=DD");
			ТабДокумент.Вывести(ОбластьМакета);
			ОбластьМакета		  			 = Макет.ПолучитьОбласть("Строка");
			Параметры = ОбластьМакета.Параметры;
			
			ВыборкаПоОС = ВыборкаПоМОЛ.Выбрать();   
			Пока ВыборкаПоОС.Следующий() Цикл
				
				Параметры.Заполнить(ВыборкаПоШапке); 
				Параметры.Заполнить(ВыборкаПоОС);
				Параметры.ЛиквидационнаяСтоимость = Формат(ВыборкаПоОС.ЛиквидационнаяСтоимость, "ЧЦ=15; ЧДЦ=2; ЧН=");   
				Параметры.ДатаВвода 			  = Формат(ВыборкаПоОС.ДатаВвода, "ДЛФ=Д");   
				СдалоПодразделение 				  =  ВыборкаПоОС.Подразделение;
				ТабДокумент.Вывести(ОбластьМакета);    
				
			КонецЦикла;   
			
			ОбластьМакета		  = Макет.ПолучитьОбласть("Подвал");
			Параметры = ОбластьМакета.Параметры;
			Параметры.Заполнить(ВыборкаПоКомиссии);	
			ДанныеФизЛицаСдал	= ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, ВыборкаПоМОЛ.МОЛ, Ссылка.Дата);
			Параметры.МОЛ_Должность 	 =  ДанныеФизЛицаСдал.Должность;
			Параметры.МОЛ_ФИО			 =  ДанныеФизЛицаСдал.Представление;
			Параметры.СдалоПодразделение =  СдалоПодразделение;
			ТабДокумент.Вывести(ОбластьМакета);    
			
			//Данные БУ
			ОбластьМакета  = Макет.ПолучитьОбласть("ДанныеБУШапка");
			ТабДокумент.Вывести(ОбластьМакета);
			ОбластьМакета  = Макет.ПолучитьОбласть("ДанныеБУСтрока");
			Параметры      = ОбластьМакета.Параметры;  
			Если СписокМОЛ.Количество() = 1 Тогда 
				
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Хозрасчетный.СчетДт КАК СчетДт,
				|	Хозрасчетный.СчетКт КАК СчетКт,
				|	Хозрасчетный.Сумма КАК Сумма
				|ИЗ
				|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
				|ГДЕ
				|	Хозрасчетный.Регистратор = &Ссылка
				|
				|УПОРЯДОЧИТЬ ПО
				|	Хозрасчетный.НомерСтроки";      
				Результат = Запрос.Выполнить();
				ВыборкаБУ = Результат.Выбрать();  
				
				Пока ВыборкаБУ.Следующий() Цикл 
					
					Параметры.Заполнить(ВыборкаБУ); 
					Параметры.СчетКт = ?(ВыборкаБУ.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка(), "-", ВыборкаБУ.СчетКт);
					Параметры.СчетДт = ?(ВыборкаБУ.СчетДт = ПланыСчетов.Хозрасчетный.ПустаяСсылка(), "-", ВыборкаБУ.СчетДт);
					ТабДокумент.Вывести(ОбластьМакета);    
					
				КонецЦикла;
				
			Иначе       
				
				ТабДокумент.Вывести(ОбластьМакета);      
				
			КонецЕсли; 
			
			ОбластьМакета = Макет.ПолучитьОбласть("ДанныеБУПодвал");
			Параметры = ОбластьМакета.Параметры;
			Параметры.ОтветственныйДолжность = ДанныеФизЛицаОтветственный.Должность;
			Параметры.ОтветственныйФИО		 = ДанныеФизЛицаОтветственный.Представление;
			Параметры.ГлавныйБухгалтер 		 = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
			ТабДокумент.Вывести(ОбластьМакета);  
			НеПервая = Истина;
			
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;
	
КонецФункции // ПечатьСписанияОС_2016()

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОЗ3") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОЗ3", 
			НСтр("ru='Форма ОЗ-3';uk='Форма ОЗ-3'"), ПечатьОЗ3(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета),, ИмяМакета);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСписанияОСБюжд") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктСписанияОСБюжд",
			НСтр("ru='Акт списания основных средств';uk='Акт списання основних засобів'"), ПечатьСписанияОС_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета), , ИмяМакета);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "СубконтоБУ1");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли