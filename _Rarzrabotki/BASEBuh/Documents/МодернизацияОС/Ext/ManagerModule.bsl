#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Акт о приеме-сдаче ОС (ОС-3)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОЗ2";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-2';uk='Форма ОЗ-2'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Акт передачи на ремонт, реконструкцию и модернизацию основных средств)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктПриемаРемонтБюдж";
	КомандаПечати.Представление = НСтр("ru='Акт приема отремонтированных, реконструированных и модернизированных основных средств';uk='Акт приймання відремонтованих, реконструйованих та модернізованих основних засобів'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Модернизация и ремонт ОС""';uk='Реєстр документів ""Модернізація і ремонт ОЗ""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

// Функция формирует табличный документ унифицированной формы ОЗ-2
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме ОЗ-2.
//
Функция ПечатьОЗ2(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	УстановитьПривилегированныйРежим(Истина);

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_ОЗ2");
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_ОЗ2";

	ПервыйДокумент = Истина;

	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		
		Запрос       = Новый Запрос;
		Запрос.УстановитьПараметр("Период"            , Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация"    , Ссылка.Организация);
		Запрос.УстановитьПараметр("ГлавныйБухгалтер"  , Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		Запрос.УстановитьПараметр("РуководительОтдела", Ссылка.РуководительОтдела);
		Запрос.УстановитьПараметр("Сдал"              , Ссылка.Сдал);
		Запрос.УстановитьПараметр("Принял"            , Ссылка.Принял);
		Запрос.УстановитьПараметр("Ссылка"            , Ссылка.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МодернизацияОС.Организация.НаименованиеПолное                КАК Организация,
		|	МодернизацияОС.Организация.КодПоЕДРПОУ                                   КАК ЕДРПОУ,
		|	МодернизацияОС.Номер                                          КАК НомерАкта,
		|	МодернизацияОС.Дата                                           КАК ДатаАкта,
		|	МодернизацияОС.СобытиеОС                                      КАК ВидОперации,
		|	МодернизацияОС.СчетУчетаБУВнеоборотногоАктива                 КАК СчетКт,
		|	МодернизацияОС.ДатаНачалаМодернизации                         КАК ДатаНачала,
		|	МодернизацияОС.ДатаОкончанияМодернизации                      КАК ДатаОкончания,
		|	МодернизацияОС.ЧтоНеВыполнено                                 КАК ЧтоНеВыполнено,
		|	МодернизацияОС.ЧтоИзменилось                                  КАК ЧтоИзменилось,
		|	РАЗНОСТЬДАТ(МодернизацияОС.ДатаНачалаМодернизации,
		|	            МодернизацияОС.ДатаОкончанияМодернизации,
		|	            ДЕНЬ)                                             КАК КоличествоДней
		|ИЗ
		|	Документ.МодернизацияОС КАК МодернизацияОС
		|ГДЕ
		|	МодернизацияОС.Ссылка = &Ссылка";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();

		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка.Ссылка);
		Запрос.УстановитьПараметр("Период",         Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		
		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		Запрос.УстановитьПараметр("СписокОС",       СписокОС);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер       КАК ЗаводскойНомер,
		|	МодернизацияОСОС.ОсновноеСредство.НаименованиеПолное   КАК ОсновноеСредствоНаименование,
		|	ВЫБОР
		|		КОГДА НЕ (МодернизацияОСОС.Ссылка.ВидУлучшения = ЗНАЧЕНИЕ(Перечисление.ВидыУлучшения.Ремонт))
		|			ТОГДА МодернизацияОСОС.СуммаМодернизацииБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаМодернизации,
		|	ВЫБОР
		|		КОГДА МодернизацияОСОС.Ссылка.ВидУлучшения = ЗНАЧЕНИЕ(Перечисление.ВидыУлучшения.Ремонт)
		|			ТОГДА МодернизацияОСОС.СуммаМодернизацииБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаРемонта,
		|	МодернизацияОСОС.СуммаМодернизацииБУ                   КАК СуммаМодернизацииИРемонта,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета                   КАК СчетДт,
		|	ПервоначальныеСведенияОС.ИнвентарныйНомер              КАК ИнвентарныйНомер,
		|	МестонахождениеОС.МОЛ.Код                              КАК КодМОЛа,
		|	МестонахождениеОС.Местонахождение.Наименование         КАК Подразделение
		|ИЗ
		|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК СчетаБухгалтерскогоУчетаОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|ГДЕ
		|	МодернизацияОСОС.Ссылка = &Ссылка";
		
			
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		НеПервая = Ложь;
		
		Пока Выборка.Следующий() Цикл
			
			Если НеПервая Тогда
				
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
			КонецЕсли;
			
			ОбластьМакета = Макет.ПолучитьОбласть("ОЗ2");
			ОбластьМакета.Параметры.Заполнить(ВыборкаПоШапке);
			
		
			ОбластьМакета.Параметры.Заполнить(Выборка);
			
			ДанныеФизЛицаПолучил = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Принял, Ссылка.Дата);
			ОбластьМакета.Параметры.ПринялДолжность = ДанныеФизЛицаПолучил.Должность;
			ОбластьМакета.Параметры.Принял 			= ДанныеФизЛицаПолучил.Представление;
			
			ДанныеФизЛицаСдал = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Сдал, Ссылка.Дата);
			ОбластьМакета.Параметры.СдалДолжность 	= ДанныеФизЛицаСдал.Должность;
			ОбластьМакета.Параметры.Сдал	 		= ДанныеФизЛицаСдал.Представление;
			
			ДанныеФизЛицаРуководительОтдела = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.РуководительОтдела, Ссылка.Дата);
			ОбластьМакета.Параметры.РуководительОтдела = ДанныеФизЛицаРуководительОтдела.Представление;
			
			ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);

			ОбластьМакета.Параметры.ГлавныйБухгалтер 	= ОтветственныеЛица.ГлавныйБухгалтерПредставление;
			
			ТабДокумент.Вывести(ОбластьМакета);
			НеПервая = Истина;
		
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;

КонецФункции // ПечатьОЗ2()   

Функция ПечатьПриемаРемонт_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_АктПриемаРемонтБюдж_2016");
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_АктПриемаРемонт";
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
	
		Запрос       = Новый Запрос;
		Запрос.УстановитьПараметр("Период"            , Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация"    , Ссылка.Организация);
		Запрос.УстановитьПараметр("ГлавныйБухгалтер"  , Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		Запрос.УстановитьПараметр("РуководительОтдела", Ссылка.РуководительОтдела);
		Запрос.УстановитьПараметр("Сдал"              , Ссылка.Сдал);
		Запрос.УстановитьПараметр("Принял"            , Ссылка.Принял);
		Запрос.УстановитьПараметр("Ссылка"            , Ссылка.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МодернизацияОС.Организация.НаименованиеПолное КАК Организация,
		|	МодернизацияОС.Организация.КодПоЕДРПОУ КАК ЕДРПОУ,
		|	МодернизацияОС.Номер КАК НомерАкта,
		|	МодернизацияОС.Дата КАК ДатаАкта,
		|	МодернизацияОС.СобытиеОС КАК ВидОперации,
		|	МодернизацияОС.СчетУчетаБУВнеоборотногоАктива КАК СчетКт,
		|	МодернизацияОС.ДатаНачалаМодернизации КАК ДатаНачала,
		|	МодернизацияОС.ДатаОкончанияМодернизации КАК ДатаОкончания,
		|	МодернизацияОС.ЧтоНеВыполнено КАК ЧтоНеВыполнено,
		|	МодернизацияОС.ЧтоИзменилось КАК ЧтоИзменилось,
		|	РАЗНОСТЬДАТ(МодернизацияОС.ДатаНачалаМодернизации, МодернизацияОС.ДатаОкончанияМодернизации, ДЕНЬ) КАК КоличествоДней
		|ИЗ
		|	Документ.МодернизацияОС КАК МодернизацияОС
		|ГДЕ
		|	МодернизацияОС.Ссылка = &Ссылка";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();

		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка.Ссылка);
		Запрос.УстановитьПараметр("Период",         Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		
		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		Запрос.УстановитьПараметр("СписокОС",       	 СписокОС);
		Запрос.УстановитьПараметр("УсловиеМодернизация", Перечисления.ВидыСобытийОС.Модернизация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
		|	МодернизацияОСОС.ОсновноеСредство.НаименованиеПолное КАК ОсновноеСредствоНаименование,
		|	МодернизацияОСОС.СуммаМодернизацииБУ КАК СуммаМодернизацииИРемонта,
		|	ВЫБОР
		|		КОГДА МодернизацияОСОС.Ссылка.СобытиеОС.ВидСобытияОС = &УсловиеМодернизация
		|			ТОГДА МодернизацияОСОС.СуммаМодернизацииБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаМодернизации,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета КАК СчетДт,
		|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	МестонахождениеОС.Местонахождение.Наименование КАК Подразделение,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	МестонахождениеОС.Местонахождение КАК Местонахождение
		|ИЗ
		|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|				&Период,
		|				Организация = &ТекОрганизация
		|					И ОсновноеСредство В (&СписокОС)) КАК СчетаБухгалтерскогоУчетаОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|				&Период,
		|				Организация = &ТекОрганизация
		|					И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|				&Период,
		|				Организация = &ТекОрганизация
		|					И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|ГДЕ
		|	МодернизацияОСОС.Ссылка = &Ссылка";
		
			

		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);    
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка"); 
		Параметры = ОбластьМакета.Параметры;
		Параметры.Заполнить(ВыборкаПоШапке);
		Параметры.ДатаАктаПрописью		 = Формат(ВыборкаПоШапке.ДатаАкта, "ДЛФ=ДД");
		Параметры.ДатаАкта				 = Формат(ВыборкаПоШапке.ДатаАкта, "ДЛФ=Д");
		Параметры.Должность_Руководитель = ОтветственныеЛица.РуководительДолжность;
		Параметры.ФИО_Руководитель		 = ОтветственныеЛица.РуководительПредставление;
		ТабДокумент.Вывести(ОбластьМакета);  
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка"); 
		Параметры = ОбластьМакета.Параметры; 
		
		Пока Выборка.Следующий() Цикл
			
			Параметры.Заполнить(Выборка);
			ТабДокумент.Вывести(ОбластьМакета);   
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал"); 
		Параметры = ОбластьМакета.Параметры; 
		Параметры.Заполнить(ВыборкаПоШапке);
		Параметры.ДатаНачала	= Формат(ВыборкаПоШапке.ДатаНачала, "ДЛФ=Д");
		Параметры.ДатаОкончания = Формат(ВыборкаПоШапке.ДатаОкончания, "ДЛФ=Д");
		ТабДокумент.Вывести(ОбластьМакета);  
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьМакета = Макет.ПолучитьОбласть("Страница2Шапка"); 
		Параметры = ОбластьМакета.Параметры;

		ДанныеФизЛицаПолучил = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Принял, Ссылка.Дата);
		ОбластьМакета.Параметры.ПринялДолжность = ДанныеФизЛицаПолучил.Должность;
		ОбластьМакета.Параметры.Принял 			= ДанныеФизЛицаПолучил.Представление;
		
		ДанныеФизЛицаСдал = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Сдал, Ссылка.Дата);
		ОбластьМакета.Параметры.СдалДолжность 	= ДанныеФизЛицаСдал.Должность;
		ОбластьМакета.Параметры.Сдал	 		= ДанныеФизЛицаСдал.Представление;
						
		ТабДокумент.Вывести(ОбластьМакета);  
 		ОбластьМакета = Макет.ПолучитьОбласть("Страница2ДанныеБУСтрока"); 
		Параметры = ОбластьМакета.Параметры; 
		Если Ссылка.Проведен Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Хозрасчетный.СчетДт КАК СчетДт,
			|	Хозрасчетный.СчетКт КАК СчетКт,
			|	Хозрасчетный.Сумма КАК Сумма
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
			|ГДЕ
			|	Хозрасчетный.Регистратор = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	Хозрасчетный.НомерСтроки";      
			Результат = Запрос.Выполнить();
			ВыборкаБУ = Результат.Выбрать();  
			
			Пока ВыборкаБУ.Следующий() Цикл 
				
				Параметры.Заполнить(ВыборкаБУ); 
				Параметры.СчетКт = ?(ВыборкаБУ.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка(), "-", ВыборкаБУ.СчетКт);
				Параметры.СчетДт = ?(ВыборкаБУ.СчетДт = ПланыСчетов.Хозрасчетный.ПустаяСсылка(), "-", ВыборкаБУ.СчетДт);
				ТабДокумент.Вывести(ОбластьМакета);    
				
			КонецЦикла;
			
		Иначе
			
			ТабДокумент.Вывести(ОбластьМакета);       
		
		КонецЕсли; 
		
		ОбластьМакета = Макет.ПолучитьОбласть("Страница2ДанныеБУПодвал");
		Параметры     = ОбластьМакета.Параметры;
		ДанныеФизЛицаОтветственный = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Ответственный.ФизическоеЛицо, Ссылка.Дата);
		Параметры.ОтветственныйДолжность = ДанныеФизЛицаОтветственный.Должность;
		Параметры.ОтветственныйФИО		 = ДанныеФизЛицаОтветственный.Представление;
		ОбластьМакета.Параметры.ГлавныйБухгалтер 	= ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		ТабДокумент.Вывести(ОбластьМакета);       
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;

КонецФункции // ПечатьПриемаРемонт_2016()

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОЗ2") Тогда

		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОЗ2", НСтр("ru='Форма ОЗ-2';uk='Форма ОЗ-2'"), 
			ПечатьОЗ2(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , );

	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемаРемонтБюдж") Тогда

		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриемаРемонтБюдж", 
			НСтр("ru='Акт приема отремонтированных, реконструированных и модернизированных основных средств';uk='Акт приймання відремонтованих, реконструйованих та модернізованих основних засобів'"), 
			ПечатьПриемаРемонт_2016(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , );

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "ОбъектСтроительства");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли