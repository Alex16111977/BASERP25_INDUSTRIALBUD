&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	НачалоПериода = Параметры.НачалоПериода;
	ОкончаниеПериода = Параметры.ОкончаниеПериода;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИННОрганизации", РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ОкончаниеПериода, "ИНН").ИНН);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации() = "ЕРП" Тогда
		
		ДополнительныеПараметры.Вставить("ЭтоЕРП", Истина);
		ДополнительныеПараметры.Вставить("ТипДокументаИмпорт", "ДокументСсылка.ТаможеннаяДекларацияИмпорт");
		ДополнительныеПараметры.Вставить("ПоставщикИмпорт", "КонтрагентПоставщика");
		ДополнительныеПараметры.Вставить("ИННКонтрагента", "ИННПлательщикаНДС"); 
		ДополнительныеПараметры.Вставить("СуммаДокументаНК",
		"ВЫБРАТЬ
		|	СУММА(НДСНалоговыйКредит.СуммаНДС) КАК НДС
		|ИЗ
		|	РегистрНакопления.НДСРеестрПолученныхНалоговыхДокументов КАК НДСНалоговыйКредит
		|ГДЕ
		|	НДСНалоговыйКредит.Регистратор = &Регистратор");
		ДополнительныеПараметры.Вставить("СуммаДокументаНО",
		"ВЫБРАТЬ
		|	-СУММА(НДСНалоговыеОбязательства.СуммаНДС) КАК НДС
		|ИЗ
		|	РегистрНакопления.НДСРеестрВыданныхНалоговыхДокументов КАК НДСНалоговыеОбязательства
		|ГДЕ
		|	НДСНалоговыеОбязательства.Регистратор = &Регистратор
		|
		|ИМЕЮЩИЕ
		|	СУММА(НДСНалоговыеОбязательства.СуммаНДС) < 0");

	Иначе
		
		ДополнительныеПараметры.Вставить("ЭтоЕРП", Ложь);
		ДополнительныеПараметры.Вставить("ТипДокументаИмпорт", "ДокументСсылка.ГТДИмпорт");
		ДополнительныеПараметры.Вставить("ПоставщикИмпорт", "ПоставщикТоваров");
		ДополнительныеПараметры.Вставить("ИННКонтрагента", "ИНН");
		ДополнительныеПараметры.Вставить("СуммаДокументаНК",
		"ВЫБРАТЬ
		|	СУММА(НДСНалоговыйКредит.НДС) КАК НДС
		|ИЗ
		|	РегистрНакопления.НДСНалоговыйКредит КАК НДСНалоговыйКредит
		|ГДЕ
		|	НДСНалоговыйКредит.Регистратор = &Регистратор");
		ДополнительныеПараметры.Вставить("СуммаДокументаНО",
		"ВЫБРАТЬ
		|	-СУММА(НДСНалоговыеОбязательства.НДС) КАК НДС
		|ИЗ
		|	РегистрНакопления.НДСНалоговыеОбязательства КАК НДСНалоговыеОбязательства
		|ГДЕ
		|	НДСНалоговыеОбязательства.Регистратор = &Регистратор
		|
		|ИМЕЮЩИЕ
		|	СУММА(НДСНалоговыеОбязательства.НДС) < 0");

	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Параметры.СохраненныйДокумент) Тогда
		
		СписокСохранения = Параметры.СохраненныйДокумент.ДанныеОтчета.Получить(); 
		ЗначениеВРеквизитФормы(СписокСохранения.Таблица, "Таблица");
		ЗначениеВРеквизитФормы(СписокСохранения.П2T1R, "П2T1R");            
		СписокСохранения.Свойство("НКПредыдущегоПериода", НКПредыдущегоПериода);
		СписокСохранения.Свойство("НКТекущегоПериода", НКТекущегоПериода);
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.ДанныеПредыдущегоПериода) Тогда
		
		ЗаполнитьДанныеПредыдущегоПериодаНаСервере(Параметры.ДанныеПредыдущегоПериода);

	КонецЕсли;
	
	Если Параметры.Свойство("Гр16") Тогда
		НКПредыдущегоПериода = Параметры.Гр16;
	КонецЕсли;
	
	Если Параметры.Свойство("Гр19") Тогда
		НКТекущегоПериода = Параметры.Гр19;
	КонецЕсли;
	
	ПересчитатьИтогиНаСервере();
	
	МассивДоступныхТиповДокумента = Новый Массив;
	МассивДоступныхТиповДокумента.Добавить(Тип("ДокументСсылка.РегистрацияВходящегоНалоговогоДокумента"));
	МассивДоступныхТиповДокумента.Добавить(Тип(ДополнительныеПараметры.ТипДокументаИмпорт));
	МассивДоступныхТиповДокумента.Добавить(Тип("ДокументСсылка.Приложение2КНалоговойНакладной"));
	Элементы.ТаблицаДокумент.ОграничениеТипа = Новый ОписаниеТипов(МассивДоступныхТиповДокумента); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Таблица.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыбранноеЗначение);
	НоваяСтрока.Период = НачалоМесяца(НоваяСтрока.Период);
	
	НоваяСтрока.СуммаВключитьВТаблицу = Мин(Отклонение, НоваяСтрока.СуммаДокумента);
	НоваяСтрока.СуммаСледующийПериод = НоваяСтрока.СуммаВключитьВТаблицу;

	ПересчитатьИтоги();	
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанные(ИдентификаторФормы)
	
	ЗаполнитьИтоговуюТаблицу();
	
	СписокСохранения = Новый Структура;
	СписокСохранения.Вставить("НКПредыдущегоПериода", НКПредыдущегоПериода);
	СписокСохранения.Вставить("НКТекущегоПериода", НКТекущегоПериода);
	СписокСохранения.Вставить("Таблица", РеквизитФормыВЗначение("Таблица"));
	СписокСохранения.Вставить("П2T1R", РеквизитФормыВЗначение("П2T1R"));
	
	Возврат ПоместитьВоВременноеХранилище(СписокСохранения, ИдентификаторФормы);

КонецФункции

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Если ПроверитьЗаполнение() Тогда
		
		ВладелецФормы.СохранитьРасшифровкуПриложения2(ПодготовитьДанные(ВладелецФормы.УникальныйИдентификатор));   
		
		Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоговуюТаблицу()

	П2T1R.Очистить();
	
	ТЗ = П2T1R.Выгрузить();
	
	Для каждого ИсходнаяСтрока ИЗ Таблица Цикл
		
		Если НЕ ЗначениеЗаполнено(ИсходнаяСтрока.СуммаВключитьВТаблицу) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.П2T1RXXXXG2 = Месяц(ИсходнаяСтрока.Период);
		НоваяСтрока.П2T1RXXXXG3 = Год(ИсходнаяСтрока.Период);
		НоваяСтрока.П2T1RXXXXG4 = ИсходнаяСтрока.ИНН;
		НоваяСтрока.П2T1RXXXXG51 = Месяц(ИсходнаяСтрока.ДатаДокумента);
		НоваяСтрока.П2T1RXXXXG52 = Год(ИсходнаяСтрока.ДатаДокумента);
		НоваяСтрока.П2T1RXXXXG6 = ИсходнаяСтрока.СуммаВключитьВТаблицу;
		НоваяСтрока.П2T1RXXXXG7 = ИсходнаяСтрока.ПризнакРеорганизации;
		НоваяСтрока.П2T1RXXXXG8 = ИсходнаяСтрока.СуммаНалоговыйДолг;
		НоваяСтрока.П2T1RXXXXG9 = ИсходнаяСтрока.СуммаВозмещение;
		НоваяСтрока.П2T1RXXXXG10 = ИсходнаяСтрока.СуммаСледующийПериод;
		
	КонецЦикла;
	
	ТЗ.Свернуть("П2T1RXXXXНомерСтроки, П2T1RXXXXG2, П2T1RXXXXG3, П2T1RXXXXG4, П2T1RXXXXG51, П2T1RXXXXG52, П2T1RXXXXG7","П2T1RXXXXG6, П2T1RXXXXG8, П2T1RXXXXG9, П2T1RXXXXG10");
	ТЗ.Сортировать("П2T1RXXXXG3, П2T1RXXXXG2, П2T1RXXXXG52, П2T1RXXXXG51, П2T1RXXXXG4");
	
	ж = 0;
	Для каждого СтрокаТЗ Из ТЗ Цикл
		ж = ж + 1;
		СтрокаТЗ.П2T1RXXXXНомерСтроки = ж;
	КонецЦикла;
	
	П2T1R.Загрузить(ТЗ);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		ЗаполнитьИтоговуюТаблицу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверкаПустых = Новый Структура;
	ПроверкаПустых.Вставить("Период", НСтр("ru='Не заполнен период';uk= 'Не заповнений період'"));
	ПроверкаПустых.Вставить("ДатаДокумента", НСтр("ru='Не заполнена дата составления документа';uk= 'Не заповнена дата складання документу'"));
	ПроверкаПустых.Вставить("ИНН", НСтр("ru='Не заполнен ИНН';uk= 'Не заповнений ІПН'"));

//	Если ЗначениеЗаполнено(Отклонение) Тогда
//		СообщитьОбОшибке(Отказ, НСтр("ru='Итоговая сумма по таблице не совпадает с целевой (из строки 19 декларации)';uk= 'Загальна сума таблиці відрізняється від цільвої (з рядка 19 декларації)'"), "Отклонение");
//	КонецЕсли;
	
	Для каждого ИсходнаяСтрока ИЗ Таблица Цикл
		
		СуммаДеталей = ИсходнаяСтрока.СуммаНалоговыйДолг + ИсходнаяСтрока.СуммаВозмещение + ИсходнаяСтрока.СуммаСледующийПериод;
		
		Если ИсходнаяСтрока.ПризнакРеорганизации Тогда
			
			Если ЗначениеЗаполнено(СуммаДеталей) Тогда
				СообщитьОбОшибке(Отказ, НСтр("ru='Если указан признак реорганизации, в следующих графах сум быть не должно';uk= 'Якщо встановлена ознака реорганізації, в наступних графах суми повинні бути порожні'"), "ПризнакРеорганизации", ИсходнаяСтрока.ПолучитьИдентификатор());
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ИсходнаяСтрока.СуммаВключитьВТаблицу) Тогда
				СообщитьОбОшибке(Отказ, НСтр("ru='Если сумма включения в таблицу не указана, признак реорганизации тоже должен быть снят';uk= 'Якщо не вказано суму включення в таблицю, ознаки реоганізації теж не має бути'"), "СуммаВключитьВТаблицу", ИсходнаяСтрока.ПолучитьИдентификатор());
			КонецЕсли; 
			
		Иначе
			
			Если ИсходнаяСтрока.СуммаВключитьВТаблицу <> СуммаДеталей Тогда
				СообщитьОбОшибке(Отказ, НСтр("ru='Не совпадает общая сумма включения в таблицу и ее детализация  по графам';uk= 'Не співпадають загальна сума включення в таблицю та її деталізація по графам'"), "СуммаВключитьВТаблицу", ИсходнаяСтрока.ПолучитьИдентификатор());
			КонецЕсли;

		КонецЕсли;  
		
		Если НЕ ЗначениеЗаполнено(ИсходнаяСтрока.СуммаВключитьВТаблицу) Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ПроверяемыйРеквизит Из ПроверкаПустых Цикл
			Если Не ЗначениеЗаполнено(ИсходнаяСтрока[ПроверяемыйРеквизит.Ключ]) Тогда
				СообщитьОбОшибке(Отказ, ПроверяемыйРеквизит.Значение, ПроверяемыйРеквизит.Ключ, ИсходнаяСтрока.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура СообщитьОбОшибке(Отказ, ТекстСообщения, ИмяРеквизита, НомерСтроки = Неопределено);
	
	Отказ = Истина;
	
	Сообщение = Новый СообщениеПользователю;
	
	Сообщение.Поле = ?(ЗначениеЗаполнено(НомерСтроки), "Таблица[" + Формат(НомерСтроки, "ЧГ=") + "]." + ИмяРеквизита, ИмяРеквизита);
	Сообщение.Текст = ТекстСообщения;
	
	Сообщение.Сообщить(); 
	
КонецПроцедуры

&НаКлиенте
Процедура НКТекущегоПериодаПриИзменении(Элемент)
	
	Отклонение = НКТекущегоПериода - СуммаДокументов;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПриИзменении(Элемент)
	
	СуммаДокументов = Таблица.Итог("СуммаВключитьВТаблицу");
	Отклонение = НКТекущегоПериода - СуммаДокументов;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммаВключитьВТаблицуПриИзменении(Элемент)
	
	РаспределитьСуммуСтроки(Элементы.Таблица.ТекущиеДанные);
	
КонецПроцедуры                                    

&НаКлиентеНаСервереБезКонтекста
Процедура РаспределитьСуммуСтроки(ДанныеСтроки)
	
	СуммаКРаспределению = ДанныеСтроки.СуммаВключитьВТаблицу;
	Если ДанныеСтроки.ПризнакРеорганизации Тогда
		СуммаКРаспределению = 0;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСтроки.СуммаНалоговыйДолг) Тогда
		ДанныеСтроки.СуммаНалоговыйДолг = Мин(ДанныеСтроки.СуммаНалоговыйДолг, СуммаКРаспределению);
		СуммаКРаспределению = СуммаКРаспределению - ДанныеСтроки.СуммаНалоговыйДолг;
	КонецЕсли;

	Если ЗначениеЗаполнено(ДанныеСтроки.СуммаВозмещение) Тогда
		ДанныеСтроки.СуммаВозмещение = Мин(ДанныеСтроки.СуммаВозмещение, СуммаКРаспределению);
		СуммаКРаспределению = СуммаКРаспределению - ДанныеСтроки.СуммаВозмещение;
	КонецЕсли;
	
	ДанныеСтроки.СуммаСледующийПериод = СуммаКРаспределению;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПризнакРеорганизацииПриИзменении(Элемент)
	РаспределитьСуммуСтроки(Элементы.Таблица.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммаНалоговыйДолгПриИзменении(Элемент)
	РаспределитьСуммуСтроки(Элементы.Таблица.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммаВозмещениеПриИзменении(Элемент)
	РаспределитьСуммуСтроки(Элементы.Таблица.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммаСледующийПериодПриИзменении(Элемент)
	РаспределитьСуммуСтроки(Элементы.Таблица.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Таблица.ТекущиеДанные;                                         
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПолучитьДанныеДокумента(СтрокаТаблицы.Документ, ДополнительныеПараметры)); 
	СтрокаТаблицы.СуммаВключитьВТаблицу = Мин(СтрокаТаблицы.СуммаДокумента, Отклонение);
	РаспределитьСуммуСтроки(СтрокаТаблицы);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьДанныеДокумента(Док, ДополнительныеПараметры)
	
	Результат = Новый Структура("Контрагент, ИНН, ДатаДокумента, СуммаДокумента");
	
	Если ТипЗнч(Док) = Тип("ДокументСсылка.РегистрацияВходящегоНалоговогоДокумента") Тогда
		
		Если Док.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
			Результат.Контрагент = Док.Контрагент;
			Результат.ИНН = "500000000000";
		Иначе
			Результат.Контрагент = Док.Контрагент;
			Если ЗначениеЗаполнено(Результат.Контрагент) И ТипЗнч(Результат.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
				Результат.ИНН = Результат.Контрагент[ДополнительныеПараметры.ИННКонтрагента];			
			КонецЕсли;
		КонецЕсли; 
		
		Результат.ДатаДокумента = ?(ЗначениеЗаполнено(Док.ДатаВходящегоДокумента), Док.ДатаВходящегоДокумента, Док.Дата);
		Результат.СуммаДокумента = ПолучитьСуммуНДСДокументаПоРегиструНК(Док, ДополнительныеПараметры);

	ИначеЕсли ТипЗнч(Док) = Тип(ДополнительныеПараметры.ТипДокументаИмпорт) Тогда
		
		Результат.Контрагент = Док[ДополнительныеПараметры.ПоставщикИмпорт];
		Результат.ИНН = ДополнительныеПараметры.ИННОрганизации;
		
		Результат.ДатаДокумента = Док.Дата;
		Результат.СуммаДокумента = ПолучитьСуммуНДСДокументаПоРегиструНК(Док, ДополнительныеПараметры);
		
	ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.Приложение2КНалоговойНакладной") Тогда
		
		Результат.Контрагент = Док.Контрагент;
		Результат.ИНН = "300000000000";
		
		Результат.ДатаДокумента = Док.Дата;
		Результат.СуммаДокумента = ПолучитьСуммуНДСДокументаПоРегиструНО(Док, ДополнительныеПараметры);

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСуммуНДСДокументаПоРегиструНК(Док, ДополнительныеПараметры)
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Регистратор", Док);
	Запрос.Текст = ДополнительныеПараметры.СуммаДокументаНК;
	
	Результат = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.НДС;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСуммуНДСДокументаПоРегиструНО(Док, ДополнительныеПараметры)
	
	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Регистратор", Док);
	Запрос.Текст = ДополнительныеПараметры.СуммаДокументаНО;	
	
	Результат = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.НДС;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		Элементы.Таблица.ТекущиеДанные.Период = НачалоПериода;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АвтоподборНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИННОрганизации", ДополнительныеПараметры.ИННОрганизации);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 100
	|	НАЧАЛОПЕРИОДА(НДСНалоговыйКредитОбороты.Период, МЕСЯЦ) КАК Период,
	|	НДСНалоговыйКредитОбороты.Регистратор КАК Документ,
	|	НДСНалоговыйКредитОбороты.НДСОборот КАК СуммаДокумента
	|ПОМЕСТИТЬ НК
	|ИЗ
	|	РегистрНакопления.НДСНалоговыйКредит.Обороты(&НачалоПериода, &ОкончаниеПериода, Регистратор, Организация = &Организация) КАК НДСНалоговыйКредитОбороты
	|ГДЕ
	|	НДСНалоговыйКредитОбороты.НДСОборот > 0
	|	И НЕ НДСНалоговыйКредитОбороты.Регистратор В (&ИсключаемыеРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаДокумента УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 100
	|	НАЧАЛОПЕРИОДА(НДСНалоговыеОбязательстваОбороты.Период, МЕСЯЦ) КАК Период,
	|	НДСНалоговыеОбязательстваОбороты.Регистратор КАК Документ,
	|	-НДСНалоговыеОбязательстваОбороты.НДСОборот КАК СуммаДокумента
	|ПОМЕСТИТЬ НО
	|ИЗ
	|	РегистрНакопления.НДСНалоговыеОбязательства.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И СтатьяДекларацииНДСНалоговыеОбязательства = ЗНАЧЕНИЕ(Справочник.СтатьиНалоговыхДеклараций.НДС_НОКорректировка_ЭксОтдВидСХПрИзмНДС)) КАК НДСНалоговыеОбязательстваОбороты
	|ГДЕ
	|	НДСНалоговыеОбязательстваОбороты.НДСОборот < 0
	|	И НЕ НДСНалоговыеОбязательстваОбороты.Регистратор В (&ИсключаемыеРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаДокумента УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НК.Период КАК Период,
	|	НК.Документ КАК Документ,
	|	РегистрацияВходящегоНалоговогоДокумента.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА РегистрацияВходящегоНалоговогоДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода)
	|			ТОГДА ""500000000000""
	|		ИНАЧЕ РегистрацияВходящегоНалоговогоДокумента.Контрагент.ИНН
	|	КОНЕЦ КАК ИНН,
	|	ВЫБОР
	|		КОГДА РегистрацияВходящегоНалоговогоДокумента.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РегистрацияВходящегоНалоговогоДокумента.ДатаВходящегоДокумента
	|		ИНАЧЕ РегистрацияВходящегоНалоговогоДокумента.Дата
	|	КОНЕЦ КАК ДатаДокумента,
	|	НК.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	НК КАК НК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегистрацияВходящегоНалоговогоДокумента КАК РегистрацияВходящегоНалоговогоДокумента
	|		ПО НК.Документ = РегистрацияВходящегоНалоговогоДокумента.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НК.Период,
	|	НК.Документ,
	|	ДокИмпорта.ПоставщикТоваров,
	|	&ИННОрганизации,
	|	ДокИмпорта.Дата,
	|	НК.СуммаДокумента
	|ИЗ
	|	НК КАК НК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ГТДИмпорт КАК ДокИмпорта
	|		ПО НК.Документ = ДокИмпорта.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НО.Период,
	|	НО.Документ,
	|	Приложение2КНалоговойНакладной.Контрагент,
	|	""300000000000"",
	|	Приложение2КНалоговойНакладной.Дата,
	|	НО.СуммаДокумента
	|ИЗ
	|	НО КАК НО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Приложение2КНалоговойНакладной КАК Приложение2КНалоговойНакладной
	|		ПО НО.Документ = Приложение2КНалоговойНакладной.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаДокумента УБЫВ";   
	
	Если ДополнительныеПараметры.ЭтоЕРП Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ГТДИмпорт", "Документ.ТаможеннаяДекларацияИмпорт");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДокИмпорта.ПоставщикТоваров", "ДокИмпорта.КонтрагентПоставщика");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрНакопления.НДСНалоговыеОбязательства", "РегистрНакопления.НДСРеестрВыданныхНалоговыхДокументов");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрНакопления.НДСНалоговыйКредит", "РегистрНакопления.НДСРеестрПолученныхНалоговыхДокументов");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НДСОборот", "СуммаНДСОборот");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация = &Организация", "АналитикаУчетаПоПартнерам.Организация = &Организация");	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Контрагент.ИНН", "Контрагент.ИННПлательщикаНДС");	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Пока Отклонение > 0 Цикл
		
		МассивДокументов = Таблица.Выгрузить(, "Документ").ВыгрузитьКолонку(0);
		Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", МассивДокументов);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.СуммаВключитьВТаблицу = Мин(НоваяСтрока.СуммаДокумента, Отклонение);
			НоваяСтрока.СуммаСледующийПериод = НоваяСтрока.СуммаВключитьВТаблицу;
			
			Отклонение = Отклонение - НоваяСтрока.СуммаВключитьВТаблицу;
			
			Если Отклонение = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПересчитатьИтогиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Автоподбор(Команда)
	
	Если Отклонение > 0 Тогда
		АвтоподборНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСуммы(Команда)
	
	Перем ИмяРеквизита;
	
	Если Элементы.Таблица.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ТаблицаСуммаВключитьВТаблицу","СуммаВключитьВТаблицу");
	СтруктураРеквизитов.Вставить("ТаблицаПризнакРеорганизации", "ПризнакРеорганизации");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаНалоговыйДолг", 	"СуммаНалоговыйДолг");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаВозмещение", 		"СуммаВозмещение");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаСледующийПериод", "СуммаСледующийПериод");
	
	Если НЕ СтруктураРеквизитов.Свойство(Элементы.Таблица.ТекущийЭлемент.Имя, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрока ИЗ Элементы.Таблица.ВыделенныеСтроки Цикл 
		
		ДанныеСтроки = Таблица.НайтиПоИдентификатору(ТекСтрока); 
		Если ИмяРеквизита = "ПризнакРеорганизации" Тогда
			ДанныеСтроки[ИмяРеквизита] = Ложь;			
		Иначе
			ДанныеСтроки[ИмяРеквизита] = 0;
		КонецЕсли;
		
		РаспределитьСуммуСтроки(ДанныеСтроки);
		
	КонецЦикла;
	
	ПересчитатьИтоги();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтоги()

	СуммаДокументов = Таблица.Итог("СуммаВключитьВТаблицу");
	Отклонение = НКТекущегоПериода - СуммаДокументов;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиНаСервере()

	СуммаДокументов = Таблица.Итог("СуммаВключитьВТаблицу");
	Отклонение = НКТекущегоПериода - СуммаДокументов;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьМаксимальныеСуммы(Команда)
	
	Перем ИмяРеквизита;
	
	Если Элементы.Таблица.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ТаблицаСуммаВключитьВТаблицу","СуммаВключитьВТаблицу");
	СтруктураРеквизитов.Вставить("ТаблицаПризнакРеорганизации", "ПризнакРеорганизации");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаНалоговыйДолг", 	"СуммаНалоговыйДолг");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаВозмещение", 		"СуммаВозмещение");
	СтруктураРеквизитов.Вставить("ТаблицаСуммаСледующийПериод", "СуммаСледующийПериод");
	
	Если НЕ СтруктураРеквизитов.Свойство(Элементы.Таблица.ТекущийЭлемент.Имя, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрока ИЗ Элементы.Таблица.ВыделенныеСтроки Цикл 
		
		ДанныеСтроки = Таблица.НайтиПоИдентификатору(ТекСтрока);
		
		Если ИмяРеквизита = "СуммаВключитьВТаблицу" Тогда
			
			Если ДанныеСтроки.СуммаВключитьВТаблицу < ДанныеСтроки.СуммаДокумента Тогда
				
				ДанныеСтроки.СуммаВключитьВТаблицу = Мин(ДанныеСтроки.СуммаДокумента, Отклонение + ДанныеСтроки.СуммаВключитьВТаблицу);
				РаспределитьСуммуСтроки(ДанныеСтроки);
				ПересчитатьИтоги();
				
			КонецЕсли;
		
		ИначеЕсли ИмяРеквизита = "ПризнакРеорганизации" Тогда
			
			Если НЕ ДанныеСтроки.ПризнакРеорганизации Тогда
				
				ДанныеСтроки.ПризнакРеорганизации = Истина;
				РаспределитьСуммуСтроки(ДанныеСтроки);

			КонецЕсли;
			
		ИначеЕсли НЕ ДанныеСтроки.ПризнакРеорганизации Тогда
			
			Если ИмяРеквизита = "СуммаСледующийПериод" Тогда
				РаспределитьСуммуСтроки(ДанныеСтроки);
			КонецЕсли;
			Если ИмяРеквизита = "СуммаВозмещение" Тогда
				ДанныеСтроки.СуммаВозмещение = ДанныеСтроки.СуммаВключитьВТаблицу - ДанныеСтроки.СуммаНалоговыйДолг;
			КонецЕсли;
			Если ИмяРеквизита = "СуммаНалоговыйДолг" Тогда
				ДанныеСтроки.СуммаНалоговыйДолг = ДанныеСтроки.СуммаВключитьВТаблицу;
			КонецЕсли;
			
			РаспределитьСуммуСтроки(ДанныеСтроки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПересчитатьИтоги();	
	
КонецПроцедуры

&НаСервере
Процедура УменьшитьПодобраннуюСумму()

	ТЗ = Таблица.Выгрузить(Новый Массив,"Период, ДатаДокумента, СуммаВключитьВТаблицу");
	ТЗ.Колонки.Добавить("Идентификатор");
	
	Для каждого ИсходнаяСтрока ИЗ Таблица Цикл
		
		НоваяСтрока = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
		НоваяСтрока.Идентификатор = ИсходнаяСтрока.ПолучитьИдентификатор();
		
	КонецЦикла;
	
	ТЗ.Сортировать("Период, ДатаДокумента, СуммаВключитьВТаблицу");
	
	СнятьСумму = -Отклонение;
	
	МассивГраф = СтрРазделить("СуммаСледующийПериод,СуммаВозмещение,СуммаНалоговыйДолг,СуммаВключитьВТаблицу", ",");
	
	Для каждого ТекущаяГрафа ИЗ МассивГраф Цикл
		
		Для каждого СтрокаТЗ ИЗ ТЗ Цикл
			
			ИсходнаяСтрока = Таблица.НайтиПоИдентификатору(СтрокаТЗ.Идентификатор);
			Если ИсходнаяСтрока[ТекущаяГрафа] = 0 Тогда
				Продолжить;
			КонецЕсли;

			СнятьСуммуСтроки = Мин(ИсходнаяСтрока[ТекущаяГрафа], СнятьСумму);
			СнятьСумму = СнятьСумму - СнятьСуммуСтроки;
			
			ИсходнаяСтрока[ТекущаяГрафа] = ИсходнаяСтрока[ТекущаяГрафа] - СнятьСуммуСтроки;
			Если ТекущаяГрафа = "СуммаВключитьВТаблицу" Тогда
				Если ИсходнаяСтрока.СуммаВключитьВТаблицу = 0 И ИсходнаяСтрока.ПризнакРеорганизации Тогда
					ИсходнаяСтрока.ПризнакРеорганизации = 0;
				КонецЕсли;
			Иначе
				ИсходнаяСтрока.СуммаВключитьВТаблицу = ИсходнаяСтрока.СуммаВключитьВТаблицу - СнятьСуммуСтроки;
			КонецЕсли;   
			
			Если СнятьСумму = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла; 

		Если СнятьСумму = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПересчитатьИтогиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПредыдущегоПериода(Команда)
	
	АдресДанных = ВладелецФормы.ПолучитьДанныеТаблицы1Приложения2ПредыдущегоПериода();
	
	ЗаполнитьДанныеПредыдущегоПериодаНаСервере(АдресДанных);
	
	ПересчитатьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПредыдущегоПериодаНаСервере(АдресДанных)
	
	Если АдресДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТЗ = ПолучитьИзВременногоХранилища(АдресДанных);
	Если ТЗ = Неопределено Тогда
		Возврат;
	КонецЕсли;  
	
	Таблица.Очистить();
	Для каждого СтрокаТЗ Из ТЗ Цикл
		
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		РаспределитьСуммуСтроки(НоваяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьСуммыПодОтрицательноеОтклонение(Команда)

	Если Отклонение < 0 Тогда
		УменьшитьПодобраннуюСумму();
	КонецЕсли;	
	
КонецПроцедуры
