
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы);
	
	ВнеоборотныеАктивыСлужебный.ПроверитьПериодОтчетаВерсии24(
		ЗначенияОтбораДанных.НачалоПериода, 
		ЗначенияОтбораДанных.ОкончаниеПериода, 
		НСтр("ru='Отчет не поддерживает получение данных до даты начала учета необоротных активов версии 2.5 - %1.';uk='Звіт не підтримує отримання даних до дати початку обліку необоротних активів версії 2.5 – %1.'"),
		Отказ); 
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	УстановитьПараметрыОтчета(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, Неопределено, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
	ОформитьШапкуОтчета(ДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета
 
Процедура ОформитьШапкуОтчета(ТабДок)
	
	СписокГруппЯчеек = Новый Массив;
	СписокГруппЯчеек.Добавить(НСтр("ru='Показатели';uk='Показники'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Дата принятия к учету';uk='Дата прийняття до обліку'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Первоначальная стоимость';uk='Первісна вартість'"));
	
	СписокГруппЯчеек.Добавить(НСтр("ru='Стоимость';uk='Вартість'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Амортизация';uk='Амортизація'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Остаточная стоимость';uk='Залишкова вартість'"));
	
	СписокГруппЯчеек.Добавить(НСтр("ru='Увеличение стоимости';uk='Збільшення вартості'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Начисление амортизации';uk='Нарахування амортизації'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Уменьшение стоимости';uk='Зменшення вартості'"));
	СписокГруппЯчеек.Добавить(НСтр("ru='Списание амортизации';uk='Списання амортизації'"));
	
	СписокПодчиненныхЯчеек = Новый Массив;
	СписокПодчиненныхЯчеек.Добавить(НСтр("ru='БУ';uk='БО'"));
	СписокПодчиненныхЯчеек.Добавить(НСтр("ru='НУ';uk='ПО'"));
	СписокПодчиненныхЯчеек.Добавить(НСтр("ru='ПР';uk='ПР'"));
	СписокПодчиненныхЯчеек.Добавить(НСтр("ru='ВР';uk='ТР'"));
	СписокПодчиненныхЯчеек.Добавить(НСтр("ru='УУ';uk='УО'"));
	
	ВнеоборотныеАктивыСлужебный.ОбъединитьПодчиненныеЯчейки(ТабДок, СписокГруппЯчеек, СписокПодчиненныхЯчеек);
	
КонецПроцедуры

Процедура УстановитьПараметрыОтчета(НастройкиОсновнойСхемы)

	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ПоказательБУ", НСтр("ru='БУ';uk='БО'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ПоказательНУ", НСтр("ru='НУ';uk='ПО'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ПоказательПР", НСтр("ru='ПР';uk='ПР'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ПоказательВР", НСтр("ru='ВР';uk='ТР'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ПоказательУУ", НСтр("ru='УУ';uk='УО'"));
	
	// ТипыДокументовСписаниеАмортизации
	ТипыДокументов = Новый СписокЗначений;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПереоценкаНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.СписаниеНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииНМА"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.РеализацияУслугПрочихАктивов"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ТипыДокументовСписаниеАмортизации", ТипыДокументов);
	
	// ТипыДокументовНачислениеАмортизации
	ТипыДокументов = Новый СписокЗначений;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ВводОстатковВнеоборотныхАктивов2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПереоценкаНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииНМА"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ТипыДокументовНачислениеАмортизации", ТипыДокументов);
	
	// ТипыДокументовУвеличениеСтоимости
	ТипыДокументов = Новый СписокЗначений;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ВводОстатковВнеоборотныхАктивов2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПереоценкаНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииНМА"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ТипыДокументовУвеличениеСтоимости", ТипыДокументов);
	
	// ТипыДокументовУменьшениеСтоимости
	ТипыДокументов = Новый СписокЗначений;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПереоценкаНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.СписаниеНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеНМА2_4"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.РеализацияУслугПрочихАктивов"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииНМА"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "ТипыДокументовУменьшениеСтоимости", ТипыДокументов);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы)

	ЗначенияОтбораДанных = Новый Структура;
	
	ВнеоборотныеАктивыСлужебный.ПериодОтчета(НастройкиОсновнойСхемы, ЗначенияОтбораДанных);
	
	Возврат ЗначенияОтбораДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли