#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта = "СводныеПроводки" Тогда
		
		Для каждого Макет Из СхемаКомпоновкиДанных.Макеты Цикл
			
			ПараметрРасшифровки =  Макет.Параметры.Найти("ПарамРасшифровки");
			
			Если ПараметрРасшифровки <> Неопределено Тогда
				
				ПолеРегистратор = ПараметрРасшифровки.ВыраженияПолей.Найти("Регистратор");
				ПараметрРасшифровки.ВыраженияПолей.Удалить(ПолеРегистратор);
				
			КонецЕсли;
		
		КонецЦикла;
		
	ИначеЕсли КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта = "КарточкаСчетаМеждународный" Тогда
		
		ПолеСчет = Новый ПолеКомпоновкиДанных("Счет");
		ОтборПоСчету = Неопределено;
		Для каждого ЭлементОтбора Из СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = ПолеСчет Тогда
				ОтборПоСчету = ЭлементОтбора;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если ОтборПоСчету <> Неопределено Тогда 
			
			ИдентификаторОтбораСчета = КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьИдентификаторПоОбъекту(ОтборПоСчету);
			ОтборСчета = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторОтбораСчета);
			Если ОтборСчета <> Неопределено И ОтборСчета.Использование Тогда
				
				НулевойНачальныйОстаток = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("КоличествоНачальныйОстаток");
				НулевойКонечныйОстаток 	= СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("КоличествоКонечныйОстаток");
				СхемаКомпоновкиДанных.ВычисляемыеПоля.Удалить(НулевойНачальныйОстаток);
				СхемаКомпоновкиДанных.ВычисляемыеПоля.Удалить(НулевойКонечныйОстаток);
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли;
	
	МеждународнаяОтчетностьСервер.ПриКомпоновкеРезультата(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
	Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта = "КарточкаСчетаМеждународный" Тогда
		
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Сальдо на начало';uk='Сальдо на початок'"));
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Сальдо на конец';uk='Сальдо на кінець'"));
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Оборот Дт';uk='Оборот Дт'"));
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Оборот Кт';uk='Оборот Кт'"));
		
	ИначеЕсли КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта = "СводныеПроводки" Тогда
		
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Дебет';uk='Дебет'"));
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Кредит';uk='Кредит'"));
		
	ИначеЕсли КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта = "ОтчетПоПроводкамМеждународный" Тогда
		
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Оборот Дт';uk='Оборот Дт'"));
		МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru='Оборот Кт';uk='Оборот Кт'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
