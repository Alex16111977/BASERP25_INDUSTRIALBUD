
// ПриСозданииНаСервере()
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мСпрашиватьОСохранении = Истина;
	мПрограммноеЗакрытие   = Ложь;
	
	Организация = Параметры.Организация;
	
	мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;

	ВыбранныйОтчет 	   = Параметры.ВыбранныйОтчет;
	
	мСохраненныйДок	   = Параметры.мСохраненныйДок;
	
	Элементы.ТаблицаВыбора.Доступность = Истина;
		
	ПолучитьТаблицуОтчетов();
	
	Если ТаблицаВыбора.Количество() = 0 Тогда
		Элементы.ТаблицаВыбора.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Сохранить()
//
&НаКлиенте
Процедура Сохранить(Команда)
	
	мСпрашиватьОСохранении = Ложь;
	Закрыть();
	
КонецПроцедуры // Сохранить()

// ПередЗакрытием()
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы <> Неопределено Тогда 
		
		Если ЗавершениеРаботы И Модифицированность Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	Если мПрограммноеЗакрытие = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение() Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если мСпрашиватьОСохранении <> Ложь И Модифицированность Тогда
		
		Отказ = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Настройки были изменены. Сохранить изменения?';uk='Настройки були змінені. Зберегти зміни?'"), РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
		
	ИначеЕсли мСпрашиватьОСохранении <> Ложь И НЕ Модифицированность Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДействияПриЗакрытии();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПриЗакрытии()
	
	ВыбраннаяСтрока = ТаблицаВыбора.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если ВыбраннаяСтрока.Количество() > 0 Тогда
		ВладелецФормы.СтруктураРеквизитовФормы.ВыбранныйОтчет = ВыбраннаяСтрока[0].Ссылка;	
	Иначе
		ВладелецФормы.СтруктураРеквизитовФормы.ВыбранныйОтчет = Неопределено;
	КонецЕсли;
	
	мПрограммноеЗакрытие = Истина;
	Отказ = Истина;
	
	ПараметрыВозврата = Новый Структура();
	
	Закрыть(ПараметрыВозврата);
	
КонецПроцедуры // ПередЗакрытием()

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры)Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		мПрограммноеЗакрытие = Истина;
		Закрыть();
		Возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ДействияПриЗакрытии();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуОтчетов()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр ("ДатаОкончания",	мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр ("Организация",	Организация);
	Запрос.УстановитьПараметр ("Декларация","РегламентированныйОтчетРасчетЕдиногоНалогаФизЛиц");
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегламентированныйОтчет.ДатаНачала КАК ДатаНачала,
	               |	РегламентированныйОтчет.ДатаОкончания,
	               |	РегламентированныйОтчет.ПредставлениеПериода КАК ПредставлениеПериода,
	               |	РегламентированныйОтчет.ДанныеОтчета КАК ДанныеОтчета,
	               |	РегламентированныйОтчет.НаименованиеОтчета КАК НаименованиеОтчета,
	               |	РегламентированныйОтчет.Комментарий КАК Комментарий,
	               |	РегламентированныйОтчет.Ссылка
	               |ИЗ
	               |	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	               |ГДЕ
	               |	РегламентированныйОтчет.ПометкаУдаления = ЛОЖЬ
	               |	И РегламентированныйОтчет.Организация = &Организация
	               |	И РегламентированныйОтчет.ИсточникОтчета = &Декларация
				   |	И РегламентированныйОтчет.ДатаОкончания < &ДатаОкончания
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДатаОкончания";
				   
	ТаблицаВыбора.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	ЕстьУточненки = Ложь;
	Пока Выборка.Следующий() Цикл
	
		Если НЕ мСохраненныйДок = Неопределено Тогда
			Если мСохраненныйДок.Ссылка = Выборка.Ссылка Тогда
				Продолжить;
			КонецЕсли;	
		КонецЕсли;
		
		ДанныеОтчета = Выборка.ДанныеОтчета.Получить();
		ПоказателиОтчета = ""; 
		ДанныеДекларации = ""; ЗначениеПоляТипа = "";
		Если  ТипЗнч(ДанныеОтчета) = Тип("Структура")
			И ДанныеОтчета.Свойство("ПоказателиОтчета",ПоказателиОтчета)
			И ПоказателиОтчета.Свойство("ПолеТабличногоДокументаДекларацияЕН", ДанныеДекларации)
			Тогда
			
		Иначе
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = ТаблицаВыбора.Добавить();
		
		СтрокаТаблицы.НаименованиеОтчета = Выборка.НаименованиеОтчета;
		СтрокаТаблицы.Комментарий 		 = Выборка.Комментарий;
		СтрокаТаблицы.Ссылка 			 = Выборка.Ссылка;
		СтрокаТаблицы.Период 			 = Выборка.ПредставлениеПериода;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВыбранныйОтчет) Тогда
	
		ВыбраннаяСтрока = ТаблицаВыбора.НайтиСтроки(Новый Структура("Ссылка", ВыбранныйОтчет));
		Если ВыбраннаяСтрока.Количество() = 0  Тогда
			СтрокаТаблицы = ТаблицаВыбора.Вставить(0);
			
			СтрокаТаблицы.НаименованиеОтчета = ВыбранныйОтчет.НаименованиеОтчета;
			СтрокаТаблицы.Комментарий 		 = ВыбранныйОтчет.Комментарий;
			СтрокаТаблицы.Ссылка 			 = ВыбранныйОтчет.Ссылка;
			СтрокаТаблицы.Период 			 = ВыбранныйОтчет.ПредставлениеПериода;
			СтрокаТаблицы.Пометка = Истина;
			
		Иначе
			
			ВыбраннаяСтрока[0].Пометка = Истина;
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаВыбораПометкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаВыбора.ТекущиеДанные;
	
	Для каждого СтрокаВыбора Из ТаблицаВыбора Цикл
		Если СтрокаВыбора.ПолучитьИдентификатор() = ТекущаяСтрока.ПолучитьИдентификатор() Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВыбора.Пометка = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Поле.Имя = "ТаблицаВыбораПометка" Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	ТекСтрока = ТаблицаВыбора.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если ЗначениеЗаполнено(ТекСтрока.Ссылка) Тогда
		ПоказатьЗначение(,ТекСтрока.Ссылка)
	КонецЕсли;
	
КонецПроцедуры
