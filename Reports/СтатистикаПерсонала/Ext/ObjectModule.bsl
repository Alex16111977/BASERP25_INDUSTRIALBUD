#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ИнициализироватьОтчет();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();     
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьГигКонтракты") Тогда
		ЗаголовокГруппы = НСтр("ru='Среднесписочная численность';uk='Середня за списком чисельність'");
		
		Для Каждого Группа из НастройкиОтчета.Выбор.Элементы Цикл 
			Если ВРЕГ(Строка(Группа.Заголовок)) = ВРЕГ(ЗаголовокГруппы) Тогда  
				Для Каждого ЭлементГруппы Из Группа.Элементы Цикл  
					Если ВРЕГ(Строка(ЭлементГруппы.Поле)) = "СРЕДНЕСПИСОЧНАЯЧИСЛЕННОСТЬРАБОТНИКОВПОГИГКОНТРАКТУ" Тогда
						ЭлементГруппы.Использование = Ложь;
						Прервать;
					КонецЕсли	
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
	ПараметрИсключатьПериодыПоПравилам1ПВ = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИсключатьПериодыПоПравилам1ПВ"));
	Если ПараметрИсключатьПериодыПоПравилам1ПВ.Значение = ЛОЖЬ  Тогда
		
		ЗаголовокГруппы = НСтр("ru='Неучитываемые периоды';uk='Невраховані періоди'");
		
		Для Каждого Группа из НастройкиОтчета.Выбор.Элементы Цикл 
			Если ВРЕГ(Строка(Группа.Заголовок)) = ВРЕГ(ЗаголовокГруппы) Тогда  
				Для Каждого ЭлементГруппы Из Группа.Элементы Цикл  
					Если ВРЕГ(Строка(ЭлементГруппы.Поле)) = "МОБИЛИЗАЦИЯ" 
						ИЛИ ВРЕГ(Строка(ЭлементГруппы.Поле)) = "МОБИЛИЗАЦИЯБЕЗОПЛАТЫ" 
						ИЛИ ВРЕГ(Строка(ЭлементГруппы.Поле)) = "ПРИОСТАНОВКАТРУДОВОГОДОГОВОРА"  
						ИЛИ ВРЕГ(Строка(ЭлементГруппы.Поле)) = "ОТПУСКНЕОПЛАЧИВАЕМЫЙНАВРЕМЯВОЕННОГОПОЛОЖЕНИЯ"
						Тогда
						ЭлементГруппы.Использование = Ложь;
					КонецЕсли	
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;	 
	КонецЕсли;

	СтандартнаяОбработка = ложь;
	
	ДокументРезультат.Очистить();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);

КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИнициализироватьОтчет();
	ЗначениеВДанныеФормы(ЭтотОбъект, Форма.Отчет);
	
КонецПроцедуры

// Функция возвращает структуру настроек отчета.
//
Функция ПолучитьНастройкиОтчета() Экспорт
	
	// Получим настройки по умолчанию.
	Настройки = ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию();
	
	Возврат Настройки;
	
КонецФункции


#КонецОбласти

#КонецЕсли