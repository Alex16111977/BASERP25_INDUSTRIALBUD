#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ИнициализироватьОтчет(); 
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();				   

	//УстановитьЗначенияПараметров(НастройкиОтчета);
	
	ДокументРезультат.Очистить();
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СпискиПерсональногоВоинскогоУчета";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ДатаОтчета = '00010101';
	
	УстановитьДатуОтчета(НастройкиОтчета, ДатаОтчета);
	
	ДанныеОтчета = Новый ДеревоЗначений;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	//СоответствиеПользовательскихПолей = ЗарплатаКадрыОтчеты.СоответствиеПользовательскихПолей(НастройкиОтчета);
	// Создадим и инициализируем процессор компоновки.
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	
	// Обозначим начало вывода
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ВывестиМакетСпискиПерсональногоВоинскогоУчета(ДокументРезультат, ДанныеОтчета, ДатаОтчета);

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИнициализироватьОтчет();
	ЗначениеВДанныеФормы(ЭтотОбъект, Форма.Отчет);
	
КонецПроцедуры

Процедура УстановитьДатуОтчета(НастройкиОтчета, ДатаОтчета)
	
	ЗначениеПараметраПериод = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	
	Если ЗначениеПараметраПериод <> Неопределено Тогда
		
		УстановитьДатуОтчета = Ложь;
		
		Если ТипЗнч(ЗначениеПараметраПериод.Значение) = Тип("Неопределено") Тогда
			УстановитьДатуОтчета = Истина;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеПараметраПериод.Значение) = Тип("Дата")
			И ЗначениеПараметраПериод.Значение = '00010101' Тогда
			УстановитьДатуОтчета = Истина;
		КонецЕсли; 
		
		Если ТипЗнч(ЗначениеПараметраПериод.Значение) = Тип("СтандартнаяДатаНачала")
			И Дата(ЗначениеПараметраПериод.Значение) = '00010101' Тогда
			УстановитьДатуОтчета = Истина;
		КонецЕсли; 
		
		Если УстановитьДатуОтчета Тогда
			ЗначениеПараметраПериод.Значение = ТекущаяДатаСеанса();
		КонецЕсли; 
		
		ДатаОтчета = Дата(ЗначениеПараметраПериод.Значение);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиМакетСпискиПерсональногоВоинскогоУчета(ДокументРезультат, ДанныеОтчета, ДатаОтчета)
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет 		  = УправлениеПечатью.МакетПечатнойФормы("Отчет.СпискиПерсональногоВоинскогоУчета.ПФ_MXL_UK_СпискиПерсональногоВоинскогоУчета");
	Заголовок 	  = Макет.ПолучитьОбласть("Заголовок");
	СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы"); 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаФизическихЛиц = Новый ТаблицаЗначений;
	ТаблицаФизическихЛиц.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	
	Для Каждого ДанныеОрганизации Из ДанныеОтчета.Строки Цикл
		Для Каждого ДанныеСотрудников Из ДанныеОрганизации.Строки Цикл 
			НоваяСтрокаТаблицыСотрудников = ТаблицаФизическихЛиц.Добавить();
			НоваяСтрокаТаблицыСотрудников.ФизическоеЛицо = ДанныеСотрудников.ФизическоеЛицо;
		КонецЦикла;	
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаФизическихЛиц", ТаблицаФизическихЛиц); 
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	&ТаблицаФизическихЛиц КАК ТаблицаФизическихЛиц";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФизическиеЛицаОбразование.Владелец КАК ФизическоеЛицо,
		|	ФизическиеЛицаОбразование.Ссылка,
		|	ФизическиеЛицаОбразование.ВидОбразования,
		|	ФизическиеЛицаОбразование.Специальность,
		|	ФизическиеЛицаОбразование.ВидДокумента,
		|	ФизическиеЛицаОбразование.Серия,
		|	ФизическиеЛицаОбразование.Номер,
		|	ФизическиеЛицаОбразование.ОсновноеОбразование КАК ОсновноеОбразование,
		|	ФизическиеЛицаОбразование.ВидПослевузовскогоОбразования,
		|	ФизическиеЛицаОбразование.ВидДополнительногоОбучения
		|ИЗ
		|	Справочник.ОбразованиеФизическихЛиц КАК ФизическиеЛицаОбразование
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ТаблицаОтборовФизическихЛиц
		|		ПО ФизическиеЛицаОбразование.Владелец = ТаблицаОтборовФизическихЛиц.ФизическоеЛицо
		|ГДЕ
		|	ФизическиеЛицаОбразование.ОсновноеОбразование = ИСТИНА";
	ОбразованияФизическихЛиц = Запрос.Выполнить().Выбрать();  
	
	
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РодственникиФизическихЛиц.Владелец КАК ФизическоеЛицо,
		|	РодственникиФизическихЛиц.СтепеньРодства КАК СтепеньРодства,
		|	РодственникиФизическихЛиц.Наименование КАК ФИОРодственника,
		|	РодственникиФизическихЛиц.ДатаРождения КАК ДатаРожденияРодственника
		|ИЗ
		|	Справочник.РодственникиФизическихЛиц КАК РодственникиФизическихЛиц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ТаблицаОтборовФизическихЛиц
		|		ПО РодственникиФизическихЛиц.Владелец = ТаблицаОтборовФизическихЛиц.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	РодственникиФизическихЛиц.ДатаРождения УБЫВ
		|";
	РодственникиФизическихЛиц = Запрос.Выполнить().Выбрать();     
	
	СписокПаспортов = Новый Массив;
   	СписокПаспортов.Добавить(Справочники.ВидыДокументовФизическихЛиц.Паспорт);
   	СписокПаспортов.Добавить(Справочники.ВидыДокументовФизическихЛиц.Загранпаспорт);
	
	Запрос.УстановитьПараметр("СписокПаспортов", СписокПаспортов); 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК ФизическоеЛицо,     
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ДокументВид,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
	|			,
	|			ВидДокумента В (&СписокПаспортов)
	|				И Физлицо В
	|					(ВЫБРАТЬ
	|						ТаблицаОтборовФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
	|					ИЗ
	|						ВТФизическиеЛица КАК ТаблицаОтборовФизическихЛиц)) КАК ДокументыФизическихЛицСрезПоследних";  
	ДокументыФизическихЛиц = Запрос.Выполнить().Выбрать();

	Для Каждого ДанныеОрганизации Из ДанныеОтчета.Строки Цикл  
		ПараметрыЗаголовка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеОрганизации.Организация, "Наименование, НаименованиеПолное");
		Заголовок.Параметры.ПолноеНаименованиеОрганизации = ?(ЗначениеЗаполнено(ПараметрыЗаголовка.НаименованиеПолное), ПараметрыЗаголовка.НаименованиеПолное, ПараметрыЗаголовка.Наименование);

		ДокументРезультат.Вывести(Заголовок);
		Для Каждого ДанныеСотрудников Из ДанныеОрганизации.Строки Цикл 
					
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы.Параметры, ДанныеСотрудников);    
			ДатаРегистратора = Формат(ДанныеСотрудников.ДатаРегистратора,"ДФ=dd.MM.yyyy");  
			
			Если ЗначениеЗаполнено(ДанныеСотрудников.КодПоДРФО) И ЗначениеЗаполнено(ДанныеСотрудников.НомерЗаписиВРеестреВоеннообязанных) Тогда  
				СтрокаТаблицы.Параметры.НомерЗаписиВРеестреВоеннообязанных = ДанныеСотрудников.НомерЗаписиВРеестреВоеннообязанных + "/" + ДанныеСотрудников.КодПоДРФО
			Иначе 
				СтрокаТаблицы.Параметры.НомерЗаписиВРеестреВоеннообязанных = ?(ЗначениеЗаполнено(ДанныеСотрудников.КодПоДРФО),ДанныеСотрудников.КодПоДРФО, ДанныеСотрудников.НомерЗаписиВРеестреВоеннообязанных ) 
			КонецЕсли;      
			
			Если ЗначениеЗаполнено(ДанныеСотрудников.РегистраторДоУвольнения) Тогда    
				СтрокаТаблицы.Параметры.РеквизитыРегистратора = "Наказ № " + ДанныеСотрудников.НомерРегистратораДоУвольнения + " від " + Формат(ДанныеСотрудников.ДатаРегистратораДоУвольнения,"ДФ=dd.MM.yyyy") + "/" + Символы.ПС + Символы.ПС +
				"Наказ про звільнення № " + ДанныеСотрудников.НомерРегистратора + " від " + ДатаРегистратора;
			Иначе	
				СтрокаТаблицы.Параметры.РеквизитыРегистратора = "Наказ № " + ДанныеСотрудников.НомерРегистратора + " від " + ДатаРегистратора;
			КонецЕсли; 
			
			СтрокаТаблицы.Параметры.РеквизитыСообщения = "";
			Если ЗначениеЗаполнено(ДанныеСотрудников.НомерСообщенияДоУвольнения) И ЗначениеЗаполнено(ДанныеСотрудников.ДатаСообщенияДоУвольнения) И
				ЗначениеЗаполнено(ДанныеСотрудников.НомерСообщения) И ЗначениеЗаполнено(ДанныеСотрудников.ДатаСообщения) Тогда
				СтрокаТаблицы.Параметры.РеквизитыСообщения = "Повідомлення № " + ДанныеСотрудников.НомерСообщенияДоУвольнения + " від " + Формат(ДанныеСотрудников.ДатаСообщенияДоУвольнения,"ДФ=dd.MM.yyyy") + "/" + Символы.ПС + Символы.ПС +
				"Повідомлення про звільнення № " + ДанныеСотрудников.НомерСообщения + " від " + Формат(ДанныеСотрудников.ДатаСообщения, "ДФ=dd.MM.yyyy");  
			ИначеЕсли ЗначениеЗаполнено(ДанныеСотрудников.НомерСообщенияДоУвольнения) И ЗначениеЗаполнено(ДанныеСотрудников.ДатаСообщенияДоУвольнения) И
				Не ЗначениеЗаполнено(ДанныеСотрудников.НомерСообщения) И  Не ЗначениеЗаполнено(ДанныеСотрудников.ДатаСообщения) Тогда
				СтрокаТаблицы.Параметры.РеквизитыСообщения = "Повідомлення № " + ДанныеСотрудников.НомерСообщенияДоУвольнения + " від " + Формат(ДанныеСотрудников.ДатаСообщенияДоУвольнения,"ДФ=dd.MM.yyyy");
			ИначеЕсли ЗначениеЗаполнено(ДанныеСотрудников.НомерСообщения) И ЗначениеЗаполнено(ДанныеСотрудников.ДатаСообщения) Тогда 
				Если ТипЗнч(ДанныеСотрудников.Регистратор) = Тип("ДокументСсылка.Увольнение") ИЛИ ТипЗнч(ДанныеСотрудников.Регистратор) = Тип("ДокументСсылка.УвольнениеСписком") Тогда
					СтрокаТаблицы.Параметры.РеквизитыСообщения = "Повідомлення про звільнення  № " + ДанныеСотрудников.НомерСообщения + " від " + Формат(ДанныеСотрудников.ДатаСообщения,"ДФ=dd.MM.yyyy")			
				Иначе  
					СтрокаТаблицы.Параметры.РеквизитыСообщения = "Повідомлення № " + ДанныеСотрудников.НомерСообщения + " від " + Формат(ДанныеСотрудников.ДатаСообщения,"ДФ=dd.MM.yyyy")			
				КонецЕсли	
			КонецЕсли;
			
			Отбор = Новый Структура;         
			Отбор.Вставить("ФизическоеЛицо", ДанныеСотрудников.ФизическоеЛицо); 
			
			ТекстСоставСемьи = ""; 
			ТекстОбразование = "";  
			ТекстДокументы = "";
			
			РодственникиФизическихЛиц.Сбросить();    
            Если ЗначениеЗаполнено(ДанныеСотрудников.СостояниеВБраке) Тогда
				ТекстСоставСемьи = Строка(ДанныеСотрудников.СостояниеВБраке)
			КонецЕсли;	
			Пока РодственникиФизическихЛиц.НайтиСледующий(Отбор, "ФизическоеЛицо") Цикл 
            	ТекстСоставСемьи = ТекстСоставСемьи + Символы.ПС + Символы.ПС +  РодственникиФизическихЛиц.ФИОРодственника + ?(ЗначениеЗаполнено(РодственникиФизическихЛиц.ДатаРожденияРодственника),", ","") 
				+ Формат(РодственникиФизическихЛиц.ДатаРожденияРодственника, "ДФ=dd.MM.yyyy"); 
			КонецЦикла;          
			СтрокаТаблицы.Параметры.ТекстСоставСемьи = ТекстСоставСемьи;     
			
			ОбразованияФизическихЛиц.Сбросить();
			Пока ОбразованияФизическихЛиц.НайтиСледующий(Отбор, "ФизическоеЛицо") Цикл                  
				ТекстДокумент = "";
				ТекстДокумент = Строка(ОбразованияФизическихЛиц.ВидДокумента) + " " + ОбразованияФизическихЛиц.Серия + " " + ОбразованияФизическихЛиц.Номер;
            	ТекстОбразование = ТекстОбразование + Символы.ПС + Символы.ПС + Строка(ОбразованияФизическихЛиц.ВидОбразования) +  
				?(ЗначениеЗаполнено(ОбразованияФизическихЛиц.Специальность), ", ", "") + ОбразованияФизическихЛиц.Специальность + ?(СокрЛП(ТекстДокумент) = "","", ", ") +
				ТекстДокумент;
			КонецЦикла;    
			СтрокаТаблицы.Параметры.ТекстОбразование = ТекстОбразование; 
			
			ДокументыФизическихЛиц.Сбросить(); 
			Отбор.Вставить("ДокументВид", Справочники.ВидыДокументовФизическихЛиц.Паспорт);    
			Если ДокументыФизическихЛиц.НайтиСледующий(Отбор, "ФизическоеЛицо, ДокументВид") Тогда 
				ТекстДокументы = Строка(ДокументыФизическихЛиц.ДокументВид) + " " + ДокументыФизическихЛиц.Серия + " " + ДокументыФизическихЛиц.Номер +
				?(СокрЛП(ДокументыФизическихЛиц.КемВыдан) = "", "", ", ") + ДокументыФизическихЛиц.КемВыдан + ?(СокрЛП(ДокументыФизическихЛиц.ДатаВыдачи) = "", "", ", ") +
				Формат(ДокументыФизическихЛиц.ДатаВыдачи,"ДФ=dd.MM.yyyy");
			КонецЕсли;	
			
			ДокументыФизическихЛиц.Сбросить();  
			Отбор.Удалить("ДокументВид");
			Отбор.Вставить("ДокументВид", Справочники.ВидыДокументовФизическихЛиц.Загранпаспорт);
			
			Если ДокументыФизическихЛиц.НайтиСледующий(Отбор, "ФизическоеЛицо, ДокументВид") Тогда 
            	ТекстДокументы = ТекстДокументы + Символы.ПС + Символы.ПС +  Строка(ДокументыФизическихЛиц.ДокументВид) + " " + ДокументыФизическихЛиц.Серия + " " + ДокументыФизическихЛиц.Номер +
				?(СокрЛП(ДокументыФизическихЛиц.КемВыдан) = "", "", ", ") + ДокументыФизическихЛиц.КемВыдан + ?(СокрЛП(ДокументыФизическихЛиц.ДатаВыдачи) = "", "", ", ") +
				Формат(ДокументыФизическихЛиц.ДатаВыдачи, "ДФ=dd.MM.yyyy");
			КонецЕсли;
			СтрокаТаблицы.Параметры.ТекстДокументы = ТекстДокументы; 
			
			ДокументРезультат.Вывести(СтрокаТаблицы);

		КонецЦикла;
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
