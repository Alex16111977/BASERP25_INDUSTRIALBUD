////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы управления ремонтами.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - ФиксированнаяСтруктура - свойства документа (См. ПроведениеДокументов.СвойстваДокумента).
//
// Возвращаемое значение:
//  Структура - параметры учетного механизма (См. ПроведениеДокументов.ПараметрыУчетногоМеханизма()).
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.НаработкиОбъектовЭксплуатации);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.ПериодыАктуальностиОбъектовЭксплуатации);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.ПланРемонтов);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.РемонтыРабочихЦентров);
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыСведений.НаработкиОбъектовЭксплуатации);
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам управления ремонтами.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "НаработкиОбъектовЭксплуатации");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ПериодыАктуальностиОбъектовЭксплуатации");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ПланРемонтов");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РемонтыРабочихЦентров");
	
КонецПроцедуры

#КонецОбласти

// Функция возвращает текстовое представление остаточного ресурса
//
// Параметры:
// 		КоличествоДней - Число - Количество дней.
//
// Возвращаемое значение:
// 		Строка - Строковое представление количества дней.
//
Функция ОстаточныйРесурсПоВремениСтрокой(КоличествоДней) Экспорт
	
	Исчисление = "";
	Количество = 0;
	
	Если Не ЗначениеЗаполнено(КоличествоДней) Или КоличествоДней <= 0 Тогда
		Возврат "";
	ИначеЕсли КоличествоДней > 370 Тогда
		Исчисление = НСтр("ru='год,года,лет';uk='рік,роки,років'");
		Количество = Окр(КоличествоДней/365, 1);
	ИначеЕсли КоличествоДней > 31 Тогда
		Исчисление = НСтр("ru='месяц,месяца,месяцев';uk='місяць,місяці,місяців'");
		Количество = Окр(КоличествоДней/31, 1);
	Иначе
		Исчисление = НСтр("ru='день,дня,дней';uk='день,дні,днів'");
		Количество = Окр(КоличествоДней, 1);
	КонецЕсли;
	
	СтруктураИсчисления = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Исчисление);
	
	ЧислоПервыхДвухЗнаков = Цел(Количество) - Цел(Количество/100)*100;
	ЧислоПервогоЗнака = Цел(Количество) - Цел(Количество/10)*10;
	
	Если (ЧислоПервыхДвухЗнаков>10 И ЧислоПервыхДвухЗнаков<15)
		Или ЧислоПервогоЗнака=0 Или ЧислоПервогоЗнака>4 Тогда
		
		Возврат Формат(Количество, "ЧДЦ=1") + " " + СтруктураИсчисления[2];
		
	ИначеЕсли ЧислоПервогоЗнака=1 Тогда
		
		Возврат Формат(Количество, "ЧДЦ=1") + " " + СтруктураИсчисления[0];
		
	КонецЕсли;
	
	Возврат Формат(Количество, "ЧДЦ=1") + " " + СтруктураИсчисления[1];
	
КонецФункции

#КонецОбласти
