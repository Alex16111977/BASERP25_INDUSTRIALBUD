Процедура А_ДоработкаСправочникиПередЗаписью(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник)=Тип("СправочникОбъект.ДоговорыКонтрагентов") Тогда
		
		ДоговорыКонтрагентовПередЗаписью(Источник, Отказ);
		
	ИначеЕсли ТипЗнч(Источник)=Тип("СправочникОбъект.Номенклатура") Тогда
		
		//НоменклатураПередЗаписью(Источник, Отказ);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДоговорыКонтрагентовПередЗаписью(Источник, Отказ) Экспорт
	
	Источник.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеРазделять;
	Источник.ХозяйственнаяОперация = Справочники.ДоговорыКонтрагентов.ХозяйственнаяОперация(Источник.ТипДоговора,Источник.ВариантОформленияЗакупок);
	
	Если Источник.А_НеОбновлятьГруппаФинансовогоУчета Тогда
		Возврат;
	КонецЕсли;
	
	ТипДоговора = Источник.ТипДоговора;
	ВалютаРегУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ЭтоНацВалюта = (Источник.ВалютаВзаиморасчетов = ВалютаРегУчета);
	
	ИмяГруппы = "";
	
	Если ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
		ИмяГруппы = ?(ЭтоНацВалюта, 
		"Расчеты с покупателями в национальной валюте",
		"Расчеты с покупателями в иностранной валюте");
		
	ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
		ИмяГруппы = ?(ЭтоНацВалюта,
		"Расчеты с поставщиками в национальной валюте",
		"Расчеты с поставщиками в иностранной валюте");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяГруппы) Тогда
		ГруппаФинансовогоУчета = Справочники.ГруппыФинансовогоУчетаРасчетов.НайтиПоНаименованию(ИмяГруппы);
		Если ЗначениеЗаполнено(ГруппаФинансовогоУчета) 
			И НЕ ЗначениеЗаполнено(Источник.ГруппаФинансовогоУчета)  Тогда
			Источник.ГруппаФинансовогоУчета = ГруппаФинансовогоУчета;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура А_ДоработкаДокументыПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ТипЗнч(Источник)=Тип("ДокументОбъект.ЗаказПоставщику") Тогда
		
		ЗаказПоставщику_ПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура А_ДоработкаСправочникиПриЗаписи(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник)=Тип("СправочникОбъект.ФизическиеЛица") Тогда
		
		ФизическиеЛицаПриЗаписи(Источник, Отказ); 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ФизическиеЛицаПриЗаписи(Источник, Отказ) Экспорт
	
	А_Привилегированный.СоздатьСотрудникаИзФизическогоЛица(Источник.Ссылка);
	
КонецПроцедуры

Процедура ЗаказПоставщику_ПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Источник.Организация  = А_Привилегированный.А_ПолучитьОрганизациюУпр();
	
	Если ЗначениеЗаполнено(Источник.Договор.Подразделение) Тогда
		
		Источник.Подразделение =  Источник.Договор.Подразделение;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник.Договор.НаправлениеДеятельности) Тогда
		
		Источник.НаправлениеДеятельности = Источник.Договор.НаправлениеДеятельности;
		
	КонецЕсли;
	Источник.ГруппаФинансовогоУчета =  Источник.Договор.ГруппаФинансовогоУчета;
	
	Если НЕ Источник.ЭтоНовый() Тогда
		
		Если Источник.Договор<>Источник.Ссылка.Договор 
			И НЕ РольДоступна("А_Гермес") Тогда
			
			ЕстьДокументы = А_Привилегированный.А_ПроверкаВыбораЗаказаВДокументахПрихода(Источник.Ссылка);
			Если  ЕстьДокументы Тогда
				ТекстОшибки = "По заказу поставщику существуют проведенные документы менять договор запрещено!";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Источник,
				"Договор",
				,
				Отказ);
			КонецЕсли; 
			
			
		КонецЕсли; 
		
		
		Если Источник.Валюта<>Источник.Ссылка.Валюта 
			И НЕ РольДоступна("А_Гермес")  Тогда
			
			ЕстьДокументы = А_Привилегированный.А_ПроверкаВыбораЗаказаВДокументахПрихода(Источник.Ссылка);
			Если  ЕстьДокументы
				
				Тогда
				ТекстОшибки = "По заказу поставщику существуют проведенные документы менять валюту запрещено!";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Источник,
				"Валюта",
				,
				Отказ);
			КонецЕсли; 
			
			
		КонецЕсли; 
	Иначе         
		Источник.Статус = Перечисления.СтатусыЗаказовПоставщикам.Согласован; 
		Источник.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Середній");
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Источник.Статус)  Тогда
		Источник.Статус = Перечисления.СтатусыЗаказовПоставщикам.Согласован; 
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Источник.Приоритет)  Тогда
		Источник.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Середній");
	КонецЕсли;
	
	
КонецПроцедуры


