////////////////////////////////////////////////////////////////////////////////
//  Процедуры и функции, используемые при отражения документов
//  в международном учете.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - см. ПроведениеДокументов.СвойстваДокумента - 
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет")
			Или Документ.ДополнительныеСвойства.Свойство("НеРегистрироватьКОтражениюВМеждународномУчете")
				И Документ.ДополнительныеСвойства.НеРегистрироватьКОтражениюВМеждународномУчете Тогда
			Возврат Параметры;
		КонецЕсли;
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.ОтражениеДокументовВМеждународномУчете);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыБухгалтерии.Международный);
		
		Параметры.Производный = Истина;
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам международного учета.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Или Не ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицыДляДвижений.Свойство("ТаблицаМеждународный") Тогда // без отложенных движений
		
		Если ЗначениеЗаполнено(ТаблицыДляДвижений.ТаблицаМеждународный) Тогда
			
			Движения.Международный.Записывать = Истина;
			Движения.Международный.Загрузить(ТаблицыДляДвижений.ТаблицаМеждународный);
			
		КонецЕсли;
		
	Иначе // пробросим таблицы данных для последующего отражения после записи движений документа
		
		Если ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного") Тогда
			Если ТаблицыДляДвижений.Свойство("ТаблицаОтражениеДокументовВРеглУчете") Тогда
				Движения.ОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства.Вставить(
					"ТаблицаОтражениеДокументовВРеглУчете",
					ТаблицыДляДвижений.ТаблицаОтражениеДокументовВРеглУчете);
			КонецЕсли;
			Если ТаблицыДляДвижений.Свойство("ТаблицаВыборочнойРегистрацииКОтражению") Тогда
				Движения.ОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства.Вставить(
					"ТаблицаВыборочнойРегистрацииКОтражению",
					ТаблицыДляДвижений.ТаблицаВыборочнойРегистрацииКОтражению);
			КонецЕсли;
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного")
			И ТаблицыДляДвижений.Свойство("ТаблицаОтражениеДокументовВМеждународномУчете") Тогда
				Движения.ОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства.Вставить(
					"ТаблицаОтражениеДокументовВМеждународномУчете",
					ТаблицыДляДвижений.ТаблицаОтражениеДокументовВМеждународномУчете);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возникает перед выполнением записи регистров документа.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//      используемый для хранения таблиц контроля изменений регистров
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПередЗаписьюДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

// Возникает после выполнения записи регистров документа.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//      используемый для хранения таблиц контроля изменений регистров
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПослеЗаписиДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	ЗарегистрироватьКОтражению(Документ, Документ.ДополнительныеСвойства, Документ.Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияДокументовКОтражениюВМеждународномУчете

// Возвращает документы к отражению в международном учете для заданных периодов отражения
//
// Параметры:
// 	ДокументыКОтражению -   ТаблицаЗначений, 
// 							МенеджерВременныхТаблиц - Таблица документов, которые надо вернуть к отражению, 
// 													  или менеджер временных таблиц имеющий таблицу ДокументыКОтражению
// 													  Таблица должна иметь колонки Документ, Организация, ДатаОтражения. 
//
Процедура ВернутьДокументыКОтражению(ДокументыКОтражению) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	КлючеваяОперация = "МеждународныйУчет.ВозвратДокументовКОтражению";
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	КоличествоДокументов = 0;
	
	МенеджерВременныхТаблиц = ИнициализироватьМенеджерДляВозвратаКОтражению(ДокументыКОтражению);
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		Отбор = Новый Структура("Период, Регистратор",'00010101',Неопределено);
		Пока Истина Цикл
			
			ДанныеКОтражению = ДанныеДляВозвратаКОтражениюВУчете(МенеджерВременныхТаблиц, Отбор);
			
			ВыборкаПоДокументам = ДанныеКОтражению.Регистраторы;
			Если ВыборкаПоДокументам.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			ДанныеРегистра = ДанныеКОтражению.ДанныеРегистра;
			Отбор = Новый Структура("Период, Регистратор",'00010101',Неопределено);
			Пока ВыборкаПоДокументам.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей - 
				НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
				ЗаполнитьЗначенияСвойств(Отбор, ВыборкаПоДокументам);
				НайденныеСтроки = ДанныеРегистра.Скопировать(Отбор);
				Если НайденныеСтроки <> Неопределено Тогда
					Для Каждого НовыеДанные Из НайденныеСтроки Цикл
						НоваяЗапись = НаборЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, НовыеДанные);
					КонецЦикла;
				КонецЕсли;
				НаборЗаписей.Записать();
			КонецЦикла;
			КоличествоДокументов = КоличествоДокументов + ВыборкаПоДокументам.Количество();
		КонецЦикла;
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоДокументов);
	
КонецПроцедуры

// Формирует движения по регистру ОтражениеДокументовВМеждународномУчете, выполняет очистку неактуальных записей в Международный.
// Для документов отражаемых в международном учете по данным оборотных регистров и регл. учета.
//
// Параметры:
//  Объект - ДокументОбъект - документ регистрируемый к отражению.
//  ДополнительныеСвойства - Структура - дополнительные свойства документа инициализированные при проведении документа.
//  Движения - КоллекцияДвижений - наборы записей регистров записываемые документом.
//                                 На момент вызова метода все наборы должны быть записаны.
//  Отказ - Булево - признак отказа в проведении.
//
Процедура ЗарегистрироватьКОтражению(Объект, ДополнительныеСвойства, Движения, Отказ = Ложь) Экспорт
	
	Если Отказ ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеРегистрироватьКОтражениюВМеждународномУчете") 
		 И ДополнительныеСвойства.НеРегистрироватьКОтражениюВМеждународномУчете Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		
		Если НЕ Объект.Свойство("Дата") Тогда
			ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Дата");
			Объект.Вставить("Дата", ДатаДокумента);
		КонецЕсли;
		
		Если НЕ Движения.Свойство("Международный") Тогда
			Международный = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
			Международный.Отбор.Регистратор.Установить(Объект.Ссылка);
			Движения.Вставить("Международный", Международный);
		КонецЕсли;
		
		Если НЕ Движения.Свойство("ОтражениеДокументовВМеждународномУчете") Тогда
			ОтражениеДокументовВМеждународномУчете = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
			ОтражениеДокументовВМеждународномУчете.Отбор.Регистратор.Установить(Объект.Ссылка);
			Движения.Вставить("ОтражениеДокументовВМеждународномУчете", ОтражениеДокументовВМеждународномУчете);
		КонецЕсли;
		
	КонецЕсли;
	
	ДвиженияМеждународный = Движения.Международный;
	ДвиженияМеждународный.ДополнительныеСвойства.Вставить("НеВыполнятьДопОбработкуПроводок", Истина);
	
	ДвиженияОтражениеДокументовВМеждународномУчете = Движения.ОтражениеДокументовВМеждународномУчете;
	
	ТаблицаРегистрации = Новый ТаблицаЗначений;
	ТаблицаРегистрации.Колонки.Добавить("Период",        Новый ОписаниеТипов("Дата"));
	ТаблицаРегистрации.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаРегистрации.Колонки.Добавить("ДатаОтражения", Новый ОписаниеТипов("Дата"));
	ТаблицаРегистрации.Колонки.Добавить("ХозяйственнаяОперация", Новый ОписаниеТипов("ПеречислениеСсылка.ХозяйственныеОперации"));
	
	ВыборочнаяРегистрация = Ложь;
	ТаблицаВыборочнойРегистрации = Новый ТаблицаЗначений;
	ТаблицаВыборочнойРегистрации.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаВыборочнойРегистрации.Колонки.Добавить("ДатаОтражения", Новый ОписаниеТипов("Дата"));
	
	МеждународныйУчетПоДаннымОперативногоУчета.ДополнитьТаблицыРегистрацииКОтражению(
		Объект, ДвиженияОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства, ТаблицаРегистрации);
	МеждународныйУчетПоДаннымРеглУчета.ДополнитьТаблицыРегистрацииКОтражению(
		Объект, ДвиженияОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства, ТаблицаРегистрации, ВыборочнаяРегистрация, ТаблицаВыборочнойРегистрации);
	МеждународныйУчетПоДаннымДокументов.ДополнитьТаблицыРегистрацииКОтражению(
		Объект, ДвиженияОтражениеДокументовВМеждународномУчете.ДополнительныеСвойства, ТаблицаРегистрации);
		
	ТаблицаРегистрации.ЗаполнитьЗначения(Объект.Дата, "Период");
	ТаблицаРегистрации.Свернуть("Период, Организация, ДатаОтражения, ХозяйственнаяОперация");
	ДобавитьУчетнуюПолитикуИСтатус(ТаблицаРегистрации, Объект.Ссылка);
	
	Если НЕ ВыборочнаяРегистрация Тогда
		Если ТаблицаРегистрации.Количество() > 0 Тогда
			ТаблицаРегистрации.Сортировать("Период, ДатаОтражения");
			ДвиженияОтражениеДокументовВМеждународномУчете.Загрузить(ТаблицаРегистрации);
			ДвиженияОтражениеДокументовВМеждународномУчете.Записывать = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	#Область ТекстЗапросаНовыхСтатусовРегистрации
	ТекстЗапросаНовыхСтатусов =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.Период          КАК Период,
	|	ТаблицаДанных.Организация     КАК Организация,
	|	ТаблицаДанных.ДатаОтражения   КАК ДатаОтражения,
	|	ТаблицаДанных.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НоваяРегистрация
	|ИЗ
	|	&ТаблицаДанные КАК ТаблицаДанных
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВыборочнойРегистрации.Организация    КАК Организация,
	|	ТаблицаВыборочнойРегистрации.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ВыборочнаяРегистрация
	|ИЗ
	|	&ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументов.Период          КАК Период,
	|	ОтражениеДокументов.Организация     КАК Организация,
	|	ВЫБОР КОГДА ОтражениеДокументов.ДатаОтражения = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА НАЧАЛОПЕРИОДА(ОтражениеДокументов.Период, ДЕНЬ)
	|		ИНАЧЕ ОтражениеДокументов.ДатаОтражения
	|	КОНЕЦ КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус          КАК Статус,
	|	ОтражениеДокументов.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ОтражениеДокументов.Комментарий     КАК Комментарий
	|ПОМЕСТИТЬ ТекущаяРегистрация
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Регистратор = &Ссылка
	|	И ОтражениеДокументов.УчетнаяПолитика <> ЗНАЧЕНИЕ(Справочник.УчетныеПолитики.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоваяРегистрация.Период КАК Период,
	|	НоваяРегистрация.Организация КАК Организация,
	|	НоваяРегистрация.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ВыборочнаяРегистрация.ДатаОтражения ЕСТЬ NULL И НЕ ТекущаяРегистрация.Статус ЕСТЬ NULL
	|			ТОГДА ТекущаяРегистрация.Статус
	|		КОГДА ЕСТЬNULL(ТекущаяРегистрация.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА &КОтражениюВУчетеВручную
	|		ИНАЧЕ
	|			&КОтражениюВУчете
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ВыборочнаяРегистрация.ДатаОтражения ЕСТЬ NULL
	|			ТОГДА ТекущаяРегистрация.Комментарий
	|		КОГДА ЕСТЬNULL(ТекущаяРегистрация.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА ТекущаяРегистрация.Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Комментарий,
	|	НоваяРегистрация.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	НоваяРегистрация КАК НоваяРегистрация
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ТекущаяРегистрация КАК ТекущаяРегистрация
	|	ПО 
	|		НоваяРегистрация.Организация = ТекущаяРегистрация.Организация
	|		И НоваяРегистрация.ДатаОтражения = ТекущаяРегистрация.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВыборочнаяРегистрация КАК ВыборочнаяРегистрация
	|	ПО 
	|		НоваяРегистрация.Организация = ВыборочнаяРегистрация.Организация	
	|		И НоваяРегистрация.ДатаОтражения = ВыборочнаяРегистрация.ДатаОтражения
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ИзмененияСтатусов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДатаОтражения КАК ДатаОтражения,
	|		Таблица.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА 1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА 2
	|		КОНЕЦ КАК Статус
	|	ИЗ
	|		НовыеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДатаОтражения,
	|		Таблица.Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА -1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА -2
	|		КОНЕЦ
	|	ИЗ
	|		ТекущаяРегистрация КАК Таблица
	|
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ДатаОтражения,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Статус) <> 0   
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеСтатусы.Период          КАК Период,
	|	НовыеСтатусы.Организация     КАК Организация,
	|	НовыеСтатусы.ДатаОтражения   КАК ДатаОтражения,
	|	НовыеСтатусы.Статус          КАК Статус,
	|	НовыеСтатусы.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НовыеСтатусы.Комментарий     КАК Комментарий
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Организация,
	|	ДатаОтражения
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Международный.Период                                       КАК Период,
	|	Международный.Организация                                  КАК Организация,
	|	
	|	Международный.ПодразделениеДт                              КАК ПодразделениеДт,
	|	Международный.СчетДт                                       КАК СчетДт,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(Международный.СубконтоДт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(Международный.СубконтоДт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(Международный.СубконтоДт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт3,
	|	Международный.ВалютаДт                                     КАК ВалютаДт,
	|	Международный.ВалютнаяСуммаДт                              КАК ВалютнаяСуммаДт,
	|	Международный.КоличествоДт                                 КАК КоличествоДт,
	|	
	|	Международный.ПодразделениеКт                              КАК ПодразделениеКт,
	|	Международный.СчетКт                                       КАК СчетКт,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(Международный.СубконтоКт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(Международный.СубконтоКт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(Международный.СубконтоКт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт3,
	|	Международный.ВалютаКт                                     КАК ВалютаКт,
	|	Международный.ВалютнаяСуммаКт                              КАК ВалютнаяСуммаКт,
	|	Международный.КоличествоКт                                 КАК КоличествоКт,
	|	
	|	Международный.Сумма                                        КАК Сумма,
	|	Международный.СуммаПредставления                           КАК СуммаПредставления,
	|	
	|	Международный.Содержание                                   КАК Содержание,
	|	Международный.ШаблонПроводки                               КАК ШаблонПроводки,
	|	Международный.ТипПроводки                                  КАК ТипПроводки
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(,,Регистратор = &Ссылка,,) КАК Международный
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ИзмененияСтатусов КАК ИзмененияСтатусов
	|	ПО
	|		Международный.Организация = ИзмененияСтатусов.Организация
	|		И НАЧАЛОПЕРИОДА(Международный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения
	|ГДЕ
	|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапросаНовыхСтатусов);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаДанные",     ТаблицаРегистрации);
	Запрос.УстановитьПараметр("Ссылка",            Объект.Ссылка);
	Запрос.УстановитьПараметр("ПустойВидСубконто", ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ОтраженоВУчете",      Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
	Запрос.УстановитьПараметр("КОтражениюВУчете",    Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную);
	
	Запрос.УстановитьПараметр("ТаблицаВыборочнойРегистрации", ТаблицаВыборочнойРегистрации);
	
	Результат = Запрос.ВыполнитьПакет();
	Количество = Результат.Количество();
	
	Международный = Результат[Количество - 1].Выгрузить();
	ДвиженияМеждународный.Загрузить(Международный);
	ДвиженияМеждународный.Записывать = Истина;
	
	ТаблицаРегистрация = Результат[Количество - 2].Выгрузить();
	ДвиженияОтражениеДокументовВМеждународномУчете.Загрузить(ТаблицаРегистрация);
	ДвиженияОтражениеДокументовВМеждународномУчете.Записывать = Истина;
	
	ОчиститьДвижения = ТаблицаРегистрация.Количество() = 0;// документ распровели
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		Если ДвиженияМеждународный.Количество() > 0 ИЛИ ОчиститьДвижения Тогда
			ДвиженияМеждународный.Записать();
		КонецЕсли;
		Если ДвиженияОтражениеДокументовВМеждународномУчете.Количество() > 0 ИЛИ ОчиститьДвижения Тогда
			ДвиженияОтражениеДокументовВМеждународномУчете.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Регистрирует документы расчетов с партнерами к отражению в международном учете.
//
// Параметры:
//	МассивДокументов - Массив - массив документов к регистрации в международном учете.
//
Процедура ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВМеждународномУчете(ТаблицаРасчетов) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Или ТаблицаРасчетов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасчетов.Период КАК Период,
	|	ТаблицаРасчетов.Регистратор КАК Регистратор,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	&ТаблицаРасчетов КАК ТаблицаРасчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Период КАК Период,
	|	ТаблицаДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДокументов.Регистратор КАК Документ,
	|	ТаблицаДокументов.Организация КАК Организация
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаДокументов.Период КАК Период,
	|		ТаблицаДокументов.Период КАК ДатаОтражения,
	|		ТаблицаДокументов.Регистратор КАК Регистратор,
	|		Аналитика.Организация КАК Организация
	|	ИЗ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ТаблицаДокументов.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(ТаблицаДокументов.Регистратор) <> ТИП(Документ.РегистраторРасчетов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РегистраторРасчетов.Дата КАК Период,
	|		ТаблицаДокументов.Период КАК ДатаОтражения,
	|		ТаблицаДокументов.Регистратор КАК Регистратор,
	|		Аналитика.Организация КАК Организация
	|	ИЗ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегистраторРасчетов КАК РегистраторРасчетов
	|			ПО ТаблицаДокументов.Регистратор = РегистраторРасчетов.Ссылка
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ТаблицаДокументов.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаДокументов.Период,
	|		ТаблицаДокументов.Период,
	|		ТаблицаДокументов.Регистратор,
	|		Аналитика.Контрагент
	|	ИЗ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ТаблицаДокументов.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	ГДЕ
	|		Аналитика.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаДокументов.Регистратор) В (
	|			ТИП(Документ.ВозвратТоваровМеждуОрганизациями),
	|			ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями),
	|			ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании),
	|			ТИП(Документ.ПередачаТоваровМеждуОрганизациями))
	|
	|	) КАК ТаблицаДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Период,
	|	ТаблицаДокументов.ДатаОтражения,
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Документ,
	|	Организация,
	|	ДатаОтражения";
	
	Запрос.УстановитьПараметр("ТаблицаРасчетов", ТаблицаРасчетов);
	Запрос.ВыполнитьПакет();
	
	ВернутьДокументыКОтражению(Запрос.МенеджерВременныхТаблиц);

КонецПроцедуры

// Записывает движения по регистру ОтражениеДокументаВМеждународномУчете, выполняет очистку неактуальных записей в Международный.
// Вызывается из оффлайновых общих модулей НДС.
//
// Параметры:
// 	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, имеющий таблицы
// 		ТаблицаТекущейРегистрации и ТаблицаВыборочнойРегистрации.
// 		Таблицы должны иметь колонки Документ, Организация, ДатаОтражения.
// 	КоличествоОбработанных - Число - Параметр, в котором необходимо вернуть количество возвращенных к отражению документов.
//
Процедура ВернутьДокументыКОтражениюВыборочноПакетно(МенеджерВременныхТаблиц, КоличествоОбработанных = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
	 	Возврат;
	КонецЕсли;

	ПолучитьУчетнуюПолитикуИСтатус(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	#Область ТекстЗапроса
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТекущейРегистрации.Документ КАК Документ
	|ПОМЕСТИТЬ ТолькоДокументыКОтражению
	|ИЗ
	|	ТаблицаТекущейРегистрации КАК ТаблицаТекущейРегистрации
	|;
	| 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТекущейРегистрации.Период КАК Период,
	|	ТаблицаТекущейРегистрации.Документ КАК Документ,
	|	ТаблицаТекущейРегистрации.Организация КАК Организация,
	|	ТаблицаТекущейРегистрации.ДатаОтражения КАК ДатаОтражения,
	|	ДопСвойства.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ДопСвойства.Статус КАК Статус
	|ПОМЕСТИТЬ ТаблицаТекущейРегистрацииСвернуто
	|ИЗ
	|	ТаблицаТекущейРегистрации КАК ТаблицаТекущейРегистрации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ УчетнаяПолитикаИСтатус КАК ДопСвойства
	|	ПО ТаблицаТекущейРегистрации.Организация = ДопСвойства.Организация
	|		И ТаблицаТекущейРегистрации.ДатаОтражения = ДопСвойства.ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВыборочнойРегистрации.Документ КАК Документ,
	|	ТаблицаВыборочнойРегистрации.Организация КАК Организация,
	|	ТаблицаВыборочнойРегистрации.ДатаОтражения КАК ДатаОтражения,
	|	ДопСвойства.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ДопСвойства.Статус КАК Статус
	|ПОМЕСТИТЬ ТаблицаВыборочнойРегистрацииСвернуто
	|ИЗ
	|	ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ УчетнаяПолитикаИСтатус КАК ДопСвойства
	|	ПО ТаблицаВыборочнойРегистрации.Организация = ДопСвойства.Организация
	|		И ТаблицаВыборочнойРегистрации.ДатаОтражения = ДопСвойства.ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументов.Период КАК Период,
	|	ОтражениеДокументов.Регистратор КАК Документ,
	|	ОтражениеДокументов.Организация КАК Организация,
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус КАК Статус,
	|	ОтражениеДокументов.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ОтражениеДокументов.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ТекущиеСтатусы
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТолькоДокументыКОтражению КАК ТолькоДокументыКОтражению
	|		ПО ОтражениеДокументов.Регистратор = ТолькоДокументыКОтражению.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВыборочнойРегистрации.Документ КАК Документ,
	|	МИНИМУМ(ТаблицаВыборочнойРегистрации.ДатаОтражения) КАК ДатаОтражения
	|ПОМЕСТИТЬ НачалоИзмененийВыборочнойРегистрации
	|ИЗ
	|	ТаблицаВыборочнойРегистрацииСвернуто КАК ТаблицаВыборочнойРегистрации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВыборочнойРегистрации.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТекущиеСтатусы.Период, ТаблицаТекущейРегистрации.Период) КАК Период,
	|	ТаблицаТекущейРегистрации.Документ КАК Документ,
	|	ТаблицаТекущейРегистрации.Организация КАК Организация,
	|	ТаблицаТекущейРегистрации.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|				И НЕ ТекущиеСтатусы.Статус ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Статус
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА &КОтражениюВУчетеВручную
	|		ИНАЧЕ &КОтражениюВУчете
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Комментарий,
	|	ТаблицаТекущейРегистрации.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	ТаблицаТекущейРегистрацииСвернуто КАК ТаблицаТекущейРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеСтатусы КАК ТекущиеСтатусы
	|		ПО ТаблицаТекущейРегистрации.Документ = ТекущиеСтатусы.Документ
	|			И ТаблицаТекущейРегистрации.Организация = ТекущиеСтатусы.Организация
	|			И ТаблицаТекущейРегистрации.ДатаОтражения = ТекущиеСтатусы.ДатаОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВыборочнойРегистрацииСвернуто КАК ТаблицаВыборочнойРегистрации
	|		ПО ТаблицаТекущейРегистрации.Документ = ТаблицаВыборочнойРегистрации.Документ
	|			И ТаблицаТекущейРегистрации.Организация = ТаблицаВыборочнойРегистрации.Организация
	|			И ТаблицаТекущейРегистрации.ДатаОтражения = ТаблицаВыборочнойРегистрации.ДатаОтражения
	|ГДЕ
	|	ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Документ КАК Документ,
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ИзмененияСтатусов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Документ КАК Документ,
	|		Таблица.ДатаОтражения КАК ДатаОтражения,
	|		Таблица.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА 1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА 2
	|		КОНЕЦ КАК Статус
	|	ИЗ
	|		НовыеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Документ,
	|		Таблица.ДатаОтражения,
	|		Таблица.Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА -1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА -2
	|		КОНЕЦ
	|	ИЗ
	|		ТекущиеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)) КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачалоИзмененийВыборочнойРегистрации КАК НачалоИзменений
	|		ПО Таблица.Документ = НачалоИзменений.Документ
	|ГДЕ
	|	Таблица.ДатаОтражения >= ЕСТЬNULL(НАЧАЛОПЕРИОДА(НачалоИзменений.ДатаОтражения, МЕСЯЦ), ДАТАВРЕМЯ(1, 1, 1))
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Документ,
	|	Таблица.ДатаОтражения,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Статус) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Таблица.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеСтатусы.Период КАК Период,
	|	НовыеСтатусы.Документ КАК Документ,
	|	НовыеСтатусы.Организация КАК Организация,
	|	НовыеСтатусы.ДатаОтражения КАК ДатаОтражения,
	|	НовыеСтатусы.Статус КАК Статус,
	|	НовыеСтатусы.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НовыеСтатусы.Комментарий КАК Комментарий
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
    |
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Документ,
	|	Организация,
	|	ДатаОтражения
	|
	|ИТОГИ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Международный.Период КАК Период,
	|	Международный.Регистратор КАК Регистратор,
	|	Международный.Организация КАК Организация,
	|	Международный.ПодразделениеДт КАК ПодразделениеДт,
	|	Международный.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Международный.СчетДт КАК СчетДт,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(Международный.СубконтоДт1, НЕОПРЕДЕЛЕНО) КАК СубконтоДт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(Международный.СубконтоДт2, НЕОПРЕДЕЛЕНО) КАК СубконтоДт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(Международный.СубконтоДт3, НЕОПРЕДЕЛЕНО) КАК СубконтоДт3,
	|	Международный.ВалютаДт КАК ВалютаДт,
	|	Международный.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	Международный.ПодразделениеКт КАК ПодразделениеКт,
	|	Международный.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	Международный.СчетКт КАК СчетКт,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(Международный.СубконтоКт1, НЕОПРЕДЕЛЕНО) КАК СубконтоКт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(Международный.СубконтоКт2, НЕОПРЕДЕЛЕНО) КАК СубконтоКт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(Международный.СубконтоКт3, НЕОПРЕДЕЛЕНО) КАК СубконтоКт3,
	|	Международный.ВалютаКт КАК ВалютаКт,
	|	Международный.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	Международный.Сумма КАК Сумма,
	|	Международный.СуммаПредставления КАК СуммаПредставления,
	|	Международный.Содержание КАК Содержание
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор В (ВЫБРАТЬ Т.Документ ИЗ ТолькоДокументыКОтражению КАК Т),
	|			,
	|		) КАК Международный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияСтатусов КАК ИзмененияСтатусов
	|		ПО Международный.Регистратор = ИзмененияСтатусов.Документ
	|			И Международный.Организация = ИзмененияСтатусов.Организация
	|			И (НАЧАЛОПЕРИОДА(Международный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения)
	|ГДЕ
	|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
	#КонецОбласти

	Запрос.УстановитьПараметр("ПустойВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка());

	Запрос.УстановитьПараметр("ОтраженоВУчете",      Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
	Запрос.УстановитьПараметр("КОтражениюВУчете",    Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную);

	Результат = Запрос.ВыполнитьПакет();
	Количество = Результат.Количество();

	ИзмененияСтатусов = Запрос.МенеджерВременныхТаблиц.Таблицы.Получить("ИзмененияСтатусов").ПолучитьДанные().Выгрузить();
	
	РезультатМеждународный = Результат[Количество - 1]; // РезультатЗапроса - 
	Международный = РезультатМеждународный.Выгрузить();
	Международный.Индексы.Добавить("Регистратор");
	
	РезультатПоДокументам = Результат[Количество - 2]; // РезультатЗапроса -
	ВыборкаПоДокументам = РезультатПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТипыДокументовКОтражению = Метаданные.РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СтандартныеРеквизиты.Регистратор.Тип;
	Пока ВыборкаПоДокументам.Следующий() Цикл
		ДвиженияМеждународный = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
		ДвиженияМеждународный.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Документ);
		ДвиженияМеждународный.Записывать = Истина;

		МеждународныйПоДокументу = Международный.СкопироватьКолонки();
		НайденныеСтроки = Международный.НайтиСтроки(Новый Структура("Регистратор", ВыборкаПоДокументам.Документ));
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = МеждународныйПоДокументу.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
		КонецЦикла;

		ДвиженияМеждународный.Загрузить(МеждународныйПоДокументу);
		ДвиженияМеждународный.Записать();

		ЕстьИзменения = Не ИзмененияСтатусов.Найти(ВыборкаПоДокументам.Документ, "Документ") = Неопределено;
		Если ТипыДокументовКОтражению.СодержитТип(ТипЗнч(ВыборкаПоДокументам.Документ)) И ЕстьИзменения Тогда
			НаборЗаписей = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Документ);

			Выборка = ВыборкаПоДокументам.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			КонецЦикла;

			НаборЗаписей.Записать();
			Если ТипЗнч(КоличествоОбработанных) = Тип("Число") Тогда
				КоличествоОбработанных = КоличествоОбработанных + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	СписокТаблицДляУничтожения = 
		"ТаблицаТекущейРегистрацииСвернуто,
		|ТаблицаВыборочнойРегистрацииСвернуто,
		|ТолькоДокументыКОтражению,
		|НачалоИзмененийВыборочнойРегистрации,
		|ТекущиеСтатусы,
		|НовыеСтатусы,
		|ИзмененияСтатусов";
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, СписокТаблицДляУничтожения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭтаповЗакрытияМесяца

#Область ОтражениеДокументовВМеждународномУчете

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработка.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов - 
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ОтражениеДокументовВМеждународномУчете(ТаблицаЭтапов,ТекущийРодитель) Экспорт

	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ОтражениеДокументовВМеждународномУчете,
		Ложь, Ложь, Истина);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Отразить';uk='Відобразити'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Использование_ОтражениеДокументовВМеждународномУчете");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"МеждународныйУчетПроведениеСервер.Выполнить_ОтражениеДокументовВМеждународномУчете");
	НоваяСтрока.ДействиеПодробнее  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Обработки.ОтражениеДокументовВМеждународномУчете.Формы.Форма.ПолноеИмя());
	
КонецПроцедуры

Процедура Использование_ОтражениеДокументовВМеждународномУчете(ПараметрыОбработчика) Экспорт
	
	ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика);
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного")
	 И НЕ ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного") Тогда
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru='Формирование проводок по данным оперативного и регламентированного учета не используется.';uk='Формування проводок за даними оперативного і регламентованого обліку не використовується.'",ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийМУСрезПоследних.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(
	|			&КонецПериода,
	|			Организация В (&МассивОрганизаций)) КАК УчетнаяПолитикаОрганизацийМУСрезПоследних";
	
	Запрос.Выполнить();
	
	Если ЗакрытиеМесяцаСервер.РазмерВременнойТаблицы(Запрос, "ВТОрганизации", ПараметрыОбработчика) = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru='Нет организаций с учетной политикой международного учета.';uk='Немає організацій з обліковою політикою міжнародного обліку.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Регистратор КАК Регистратор,
	|	ОтражениеДокументов.Статус КАК Статус
	|ПОМЕСТИТЬ ОтражениеДокументов
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизации КАК Организации
	|		ПО ОтражениеДокументов.Организация = Организации.Ссылка
	|ГДЕ
	|	ОтражениеДокументов.ДатаОтражения <= &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус В
	|		(ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете),
	|		 ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВРучную))
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	КоличествоДанных = ЗакрытиеМесяцаСервер.РазмерВременнойТаблицы(Запрос, "ОтражениеДокументов", ПараметрыОбработчика);
	
	ЕстьОжидающиеОтражения = НЕ МассивРезультатов[1].Пустой();
	ЕстьНеОтраженныеВУчете = НЕ МассивРезультатов[2].Пустой();
	
	Если ЕстьОжидающиеОтражения Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ожидает отражения в учете документов: %1';uk='Очікує відображення в обліку документів: %1'",ОбщегоНазначения.КодОсновногоЯзыка()),
				СокрЛП(МассивРезультатов[1].Выгрузить()[0].Количество)));
		
	КонецЕсли;
	
	Если ЕстьНеОтраженныеВУчете Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеВыполненСОшибками(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не отражено в учете документов из-за отсутствия правил отражения: %1';uk='Не відображено в обліку документів через відсутність правил відображення: %1'",ОбщегоНазначения.КодОсновногоЯзыка()),
				СокрЛП(МассивРезультатов[2].Выгрузить()[0].Количество)));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_ОтражениеДокументовВМеждународномУчете(ПараметрыОбработчика) Экспорт
	
	ОтразитьВМеждународномУчете(
		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
		КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
КонецПроцедуры

#КонецОбласти

#Область РасчетКурсовыхРазницВФункциональнойВалюте

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов - Таблица этапов для заполнения.
// 	ТекущийРодитель - Строка - идентификатор группы.
//
Процедура ДобавитьЭтап_РасчетКурсовыхРазницВФункциональнойВалюте(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетКурсовыхРазницВФункциональнойВалюте,
		Ложь, Ложь, Истина);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Рассчитать';uk='Розрахувати'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Использование_РасчетКурсовыхРазницВФункциональнойВалюте");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"МеждународныйУчетПроведениеСервер.Выполнить_РасчетКурсовыхРазницВФункциональнойВалюте");
	НоваяСтрока.ДействиеПодробнее = МеждународныйУчетПроведениеСервер.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперацияМУ();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РасчетКурсовыхРазницФункциональнаяВалюта);
КонецПроцедуры

Процедура Использование_РасчетКурсовыхРазницВФункциональнойВалюте(ПараметрыОбработчика) Экспорт
	
	ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ВсегоНеЗакрыто = Документы.РегламентнаяОперацияМеждународныйУчет.НезакрытыеКурсовыеРазницы(
		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
		КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации),
		Истина);
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, ВсегоНеЗакрыто);
	
	Если ВсегоНеЗакрыто = 0 Тогда
		
		ВведеныРегламентныеДокументы = ПроверитьНаличиеДокументаРегламентнаяОперацияМУ(ПараметрыОбработчика);
		
	Иначе
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			НСтр("ru='Есть незакрытые курсовые разницы для расчета.';uk='Є незакриті курсові різниці для розрахунку.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_РасчетКурсовыхРазницВФункциональнойВалюте(ПараметрыОбработчика) Экспорт
	
	СформироватьДокументРегламентнаяОперацияМУ(ПараметрыОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область ЗакрытиеСчетовУчетаДоходовИРасходовМУ

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработка.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов -
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ЗакрытиеСчетовУчетаДоходовИРасходовМУ(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ЗакрытиеСчетовУчетаДоходовИРасходовМУ,
		Ложь, Истина);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Выполнить';uk='Виконати'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Использование_ЗакрытиеСчетовУчетаДоходовИРасходовМУ");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"МеждународныйУчетПроведениеСервер.Выполнить_ЗакрытиеСчетовУчетаДоходовИРасходовМУ");
	НоваяСтрока.ДействиеПодробнее = МеждународныйУчетПроведениеСервер.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперацияМУ();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов);
КонецПроцедуры

Процедура Использование_ЗакрытиеСчетовУчетаДоходовИРасходовМУ(ПараметрыОбработчика) Экспорт
	
	ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ВсегоНеЗакрыто = Документы.РегламентнаяОперацияМеждународныйУчет.ЕстьНезакрытыеСчетаУчетаДоходовРасходов(
		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
		КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, ВсегоНеЗакрыто);
	
	Если ВсегоНеЗакрыто = 0 Тогда
		
		ВведеныРегламентныеДокументы = ПроверитьНаличиеДокументаРегламентнаяОперацияМУ(ПараметрыОбработчика,,, Истина);
		
	Иначе
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			НСтр("ru='Есть незакрытые счета учета.';uk='Є незакриті рахунки обліку.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_ЗакрытиеСчетовУчетаДоходовИРасходовМУ(ПараметрыОбработчика) Экспорт
	
	СформироватьДокументРегламентнаяОперацияМУ(ПараметрыОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область РасчетКурсовыхРазницВВалютеПредставления

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработка.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов - 
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетКурсовыхРазницВВалютеПредставления(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетКурсовыхРазницВВалютеПредставления,
		Ложь, Ложь, Истина);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Рассчитать';uk='Розрахувати'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Использование_РасчетКурсовыхРазницВВалютеПредставления");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"МеждународныйУчетПроведениеСервер.Выполнить_РасчетКурсовыхРазницВВалютеПредставления");
	НоваяСтрока.ДействиеПодробнее = МеждународныйУчетПроведениеСервер.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперацияМУ();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РасчетКурсовыхРазницВалютаПредставления);
КонецПроцедуры

Процедура Использование_РасчетКурсовыхРазницВВалютеПредставления(ПараметрыОбработчика) Экспорт
	
	ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаМеждународногоУчета = МеждународнаяОтчетностьВызовСервера.УчетнаяВалюта();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если ВалютаМеждународногоУчета.Функциональная = ВалютаМеждународногоУчета.Представления Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru='Функциональная валюта совпадает с валютой представления.';uk='Функціональна валюта збігається з валютою подання.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	Иначе
		
		ВсегоНеПересчитано = Документы.РегламентнаяОперацияМеждународныйУчет.НеПересчитаноВВалютуПредставления(
			ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
			КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации),
			Истина);
		
		ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, ВсегоНеПересчитано);
		
		Если ВсегоНеПересчитано = 0 Тогда
			
			ВведеныРегламентныеДокументы = ПроверитьНаличиеДокументаРегламентнаяОперацияМУ(ПараметрыОбработчика);
			
		Иначе
			
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
				ПараметрыОбработчика,
				НСтр("ru='Есть незакрытые курсовые разницы для расчета.';uk='Є незакриті курсові різниці для розрахунку.'",ОбщегоНазначения.КодОсновногоЯзыка()));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_РасчетКурсовыхРазницВВалютеПредставления(ПараметрыОбработчика) Экспорт
	
	СформироватьДокументРегламентнаяОперацияМУ(ПараметрыОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область УстановкаДатыЗапретаФормированияПроводокМУ

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - ТаблицаЗначений - (См. Обработка.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов()).
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_УстановкаДатыЗапретаФормированияПроводокМУ(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.УстановкаДатыЗапретаФормированияПроводокМУ,
		Ложь, Ложь, Истина);
	НоваяСтрока.ТекстВыполнить = НСтр("ru='Установить';uk='Встановити'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Использование_УстановкаДатыЗапретаФормированияПроводокМУ");
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"МеждународныйУчетПроведениеСервер.Оформление_УстановкаДатыЗапретаФормированияПроводокМУ");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.РегистрыСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет.Формы.ДатыЗапретаФормирования.ПолноеИмя());
КонецПроцедуры

Процедура Использование_УстановкаДатыЗапретаФормированияПроводокМУ(ПараметрыОбработчика) Экспорт
	
	ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	ПараметрыОбработчика - см. Обработки.ОперацииЗакрытияМесяца.ИнициализироватьПараметрыОбработчикаЭтапа
//
Процедура Оформление_УстановкаДатыЗапретаФормированияПроводокМУ(ПараметрыОбработчика) Экспорт
	
	ДатаЗапрета = МеждународныйУчетОбщегоНазначения.ДатаЗапретаФормированияПроводок(ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
	ПараметрыОбработчика.ДанныеЭтапа.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Дата запрета формирования проводок %1';uk='Дата заборони формування проводок %1'"),
		Формат(ДатаЗапрета, "ДЛФ=DD"));
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Описание действия "Открыть форму списка документов ""Регламентная операция (международный учет)""".
// Отбор по типам операций берется из свойства ТипыРегламентныхОпераций описания этапа закрытия месяца.
//
// Возвращаемое значение:
//	Структура - см. СтруктураОписанияДействия().
//
Функция ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперацияМУ() Экспорт
	
	Описание = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет.Формы.ФормаСписка.ПолноеИмя(),
		Истина);
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

#КонецОбласти

// Обработчик вызываемый регламентным заданием ОтражениеДокументовВМеждународномУчете.
//
Процедура ОтразитьВМеждународномУчетеРегламентнымЗаданием() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтражениеДокументовВМеждународномУчете);
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	ОтразитьВМеждународномУчете(,,,,Ложь);

КонецПроцедуры

// Выполняет отражение документов в международном учете.
//
// Параметры:
//	МассивОрганизаций		- СправочникСсылка.Организации, Массив - отбор документов по организациям.
//	ДатаОкончания			- Дата - отбор документов до даты.
//	Документы				- ДокументСсылка, Массив - документ или массив документов к отражению.
//	АвтоматическоеОтражение - Булево - признак необходимости записи сформированных проводок.
//	ВыполнитьПересчеты - Булево - признак необходимости выполнения отложенного распределения взаиморасчетов с партнерами.
//
// Возвращаемое значение:
//	ТаблицаПроводок - ТаблицаЗначений - Таблица сформированных проводок, 
//										соответствует структуре регистра бухгалтерии Международный.
//
Функция ОтразитьВМеждународномУчете(МассивОрганизаций = Неопределено, ДатаОкончания = Неопределено, Документы = Неопределено, АвтоматическоеОтражение = Истина, ВыполнитьПересчеты = Истина) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(МассивОрганизаций, ДатаОкончания, Документы);
	КонецЕсли;
	
	ОписаниеЗамераОтражениеВМеждународномУчете = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("МеждународныйУчет.ОтражениеВМеждународномУчете");
	

	ПараметрыОтражения = ПараметрыФормированияПроводок(МассивОрганизаций, ДатаОкончания, Документы);
	
	ТаблицаПроводок = ТаблицаПроводок();
	
	КоличествоДокументов = 0;
	МоментВремени = Новый МоментВремени('00010101');
	Пока МоментВремени <> Неопределено Цикл
		
		ДокументыКОтражениюВУчете = ДокументыКОтражениюВУчете(
			ПараметрыОтражения.МенеджерВременныхТаблиц, 
			МассивОрганизаций, 
			ДатаОкончания, 
			Документы, 
			МоментВремени
		);
		Если МоментВремени = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоДокументов = КоличествоДокументов + ДокументыКОтражениюВУчете.Количество();
		
		НачатьТранзакцию();
		Попытка
		
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОтражениеДокументовВМеждународномУчете.НаборЗаписей");
			УстановитьДанныеБлокировки(ЭлементБлокировки, ПараметрыОтражения.МенеджерВременныхТаблиц);
			БлокировкаДанных.Заблокировать();
			
			МеждународныйУчетПоДаннымОперативногоУчета.Отразить(ПараметрыОтражения, ТаблицаПроводок);
			МеждународныйУчетПоДаннымРеглУчета.Отразить(ПараметрыОтражения, ТаблицаПроводок);
			МеждународныйУчетПоДаннымДокументов.Отразить(ПараметрыОтражения, ТаблицаПроводок);
			
			ПересчитатьТаблицуПроводокПоКурсу(ПараметрыОтражения, ТаблицаПроводок);
			
			СвернутьТаблицуПроводок(ПараметрыОтражения, ТаблицаПроводок);
			УдалитьПроводкиБезСуммИКоличеств(ТаблицаПроводок);
			УдалитьНезначащиеПроводки(ПараметрыОтражения, ТаблицаПроводок);
			ДобавитьПроводкиПоДругимОрганизациямПериодам(ПараметрыОтражения, ТаблицаПроводок);
			
			Если АвтоматическоеОтражение Тогда
				ЗаписатьПроводки(ТаблицаПроводок, ДокументыКОтражениюВУчете);
				ТаблицаПроводок.Очистить();
			КонецЕсли;
		
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось сформировать проводки международного учета по причине';uk='Не вдалося сформувати проводки міжнародного обліку з причини'") + ":"
							+ Символы.ПС + Символы.Таб + "%Причина%";
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, , ТекстСообщения);
			ВызватьИсключение ТекстСообщения;
		КонецПопытки;

		
		УничтожитьВременныеТаблицы(ПараметрыОтражения.МенеджерВременныхТаблиц);
		
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераОтражениеВМеждународномУчете, КоличествоДокументов);
	
	Возврат ТаблицаПроводок;
	
КонецФункции

// По передаваемой в качестве параметра таблице проводок, возвращает таблицу проводок, удовлетворяющих условиям записи.
//
// Параметры:
//	ПроводкиДокумента - ТаблицаЗначений - таблица проводок.
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица записываемых проводок.
//
Функция ЗаписываемыеПроводки(ПроводкиДокумента) Экспорт

	ЗаписываемыеПроводки = ПроводкиДокумента.Скопировать();
	
	ИсключаемыеПроводки = Новый Массив;
	Для каждого Проводка Из ЗаписываемыеПроводки Цикл

		Если Проводка.Статус <> Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете
			И Проводка.Статус <> Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную Тогда
			ИсключаемыеПроводки.Добавить(Проводка);
		Иначе
		    Если Проводка.Сумма = 0
					И Проводка.СуммаПредставления = 0
					И Проводка.КоличествоДт = 0 
					И Проводка.КоличествоКт = 0 Тогда
				ИсключаемыеПроводки.Добавить(Проводка);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Проводка Из ИсключаемыеПроводки Цикл
		ЗаписываемыеПроводки.Удалить(Проводка);
	КонецЦикла;
	
	Возврат ЗаписываемыеПроводки;

КонецФункции

// Обновляет статус отражения документа в международном учете на основе статусов сформированных проводок.
//
// Параметры:
//	ПроводкиДокумента - ТаблицаЗначений - таблица проводок документа.
//	Документ - ДокументСсылка - ссылка на документ.
//	ОбновлятьЗаписиОтраженияДляОтсутствующихПроводок - Булево - если среди таблицы проводок нет записей по отраженной организации, по умолчанию будет вставать "Отражено".
//
Процедура ОбновитьСтатусОтраженияДокумента(ПроводкиДокумента, Документ) Экспорт

	ЗаписиОтраженияДокумента = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
	ЗаписиОтраженияДокумента.Отбор.Регистратор.Установить(Документ);
	ЗаписиОтраженияДокумента.Прочитать();
	ПроводкиДокумента.Колонки.Добавить("Отмечено", Новый ОписаниеТипов("Булево"));
	
	Для каждого Запись Из ЗаписиОтраженияДокумента Цикл
		ПараметрыОтбора = Новый Структура("Организация", Запись.Организация);
		ПроводкиОрганизации = ПроводкиДокумента.НайтиСтроки(ПараметрыОтбора);
		Если ПроводкиДокумента.Количество() > 0 Тогда
			Статус = Неопределено;
			Комментарий = "";
			УникальныеЗначения = Новый Соответствие;
			Для Каждого Проводка Из ПроводкиОрганизации Цикл
				Если НЕ ЗначениеЗаполнено(Проводка.Период) ИЛИ НачалоДня(Проводка.Период) = Запись.ДатаОтражения Тогда
					Статус = МеждународныйУчетСерверПовтИсп.Статус(Статус, Проводка.Статус);
					ЗаполнитьКомментарий(Комментарий, Проводка, УникальныеЗначения);
					Проводка.Отмечено  = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ЗначениеЗаполнено(Статус) Тогда
				Запись.Статус = Статус;
				Запись.Комментарий = Комментарий;
			Иначе
				Запись.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете;
				Запись.Комментарий = "";
			КонецЕсли;
		Иначе
			Запись.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете;
			Запись.Комментарий = "";
		КонецЕсли;
	КонецЦикла;
	
	ЗаписиОтраженияДокумента.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСтатусПроводки(Проводка, Статус) Экспорт

	Проводка.Статус = МеждународныйУчетСерверПовтИсп.Статус(Проводка.Статус, Статус);

КонецПроцедуры

// Параметры:
// 	Проводка - Структура - Описание проводки:
//   * МассивОшибок - Массив из Строка - текст ошибки
// 	ТекстОшибки - Строка - 
Процедура ДобавитьОшибкуПроводки(Проводка, ТекстОшибки) Экспорт

	Проводка.МассивОшибок.Добавить(ТекстОшибки);

КонецПроцедуры

// Параметры:
// 	ТаблицаПроводок - ТаблицаЗначений - 
// 	Проводка - Структура - 
Процедура ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка) Экспорт

	СтрокаТаблицы = ТаблицаПроводок.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Проводка);
	
	ЗаполнитьСтатусВТаблицеПроводок(СтрокаТаблицы, Проводка);
	
	ЗаполнитьКомментарийВТаблицеПроводок(СтрокаТаблицы, Проводка);

КонецПроцедуры

// Параметры:
// 	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - 
// 	Настройка - Структура - 
// Возвращаемое значение:
// 	Строка - 
Функция ВыражениеЗаполненияСубконтоПоНастройке(СхемаКомпоновкиДанных, Настройка) Экспорт
	
	ВыражениеВычисляемогоПоля = "Неопределено";
	Если Настройка.ЗаполнятьИзИсточника Тогда
		ВыражениеНастройки = Настройка.Выражение;
		Если Найти(ВыражениеНастройки, "СчетДт.") > 0 Тогда
			ВыражениеНастройки = СтрЗаменить(ВыражениеНастройки, "СчетДт.", "");
		ИначеЕсли Найти(ВыражениеНастройки, "СчетКт.") > 0 Тогда
			ВыражениеНастройки = СтрЗаменить(ВыражениеНастройки, "СчетКт.", "");
		ИначеЕсли ПустаяСтрока(ВыражениеНастройки) Тогда
			ВыражениеНастройки = "Неопределено";
		КонецЕсли;
		ВыражениеВычисляемогоПоля = ВыражениеНастройки;
	Иначе
		ИмяПараметра = "УказанноеЗначение_" + СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
		Параметр = СхемаКомпоновкиДанных.Параметры.Добавить();
		Параметр.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
		Параметр.Имя = ИмяПараметра;
		Параметр.Значение = Настройка.УказанноеЗначение;
		ВыражениеВычисляемогоПоля = "&" + ИмяПараметра;
	КонецЕсли;
	
	Возврат ВыражениеВычисляемогоПоля;
	
КонецФункции

// Возвращает общие параметры формирования проводок
//
// Возвращаемое значение:
// 	Структура - Содержит параметры:
// 		* СтруктураПроводки - См. СтруктураПроводки
// 		* КоличествоСубконто - Число - максимальное количество субконто плана счетов Международный
// 		* БалансовыеИзмерения - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* НебалансовыеИзмерения - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* БалансовыеРесурсы - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* НебалансовыеРесурсы - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* Реквизиты - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* ИнвертируемыеПоля - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* ИнвертируемыеРесурсы - Массив из Строка - имена полей регистра бухгалтерии Международный
// 		* СвернутьТаблицуПроводок - Булево - признак необходимости свертки проводок перед записью
// 		* УдалитьНезначащиеПроводки - Булево - признак необходимости удаления проводок с одинаковыми значениями Дт и Кт
//
Функция НовыеПараметрыФормированияПроводок() Экспорт
	
	ПараметрыФормированияПроводок = Новый Структура;
	ПараметрыФормированияПроводок.Вставить("СтруктураПроводки", СтруктураПроводки());
	
	ВалютыУчета = Новый Структура;
	ВалютыМеждународногоУчета = МеждународнаяОтчетностьВызовСервера.УчетнаяВалюта();
	ВалютыУчета.Вставить("ФункциональнаяВалюта", ВалютыМеждународногоУчета.Функциональная);
	ВалютыУчета.Вставить("ВалютаПредставления", ВалютыМеждународногоУчета.Представления);
	ВалютыУчета.Вставить("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	ВалютыУчета.Вставить("ВалютаРегл", Константы.ВалютаРегламентированногоУчета.Получить());
	ВалютыУчета.Вставить("УчетВФункциональнойВалютеУпр", 
		Константы.УчетВФункциональнойВалюте.Получить() = Перечисления.ВидыУчетаВФункциональнойВалюте.ВВалютеУпр);

	ПараметрыФормированияПроводок.Вставить("ВалютыУчета", ВалютыУчета);
	
	ОписаниеПолейРегистра = РегистрыБухгалтерии.Международный.ОписаниеПолейРегистра();
	
	ПараметрыФормированияПроводок.Вставить("КоличествоСубконто",    ОписаниеПолейРегистра.КоличествоСубконто);
	ПараметрыФормированияПроводок.Вставить("БалансовыеИзмерения",   ОписаниеПолейРегистра.БалансовыеИзмерения);
	ПараметрыФормированияПроводок.Вставить("НебалансовыеИзмерения", ОписаниеПолейРегистра.НебалансовыеИзмерения);
	ПараметрыФормированияПроводок.Вставить("БалансовыеРесурсы",     ОписаниеПолейРегистра.БалансовыеРесурсы);
	ПараметрыФормированияПроводок.Вставить("НебалансовыеРесурсы",   ОписаниеПолейРегистра.НебалансовыеРесурсы);
	ПараметрыФормированияПроводок.Вставить("Реквизиты",             ОписаниеПолейРегистра.Реквизиты);
	
	ПараметрыФормированияПроводок.Вставить("ИнвертируемыеПоля",    РегистрыБухгалтерии.Международный.ИнвертируемыеПоля(ОписаниеПолейРегистра));
	ПараметрыФормированияПроводок.Вставить("ИнвертируемыеРесурсы", РегистрыБухгалтерии.Международный.ИнвертируемыеРесурсы(ОписаниеПолейРегистра));
	
	ПараметрыФормированияПроводок.Вставить("СвернутьТаблицуПроводок", Ложь);
	ПараметрыФормированияПроводок.Вставить("УдалитьНезначащиеПроводки", Ложь);
	
	Возврат ПараметрыФормированияПроводок;
	
КонецФункции

#КонецОбласти

#Область ФормированиеПроводок

// Возвращает общие параметры формирования проводок
//
// Параметры:
// 	ПараметрыОтраженияВМеждународномУчете - см. ПараметрыОтраженияВМеждународномУчете
// 	
// Возвращаемое значение:
// 	Структура - Содержит параметры:
// 		* СтруктураПроводки - См. НовыеПараметрыФормированияПроводок
// 		* МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - 
Функция ПараметрыФормированияПроводок(МассивОрганизаций, ДатаОкончания, Документы) Экспорт
	
	ПараметрыФормированияПроводок = НовыеПараметрыФормированияПроводок();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПараметрыФормированияПроводок.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
	ИнициализироватьТаблицуУчетнаяПолитикаОрганизаций(МенеджерВременныхТаблиц, МассивОрганизаций, ДатаОкончания, Документы);
	
	МеждународныйУчетПоДаннымОперативногоУчета.ДополнитьПараметрыОтражения(ПараметрыФормированияПроводок);
	
	Возврат ПараметрыФормированияПроводок;
	
КонецФункции

Процедура ИнициализироватьТаблицуУчетнаяПолитикаОрганизаций(МенеджерВременныхТаблиц, МассивОрганизаций, ДатаОкончания, Документы)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Период КАК Период,
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|";
	
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(МассивОрганизаций));
	Запрос.УстановитьПараметр("МассивОрганизаций",  ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(НЕ ЗначениеЗаполнено(ДатаОкончания) ИЛИ ЗначениеЗаполнено(Документы), '39991231', ДатаОкончания));
	Запрос.УстановитьПараметр("Документы", Документы);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДокументыКОтражениюВУчете(МенеджерВременныхТаблиц, МассивОрганизаций, ДатаОкончания, Документы, МоментВремени)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ТаблицаДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК ПериодУчетнойПолитики
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДокументов.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ТаблицаДокументов.ДатаОтражения >= УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	(&НеПроверятьСтатусОтражения
	|			ИЛИ ТаблицаДокументов.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете)))
	|	И (ТаблицаДокументов.Организация В (&МассивОрганизаций) ИЛИ &ПоВсемОрганизациям)
	|	И (ТаблицаДокументов.Регистратор В (&Документы) ИЛИ &Документы = НЕОПРЕДЕЛЕНО)
	|	И ТаблицаДокументов.ДатаОтражения <= &ДатаОкончания
	|	И ТаблицаДокументов.МоментВремени > &МоментВремени
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.ДатаОтражения,
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация,
	|	ТаблицаДокументов.МоментВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодУчетнойПолитики,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика, НЕОПРЕДЕЛЕНО) КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДокументов.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ТаблицаДокументов.ПериодУчетнойПолитики = УчетнаяПолитикаОрганизаций.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.ДатаОтражения,
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика, НЕОПРЕДЕЛЕНО)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаОтражения,
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Регистратор КАК РегистраторДанныеРегистра,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Статус КАК Статус,
	|	ДанныеРегистра.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ДокументыЧастичноОтраженные
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ТаблицаДокументов.Регистратор = ДанныеРегистра.Регистратор
	|ГДЕ
	|	ДанныеРегистра.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаОтражения,
	|	Регистратор,
	|	Организация
	|;
	|
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Документ,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени,
	|	Проводки.Период КАК Период,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.НомерСтроки КАК НомерСтроки,
	|	Проводки.Активность КАК Активность,
	|	Проводки.СчетДт КАК СчетДт,
	|	Проводки.СчетКт КАК СчетКт,
	|	Проводки.Организация КАК Организация,
	|	Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|	Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|	Проводки.ВалютаДт КАК ВалютаДт,
	|	Проводки.ВалютаКт КАК ВалютаКт,
	|	Проводки.Сумма КАК Сумма,
	|	Проводки.СуммаПредставления КАК СуммаПредставления,
	|	Проводки.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	Проводки.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	Проводки.КоличествоДт КАК КоличествоДт,
	|	Проводки.КоличествоКт КАК КоличествоКт,
	|	Проводки.Содержание КАК Содержание,
	|	Проводки.ШаблонПроводки КАК ШаблонПроводки,
	|	Проводки.ТипПроводки КАК ТипПроводки,
	|	Проводки.СубконтоДт1 КАК СубконтоДт1,
	|	Проводки.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|	Проводки.СубконтоДт2 КАК СубконтоДт2,
	|	Проводки.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|	Проводки.СубконтоДт3 КАК СубконтоДт3,
	|	Проводки.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|	Проводки.СубконтоКт1 КАК СубконтоКт1,
	|	Проводки.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|	Проводки.СубконтоКт2 КАК СубконтоКт2,
	|	Проводки.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|	Проводки.СубконтоКт3 КАК СубконтоКт3,
	|	Проводки.ВидСубконтоКт3 КАК ВидСубконтоКт3
	|ПОМЕСТИТЬ ПроводкиДругихОрганизацийПериодов
	|ИЗ
	|	ДокументыЧастичноОтраженные КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Международный.ДвиженияССубконто(
	|			,
	|			&ДатаОкончания,
	|			Регистратор В (&Документы) ИЛИ &Документы = НЕОПРЕДЕЛЕНО,
	|			,
	|		) КАК Проводки
	|		ПО ТаблицаДокументов.ДатаОтражения = НАЧАЛОПЕРИОДА(Проводки.Период, ДЕНЬ)
	|		И ТаблицаДокументов.Регистратор = Проводки.Регистратор
	|		И ТаблицаДокументов.Организация = Проводки.Организация
	|		
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Документ,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.НомерСтроки КАК НомерСтроки,
	|	ДанныеРегистра.Активность КАК Активность,
	|	ДанныеРегистра.СчетДт КАК СчетДт,
	|	ДанныеРегистра.СчетКт КАК СчетКт,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ПодразделениеДт КАК ПодразделениеДт,
	|	ДанныеРегистра.ПодразделениеКт КАК ПодразделениеКт,
	|	ДанныеРегистра.ВалютаДт КАК ВалютаДт,
	|	ДанныеРегистра.ВалютаКт КАК ВалютаКт,
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаПредставления КАК СуммаПредставления,
	|	ДанныеРегистра.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	ДанныеРегистра.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	ДанныеРегистра.КоличествоДт КАК КоличествоДт,
	|	ДанныеРегистра.КоличествоКт КАК КоличествоКт,
	|	ДанныеРегистра.Содержание КАК Содержание,
	|	ДанныеРегистра.ШаблонПроводки КАК ШаблонПроводки,
	|	ДанныеРегистра.ТипПроводки КАК ТипПроводки,
	|	ДанныеРегистра.СубконтоДт1 КАК СубконтоДт1,
	|	ДанныеРегистра.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|	ДанныеРегистра.СубконтоДт2 КАК СубконтоДт2,
	|	ДанныеРегистра.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|	ДанныеРегистра.СубконтоДт3 КАК СубконтоДт3,
	|	ДанныеРегистра.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|	ДанныеРегистра.СубконтоКт1 КАК СубконтоКт1,
	|	ДанныеРегистра.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|	ДанныеРегистра.СубконтоКт2 КАК СубконтоКт2,
	|	ДанныеРегистра.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|	ДанныеРегистра.СубконтоКт3 КАК СубконтоКт3,
	|	ДанныеРегистра.ВидСубконтоКт3 КАК ВидСубконтоКт3
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроводкиДругихОрганизацийПериодов КАК ДанныеРегистра
	|		ПО ТаблицаДокументов.Регистратор = ДанныеРегистра.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|ИТОГИ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДокументов;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыЧастичноОтраженные;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПроводкиДругихОрганизацийПериодов;
	|";
	
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(МассивОрганизаций));
	Запрос.УстановитьПараметр("МассивОрганизаций",  ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(НЕ ЗначениеЗаполнено(ДатаОкончания) ИЛИ ЗначениеЗаполнено(Документы), '39991231', ДатаОкончания));
	Запрос.УстановитьПараметр("Документы", Документы);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("НеПроверятьСтатусОтражения", Документы <> Неопределено);
	
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[4].Пустой() Тогда
		ДокументыКОтражениюВУчете = Результат[5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ТаблицаДокументовКОтражениюВУчете = Результат[4].Выгрузить();
		МоментВремени = ТаблицаДокументовКОтражениюВУчете[ТаблицаДокументовКОтражениюВУчете.Количество() - 1].МоментВремени;
	Иначе
		ДокументыКОтражениюВУчете = Неопределено;
		МоментВремени = Неопределено;
	КонецЕсли;
	
	Возврат ДокументыКОтражениюВУчете;
	
КонецФункции

Процедура УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"УНИЧТОЖИТЬ ДокументыКОтражению
	|";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура УстановитьДанныеБлокировки(ЭлементБлокировки, МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыКОтражению.Регистратор КАК Регистратор
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|";
	Результат = Запрос.Выполнить();
	
	ЭлементБлокировки.ИсточникДанных  = Результат;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");

КонецПроцедуры

Процедура ЗаписатьПроводки(ТаблицаПроводок, ДокументыКОтражениюВУчете)

	Пока ДокументыКОтражениюВУчете.Следующий() Цикл
		
		Регистратор = ДокументыКОтражениюВУчете.Документ;
		
		ПроводкиДокумента = ТаблицаПроводок.Скопировать(Новый Структура("Регистратор", Регистратор));
		ОбновитьСтатусОтраженияДокумента(ПроводкиДокумента, Регистратор);
		ЗаписываемыеПроводки = ЗаписываемыеПроводки(ПроводкиДокумента);
		
		ЗаписываемыеПроводки.Сортировать("Период,Регистратор,Организация");
		
		НаборЗаписей = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписыватьПустойНабор", Истина);
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
		Для Каждого Проводка Из ЗаписываемыеПроводки Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Проводка);
			НаборЗаписей.УстановитьСубконто(НоваяЗапись, Проводка, "Дт");
			НаборЗаписей.УстановитьСубконто(НоваяЗапись, Проводка, "Кт");
		КонецЦикла;
		НаборЗаписей.Записать();
		
	КонецЦикла;

КонецПроцедуры

Процедура ПересчитатьТаблицуПроводокПоКурсу(ПараметрыОтражения, ТаблицаПроводок)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПроводок.Период КАК Период,
	|	ТаблицаПроводок.Организация КАК Организация,
	|	&ТекстВыборкиОстальныхКолонок,
	|	ВЫБОР КОГДА ТаблицаПроводок.Сумма = НЕОПРЕДЕЛЕНО
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаПроводок.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаПроводок.СуммаПредставления = НЕОПРЕДЕЛЕНО
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаПроводок.СуммаПредставления
	|	КОНЕЦ КАК СуммаПредставления,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной КАК ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления КАК ВалютаХраненияСуммыПредставления
	|ПОМЕСТИТЬ ТаблицаПроводок
	|ИЗ
	|	&ТаблицаПроводок КАК ТаблицаПроводок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период,
	|	ВалютаХраненияСуммыФункциональной,
	|	ВалютаХраненияСуммыПредставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПроводок.Период,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления
	|ПОМЕСТИТЬ Валюты
	|ИЗ
	|	ТаблицаПроводок КАК ТаблицаПроводок
	|ГДЕ
	|	(ТаблицаПроводок.ВалютаХраненияСуммыФункциональной <> &ФункциональнаяВалюта
	|			ИЛИ ТаблицаПроводок.ВалютаХраненияСуммыПредставления <> &ВалютаПредставления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаПроводок.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалют.Период КАК Период,
	|	КурсыВалют.Валюта КАК Валюта,
	|	МАКСИМУМ(КурсыВалют.ПериодКурса) КАК ПериодКурса
	|ПОМЕСТИТЬ ПериодыКурсовВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Валюты.Период КАК Период,
	|		Валюты.ВалютаХраненияСуммыФункциональной КАК Валюта,
	|		КурсыВалют.Период КАК ПериодКурса
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = Валюты.ВалютаХраненияСуммыФункциональной)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		Валюты.ВалютаХраненияСуммыПредставления,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = Валюты.ВалютаХраненияСуммыПредставления)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		&ФункциональнаяВалюта,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = &ФункциональнаяВалюта)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		&ВалютаПредставления,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = &ВалютаПредставления)) КАК КурсыВалют
	|
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалют.Период,
	|	КурсыВалют.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КурсыВалют.Период,
	|	КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалют.Период КАК Период,
	|	ПериодыКурсовВалют.Валюта КАК Валюта,
	|	КурсыВалют.Курс / КурсыВалют.Кратность КАК Курс
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	ПериодыКурсовВалют КАК ПериодыКурсовВалют
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалют.ПериодКурса = КурсыВалют.Период
	|			И ПериодыКурсовВалют.Валюта = КурсыВалют.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодыКурсовВалют.Период,
	|	ПериодыКурсовВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Валюты.Период,
	|	Валюты.ВалютаХраненияСуммыФункциональной,
	|	Валюты.ВалютаХраненияСуммыПредставления,
	|	ЕСТЬNULL(КурсыВалютХраненияСуммыФункциональной.Курс, 1) / ЕСТЬNULL(КурсыФункциональнойВалюты.Курс, 1) КАК КоэффициентПересчетаСуммы,
	|	ЕСТЬNULL(КурсыВалютХраненияСуммыПредставления.Курс, 1) / ЕСТЬNULL(КурсыВалютыПредставления.Курс, 1) КАК КоэффициентПересчетаСуммыПредставления
	|ПОМЕСТИТЬ КоэффициентыПересчета
	|ИЗ
	|	Валюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютХраненияСуммыФункциональной
	|		ПО Валюты.Период = КурсыВалютХраненияСуммыФункциональной.Период
	|			И Валюты.ВалютаХраненияСуммыФункциональной = КурсыВалютХраненияСуммыФункциональной.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютХраненияСуммыПредставления
	|		ПО Валюты.Период = КурсыВалютХраненияСуммыПредставления.Период
	|			И Валюты.ВалютаХраненияСуммыПредставления = КурсыВалютХраненияСуммыПредставления.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыФункциональнойВалюты
	|		ПО Валюты.Период = КурсыФункциональнойВалюты.Период
	|			И (&ФункциональнаяВалюта = КурсыФункциональнойВалюты.Валюта)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютыПредставления
	|		ПО Валюты.Период = КурсыВалютыПредставления.Период
	|			И (&ВалютаПредставления = КурсыВалютыПредставления.Валюта)
	|ГДЕ
	|	(ЕСТЬNULL(КурсыВалютХраненияСуммыФункциональной.Курс, 1) / ЕСТЬNULL(КурсыФункциональнойВалюты.Курс, 1) <> 1
	|			ИЛИ ЕСТЬNULL(КурсыВалютХраненияСуммыПредставления.Курс, 1) / ЕСТЬNULL(КурсыВалютыПредставления.Курс, 1) <> 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроводок.Период КАК Период,
	|	ТаблицаПроводок.Организация,
	|	&ТекстВыборкиОстальныхКолонок,
	|	ТаблицаПроводок.Сумма * ЕСТЬNULL(КоэффициентыПересчета.КоэффициентПересчетаСуммы, 1) КАК Сумма,
	|	ТаблицаПроводок.СуммаПредставления * ЕСТЬNULL(КоэффициентыПересчета.КоэффициентПересчетаСуммыПредставления, 1) КАК СуммаПредставления,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления
	|ИЗ
	|	ТаблицаПроводок КАК ТаблицаПроводок
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыПересчета КАК КоэффициентыПересчета
	|		ПО ТаблицаПроводок.Период = КоэффициентыПересчета.Период
	|			И ТаблицаПроводок.ВалютаХраненияСуммыФункциональной = КоэффициентыПересчета.ВалютаХраненияСуммыФункциональной
	|			И ТаблицаПроводок.ВалютаХраненияСуммыПредставления = КоэффициентыПересчета.ВалютаХраненияСуммыПредставления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаПроводок.Организация,
	|	ТаблицаПроводок.Период";
	
	Запрос.УстановитьПараметр("ТаблицаПроводок", ТаблицаПроводок);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта", ПараметрыОтражения.ВалютыУчета.ФункциональнаяВалюта);
	Запрос.УстановитьПараметр("ВалютаПредставления", ПараметрыОтражения.ВалютыУчета.ВалютаПредставления);
	
	ШаблонТекстаВыборкиКолонки = "	ТаблицаПроводок.%1,"+Символы.ПС;
	ТекстВыборкиОстальныхКолонок = "";
	СтруктураКолонокИсключений = Новый Структура("Период, Сумма, СуммаПредставления, Организация, ВалютаХраненияСуммыФункциональной, ВалютаХраненияСуммыПредставления, МоментВремени");
	Для каждого Колонка Из ТаблицаПроводок.Колонки Цикл
		Если СтруктураКолонокИсключений.Свойство(Колонка.Имя) Тогда
			Продолжить;
		КонецЕсли;
		ТекстВыборкиОстальныхКолонок = ТекстВыборкиОстальныхКолонок + СтрШаблон(ШаблонТекстаВыборкиКолонки, Колонка.Имя);
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВыборкиОстальныхКолонок,", ТекстВыборкиОстальныхКолонок);
	
	ТаблицаПроводок = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ДобавитьПроводкиПоДругимОрганизациямПериодам(ПараметрыФормированияПроводок, ТаблицаПроводок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыФормированияПроводок.МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОтражениеДокументовВУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВУчете.Организация КАК Организация,
	|	ОтражениеДокументовВУчете.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ДокументыМножественногоОтражения
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВУчете
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ГДЕ
	|			ДокументыКОтражению.Регистратор = ОтражениеДокументовВУчете.Регистратор
	|		)
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ГДЕ
	|			ДокументыКОтражению.Регистратор = ОтражениеДокументовВУчете.Регистратор
	|			И ДокументыКОтражению.Организация = ОтражениеДокументовВУчете.Организация
	|			И ДокументыКОтражению.ДатаОтражения = ОтражениеДокументовВУчете.ДатаОтражения
	|		)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|//////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки.Период КАК Период,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.НомерСтроки КАК НомерСтроки,
	|	Проводки.СчетДт КАК СчетДт,
	|	Проводки.СчетКт КАК СчетКт,
	|	Проводки.Организация КАК Организация,
	|	Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|	Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|	Проводки.ВалютаДт КАК ВалютаДт,
	|	Проводки.ВалютаКт КАК ВалютаКт,
	|	Проводки.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Проводки.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	Проводки.Сумма КАК Сумма,
	|	Проводки.СуммаПредставления КАК СуммаПредставления,
	|	Проводки.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	Проводки.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	Проводки.КоличествоДт КАК КоличествоДт,
	|	Проводки.КоличествоКт КАК КоличествоКт,
	|	Проводки.СубконтоДт1 КАК СубконтоДт1,
	|	ЕСТЬNULL(Проводки.ВидСубконтоДт1, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт1,
	|	Проводки.СубконтоДт2 КАК СубконтоДт2,
	|	ЕСТЬNULL(Проводки.ВидСубконтоДт2, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт2,
	|	Проводки.СубконтоДт3 КАК СубконтоДт3,
	|	ЕСТЬNULL(Проводки.ВидСубконтоДт3, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт3,
	|	Проводки.СубконтоКт1 КАК СубконтоКт1,
	|	ЕСТЬNULL(Проводки.ВидСубконтоКт1, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт1,
	|	Проводки.СубконтоКт2 КАК СубконтоКт2,
	|	ЕСТЬNULL(Проводки.ВидСубконтоКт2, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт2,
	|	Проводки.СубконтоКт3 КАК СубконтоКт3,
	|	ЕСТЬNULL(Проводки.ВидСубконтоКт3, 
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт3,
	|	Проводки.Содержание КАК Содержание,
	|	Проводки.ШаблонПроводки КАК ШаблонПроводки,
	|	Проводки.ТипПроводки КАК ТипПроводки,
	|	Проводки.СоответствиеСчетаДт КАК СоответствиеСчетаДт,
	|	Проводки.СоответствиеСчетаКт КАК СоответствиеСчетаКт,
	|	Проводки.СоответствиеОборотов КАК СоответствиеОборотов
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(
	|		,
	|		,
	|		(Регистратор, Организация) В (
	|			ВЫБРАТЬ
	|				ДокументыКОтражению.Регистратор,
	|				ДокументыКОтражению.Организация
	|			ИЗ
	|				ДокументыМножественногоОтражения КАК ДокументыКОтражению
	|		),
	|		,
	|	) КАК Проводки
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ДокументыМножественногоОтражения КАК ДокументыКОтражению
	|		ГДЕ
	|			
	|			ДокументыКОтражению.Регистратор = Проводки.Регистратор
	|			И ДокументыКОтражению.Организация = Проводки.Организация
	|			И ДокументыКОтражению.ДатаОтражения = НАЧАЛОПЕРИОДА(Проводки.Период, ДЕНЬ)
	|		)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыМножественногоОтражения
	|;
	|";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Проводка = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыФормированияПроводок.СтруктураПроводки);
		ЗаполнитьЗначенияСвойств(Проводка, Выборка);
		ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКомментарий(Комментарий, Проводка, УникальныеЗначения)

	Если УникальныеЗначения[Проводка.Комментарий] = Неопределено Тогда
		Комментарий = ?(Не ПустаяСтрока(Комментарий), Комментарий + Символы.ПС, Комментарий);
		Комментарий = Комментарий + Проводка.Комментарий;
		УникальныеЗначения.Вставить(Проводка.Комментарий, Истина);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСтатусВТаблицеПроводок(СтрокаТаблицы, Проводка)

	Если ЗначениеЗаполнено(СтрокаТаблицы.Статус) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОшибок = Проводка.МассивОшибок.Количество();
	Если КоличествоОшибок > 0 Тогда
		СтрокаТаблицы.Статус = Проводка.Статус;
	Иначе
		СтрокаТаблицы.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьКомментарийВТаблицеПроводок(СтрокаТаблицы, Проводка)

	МассивОшибок = Проводка.МассивОшибок;
	КоличествоОшибок = МассивОшибок.Количество();
	Если КоличествоОшибок > 0 Тогда
		
		Если ЗначениеЗаполнено(Проводка.ШаблонПроводки) Тогда
			ТекстШаблонПроводки = НСтр("ru='Шаблон проводки ""%ШаблонПроводки%""';uk='Шаблон проводки ""%ШаблонПроводки%""'") + Символы.ПС;
			ТекстШаблонПроводки = СтрЗаменить(ТекстШаблонПроводки, "%ШаблонПроводки%", Проводка.ШаблонПроводки);
			ТекстКоличествоОшибок = НСтр("ru='Обнаружены ошибки (%КоличествоОшибок%):';uk='Виявлені помилки (%КоличествоОшибок%):'");
			ТекстКоличествоОшибок = СтрЗаменить(ТекстКоличествоОшибок, "%КоличествоОшибок%", КоличествоОшибок);
			ОписаниеОшибок = ТекстШаблонПроводки + ТекстКоличествоОшибок;
			Для К = 1 По КоличествоОшибок Цикл
				ОписаниеОшибки = НСтр("ru='%НомерОшибки%. %ТекстОшибки%';uk='%НомерОшибки%. %ТекстОшибки%'");
				ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%НомерОшибки%", К);
				ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ТекстОшибки%", МассивОшибок[К - 1]);
				ОписаниеОшибок = ОписаниеОшибок + Символы.ПС + ОписаниеОшибки;
			КонецЦикла;
		Иначе
			ОписаниеОшибок = НСтр("ru='%ТекстОшибки%';uk='%ТекстОшибки%'");
			ОписаниеОшибок = СтрЗаменить(ОписаниеОшибок, "%ТекстОшибки%", Проводка.МассивОшибок[0]);
		КонецЕсли;
		
		СтрокаТаблицы.Комментарий = ОписаниеОшибок;
		
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьОффлайновыеРасчеты(МассивОрганизаций = Неопределено, ДатаОкончания = Неопределено, ДокументыКОтражению = Неопределено)
	
	НачалоМесяца = НачалоМесяца(ДатаОкончания);
	КонецМесяца = КонецМесяца(ДатаОкончания);
	ОкончаниеРасчета = КонецМесяца(ДатаОкончания)+1;

	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Если ДокументыКОтражению = Неопределено Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ОтражениеДокументов.Регистратор КАК Регистратор
			|ИЗ
			|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
			|ГДЕ
			|	ОтражениеДокументов.Статус В (
			|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете))
			|	И (ОтражениеДокументов.Организация В (&Организации)
			|			ИЛИ &ПоВсемОрганизациям)
			|	И (ОтражениеДокументов.Период <= &ДатаОкончания)");
			
			Запрос.УстановитьПараметр("ДатаОкончания", ?(ЗначениеЗаполнено(ДатаОкончания),ДатаОкончания,'29990101'));
			Запрос.УстановитьПараметр("Организации", МассивОрганизаций);
			Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(МассивОрганизаций));
			
			МассивДокументыКОтражению = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
			
		ИначеЕсли ТипЗнч(ДокументыКОтражению) = Тип("Массив") Тогда
			МассивДокументыКОтражению = ДокументыКОтражению;
		Иначе
			МассивДокументыКОтражению = Новый Массив;
			МассивДокументыКОтражению.Добавить(ДокументыКОтражению);
		КонецЕсли;
		
		РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументыКОтражению);
		РегистрыСведений.СуммыДокументовВВалютахУчета.ОбновитьДвиженияПоОборотнымРегистрамНепересчитываемыхДокументов(МассивДокументыКОтражению);
	Иначе
		Если ДокументыКОтражению = Неопределено Тогда
			
			Если ЗначениеЗаполнено(ДатаОкончания) Тогда
				КонецРасчета = КонецМесяца(ДатаОкончания)+1;
			Иначе
				КонецРасчета = КонецМесяца(ТекущаяДатаСеанса())+1;
			КонецЕсли;
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			Если ЗначениеЗаполнено(МассивОрганизаций) Тогда
				АналитикиРасчета.Организации = ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций);
			КонецЕсли;
			РаспределениеВзаиморасчетовВызовСервера.РассчитатьВсе(КонецРасчета, АналитикиРасчета);
			
			Возврат;
			
		КонецЕсли;
		
		Если ТипЗнч(ДокументыКОтражению) = Тип("Массив") Тогда
			МассивДокументыКОтражению = ДокументыКОтражению;
		Иначе
			МассивДокументыКОтражению = Новый Массив;
			МассивДокументыКОтражению.Добавить(ДокументыКОтражению);
		КонецЕсли;
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Расчеты.Регистратор В (&ДокументыКОтражению)
		|	И Расчеты.Активность
		|");
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца);
		Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца);
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
		МассивАналитикПоставщиков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		Если МассивАналитикПоставщиков.Количество() > 0 Тогда
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикПоставщиков;
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(ОкончаниеРасчета, АналитикиРасчета);
		КонецЕсли;
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Расчеты.Регистратор В (&ДокументыКОтражению)
		|	И Расчеты.Активность
		|");
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца);
		Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца);
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
		МассивАналитикКлиентов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		Если МассивАналитикКлиентов.Количество() > 0 Тогда
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикКлиентов;
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеРасчета, АналитикиРасчета);
		КонецЕсли;
	КонецЕсли;
	
	ВнеоборотныеАктивы.ОтложенноеФормированиеДвиженийПриФормированииПроводок(МассивДокументыКОтражению);
	ВнеоборотныеАктивы.РассчитатьСтоимостьВнеоборотныхАктивовПриФормированииПроводок(МассивДокументыКОтражению);
	
КонецПроцедуры

Функция СтруктураПроводки()

	Проводка = Новый Структура;
	СтруктураНабораЗаписей = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	Для каждого Колонка Из СтруктураНабораЗаписей.Колонки Цикл
		Если Колонка.Имя <> "Активность" Тогда
			Проводка.Вставить(Колонка.Имя); 
		КонецЕсли;
	КонецЦикла;
	Проводка.Вставить("Статус");
	Проводка.Вставить("ШаблонПроводки");
	Проводка.Вставить("МассивОшибок", Новый Массив);
	Проводка.Вставить("ВалютаХраненияСуммыПредставления");
	Проводка.Вставить("ВалютаХраненияСуммыФункциональной");
	
	Возврат Проводка;

КонецФункции

Функция ТаблицаПроводок()

	ТаблицаПроводок = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаПроводок.Колонки.Удалить("Активность");
	ТаблицаПроводок.Колонки.Добавить("Статус", Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОтраженияВМеждународномУчете"));
	ТаблицаПроводок.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	ТаблицаПроводок.Колонки.Добавить("ВалютаХраненияСуммыПредставления", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаПроводок.Колонки.Добавить("ВалютаХраненияСуммыФункциональной", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Процедура УдалитьПроводкиБезСуммИКоличеств(ПроводкиДокумента)
	
	ПроводкиКУдалению = Новый Массив;
	Для каждого Проводка Из ПроводкиДокумента Цикл
		Если (Проводка.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете
				ИЛИ Проводка.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
			И Проводка.Сумма = 0
			И Проводка.СуммаПредставления = 0
			И Проводка.КоличествоДт = 0 
			И Проводка.КоличествоКт = 0 Тогда
			Если НЕ ЗначениеЗаполнено(Проводка.Комментарий) Тогда
				ПроводкиКУдалению.Добавить(Проводка);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Проводка Из ПроводкиКУдалению Цикл
		ПроводкиДокумента.Удалить(Проводка);
	КонецЦикла;
	
КонецПроцедуры

Процедура СвернутьТаблицуПроводок(ПараметрыФормированияПроводок, ТаблицаПроводок)
	
	Если НЕ ПараметрыФормированияПроводок.СвернутьТаблицуПроводок Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтрок = Новый Структура();
	ОтборСтрок.Вставить("Статус", Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
	
	СтрокиОтраженыВУчете = ТаблицаПроводок.НайтиСтроки(ОтборСтрок);
	
	Если СтрокиОтраженыВУчете.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПоляГруппировки = Новый Массив();
	ПоляГруппировки.Добавить("Период");
	ПоляГруппировки.Добавить("Регистратор");
	ПоляГруппировки.Добавить("СчетДт");
	ПоляГруппировки.Добавить("СчетКт");
	ПоляГруппировки.Добавить("Статус");
	ПоляГруппировки.Добавить("ВалютаХраненияСуммыПредставления");
	ПоляГруппировки.Добавить("ВалютаХраненияСуммыФункциональной");
	ПоляГруппировки.Добавить("Комментарий");
	
	Для НомерСубконто = 1 По ПараметрыФормированияПроводок.КоличествоСубконто Цикл
		ПоляГруппировки.Добавить("ВидСубконтоДт" + НомерСубконто);
		ПоляГруппировки.Добавить("ВидСубконтоКт" + НомерСубконто);
		ПоляГруппировки.Добавить("СубконтоДт" + НомерСубконто);
		ПоляГруппировки.Добавить("СубконтоКт" + НомерСубконто);
	КонецЦикла;
	
	Для Каждого ИмяИзмерения Из ПараметрыФормированияПроводок.БалансовыеИзмерения Цикл
		ПоляГруппировки.Добавить(ИмяИзмерения);
	КонецЦикла;
	
	Для Каждого ИмяИзмерения Из ПараметрыФормированияПроводок.НебалансовыеИзмерения Цикл
		ПоляГруппировки.Добавить(ИмяИзмерения + "Дт");
		ПоляГруппировки.Добавить(ИмяИзмерения + "Кт");
	КонецЦикла;
	
	Для Каждого ИмяРеквизита Из ПараметрыФормированияПроводок.Реквизиты Цикл
		ПоляГруппировки.Добавить(ИмяРеквизита);
	КонецЦикла;
	
	ПоляСуммирования = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыФормированияПроводок.БалансовыеРесурсы);
	
	Для Каждого ИмяРесурса Из ПараметрыФормированияПроводок.НебалансовыеРесурсы Цикл
		ПоляСуммирования.Добавить(ИмяРесурса + "Дт");
		ПоляСуммирования.Добавить(ИмяРесурса + "Кт");
	КонецЦикла;
	
	СтрокаГруппировки = СтрСоединить(ПоляГруппировки, ",");
	СтрокаСуммирования = СтрСоединить(ПоляСуммирования, ",");
	
	ТаблицаОтраженыВУчете = ТаблицаПроводок.Скопировать(ОтборСтрок);
	ТаблицаОтраженыВУчете.Свернуть(СтрокаГруппировки, СтрокаСуммирования);
	
	Если ТаблицаОтраженыВУчете.Количество() <> СтрокиОтраженыВУчете.Количество() Тогда
		Для Каждого СтрокаТаблицыПроводок Из СтрокиОтраженыВУчете Цикл
			ТаблицаПроводок.Удалить(СтрокаТаблицыПроводок);
		КонецЦикла;
		
		Для Каждого СвернутаяСтрока Из ТаблицаОтраженыВУчете Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаПроводок.Добавить(), СвернутаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьНезначащиеПроводки(ПараметрыФормированияПроводок, ТаблицаПроводок)

	Если НЕ ПараметрыФормированияПроводок.УдалитьНезначащиеПроводки Тогда
		Возврат;
	КонецЕсли;
	
	ПоляПроводки = Новый Структура();
	ПоляПроводки.Вставить("СчетДт", "СчетКт");
	
	Для НомерСубконто = 1 По ПараметрыФормированияПроводок.КоличествоСубконто Цикл
		ПоляПроводки.Вставить("СубконтоДт" + НомерСубконто, "СубконтоКт" + НомерСубконто);
	КонецЦикла;
	
	Для Каждого ИмяИзмерения Из ПараметрыФормированияПроводок.НебалансовыеИзмерения Цикл
		ПоляПроводки.Вставить(ИмяИзмерения + "Дт", ИмяИзмерения + "Кт");
	КонецЦикла;
	
	Для Каждого ИмяРесурса Из ПараметрыФормированияПроводок.НебалансовыеРесурсы Цикл
		ПоляПроводки.Вставить(ИмяРесурса + "Дт", ИмяРесурса + "Кт");
	КонецЦикла;
	
	УдаляемыеПроводки = Новый Массив();
	
	Для Каждого СтрокаТаблицыПроводок Из ТаблицаПроводок Цикл
		Если СтрокаТаблицыПроводок.Статус <> Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете Тогда
			Продолжить;
		КонецЕсли;
		
		ПоляДтРавныПолямКт = Истина;
		Для Каждого ИмяПоля Из ПоляПроводки Цикл
			Если СтрокаТаблицыПроводок[ИмяПоля.Ключ] <> СтрокаТаблицыПроводок[ИмяПоля.Значение] Тогда
				ПоляДтРавныПолямКт = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ПоляДтРавныПолямКт Тогда
			УдаляемыеПроводки.Добавить(СтрокаТаблицыПроводок);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицыПроводок Из УдаляемыеПроводки Цикл
		ТаблицаПроводок.Удалить(СтрокаТаблицыПроводок);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияДокументовКОтражениюВМеждународномУчете

Процедура ДобавитьУчетнуюПолитикуИСтатус(ТаблицаОтражения, Регистратор)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтражения.Период          КАК Период,
	|	ТаблицаОтражения.Организация     КАК Организация,
	|	ТаблицаОтражения.ДатаОтражения   КАК ДатаОтражения,
	|	ТаблицаОтражения.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ТаблицаДанные
	|ИЗ
	|	&ТаблицаОтражения КАК ТаблицаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанные.Период КАК Период,
	|	ТаблицаДанные.Организация,
	|	ТаблицаДанные.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДанные.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(УчетнаяПолитикаМФУ.Период) КАК Месяц
	|ПОМЕСТИТЬ ПериодыУчетнойПолитики
	|ИЗ
	|	ТаблицаДанные КАК ТаблицаДанные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ТаблицаДанные.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ТаблицаДанные.ДатаОтражения >= УчетнаяПолитикаМФУ.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанные.Период,
	|	ТаблицаДанные.Организация,
	|	ТаблицаДанные.ДатаОтражения,
	|	ТаблицаДанные.ХозяйственнаяОперация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыУчетнойПолитики.Период КАК Период,
	|	ПериодыУчетнойПолитики.Организация КАК Организация,
	|	ПериодыУчетнойПолитики.ДатаОтражения КАК ДатаОтражения,
	|	ПериодыУчетнойПолитики.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	УчетнаяПолитикаМФУ.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ПериодыУчетнойПолитики.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ПериодыУчетнойПолитики.Месяц = УчетнаяПолитикаМФУ.Период
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКОтражению.Период КАК Период,
	|	ТаблицаКОтражению.Организация КАК Организация,
	|	ТаблицаКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаКОтражению.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА ТаблицаКОтражению.Статус = 1 ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсходнаяТаблица.Период КАК Период,
	|		ИсходнаяТаблица.Организация КАК Организация,
	|		ИсходнаяТаблица.ДатаОтражения КАК ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика КАК УчетнаяПолитика,
	|		МИНИМУМ (ВЫБОР
	|			КОГДА ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную)
	|				ИЛИ ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ) КАК Статус
	|	ИЗ
	|		ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|		ПО
	|			ИсходнаяТаблица.Организация = ОтражениеДокументов.Организация
	|			И ОтражениеДокументов.Регистратор = &Регистратор
	|
	|	СГРУППИРОВАТЬ ПО
	|		ИсходнаяТаблица.Период,
	|		ИсходнаяТаблица.Организация,
	|		ИсходнаяТаблица.ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика
	|	) КАК ТаблицаКОтражению
	|");
	
	Запрос.УстановитьПараметр("ТаблицаОтражения", ТаблицаОтражения);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	ТаблицаОтражения = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПолучитьУчетнуюПолитикуИСтатус(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Документ        КАК Документ,
	|	ВложенныйЗапрос.Организация     КАК Организация,
	|	ВложенныйЗапрос.ДатаОтражения   КАК ДатаОтражения,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ТаблицаДанные
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаОтражения.Документ        КАК Документ,
	|		ТаблицаОтражения.Организация     КАК Организация,
	|		ТаблицаОтражения.ДатаОтражения   КАК ДатаОтражения,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаТекущейРегистрации КАК ТаблицаОтражения
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаОтражения.Документ        КАК Документ,
	|		ТаблицаОтражения.Организация     КАК Организация,
	|		ТаблицаОтражения.ДатаОтражения   КАК ДатаОтражения,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаВыборочнойРегистрации КАК ТаблицаОтражения
	|) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанные.Организация КАК Организация,
	|	ТаблицаДанные.ДатаОтражения КАК ДатаОтражения,
	|	МАКСИМУМ(УчетнаяПолитикаМФУ.Период) КАК Месяц
	|ПОМЕСТИТЬ ПериодыУчетнойПолитики
	|ИЗ
	|	ТаблицаДанные КАК ТаблицаДанные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ТаблицаДанные.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ТаблицаДанные.ДатаОтражения >= УчетнаяПолитикаМФУ.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанные.Организация,
	|	ТаблицаДанные.ДатаОтражения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанные.Документ КАК Документ,
	|	ТаблицаДанные.Организация КАК Организация,
	|	ТаблицаДанные.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДанные.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	УчетнаяПолитикаМФУ.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	ТаблицаДанные КАК ТаблицаДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|	ПО ТаблицаДанные.Организация = ПериодыУчетнойПолитики.Организация
	|		И ТаблицаДанные.ДатаОтражения = ПериодыУчетнойПолитики.ДатаОтражения
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ПериодыУчетнойПолитики.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ПериодыУчетнойПолитики.Месяц = УчетнаяПолитикаМФУ.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиХозяйственныхОпераций.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПравилаОтражения.УчетнаяПолитика КАК УчетнаяПолитика,
	|	МАКСИМУМ(ШаблоныПроводок.РучноеУточнениеПроводки) КАК РучноеУточнениеПроводки
	|ПОМЕСТИТЬ ПравилаОтражения
	|ИЗ
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|	ПО
	|		ШаблоныПроводок.Операция = НастройкиХозяйственныхОпераций.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|	ПО
	|		ШаблоныПроводок.Ссылка = ПравилаОтражения.ШаблонПроводки
	|СГРУППИРОВАТЬ ПО
	|	НастройкиХозяйственныхОпераций.ХозяйственнаяОперация,
	|	ПравилаОтражения.УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКОтражению.Документ КАК Документ,
	|	ТаблицаКОтражению.Организация КАК Организация,
	|	ТаблицаКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаКОтражению.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА ТаблицаКОтражению.Статус = 1 ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ УчетнаяПолитикаИСтатус
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсходнаяТаблица.Документ КАК Документ,
	|		ИсходнаяТаблица.Организация КАК Организация,
	|		ИсходнаяТаблица.ДатаОтражения КАК ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика КАК УчетнаяПолитика,
	|		МИНИМУМ (ВЫБОР
	|			КОГДА ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную)
	|				ИЛИ ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|				ИЛИ ПравилаОтражения.РучноеУточнениеПроводки
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ) КАК Статус
	|	ИЗ
	|		ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ПравилаОтражения КАК ПравилаОтражения
	|		ПО
	|			ИсходнаяТаблица.УчетнаяПолитика = ПравилаОтражения.УчетнаяПолитика
	|			И ИсходнаяТаблица.ХозяйственнаяОперация = ПравилаОтражения.ХозяйственнаяОперация
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|		ПО
	|			ИсходнаяТаблица.Организация = ОтражениеДокументов.Организация
	|			И ИсходнаяТаблица.Документ = ОтражениеДокументов.Регистратор
	|
	|	СГРУППИРОВАТЬ ПО
	|		ИсходнаяТаблица.Документ,
	|		ИсходнаяТаблица.Организация,
	|		ИсходнаяТаблица.ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика
	|	) КАК ТаблицаКОтражению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДанные;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыУчетнойПолитики;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсходнаяТаблица;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПравилаОтражения;
	|");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ИнициализироватьМенеджерДляВозвратаКОтражению(ДокументыКОтражению)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если ТипЗнч(ДокументыКОтражению) = Тип("ТаблицаЗначений") Тогда
		Если ДокументыКОтражению.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Период КАК Период,
		|	ДокументыКОтражению.Документ КАК Документ,
		|	ДокументыКОтражению.Организация КАК Организация,
		|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Документ,
		|	Организация,
		|	ДатаОтражения
		|;
		|/////////////////////////////////////////////////
		|");
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
	Иначе
		МенеджерВременныхТаблиц = ДокументыКОтражению;
	КонецЕсли;
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ДанныеДляВозвратаКОтражениюВУчете(ДокументыКОтражению, Отбор)

	#Область ТекстЗапросаТаблицаОтраженияДокументовВМеждународномУчете
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	МИНИМУМ(ДокументыКОтражению.Период) КАК Период,
	|	ДокументыКОтражению.Документ КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) КАК ДатаОтражения
	|ПОМЕСТИТЬ ЗаписиКОтражению
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Документ) <> ТИП(Документ.КорректировкаРегистров)
	|	И ДокументыКОтражению.Период >= &Период
	|	И (ДокументыКОтражению.Документ > &Регистратор ИЛИ &Регистратор = Неопределено)
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтражению.Документ,
	|	ДокументыКОтражению.Организация,
	|	НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ)
	|
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаписиКОтражению.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТолькоДокументыКОтражению
	|ИЗ
	|	ЗаписиКОтражению КАК ЗаписиКОтражению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Статус,
	|	ДанныеРегистра.УчетнаяПолитика,
	|	ДанныеРегистра.Комментарий
	|ПОМЕСТИТЬ ВсеЗаписиРегистратора
	|ИЗ
	|	ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ПериодыРегистратора
	|ИЗ
	|	ВсеЗаписиРегистратора КАК ДанныеРегистра
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Статус КАК Статус,
	|	ДанныеРегистра.УчетнаяПолитика,
	|	ДанныеРегистра.Комментарий
	|ПОМЕСТИТЬ ВременнаяТаблицаРегистра
	|ИЗ
	|	ЗаписиКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|		И ДокументыКОтражению.Организация = ДанныеРегистра.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаОтражения, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Если регистрации не было, то надо зарегистрировать.
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете) КАК Статус,
	|	НЕОПРЕДЕЛЕНО КАК УчетнаяПолитика,
	|	"""" КАК Комментарий
	|ИЗ
	|	ЗаписиКОтражению КАК ДокументыКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|		И ДокументыКОтражению.Организация = ДанныеРегистра.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаОтражения, ДЕНЬ)
	|ГДЕ
	|	ДанныеРегистра.Регистратор ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОтражению.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ МесяцОтражения
	|ИЗ
	|	ЗаписиКОтражению КАК ДокументыКОтражению
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаОтражения, МЕСЯЦ) КАК Период
	|ИЗ
	|	ВременнаяТаблицаРегистра КАК ДанныеРегистра
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцОтражения.Организация КАК Организация,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК Период,
	|	МесяцОтражения.Период КАК ПериодРегистрации
	|ПОМЕСТИТЬ ПериодыУчетнойПолитики
	|ИЗ
	|	МесяцОтражения КАК МесяцОтражения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|	ПО МесяцОтражения.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И МесяцОтражения.Период >= УчетнаяПолитикаОрганизаций.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцОтражения.Организация,
	|	МесяцОтражения.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	ПериодыУчетнойПолитики.ПериодРегистрации КАК Период,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК Политика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыУчетнойПолитики
	|	ПО УчетнаяПолитикаОрганизаций.Организация = ПериодыУчетнойПолитики.Организация
	|		И УчетнаяПолитикаОрганизаций.Период = ПериодыУчетнойПолитики.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПериодыРегистратора.Период, ОтражениеДокументовВМеждународномУчете.Период) КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.Политика, ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика) КАК УчетнаяПолитика,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий
	|ПОМЕСТИТЬ ТаблицаРегистра
	|ИЗ
	|	ВременнаяТаблицаРегистра КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО ОтражениеДокументовВМеждународномУчете.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(ОтражениеДокументовВМеждународномУчете.ДатаОтражения, МЕСЯЦ) = УчетнаяПолитикаОрганизаций.Период
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПериодыРегистратора КАК ПериодыРегистратора
	|	ПО ОтражениеДокументовВМеждународномУчете.Регистратор = ПериодыРегистратора.Регистратор
	|ГДЕ
	|	НЕ (УчетнаяПолитикаОрганизаций.Политика ЕСТЬ NULL
	|		ИЛИ ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика = ЗНАЧЕНИЕ(Справочник.УчетныеПолитики.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МесяцОтражения;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыУчетнойПолитики;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВременнаяТаблицаРегистра;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыРегистратора;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус КАК Статус,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ втОстальныеЗаписиРегистратора
	|ИЗ
	|	ВсеЗаписиРегистратора КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗаписиКОтражению КАК ДокументыКОтражению
	|	ПО ДокументыКОтражению.Регистратор = ОтражениеДокументовВМеждународномУчете.Регистратор
	|		И ДокументыКОтражению.Организация = ОтражениеДокументовВМеждународномУчете.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = ОтражениеДокументовВМеждународномУчете.ДатаОтражения
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО ОтражениеДокументовВМеждународномУчете.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(ОтражениеДокументовВМеждународномУчете.ДатаОтражения, МЕСЯЦ) = УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	ДокументыКОтражению.Регистратор ЕСТЬ NULL
	|	И НЕ (ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика ЕСТЬ NULL
	|		ИЛИ ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика = ЗНАЧЕНИЕ(Справочник.УчетныеПолитики.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период         КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор    КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация    КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения  КАК ДатаОтражения,
	|	ВЫБОР 
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВМеждународномУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ОтражениеДокументовВМеждународномУчете.Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВМеждународномУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ОтражениеДокументовВМеждународномУчете.Комментарий
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВМеждународномУчете.Комментарий
	|	КОНЕЦ КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НовыеДанныеРегистра
	|ИЗ
	|	ТаблицаРегистра КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ЗаписиКОтражению КАК ДокументыКОтражению
	|	ПО
	|		НАЧАЛОПЕРИОДА(ОтражениеДокументовВМеждународномУчете.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДокументыКОтражению.Период, ДЕНЬ)
	|		И НАЧАЛОПЕРИОДА(ОтражениеДокументовВМеждународномУчете.ДатаОтражения, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ)
	|		И ОтражениеДокументовВМеждународномУчете.Организация = ДокументыКОтражению.Организация
	|		И ОтражениеДокументовВМеждународномУчете.Регистратор = ДокументыКОтражению.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус КАК Статус,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	втОстальныеЗаписиРегистратора КАК ОтражениеДокументовВМеждународномУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеДанныеРегистра.Период КАК Период,
	|	НовыеДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	НовыеДанныеРегистра КАК НовыеДанныеРегистра
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанныеРегистра.Период КАК Период,
	|	НовыеДанныеРегистра.Регистратор КАК Регистратор,
	|	НовыеДанныеРегистра.Организация КАК Организация,
	|	НовыеДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	НовыеДанныеРегистра.Статус КАК Статус,
	|	НовыеДанныеРегистра.Комментарий КАК Комментарий,
	|	НовыеДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	НовыеДанныеРегистра КАК НовыеДанныеРегистра
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЗаписиКОтражению;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТолькоДокументыКОтражению;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УчетнаяПолитикаОрганизаций;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеДанныеРегистра;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРегистра;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстальныеЗаписиРегистратора;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеЗаписиРегистратора;
	|";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДокументыКОтражению;
	Запрос.УстановитьПараметр("Период", Отбор.Период);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();  
	
	ДанныеРегистра = РезультатЗапроса[16].Выгрузить(); // ТаблицаЗначений - 
	ДанныеРегистра.Индексы.Добавить("Период,Регистратор");
	
	Результат = Новый Структура("Регистраторы, ДанныеРегистра");
	Результат.Регистраторы = РезультатЗапроса[15].Выбрать();
	Результат.ДанныеРегистра = ДанныеРегистра;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область УниверсальныеПроверкиЭтаповЗакрытияМесяца

// Проверяет использование международного учета.
//
// Параметры:
//	ПараметрыОбработчика - Структура - параметры обработчика события этапа.
//	
// Возвращаемое значение:
//	Булево - признак использования международного учета.
//
Функция ПроверитьИспользованиеМеждународногоУчета(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru='Международный финансовый учет не ведется.';uk='Міжнародний фінансовий облік не ведеться.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет наличие учетной политики международного учета хотя бы у одной рассчитываемой организации в рассчитываемом периоде.
//
// Параметры:
//	ПараметрыОбработчика 			  - Структура - параметры обработчика события этапа
//	ТолькоФормироватьВременнуюТаблицу - Булево - если Истина, то не нужно изменять состояние этапа.
//	
// Возвращаемое значение:
//	Булево - признак наличие учетной политики.
//
Функция ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика, ТолькоФормироватьВременнуюТаблицу = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ВТОрганизации");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(&КонецПериода, Организация В (&МассивОрганизаций)) КАК Т";
	
	Запрос.Выполнить();
	
	Если ЗакрытиеМесяцаСервер.РазмерВременнойТаблицы(Запрос, "ВТОрганизации", ПараметрыОбработчика) = 0 Тогда
		
		Если НЕ ТолькоФормироватьВременнуюТаблицу Тогда
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
				ПараметрыОбработчика,
				НСтр("ru='Не введены учетные политики международного учета.';uk='Не введені облікові політики міжнародного обліку.'",ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет наличие документов "Регламентная операция (международный учет)".
//
// Параметры:
//	ПараметрыОбработчика 		- Структура - параметры обработчика события этапа
//	ТипОперации 		 		- ПеречислениеСсылка.ТипыРегламентныхОперацийМеждународныйУчет - тип операции документа
//									если не указан, то анализируется типы операций из свойства ТипыРегламентныхОпераций данного этапа
//	ИмяТаблицыОтбораОрганизаций - Строка - имя временной таблицы для отбора документов по организациям
//									организации в указанной таблице должны находиться в поле с именем "Ссылка"
//	НаличиеДокументаОбязательно - Булево - признак обязательного наличия документа.
//	
// Возвращаемое значение:
//	Булево - признак наличия документов.
//
Функция ПроверитьНаличиеДокументаРегламентнаяОперацияМУ(ПараметрыОбработчика, Знач ТипОперации = Неопределено,
			ИмяТаблицыОтбораОрганизаций = "ВТОрганизации", НаличиеДокументаОбязательно = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Если НЕ ЗначениеЗаполнено(ИмяТаблицыОтбораОрганизаций) Тогда
		
		// Проверка по всем рассчитываемым организациям.
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ПОМЕСТИТЬ ВТОрганизации
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка В(&МассивОрганизаций)";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операции.Ссылка    КАК ТипОперации,
	|	Организации.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОперации
	|ИЗ
	|	Перечисление.ТипыРегламентныхОперацийМеждународныйУчет КАК Операции
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТОрганизации КАК Организации
	|		ПО ИСТИНА
	|ГДЕ
	|	Операции.Ссылка В (&ТипыОпераций)
	|	И НЕ (Организации.Ссылка ЕСТЬ NULL)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.ТипОперации КАК ТипОперации,
	|	Операции.Организация КАК Организация,
	|	КОЛИЧЕСТВО(РегламентнаяОперация.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	ВТОперации КАК Операции
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегламентнаяОперацияМеждународныйУчет КАК РегламентнаяОперация
	|		ПО (РегламентнаяОперация.Организация = Операции.Организация)
	|			И (РегламентнаяОперация.ТипОперации = Операции.ТипОперации)
	|			И (РегламентнаяОперация.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
	|			И (РегламентнаяОперация.Проведен)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операции.ТипОперации,
	|	Операции.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.ТипОперации,
	|	Операции.Организация";
	
	Если ЗначениеЗаполнено(ИмяТаблицыОтбораОрганизаций) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОрганизации", ИмяТаблицыОтбораОрганизаций);
	КонецЕсли;
	
	ЕстьОшибки    = Ложь;
	ЕстьДокументы = Ложь;
	Период 		  = РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации);
	ТипыОпераций  = ?(ЗначениеЗаполнено(ТипОперации),
		ОбщегоНазначенияУТКлиентСервер.Массив(ТипОперации),
		ПараметрыОбработчика.ДанныеЭтапа.ТипыРегламентныхОпераций);
	
	Запрос.УстановитьПараметр("ТипыОпераций", ТипыОпераций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, Выборка.Количество());
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоДокументов > 1 Тогда
			
			ЕстьОшибки = Истина;
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
				ПараметрыОбработчика,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='По организации ""%1"" за период %2 сформировано несколько документов регламентных операций ""%3"".';uk='По організації ""%1"" за період %2 сформовано кілька документів регламентних операцій ""%3"".'",ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Организация,
					Период,
					СокрЛП(Выборка.ТипОперации)));
					
			Продолжить;
			
		ИначеЕсли Выборка.КоличествоДокументов = 0 Тогда
			
			Если НЕ НаличиеДокументаОбязательно Тогда
				// Нет данных к расчету и нет регламентного документа - расчет не требуется.
			Иначе
				// Есть данные для расчета, но нет регламентного документа - расчет не выполнен.
				ЕстьОшибки = Истина;
				ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
					ПараметрыОбработчика,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='По организации ""%1"" за период %2 не сформирована регламентная операция ""%3"".';uk='По організації ""%1"" за період %2 не сформована регламентна операція ""%3"".'",ОбщегоНазначения.КодОсновногоЯзыка()),
						Выборка.Организация,
						Период,
						СокрЛП(Выборка.ТипОперации)));
			КонецЕсли;
			
		Иначе // есть один документ
			ЕстьДокументы = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ НаличиеДокументаОбязательно И НЕ ЕстьОшибки И НЕ ЕстьДокументы Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='За период %1 нет данных для расчета и отсутствует документ ""Регламентная операция (международный учет)"".';uk='За період %1 немає даних для розрахунку і відсутній документ ""Регламентна операція (міжнародний облік)"".'",ОбщегоНазначения.КодОсновногоЯзыка()),
				Период));
		
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос,
		"ВТОперации" + ?(НЕ ЗначениеЗаполнено(ИмяТаблицыОтбораОрганизаций), ", ВТОрганизации", ""));
	
	Возврат НЕ ЕстьОшибки;
	
КонецФункции

#КонецОбласти

#Область ФормированиеРегламентныхОпераций

// Формирует документы "Регламентная операция (международный учет)".
//
// Параметры:
//	ПараметрыОбработчика 	- Структура - параметры обработчика события этапа
//	ТипОперации 			- ПеречислениеСсылка.ТипыРегламентныхОперацийМеждународныйУчет - тип операции документа
//								если не указан, то создаются документы со всеми типами операций из свойства ТипыРегламентныхОпераций данного этапа
//	УничтожатьВТОрганизации - Булево - признак необходимости удаления временной таблицы ВТОрганизации.
//	
Процедура СформироватьДокументРегламентнаяОперацияМУ(ПараметрыОбработчика, Знач ТипОперации = Неопределено, УничтожатьВТОрганизации = Истина) Экспорт
	
	Если НЕ ПроверитьНаличиеУчетнойПолитикиМеждународногоУчета(ПараметрыОбработчика, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА РегламентнаяОперация.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ РегламентнаяОперация.Ссылка
	|	КОНЕЦ КАК РегламентныйДокумент
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	ВТОрганизации КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегламентнаяОперацияМеждународныйУчет КАК РегламентнаяОперация
	|		ПО (РегламентнаяОперация.Организация = Т.Ссылка)
	|			И (РегламентнаяОперация.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
	|			И (РегламентнаяОперация.ТипОперации = &ТипОперации)
	|			И (РегламентнаяОперация.Проведен)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегламентныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	МАКСИМУМ(Т.РегламентныйДокумент) КАК РегламентныйДокумент
	|ПОМЕСТИТЬ ВТАктуальныеДокументы
	|ИЗ
	|	ВТДокументы КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегламентныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.РегламентныйДокумент,
	|	ВЫБОР
	|		КОГДА Т.РегламентныйДокумент <> Т2.РегламентныйДокумент
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удалить
	|ИЗ
	|	ВТДокументы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАктуальныеДокументы КАК Т2
	|		ПО Т.Организация = Т2.Организация
	|			И Т.РегламентныйДокумент = Т2.РегламентныйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	ТипыОпераций = ?(ЗначениеЗаполнено(ТипОперации),
		ОбщегоНазначенияУТКлиентСервер.Массив(ТипОперации),
		ПараметрыОбработчика.ДанныеЭтапа.ТипыРегламентныхОпераций);
	
	Для Каждого ТипОперации Из ТипыОпераций Цикл
		
		Запрос.УстановитьПараметр("ТипОперации", ТипОперации);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.РегламентныйДокумент) Тогда
				РегламентныйДокумент = Документы.РегламентнаяОперацияМеждународныйУчет.СоздатьДокумент();
			Иначе
				РегламентныйДокумент = Выборка.РегламентныйДокумент.ПолучитьОбъект();
			КонецЕсли;
			
			Попытка
			
				Если Выборка.Удалить Тогда
					РегламентныйДокумент.УстановитьПометкуУдаления(Истина);
					Продолжить;
				ИначеЕсли РегламентныйДокумент.Проведен Тогда
					РегламентныйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;
				
				РегламентныйДокумент.Дата 	     	= КонецМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации);
				РегламентныйДокумент.Организация 	= Выборка.Организация;
				РегламентныйДокумент.ТипОперации   	= ТипОперации;
				РегламентныйДокумент.Ответственный 	= Пользователи.ТекущийПользователь();
				
				РегламентныйДокумент.Записать(РежимЗаписиДокумента.Проведение);
				
			Исключение
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Формирование документа ""Регламентная операция (международный учет)"" по организации ""%1"" за период %2 завершилось с ошибкой:
|%3'
|;uk='Формування документа ""Регламентна операція (міжнародний облік)"" по організації ""%1"" за період %2 завершилося з помилкою: 
|%3'",ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Организация,	
					РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
					ПараметрыОбработчика,
					ТекстОшибки,
					Выборка.Организация,
					,
					,
					?(РегламентныйДокумент.ЭтоНовый(), Неопределено, РегламентныйДокумент.Ссылка));
				
			КонецПопытки;
			
		КонецЦикла;
		
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ВТДокументы, ВТАктуальныеДокументы");
		
	КонецЦикла;
	
	Если УничтожатьВТОрганизации Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ВТОрганизации");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
