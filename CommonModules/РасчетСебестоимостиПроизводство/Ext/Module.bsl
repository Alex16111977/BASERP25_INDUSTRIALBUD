
// Вызываются процедуры и функции общих модулей:
//	- РасчетСебестоимостиПрикладныеАлгоритмы.

#Область ПрограммныйИнтерфейс

#Область Инициализация

// Перед началом расшифровки заполняет все необходимые параметры и инициализирует все структуры данных, используемые алгоритмом расчета.
//
// Параметры:
//	Дата - Дата - период документа распределения
//	МассивОрганизаций - Массив - Организации
//	Регистратор - ДокументСсылка - документ распределения расходов
//
// Возвращаемое значение:
//	Структура - параметры, используемые при расчете.
//
Функция ИнициализироватьПараметрыРасшифровкиРаспределения(Дата, МассивОрганизаций, Регистратор) Экспорт
	
	ПараметрыРасчета = Неопределено;
	ПараметрыОтладки = Неопределено;
	
	ПараметрыЗапуска = Новый Структура(
		"Дата, МассивОрганизаций, РасшифровкаРаспределения",
		Дата, МассивОрганизаций, Истина);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыОтладки(ПараметрыОтладки, ПараметрыЗапуска);
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыРасчетаПартий(ПараметрыЗапуска, ПараметрыРасчета, ПараметрыОтладки);
	
	ПараметрыРасчета.Вставить("Регистратор", Регистратор);
	
	Возврат ПараметрыРасчета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапов_1_2_3_4_5

// Вт Этапы, ПартииПроизводстваПоЭтапам, ПартииПроизводстваСводно, РаспределенныеОтчеты,
// втДолиПоПереработке, втПродукция, втДолиИтог, Продукция, ДолиВыпуска.
Функция ТекстПартииПроизводства() Экспорт
	
	ТекстЗапроса = "
	//++ НЕ УТКА
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Этап,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	Реквизиты.ПартияПроизводства КАК ПартияПроизводства,
	|	Реквизиты.ТипПроизводственногоПроцесса
	|ПОМЕСТИТЬ ЭтапыТекущегоПериода
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Организация В(&МассивОрганизаций)
	|	И НЕ Реквизиты.ПроизводствоНаСтороне
	|	И Реквизиты.Проведен
	|	И Реквизиты.ФактическоеНачалоЭтапа <= &КонецПериода
	|	И (Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
	|		ИЛИ Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|			И Реквизиты.ФактическоеОкончаниеЭтапа >= &НачалоПериода)
	|;
	//-- НЕ УТКА
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|	ДД.ДокументПартии				КАК ДокументПартии,
	|	ДД.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	ДД.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	ДД.Этап							КАК Этап,
	|	ДД.Организация					КАК Организация,
	|	ДД.Подразделение				КАК Подразделение,
	|	ДД.Номенклатура					КАК Номенклатура,
	|	ДД.Назначение					КАК Назначение,
    |	ДД.НалоговоеНазначение			КАК НалоговоеНазначение,
	|	ДД.НаправлениеДеятельности		КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ПартииПроизводстваПоЭтапам
	|ИЗ
	|	(
	// производство без заказа 2.2
	|	ВЫБРАТЬ
	|		СпрПартииПроизводства.Ссылка				КАК ПартияПроизводства,
	|		СпрПартииПроизводства.Документ				КАК ДокументПартии,
	|		СпрПартииПроизводства.Код					КАК НомерГруппыЗатрат,
	|		ВЫБОР
	|			КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ										КАК АналитикаФинансовогоУчета,
	|		РеквизитыДокумента.Ссылка					КАК Этап,
	|		РеквизитыДокумента.Организация				КАК Организация,
	|		РеквизитыДокумента.Подразделение			КАК Подразделение,
	|		СпрПартииПроизводства.ОсновноеИзделиеНоменклатура КАК Номенклатура,
	|		СпрПартииПроизводства.Назначение			КАК Назначение,
    |		СпрПартииПроизводства.НалоговоеНазначение	КАК НалоговоеНазначение,
	|		ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))	КАК НаправлениеДеятельности
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТоваров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК РеквизитыДокумента
	|		ПО РеквизитыДокумента.Ссылка = ТаблицаТоваров.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТоваров.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТоваров.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		РеквизитыДокумента.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И РеквизитыДокумента.Организация В(&МассивОрганизаций)
	|		И РеквизитыДокумента.Проведен
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		АктивныеПартии.ПартияПроизводства 			КАК ПартияПроизводства,
	|		РеквизитыПП.Документ						КАК ДокументПартии,
	|		РеквизитыПП.Код								КАК НомерГруппыЗатрат,
	|		ВЫБОР
	|			КОГДА НЕ РеквизитыПП.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				ТОГДА РеквизитыПП.ГруппаПродукции
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ										КАК АналитикаФинансовогоУчета,
	|		ЕСТЬNULL(Этапы.Этап,
	|			ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)) КАК Этап,
	|		ЗаказНаПроизводство.Организация				КАК Организация,
	|		ЕСТЬNULL(Этапы.Подразделение,
	|			ЗаказНаПроизводство.Подразделение) 		КАК Подразделение,
	|		РеквизитыПП.ОсновноеИзделиеНоменклатура 	КАК Номенклатура,
	|		РеквизитыПП.Назначение						КАК Назначение,
    |		РеквизитыПП.НалоговоеНазначение				КАК НалоговоеНазначение,
	|		ЕСТЬNULL(РеквизитыПП.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))	КАК НаправлениеДеятельности
	|	ИЗ
	|		АктивныеПартии КАК АктивныеПартии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство
	|			ПО АктивныеПартии.ПартияПроизводства = ЗаказНаПроизводство.ПартияПроизводства
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК РеквизитыПП
	|			ПО РеквизитыПП.Ссылка = АктивныеПартии.ПартияПроизводства
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ЭтапыТекущегоПериода КАК Этапы
	|			ПО АктивныеПартии.ПартияПроизводства = Этапы.ПартияПроизводства
	|				И Этапы.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.БезСпецификаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// производство по заказам 2.2
	|	ВЫБРАТЬ
	|		СпрПартииПроизводства.Ссылка				КАК ПартияПроизводства,
	|		СпрПартииПроизводства.Документ				КАК ДокументПартии,
	|		СпрПартииПроизводства.Код					КАК НомерГруппыЗатрат,
	|		ВЫБОР
	|			КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ										КАК АналитикаФинансовогоУчета,
	|		ЭтапыТекущегоПериода.Этап					КАК Этап,
	|		ЭтапыТекущегоПериода.Организация			КАК Организация,
	|		ЭтапыТекущегоПериода.Подразделение			КАК Подразделение,
	|		СпрПартииПроизводства.ОсновноеИзделиеНоменклатура КАК Номенклатура,
	|		СпрПартииПроизводства.Назначение			КАК Назначение,
    |		СпрПартииПроизводства.НалоговоеНазначение	КАК НалоговоеНазначение,
	|		ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))	КАК НаправлениеДеятельности
	|	ИЗ
	|		ЭтапыТекущегоПериода КАК ЭтапыТекущегоПериода
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ЭтапыТекущегоПериода.ПартияПроизводства
	|	ГДЕ
	|		НЕ ЭтапыТекущегоПериода.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.БезСпецификаций)
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// отчеты переработчиков без этапов
	|	ВЫБРАТЬ
	|		СпрПартииПроизводства.Ссылка				КАК ПартияПроизводства,
	|		СпрПартииПроизводства.Документ				КАК ДокументПартии,
	|		СпрПартииПроизводства.Код					КАК НомерГруппыЗатрат,
	|		ВЫБОР
	|			КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ										КАК АналитикаФинансовогоУчета,
	|		РеквизитыОтчета.Ссылка						КАК Этап,
	|		РеквизитыОтчета.Организация					КАК Организация,
	|		РеквизитыОтчета.Подразделение				КАК Подразделение,
	|		СпрПартииПроизводства.ОсновноеИзделиеНоменклатура КАК Номенклатура,
	|		СпрПартииПроизводства.Назначение			КАК Назначение,
    |		СпрПартииПроизводства.НалоговоеНазначение	КАК НалоговоеНазначение,
	|		ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|		ПО РеквизитыОтчета.Ссылка = ТаблицаТоваров.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТоваров.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТоваров.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		РеквизитыОтчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И РеквизитыОтчета.Организация В(&МассивОрганизаций)
	|		И РеквизитыОтчета.Проведен
	|		И НЕ РеквизитыОтчета.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//++ НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// отчеты переработчиков по этапам
	|	ВЫБРАТЬ
	|		СпрПартииПроизводства.Ссылка				КАК ПартияПроизводства,
	|		СпрПартииПроизводства.Документ				КАК ДокументПартии,
	|		СпрПартииПроизводства.Код					КАК НомерГруппыЗатрат,
	|		ВЫБОР
	|			КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ										КАК АналитикаФинансовогоУчета,
	|		РеквизитыЭтапа.Ссылка						КАК Этап,
	|		РеквизитыОтчета.Организация					КАК Организация,
	|		РеквизитыОтчета.Подразделение				КАК Подразделение,
	|		СпрПартииПроизводства.ОсновноеИзделиеНоменклатура КАК Номенклатура,
	|		СпрПартииПроизводства.Назначение			КАК Назначение,
    |		СпрПартииПроизводства.НалоговоеНазначение	КАК НалоговоеНазначение,
	|		ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|				ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|		ПО РеквизитыОтчета.Ссылка = ТаблицаТоваров.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|		ПО РеквизитыЭтапа.Ссылка = ТаблицаТоваров.ЭтапПроизводства
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = РеквизитыЭтапа.ПартияПроизводства
	|
	|	ГДЕ
	|		РеквизитыОтчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И РеквизитыОтчета.Организация В(&МассивОрганизаций)
	|		И РеквизитыОтчета.Проведен
	|		И РеквизитыОтчета.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//-- НЕ УТКА
	|	) КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ДокументПартии,
	|	ДД.НомерГруппыЗатрат,
	|	ДД.АналитикаФинансовогоУчета,
	|	ДД.Этап,
	|	ДД.Организация,
	|	ДД.Подразделение,
	|	ДД.Номенклатура,
	|	ДД.Назначение,
    |	ДД.НалоговоеНазначение,
	|	ДД.НаправлениеДеятельности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|	ДД.ДокументПартии				КАК ДокументПартии,
	|	ДД.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	ДД.НаправлениеДеятельности		КАК НаправлениеДеятельности,
	|	ДД.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	ДД.Организация					КАК Организация,
	|	ДД.Номенклатура					КАК Номенклатура,
	|	ДД.Назначение					КАК Назначение,
    |	ДД.НалоговоеНазначение			КАК НалоговоеНазначение
	|ПОМЕСТИТЬ ПартииПроизводстваСводно
	|ИЗ
	|	ПартииПроизводстваПоЭтапам КАК ДД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Обслуживание переработки на стороне.
	// Отчеты переработчиков, материалы которых не подлежат распределению.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыОтчета.Ссылка			КАК ОтчетПереработчика,
	|	СпрПартииПроизводства.Ссылка	КАК ПартияПроизводства
	|ПОМЕСТИТЬ РаспределенныеОтчеты
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|	ПО РеквизитыОтчета.Ссылка = ТаблицаТоваров.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|	ПО СпрПартииПроизводства.Документ = ТаблицаТоваров.Ссылка
	|	И СпрПартииПроизводства.Код = ТаблицаТоваров.НомерГруппыЗатрат
	|	И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|ГДЕ
	|	РеквизитыОтчета.Проведен
	|	И РеквизитыОтчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеквизитыОтчета.Организация В(&МассивОрганизаций)
	|	И НЕ РеквизитыОтчета.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыОтчета.Ссылка				КАК ОтчетПереработчика,
	|	РеквизитыЭтапа.ПартияПроизводства	КАК ПартияПроизводства
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|	ПО РеквизитыОтчета.Ссылка = ТаблицаТоваров.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|	ПО РеквизитыЭтапа.Ссылка = ТаблицаТоваров.ЭтапПроизводства
	|
	|ГДЕ
	|	РеквизитыОтчета.Проведен
	|	И РеквизитыОтчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеквизитыОтчета.Организация В(&МассивОрганизаций)
	|	И РеквизитыОтчета.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//-- НЕ УТКА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОтчетПереработчика,
	|	ПартияПроизводства
	|;";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстОстаткиПартийМатериаловВНЗП() Экспорт
	
	Возврат "
	|ВЫБРАТЬ
	|	ДД.Организация						КАК Организация,
	|	ДД.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ДД.ПартияПроизводства				КАК ПартияПроизводства,
	|	ДД.ВидЗапасов						КАК ВидЗапасов,
	|	ДД.РазделУчета						КАК РазделУчета,
	|	ДД.АналитикаУчетаПартий				КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета		КАК АналитикаФинансовогоУчета,
    |	ДД.НалоговоеНазначение				КАК НалоговоеНазначение,
	|	СУММА(ДД.Количество)				КАК Количество
	|ПОМЕСТИТЬ ОстаткиМатериалов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДД.Организация					КАК Организация,
	|		ДД.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ДД.Партия						КАК ПартияПроизводства,
	|		ДД.ВидЗапасов					КАК ВидЗапасов,
	|		ДД.РазделУчета					КАК РазделУчета,
	|		ДД.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|		ДД.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
    |		ДД.НалоговоеНазначение			КАК НалоговоеНазначение,
	|		ДД.КоличествоОстаток			КАК Количество
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров.Остатки(
	|			&ГраницаКонецПредыдущегоПериода,
	|			Организация В (&МассивОрганизаций)
	|			И Партия В (ВЫБРАТЬ
	|							Т.ПартияПроизводства
	|						ИЗ
	|							ПартииПроизводстваСводно КАК Т)) КАК ДД
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Организация					КАК Организация,
	|		ДД.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ДД.Партия						КАК ПартияПроизводства,
	|		ДД.ВидЗапасов					КАК ВидЗапасов,
	|		ДД.РазделУчета					КАК РазделУчета,
	|		ДД.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|		ДД.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
    |		ДД.НалоговоеНазначение			КАК НалоговоеНазначение,
	|		ДД.Количество					КАК Количество
	|	ИЗ
	|		ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК Партии
	|		ПО ДД.Партия = Партии.ПартияПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеОтчеты КАК РаспределенныеОтчеты
	|		ПО ДД.Регистратор = РаспределенныеОтчеты.ОтчетПереработчика
	|		И ДД.Партия = РаспределенныеОтчеты.ПартияПроизводства
	|
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ДД.Организация В (&МассивОрганизаций)
	|		И ДД.СлужебноеВидДвиженияПриход
	|		И НЕ ДД.РасчетПартий
			// Отчеты переработчиков по выпускающим этапам сами списывают материалы на продукцию.
	|		И РаспределенныеОтчеты.ОтчетПереработчика ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Организация					КАК Организация,
	|		ДД.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ДД.Партия						КАК ПартияПроизводства,
	|		ДД.ВидЗапасов					КАК ВидЗапасов,
	|		ДД.РазделУчета					КАК РазделУчета,
	|		ДД.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|		ДД.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
    |		ДД.НалоговоеНазначение			КАК НалоговоеНазначение,
	|		ДД.Количество					КАК Количество
	|	ИЗ
	|		ВтПроизводственныеЗатратыСебестоимостьТоваров КАК ДД
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК Партии
	|		ПО ДД.Партия = Партии.ПартияПроизводства
	|
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ДД.Организация В (&МассивОрганизаций)
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|) КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Организация,
	|	ДД.АналитикаУчетаНоменклатуры,
	|	ДД.ПартияПроизводства,
	|	ДД.ВидЗапасов,
	|	ДД.РазделУчета,
	|	ДД.АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета,
    |	ДД.НалоговоеНазначение
	|;	
	|	
	|ВЫБРАТЬ
	|	Остатки.ПартияПроизводства			КАК ПартияПроизводства,
	|	Остатки.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ДД.Партия							КАК ПартияМатериала,
	|	ДД.АналитикаУчетаПартий				КАК АналитикаУчетаПартий,
	|	СУММА(ДД.Количество)				КАК Количество
	|ПОМЕСТИТЬ ПартииМатериаловВНЗП
	|ИЗ
	|	ОстаткиМатериалов КАК Остатки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
	|	ПО Остатки.ПартияПроизводства = ДД.КорПартия
	|	И Остатки.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
	|
	|ГДЕ
	|	ДД.Период <= &КонецПериода
	|	И НЕ ДД.СлужебноеВидДвиженияПриход
	|	И НЕ ДД.Партия = НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.ПартияПроизводства,
	|	ДД.Партия,
	|	ДД.АналитикаУчетаПартий,
	|	Остатки.АналитикаУчетаНоменклатуры
	|;";
	
КонецФункции

Функция ТекстПустаяТаблицаПартииМатериаловВНЗП() Экспорт
	
	Возврат "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ПартияМатериала,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	0 КАК Количество
	|ПОМЕСТИТЬ ПартииМатериаловВНЗП
	|;";
	
КонецФункции

Функция ТекстДолиСтоимостиПоПродукции() Экспорт
	
	Возврат "
	//++ НЕ УТКА
	// доли по переработке и фактическое количество продукции
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.ПартияПроизводства		КАК ПартияПроизводства,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.Характеристика			КАК Характеристика,
	|	ДД.КодСтроки				КАК КодСтроки,
	|	СУММА(ДД.КоличествоПлан)	КАК КоличествоПлан,
	|	СУММА(ДД.КоличествоФакт)	КАК КоличествоФакт,
	|	СУММА(ДД.ДоляСтоимостиПлан)	КАК ДоляСтоимостиПлан
	|ПОМЕСТИТЬ втДолиПоПереработке
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		РеквизитыЭтапа.ПартияПроизводства	КАК ПартияПроизводства,
	|		ТаблицаТоваров.Номенклатура			КАК Номенклатура,
	|		ТаблицаТоваров.Характеристика		КАК Характеристика,
	|		ТаблицаТоваров.КодСтроки			КАК КодСтроки,
	|		ТаблицаТоваров.Количество			КАК КоличествоПлан,
	|		0									КАК КоличествоФакт,
	|		ВЫБОР
	|			КОГДА ТаблицаТоваров.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.ДоляСтоимости
	|		КОНЕЦ								КАК ДоляСтоимостиПлан
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|		ПО ПартииПроизводства.ПартияПроизводства = РеквизитыЭтапа.ПартияПроизводства
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаТоваров
	|		ПО ТаблицаТоваров.Ссылка = РеквизитыЭтапа.Ссылка
	|
	|	ГДЕ
	|		РеквизитыЭтапа.ПроизводствоНаСтороне
	|		И РеквизитыЭтапа.Проведен
	|		И НЕ ТаблицаТоваров.Отменено
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РеквизитыЭтапа.ПартияПроизводства	КАК ПартияПроизводства,
	|		ТаблицаТоваров.Номенклатура			КАК Номенклатура,
	|		ТаблицаТоваров.Характеристика		КАК Характеристика,
	|		ТаблицаТоваров.КодСтроки			КАК КодСтроки,
	|		0									КАК КоличествоПлан,
	|		ТаблицаТоваров.Количество			КАК КоличествоФакт,
	|		0									КАК ДоляСтоимостиПлан
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|		ПО РеквизитыЭтапа.Ссылка = ТаблицаТоваров.ЭтапПроизводства
	|
	|	ГДЕ
	|		ТаблицаТоваров.Ссылка.Проведен
	|		И ТаблицаТоваров.Ссылка.Дата <= &КонецПериода
	|	) КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.Номенклатура,
	|	ДД.Характеристика,
	|	ДД.КодСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.Номенклатура,
	|	ДД.Характеристика,
	|	ДД.КодСтроки
	|;
	//-- НЕ УТКА
	|
	|////////////////////////////////////////////////////////////////////////////////
	// план и факт по производству без заказов - товары
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ											КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства			КАК ПартияПроизводства,
	|	РеквизитыДокумента.Ссылка						КАК ПартияВыпуска,
	|	РеквизитыДокумента.Дата							КАК ДатаПроизводства,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	РеквизитыДокумента.Организация					КАК Организация,
	|	РеквизитыДокумента.Подразделение				КАК Подразделение,
    |	РеквизитыДокумента.НалоговоеНазначение		    КАК НалоговоеНазначение,
	|	АналитикаПоПолучателю.Номенклатура				КАК Номенклатура,
	|	АналитикаПоПолучателю.Характеристика			КАК Характеристика,
	|	АналитикаПоПолучателю.Серия						КАК Серия,
	|	ТаблицаТовары.Спецификация						КАК Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки				КАК ИдентификаторСтроки,
	|	ТаблицаТовары.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			И НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА АналитикаПоПодразделению.КлючАналитики
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|			И НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА АналитикаПоПодразделениюБезНазначения.КлючАналитики
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК СтатьяРасходовСписания,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК СтатьяАктивовПассивов,
	|	ТаблицаТовары.АналитикаРасходов					КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов			КАК АналитикаАктивовПассивов,
	|	СУММА(ТаблицаТовары.Количество)					КАК Факт,
	|	СУММА(ТаблицаТовары.Количество)					КАК План,
	|	СУММА(ТаблицаТовары.Количество)					КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|		КОНЕЦ)										КАК ДоляСтоимостиФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество - ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|				- ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|					* ТаблицаТовары.ДоляСтоимости
	|					/ ТаблицаТовары.Количество
	|		КОНЕЦ)										КАК ДоляСтоимости,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|		КОНЕЦ)										КАК ПолнаяДоляСтоимости
	|ПОМЕСТИТЬ втПродукция
	|ИЗ
	|	ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВидыЗапасовВыходныеИзделия КАК ТаблицаТовары
	|	ПО ТаблицаТовары.Ссылка = ПартииПроизводства.ДокументПартии
	|	И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК РеквизитыДокумента
	|	ПО РеквизитыДокумента.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|	ПО АналитикаПоПолучателю.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|	ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|	ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|	И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|	И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|	И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|	И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделению
	|	ПО АналитикаПоПолучателю.Номенклатура = АналитикаПоПодразделению.Номенклатура
	|	И АналитикаПоПолучателю.Характеристика = АналитикаПоПодразделению.Характеристика
	|	И АналитикаПоПолучателю.Серия = АналитикаПоПодразделению.Серия
	|	И РеквизитыДокумента.Подразделение = АналитикаПоПодразделению.МестоХранения
	|	И АналитикаПоПолучателю.Назначение = АналитикаПоПодразделению.Назначение
	|	И АналитикаПоПолучателю.СтатьяКалькуляции = АналитикаПоПодразделению.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделениюБезНазначения
	|	ПО АналитикаПоПодразделению.Номенклатура = АналитикаПоПодразделениюБезНазначения.Номенклатура
	|	И АналитикаПоПодразделению.Характеристика = АналитикаПоПодразделениюБезНазначения.Характеристика
	|	И АналитикаПоПодразделению.Серия = АналитикаПоПодразделениюБезНазначения.Серия
	|	И АналитикаПоПодразделению.МестоХранения = АналитикаПоПодразделениюБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПоПодразделениюБезНазначения.Назначение
	|	И АналитикаПоПодразделению.СтатьяКалькуляции = АналитикаПоПодразделениюБезНазначения.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартий
	|	ПО АналитикиУчетаПартий.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Контрагент				= НЕОПРЕДЕЛЕНО
	|	И АналитикиУчетаПартий.СтавкаНДС				= ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
    |	И АналитикиУчетаПартий.НалоговоеНазначение 		= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И АналитикиУчетаПартий.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	И АналитикиУчетаПартий.КодСтроки				= ТаблицаТовары.КодСтроки
	|	И РеквизитыДокумента.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|	ПО ПартииМатериаловВНЗП.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|	И ПартииМатериаловВНЗП.ПартияМатериала = ПартииПроизводства.ДокументПартии
	|	И ПартииМатериаловВНЗП.АналитикаУчетаПартий = АналитикиУчетаПартий.КлючАналитики
	|
	|ГДЕ
	|	РеквизитыДокумента.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыДокумента.Ссылка,
	|	РеквизитыДокумента.Дата,
	|	АналитикиУчетаПартий.КлючАналитики,
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыДокумента.Организация,
	|	РеквизитыДокумента.Подразделение,
    |	РеквизитыДокумента.НалоговоеНазначение,
	|	АналитикаПоПолучателю.Номенклатура,
	|	АналитикаПоПолучателю.Характеристика,
	|	АналитикаПоПолучателю.Серия,
	|	ТаблицаТовары.Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			И НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА АналитикаПоПодразделению.КлючАналитики
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|			И НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА АналитикаПоПодразделениюБезНазначения.КлючАналитики
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	ТаблицаТовары.Получатель,
	|	ТаблицаТовары.АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// план и факт по производству без заказов - работы
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)					КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)	КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства			КАК ПартияПроизводства,
	|	РеквизитыДокумента.Ссылка						КАК ПартияВыпуска,
	|	РеквизитыДокумента.Дата							КАК ДатаПроизводства,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	РеквизитыДокумента.Организация					КАК Организация,
	|	РеквизитыДокумента.Подразделение				КАК Подразделение,
    |	РеквизитыДокумента.НалоговоеНазначение		    КАК НалоговоеНазначение,
	|	АналитикаПоПолучателю.Номенклатура				КАК Номенклатура,
	|	АналитикаПоПолучателю.Характеристика			КАК Характеристика,
	|	АналитикаПоПолучателю.Серия						КАК Серия,
	|	ТаблицаТовары.Спецификация						КАК Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки				КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК СтатьяРасходовСписания,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК СтатьяАктивовПассивов,
	|	ТаблицаТовары.АналитикаРасходов					КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов			КАК АналитикаАктивовПассивов,
	|	СУММА(ТаблицаТовары.Количество)					КАК Факт,
	|	СУММА(ТаблицаТовары.Количество)					КАК План,
	|	СУММА(ТаблицаТовары.Количество)					КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|		КОНЕЦ)										КАК ДоляСтоимостиФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество - ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|					- ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|						* ТаблицаТовары.ДоляСтоимости
	|						/ ТаблицаТовары.Количество
	|		КОНЕЦ)										КАК ДоляСтоимости,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|		КОНЕЦ)										КАК ПолнаяДоляСтоимости
	|ИЗ
	|	ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	ПО ТаблицаТовары.Ссылка = ПартииПроизводства.ДокументПартии
	|	И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК РеквизитыДокумента
	|	ПО РеквизитыДокумента.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|	ПО АналитикаПоПолучателю.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|	ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|	И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|	И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|	И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|	И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО СпрНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартий
	|	ПО АналитикиУчетаПартий.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Контрагент				= НЕОПРЕДЕЛЕНО
	|	И АналитикиУчетаПартий.СтавкаНДС				= ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
    |	И АналитикиУчетаПартий.НалоговоеНазначение  		= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И АналитикиУчетаПартий.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	И АналитикиУчетаПартий.КодСтроки				= ТаблицаТовары.КодСтроки
	|	И РеквизитыДокумента.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|	ПО ПартииМатериаловВНЗП.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|	И ПартииМатериаловВНЗП.ПартияМатериала = ПартииПроизводства.ДокументПартии
	|	И ПартииМатериаловВНЗП.АналитикаУчетаПартий = АналитикиУчетаПартий.КлючАналитики
	|
	|ГДЕ
	|	СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И РеквизитыДокумента.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыДокумента.Ссылка,
	|	РеквизитыДокумента.Дата,
	|	АналитикиУчетаПартий.КлючАналитики,
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыДокумента.Организация,
	|	РеквизитыДокумента.Подразделение,
    |	РеквизитыДокумента.НалоговоеНазначение,
	|	АналитикаПоПолучателю.Номенклатура,
	|	АналитикаПоПолучателю.Характеристика,
	|	АналитикаПоПолучателю.Серия,
	|	ТаблицаТовары.Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	ТаблицаТовары.Получатель,
	|	ТаблицаТовары.АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// план и факт по этапам (выпущенные товары)
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)		КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца) ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца) ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства								КАК ПартияПроизводства,
	|	РеквизитыЭтапа.Ссылка												КАК ПартияВыпуска,
	|	ВидыЗапасов.ДатаОперации											КАК ДатаПроизводства,
	|	ЕСТЬNULL(АналитикиУчетаПартийТоваров.КлючАналитики,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))	КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	РеквизитыЭтапа.Организация											КАК Организация,
	|	РеквизитыЭтапа.Подразделение										КАК Подразделение,
    |	РеквизитыЭтапа.НалоговоеНазначение									КАК НалоговоеНазначение,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура					КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика				КАК Характеристика,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия						КАК Серия,
	|	РеквизитыЭтапа.Спецификация											КАК Спецификация,
	|	ВидыЗапасов.ИдентификаторСтроки										КАК ИдентификаторСтроки,
	|	ВидыЗапасов.ВидЗапасов												КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ																КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|			ТОГДА ВидыЗапасов.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																КАК СтатьяРасходовСписания,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|			ТОГДА ВидыЗапасов.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																КАК СтатьяАктивовПассивов,
	|	ВидыЗапасов.АналитикаРасходов										КАК АналитикаРасходов,
	|	ВидыЗапасов.АналитикаАктивовПассивов								КАК АналитикаАктивовПассивов,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыЗапасов.ДатаОперации <= &КонецПериода
	|				ТОГДА ВидыЗапасов.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)															КАК Факт,
	|	СУММА(ВидыЗапасов.Количество)										КАК План,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыЗапасов.ДатаОперации МЕЖДУ &НачалоПериода И &КонецПериода
	|				ТОГДА ВидыЗапасов.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)															КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыЗапасов.ДатаОперации <= &КонецПериода
	|				ТОГДА ВЫБОР
	|						КОГДА ВидыЗапасов.ДоляСтоимости = 0
	|							ТОГДА ВидыЗапасов.Количество
	|						ИНАЧЕ ВидыЗапасов.ДоляСтоимости
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ)															КАК ДоляСтоимостиФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыЗапасов.ДоляСтоимости = 0
	|				ТОГДА ВидыЗапасов.Количество - ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|			ИНАЧЕ ВидыЗапасов.ДоляСтоимости
	|					- ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|						* ВидыЗапасов.ДоляСтоимости
	|						/ ВидыЗапасов.Количество
	|		КОНЕЦ)															КАК ДоляСтоимости,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыЗапасов.ДоляСтоимости = 0
	|				ТОГДА ВидыЗапасов.Количество
	|			ИНАЧЕ ВидыЗапасов.ДоляСтоимости
	|		КОНЕЦ)															КАК ПолнаяДоляСтоимости
	|ИЗ
	|	ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|	ПО РеквизитыЭтапа.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыЗапасовИзделия КАК ВидыЗапасов
	|	ПО ВидыЗапасов.Ссылка = РеквизитыЭтапа.Ссылка
	|	И ВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО Аналитика.Ссылка = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|	И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|	И Аналитика.Серия = АналитикаБезНазначения.Серия
	|	И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийТоваров
	|	ПО АналитикиУчетаПартийТоваров.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.Контрагент			= НЕОПРЕДЕЛЕНО
	|	И АналитикиУчетаПартийТоваров.СтавкаНДС				= ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
    |	И АналитикиУчетаПартийТоваров.НалоговоеНазначение	= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.ВидЦенности			= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	И АналитикиУчетаПартийТоваров.КодСтроки				= ВидыЗапасов.КодСтроки
	|	И РеквизитыЭтапа.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|	ПО ПартииМатериаловВНЗП.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|	И ПартииМатериаловВНЗП.ПартияМатериала = РеквизитыЭтапа.Ссылка
	|	И ПартииМатериаловВНЗП.АналитикаУчетаПартий = АналитикиУчетаПартийТоваров.КлючАналитики
	|
	|ГДЕ
	|	НЕ РеквизитыЭтапа.ПроизводствоНаСтороне
	|	И РеквизитыЭтапа.Проведен
	|	И ВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца) ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца) ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ,
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыЭтапа.Ссылка,
	|	ВидыЗапасов.ДатаОперации,
	|	ЕСТЬNULL(АналитикиУчетаПартийТоваров.КлючАналитики,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыЭтапа.Организация,
	|	РеквизитыЭтапа.Подразделение,
    |	РеквизитыЭтапа.НалоговоеНазначение,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия,
	|	РеквизитыЭтапа.Спецификация,
	|	ВидыЗапасов.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	ВидыЗапасов.СтатьяРасходов,
	|	ВидыЗапасов.АналитикаРасходов,
	|	ВидыЗапасов.АналитикаАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// план и факт по этапам (не выпущенные товары, работы)
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства							КАК ПартияПроизводства,
	|	РеквизитыЭтапа.Ссылка											КАК ПартияВыпуска,
	|	ТаблицаТовары.ДатаПроизводства									КАК ДатаПроизводства,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЕСТЬNULL(АналитикиУчетаПартийРабот.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаПартийТоваров.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
	|	КОНЕЦ															КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета					КАК АналитикаФинансовогоУчета,
	|	РеквизитыЭтапа.Организация										КАК Организация,
	|	РеквизитыЭтапа.Подразделение									КАК Подразделение,
    |	РеквизитыЭтапа.НалоговоеНазначение							    КАК НалоговоеНазначение,
	|	ТаблицаТовары.Номенклатура										КАК Номенклатура,
	|	ТаблицаТовары.Характеристика									КАК Характеристика,
	|	ТаблицаТовары.Серия												КАК Серия,
	|	РеквизитыЭтапа.Спецификация										КАК Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки								КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)					КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ															КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|			ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ															КАК СтатьяРасходовСписания,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|			ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ															КАК СтатьяАктивовПассивов,
	|	ТаблицаТовары.АналитикаРасходов									КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов							КАК АналитикаАктивовПассивов,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.Произведено
	|				И ТаблицаТовары.ДатаПроизводства <= &КонецПериода
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)														КАК Факт,
	|	СУММА(ТаблицаТовары.Количество)									КАК План,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.Произведено
	|				И ТаблицаТовары.ДатаПроизводства МЕЖДУ &НачалоПериода И &КонецПериода
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)														КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.Произведено
	|				И ТаблицаТовары.ДатаПроизводства <= &КонецПериода
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|							ТОГДА ТаблицаТовары.Количество
	|						ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ)														КАК ДоляСтоимостиФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество - ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|					- ЕСТЬNULL(ПартииМатериаловВНЗП.Количество, 0)
	|						* ТаблицаТовары.ДоляСтоимости
	|						/ ТаблицаТовары.Количество
	|		КОНЕЦ)														КАК ДоляСтоимости,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|		КОНЕЦ)														КАК ПолнаяДоляСтоимости
	|ИЗ
	|	ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|	ПО РеквизитыЭтапа.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаТовары
	|	ПО ТаблицаТовары.Ссылка = РеквизитыЭтапа.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО Аналитика.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|	И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|	И Аналитика.Серия = АналитикаБезНазначения.Серия
	|	И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийТоваров
	|	ПО АналитикиУчетаПартийТоваров.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.Контрагент			= НЕОПРЕДЕЛЕНО
	|	И АналитикиУчетаПартийТоваров.СтавкаНДС				= ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
    |	И АналитикиУчетаПартийТоваров.НалоговоеНазначение 	= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И АналитикиУчетаПартийТоваров.ВидЦенности			= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	И АналитикиУчетаПартийТоваров.КодСтроки				= ТаблицаТовары.КодСтроки
	|	И РеквизитыЭтапа.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийРабот
	|	ПО АналитикиУчетаПартийРабот.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартийРабот.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И АналитикиУчетаПартийРабот.Контрагент				= НЕОПРЕДЕЛЕНО
	|	И АналитикиУчетаПартийРабот.СтавкаНДС				= ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
    |	И АналитикиУчетаПартийРабот.НалоговоеНазначение		= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	И АналитикиУчетаПартийРабот.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	И АналитикиУчетаПартийРабот.КодСтроки				= ТаблицаТовары.КодСтроки
	|	И РеквизитыЭтапа.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|	ПО ПартииМатериаловВНЗП.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|	И ПартииМатериаловВНЗП.ПартияМатериала = РеквизитыЭтапа.Ссылка
	|	И (ПартииМатериаловВНЗП.АналитикаУчетаПартий = АналитикиУчетаПартийТоваров.КлючАналитики
	|		ИЛИ ПартииМатериаловВНЗП.АналитикаУчетаПартий = АналитикиУчетаПартийРабот.КлючАналитики)
	|
	|ГДЕ
	|	НЕ РеквизитыЭтапа.ПроизводствоНаСтороне
	|	И РеквизитыЭтапа.Проведен
	|	И НЕ ТаблицаТовары.Отменено
	|	И (ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ИЛИ НЕ ТаблицаТовары.Произведено)
	|
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыЭтапа.Ссылка,
	|	ТаблицаТовары.ДатаПроизводства,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЕСТЬNULL(АналитикиУчетаПартийРабот.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаПартийТоваров.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
	|	КОНЕЦ,
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыЭтапа.Организация,
	|	РеквизитыЭтапа.Подразделение,
    |	РеквизитыЭтапа.НалоговоеНазначение,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	РеквизитыЭтапа.Спецификация,
	|	ТаблицаТовары.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	ТаблицаТовары.СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// план и факт по отчетам переработчиков по этапам
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства			КАК ПартияПроизводства,
	|	РеквизитыОтчета.Ссылка							КАК ПартияВыпуска,
	|	РеквизитыОтчета.Дата							КАК ДатаПроизводства,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	РеквизитыОтчета.Организация						КАК Организация,
	|	РеквизитыОтчета.Подразделение					КАК Подразделение,
    |	РеквизитыОтчета.НалоговоеНазначение			КАК НалоговоеНазначение,
	|	ТаблицаТовары.Номенклатура						КАК Номенклатура,
	|	ТаблицаТовары.Характеристика					КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	РеквизитыЭтапа.Спецификация						КАК Спецификация,
	|	""""											КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаАктивовПассивов,
	|	СУММА(ВЫБОР
	|			КОГДА РеквизитыОтчета.Дата <= &КонецПериода
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)										КАК Факт,
	|	МАКСИМУМ(ДолиПоПереработке.КоличествоПлан) КАК План,
	|	СУММА(ВЫБОР
	|			КОГДА РеквизитыОтчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|				ТОГДА ТаблицаТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ)										КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА РеквизитыОтчета.Дата <= &КонецПериода
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаТовары.ДоляСтоимости = 0
	|							ТОГДА ТаблицаТовары.Количество
	|						ИНАЧЕ ТаблицаТовары.ДоляСтоимости
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|	КОНЕЦ)											КАК ДоляСтоимостиФакт,
	|	МАКСИМУМ(ДолиПоПереработке.ДоляСтоимостиПлан)	КАК ДоляСтоимости,
	|	МАКСИМУМ(ДолиПоПереработке.ДоляСтоимостиПлан)	КАК ПолнаяДоляСтоимости
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭтапа
	|	ПО РеквизитыЭтапа.Ссылка = ТаблицаТовары.ЭтапПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК ПартииПроизводства
	|	ПО ПартииПроизводства.ПартияПроизводства = РеквизитыЭтапа.ПартияПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|	ПО РеквизитыОтчета.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДолиПоПереработке КАК ДолиПоПереработке
	|	ПО ДолиПоПереработке.ПартияПроизводства = ПартииПроизводства.ПартияПроизводства
	|	И ДолиПоПереработке.Номенклатура = ТаблицаТовары.Номенклатура
	|	И ДолиПоПереработке.Характеристика = ТаблицаТовары.Характеристика
	|	И ДолиПоПереработке.КодСтроки = ТаблицаТовары.КодСтроки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|	И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|	И Аналитика.Серия = АналитикаБезНазначения.Серия
	|	И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартий
	|	ПО АналитикиУчетаПартий.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Поставщик				= РеквизитыОтчета.Партнер
	|	И АналитикиУчетаПартий.Контрагент				= РеквизитыОтчета.Контрагент
	|	И АналитикиУчетаПартий.СтавкаНДС				= РеквизитыОтчета.СтавкаНДС
    |	И АналитикиУчетаПартий.НалоговоеНазначение		= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) // при ПУ 2.2 НалоговоеНазначение не заполняется
	|	И АналитикиУчетаПартий.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	И АналитикиУчетаПартий.КодСтроки				= ТаблицаТовары.КодСтрокиПродукция
	|	И РеквизитыОтчета.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|ГДЕ
	|	РеквизитыОтчета.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыОтчета.Ссылка,
	|	РеквизитыОтчета.Дата,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыОтчета.Организация,
	|	РеквизитыОтчета.Подразделение,
    |	РеквизитыОтчета.НалоговоеНазначение,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	РеквизитыЭтапа.Спецификация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ
	//-- НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// план и факт по отчетам переработчиков без этапов
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	ПартииПроизводства.ПартияПроизводства			КАК ПартияПроизводства,
	|	РеквизитыОтчета.Ссылка							КАК ПартияВыпуска,
	|	РеквизитыОтчета.Дата							КАК ДатаПроизводства,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	РеквизитыОтчета.Организация						КАК Организация,
	|	РеквизитыОтчета.Подразделение					КАК Подразделение,
    |	РеквизитыОтчета.НалоговоеНазначение				КАК НалоговоеНазначение,
	|	ТаблицаТоваров.Номенклатура						КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика					КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаТоваров.Спецификация						КАК Спецификация,
	|	""""											КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаАктивовПассивов,
	|	СУММА(ТаблицаТоваров.Количество)				КАК Факт,
	|	СУММА(ТаблицаТоваров.Количество)				КАК План,
	|	СУММА(ТаблицаТоваров.Количество)				КАК КоличествоПродукции,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТоваров.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.ДоляСтоимости
	|		КОНЕЦ)										КАК ДоляСтоимостиФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТоваров.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.ДоляСтоимости
	|		КОНЕЦ)										КАК ДоляСтоимости,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТоваров.ДоляСтоимости = 0
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.ДоляСтоимости
	|		КОНЕЦ)										КАК ПолнаяДоляСтоимости
	|ИЗ
	|	ПартииПроизводстваСводно КАК ПартииПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК ТаблицаТоваров
	|	ПО ПартииПроизводства.ДокументПартии = ТаблицаТоваров.Ссылка
	|	И ПартииПроизводства.НомерГруппыЗатрат = ТаблицаТоваров.НомерГруппыЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|	ПО РеквизитыОтчета.Ссылка = ТаблицаТоваров.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО ТаблицаТоваров.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|	И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|	И Аналитика.Серия = АналитикаБезНазначения.Серия
	|	И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартий
	|	ПО АналитикиУчетаПартий.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	И АналитикиУчетаПартий.Поставщик				= РеквизитыОтчета.Партнер
	|	И АналитикиУчетаПартий.Контрагент				= РеквизитыОтчета.Контрагент
	|	И АналитикиУчетаПартий.СтавкаНДС				= РеквизитыОтчета.СтавкаНДС
    |	И АналитикиУчетаПартий.НалоговоеНазначение		= ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) // при ПУ 2.2 НалоговоеНазначение не заполняется
	|	И АналитикиУчетаПартий.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	И АналитикиУчетаПартий.КодСтроки				= ТаблицаТоваров.КодСтрокиПродукция
	|	И ПартииПроизводства.Организация В (&ОрганизацииСФИФОСкользящая)
	|
	|ГДЕ
	|	РеквизитыОтчета.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииПроизводства.ПартияПроизводства,
	|	РеквизитыОтчета.Ссылка,
	|	РеквизитыОтчета.Дата,
	|	ЕСТЬNULL(АналитикиУчетаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
	|	ПартииПроизводства.АналитикаФинансовогоУчета,
	|	РеквизитыОтчета.Организация,
	|	РеквизитыОтчета.Подразделение,
    |	РеквизитыОтчета.НалоговоеНазначение,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Спецификация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ
	|			АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства				КАК ПартияПроизводства,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ДД.ДоляСтоимости < 0 
	|				ТОГДА 0 
	|			ИНАЧЕ ДД.ДоляСтоимости 
	|		КОНЕЦ)							КАК СуммаДолейПоЭтапу,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ДД.ПолнаяДоляСтоимости < 0 
	|				ТОГДА 0 
	|			ИНАЧЕ ДД.ПолнаяДоляСтоимости 
	|		КОНЕЦ)							КАК СуммаПолныхДолейПоЭтапу,
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ДД.Факт <> ДД.План
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ)								КАК ПроизводствоЗавершено
	|ПОМЕСТИТЬ втДолиИтог
	|ИЗ
	|	втПродукция КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ХозяйственнаяОперация			КАК ХозяйственнаяОперация,
	|	ДД.РазделУчета						КАК РазделУчета,
	|	ДД.ПартияПроизводства				КАК ПартияПроизводства,
	|	ДД.ПартияВыпуска					КАК ПартияВыпуска,
	|	ДД.ДатаПроизводства					КАК ДатаПроизводства,
	|	ДД.АналитикаУчетаПартий				КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета		КАК АналитикаФинансовогоУчета,
	|	ДД.Организация						КАК Организация,
    |	ДД.НалоговоеНазначение				КАК НалоговоеНазначение,
	|	ДД.Подразделение					КАК Подразделение,
	|	ДД.Номенклатура						КАК Номенклатура,
	|	ДД.Характеристика					КАК Характеристика,
	|	ДД.Серия							КАК Серия,
	|	ДД.Спецификация						КАК Спецификация,
	|	ДД.ВидЗапасов						КАК ВидЗапасов,
	|	ДД.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ДД.КоличествоПродукции				КАК КоличествоПродукции,
	|	ДД.СтатьяРасходовСписания			КАК СтатьяРасходовСписания,
	|	ДД.АналитикаРасходов				КАК АналитикаРасходов,
	|	ДД.СтатьяАктивовПассивов			КАК СтатьяАктивовПассивов,
	|	ДД.АналитикаАктивовПассивов			КАК АналитикаАктивовПассивов,
	|	ДД.ИдентификаторСтроки				КАК ИдентификаторСтроки,
	|	ДД.Факт								КАК Факт,
	|	ДД.План								КАК План,
	|	ДД.ДоляСтоимости					КАК ДоляСтоимости,
	|	ДД.ПолнаяДоляСтоимости				КАК ПолнаяДоляСтоимости,
	|	ВЫБОР КОГДА ДолиИтог.СуммаПолныхДолейПоЭтапу = 0
	|		ИЛИ ДД.План = 0
	|		ТОГДА 0
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ДД.ПолнаяДоляСтоимости / ДолиИтог.СуммаПолныхДолейПоЭтапу * (ДД.Факт / ДД.План) КАК ЧИСЛО(23,10))
	|	КОНЕЦ								КАК ПолнаяДоляНаименованияВсего,
	|	ВЫБОР КОГДА ДолиИтог.СуммаПолныхДолейПоЭтапу = 0
	|		ИЛИ ДД.План = 0
	|		ТОГДА 0
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ДД.ПолнаяДоляСтоимости / ДолиИтог.СуммаПолныхДолейПоЭтапу * (ДД.КоличествоПродукции / ДД.План) КАК ЧИСЛО(23,10))
	|	КОНЕЦ								КАК ПолнаяДоляНаименованияМесяца,
	|	ВЫБОР КОГДА ДолиИтог.СуммаДолейПоЭтапу = 0
	|		ИЛИ ДД.План = 0
	|		ТОГДА 0
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ДД.ДоляСтоимости / ДолиИтог.СуммаДолейПоЭтапу * (ДД.Факт / ДД.План) КАК ЧИСЛО(23,10))
	|	КОНЕЦ								КАК ДоляНаименованияВсего,
	|	ВЫБОР КОГДА ДолиИтог.СуммаДолейПоЭтапу = 0 
	|		ИЛИ ДД.План = 0
	|		ТОГДА 0
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ДД.ДоляСтоимости / ДолиИтог.СуммаДолейПоЭтапу * (ДД.КоличествоПродукции / ДД.План) КАК ЧИСЛО(23,10))
	|	КОНЕЦ								КАК ДоляНаименованияМесяца,
	|	ДолиИтог.ПроизводствоЗавершено		КАК ПроизводствоЗавершено
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	втПродукция КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДолиИтог КАК ДолиИтог
	|		ПО ДД.ПартияПроизводства = ДолиИтог.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства					КАК ПартияПроизводства,
	|	ДД.ПроизводствоЗавершено				КАК ПроизводствоЗавершено,
	|	СУММА(ДД.ДоляНаименованияВсего)			КАК ДоляВыпускаВсего,
	|	СУММА(ДД.ДоляНаименованияМесяца)		КАК ДоляВыпускаМесяца
	|ПОМЕСТИТЬ ДолиВыпуска
	|ИЗ
	|	Продукция КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ПроизводствоЗавершено
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|ВЫБРАТЬ
	|	ПартииМатериаловВНЗП.ПартияПроизводства			КАК ПартияПроизводства,
	|	Продукция.ПартияВыпуска							КАК ПартияВыпуска,
	|	ПартииМатериаловВНЗП.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	СУММА(ПартииМатериаловВНЗП.Количество)			КАК Количество
	|ПОМЕСТИТЬ ЗамкнутыеПартии
	|ИЗ
	|	ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО ПартииМатериаловВНЗП.ПартияПроизводства = Продукция.ПартияПроизводства
	|		И ПартииМатериаловВНЗП.ПартияМатериала = Продукция.ПартияВыпуска
	|		И ПартииМатериаловВНЗП.АналитикаУчетаПартий = Продукция.АналитикаУчетаПартий
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииМатериаловВНЗП.ПартияПроизводства,
	|	Продукция.ПартияВыпуска,
	|	ПартииМатериаловВНЗП.АналитикаУчетаНоменклатуры
	|;
	|";	
	
КонецФункции

// Таблицы: ДанныеПоПродукции
Функция ТекстДанныеПоВыпущеннойПродукции() Экспорт
	
	Возврат "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.ДатаПроизводства                                      КАК ДатаПроизводства,
	|	ТаблицаТоваров.Организация                                           КАК Организация,
	|	ТаблицаТоваров.Подразделение                                         КАК Подразделение,
	|	ТаблицаТоваров.Номенклатура                                          КАК Продукция,
	|	ПартииПроизводства.АналитикаФинансовогоУчета                         КАК ГруппаПродукции,
	|	ТаблицаТоваров.ПартияПроизводства                                    КАК ПартияПроизводства,
    |	ПартииПроизводства.НалоговоеНазначение                                КАК НалоговоеНазначение,
	|	ПартииПроизводства.Назначение                                        КАК Назначение,
	|	ПартииПроизводства.НаправлениеДеятельности                           КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ЗаказНаПроизводство,
	|	0                                                                    КАК КодСтрокиПродукция,
	|	ТаблицаТоваров.КоличествоПродукции                                   КАК Количество,
	|	&Объем * ТаблицаТоваров.КоличествоПродукции                          КАК Объем,
	|	&Вес * ТаблицаТоваров.КоличествоПродукции                            КАК Вес,
	|	ЕСТЬNULL(Цены.Цена, 0) * ТаблицаТоваров.КоличествоПродукции          КАК Стоимость,
	|	ТаблицаТоваров.План - ТаблицаТоваров.Факт                            КАК КоличествоПлан,
	|	&Объем * (ТаблицаТоваров.План - ТаблицаТоваров.Факт)                 КАК ОбъемПлан,
	|	&Вес * (ТаблицаТоваров.План - ТаблицаТоваров.Факт)                   КАК ВесПлан,
	|	ЕСТЬNULL(Цены.Цена, 0) * (ТаблицаТоваров.План - ТаблицаТоваров.Факт) КАК СтоимостьПлан
	|ПОМЕСТИТЬ ДанныеПоПродукции
	|ИЗ
	|	Продукция КАК ТаблицаТоваров
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК ПартииПроизводства
	|		ПО ПартииПроизводства.ПартияПроизводства = ТаблицаТоваров.ПартияПроизводства
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
	|		ПО СН.Ссылка = ТаблицаТоваров.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода, ВидЦены = &ВидПлановыхЦен) КАК Цены
	|		ПО Цены.Номенклатура = ТаблицаТоваров.Номенклатура
	|		И Цены.Характеристика = ТаблицаТоваров.Характеристика
	|	
	|ГДЕ
	|	ТаблицаТоваров.ПолнаяДоляНаименованияМесяца > 0 ИЛИ &УчитыватьБудущиеВыпуски
	|
	//++ Устарело_Производство21
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// производство без заказов 2.1
	|ВЫБРАТЬ
	|	Реквизиты.Дата                                   КАК ДатаПроизводства,
	|	Реквизиты.Организация                            КАК Организация,
	|	Реквизиты.Подразделение                          КАК Подразделение,
	|	ДД.Номенклатура                                  КАК Продукция,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			ТОГДА СН.ГруппаАналитическогоУчета
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                            КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
    |	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	ДД.Назначение                                    КАК Назначение,
	|	ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))КАК НаправлениеДеятельности,
	|	ДД.Ссылка                                        КАК ЗаказНаПроизводство,
	|	ДД.КодСтроки                                     КАК КодСтрокиПродукция,
	|	ДД.Количество                                    КАК Количество,
	|	&Объем * ДД.Количество КАК Объем,
	|	&Вес * ДД.Количество   КАК Вес,
	|	ЕСТЬNULL(Цены.Цена, 0) * ДД.Количество           КАК Стоимость,
	|	0			                                     КАК КоличествоПлан,
	|	0 												 КАК ОбъемПлан,
	|	0												 КАК ВесПлан,
	|	0									             КАК СтоимостьПлан
	|ИЗ
	|	Документ.ВыпускПродукции.Товары КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Реквизиты
	|			ПО (Реквизиты.Ссылка = ДД.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
	|			ПО (СН.Ссылка = ДД.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода, ВидЦены = &ВидПлановыхЦен) КАК Цены
	|			ПО Цены.Номенклатура = ДД.Номенклатура
	|			И Цены.Характеристика = ДД.Характеристика
	|
	|ГДЕ
	|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Реквизиты.Организация В (&МассивОрганизаций)
	|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
	|	И Реквизиты.Проведен
	|	И НЕ Реквизиты.ВыпускПоРаспоряжениям
	|	И &ВключатьДанныеПроизводства21
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// производство по заказам 2.1
	|ВЫБРАТЬ
	|	Реквизиты.Дата                                   КАК ДатаПроизводства,
	|	Реквизиты.Организация                            КАК Организация,
	|	Реквизиты.Подразделение                          КАК Подразделение,
	|	ДД.Номенклатура                                  КАК Продукция,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                            КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
    |	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	Продукция.Назначение                             КАК Назначение,
	|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))КАК НаправлениеДеятельности,
	|	МаршрутныйЛист.Распоряжение                      КАК ЗаказНаПроизводство,
	|	МаршрутныйЛист.КодСтроки                         КАК КодСтрокиПродукция,
	|	ДД.Количество                                    КАК Количество,
	|	&Объем * ДД.Количество КАК Объем,
	|	&Вес * ДД.Количество   КАК Вес,
	|	ЕСТЬNULL(Цены.Цена, 0) * ДД.Количество           КАК Стоимость,
	|	0			                                     КАК КоличествоПлан,
	|	0 												 КАК ОбъемПлан,
	|	0												 КАК ВесПлан,
	|	0									             КАК СтоимостьПлан
	|ИЗ
	|	Документ.ВыпускПродукции.Товары КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Реквизиты
	|			ПО (Реквизиты.Ссылка = ДД.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
	|			ПО (СН.Ссылка = ДД.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода, ВидЦены = &ВидПлановыхЦен) КАК Цены
	|			ПО Цены.Номенклатура = ДД.Номенклатура
	|			И Цены.Характеристика = ДД.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
	|			ПО ДД.Распоряжение = МаршрутныйЛист.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Продукция
	|			ПО МаршрутныйЛист.Распоряжение = Продукция.Ссылка
	|			И МаршрутныйЛист.КодСтроки = Продукция.КодСтроки
	|
	|ГДЕ
	|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Реквизиты.Организация В (&МассивОрганизаций)
	|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
	|	И Реквизиты.Проведен
	|	И Реквизиты.ВыпускПоРаспоряжениям
	|	И &ВключатьДанныеПроизводства21
	//-- НЕ УТКА
	//-- Устарело_Производство21
	|;
	|";
	
КонецФункции

// Таблицы: ДанныеПоТрудозатратам
Функция ТекстДанныеПоТрудозатратам(ПараметрыРасчета = Неопределено) Экспорт
	
	ДляРасшифровки 				= РасчетСебестоимостиПрикладныеАлгоритмы.РасчетВызванДляРасшифровки(ПараметрыРасчета);
	РасчетСебестоимостиВыполнен = РасчетСебестоимостиПрикладныеАлгоритмы.РасчетСебестоимостиВыполненДляРасшифровки(ПараметрыРасчета);
	
	// Данные по трудозатратам выбираются в одну таблицу т.к. источники данных для плана и факта одинаковые.
	// Исключением являются выпуски без заказа 2.1, для них данные выбираются из документов производства без заказов.
	//
	Текст = "
	|ВЫБРАТЬ
	|	ДД.Организация,
	|	ДД.Подразделение,
	|	ДД.ВидРабот,
	|	СУММА(ДД.Количество) КАК Количество,
	|	СУММА(ДД.Стоимость) КАК Стоимость,
	|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл
	|ПОМЕСТИТЬ ТекущиеТрудозатратыНЗП
	|ИЗ
	|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
	|ГДЕ
	|	ДД.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	//++ Устарело_Производство21
	//++ НЕ УТКА
	|	И ДД.ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	//-- НЕ УТКА
	//-- Устарело_Производство21
	|	И ДД.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Организация,
	|	ДД.Подразделение,
	|	ДД.ВидРабот
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	ВидРабот
	|;
	// производство 2.2
	|ВЫБРАТЬ
	|	ДД.Организация								КАК Организация,
	|	ДД.Подразделение							КАК Подразделение,
	|	ДД.ВидРабот									КАК ВидРабот,
	|	ДД.ПартияПроизводства.ГруппаПродукции		КАК ГруппаПродукции,
	|	ДД.ПартияПроизводства.Назначение			КАК Назначение,
	|	ДД.ПартияПроизводства						КАК ПартияПроизводства,
    |	ДД.ПартияПроизводства.НалоговоеНазначение	КАК НалоговоеНазначение,
	|	НЕОПРЕДЕЛЕНО								КАК ЗаказНаПроизводство,
	|	0											КАК КодСтрокиПродукция,
	|	ЕСТЬNULL(ДД.ПартияПроизводства.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СУММА(ДД.Количество)						КАК Количество,
	|	СУММА(ДД.НормативнаяСтоимость)				КАК НормативнаяСтоимость,
	|	СУММА(ДД.Стоимость)							КАК Стоимость,
	|	СУММА(ДД.СтоимостьРегл)						КАК СтоимостьРегл
	|ПОМЕСТИТЬ ДанныеПоТрудозатратам
	|ИЗ
	|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
	|
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|	И ДД.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение,
	|	ДД.ВидРабот,
	|	ДД.ПартияПроизводства.Назначение,
    |	ДД.ПартияПроизводства.НалоговоеНазначение,
	|	ЕСТЬNULL(ДД.ПартияПроизводства.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	ДД.Организация,
	|	ДД.ПартияПроизводства,
	|	ДД.ПартияПроизводства.НаправлениеДеятельности,
	|	ДД.ПартияПроизводства.ГруппаПродукции
	|";
	
	РасчетСебестоимостиЛокализация.ДополнитьЗапросДаннымиПоТрудозатратам(Текст);
	
	Текст = Текст + ";";
	
	Если РасчетСебестоимостиВыполнен ИЛИ ДляРасшифровки Тогда
		Текст = СтрЗаменить(Текст,
			"ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства",
			"РегистрНакопления.ТрудозатратыНезавершенногоПроизводства");
		Текст = СтрЗаменить(Текст,
			"ДД.СлужебноеВидДвиженияПриход",
			"ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Активность");
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Функция ОтборПоМатериаламИерархия(ПараметрыРасчета) Экспорт
	
	Контекст = ПараметрыРасчета.РаспределениеПартий.ОписаниеДвижений.Контекст;
	
	Материалы = Новый ТаблицаЗначений;
	Материалы.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	Материалы.Колонки.Добавить("Материал",    Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Если Контекст = "РаспределениеНоменклатурыНаПроизводство"
		ИЛИ Контекст = "РаспределениеМатериаловИРаботПоБазе" Тогда
		
		// Этап 2
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Д.Ссылка КАК Регистратор,
		|	ТЧ.Материал КАК Материал,
		|	ТЧ.Материал.ЭтоГруппа КАК ЭтоГруппа
		|ИЗ
		|	Документ.РаспределениеПроизводственныхЗатрат.ОтборПоМатериалам КАК ТЧ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат КАК Д
		|		ПО ТЧ.Ссылка = Д.Ссылка
		|
		|ГДЕ
		|	Д.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Д.Организация В (&МассивОрганизаций)
		|	И Д.Проведен
		|	И Д.КоличествоКРаспределениюПоПравилу <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Д.Ссылка КАК Регистратор,
		|	ТЧ.Материал КАК Материал,
		|	ТЧ.Материал.ЭтоГруппа КАК ЭтоГруппа
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов.ОтборПоМатериалам КАК ТЧ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Д
		|		ПО ТЧ.Ссылка = Д.Ссылка
		|
		|ГДЕ
		|	Д.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Д.Организация В (&МассивОрганизаций)
		|	И Д.Проведен
		|	И Д.КоличествоПоПравилу <> 0
		|";
		
	ИначеЕсли Контекст = "ПрочиеРасходыНЗППоБазе_КРаспределению"
	 ИЛИ Контекст = "ПрочиеРасходыНЗППоБазе_ПоПодразделениям" 
	 ИЛИ Контекст = "ПрочиеРасходыНЗППоБазе_ПоЭтапам" Тогда
		
		// Этап 15
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Д.Ссылка КАК Регистратор,
		|	ТЧ.Материал КАК Материал,
		|	ТЧ.Материал.ЭтоГруппа КАК ЭтоГруппа
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ОтборПоМатериалам КАК ТЧ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Д
		|		ПО ТЧ.Ссылка = Д.Ссылка
		|
		|ГДЕ
		|	Д.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Д.Организация В (&МассивОрганизаций)
		|	И Д.Проведен";
		
	Иначе
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для контекста %1 не предусмотрено формирование отбора по материалам с иерархией';uk='Для контексту %1 не передбачено формування відбору за матеріалами з ієрархією'",ОбщегоНазначения.КодОсновногоЯзыка()),
			Контекст);
		
		РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
			ПараметрыРасчета,
			Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаРаспределенияПартий,
			ТекстДляПротокола);
			
		ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстИсключениеДляРегистрацииПроблемы(ТекстДляПротокола);
		
	КонецЕсли;

	Результат = Запрос.Выполнить().Выбрать();
	
	ЗапросИерархия = Новый Запрос;
	ЗапросИерархия.Текст =
	"ВЫБРАТЬ
	|	Н.Ссылка КАК Материал
	|ИЗ
	|	Справочник.Номенклатура КАК Н
	|ГДЕ
	|	Н.Ссылка В ИЕРАРХИИ(&Номенклатура)
	|	И НЕ Н.ЭтоГруппа";
		
	Пока Результат.Следующий() Цикл
		
		Если Результат.ЭтоГруппа Тогда
			
			ЗапросИерархия.УстановитьПараметр("Номенклатура", Результат.Материал);
			
			РезультатИерархия = ЗапросИерархия.Выполнить().Выбрать();
			
			Пока РезультатИерархия.Следующий() Цикл
				
				НоваяСтрока = Материалы.Добавить();
				НоваяСтрока.Регистратор = Результат.Регистратор;
				НоваяСтрока.Материал 	= РезультатИерархия.Материал;
				
			КонецЦикла;
			
		Иначе
			
			НоваяСтрока = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Материалы;
	
КонецФункции

Функция ТекстРаспаковкаНаборов(ИсточникСпискаСпецификаций) Экспорт // вт МатериалыИУслугиСпецификаций
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсточникСпискаСпецификаций.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ ВТСпецификации
	|ИЗ
	|	&ИсточникСпискаСпецификаций КАК ИсточникСпискаСпецификаций
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация
	|;
	|%ТекстЗапросаМатериалыСпецификацийВключаяСоставНаборов%
	|;
	|
	|УНИЧТОЖИТЬ ВТСпецификации
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникСпискаСпецификаций", ИсточникСпискаСпецификаций);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
		"%ТекстЗапросаМатериалыСпецификацийВключаяСоставНаборов%",
		Справочники.РесурсныеСпецификации.ТекстЗапросаМатериалыСпецификацийВключаяСоставНаборов("ВТСпецификации"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти