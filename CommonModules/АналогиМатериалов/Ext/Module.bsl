////////////////////////////////////////////////////////////////
// Модуль "АналогиМатериалов" содержит процедуры и функции для 
// работы с механизмом замены материалов на аналоги.
//
////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область СтруктураДанных

// Возвращает структуру полей для выгрузки и заполнения таблицы.
//
// Поле Ссылка должна быть описана а менеджере объекта
// в экспортируемой процедуре "СтруктураПолейТаблицы", если поле получаются из таблицы объекта,
// тогда в элементе с ключом ОсновныеПоля, иначе в ДополнительныеПоля.
//
// Параметры:
//  ПолноеИмяОбъекта - Строка, Неопределено - полное имя объекта метаданных. Пример: "Документ.ЗаказПоставщику"
//
// Возвращаемое значение:
// 	Структура - содержит поля:
//		* ОсновныеПоля		  - Строка 	  - основные поля таблицы, разделенные запятыми
//		* ДополнительныеПоля  - Структура - структура дополнительных полей - ключ:	имя поле 
//																			 значение:	описание типов
//		* ПодменяемыеПоля     - Структура - структура подменяемых полей - клюя:	имя	заполняемого поле
//																		  значение:	имя	поле источник данных для заполнения
//		* ЗначенияПоУмолчанию - Структура - структура значений по умолчанию - ключ:	имя поле
//																			  значение:	значение поле
// 
Функция СтруктураПолейТаблицыДляЗаменыНаАналоги(ПолноеИмяОбъекта = Неопределено) Экспорт
	
	СтруктураПолей = ПроизводствоСервер.СтруктураПолейТаблицы();
	
	СтруктураПолей.ОсновныеПоля = "НомерСтроки,
				   				  |Номенклатура,
				   				  |Характеристика, 
				   				  |Количество";	
	
	СтруктураПолей.ДополнительныеПоля.Вставить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	СтруктураПолей.ДополнительныеПоля.Вставить("КлючСвязиСпецификация", Новый ОписаниеТипов("УникальныйИдентификатор"));
	СтруктураПолей.ДополнительныеПоля.Вставить("Комментарий", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта);
		МенеджерОбъекта.ДополнитьСтруктуруПолейТаблицыДляЗаменыНаАналоги(СтруктураПолей);
	КонецЕсли;
	
	Возврат СтруктураПолей;
	
КонецФункции

// Возвращает структуру параметров выбора аналогов
// 
// Параметры:
// 	ТипИсточника - ОписаниеТипов - тип источника разреза формирования параметров
// Возвращаемое значение:
// 	Структура - содержит:
// * ПараметрыТоваров - Структура - параметры товаров
// * ПараметрыАналогов - Структура- состоит из:
// ** ТаблицаПараметров см. ТаблицаПараметровАналогов
// ** КорректировкиОстатков
// ** Склад
// * ПараметрыОбщие - Структура - параметры общие.
//
Функция ПараметрыВыбораАналогов(ТипИсточника) Экспорт

	Параметры = Новый Структура(
		"ПараметрыТоваров, ПараметрыАналогов, ПараметрыОбщие", Новый Структура, Новый Структура, Новый Структура);
	
	Параметры.ПараметрыОбщие.Вставить("ПоказатьОстатки");

	Параметры.ПараметрыТоваров.Вставить("Товары");
	Параметры.ПараметрыТоваров.Вставить("Ссылка");
	Параметры.ПараметрыТоваров.Вставить("Склад");
	
	Параметры.ПараметрыАналогов.Вставить("ТаблицаПараметров", ТаблицаПараметровАналогов(ТипИсточника));
	Параметры.ПараметрыАналогов.Вставить("КорректировкиОстатков");
	Параметры.ПараметрыАналогов.Вставить("Склад");
		
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область ФормированияДанных

// Возвращает адрес временного хранилища таблицы корректировки остатков созданного по данным табличной части
//
// Параметры:
// 	МассивСсылок           	  - Массив				 		  				  - массив ссылок источников данных
// 	Таблица            	 	  - ДанныеФормыКоллекция 				 		  - таблица формы связанная с табличной части
// 	ИмяТабличнойЧасти     	  - Строка           					 		  - имя табличной части
// 	Склад      			 	  - СправочникСсылка.Склады, Неопределено 		  - склад остатков, если не заполнен, 
//																   			    тогда склад получается из табличной части
// 	АдресВоВременномХранилище - УникальныйИдентификатор, Строка, Неопределено - адрес временного хранилища
//
// Возвращаемое значение:
// 	Строка - адрес временного хранилища таблицы значение 
// 
Функция ПоместитьВоВременноеХранилищеКорректировкиОстатков(
			МассивСсылок, Таблица, ИмяТабличнойЧасти, Склад = Неопределено, АдресВоВременномХранилище = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Количество КАК Количество,
	|	&Склад КАК Склад
	|	%1
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	ИСТИНА
	|	%2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатков.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаОстатков.Количество) КАК Количество,
	|	ТаблицаОстатков.Склад КАК Склад
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		- Таблица.Количество КАК Количество,
	|		Таблица.Склад КАК Склад
	|	ИЗ
	|		Таблица КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Количество КАК Количество,
	|		&Склад КАК Склад
	|	ИЗ
	|		%3 КАК Шапка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ %4.%5 КАК Таблица
	|			ПО Шапка.Ссылка = Таблица.Ссылка
	|	ГДЕ
	|		Шапка.Ссылка В (&МассивСсылок)
	|		И Шапка.Проведен
	|		%6) КАК ТаблицаОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатков.Номенклатура,
	|	ТаблицаОстатков.Характеристика,
	|	ТаблицаОстатков.Склад
	|";
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(МассивСсылок[0]);
	
	ОтборСтрок = МенеджерОбъекта.ОтборСтрокКорректировкиОстатков();
	
	ШаблонПолей = "%1 %2%3"; 
	ШаблонПолейВЗапросе = "%1%2%3%4%5";
	ШаблонУсловиеВЗапросе = "%1 %2%3 %4 %5%6%7";
	
	Для каждого ЭлементОтбора Из ОтборСтрок Цикл
		
		ШаблонПолей = СтрШаблон(ШаблонПолей, ",", ЭлементОтбора.Ключ, "%1 %2%3");
		
		ШаблонПолейВЗапросе = СтрШаблон(
			ШаблонПолейВЗапросе, 
			",", 
			Символы.ПС,
			"Таблица.",
			ЭлементОтбора.Ключ, 
			"%1%2%3%4%5");
		
		ШаблонУсловиеВЗапросе = СтрШаблон(
			ШаблонУсловиеВЗапросе, 
			"И", 
			"Таблица.", 
			ЭлементОтбора.Ключ, 
			ЭлементОтбора.Значение.ВидСравнения, 
			ЭлементОтбора.Значение.ПравоеЗначение,
			Символы.ПС,
			"%1 %2%3 %4 %5%6%7");
						
	КонецЦикла;
	
	ШаблонПолей = СокрП(СтрШаблон(ШаблонПолей, "", "", "")) + ?(Склад = Неопределено, ", Склад", "");
	ШаблонПолейВЗапросе = СтрШаблон(ШаблонПолейВЗапросе, "", "", "", "");
	ШаблонУсловиеВЗапросе = СокрП(СтрШаблон(ШаблонУсловиеВЗапросе, "", "", "", "", "", "", ""));
	
	ТекстЗапроса = СтрШаблон(
		ТекстЗапроса, 
		ШаблонПолейВЗапросе, 
		ШаблонУсловиеВЗапросе, 
		ОбщегоНазначения.ИмяТаблицыПоСсылке(МассивСсылок[0]), 
		ОбщегоНазначения.ИмяТаблицыПоСсылке(МассивСсылок[0]),
		ИмяТабличнойЧасти,
		ШаблонУсловиеВЗапросе);
		
	Запрос = Новый Запрос;
	Запрос.Текст = ?(Склад = Неопределено, СтрЗаменить(ТекстЗапроса, "&Склад", "Таблица.Склад"), ТекстЗапроса);
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("Таблица", Таблица.Выгрузить(, "Номенклатура, Характеристика, Количество" + ШаблонПолей));
	
	Если Склад <> Неопределено Тогда
		Запрос.УстановитьПараметр("Склад", Склад);	
	КонецЕсли;	
	
	Возврат ПоместитьВоВременноеХранилище(Запрос.Выполнить().Выгрузить(), АдресВоВременномХранилище);
	
КонецФункции	

// Получает таблицу товаров из временного хранилища
//
// Параметры:
//	Параметры - Структура - структура, которая содержит элемент со значением адреса временного хранилища
//	ИмяКлюча  - Строка    - Имя ключа элемента структуры со значением адреса временного хранилища, по умолчанию "Товары"
// 
// Возвращаемое значение:
//	ТаблицаЗначений - таблица товаров.
//
Функция ПолучитьТаблицуТоваров(Параметры, ИмяКлюча = "Товары") Экспорт
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(Параметры[ИмяКлюча]);
	
	Параметры.Удалить(ИмяКлюча);
	
	Для каждого ПараметрТоваров Из Параметры Цикл
		Если ЗначениеЗаполнено(ПараметрТоваров.Значение) Тогда
			ТаблицаТоваров.ЗаполнитьЗначения(ПараметрТоваров.Значение, ПараметрТоваров.Ключ);	
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ТаблицаТоваров;
	
КонецФункции

// Получает данные из временного хранилища 
// и заполняет полученными данными значение элемента структуры с этим ключом,
// в случаи отсутствия адреса временного хранилище, удаляет элемент структуры
//
// Параметры:
//	Параметры - Структура - структура, которая содержит элемент с ключом 
//							и значением адреса временного хранилища
//	Ключ      - Строка    - имя ключа
//
Процедура ЗаполнитьДанныеПараметра(Параметры, Ключ) Экспорт  
	
	Данные = ?(
		ЭтоАдресВременногоХранилища(Параметры[Ключ]), ПолучитьИзВременногоХранилища(Параметры[Ключ]), Неопределено);
	
	Если Данные = Неопределено Тогда
		Параметры.Удалить(Ключ);	
	Иначе
		Параметры[Ключ] = Данные;
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область ЗаменаНаАналоги

// Выполняет замену материалов на подобранные аналоги
//
// Параметры:
//  ТабличнаяЧасть	   	  - ДанныеФормыКоллекция - табличная часть в которой требуется выполнить замену
//  АдресВХранилище	   	  - Строка				 - адрес хранилища в котором находятся подобранные аналоги
//  ДополнительныеПоля 	  - Строка, Неопределено - поля таблицы, которые нужно заполнить в строках с заменяемыми аналогами, 
//												   на основании строк замененных материалов
//  РезервироватьНаСкладе - Булево               - Истина: аналоги резервируются на складе с учетом свободного остатка
//														   (значение реквизита "ВариантОбеспечения" устанавливается в "СоСклада")
//												   Ложь: аналоги не резервируются на складе
// 
// Возвращаемое значение:
//  Структура - содержит поля:
//  * ИндексыНовыхСтрок		 - Массив - содержит индексы новых строк (аналоги)
//  * ИндексыИзмененныхСтрок - Массив - содержит индексы измененных строк (материалы)
//
Функция ВыполнитьЗаменуНаАналоги(
			ТабличнаяЧасть, АдресВХранилище, ДополнительныеПоля = Неопределено, РезервироватьНаСкладе = Ложь) Экспорт
	
	ТаблицаЗамен = ПолучитьИзВременногоХранилища(АдресВХранилище);//ТаблицаЗначений - 
	ТаблицаЗамен.Индексы.Добавить("ЭтоАналог");
	
	Если РезервироватьНаСкладе Тогда
		
		ТаблицаЗамен.Колонки.Добавить("ВариантОбеспечения", Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОбеспечения"));
		ТаблицаЗамен.Колонки.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
		ТаблицаЗамен.Индексы.Добавить("ЭтоАналог, Обработано, НомерПодбора");
		ТаблицаЗамен.Индексы.Добавить("ЭтоАналог, Обработано, НомерПодбора, Номенклатура, Характеристика");
		
		СтруктураОтбора = Новый Структура("ЭтоАналог, Обработано, НомерПодбора", Истина, Ложь);
		СтруктураОтбораПоНоменклатуре = Новый Структура(
			"ЭтоАналог, Обработано, НомерПодбора, Номенклатура, Характеристика", Истина, Ложь);
		
		МассивНомеровПодбора = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаЗамен.ВыгрузитьКолонку("НомерПодбора"));
		Для каждого НомерПодбора Из МассивНомеровПодбора Цикл
			
			СтруктураОтбора.НомерПодбора = НомерПодбора;
			
			Пока Истина Цикл
				
				МассивНеОбработанныхСтрок = ТаблицаЗамен.НайтиСтроки(СтруктураОтбора);
				Если МассивНеОбработанныхСтрок.Количество() = 0 Тогда
					Прервать;
				Иначе
					
					ЗаполнитьЗначенияСвойств(СтруктураОтбораПоНоменклатуре, МассивНеОбработанныхСтрок[0]);
					МассивСтрок = ТаблицаЗамен.НайтиСтроки(СтруктураОтбораПоНоменклатуре);
					
					ОстатокНаСкладе = МассивСтрок[0].ОстатокНаСкладе;
					Для каждого СтрокаТаблицы Из МассивСтрок Цикл
						
						Если ОстатокНаСкладе = 0 Тогда
							
							СтрокаТаблицы.Обработано = Истина;
							
						ИначеЕсли СтрокаТаблицы.Количество > ОстатокНаСкладе Тогда
							
							НоваяСтрокаТаблицы = ТаблицаЗамен.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, СтрокаТаблицы,, "Количество");
							НоваяСтрокаТаблицы.Количество = СтрокаТаблицы.Количество - ОстатокНаСкладе;
							НоваяСтрокаТаблицы.Обработано = Истина;
							
							СтрокаТаблицы.Количество = ОстатокНаСкладе;
							СтрокаТаблицы.Обработано = Истина;
							СтрокаТаблицы.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.СоСклада;
							
							ОстатокНаСкладе = 0;
							
						Иначе
							
							СтрокаТаблицы.Обработано = Истина;
							СтрокаТаблицы.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.СоСклада;
							
							ОстатокНаСкладе = ОстатокНаСкладе - СтрокаТаблицы.Количество;	
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;	
	
	Если ДополнительныеПоля <> Неопределено Тогда
		
		МассивДополнительныхПолей = СтрРазделить(ДополнительныеПоля, ", ", Ложь);
		Для каждого ДополнительноеПоле Из МассивДополнительныхПолей Цикл
			Если ТаблицаЗамен.Колонки.Найти(ДополнительноеПоле) = Неопределено Тогда
				ТаблицаЗамен.Колонки.Добавить(ДополнительноеПоле);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаЗамен.Индексы.Добавить("ЭтоАналог, КлючСвязи");
		
		МассивПолей = СтрРазделить(ДополнительныеПоля, ",");
		МассивКлючей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаЗамен.ВыгрузитьКолонку("КлючСвязи"));
		
		Для каждого Ключ Из МассивКлючей Цикл
			
			СтруктураПолей = Новый Структура(ДополнительныеПоля);
			Для каждого Поле Из СтруктураПолей Цикл
				СтруктураПолей.Вставить(Поле.Ключ, Новый Массив);	
			КонецЦикла;	
			
			МассивНомеровСтрок = СтрРазделить(Ключ, ";", Ложь);
			Для каждого НомерСтроки Из МассивНомеровСтрок Цикл
				
				СтрокаТЧ = ТабличнаяЧасть[НомерСтроки - 1];
				Для каждого Поле Из СтруктураПолей Цикл
					ЗначениеКлюча = Поле.Значение;//Массив - 
					ЗначениеКлюча.Добавить(СтрокаТЧ[Поле.Ключ]);	
				КонецЦикла;	
				
			КонецЦикла;
			
			Для каждого Поле Из СтруктураПолей Цикл
				
				МассивЗначений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Поле.Значение);
				Если МассивЗначений.Количество() = 1 Тогда
					
					МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог, КлючСвязи", Истина, Ключ));
					Для каждого СтрокаТаблицы Из МассивСтрок Цикл
						СтрокаТаблицы[Поле.Ключ] = МассивЗначений[0];	
					КонецЦикла;	
					
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЦикла;	
		
	КонецЕсли;
	
	ИндексыНовыхСтрок = Новый Массив;
	ИндексыИзмененныхСтрок = Новый Массив;
	КоличествоУдаленныхСтрок = 0;
	
	ТаблицаЗамен.Сортировать("НомерСтрокиИсходный");
	
	МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог", Ложь));
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		ИндексСтроки = СтрокаТаблицы.НомерСтрокиИсходный - 1 - КоличествоУдаленныхСтрок;
		
		СтрокаТЧ = ТабличнаяЧасть[ИндексСтроки];
		Если СтрокаТЧ.Количество = СтрокаТаблицы.Количество Тогда
			
			ТабличнаяЧасть.Удалить(СтрокаТЧ);
			
			ИндексМассива = ИндексыИзмененныхСтрок.Найти(ИндексСтроки);
			Если ИндексМассива <> Неопределено Тогда
				ИндексыИзмененныхСтрок.Удалить(ИндексМассива);	
			КонецЕсли;
			
			КоличествоУдаленныхСтрок = КоличествоУдаленныхСтрок + 1;
				
		Иначе
			
			СтрокаТЧ.Количество = СтрокаТЧ.Количество - СтрокаТаблицы.Количество;
			СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
			СтрокаТЧ.Упаковка = Неопределено;
			
			ИндексМассива = ИндексыИзмененныхСтрок.Найти(ИндексСтроки);
			Если ИндексМассива = Неопределено Тогда
				ИндексыИзмененныхСтрок.Добавить(ИндексСтроки);
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;
	
	КолонкиГруппировок = "Номенклатура, Характеристика, Склад, ЭтоАналог%1";
	
	Если РезервироватьНаСкладе Тогда
		КолонкиГруппировок = СтрШаблон(КолонкиГруппировок, ", ВариантОбеспечения%1");
	КонецЕсли;
	
	Если ДополнительныеПоля <> Неопределено Тогда
		МассивПолейГруппировки = СтрРазделить(Лев(КолонкиГруппировок, СтрДлина(КолонкиГруппировок) - 2), ", ", Ложь);
		Для каждого ДополнительноеПоле Из МассивДополнительныхПолей Цикл
			Если МассивПолейГруппировки.Найти(ДополнительноеПоле) = Неопределено Тогда
				КолонкиГруппировок = СтрШаблон(КолонкиГруппировок, ", " + ДополнительноеПоле + "%1");	
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
	ТаблицаЗамен.Свернуть(СтрШаблон(КолонкиГруппировок, ""), "Количество");
	
	МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог", Истина));
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		СтрокаТЧ = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТаблицы);
		СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
		
		ИндексыНовыхСтрок.Добавить(СтрокаТЧ.НомерСтроки - 1);
		
	КонецЦикла;
	
	Возврат Новый Структура("ИндексыНовыхСтрок, ИндексыИзмененныхСтрок", ИндексыНовыхСтрок, ИндексыИзмененныхСтрок);
	
КонецФункции	

// Заменяет материалы на аналоги с учетом приоритета и свободного остатка
//
// Параметры:
//  ТабличнаяЧасть	   	  	- ДанныеФормыКоллекция - табличная часть в которой требуется выполнить замену.
//	ПараметрыВыбораАналогов - см. АналогиМатериалов.ПараметрыВыбораАналогов
//  ДополнительныеПоля 	  	- Строка, Неопределено - поля таблицы, которые нужно заполнить в строках с заменяемыми аналогами, 
//												   	 на основании строк замененных материалов.
//  РезервироватьНаСкладе 	- Булево               - Истина: аналоги резервируются на складе с учетом свободного остатка
//														   (значение реквизита "ВариантОбеспечения" устанавливается в "СоСклада")
//												   	 Ложь: аналоги не резервируются на складе.
// 
// Возвращаемое значение:
//  Структура - содержит поля:
//  * ИндексыНовыхСтрок		 - Массив - содержит индексы новых строк (аналоги)
//  * ИндексыИзмененныхСтрок - Массив - содержит индексы измененных строк (материалы)
//
Функция ВыполнитьЗаменуНаАналогиАвтоматически(
			ТабличнаяЧасть, ПараметрыВыбораАналогов, ДополнительныеПоля = Неопределено, РезервироватьНаСкладе = Ложь) Экспорт
	
	СтруктураСтрок = Новый Структура("ИндексыНовыхСтрок, ИндексыИзмененныхСтрок", Новый Массив, Новый Массив);
	
	ТаблицаЗамен = ПолучитьТаблицуЗамены(ПараметрыВыбораАналогов,, РезервироватьНаСкладе);
	Если ТаблицаЗамен = Неопределено Тогда
		Возврат СтруктураСтрок;	
	КонецЕсли;
	
	КолонкиГруппировок = "Ссылка, НомерСтроки, Номенклатура, Характеристика, Склад, ЭтоАналог" 
						 + ?(РезервироватьНаСкладе,", ВариантОбеспечения", "");
	
	ТаблицаЗамен.Свернуть(КолонкиГруппировок, "Количество");
	
	ЗначенияДополнительныхПолей = Новый Структура;
	
	Если ДополнительныеПоля <> Неопределено Тогда
		
		СтруктураПолей = Новый Структура(ДополнительныеПоля);
		Для каждого Поле Из СтруктураПолей Цикл
			СтруктураПолей.Вставить(Поле.Ключ, Новый Массив);	
		КонецЦикла;	
		
		МассивНомеровСтрок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаЗамен.ВыгрузитьКолонку("НомерСтроки"));
		МассивНомеровСтрок.Удалить(МассивНомеровСтрок.Найти(0));
		
		Для каждого НомерСтроки Из МассивНомеровСтрок Цикл
			
			СтрокаТЧ = ТабличнаяЧасть[НомерСтроки - 1];
			Для каждого Поле Из СтруктураПолей Цикл
				ЗначениеКлюча = Поле.Значение;//Массив - 
				ЗначениеКлюча.Добавить(СтрокаТЧ[Поле.Ключ]);	
			КонецЦикла;	
			
		КонецЦикла;
		
		Для каждого Поле Из СтруктураПолей Цикл
			
			МассивЗначений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Поле.Значение);
			Если МассивЗначений.Количество() = 1 Тогда
				ЗначенияДополнительныхПолей.Вставить(Поле.Ключ, МассивЗначений[0]);
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;
	
	
	ТаблицаЗамен.Сортировать("НомерСтроки Убыв");
	
	МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог", Ложь));
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		ИндексСтроки = СтрокаТаблицы.НомерСтроки - 1;
		СтрокаТЧ = ТабличнаяЧасть[ИндексСтроки];
		
		Если СтрокаТаблицы.Количество = 0 Тогда
			ТабличнаяЧасть.Удалить(СтрокаТЧ);
		Иначе
			
			СтрокаТЧ.Количество = СтрокаТаблицы.Количество;
			СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
			СтрокаТЧ.Упаковка = Неопределено;
			
			СтруктураСтрок.ИндексыИзмененныхСтрок.Добавить(ИндексСтроки);
			
		КонецЕсли;	
		
	КонецЦикла;
	
	МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог", Истина));
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		СтрокаТЧ = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТаблицы,, "НомерСтроки");
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ЗначенияДополнительныхПолей);
		СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
		
		СтруктураСтрок.ИндексыНовыхСтрок.Добавить(СтрокаТЧ.НомерСтроки - 1);
		
	КонецЦикла;
	
	Возврат СтруктураСтрок;
	
КонецФункции	

// Получает таблицу замены для формирования этапов.
//
// Параметры:
//  Товары  - ТаблицаЗначений - таблица материалов с колонками:
//             * Идентификатор           - Число - идентификатор.
//             * Номенклатура            - СправочникСсылка.Номенклатура -
//             * Характеристика          - СправочникСсылка.ХарактеристикиНоменклатуры -
//             * Подразделение           - СправочникСсылка.СтруктураПредприятия -
//             * НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности -
//             * ЗаказНаПроизводство     - ДокументСсылка.ЗаказНаПроизводство2_2 -
//             * Спецификация            - СправочникСсылка.РесурсныеСпецификации -
//             * КлючСвязиСпецификация   - УникальныйИдентификатор -
//             * Изделие                 - СправочникСсылка.Номенклатура -
//             * ХарактеристикаИзделия   - СправочникСсылка.ХарактеристикиНоменклатуры -
//             * Количество              - Число -
//  Резервы - ТаблицаЗначений, Неопределено - таблица резервов с колонками:
//             * Номенклатура - СправочникСсылка.Номенклатура -
//             * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
//             * Склад - СправочникСсылка.Склады -
//             * Подразделение - СправочникСсылка.СтруктураПредприятия -
//             * Назначение - СправочникСсылка.Назначения -
//             * ТипНоменклатуры - ПеречислениеСсылка.ТипыНоменклатуры -
//             * ВариантОбеспечения - ПеречислениеСсылка.ВариантыОбеспечения -
//             * ДатаОтгрузки - Дата -
//             * ДатаОтгрузкиРабот - Дата -
//             * Количество - Число -
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица замены.
//
Функция ПолучитьТаблицуЗаменыДляФормированияЭтапов(Товары, Резервы = Неопределено) Экспорт
	
	ТипыРазрешений = Новый Массив;
	ТипыРазрешений.Добавить(Тип("ДокументСсылка.РазрешениеНаЗаменуМатериалов"));
	ТипыРазрешений.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	
	ТаблицаРезультатЗамен = Новый ТаблицаЗначений;
	ТаблицаРезультатЗамен.Колонки.Добавить("Идентификатор", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
	ТаблицаРезультатЗамен.Колонки.Добавить("НомерЗамены", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
	ТаблицаРезультатЗамен.Колонки.Добавить("Разрешение", Новый ОписаниеТипов(ТипыРазрешений));
	ТаблицаРезультатЗамен.Колонки.Добавить("КоличествоЗамены", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаРезультатЗамен.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаРезультатЗамен.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаРезультатЗамен.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаРезультатЗамен.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	Если Товары.Количество() = 0 Тогда
		Возврат ТаблицаРезультатЗамен;	
	КонецЕсли;
	
	ТипИдентификатора = ОбщегоНазначения.ОписаниеТипаЧисло(15, 0);
	
	ПараметрыВыбораАналогов = АналогиМатериалов.ПараметрыВыбораАналогов(ТипИдентификатора);
	
	ТаблицаДанных = Товары.Скопировать();
	ТаблицаДанных.Колонки.Добавить("Ссылка", ТипИдентификатора);
	ТаблицаДанных.Колонки.Добавить("НомерСтроки", ТипИдентификатора);
	
	Для каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		СтрокаТаблицы.Ссылка = СтрокаТаблицы.Идентификатор;
		СтрокаТаблицы.НомерСтроки = СтрокаТаблицы.Идентификатор + 1;
	КонецЦикла;	
	
	ИсходныеДанные = Новый Структура(
		"ТаблицаДанных, УникальныйИдентификатор", ТаблицаДанных, Новый УникальныйИдентификатор);
	
	СтруктураПолей = СтруктураПолейТаблицыДляЗаменыНаАналоги();
	СтруктураПолей.ОсновныеПоля = "Ссылка, " + Символы.ПС + СтруктураПолей.ОсновныеПоля;
	
	ТоварыАдресВХранилище = ПроизводствоСервер.ПоместитьВоВременноеХранилищеДанныеСтрок(
		ИсходныеДанные, Неопределено, "ТаблицаДанных", Ложь, СтруктураПолей);			
	
	ДанныеТоваров = Новый Структура;
	ДанныеТоваров.Вставить("Товары", ТоварыАдресВХранилище);
	
	Если Резервы <> Неопределено И Резервы.Количество() > 0 Тогда
		
		ТаблицаКорректировкиОстатков = Резервы.Скопировать();
		ТаблицаКорректировкиОстатков.Колонки.Количество.Имя = "КоличествоИсходное";
		ТаблицаКорректировкиОстатков.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
		
		Для каждого СтрокаТаблицы Из ТаблицаКорректировкиОстатков Цикл
			СтрокаТаблицы.Количество = - СтрокаТаблицы.КоличествоИсходное;	
		КонецЦикла;
		
		ТаблицаКорректировкиОстатков.Колонки.Удалить("КоличествоИсходное");
		
		ДанныеТоваров.Вставить("КорректировкиОстатков", ПоместитьВоВременноеХранилище(ТаблицаКорректировкиОстатков));
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыВыбораАналогов.ПараметрыТоваров, ДанныеТоваров);
	ЗаполнитьЗначенияСвойств(ПараметрыВыбораАналогов.ПараметрыАналогов, ДанныеТоваров);
	
	ДатаДействияРазрешений = НачалоДня(ТекущаяДатаСеанса());
	ТаблицаПараметров = ПараметрыВыбораАналогов.ПараметрыАналогов.ТаблицаПараметров;
	Для каждого СтрокаТовары Из Товары Цикл
		СтрокаТаблицы = ТаблицаПараметров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТовары);
		СтрокаТаблицы.ДатаДействияРазрешений = ДатаДействияРазрешений;
		СтрокаТаблицы.Ссылка = СтрокаТовары.Идентификатор;
	КонецЦикла;	
	
	ПараметрыВыбораАналогов.ПараметрыАналогов.ТаблицаПараметров = ПоместитьВоВременноеХранилище(ТаблицаПараметров);
	
	ТаблицаЗамен = ПолучитьТаблицуЗамены(ПараметрыВыбораАналогов, Ложь, Ложь);
	Если ТаблицаЗамен = Неопределено Тогда
		Возврат ТаблицаРезультатЗамен;
	КонецЕсли;
	
	ТаблицаЗамен.Индексы.Добавить("ЭтоАналог");
	МассивСтрок = ТаблицаЗамен.НайтиСтроки(Новый Структура("ЭтоАналог", Истина));
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		
		НомерЗамены = 0;
		КоличествоЗамены = 0;
		
		Для каждого ЭлементСтрокаКоличество Из СтрокаТаблицы.ЗамененноеКоличествоПострочно Цикл
			НомерЗамены = ЭлементСтрокаКоличество.Ключ - 1;
			КоличествоЗамены = ЭлементСтрокаКоличество.Значение;
			Прервать;
		КонецЦикла;	
		
		СтрокаРезультат = ТаблицаРезультатЗамен.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаТаблицы);
		СтрокаРезультат.Идентификатор = СтрокаТаблицы.Ссылка;
		СтрокаРезультат.НомерЗамены = НомерЗамены;
		СтрокаРезультат.КоличествоЗамены = КоличествоЗамены;
		
	КонецЦикла;
	
	Возврат ТаблицаРезультатЗамен;
		
КонецФункции

// Получает таблицу замены
//
// Параметры:
//	ПараметрыВыбораАналогов 	   - см. АналогиМатериалов.ПараметрыВыбораАналогов
//  УчестьРазрешенияМногиеКоМногим - Булево    - Истина: разрешения многие ко многим учитываются
//												 Ложь: разрешения многие ко многим игнорируются.
//  РезервироватьНаСкладе 		   - Булево    - Истина: аналоги резервируются на складе с учетом свободного остатка
//												   	     (значение реквизита "ВариантОбеспечения" устанавливается в "СоСклада")
//												 Ложь: аналоги не резервируются на складе.
// 
// Возвращаемое значение:
//  - Неопределено    - возможность замены отсутствует.
//  - ТаблицаЗначений - таблица замены с колонками:
//    * НомерСтроки                   - Число - номер строки.
//    * Ссылка                        - ДокументСсылка - документ источник.
//    * Номенклатура                  - СправочникСсылка.Номенклатура - номенклатура.
//    * Характеристика                - СправочникСсылка.ХарактеристикиНоменклатуры - характеристирк номенклатуры.
//    * Количество                    - Число - количество.
//    * Склад                         - СправочникСсылка.Склады - склад.
//    * Разрешение                    - ДокументСсылка.РазрешениеНаЗаменуМатериалов, ДокументСсылка.КорректировкаРегистров - разрешение.
//    * ЗамененноеКоличествоПострочно - Соответствие - Ключ: номер строки материала,
//                                                     Значение: замененное количество.
//    * ЭтоАналог                     - Булево - признак аналога.
//    * ВариантОбеспечения            - ПеречислениеСсылка.ВариантыОбеспечения - при резервирование на складе.
//
Функция ПолучитьТаблицуЗамены(
			ПараметрыВыбораАналогов, УчестьРазрешенияМногиеКоМногим = Истина, РезервироватьНаСкладе = Ложь) Экспорт
			
	ТаблицаТоваров = АналогиМатериалов.ПолучитьТаблицуТоваров(ПараметрыВыбораАналогов.ПараметрыТоваров);
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыАналогов = ПараметрыВыбораАналогов.ПараметрыАналогов;
	
	ЗаполнитьДанныеПараметра(ПараметрыАналогов, "ТаблицаПараметров");
	ЗаполнитьДанныеПараметра(ПараметрыАналогов, "КорректировкиОстатков");
	
	ПолучитьСкладИзОбеспечение = ПараметрыВыбораАналогов.ПараметрыАналогов.Склад = Неопределено;
	
	Если ПолучитьСкладИзОбеспечение Тогда
		ПараметрыАналогов.Удалить("Склад");
		ПараметрыАналогов.Вставить("СкладПоУмолчанию", Справочники.Склады.СкладПоУмолчанию());
	КонецЕсли;	
	
	ПараметрыОтборовОстатки = Новый Структура(
		"КорректироватьОстатки, ТекстыЗаменВЗапросах", 
		ПараметрыАналогов.Свойство("КорректировкиОстатков"), 
		Новый Соответствие);
		
	ПараметрыОтборовОстатки.ТекстыЗаменВЗапросах.Вставить("//ОтборПоАналогам_", "");
	
	ПараметрыОтборовРазрешения = Новый Структура("ТекстыЗаменВЗапросах", Новый Соответствие);
	ПараметрыОтборовРазрешенияПоПриоритетам = Новый Структура("ТекстыЗаменВЗапросах", Новый Соответствие);
	Если УчестьРазрешенияМногиеКоМногим Тогда
		ПараметрыОтборовРазрешения.ТекстыЗаменВЗапросах.Вставить("//УчестьМногиеКоМногим_");
		ПараметрыОтборовРазрешенияПоПриоритетам.ТекстыЗаменВЗапросах.Вставить("//УчестьМногиеКоМногим_");
	КонецЕсли;
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаТовары());
	МассивТекстов.Добавить(ТекстЗапросаТаблицаМатериалов(Истина));
	МассивТекстов.Добавить(ТекстЗапросаДоступныеРазрешенияМногиеКоМногим(ПараметрыОтборовРазрешения));
	МассивТекстов.Добавить(ТекстЗапросаТаблицаАналогов(ПолучитьСкладИзОбеспечение));
	МассивТекстов.Добавить(ТекстЗапросаТаблицаОстатков(ПараметрыОтборовОстатки));
	МассивТекстов.Добавить(УправлениеДаннымиОбИзделиях.ТекстЗапросаДанныеУпаковок(СтрРазделить("Материалы,Аналоги",",")));
	МассивТекстов.Добавить(ТекстЗапросаРазрешенияПоПриоритетам(ПараметрыОтборовРазрешенияПоПриоритетам));
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТоваров);
	Запрос.УстановитьПараметр(
		"ПустойУникальныйИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Для каждого ПараметрАналогов Из ПараметрыАналогов Цикл
		Запрос.УстановитьПараметр(ПараметрАналогов.Ключ, ПараметрАналогов.Значение);
	КонецЦикла;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();  
	
	ТаблицаМатериаловРазрешений = МассивРезультатов[МассивРезультатов.ВГраница() - 2].Выгрузить();
	Если ТаблицаМатериаловРазрешений.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаМатериаловРазрешений.Индексы.Добавить("Разрешение, Ссылка");
	ТаблицаМатериаловРазрешений.Индексы.Добавить("Разрешение, КонтролироватьКратность");
	
	ТаблицаАналоговРазрешений = МассивРезультатов[МассивРезультатов.ВГраница() - 1].Выгрузить();
	ТаблицаАналоговРазрешений.Индексы.Добавить("Разрешение, Ссылка");
	ТаблицаАналоговРазрешений.Индексы.Добавить("ОстатокНаСкладе");
	
	ТаблицаРазрешенийПоПриоритетам = МассивРезультатов[МассивРезультатов.ВГраница()].Выгрузить(); // см. СлужебнаяСтруктураТаблицыЗначений
	ТаблицаРазрешенийПоПриоритетам.Индексы.Добавить("Разрешение, Ссылка");
	ТаблицаРазрешенийПоПриоритетам.Индексы.Добавить("НомерСтроки");
	
	ТаблицаДанныеУпаковок = МассивРезультатов[МассивРезультатов.ВГраница() - 3].Выгрузить();
	УправлениеДаннымиОбИзделиях.ЗаполнитьДанныеУпаковокВНабореДанных(ТаблицаМатериаловРазрешений, ТаблицаДанныеУпаковок);
	УправлениеДаннымиОбИзделиях.ЗаполнитьДанныеУпаковокВНабореДанных(ТаблицаАналоговРазрешений, ТаблицаДанныеУпаковок);
	
	ТаблицаТоваровПоРазрешению = ТаблицаТоваров.СкопироватьКолонки("Номенклатура, Характеристика, КлючСвязиСпецификация, Количество");
	ТаблицаТоваровПоРазрешению.Колонки.Добавить("НомераСтрок", Новый ОписаниеТипов("Массив"));
	ТаблицаТоваровПоРазрешению.Индексы.Добавить("Номенклатура, Характеристика, КлючСвязиСпецификация");
	
	ТипыРазрешений = Новый Массив;
	ТипыРазрешений.Добавить(Тип("ДокументСсылка.РазрешениеНаЗаменуМатериалов"));
	ТипыРазрешений.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	
	ТаблицаЗамен = ТаблицаТоваров.СкопироватьКолонки("НомерСтроки, Ссылка, Номенклатура, Характеристика, Количество, Склад");
	ТаблицаЗамен.Колонки.Добавить("Разрешение", Новый ОписаниеТипов(ТипыРазрешений));
	ТаблицаЗамен.Колонки.Добавить("ЗамененноеКоличествоПострочно", Новый ОписаниеТипов("Соответствие"));
	ТаблицаЗамен.Колонки.Добавить("ЭтоАналог", Новый ОписаниеТипов("Булево"));
	
	Если РезервироватьНаСкладе Тогда
		ТаблицаЗамен.Колонки.Добавить("ВариантОбеспечения", Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОбеспечения"));
	КонецЕсли;
	
	ТаблицаЗамен.Индексы.Добавить("НомерСтроки");
	
	ОтборПоРазрешению = Новый Структура("Разрешение, Ссылка");
	ОтборПоКратности = Новый Структура("Разрешение, КонтролироватьКратность", Неопределено, Истина);
	ОтборПоНоменклатуре = Новый Структура("Номенклатура, Характеристика, КлючСвязиСпецификация");
			
	Пока ТаблицаРазрешенийПоПриоритетам.Количество() > 0 Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборПоКратности, ТаблицаРазрешенийПоПриоритетам[0], "Разрешение");
		КонтролироватьКратность = ТаблицаМатериаловРазрешений.НайтиСтроки(ОтборПоКратности).Количество() > 0;
		
		ЗаполнитьЗначенияСвойств(ОтборПоРазрешению, ТаблицаРазрешенийПоПриоритетам[0]);
		
		ТаблицаТоваровПоРазрешению.Очистить();
		
		МассивСтрокРазрешения = ТаблицаРазрешенийПоПриоритетам.НайтиСтроки(ОтборПоРазрешению);
		Для каждого СтрокаТаблицы Из МассивСтрокРазрешения Цикл
			
			ЗаполнитьЗначенияСвойств(ОтборПоНоменклатуре, СтрокаТаблицы);
			
			МассивСтрокТовары = ТаблицаТоваровПоРазрешению.НайтиСтроки(ОтборПоНоменклатуре);
			Если МассивСтрокТовары.Количество() > 0 Тогда
				СтрокаТовары = МассивСтрокТовары[0];
			Иначе
				СтрокаТовары = ТаблицаТоваровПоРазрешению.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, ОтборПоНоменклатуре); 
			КонецЕсли;
			
			СтрокаТовары.Количество = СтрокаТовары.Количество + СтрокаТаблицы.Количество;
			
			МассивНомеровСтрок = СтрокаТовары.НомераСтрок; // Массив - 
			МассивНомеровСтрок.Добавить(СтрокаТаблицы.НомерСтроки);
			
		КонецЦикла;
			
		МассивКоэффициентов = Новый Массив;
		
		МассивСтрокМатериалы = ТаблицаМатериаловРазрешений.НайтиСтроки(ОтборПоРазрешению);
		Для каждого СтрокаТаблицы Из МассивСтрокМатериалы Цикл
			
			ЗаполнитьЗначенияСвойств(ОтборПоНоменклатуре, СтрокаТаблицы);
			
			МассивСтрокТоваровПоРазрешению = ТаблицаТоваровПоРазрешению.НайтиСтроки(ОтборПоНоменклатуре);
			Если МассивСтрокТоваровПоРазрешению.Количество() > 0 Тогда
			
				КоэффициентРасчета = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
					КонтролироватьКратность,
					МассивСтрокТоваровПоРазрешению[0].Количество,
					СтрокаТаблицы.Норматив,
					СтрокаТаблицы.ДанныеУпаковки);
			
			Иначе
				КоэффициентРасчета = 0;
			КонецЕсли;
			
			КоэффициентВыбран = МассивКоэффициентов.Количество() > 0;
			
			Если Не КоэффициентВыбран 
			 Или КоэффициентВыбран И МассивКоэффициентов[0] > КоэффициентРасчета Тогда
				МассивКоэффициентов.Вставить(0, КоэффициентРасчета);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивСтрокАналоги = ТаблицаАналоговРазрешений.НайтиСтроки(ОтборПоРазрешению);
		Для каждого СтрокаТаблицы Из МассивСтрокАналоги Цикл
			
			КоэффициентРасчета = АналогиМатериаловКлиентСервер.КоэффициентПоНормативу(
				КонтролироватьКратность,
				СтрокаТаблицы.ОстатокНаСкладе,
				СтрокаТаблицы.Норматив,
				СтрокаТаблицы.ДанныеУпаковки);
			
			Если МассивКоэффициентов[0] > КоэффициентРасчета Тогда
				МассивКоэффициентов.Вставить(0, КоэффициентРасчета);
			КонецЕсли;
			
		КонецЦикла;
		
		КоэффициентРасчета = МассивКоэффициентов[0];
		
		Если КоэффициентРасчета > 0 Тогда
			
			ЗамененноеКоличествоПострочно = Новый Соответствие;//Ключ: номер строки материала; Значение: замененное количество
		
			Для каждого СтрокаТаблицы Из МассивСтрокМатериалы Цикл
				
				КоличествоКРаспределению = КоэффициентРасчета * СтрокаТаблицы.Норматив
					* СтрокаТаблицы.ДанныеУпаковки.Числитель / СтрокаТаблицы.ДанныеУпаковки.Знаменатель;
				
				ЗаполнитьЗначенияСвойств(ОтборПоНоменклатуре, СтрокаТаблицы);
				МассивНомеровСтрок = ТаблицаТоваровПоРазрешению.НайтиСтроки(ОтборПоНоменклатуре)[0].НомераСтрок;
				Для каждого НомерСтроки Из МассивНомеровСтрок Цикл
					
					ОтборПоНомеру = Новый Структура("НомерСтроки", НомерСтроки);
					
					МассивСтрокПоНомеру = ТаблицаРазрешенийПоПриоритетам.НайтиСтроки(ОтборПоНомеру);
					
					КоличествоМатериала = Мин(МассивСтрокПоНомеру[0].Количество, КоличествоКРаспределению);
					
					Если КоличествоМатериала > 0 Тогда
						ЗамененноеКоличествоПострочно.Вставить(НомерСтроки, КоличествоМатериала);
					КонецЕсли;
					
					Для каждого СтрокаПоПриоритетам Из МассивСтрокПоНомеру Цикл
						СтрокаПоПриоритетам.Количество = СтрокаПоПриоритетам.Количество - КоличествоМатериала;
					КонецЦикла;
					
					МассивСтрокТаблицыЗамен = ТаблицаЗамен.НайтиСтроки(ОтборПоНомеру);
					СтрокаТаблицыЗамен = 
						?(МассивСтрокТаблицыЗамен.Количество() > 0, МассивСтрокТаблицыЗамен[0], ТаблицаЗамен.Добавить()); 
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗамен, СтрокаПоПриоритетам,, "Разрешение");
						
					Если СтрокаПоПриоритетам.Количество = 0 Тогда
						ИндексСтроки = МассивСтрокПоНомеру.ВГраница();
						Пока ИндексСтроки > -1 Цикл
							ТаблицаРазрешенийПоПриоритетам.Удалить(МассивСтрокПоНомеру[ИндексСтроки]);
							ИндексСтроки = ИндексСтроки - 1;
						КонецЦикла;
					КонецЕсли;
					
					КоличествоКРаспределению = КоличествоКРаспределению - КоличествоМатериала;
					Если КоличествоКРаспределению = 0 Тогда
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
			УдалитьРазрешенияПоАналогам = Ложь;
			Для каждого СтрокаТаблицы Из МассивСтрокАналоги Цикл
				
				КоличествоАналога = КоэффициентРасчета * СтрокаТаблицы.Норматив
					* СтрокаТаблицы.ДанныеУпаковки.Числитель / СтрокаТаблицы.ДанныеУпаковки.Знаменатель;
				
				ОтборПоСкладу = Новый Структура("Склад, Номенклатура, Характеристика");
				ЗаполнитьЗначенияСвойств(ОтборПоСкладу, СтрокаТаблицы);
				
				МассивСтрок = ТаблицаАналоговРазрешений.НайтиСтроки(ОтборПоСкладу);
				Для каждого СтрокаТаблицыАналогов Из МассивСтрок Цикл
					СтрокаТаблицыАналогов.ОстатокНаСкладе = СтрокаТаблицыАналогов.ОстатокНаСкладе - КоличествоАналога;	
				КонецЦикла;
				
				СтрокаТаблицыЗамен = ТаблицаЗамен.Добавить(); 
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗамен, СтрокаТаблицы);
				СтрокаТаблицыЗамен.Количество = КоличествоАналога;
				СтрокаТаблицыЗамен.ЗамененноеКоличествоПострочно = ЗамененноеКоличествоПострочно;
				СтрокаТаблицыЗамен.ЭтоАналог = Истина;
				
				Если РезервироватьНаСкладе Тогда
					СтрокаТаблицыЗамен.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.СоСклада;
				КонецЕсли;
				
				Если СтрокаТаблицыАналогов.ОстатокНаСкладе = 0 Тогда
					УдалитьРазрешенияПоАналогам = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Пока УдалитьРазрешенияПоАналогам Цикл
				
				МассивСтрок = ТаблицаАналоговРазрешений.НайтиСтроки(Новый Структура("ОстатокНаСкладе", 0));
				Если МассивСтрок.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ОтборПоРазрешению, МассивСтрок[0]);
				
				МассивУдаляемыхСтрок = ТаблицаАналоговРазрешений.НайтиСтроки(ОтборПоРазрешению);
				ИндексСтроки = МассивУдаляемыхСтрок.ВГраница();
				Пока ИндексСтроки > -1 Цикл
					ТаблицаАналоговРазрешений.Удалить(МассивУдаляемыхСтрок[ИндексСтроки]);
					ИндексСтроки = ИндексСтроки - 1;
				КонецЦикла;
				
				МассивУдаляемыхСтрок = ТаблицаРазрешенийПоПриоритетам.НайтиСтроки(ОтборПоРазрешению);
				ИндексСтроки = МассивУдаляемыхСтрок.ВГраница();
				Пока ИндексСтроки > -1 Цикл
					ТаблицаРазрешенийПоПриоритетам.Удалить(МассивУдаляемыхСтрок[ИндексСтроки]);
					ИндексСтроки = ИндексСтроки - 1;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		МассивУдаляемыхСтрок = ТаблицаРазрешенийПоПриоритетам.НайтиСтроки(ОтборПоРазрешению);
		ИндексСтроки = МассивУдаляемыхСтрок.ВГраница();
		Пока ИндексСтроки > -1 Цикл
			ТаблицаРазрешенийПоПриоритетам.Удалить(МассивУдаляемыхСтрок[ИндексСтроки]);
			ИндексСтроки = ИндексСтроки - 1;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ?(ТаблицаЗамен.Количество() = 0, Неопределено, ТаблицаЗамен);
	
КонецФункции	

#КонецОбласти

#Область НаличиеАналогов

// Заполняет признак наличия аналогов
//
// Параметры:
//  Таблица                 - ДанныеФормыКоллекция, ТаблицаЗначений - список в котором нужно заполнить признак "ЕстьАналогиМатериала", содержит:
//                             * НомерСтроки          - Число -
//                             * ЕстьАналогиМатериала - Булево -
//	ПараметрыВыбораАналогов - см. АналогиМатериалов.ПараметрыВыбораАналогов
//
Процедура ОтметитьНаличиеАналогов(Таблица, ПараметрыВыбораАналогов) Экспорт
	
	ТаблицаТоваров = ПолучитьТаблицуТоваров(ПараметрыВыбораАналогов.ПараметрыТоваров);
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыАналогов = ПараметрыВыбораАналогов.ПараметрыАналогов;
	
	ЗаполнитьДанныеПараметра(ПараметрыАналогов, "ТаблицаПараметров");
	
	ПараметрыОтборовРазрешения = Новый Структура("ТекстыЗаменВЗапросах", Новый Соответствие);
	ПараметрыОтборовРазрешения.ТекстыЗаменВЗапросах.Вставить("//УчестьМногиеКоМногим_");
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаТовары());
	МассивТекстов.Добавить(ТекстЗапросаТаблицаМатериалов());
	МассивТекстов.Добавить(ТекстЗапросаДоступныеРазрешенияМногиеКоМногим(ПараметрыОтборовРазрешения));
	МассивТекстов.Добавить(ТекстЗапросаНаличиеАналогов());
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТоваров);
	Запрос.УстановитьПараметр(
		"ПустойУникальныйИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Для каждого ПараметрАналогов Из ПараметрыАналогов Цикл
		Запрос.УстановитьПараметр(ПараметрАналогов.Ключ, ПараметрАналогов.Значение);
	КонецЦикла;	
	
	ТаблицаНаличиеАналогов = Запрос.Выполнить().Выгрузить();
	ТаблицаНаличиеАналогов.Индексы.Добавить("НомерСтроки");
	
	Для каждого СтрокаТаблицы Из Таблица Цикл
		СтрокаТаблицы.ЕстьАналогиМатериала = 
			ТаблицаНаличиеАналогов.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаТаблицы.НомерСтроки)).Количество() > 0;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросов

// Возвращает текст запроса временной таблицы "Товары"
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаТовары() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	Товары.Количество КАК Количество,
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	КлючСвязиСпецификация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса со временными таблицами определяющими таблицу материалов из доступных разрешений 
//
// Параметры:
//  РассчитатьПриоритетПрименения - Булево - Истина: рассчитывается приоритет применения
//                                           Ложь: приоритет применения не рассчитывается.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаТаблицаМатериалов(РассчитатьПриоритетПрименения = Ложь) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПараметров.Ссылка КАК Ссылка,
	|	ТаблицаПараметров.ДатаДействияРазрешений КАК ДатаДействияРазрешений,
	|	ТаблицаПараметров.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ТаблицаПараметров.ЗаказКлиента КАК ЗаказКлиента,
	|	ТаблицаПараметров.Спецификация КАК Спецификация,
	|	ТаблицаПараметров.Изделие КАК Изделие,
	|	ТаблицаПараметров.ХарактеристикаИзделия КАК ХарактеристикаИзделия,
	|	ТаблицаПараметров.Подразделение КАК Подразделение,
	|	ТаблицаПараметров.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ТаблицаПараметров
	|ИЗ
	|	&ТаблицаПараметров КАК ТаблицаПараметров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаДействияРазрешений,
	|	ЗаказНаПроизводство,
	|	ЗаказКлиента,
	|	Спецификация,
	|	Изделие,
	|	ХарактеристикаИзделия,
	|	Подразделение,
	|	НаправлениеДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПараметров.Ссылка КАК Ссылка,
	|	АналогиМатериалов.Период КАК Период,
	|	АналогиМатериалов.Регистратор КАК Разрешение,
	|	АналогиМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	АналогиМатериалов.Материал КАК Номенклатура,
	|	АналогиМатериалов.ХарактеристикаМатериала КАК Характеристика,
	|	АналогиМатериалов.КоличествоУпаковокМатериала КАК КоличествоУпаковок,
	|	АналогиМатериалов.УпаковкаМатериала КАК Упаковка,
	|	&Приоритет КАК Приоритет
	|ПОМЕСТИТЬ АналогиМатериалов
	|ИЗ
	|	РегистрСведений.АналогиМатериалов КАК АналогиМатериалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПараметров КАК ТаблицаПараметров
	|		ПО АналогиМатериалов.Период <= ТаблицаПараметров.ДатаДействияРазрешений
	|			И (АналогиМатериалов.ПериодЗавершения = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ АналогиМатериалов.ПериодЗавершения >= ТаблицаПараметров.ДатаДействияРазрешений)
	|			И ((ТаблицаПараметров.Ссылка, АналогиМатериалов.Материал, АналогиМатериалов.ХарактеристикаМатериала, АналогиМатериалов.КлючСвязиСпецификация) В
	|				(ВЫБРАТЬ
	|					Товары.Ссылка КАК Ссылка,
	|					Товары.Номенклатура КАК Номенклатура,
	|					Товары.Характеристика КАК Характеристика,
	|					ВЫБОР
	|						КОГДА АналогиМатериалов.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|								И АналогиМатериалов.Спецификация = ТаблицаПараметров.Спецификация
	|							ТОГДА Товары.КлючСвязиСпецификация
	|						ИНАЧЕ АналогиМатериалов.КлючСвязиСпецификация
	|					КОНЕЦ КАК КлючСвязиСпецификация
	|				ИЗ
	|					Товары КАК Товары))
	|			И (АналогиМатериалов.ЗаказНаПроизводство В (ТаблицаПараметров.ЗаказНаПроизводство, 
	|															ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка),
	//++ Устарело_Производство21
	|															ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка),
	//-- Устарело_Производство21
	|															НЕОПРЕДЕЛЕНО))
	|			И (АналогиМатериалов.ЗаказКлиента В (ТаблицаПараметров.ЗаказКлиента, ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)))
	|			И (АналогиМатериалов.Спецификация В (ТаблицаПараметров.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)))
	|			И (АналогиМатериалов.Изделие = ТаблицаПараметров.Изделие
	|					И АналогиМатериалов.ХарактеристикаИзделия = ТаблицаПараметров.ХарактеристикаИзделия
	|				ИЛИ АналогиМатериалов.Изделие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И АналогиМатериалов.ХарактеристикаИзделия = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (АналогиМатериалов.Подразделение В (ТаблицаПараметров.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|			И (АналогиМатериалов.НаправлениеДеятельности В (ТаблицаПараметров.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)))
	|ГДЕ
	|	ТаблицаПараметров.Ссылка ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Разрешение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналогиМатериалов.Разрешение КАК Ссылка,
	|	АналогиМатериалов.Номенклатура КАК Номенклатура,
	|	АналогиМатериалов.Характеристика КАК Характеристика,
	|	АналогиМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	АналогиМатериалов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	АналогиМатериалов.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ КорректировкаРегистровМатериалы
	|ИЗ
	|	АналогиМатериалов КАК АналогиМатериалов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(АналогиМатериалов.Разрешение) = ТИП(Документ.КорректировкаРегистров)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	КлючСвязиСпецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРегистровМатериалы.Ссылка КАК Ссылка,
	|	КорректировкаРегистровМатериалы.Номенклатура КАК Номенклатура,
	|	КорректировкаРегистровМатериалы.Характеристика КАК Характеристика,
	|	КорректировкаРегистровМатериалы.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	КорректировкаРегистровМатериалы.КоличествоУпаковок КАК КоличествоУпаковок,
	|	КорректировкаРегистровМатериалы.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ Материалы
	|ИЗ
	|	КорректировкаРегистровМатериалы КАК КорректировкаРегистровМатериалы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазрешениеМатериалы.Ссылка,
	|	РазрешениеМатериалы.Номенклатура,
	|	РазрешениеМатериалы.Характеристика,
	|	РазрешениеМатериалы.КлючСвязиСпецификация,
	|	РазрешениеМатериалы.КоличествоУпаковок,
	|	РазрешениеМатериалы.Упаковка
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.Материалы КАК РазрешениеМатериалы
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				АналогиМатериалов КАК АналогиМатериалов
	|			ГДЕ
	|				АналогиМатериалов.Разрешение = РазрешениеМатериалы.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	КлючСвязиСпецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.Ссылка КАК Разрешение
	|ПОМЕСТИТЬ ИсключаемыеРазрешения
	|ИЗ
	|	Материалы КАК Материалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ АналогиМатериалов КАК АналогиМатериалов
	|		ПО Материалы.Ссылка = АналогиМатериалов.Разрешение
	|			И Материалы.Номенклатура = АналогиМатериалов.Номенклатура
	|			И Материалы.Характеристика = АналогиМатериалов.Характеристика
	|			И Материалы.КлючСвязиСпецификация = АналогиМатериалов.КлючСвязиСпецификация
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				АналогиМатериалов КАК АналогиМатериалов
	|			ГДЕ
	|				АналогиМатериалов.Разрешение = Материалы.Ссылка)
	|	И АналогиМатериалов.Разрешение ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Разрешение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналогиМатериалов.Ссылка КАК Ссылка,
	|	АналогиМатериалов.Период КАК Период,
	|	АналогиМатериалов.Разрешение КАК Разрешение,
	|	АналогиМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	АналогиМатериалов.Номенклатура КАК Номенклатура,
	|	АналогиМатериалов.Характеристика КАК Характеристика,
	|	АналогиМатериалов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	АналогиМатериалов.Упаковка КАК Упаковка,
	|	АналогиМатериалов.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ТаблицаМатериалов
	|ИЗ
	|	АналогиМатериалов КАК АналогиМатериалов
	|ГДЕ
	|	НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					ИсключаемыеРазрешения КАК ИсключаемыеРазрешения
	|				ГДЕ
	|					ИсключаемыеРазрешения.Разрешение = АналогиМатериалов.Разрешение)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Разрешение,
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	КлючСвязиСпецификация";
	
	ТекстЗапросаПолеПриоритет = ?(РассчитатьПриоритетПрименения, 
	"ВЫБОР
	|		КОГДА АналогиМатериалов.ЗаказНаПроизводство В (ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка),
	//++ Устарело_Производство21
	|															ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка),
	//-- Устарело_Производство21
	|															НЕОПРЕДЕЛЕНО)
	|			ТОГДА 0
	|		ИНАЧЕ 100000
	|	КОНЕЦ + ВЫБОР
	|		КОГДА АналогиМатериалов.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 10000
	|	КОНЕЦ + ВЫБОР
	|		КОГДА АналогиМатериалов.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 1000
	|	КОНЕЦ + ВЫБОР
	|		КОГДА АналогиМатериалов.Изделие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И АналогиМатериалов.ХарактеристикаИзделия = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 100
	|	КОНЕЦ + ВЫБОР
	|		КОГДА АналогиМатериалов.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 10
	|	КОНЕЦ + ВЫБОР
	|		КОГДА АналогиМатериалов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ", "1");
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Приоритет", ТекстЗапросаПолеПриоритет);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса со временными таблицами определяющими таблицу аналогов из доступных разрешений
//
// Параметры:
//  ПолучитьСкладИзОбеспечение - Булево - Истина: Склад определяется из схемы обеспечения
//                                        Ложь: Склад передается как параметр.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаТаблицаАналогов(ПолучитьСкладИзОбеспечение = Истина) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаРегистровМатериалы.Ссылка КАК Ссылка,
	|	АналогиМатериалов.Аналог КАК Номенклатура,
	|	АналогиМатериалов.ХарактеристикаАналога КАК Характеристика,
	|	АналогиМатериалов.КоличествоУпаковокАналога КАК КоличествоУпаковок,
	|	АналогиМатериалов.УпаковкаАналога КАК Упаковка
	|ПОМЕСТИТЬ Аналоги
	|ИЗ
	|	КорректировкаРегистровМатериалы КАК КорректировкаРегистровМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналогиМатериалов КАК АналогиМатериалов
	|		ПО КорректировкаРегистровМатериалы.Ссылка = АналогиМатериалов.Регистратор
	|			И КорректировкаРегистровМатериалы.Номенклатура = АналогиМатериалов.Материал
	|			И КорректировкаРегистровМатериалы.Характеристика = АналогиМатериалов.ХарактеристикаМатериала
	|			И КорректировкаРегистровМатериалы.КлючСвязиСпецификация = АналогиМатериалов.КлючСвязиСпецификация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазрешениеАналоги.Ссылка,
	|	РазрешениеАналоги.Номенклатура,
	|	РазрешениеАналоги.Характеристика,
	|	РазрешениеАналоги.КоличествоУпаковок,
	|	РазрешениеАналоги.Упаковка
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.Аналоги КАК РазрешениеАналоги
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				АналогиМатериалов КАК АналогиМатериалов
	|			ГДЕ
	|				АналогиМатериалов.Разрешение = РазрешениеАналоги.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаМатериалов.Ссылка КАК Ссылка,
	|	ТаблицаМатериалов.Разрешение КАК Разрешение,
	|	ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|	ТаблицаАналогов.Характеристика КАК Характеристика,
	|	ТаблицаАналогов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаАналогов.Упаковка КАК Упаковка,
	|	ТаблицаПараметров.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ТаблицаАналоговСИсточниками
	|ИЗ
	|	ТаблицаМатериалов КАК ТаблицаМатериалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Аналоги КАК ТаблицаАналогов
	|		ПО ТаблицаМатериалов.Разрешение = ТаблицаАналогов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПараметров КАК ТаблицаПараметров
	|		ПО ТаблицаМатериалов.Ссылка = ТаблицаПараметров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналогов.Ссылка КАК Ссылка,
	|	ТаблицаАналогов.Разрешение КАК Разрешение,
	|	ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|	ТаблицаАналогов.Характеристика КАК Характеристика,
	|	ТаблицаАналогов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаАналогов.Упаковка КАК Упаковка,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаАналогов
	|ИЗ
	|	ТаблицаАналоговСИсточниками КАК ТаблицаАналогов
	|//&ТекстСоединенияСклад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	Если ПолучитьСкладИзОбеспечение Тогда
		
		СтруктураТекстовЗапроса = ОбеспечениеПроизводства.ТекстЗапросаНастройкиПередачиМатериалов(
			"ТаблицаАналогов", 
			"Подразделение", 
			ОбеспечениеПроизводства.ВариантЗаполненияОбеспеченияПоВерсииПроизводства(Истина));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Склад", СтруктураТекстовЗапроса.ТекстПолеСклад);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&ТекстСоединенияСклад", СтруктураТекстовЗапроса.ТекстСоединения);
		
	КонецЕсли;	
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса со временными таблицами определяющими возможные и доступные разрешения многие ко многим
//
// Параметры:
//  ПараметрыОтборов - Структура - содержит:
//                      * ТекстыЗаменВЗапросах - Массив из Строка - определяет необходимость получения доступных разрешений многие ко многим.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаДоступныеРазрешенияМногиеКоМногим(ПараметрыОтборов = Неопределено) Экспорт 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Материалы.Ссылка КАК Разрешение,
	|	СУММА(1) КАК КоличествоМатериалов
	|ПОМЕСТИТЬ РазрешенияМногиеКоМногим
	|ИЗ
	|	Материалы КАК Материалы
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ТаблицаМатериалов КАК ТаблицаМатериалов
	|			ГДЕ
	|				ТаблицаМатериалов.Разрешение = Материалы.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Материалы.Ссылка
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Разрешение
	|//УчестьМногиеКоМногим_;
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_////////////////////////////////////////////////////////////////////////////////
	|//УчестьМногиеКоМногим_ВЫБРАТЬ
	|//УчестьМногиеКоМногим_	РазрешенияМногиеКоМногим.Разрешение КАК Разрешение,
	|//УчестьМногиеКоМногим_	Материалы.Номенклатура КАК Номенклатура,
	|//УчестьМногиеКоМногим_	Материалы.Характеристика КАК Характеристика,
	|//УчестьМногиеКоМногим_	Материалы.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|//УчестьМногиеКоМногим_	РазрешенияМногиеКоМногим.КоличествоМатериалов КАК КоличествоМатериалов
	|//УчестьМногиеКоМногим_ПОМЕСТИТЬ РазрешенияМногиеКоМногимМатериалы
	|//УчестьМногиеКоМногим_ИЗ
	|//УчестьМногиеКоМногим_	РазрешенияМногиеКоМногим КАК РазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_		ЛЕВОЕ СОЕДИНЕНИЕ Материалы КАК Материалы
	|//УчестьМногиеКоМногим_		ПО РазрешенияМногиеКоМногим.Разрешение = Материалы.Ссылка
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ИНДЕКСИРОВАТЬ ПО
	|//УчестьМногиеКоМногим_	Номенклатура,
	|//УчестьМногиеКоМногим_	Характеристика,
	|//УчестьМногиеКоМногим_	КлючСвязиСпецификация,
	|//УчестьМногиеКоМногим_	КоличествоМатериалов
	|//УчестьМногиеКоМногим_;
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_////////////////////////////////////////////////////////////////////////////////
	|//УчестьМногиеКоМногим_ВЫБРАТЬ
	|//УчестьМногиеКоМногим_	ДоступныеРазрешенияМногиеКоМногим.Ссылка КАК Ссылка,
	|//УчестьМногиеКоМногим_	ДоступныеРазрешенияМногиеКоМногим.Разрешение КАК Разрешение
	|//УчестьМногиеКоМногим_ПОМЕСТИТЬ ДоступныеРазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_ИЗ
	|//УчестьМногиеКоМногим_	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|//УчестьМногиеКоМногим_		Товары.Ссылка КАК Ссылка,
	|//УчестьМногиеКоМногим_		Товары.Номенклатура КАК Номенклатура,
	|//УчестьМногиеКоМногим_		Товары.Характеристика КАК Характеристика,
	|//УчестьМногиеКоМногим_		РазрешенияМногиеКоМногимМатериалы.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|//УчестьМногиеКоМногим_		РазрешенияМногиеКоМногимМатериалы.Разрешение КАК Разрешение,
	|//УчестьМногиеКоМногим_		РазрешенияМногиеКоМногимМатериалы.КоличествоМатериалов КАК КоличествоМатериалов
	|//УчестьМногиеКоМногим_	ИЗ
	|//УчестьМногиеКоМногим_		Товары КАК Товары
	|//УчестьМногиеКоМногим_			ЛЕВОЕ СОЕДИНЕНИЕ РазрешенияМногиеКоМногимМатериалы КАК РазрешенияМногиеКоМногимМатериалы
	|//УчестьМногиеКоМногим_			ПО Товары.Номенклатура = РазрешенияМногиеКоМногимМатериалы.Номенклатура
	|//УчестьМногиеКоМногим_				И Товары.Характеристика = РазрешенияМногиеКоМногимМатериалы.Характеристика
	|//УчестьМногиеКоМногим_				И (Товары.КлючСвязиСпецификация = РазрешенияМногиеКоМногимМатериалы.КлючСвязиСпецификация
	|//УчестьМногиеКоМногим_					ИЛИ РазрешенияМногиеКоМногимМатериалы.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор)
	|//УчестьМногиеКоМногим_	ГДЕ
	|//УчестьМногиеКоМногим_		РазрешенияМногиеКоМногимМатериалы.Разрешение ЕСТЬ НЕ NULL ) КАК ДоступныеРазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_СГРУППИРОВАТЬ ПО
	|//УчестьМногиеКоМногим_	ДоступныеРазрешенияМногиеКоМногим.Ссылка,
	|//УчестьМногиеКоМногим_	ДоступныеРазрешенияМногиеКоМногим.Разрешение
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ИМЕЮЩИЕ
	|//УчестьМногиеКоМногим_	СУММА(1) = МАКСИМУМ(ДоступныеРазрешенияМногиеКоМногим.КоличествоМатериалов)
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ИНДЕКСИРОВАТЬ ПО
	|//УчестьМногиеКоМногим_	Разрешение,
	|//УчестьМногиеКоМногим_	Ссылка
	|";
	
	Если ПараметрыОтборов <> Неопределено И ПараметрыОтборов.Свойство("ТекстыЗаменВЗапросах") Тогда
		Для каждого ТекстЗамены Из ПараметрыОтборов.ТекстыЗаменВЗапросах Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстЗамены.Ключ, ТекстЗамены.Значение);	
		КонецЦикла;	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса, определяющий наличие аналогов
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаНаличиеАналогов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ
	|				ИСТИНА
	|			ИЗ
	|				ТаблицаМатериалов КАК ТаблицаМатериалов
	|			ГДЕ
	|				ТаблицаМатериалов.Номенклатура = Товары.Номенклатура
	|				И ТаблицаМатериалов.Характеристика = Товары.Характеристика
	|				И (ТаблицаМатериалов.КлючСвязиСпецификация = Товары.КлючСвязиСпецификация
	|					ИЛИ ТаблицаМатериалов.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор)
	|				И НЕ ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							РазрешенияМногиеКоМногим КАК РазрешенияМногиеКоМногим
	|						ГДЕ
	|							РазрешенияМногиеКоМногим.Разрешение = ТаблицаМатериалов.Разрешение))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ДоступныеРазрешенияМногиеКоМногим КАК ДоступныеРазрешенияМногиеКоМногим
	|					ЛЕВОЕ СОЕДИНЕНИЕ Материалы КАК Материалы
	|					ПО
	|						ДоступныеРазрешенияМногиеКоМногим.Разрешение = Материалы.Ссылка
	|			ГДЕ
	|				ДоступныеРазрешенияМногиеКоМногим.Ссылка = Товары.Ссылка
	|				И Материалы.Номенклатура = Товары.Номенклатура
	|				И Материалы.Характеристика = Товары.Характеристика
	|				И (Материалы.КлючСвязиСпецификация = Товары.КлючСвязиСпецификация
	|					ИЛИ Материалы.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор))";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

// Возвращает текст запроса отобранных разрешений, материалов, аналогов
//
// Параметры:
//  ПараметрыОтборов - Структура - содержит:
//                      * ТекстыЗаменВЗапросах - Массив из Строка - определяет необходимость отбора доступных разрешений многие ко многим.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаРазрешенияПоПриоритетам(ПараметрыОтборов = Неопределено) Экспорт
	
	ТекстЗапроса =
	"
	|ВЫБРАТЬ
	|	ТаблицаМатериалов.Ссылка КАК Ссылка,
	|	ТаблицаМатериалов.Разрешение КАК Разрешение,
	|	ТаблицаМатериалов.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалов.Характеристика КАК Характеристика,
	|	ТаблицаМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	ТаблицаМатериалов.КоличествоУпаковок КАК Норматив,
	|	ТаблицаМатериалов.Упаковка КАК Упаковка,
	|	НЕОПРЕДЕЛЕНО КАК ДанныеУпаковки,
	|	ТаблицаМатериалов.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук) КАК КонтролироватьКратность
	|ИЗ
	|	ТаблицаМатериалов КАК ТаблицаМатериалов
	|ГДЕ
	|	НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РазрешенияМногиеКоМногим КАК РазрешенияМногиеКоМногим
	|				ГДЕ
	|					РазрешенияМногиеКоМногим.Разрешение = ТаблицаМатериалов.Разрешение)
	|
	|//УчестьМногиеКоМногим_ОБЪЕДИНИТЬ ВСЕ
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ВЫБРАТЬ
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Ссылка,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Разрешение,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Номенклатура,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Характеристика,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.КлючСвязиСпецификация,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.КоличествоУпаковок,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Упаковка,
	|//УчестьМногиеКоМногим_	НЕОПРЕДЕЛЕНО,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
	|//УчестьМногиеКоМногим_ИЗ
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов КАК ТаблицаМатериалов
	|//УчестьМногиеКоМногим_ГДЕ
	|//УчестьМногиеКоМногим_	ИСТИНА В
	|//УчестьМногиеКоМногим_			(ВЫБРАТЬ ПЕРВЫЕ 1
	|//УчестьМногиеКоМногим_				ИСТИНА
	|//УчестьМногиеКоМногим_			ИЗ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим КАК ДоступныеРазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_			ГДЕ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим.Разрешение = ТаблицаМатериалов.Разрешение
	|//УчестьМногиеКоМногим_				И ДоступныеРазрешенияМногиеКоМногим.Ссылка = ТаблицаМатериалов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналогов.Ссылка КАК Ссылка,
	|	ТаблицаАналогов.Разрешение КАК Разрешение,
	|	ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|	ТаблицаАналогов.Характеристика КАК Характеристика,
	|	ТаблицаАналогов.КоличествоУпаковок КАК Норматив,
	|	ТаблицаАналогов.Упаковка КАК Упаковка,
	|	НЕОПРЕДЕЛЕНО КАК ДанныеУпаковки,
	|	ТаблицаАналогов.Склад КАК Склад,
	|	ЕСТЬNULL(ТаблицаОстатков.КоличествоОстаток, 0) КАК ОстатокНаСкладе
	|ИЗ
	|	ТаблицаАналогов КАК ТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО ТаблицаАналогов.Номенклатура = ТаблицаОстатков.Номенклатура
	|			И ТаблицаАналогов.Характеристика = ТаблицаОстатков.Характеристика
	|			И ТаблицаАналогов.Склад = ТаблицаОстатков.Склад
	|ГДЕ
	|	НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РазрешенияМногиеКоМногим КАК РазрешенияМногиеКоМногим
	|				ГДЕ
	|					РазрешенияМногиеКоМногим.Разрешение = ТаблицаАналогов.Разрешение)
	|
	|//УчестьМногиеКоМногим_ОБЪЕДИНИТЬ ВСЕ
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ВЫБРАТЬ
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Ссылка,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Разрешение,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Номенклатура,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Характеристика,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.КоличествоУпаковок,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Упаковка,
	|//УчестьМногиеКоМногим_	НЕОПРЕДЕЛЕНО,
	|//УчестьМногиеКоМногим_	ТаблицаАналогов.Склад,
	|//УчестьМногиеКоМногим_	ЕСТЬNULL(ТаблицаОстатков.КоличествоОстаток, 0)
	|//УчестьМногиеКоМногим_ИЗ
	|//УчестьМногиеКоМногим_	ТаблицаАналогов КАК ТаблицаАналогов
	|//УчестьМногиеКоМногим_		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|//УчестьМногиеКоМногим_		ПО ТаблицаАналогов.Номенклатура = ТаблицаОстатков.Номенклатура
	|//УчестьМногиеКоМногим_			И ТаблицаАналогов.Характеристика = ТаблицаОстатков.Характеристика
	|//УчестьМногиеКоМногим_			И ТаблицаАналогов.Склад = ТаблицаОстатков.Склад
	|
	|//УчестьМногиеКоМногим_ГДЕ
	|//УчестьМногиеКоМногим_	ИСТИНА В
	|//УчестьМногиеКоМногим_			(ВЫБРАТЬ ПЕРВЫЕ 1
	|//УчестьМногиеКоМногим_				ИСТИНА
	|//УчестьМногиеКоМногим_			ИЗ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим КАК ДоступныеРазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_			ГДЕ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим.Разрешение = ТаблицаАналогов.Разрешение
	|//УчестьМногиеКоМногим_				И ДоступныеРазрешенияМногиеКоМногим.Ссылка = ТаблицаАналогов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Количество КАК Количество,
	|	ТаблицаМатериалов.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	ТаблицаМатериалов.Разрешение КАК Разрешение,
	|	ТаблицаМатериалов.Приоритет КАК Приоритет,
	|	ТаблицаМатериалов.Период КАК Период
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМатериалов КАК ТаблицаМатериалов
	|		ПО Товары.Номенклатура = ТаблицаМатериалов.Номенклатура
	|			И Товары.Характеристика = ТаблицаМатериалов.Характеристика
	|			И (Товары.КлючСвязиСпецификация = ТаблицаМатериалов.КлючСвязиСпецификация
	|				ИЛИ ТаблицаМатериалов.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор)
	|			И Товары.Ссылка = ТаблицаМатериалов.Ссылка
	|ГДЕ
	|	ТаблицаМатериалов.Разрешение ЕСТЬ НЕ NULL 
	|	И НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РазрешенияМногиеКоМногим КАК РазрешенияМногиеКоМногим
	|				ГДЕ
	|					РазрешенияМногиеКоМногим.Разрешение = ТаблицаМатериалов.Разрешение)
	|
	|//УчестьМногиеКоМногим_ОБЪЕДИНИТЬ ВСЕ
	|//УчестьМногиеКоМногим_
	|//УчестьМногиеКоМногим_ВЫБРАТЬ
	|//УчестьМногиеКоМногим_	Товары.Ссылка,
	|//УчестьМногиеКоМногим_	Товары.НомерСтроки,
	|//УчестьМногиеКоМногим_	Товары.Номенклатура,
	|//УчестьМногиеКоМногим_	Товары.Характеристика,
	|//УчестьМногиеКоМногим_	Товары.Количество,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.КлючСвязиСпецификация,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Разрешение,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Приоритет,
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Период
	|//УчестьМногиеКоМногим_ИЗ
	|//УчестьМногиеКоМногим_	Товары КАК Товары
	|//УчестьМногиеКоМногим_		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМатериалов КАК ТаблицаМатериалов
	|//УчестьМногиеКоМногим_		ПО Товары.Номенклатура = ТаблицаМатериалов.Номенклатура
	|//УчестьМногиеКоМногим_			И Товары.Характеристика = ТаблицаМатериалов.Характеристика
	|//УчестьМногиеКоМногим_			И (Товары.КлючСвязиСпецификация = ТаблицаМатериалов.КлючСвязиСпецификация
	|//УчестьМногиеКоМногим_				ИЛИ ТаблицаМатериалов.КлючСвязиСпецификация = &ПустойУникальныйИдентификатор)
	|//УчестьМногиеКоМногим_			И Товары.Ссылка = ТаблицаМатериалов.Ссылка
	|//УчестьМногиеКоМногим_ГДЕ
	|//УчестьМногиеКоМногим_	ТаблицаМатериалов.Разрешение ЕСТЬ НЕ NULL 
	|//УчестьМногиеКоМногим_	И ИСТИНА В
	|//УчестьМногиеКоМногим_			(ВЫБРАТЬ ПЕРВЫЕ 1
	|//УчестьМногиеКоМногим_				ИСТИНА
	|//УчестьМногиеКоМногим_			ИЗ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим КАК ДоступныеРазрешенияМногиеКоМногим
	|//УчестьМногиеКоМногим_			ГДЕ
	|//УчестьМногиеКоМногим_				ДоступныеРазрешенияМногиеКоМногим.Разрешение = ТаблицаМатериалов.Разрешение
	|//УчестьМногиеКоМногим_				И ДоступныеРазрешенияМногиеКоМногим.Ссылка = ТаблицаМатериалов.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Приоритет УБЫВ,
	|	Период УБЫВ,
	|	Разрешение УБЫВ";
	
	Если ПараметрыОтборов <> Неопределено И ПараметрыОтборов.Свойство("ТекстыЗаменВЗапросах") Тогда
		Для каждого ТекстЗамены Из ПараметрыОтборов.ТекстыЗаменВЗапросах Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстЗамены.Ключ, ТекстЗамены.Значение);	
		КонецЦикла;	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции	

// Возвращает текст запроса временной таблицы "ТаблицаОстатков"
//
// Параметры:
//  ПараметрыОтборов - Структура - содержит:
//                      * ТекстыЗаменВЗапросах - Массив из Строка - определяет необходимость получения остатков по материалам, по аналогам.
//                      * КорректироватьОстатки - Булево - определяет необходимость корректировки остатков.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаТаблицаОстатков(ПараметрыОтборов) Экспорт
		
	ТекстЗапросаКорректировки = 
	"ВЫБРАТЬ
	|	КорректировкиОстатков.Номенклатура КАК Номенклатура,
	|	КорректировкиОстатков.Характеристика КАК Характеристика,
	|	КорректировкиОстатков.Склад КАК Склад,
	|	КорректировкиОстатков.Количество КАК Количество
	|//ТаблицаКорректировки_ПОМЕСТИТЬ КорректировкиОстатков
	|ИЗ
	|//ТаблицаКорректировки_&КорректировкиОстатков КАК КорректировкиОстатков
	|";
	
	ТекстЗапросаОстатки = 
	"ВЫБРАТЬ
	|	СвободныеОстаткиОстатки.Номенклатура КАК Номенклатура,
	|	СвободныеОстаткиОстатки.Характеристика КАК Характеристика,
	|	СвободныеОстаткиОстатки.Склад КАК Склад,
	|	СвободныеОстаткиОстатки.ВНаличииОстаток - СвободныеОстаткиОстатки.ВРезервеСоСкладаОстаток - СвободныеОстаткиОстатки.ВРезервеПодЗаказОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.Остатки(
	|			,
	|//ОтборПоМатериалам_			(Номенклатура, Характеристика) В
	|//ОтборПоМатериалам_					(ВЫБРАТЬ
	|//ОтборПоМатериалам_						Товары.Номенклатура КАК Номенклатура,
	|//ОтборПоМатериалам_						Товары.Характеристика КАК Характеристика
	|//ОтборПоМатериалам_					ИЗ
	|//ОтборПоМатериалам_						Товары КАК Товары)
	|//ОтборПоМатериалам_//ОтборПоАналогам_			ИЛИ 
	|//ОтборПоАналогам_			(Номенклатура, Характеристика, Склад) В
	|//ОтборПоАналогам_					(ВЫБРАТЬ
	|//ОтборПоАналогам_						ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|//ОтборПоАналогам_						ТаблицаАналогов.Характеристика КАК Характеристика,
	|//ОтборПоАналогам_						ТаблицаАналогов.Склад КАК Склад
	|//ОтборПоАналогам_					ИЗ
	|//ОтборПоАналогам_						ТаблицаАналогов КАК ТаблицаАналогов)
	|	) КАК СвободныеОстаткиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Резерв.Номенклатура,
	|	Резерв.Характеристика,
	|	Резерв.Склад,
	|	Резерв.Количество
	|ИЗ
	|	РегистрСведений.ДоступныеОстаткиПланируемыхПоступлений КАК Резерв
	|ГДЕ
	|	Резерв.ДатаДоступности = ДАТАВРЕМЯ(1, 1, 1)
	|	И Резерв.Количество < 0
	|	И (
	|		ИСТИНА
	|//ОтборПоМатериалам_	И	(Резерв.Номенклатура, Резерв.Характеристика) В
	|//ОтборПоМатериалам_				(ВЫБРАТЬ
	|//ОтборПоМатериалам_					Товары.Номенклатура КАК Номенклатура,
	|//ОтборПоМатериалам_					Товары.Характеристика КАК Характеристика
	|//ОтборПоМатериалам_				ИЗ
	|//ОтборПоМатериалам_					Товары КАК Товары)
	|//ОтборПоМатериалам_//ОтборПоАналогам_		ИЛИ
	|//ОтборПоМатериалам_//ОтборПоАналогам_		ИСТИНА
	|//ОтборПоАналогам_		И	(Резерв.Номенклатура, Резерв.Характеристика, Резерв.Склад) В
	|//ОтборПоАналогам_				(ВЫБРАТЬ
	|//ОтборПоАналогам_					ТаблицаАналогов.Номенклатура КАК Номенклатура,
	|//ОтборПоАналогам_					ТаблицаАналогов.Характеристика КАК Характеристика,
	|//ОтборПоАналогам_					ТаблицаАналогов.Склад КАК Склад
	|//ОтборПоАналогам_				ИЗ
	|//ОтборПоАналогам_					ТаблицаАналогов КАК ТаблицаАналогов)
	|	)";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатков.Характеристика КАК Характеристика,
	|	ТаблицаОстатков.Склад КАК Склад,
	|	СУММА(ТаблицаОстатков.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|%1
	|	) КАК ТаблицаОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатков.Номенклатура,
	|	ТаблицаОстатков.Характеристика,
	|	ТаблицаОстатков.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Склад";
	
	Если ПараметрыОтборов.КорректироватьОстатки Тогда
		
		ТекстыЗапросов = Новый Массив;
		ТекстыЗапросов.Добавить(ТекстЗапросаОстатки);
		ТекстыЗапросов.Добавить(СтрЗаменить(ТекстЗапросаКорректировки, "//ТаблицаКорректировки_&", ""));
		ТекстЗапросаОстатки = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
		ТекстыЗапросов.Очистить();
		ТекстыЗапросов.Добавить(СтрЗаменить(ТекстЗапросаКорректировки, "//ТаблицаКорректировки_", ""));
		ТекстыЗапросов.Добавить(ТекстЗапроса);
		ТекстЗапроса = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
		
	КонецЕсли;	
	
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, ТекстЗапросаОстатки);
	
	Если ПараметрыОтборов.Свойство("ТекстыЗаменВЗапросах") Тогда
		Для каждого ТекстЗамены Из ПараметрыОтборов.ТекстыЗаменВЗапросах Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстЗамены.Ключ, ТекстЗамены.Значение);	
		КонецЦикла;	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает таблицу (структуру таблицы) параметров необходимые для выбора аналогов
// 
// Параметры:
// 	ТипИсточника - ОписаниеТипов - Описание
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание:
// * ДатаДействияРазрешений - Дата - дата действия разрешений
// * Ссылка - ДокументСсылка - ссылка на источник
// * ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство, ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство
// * ЗаказКлиента - ДокументСсылка.ЗаказКлиента -
// * Спецификация - СправочникСсылка.РесурсныеСпецификации -
// * Изделие - СправочникСсылка.Номенклатура -
// * ХарактеристикаИзделия - СправочникСсылка.ХарактеристикиНоменклатуры -
// * Подразделение - СправочникСсылка.СтруктураПредприятия -
// * НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности -
//
Функция ТаблицаПараметровАналогов(ТипИсточника)

	Типы = Новый Массив;
	Типы.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство2_2"));
	//++ Устарело_Производство21
	Типы.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	//-- Устарело_Производство21 
				
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("ДатаДействияРазрешений", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаПараметров.Колонки.Добавить("Ссылка", ТипИсточника);
	ТаблицаПараметров.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов(Типы));
	ТаблицаПараметров.Колонки.Добавить("ЗаказКлиента", Новый ОписаниеТипов("ДокументСсылка.ЗаказКлиента"));	
	ТаблицаПараметров.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	ТаблицаПараметров.Колонки.Добавить("Изделие", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));	
	ТаблицаПараметров.Колонки.Добавить("ХарактеристикаИзделия", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));	
	ТаблицаПараметров.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));	
	ТаблицаПараметров.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));
	
	Возврат ТаблицаПараметров;
	
КонецФункции

// Возвращает служебную таблицу значений
//
// Возвращаемое значение:
//  ТаблицаЗначений - Содержит:
//  * НомерСтроки - Число - 
//  * Ссылка - ДокументСсылка, СправочникСсылка - 
//  * Идентификатор - Число, УникальныйИдентификатор - 
//  * НачалоЭтапа - Дата -
//  * Описание - Строка - 
//  * Строки - КоллекцияСтрокДереваЗначений - 
//
Функция СлужебнаяСтруктураТаблицыЗначений() Экспорт
	
	Возврат Новый ТаблицаЗначений;
	
КонецФункции

// Возвращает служебное дерево значений
//
// Возвращаемое значение:
//  ДеревоЗначений - Содержит:
//  * Ссылка - ДокументСсылка, СправочникСсылка - 
//
Функция СлужебнаяСтруктураДеревоЗначений() Экспорт
	
	Возврат Новый ДеревоЗначений;
	
КонецФункции

// Возвращает служебнуые структуры
//
// Возвращаемое значение:
//  Структура - Содержит:
//  * ДанныеСпецификаций - Массив из Структура - содержит:
//                                    ** ВыходныеИзделия - ТаблицаЗначений - 
//                                    ** МатериалыИУслуги - ТаблицаЗначений - содержит:
//                                                           *** НомерСтроки - Число - 
//
Функция СлужебнаяСтруктураСтруктуры() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Возвращает служебнуый массив из структур
//
// Возвращаемое значение:
//  Массив из Структура - Содержит:
//  * Идентификатор - Число, УникальныйИдентификатор - 
//
Функция СлужебнаяСтруктураМассиваИзСтруктур() Экспорт
	
	Возврат Новый Структура;
	
КонецФункции

// Возвращает служебный табличный документ
//
// Возвращаемое значение:
//  ТабличныйДокумент - Содержит:
//  * Параметры - ПараметрыМакетаТабличногоДокумента - Содержит:
//                 ** НомерСтроки -
//                 ** ТекстЗаголовка -
//
Функция СлужебнаяСтруктураТабличныйДокумент() Экспорт
	
	Возврат Новый ТабличныйДокумент;
	
КонецФункции

#КонецОбласти